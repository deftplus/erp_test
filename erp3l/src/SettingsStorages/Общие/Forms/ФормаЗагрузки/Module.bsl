
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КлючОбъекта = Параметры.КлючОбъекта;
	КлючТекущихНастроек = Параметры.КлючТекущихНастроек;
	
	ТекущаяСтрокаСохраненныхНастроек = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КлючОбъекта", КлючОбъекта);
	Запрос.УстановитьПараметр("КлючТекущихНастроек", КлючТекущихНастроек);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХранилищеНастроек.КлючНастроек КАК КлючНастроек,
	|	ВЫБОР
	|		КОГДА ХранилищеНастроек.КлючНастроек = &КлючТекущихНастроек
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТекущаяНастройка
	|ИЗ
	|	РегистрСведений.ХранилищеНастроек КАК ХранилищеНастроек
	|ГДЕ
	|	ХранилищеНастроек.КлючОбъекта = &КлючОбъекта
	|
	|УПОРЯДОЧИТЬ ПО
	|	КлючНастроек";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Строка = СохраненныеНастройки.Добавить();
		ЗаполнитьЗначенияСвойств(Строка, Выборка);
		Если Выборка.ТекущаяНастройка Тогда
			ТекущаяСтрокаСохраненныхНастроек = Строка;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ТекущаяСтрокаСохраненныхНастроек = Неопределено Тогда
		Элементы.СохраненныеНастройки.ТекущаяСтрока = ТекущаяСтрокаСохраненныхНастроек.ПолучитьИдентификатор();
	КонецЕсли;
	
	Если СохраненныеНастройки.Количество() = 0 Тогда
		Элементы.Выбрать.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	Закрыть(Новый ВыборНастроек(Элементы.СохраненныеНастройки.ТекущиеДанные.КлючНастроек));
	
КонецПроцедуры

&НаКлиенте
Процедура СохраненныеПараметрыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Закрыть(Новый ВыборНастроек(СохраненныеНастройки.НайтиПоИдентификатору(ВыбраннаяСтрока).КлючНастроек));
	
КонецПроцедуры

&НаКлиенте
Процедура СохраненныеНастройкиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СохраненныеНастройкиПередУдалением(Элемент, Отказ)
	
	УдалитьНаСервере(КлючОбъекта, Элементы.СохраненныеНастройки.ТекущиеДанные.КлючНастроек);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьНаСервере(КлючОбъекта, КлючНастроек)
	
	ХранилищаНастроек.Общие.Удалить(КлючОбъекта, КлючНастроек);
	
КонецПроцедуры
