
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КлючОбъекта = Параметры.КлючОбъекта;
	КлючТекущихНастроек = Параметры.КлючТекущихНастроек;
	
	ТекущаяСтрокаСохраненныхНастроек = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КлючОбъекта", КлючОбъекта);
	Запрос.УстановитьПараметр("КлючТекущихНастроек", КлючТекущихНастроек);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХранилищеНастроек.КлючНастроек КАК КлючНастроек,
	|	ВЫБОР
	|		КОГДА ХранилищеНастроек.КлючНастроек = &КлючТекущихНастроек
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТекущаяНастройка
	|ИЗ
	|	РегистрСведений.ХранилищеНастроек КАК ХранилищеНастроек
	|ГДЕ
	|	ХранилищеНастроек.КлючОбъекта = &КлючОбъекта
	|
	|УПОРЯДОЧИТЬ ПО
	|	КлючНастроек";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Строка = РанееСохраненныеНастройки.Добавить();
		ЗаполнитьЗначенияСвойств(Строка, Выборка);
		Если Выборка.ТекущаяНастройка Тогда
			ТекущаяСтрокаСохраненныхНастроек = Строка;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ТекущаяСтрокаСохраненныхНастроек = Неопределено Тогда
		Элементы.РанееСохраненныеНастройки.ТекущаяСтрока = ТекущаяСтрокаСохраненныхНастроек.ПолучитьИдентификатор();
		КлючНастроек = ТекущаяСтрокаСохраненныхНастроек.КлючНастроек;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ТекущийЭлемент = Элементы.КлючНастроек;
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	Закрыть(Новый ВыборНастроек(КлючНастроек));
	
КонецПроцедуры

&НаКлиенте
Процедура РанееСохраненныеНастройкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Закрыть(Новый ВыборНастроек(РанееСохраненныеНастройки.НайтиПоИдентификатору(ВыбраннаяСтрока).КлючНастроек));
	
КонецПроцедуры

&НаКлиенте
Процедура РанееСохраненныеНастройкиПриАктивизацииСтроки(Элемент)
	
	Если НЕ Элементы.РанееСохраненныеНастройки.ТекущиеДанные = Неопределено Тогда
	
		КлючНастроек = Элементы.РанееСохраненныеНастройки.ТекущиеДанные.КлючНастроек;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РанееСохраненныеНастройкиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура РанееСохраненныеНастройкиПередУдалением(Элемент, Отказ)
	
	УдалитьНаСервере(КлючОбъекта, Элементы.РанееСохраненныеНастройки.ТекущиеДанные.КлючНастроек);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьНаСервере(КлючОбъекта, КлючНастроек)
	
	ХранилищаНастроек.Общие.Удалить(КлючОбъекта, КлючНастроек);
	
КонецПроцедуры

