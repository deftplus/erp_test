#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	
#Область ОбработчикиСобытийОбъекта


Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	ВалютаИтогов = Константы.ВалютаУправленческогоУчета.Получить();
	ЗаполнитьТаблицуТехСчетов();
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если НЕ БезТрансляции И ТрансляцияПлановСчетовНаМСФОДляЭлиминации.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрокаНеУказаныТрансляции(),, 
			"ТрансляцияПлановСчетовНаМСФОДляЭлиминации",, Отказ);
	КонецЕсли;
КонецПроцедуры


#КонецОбласти


#Область ВнешнийПрограмныйИнтерфейс


Процедура ЗаполнитьТаблицуТехСчетов() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТехСчета.ТипСчета КАК ТипСчета,
		|	ТехСчета.СпособЗаполненияСубконто1 КАК СпособЗаполненияСубконто1,
		|	ТехСчета.ЗначениеЗаполненияСубконто1 КАК ЗначениеЗаполненияСубконто1,
		|	ТехСчета.СпособЗаполненияСубконто2 КАК СпособЗаполненияСубконто2,
		|	ТехСчета.СпособЗаполненияСубконто3 КАК СпособЗаполненияСубконто3,
		|	ТехСчета.ЗначениеЗаполненияСубконто2 КАК ЗначениеЗаполненияСубконто2,
		|	ТехСчета.ЗначениеЗаполненияСубконто3 КАК ЗначениеЗаполненияСубконто3,
		|	ТехСчета.Счет КАК Счет
		|ПОМЕСТИТЬ ТехСчетаИзДокумента
		|ИЗ
		|	&ТехСчетаПараметр КАК ТехСчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТипыТехническихСчетовДляСверкиЭлиминации.Ссылка КАК Ссылка,
		|	ТипыТехническихСчетовДляСверкиЭлиминации.Порядок КАК Порядок
		|ПОМЕСТИТЬ ВидТехСчетов
		|ИЗ
		|	Перечисление.ТипыТехническихСчетовДляСверкиЭлиминации КАК ТипыТехническихСчетовДляСверкиЭлиминации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВидТехСчетов.Ссылка КАК ТипСчета,
		|	МАКСИМУМ(ЕСТЬNULL(ТехСчетаИзДокумента.Счет, ЗНАЧЕНИЕ(Справочник.СчетаБД.ПустаяСсылка))) КАК Счет,
		|	МАКСИМУМ(ЕСТЬNULL(ТехСчетаИзДокумента.СпособЗаполненияСубконто1, ЗНАЧЕНИЕ(Перечисление.СпособыЗаполненияЗначенийСубконтоСчета.ПустаяСсылка))) КАК СпособЗаполненияСубконто1,
		|	МАКСИМУМ(ЕСТЬNULL(ТехСчетаИзДокумента.СпособЗаполненияСубконто2, ЗНАЧЕНИЕ(Перечисление.СпособыЗаполненияЗначенийСубконтоСчета.ПустаяСсылка))) КАК СпособЗаполненияСубконто2,
		|	МАКСИМУМ(ЕСТЬNULL(ТехСчетаИзДокумента.СпособЗаполненияСубконто3, ЗНАЧЕНИЕ(Перечисление.СпособыЗаполненияЗначенийСубконтоСчета.ПустаяСсылка))) КАК СпособЗаполненияСубконто3,
		|	МАКСИМУМ(ЕСТЬNULL(ТехСчетаИзДокумента.ЗначениеЗаполненияСубконто1, НЕОПРЕДЕЛЕНО)) КАК ЗначениеЗаполненияСубконто1,
		|	МАКСИМУМ(ЕСТЬNULL(ТехСчетаИзДокумента.ЗначениеЗаполненияСубконто2, НЕОПРЕДЕЛЕНО)) КАК ЗначениеЗаполненияСубконто2,
		|	МАКСИМУМ(ЕСТЬNULL(ТехСчетаИзДокумента.ЗначениеЗаполненияСубконто3, НЕОПРЕДЕЛЕНО)) КАК ЗначениеЗаполненияСубконто3
		|ИЗ
		|	ВидТехСчетов КАК ВидТехСчетов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТехСчетаИзДокумента КАК ТехСчетаИзДокумента
		|		ПО ВидТехСчетов.Ссылка = ТехСчетаИзДокумента.ТипСчета
		|
		|СГРУППИРОВАТЬ ПО
		|	ВидТехСчетов.Ссылка";
		
	Запрос.УстановитьПараметр("ТехСчетаПараметр", ТехническиеСчета.Выгрузить());
	РезультатЗапроса = Запрос.Выполнить();
	
	ТехническиеСчета.Загрузить(РезультатЗапроса.Выгрузить());
КонецПроцедуры

Процедура ЗаполнитьТаблицуТехСчетовПоРеквизитамОбъекта() Экспорт
	ЗаполнитьТаблицуТехСчетов();
	
	ЗаполнитьСтрокуТехСчетаПоРеквизиту(
		Перечисления.ТипыТехническихСчетовДляСверкиЭлиминации.СчетУрегулирования,
		Удалить_ТехническийСчетУрегулирования,
		Удалить_НомерСубконтоРазделаВГОСчетУрегулирования,
		Удалить_НомерСубконтоДебиторСчетУрегулирования,
		Удалить_НомерСубконтоКредиторСчетУрегулирования);
		
	ЗаполнитьСтрокуТехСчетаПоРеквизиту(
		Перечисления.ТипыТехническихСчетовДляСверкиЭлиминации.СчетАвтоматическогоУрегулирования,
		Удалить_ТехническийСчетСписанияРасхождений,
		Удалить_НомерСубконтоРазделаВГОСчетСписанияРасхождений,
		Удалить_НомерСубконтоДебиторСчетСписанияРасхождений,
		Удалить_НомерСубконтоКредиторСчетСписанияРасхождений);
		
	ЗаполнитьСтрокуТехСчетаПоРеквизиту(
		Перечисления.ТипыТехническихСчетовДляСверкиЭлиминации.СчетЭлиминации,
		Удалить_ТехническийСчетДляЭлиминации,
		Удалить_НомерСубконтоРазделаВГОСчетЭлиминации,
		Удалить_НомерСубконтоДебиторСчетЭлиминации,
		Удалить_НомерСубконтоКредиторСчетЭлиминации);
	
КонецПроцедуры


#КонецОбласти


#Область ВнутреннийПрограмныйИнтерфейс


Процедура ЗаполнитьСтрокуТехСчетаПоРеквизиту(ТипСчета, Счет, НомерСубконтоРазделаВГО,
											НомерСубконтоДебитор, НомерСубконтоКредитор)
	мСтрокТипаСчета = ТехническиеСчета.НайтиСтроки(Новый Структура("ТипСчета", ТипСчета));
	Если мСтрокТипаСчета.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	СтрокаТипаСчета = мСтрокТипаСчета[0];
	СтрокаТипаСчета.Счет = Счет;
	
	УстановитьСубконтоСтрокиТехСчета(СтрокаТипаСчета,
		НомерСубконтоРазделаВГО, Перечисления.СпособыЗаполненияЗначенийСубконтоСчета.РазделСверки, Неопределено);
	УстановитьСубконтоСтрокиТехСчета(СтрокаТипаСчета,
		НомерСубконтоДебитор, Перечисления.СпособыЗаполненияЗначенийСубконтоСчета.ОрганизацияДебитор, Неопределено);
	УстановитьСубконтоСтрокиТехСчета(СтрокаТипаСчета,
		НомерСубконтоКредитор, Перечисления.СпособыЗаполненияЗначенийСубконтоСчета.ОрганизацияКредитор, Неопределено);
КонецПроцедуры

Процедура УстановитьСубконтоСтрокиТехСчета(СтрокаТипаСчета, НомерСубконто, СпособЗаполнения, ЗначениеЗаполнения)
	Если НомерСубконто > 0 Тогда
		СтрокаТипаСчета["СпособЗаполненияСубконто" + НомерСубконто] = СпособЗаполнения;
		СтрокаТипаСчета["ЗначениеЗаполненияСубконто" + НомерСубконто] = ЗначениеЗаполнения;
	КонецЕсли;
КонецПроцедуры


#КонецОбласти


#Область Локализация


Функция СтрокаНеУказаныТрансляции()
	Возврат НСтр("ru='Не указаны трансляции для планов счетов НСБУ.'");
КонецФункции


#КонецОбласти


#КонецЕсли
