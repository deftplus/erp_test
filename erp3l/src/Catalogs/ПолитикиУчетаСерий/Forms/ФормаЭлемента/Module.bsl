
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Объект.Предопределенный Тогда
		ТекстИсключения = НСтр("ru = 'Это служебная политика учета серий, редактирование запрещено.';
								|en = 'This is service policy of batch accounting, editing is prohibited.'");
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);

	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Если Не ЗначениеЗаполнено(Объект.ТипПолитики)
			И Не ПолучитьФункциональнуюОпцию("ИспользоватьОрдерныеСклады") Тогда
			Объект.ТипПолитики = Перечисления.ТипыПолитикУказанияСерий.СправочноеУказаниеСерий;
		КонецЕсли;
		
		ТипПолитикиПриИзмененииНаСервере();
		УстановитьНастройкуПриЧтенииСоздании();
	Иначе
		Элементы.НастройкаУказанияСерийПриОтгрузке.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	//++ НЕ УТ
	НастройкиПроизводства = ПроизводствоСервер.НастройкиПодсистемыПроизводство();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, НастройкиПроизводства, "ИспользуетсяПроизводство21, ИспользуетсяПроизводство22");
	//-- НЕ УТ
	
	Если ИспользуетсяПроизводство21 Тогда
		
		Если ПолучитьФункциональнуюОпцию("КомплекснаяАвтоматизация") Тогда
			Элементы.УказыватьПриПроизводствеПродукции.Видимость = Ложь;
			Элементы.УказыватьПриРасходеМатериалов.Видимость = Ложь;
		Иначе
			Элементы.УказыватьПриОтгрузкеНаВнутренниеНужды.Заголовок = НСтр("ru = 'в производство, на внутренние нужды';
																			|en = 'to production, for internal needs'");
		КонецЕсли;
		
		Если ИспользуетсяПроизводство22 Тогда
			Элементы.УказыватьПриПроизводствеПродукции.Заголовок = НСтр("ru = 'при вводе выходных изделий в маршрутных листах и этапах производства';
																		|en = 'when adding product to operation sheets and production stages'");
		Иначе
			Элементы.УказыватьПриПроизводствеПродукции.Заголовок = НСтр("ru = 'при вводе выходных изделий в маршрутных листах';
																		|en = 'when adding product to operation sheets'");
		КонецЕсли;
		
	Иначе
		
		Если ИспользуетсяПроизводство22 Тогда
			Элементы.УказыватьПриПроизводствеПродукции.Заголовок = НСтр("ru = 'при выпуске продукции';
																		|en = 'on product release'");
		КонецЕсли;
		
		Элементы.ДекорацияУказыватьПриДвиженииМатериалов1.Видимость = Ложь;
		Элементы.ДекорацияУказыватьПриДвиженииМатериалов2.Видимость = Ложь;
		
	КонецЕсли;
	
	Элементы.УчетСерийВНеотфактурованныхПоставкахТоваров.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьНеотфактурованныеПоставки");
	Элементы.УчетТоваровВПутиОтПоставщикаПоСериям.Видимость        = ПолучитьФункциональнуюОпцию("ИспользоватьТоварыВПутиОтПоставщиков");
	Элементы.УчетСерийВПереданныхНаХранениеТоварах.Видимость = ПолучитьФункциональнуюОпцию(
																"ИспользоватьПередачуНаОтветственноеХранениеСПравомПродажи");
	
	ЗаполнитьСписокТиповПолитик();
	НастроитьЭлементыФормыНаСервере();
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	УстановитьНастройкуПриЧтенииСоздании();

	МодификацияКонфигурацииПереопределяемый.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Объект.УказыватьПриПланированииОтгрузки = (НастройкаУказанияСерийПриОтгрузке = "ПриПланированииОтгрузки");
	Объект.УказыватьПриПланированииОтбора   = (НастройкаУказанияСерийПриОтгрузке = "ПриПланированииОтбора");
	Объект.УказыватьПоФактуОтбора           = (НастройкаУказанияСерийПриОтгрузке = "ПоФактуОтбора");
КонецПроцедуры

&НаКлиенте
Процедура  ПослеЗаписи(ПараметрыЗаписи)

	МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(ЭтаФорма, ПараметрыЗаписи);

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	МодификацияКонфигурацииПереопределяемый.ПередЗаписьюНаСервере(ЭтаФорма, Отказ, ТекущийОбъект, ПараметрыЗаписи);

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
	МодификацияКонфигурацииПереопределяемый.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипПолитикиПриИзменении(Элемент)
	
	ТипПолитикиПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УказыватьПриПриемкеПриИзменении(Элемент)
	Если Не Объект.УказыватьПриПриемке Тогда
		
		Объект.УказыватьПриПриемкеКомплектующихПослеРазборки    = Ложь;
		Объект.УказыватьПриПриемкеОтПоставщика                  = Ложь;
		Объект.УказыватьПриПриемкеПоВозвратуОтКлиента           = Ложь;
		Объект.УказыватьПриВозвратеНепринятыхПолучателемТоваров = Ложь;
		Объект.УказыватьПриПриемкеПоПеремещению                 = Ложь;
		Объект.УказыватьПриПриемкеПоПрочемуОприходованию        = Ложь;
		Объект.УказыватьПриПриемкеСобранныхКомплектов           = Ложь;
		Объект.УказыватьПриПриемкеПродукцииИзПроизводства       = Ложь;
		
	КонецЕсли;
	
	НастроитьЭлементыФормыНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УказыватьПриОтгрузкеПриИзменении(Элемент)
	Если Не Объект.УказыватьПриОтгрузке Тогда
		
		Объект.УказыватьПриОтгрузкеКлиенту                = Ложь;
		Объект.УказыватьПриОтгрузкеВРозницу               = Ложь;
		Объект.УказыватьПриОтгрузкеКомплектовДляРазборки  = Ложь;
		Объект.УказыватьПриОтгрузкеКомплектующихДляСборки = Ложь;
		Объект.УказыватьПриОтгрузкеНаВнутренниеНужды      = Ложь;
		Объект.УказыватьПриОтгрузкеПоВозвратуПоставщику   = Ложь;
		Объект.УказыватьПриОтгрузкеПоПеремещению          = Ложь;
		
		НастройкаУказанияСерийПриОтгрузке = "";
	Иначе
		НастройкаУказанияСерийПриОтгрузке = "ПоФактуОтбора";
	КонецЕсли;
	
	НастроитьЭлементыФормыНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УказыватьПриДвиженииМатериаловПриИзменении(Элемент)
	
	Если Не Объект.УказыватьПриДвиженииМатериалов Тогда
		
		Объект.УказыватьПриПолученииМатериалов = Ложь;
		Объект.УказыватьПриВозвратеНаСклад = Ложь;
		Объект.УказыватьПриРасходеМатериалов = Ложь;
		Объект.УказыватьПриОтраженииЗатратНаПроизводство = Ложь;
		
	КонецЕсли;
	
	НастроитьЭлементыФормыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УказыватьПриДвиженииПродукцииПриИзменении(Элемент)
	
	Если Не Объект.УказыватьПриДвиженииПродукции Тогда
		
		Объект.УказыватьПриПроизводствеПродукции = Ложь;
		Объект.УказыватьПриВыпускеВПодразделение = Ложь;
		
	КонецЕсли;
	
	НастроитьЭлементыФормыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УказыватьПриРасходеМатериаловПриИзменении(Элемент)
	
	Если НЕ ИспользуетсяПроизводство21 И ИспользуетсяПроизводство22 Тогда
		
		Объект.УказыватьПриДвиженииМатериалов = Объект.УказыватьПриРасходеМатериалов;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УказыватьПриПроизводствеПродукцииПриИзменении(Элемент)
	
	Если НЕ ИспользуетсяПроизводство21 И ИспользуетсяПроизводство22 Тогда
		
		Объект.УказыватьПриДвиженииПродукции = Объект.УказыватьПриПроизводствеПродукции;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Обработчик команды, создаваемой механизмом запрета редактирования ключевых реквизитов.
//
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	Если Не Объект.Ссылка.Пустая() Тогда
		ОткрытьФорму("Справочник.ПолитикиУчетаСерий.Форма.РазблокированиеРеквизитов",,,,,, 
			Новый ОписаниеОповещения("Подключаемый_РазрешитьРедактированиеРеквизитовОбъектаЗавершение", ЭтотОбъект), 
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъектаЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Если ТипЗнч(Результат) = Тип("Массив") И Результат.Количество() > 0 Тогда
        
        ЗапретРедактированияРеквизитовОбъектовКлиент.УстановитьДоступностьЭлементовФормы(ЭтаФорма, Результат);
        
        Элементы.НастройкаУказанияСерийПриОтгрузке.ТолькоПросмотр = Ложь;
    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаСервере
Процедура УстановитьНастройкуПриЧтенииСоздании()
	
	Если Объект.УказыватьПриПланированииОтгрузки Тогда
		НастройкаУказанияСерийПриОтгрузке = "ПриПланированииОтгрузки";
	ИначеЕсли Объект.УказыватьПриПланированииОтбора Тогда
		НастройкаУказанияСерийПриОтгрузке = "ПриПланированииОтбора";
	ИначеЕсли Объект.УказыватьПоФактуОтбора Тогда
		НастройкаУказанияСерийПриОтгрузке = "ПоФактуОтбора";
	КонецЕсли;
	
	НастроитьЭлементыФормыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормыНаСервере()
	
	ИспользоватьОрдернуюСхемуПриОтраженииИзлишковНедостач = ПолучитьФункциональнуюОпцию("ИспользоватьОрдернуюСхемуПриОтраженииИзлишковНедостач");
	
	//++ НЕ УТ
	ИспользуетсяПроизводство22Без21 = ИспользуетсяПроизводство22 И Не ИспользуетсяПроизводство21;
	//-- НЕ УТ
	
	Если Объект.ТипПолитики = Перечисления.ТипыПолитикУказанияСерий.СправочноеУказаниеСерий Тогда
		
		Элементы.УказыватьПриПриемкеПоВозвратуОтКлиента.Доступность           = Объект.УказыватьПриПриемке;
		Элементы.УказыватьПриВозвратеНепринятыхПолучателемТоваров.Доступность = Объект.УказыватьПриПриемке;
		Элементы.УказыватьПриПриемкеОтПоставщика.Доступность                  = Объект.УказыватьПриПриемке;
		Элементы.УказыватьПриПриемкеПоПеремещению.Доступность                 = Объект.УказыватьПриПриемке;
		Элементы.УказыватьПриПриемкеСобранныхКомплектов.Доступность           = Объект.УказыватьПриПриемке;
		Элементы.УказыватьПриПриемкеКомплектующихПослеРазборки.Доступность    = Объект.УказыватьПриПриемке;
		Элементы.УказыватьПриПриемкеПоПрочемуОприходованию.Доступность        = Объект.УказыватьПриПриемке;
		Элементы.УказыватьПриПриемкеПродукцииИзПроизводства.Доступность       = Объект.УказыватьПриПриемке;
		
		Элементы.УказыватьПриОтгрузкеКлиенту.Доступность                = Объект.УказыватьПриОтгрузке;
		Элементы.УказыватьПриОтгрузкеВРозницу.Доступность               = Объект.УказыватьПриОтгрузке;
		Элементы.УказыватьПриОтгрузкеКомплектующихДляСборки.Доступность = Объект.УказыватьПриОтгрузке;
		Элементы.УказыватьПриОтгрузкеНаВнутренниеНужды.Доступность      = Объект.УказыватьПриОтгрузке;
		Элементы.УказыватьПриОтгрузкеПоВозвратуПоставщику.Доступность   = Объект.УказыватьПриОтгрузке;
		Элементы.УказыватьПриОтгрузкеПоПеремещению.Доступность          = Объект.УказыватьПриОтгрузке;
		Элементы.УказыватьПриОтгрузкеКомплектовДляРазборки.Доступность  = Объект.УказыватьПриОтгрузке;
		
		Элементы.УказыватьПриОтраженииИзлишков.Доступность              = ИспользоватьОрдернуюСхемуПриОтраженииИзлишковНедостач;
		Элементы.УказыватьПриПеремещенииМеждуПомещениями.Доступность    = Истина;
		Элементы.УказыватьПриПриемке.Доступность                        = Истина;
		Элементы.УказыватьПриОтгрузке.Доступность                       = Истина;
		Элементы.НастройкаУказанияСерийПриОтгрузке.Доступность          = Ложь;
		
		//++ НЕ УТ
		Элементы.УказыватьПриДвиженииМатериалов.Доступность             = Истина;
		Элементы.УказыватьПриПолученииМатериалов.Доступность            = Объект.УказыватьПриДвиженииМатериалов;
		Элементы.УказыватьПриВозвратеНаСклад.Доступность                = Объект.УказыватьПриДвиженииМатериалов;
		Элементы.УказыватьПриРасходеМатериалов.Доступность              = Объект.УказыватьПриДвиженииМатериалов Или ИспользуетсяПроизводство22Без21;
		Элементы.УказыватьПриОтраженииЗатратНаПроизводство.Доступность  = Объект.УказыватьПриДвиженииМатериалов;
		
		Элементы.УказыватьПриДвиженииПродукции.Доступность              = Истина;
		Элементы.УказыватьПриПроизводствеПродукции.Доступность          = Объект.УказыватьПриДвиженииПродукции Или ИспользуетсяПроизводство22Без21;
		Элементы.УказыватьПриВыпускеВПодразделение.Доступность          = Объект.УказыватьПриДвиженииПродукции;
		
		Элементы.ГруппаУчитыватьСерииВПроизводстве.Заголовок = НСтр("ru = 'В производстве';
																	|en = 'In production'");
		
		Элементы.УчетСерийВПереданныхНаХранениеТоварах.Доступность = Ложь;
		//-- НЕ УТ
		
		Элементы.УчетСерийВНеотфактурованныхПоставкахТоваров.Доступность = Ложь;
		Элементы.УчетТоваровВПутиОтПоставщикаПоСериям.Доступность        = Ложь;
		
	ИначеЕсли Объект.ТипПолитики = Перечисления.ТипыПолитикУказанияСерий.АвторасчетПоFEFOОстатковСерий Тогда
		
		Элементы.УказыватьПриПриемкеПоВозвратуОтКлиента.Доступность           = Ложь;
		Элементы.УказыватьПриВозвратеНепринятыхПолучателемТоваров.Доступность = Ложь;
		Элементы.УказыватьПриПриемкеОтПоставщика.Доступность                  = Ложь;
		Элементы.УказыватьПриПриемкеПоПеремещению.Доступность                 = Ложь;
		Элементы.УказыватьПриПриемкеСобранныхКомплектов.Доступность           = Ложь;
		Элементы.УказыватьПриПриемкеКомплектующихПослеРазборки.Доступность    = Ложь;
		Элементы.УказыватьПриПриемкеПоПрочемуОприходованию.Доступность        = Ложь;
		Элементы.УказыватьПриПриемкеПродукцииИзПроизводства.Доступность       = Ложь;
		
		Элементы.УказыватьПриОтгрузкеКлиенту.Доступность                = Ложь;
		Элементы.УказыватьПриОтгрузкеВРозницу.Доступность               = Ложь;
		Элементы.УказыватьПриОтгрузкеКомплектующихДляСборки.Доступность = Ложь;
		Элементы.УказыватьПриОтгрузкеНаВнутренниеНужды.Доступность      = Ложь;
		Элементы.УказыватьПриОтгрузкеПоВозвратуПоставщику.Доступность   = Ложь;
		Элементы.УказыватьПриОтгрузкеПоПеремещению.Доступность          = Ложь;
		Элементы.УказыватьПриОтгрузкеКомплектовДляРазборки.Доступность  = Ложь;
		
		Элементы.УказыватьПриОтраженииИзлишков.Доступность              = Ложь;
		Элементы.УказыватьПриПеремещенииМеждуПомещениями.Доступность    = Ложь;
		Элементы.УказыватьПриПриемке.Доступность                        = Ложь;
		Элементы.УказыватьПриОтгрузке.Доступность                       = Ложь;
		Элементы.НастройкаУказанияСерийПриОтгрузке.Доступность          = Ложь;
		
		//++ НЕ УТ
		Элементы.УказыватьПриДвиженииМатериалов.Доступность             = Ложь;
		Элементы.УказыватьПриПолученииМатериалов.Доступность            = Ложь;
		Элементы.УказыватьПриВозвратеНаСклад.Доступность                = Ложь;
		Элементы.УказыватьПриРасходеМатериалов.Доступность              = ИспользуетсяПроизводство22Без21;
		Элементы.УказыватьПриОтраженииЗатратНаПроизводство.Доступность  = Ложь;
		
		Элементы.УказыватьПриДвиженииПродукции.Доступность              = Ложь;
		Элементы.УказыватьПриПроизводствеПродукции.Доступность          = Истина;
		Элементы.УказыватьПриВыпускеВПодразделение.Доступность          = Ложь;
		
		Если ИспользуетсяПроизводство22Без21 Тогда
			Элементы.ГруппаУчитыватьСерииВПроизводстве.Заголовок = НСтр("ru = 'В производстве (справочное указание)';
																		|en = 'In production (reference specification)'");
		Иначе
			Элементы.ГруппаУчитыватьСерииВПроизводстве.Заголовок = НСтр("ru = 'В производстве';
																		|en = 'In production'");
		КонецЕсли;
		
		Элементы.УчетСерийВПереданныхНаХранениеТоварах.Доступность = Ложь;
		//-- НЕ УТ
		
		Элементы.УчетСерийВНеотфактурованныхПоставкахТоваров.Доступность = Ложь;
		Элементы.УчетТоваровВПутиОтПоставщикаПоСериям.Доступность        = Ложь;
		
	ИначеЕсли Объект.ТипПолитики = Перечисления.ТипыПолитикУказанияСерий.УправлениеОстаткамиСерий
		Или Объект.ТипПолитики = Перечисления.ТипыПолитикУказанияСерий.УправлениеПоFEFOОстаткамиСерий Тогда
		
		Элементы.УказыватьПриПриемкеПоВозвратуОтКлиента.Доступность           = Ложь;
		Элементы.УказыватьПриВозвратеНепринятыхПолучателемТоваров.Доступность = Ложь;
		Элементы.УказыватьПриПриемкеОтПоставщика.Доступность                  = Ложь;
		Элементы.УказыватьПриПриемкеПоПеремещению.Доступность                 = Ложь;
		Элементы.УказыватьПриПриемкеСобранныхКомплектов.Доступность           = Ложь;
		Элементы.УказыватьПриПриемкеКомплектующихПослеРазборки.Доступность    = Ложь;
		Элементы.УказыватьПриПриемкеПоПрочемуОприходованию.Доступность        = Ложь;
		Элементы.УказыватьПриПриемкеПродукцииИзПроизводства.Доступность       = Ложь;
		
		Элементы.УказыватьПриОтгрузкеКлиенту.Доступность                = Ложь;
		Элементы.УказыватьПриОтгрузкеВРозницу.Доступность               = Ложь;
		Элементы.УказыватьПриОтгрузкеКомплектующихДляСборки.Доступность = Ложь;
		Элементы.УказыватьПриОтгрузкеНаВнутренниеНужды.Доступность      = Ложь;
		Элементы.УказыватьПриОтгрузкеПоВозвратуПоставщику.Доступность   = Ложь;
		Элементы.УказыватьПриОтгрузкеПоПеремещению.Доступность          = Ложь;
		Элементы.УказыватьПриОтгрузкеКомплектовДляРазборки.Доступность  = Ложь;
		
		Элементы.УказыватьПриОтраженииИзлишков.Доступность              = Ложь;
		Элементы.УказыватьПриПеремещенииМеждуПомещениями.Доступность    = Ложь;
		Элементы.УказыватьПриПриемке.Доступность                        = Ложь;
		Элементы.УказыватьПриОтгрузке.Доступность                       = Ложь;
		
		Элементы.НастройкаУказанияСерийПриОтгрузке.Доступность =
			(Объект.ТипПолитики = Перечисления.ТипыПолитикУказанияСерий.УправлениеОстаткамиСерий);
			
		//++ НЕ УТ
		Элементы.УказыватьПриДвиженииМатериалов.Доступность             = Ложь;
		Элементы.УказыватьПриПолученииМатериалов.Доступность            = Ложь;
		Элементы.УказыватьПриВозвратеНаСклад.Доступность                = Ложь;
		Элементы.УказыватьПриРасходеМатериалов.Доступность              = ИспользуетсяПроизводство22Без21;
		Элементы.УказыватьПриОтраженииЗатратНаПроизводство.Доступность  = Ложь;
		
		Элементы.УказыватьПриДвиженииПродукции.Доступность              = Ложь;
		Элементы.УказыватьПриПроизводствеПродукции.Доступность          = Истина;
		Элементы.УказыватьПриВыпускеВПодразделение.Доступность          = Ложь;
		
		Если ИспользуетсяПроизводство22Без21 Тогда
			Элементы.ГруппаУчитыватьСерииВПроизводстве.Заголовок = НСтр("ru = 'В производстве (справочное указание)';
																		|en = 'In production (reference specification)'");
		Иначе
			Элементы.ГруппаУчитыватьСерииВПроизводстве.Заголовок = НСтр("ru = 'В производстве';
																		|en = 'In production'");
		КонецЕсли;
		
		Элементы.УчетСерийВПереданныхНаХранениеТоварах.Доступность = Ложь;
		//-- НЕ УТ
		
		Элементы.УчетСерийВНеотфактурованныхПоставкахТоваров.Доступность = Ложь;
		Элементы.УчетТоваровВПутиОтПоставщикаПоСериям.Доступность        = Ложь;
		
	ИначеЕсли Объект.ТипПолитики = Перечисления.ТипыПолитикУказанияСерий.УчетСебестоимостиПоСериям Тогда
		
		Элементы.УказыватьПриПриемкеПоВозвратуОтКлиента.Доступность           = Ложь;
		Элементы.УказыватьПриВозвратеНепринятыхПолучателемТоваров.Доступность = Ложь;
		Элементы.УказыватьПриПриемкеОтПоставщика.Доступность                  = Ложь;
		Элементы.УказыватьПриПриемкеПоПеремещению.Доступность                 = Ложь;
		Элементы.УказыватьПриПриемкеСобранныхКомплектов.Доступность           = Ложь;
		Элементы.УказыватьПриПриемкеКомплектующихПослеРазборки.Доступность    = Ложь;
		Элементы.УказыватьПриПриемкеПоПрочемуОприходованию.Доступность        = Ложь;
		Элементы.УказыватьПриПриемкеПродукцииИзПроизводства.Доступность       = Ложь;
		
		Элементы.УказыватьПриОтгрузкеКлиенту.Доступность                = Ложь;
		Элементы.УказыватьПриОтгрузкеВРозницу.Доступность               = Ложь;
		Элементы.УказыватьПриОтгрузкеКомплектующихДляСборки.Доступность = Ложь;
		Элементы.УказыватьПриОтгрузкеНаВнутренниеНужды.Доступность      = Ложь;
		Элементы.УказыватьПриОтгрузкеПоВозвратуПоставщику.Доступность   = Ложь;
		Элементы.УказыватьПриОтгрузкеПоПеремещению.Доступность          = Ложь;
		Элементы.УказыватьПриОтгрузкеКомплектовДляРазборки.Доступность  = Ложь;
		
		Элементы.УказыватьПриОтраженииИзлишков.Доступность              = Ложь;
		Элементы.УказыватьПриПеремещенииМеждуПомещениями.Доступность    = Ложь;
		Элементы.УказыватьПриПриемке.Доступность                        = Ложь;
		Элементы.УказыватьПриОтгрузке.Доступность                       = Ложь;
		
		Элементы.НастройкаУказанияСерийПриОтгрузке.Доступность          = Ложь;
		
		//++ НЕ УТ
		Элементы.УказыватьПриДвиженииМатериалов.Доступность             = Ложь;
		Элементы.УказыватьПриПолученииМатериалов.Доступность            = Ложь;
		Элементы.УказыватьПриВозвратеНаСклад.Доступность                = Ложь;
		Элементы.УказыватьПриРасходеМатериалов.Доступность              = Ложь;
		Элементы.УказыватьПриОтраженииЗатратНаПроизводство.Доступность  = Ложь;
		
		Элементы.УказыватьПриДвиженииПродукции.Доступность              = Ложь;
		Элементы.УказыватьПриПроизводствеПродукции.Доступность          = НЕ ИспользуетсяПроизводство22;
		Элементы.УказыватьПриВыпускеВПодразделение.Доступность          = Ложь;
		
		Элементы.ГруппаУчитыватьСерииВПроизводстве.Заголовок = НСтр("ru = 'В производстве';
																	|en = 'In production'");
		
		Элементы.УчетСерийВПереданныхНаХранениеТоварах.Доступность = Истина;
		//-- НЕ УТ
		
		Элементы.УчетСерийВНеотфактурованныхПоставкахТоваров.Доступность = Истина;
		Элементы.УчетТоваровВПутиОтПоставщикаПоСериям.Доступность        = Истина;
		
	КонецЕсли;
	
	НастроитьЭлементыФормыНаСервереЛокализация(ИспользоватьОрдернуюСхемуПриОтраженииИзлишковНедостач);
	
КонецПроцедуры

&НаСервере
Процедура ТипПолитикиПриИзмененииНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.ТипПолитики) Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.ТипПолитики = Перечисления.ТипыПолитикУказанияСерий.СправочноеУказаниеСерий Тогда
		Объект.УчетСерийПоFEFO                = Ложь;
		Объект.УчитыватьОстаткиСерий          = Ложь;
		Объект.УчитыватьСебестоимостьПоСериям = Ложь;
		
		Если Объект.УказыватьПриОтгрузке Тогда
			НастройкаУказанияСерийПриОтгрузке = "ПоФактуОтбора";
		Иначе
			НастройкаУказанияСерийПриОтгрузке = "";
		КонецЕсли;
		
		Объект.УказыватьПриПересчетеТоваров  = Ложь;
		Объект.УказыватьПриОтраженииНедостач = Ложь;
		
		Объект.УчетСерийВНеотфактурованныхПоставкахТоваров = Ложь;
		Объект.УчетТоваровВПутиОтПоставщикаПоСериям        = Ложь;
		
		//++ НЕ УТ
		Объект.УчетСерийВПереданныхНаХранениеТоварах = Ложь;
		//-- НЕ УТ
		
	ИначеЕсли Объект.ТипПолитики = Перечисления.ТипыПолитикУказанияСерий.АвторасчетПоFEFOОстатковСерий Тогда
		Объект.УчетСерийПоFEFO                     = Ложь;
		Объект.УчитыватьОстаткиСерий               = Ложь;
		Объект.УчитыватьСебестоимостьПоСериям = Ложь;
		
		Объект.УказыватьПриОтгрузке                       = Ложь;
		Объект.УказыватьПриОтгрузкеКлиенту                = Ложь;
		Объект.УказыватьПриОтгрузкеВРозницу               = Ложь;
		Объект.УказыватьПриОтгрузкеКомплектующихДляСборки = Ложь;
		Объект.УказыватьПриОтгрузкеНаВнутренниеНужды      = Ложь;
		Объект.УказыватьПриОтгрузкеПоВозвратуПоставщику   = Ложь;
		Объект.УказыватьПриОтгрузкеПоПеремещению          = Ложь;
		Объект.УказыватьПриОтгрузкеКомплектовДляРазборки  = Ложь;
		
		НастройкаУказанияСерийПриОтгрузке = "";
		
		Объект.УказыватьПриПриемке                              = Истина;
		Объект.УказыватьПриПриемкеПоВозвратуОтКлиента           = Истина;
		Объект.УказыватьПриВозвратеНепринятыхПолучателемТоваров = Истина;
		Объект.УказыватьПриПриемкеОтПоставщика                  = Истина;
		Объект.УказыватьПриПриемкеПоПеремещению                 = Истина;
		Объект.УказыватьПриПриемкеСобранныхКомплектов           = Истина;
		Объект.УказыватьПриПриемкеКомплектующихПослеРазборки    = Истина;
		Объект.УказыватьПриПриемкеПоПрочемуОприходованию        = Истина;
		Объект.УказыватьПриПриемкеПродукцииИзПроизводства       = Истина;
		
		Объект.УказыватьПриОтраженииИзлишков = Истина;
		Объект.УказыватьПриОтраженииНедостач = Ложь;
		Объект.УказыватьПриПересчетеТоваров  = Ложь;
		
		Объект.УказыватьПриПеремещенииМеждуПомещениями = Истина;
		
		//++ НЕ УТ
		Объект.УказыватьПриДвиженииМатериалов                      = Истина;
		Объект.УказыватьПриДвиженииПродукции                       = Истина;
		Объект.УказыватьПриПолученииМатериалов                     = Истина;
		Объект.УказыватьПриВозвратеНаСклад                         = Ложь;
		Если ИспользуетсяПроизводство21 Тогда
			Объект.УказыватьПриРасходеМатериалов			       = Ложь;
		КонецЕсли;
		Объект.УказыватьПриОтраженииЗатратНаПроизводство           = Ложь;
		Объект.УказыватьПриВыпускеВПодразделение                   = Истина;
		Объект.УчетСерийВПереданныхНаХранениеТоварах               = Ложь;
		//-- НЕ УТ
		
		Объект.УчетСерийВНеотфактурованныхПоставкахТоваров = Ложь;
		Объект.УчетТоваровВПутиОтПоставщикаПоСериям        = Ложь;
		
	ИначеЕсли Объект.ТипПолитики = Перечисления.ТипыПолитикУказанияСерий.УправлениеОстаткамиСерий Тогда
		Объект.УчетСерийПоFEFO                     = Ложь;
		Объект.УчитыватьСебестоимостьПоСериям = Ложь;
		Объект.УчитыватьОстаткиСерий               = Истина;
		
		Объект.УказыватьПриОтгрузке                       = Истина;
		Объект.УказыватьПриОтгрузкеКлиенту                = Истина;
		Объект.УказыватьПриОтгрузкеВРозницу               = Истина;
		Объект.УказыватьПриОтгрузкеКомплектующихДляСборки = Истина;
		Объект.УказыватьПриОтгрузкеНаВнутренниеНужды      = Истина;
		Объект.УказыватьПриОтгрузкеПоВозвратуПоставщику   = Истина;
		Объект.УказыватьПриОтгрузкеПоПеремещению          = Истина;
		Объект.УказыватьПриОтгрузкеКомплектовДляРазборки  = Истина;
		
		Объект.УказыватьПриПриемке                              = Истина;
		Объект.УказыватьПриПриемкеПоВозвратуОтКлиента           = Истина;
		Объект.УказыватьПриВозвратеНепринятыхПолучателемТоваров = Истина;
		Объект.УказыватьПриПриемкеОтПоставщика                  = Истина;
		Объект.УказыватьПриПриемкеПоПеремещению                 = Истина;
		Объект.УказыватьПриПриемкеСобранныхКомплектов           = Истина;
		Объект.УказыватьПриПриемкеКомплектующихПослеРазборки    = Истина;
		Объект.УказыватьПриПриемкеПоПрочемуОприходованию        = Истина;
		Объект.УказыватьПриПриемкеПродукцииИзПроизводства       = Истина;
		
		Объект.УказыватьПриОтраженииИзлишков = Истина;
		Объект.УказыватьПриОтраженииНедостач = Истина;
		Объект.УказыватьПриПересчетеТоваров  = Истина;
		
		Объект.УказыватьПриПеремещенииМеждуПомещениями = Истина;
		
		//++ НЕ УТ
		Объект.УказыватьПриДвиженииМатериалов                      = Истина;
		Объект.УказыватьПриДвиженииПродукции                       = Истина;
		Объект.УказыватьПриПолученииМатериалов                     = Истина;
		Объект.УказыватьПриВозвратеНаСклад                         = Истина;
		Если ИспользуетсяПроизводство21 Тогда
			Объект.УказыватьПриРасходеМатериалов				   = Истина;
		КонецЕсли;
		Объект.УказыватьПриОтраженииЗатратНаПроизводство           = Истина;
		Объект.УказыватьПриВыпускеВПодразделение                   = Истина;
		Объект.УчетСерийВПереданныхНаХранениеТоварах               = Ложь;
		//-- НЕ УТ
		
		Объект.УчетСерийВНеотфактурованныхПоставкахТоваров = Ложь;
		Объект.УчетТоваровВПутиОтПоставщикаПоСериям        = Ложь;
		
	ИначеЕсли Объект.ТипПолитики = Перечисления.ТипыПолитикУказанияСерий.УправлениеПоFEFOОстаткамиСерий Тогда
		Объект.УчетСерийПоFEFO                     = Истина;
		Объект.УчитыватьСебестоимостьПоСериям = Ложь;
		Объект.УчитыватьОстаткиСерий               = Истина;
		НастройкаУказанияСерийПриОтгрузке          = "ПриПланированииОтбора";
		
		Объект.УказыватьПриОтгрузке                       = Истина;
		Объект.УказыватьПриОтгрузкеКлиенту                = Истина;
		Объект.УказыватьПриОтгрузкеВРозницу               = Истина;
		Объект.УказыватьПриОтгрузкеКомплектующихДляСборки = Истина;
		Объект.УказыватьПриОтгрузкеНаВнутренниеНужды      = Истина;
		Объект.УказыватьПриОтгрузкеПоВозвратуПоставщику   = Истина;
		Объект.УказыватьПриОтгрузкеПоПеремещению          = Истина;
		Объект.УказыватьПриОтгрузкеКомплектовДляРазборки  = Истина;
		
		Объект.УказыватьПриПриемке                              = Истина;
		Объект.УказыватьПриПриемкеПоВозвратуОтКлиента           = Истина;
		Объект.УказыватьПриВозвратеНепринятыхПолучателемТоваров = Истина;
		Объект.УказыватьПриПриемкеОтПоставщика                  = Истина;
		Объект.УказыватьПриПриемкеПоПеремещению                 = Истина;
		Объект.УказыватьПриПриемкеПоПрочемуОприходованию        = Истина;
		Объект.УказыватьПриПриемкеСобранныхКомплектов           = Истина;
		Объект.УказыватьПриПриемкеКомплектующихПослеРазборки    = Истина;
		Объект.УказыватьПриПриемкеПродукцииИзПроизводства       = Истина;
		
		Объект.УказыватьПриОтраженииИзлишков              = Истина;
		Объект.УказыватьПриОтраженииНедостач              = Истина;
		Объект.УказыватьПриПересчетеТоваров               = Истина;
		
		Объект.УказыватьПриПеремещенииМеждуПомещениями = Истина;
		
		//++ НЕ УТ
		Объект.УказыватьПриДвиженииМатериалов                      = Истина;
		Объект.УказыватьПриДвиженииПродукции                       = Истина;
		Объект.УказыватьПриПолученииМатериалов                     = Истина;
		Объект.УказыватьПриВозвратеНаСклад                         = Истина;
		Если ИспользуетсяПроизводство21 Тогда
			Объект.УказыватьПриРасходеМатериалов				   = Истина;
		КонецЕсли;
		Объект.УказыватьПриОтраженииЗатратНаПроизводство           = Истина;
		Объект.УказыватьПриВыпускеВПодразделение                   = Истина;
		Объект.УчетСерийВПереданныхНаХранениеТоварах               = Ложь;
		//-- НЕ УТ
		
		Объект.УчетСерийВНеотфактурованныхПоставкахТоваров = Ложь;
		Объект.УчетТоваровВПутиОтПоставщикаПоСериям        = Ложь;
		
	ИначеЕсли Объект.ТипПолитики = Перечисления.ТипыПолитикУказанияСерий.УчетСебестоимостиПоСериям Тогда
		Объект.УчетСерийПоFEFO                     = Ложь;
		Объект.УчитыватьСебестоимостьПоСериям = Истина;
		Объект.УчитыватьОстаткиСерий               = Истина;
		НастройкаУказанияСерийПриОтгрузке          = "ПриПланированииОтгрузки";
		
		Объект.УказыватьПриОтгрузке                       = Истина;
		Объект.УказыватьПриОтгрузкеКлиенту                = Истина;
		Объект.УказыватьПриОтгрузкеВРозницу               = Истина;
		Объект.УказыватьПриОтгрузкеКомплектующихДляСборки = Истина;
		Объект.УказыватьПриОтгрузкеНаВнутренниеНужды      = Истина;
		Объект.УказыватьПриОтгрузкеПоВозвратуПоставщику   = Истина;
		Объект.УказыватьПриОтгрузкеПоПеремещению          = Истина;
		Объект.УказыватьПриОтгрузкеКомплектовДляРазборки  = Истина;
		
		Объект.УказыватьПриПриемке                              = Истина;
		Объект.УказыватьПриПриемкеПоВозвратуОтКлиента           = Истина;
		Объект.УказыватьПриВозвратеНепринятыхПолучателемТоваров = Истина;
		Объект.УказыватьПриПриемкеОтПоставщика                  = Истина;
		Объект.УказыватьПриПриемкеПоПеремещению                 = Истина;
		Объект.УказыватьПриПриемкеПоПрочемуОприходованию        = Истина;
		Объект.УказыватьПриПриемкеСобранныхКомплектов           = Истина;
		Объект.УказыватьПриПриемкеКомплектующихПослеРазборки    = Истина;
		Объект.УказыватьПриПриемкеПродукцииИзПроизводства       = Истина;
		
		Объект.УказыватьПриОтраженииИзлишков              = Истина;
		Объект.УказыватьПриОтраженииНедостач              = Истина;
		Объект.УказыватьПриПересчетеТоваров               = Истина;
		
		Объект.УказыватьПриПеремещенииМеждуПомещениями = Истина;
		
		//++ НЕ УТ
		Объект.УказыватьПриДвиженииМатериалов                      = Истина;
		Объект.УказыватьПриДвиженииПродукции                       = Истина;
		Объект.УказыватьПриПолученииМатериалов                     = Истина;
		Объект.УказыватьПриВозвратеНаСклад                         = Истина;
		Объект.УказыватьПриРасходеМатериалов					   = Истина;
		Если ИспользуетсяПроизводство22 Тогда
			Объект.УказыватьПриПроизводствеПродукции               = Истина;
		КонецЕсли;
		Объект.УказыватьПриОтраженииЗатратНаПроизводство           = Истина;
		Объект.УказыватьПриВыпускеВПодразделение                   = Истина;
		Объект.УчетСерийВПереданныхНаХранениеТоварах               = Ложь;
		//-- НЕ УТ
		
		Объект.УчетСерийВНеотфактурованныхПоставкахТоваров = Ложь;
		Объект.УчетТоваровВПутиОтПоставщикаПоСериям        = Ложь;
		
	КонецЕсли;
	
	ТипПолитикиПриИзмененииНаСервереЛокализация();
	
	НастроитьЭлементыФормыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокТиповПолитик()
	
	ПодсистемаГИСМ = ОбщегоНазначения.ПодсистемаСуществует("ГосИС.ГИСМ");
	СписокВыбораЭлементовТиповПолитик = Элементы.ТипПолитики.СписокВыбора;
	СписокВыбораЭлементовТиповПолитик.Очистить();
	
	Для Каждого ТипПолитики Из Перечисления.ТипыПолитикУказанияСерий Цикл
		Если Не ТипПолитики = Перечисления.ТипыПолитикУказанияСерий.МаркировкаПродукцииДляГИСМ
			Или ПодсистемаГИСМ Тогда
			СписокВыбораЭлементовТиповПолитик.Добавить(ТипПолитики);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Локализация

&НаСервере
Процедура НастроитьЭлементыФормыНаСервереЛокализация(ИспользоватьОрдернуюСхемуПриОтраженииИзлишковНедостач)
	
	//++ Локализация
	
	//++ НЕ УТ
	
	ИспользуетсяПроизводство22Без21 = ИспользуетсяПроизводство22 И Не ИспользуетсяПроизводство21;
	
	Если Объект.ТипПолитики = Перечисления.ТипыПолитикУказанияСерий.СправочноеУказаниеСерий Тогда
		Элементы.ГруппаВЭксплуатации.Заголовок = НСтр("ru = 'В эксплуатации (управление остатками серий)';
														|en = 'In operation (remaining batch management)'");
	Иначе
		Элементы.ГруппаВЭксплуатации.Заголовок = НСтр("ru = 'В эксплуатации';
														|en = 'Operating'");
	КонецЕсли;
	
	//-- НЕ УТ
	
	Если Объект.ТипПолитики = Перечисления.ТипыПолитикУказанияСерий.МаркировкаПродукцииДляГИСМ Тогда
		
		Элементы.УказыватьПриПриемке.Доступность                    = Ложь;
		Элементы.УказыватьПриПриемкеПоВозвратуОтКлиента.Доступность = Ложь;
		Элементы.УказыватьПриПриемкеПродукцииИзПроизводства.Доступность       = Объект.УказыватьПриПриемке;
		Элементы.УказыватьПриВозвратеНепринятыхПолучателемТоваров.Доступность = Объект.УказыватьПриПриемке;
		Элементы.УказыватьПриПриемкеОтПоставщика.Доступность                  = Объект.УказыватьПриПриемке;
		Элементы.УказыватьПриПриемкеПоПеремещению.Доступность                 = Объект.УказыватьПриПриемке;
		Элементы.УказыватьПриПриемкеСобранныхКомплектов.Доступность           = Объект.УказыватьПриПриемке;
		Элементы.УказыватьПриПриемкеКомплектующихПослеРазборки.Доступность    = Объект.УказыватьПриПриемке;
		Элементы.УказыватьПриПриемкеПоПрочемуОприходованию.Доступность        = Объект.УказыватьПриПриемке;
		
		Элементы.УказыватьПриОтгрузке.Доступность                     = Ложь;
		Элементы.УказыватьПриОтгрузкеКлиенту.Доступность              = Ложь;
		Элементы.УказыватьПриОтгрузкеВРозницу.Доступность             = Ложь;
		Элементы.УказыватьПриОтгрузкеПоВозвратуПоставщику.Доступность = Ложь;
		Элементы.УказыватьПриОтгрузкеНаВнутренниеНужды.Доступность    = Ложь;
		Элементы.УказыватьПриОтгрузкеКомплектующихДляСборки.Доступность = Объект.УказыватьПриОтгрузке;
		Элементы.УказыватьПриОтгрузкеПоПеремещению.Доступность          = Объект.УказыватьПриОтгрузке;
		Элементы.УказыватьПриОтгрузкеКомплектовДляРазборки.Доступность  = Объект.УказыватьПриОтгрузке;
		
		Элементы.УказыватьПриОтраженииИзлишков.Доступность              = ИспользоватьОрдернуюСхемуПриОтраженииИзлишковНедостач;
		Элементы.УказыватьПриПеремещенииМеждуПомещениями.Доступность    = Истина;
		Элементы.НастройкаУказанияСерийПриОтгрузке.Доступность          = Ложь;
		
		//++ НЕ УТ
		Элементы.УказыватьПриДвиженииМатериалов.Доступность             = Истина;
		Элементы.УказыватьПриПолученииМатериалов.Доступность            = Объект.УказыватьПриДвиженииМатериалов;
		Элементы.УказыватьПриВозвратеНаСклад.Доступность                = Объект.УказыватьПриДвиженииМатериалов;
		Элементы.УказыватьПриРасходеМатериалов.Доступность              = Объект.УказыватьПриДвиженииМатериалов Или ИспользуетсяПроизводство22Без21;
		Элементы.УказыватьПриОтраженииЗатратНаПроизводство.Доступность  = Объект.УказыватьПриДвиженииМатериалов;
		
		Элементы.УказыватьПриДвиженииПродукции.Доступность              = Истина;
		Элементы.УказыватьПриПроизводствеПродукции.Доступность          = Объект.УказыватьПриДвиженииПродукции Или ИспользуетсяПроизводство22Без21;
		Элементы.УказыватьПриВыпускеВПодразделение.Доступность          = Объект.УказыватьПриДвиженииПродукции;
		
		Если ИспользуетсяПроизводство22Без21 Тогда
			Элементы.ГруппаУчитыватьСерииВПроизводстве.Заголовок = НСтр("ru = 'В производстве (справочное указание)';
																		|en = 'In production (reference specification)'");
		Иначе
			Элементы.ГруппаУчитыватьСерииВПроизводстве.Заголовок = НСтр("ru = 'В производстве';
																		|en = 'In production'");
		КонецЕсли;
		
		Элементы.УчетСерийВПереданныхНаХранениеТоварах.Доступность = Ложь;
		//-- НЕ УТ
		
		Элементы.УчетСерийВНеотфактурованныхПоставкахТоваров.Доступность = Ложь;
		Элементы.УчетТоваровВПутиОтПоставщикаПоСериям.Доступность        = Ложь;
		
	КонецЕсли;
	//-- Локализация
	
	Возврат;

КонецПроцедуры

&НаСервере
Процедура ТипПолитикиПриИзмененииНаСервереЛокализация()
	
	//++ Локализация
	Объект.УказыватьПриМаркировкеПродукцииДляГИСМ = Объект.ТипПолитики = Перечисления.ТипыПолитикУказанияСерий.МаркировкаПродукцииДляГИСМ;
	
	Если Объект.ТипПолитики = Перечисления.ТипыПолитикУказанияСерий.МаркировкаПродукцииДляГИСМ Тогда
		Объект.УчетСерийПоFEFO                     = Ложь;
		Объект.УчитыватьОстаткиСерий               = Ложь;
		Объект.УчитыватьСебестоимостьПоСериям      = Ложь;
		
		Объект.УказыватьПриОтгрузке                     = Истина;
		Объект.УказыватьПриОтгрузкеКлиенту              = Истина;
		Объект.УказыватьПриОтгрузкеВРозницу             = Истина;
		Объект.УказыватьПриОтгрузкеПоВозвратуПоставщику = Истина;
		Объект.УказыватьПриОтгрузкеНаВнутренниеНужды    = Истина;
		
		НастройкаУказанияСерийПриОтгрузке = "ПоФактуОтбора";
		
		Объект.УказыватьПриПриемке = Истина;
		Объект.УказыватьПриПриемкеОтПоставщика = Истина;
		Объект.УказыватьПриПриемкеПоВозвратуОтКлиента = Истина;
		Объект.УказыватьПриПриемкеПродукцииИзПроизводства = Истина;
		
		Объект.УказыватьПриМаркировкеПродукцииДляГИСМ = Истина;
		
		Объект.УказыватьПриПересчетеТоваров  = Ложь;
		Объект.УказыватьПриОтраженииНедостач = Ложь;
		
		Объект.УчетСерийВНеотфактурованныхПоставкахТоваров = Ложь;
		Объект.УчетТоваровВПутиОтПоставщикаПоСериям        = Ложь;
		
		//++ НЕ УТ
		Объект.УчетСерийВПереданныхНаХранениеТоварах = Ложь;
		//-- НЕ УТ
		
	КонецЕсли;
	//-- Локализация
	
	Возврат;

КонецПроцедуры

#КонецОбласти

#КонецОбласти
