
&НаКлиенте
Процедура ЗаполнитьПоГруппеОрганизаций(Команда)
		
	Оповещение = Новый ОписаниеОповещения("Подключаемый_ВыборГруппыОрганизаций", ЭтотОбъект);
	ОткрытьФорму("Справочник.Организации.ФормаВыбора", ,,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыборГруппыОрганизаций(Результат, ДополнительныеПараметры) Экспорт
	
	ОрганизацияРодитель = Результат;
	
	Если ОрганизацияРодитель <> Неопределено Тогда
		
		ОчиститьПолеИЗаполнитьТаблицуОрганизаций(ОрганизацияРодитель);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПолеИЗаполнитьТаблицуОрганизаций(ОбъектЗаполненияВход)
	
	Если Объект.Организации.Количество() > 0 Тогда
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("ОбъектЗаполнения", ОбъектЗаполненияВход);
		ТекстВопроса = НСтр("ru = 'Удалить существующие элементы?'");
		Режим = РежимДиалогаВопрос.ДаНетОтмена;
		Оповещение = Новый ОписаниеОповещения("ПодтверждениеОчисткиПоля_Завершение", ЭтаФорма, СтруктураПараметров);
		ПоказатьВопрос(Оповещение, ТекстВопроса, Режим, 0);
	Иначе
		ЗаполнитьОрганизации(ОбъектЗаполненияВход);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодтверждениеОчисткиПоля_Завершение(Результат, ДопПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Объект.Организации.Очистить();
		
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда	
		
		Возврат;	
		
	Иначе
		// Не очищаем поле.
		
	КонецЕсли;
	
	ЗаполнитьОрганизации(ДопПараметры.ОбъектЗаполнения);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьМассивИсключений()
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	ГруппыДоступаОрганизацииВидыОтчетовОрганизации.Организация КАК Организация
	|ИЗ
	|	Справочник.ГруппыДоступаОрганизацииВидыОтчетов.Организации КАК ГруппыДоступаОрганизацииВидыОтчетовОрганизации
	|ГДЕ
	|	НЕ ГруппыДоступаОрганизацииВидыОтчетовОрганизации.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",Объект.Ссылка);
	
	МассивИсключений=Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Организация");
	
	Для Каждого Строка ИЗ Объект.Организации Цикл
		
		МассивИсключений.Добавить(Строка.Организация);
		
	КонецЦикла;
	
	Возврат МассивИсключений;
		
КонецФункции // ПолучитьМассивИсключений()

&НаСервере
Процедура ЗаполнитьОрганизации(ОрганизацияРодитель)
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	Организации.Ссылка КАК Организация
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка В ИЕРАРХИИ(&Организация)
	|	И НЕ Организации.Ссылка В (&МассивИсключений)";
	
	Запрос.УстановитьПараметр("Организация",ОрганизацияРодитель);
	Запрос.УстановитьПараметр("МассивИсключений",ПолучитьМассивИсключений());
	
	ОбщегоНазначенияУХ.ЗагрузитьВТаблицуЗначений(Запрос.Выполнить().Выгрузить(),Объект.Организации);
	Модифицированность=Истина;	
	
КонецПроцедуры // ЗаполнитьОрганизации()
