&НаСервере
Процедура ОбновитьОтборПоВладельцу()
	
	ОбщегоНазначенияКлиентСерверУХ.УстановитьЭлементОтбора(Список.Отбор,"Владелец",ВидОтчета,ВидСравненияКомпоновкиДанных.Равно,,Истина);
		
КонецПроцедуры // ОбновитьОтборПоВладельцу()

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Если Параметры.Отбор.Свойство("Владелец") И ЗначениеЗаполнено(Параметры.Отбор.Владелец) Тогда
		
		ВидОтчета=Параметры.Отбор.Владелец;
				
		ОбновитьОтборПоВладельцу();
		
		Элементы.ВидОтчета.Доступность=Ложь;
		
	ИначеЕсли НЕ ЗначениеЗаполнено(ВидОтчета) Тогда
		
		Элементы.ВидОтчета.Доступность=Истина;
		
		ЭлементВИБ=Справочники.ВидыОтчетов.Выбрать();
		
		Пока ЭлементВИБ.Следующий() Цикл
			
			Если  НЕ(ЭлементВИБ.ЭтоГруппа ИЛИ ЭлементВИБ.ПометкаУдаления)  Тогда
				
				ВидОтчета=ЭлементВИБ.Ссылка;
				ОбновитьОтборПоВладельцу();
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОтчетаПриИзменении(Элемент)
	
	ОбновитьОтборПоВладельцу();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
		
	Если Параметры.Отбор.Свойство("Владелец") И ЗначениеЗаполнено(Параметры.Отбор.Владелец) Тогда
		
		ВидОтчета=Параметры.Отбор.Владелец;
	
	ИначеЕсли ЗначениеЗаполнено(ВидОтчета) Тогда
		
		ОбновитьОтборПоВладельцу();

	КонецЕсли;
	
КонецПроцедуры

// Возвращает незанятое наименование по умолчанию для бланка отчета, принадлежащего
// виду отчета ВидОтчетаВход.
&НаСервереБезКонтекста
Функция СгенерироватьНаименованиеНовогоБланка(ВидОтчетаВход)
	РезультатФункции = Справочники.ВидыОтчетов.СгенерироватьНаименованиеНовогоБланкаДляВидаОтчета(ВидОтчетаВход);
	Возврат РезультатФункции;
КонецФункции

&НаКлиенте
Процедура СоздатьБланкЭкземпляраОтчета(Команда)
	Если ЗначениеЗаполнено(ВидОтчета) Тогда
		Если Не ОбщегоНазначенияУХ.ПолучитьЗначениеРеквизита(ВидОтчета, "ЭтоГруппа") Тогда
			НовоеНаименование = СгенерироватьНаименованиеНовогоБланка(ВидОтчета);
			СтруктураПараметров = Новый Структура;
			ПараметрыЗаполнения = Новый Структура;
			ПараметрыЗаполнения.Вставить("РежимБланка", 0);
			СтруктураПараметров.Вставить("ЗначенияЗаполнения", ПараметрыЗаполнения);
			СтруктураПараметров.Вставить("НаименованиеБланка", НовоеНаименование);
			ОткрытьФорму("Справочник.БланкиОтчетов.Форма.ФормаМакета", СтруктураПараметров, ВладелецФормы); 
		Иначе
			ТекстСообщения = НСтр("ru = 'Выбрана группа отчетов. Операция отменена.'");
			ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
		КонецЕсли;
	Иначе
		ТекстСообщения = НСтр("ru = 'Требуется выбрать вид отчета. Операция отменена.'");
		ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СоздатьБланкСводнойТаблицы(Команда)
	Если ЗначениеЗаполнено(ВидОтчета) Тогда
		Если Не ОбщегоНазначенияУХ.ПолучитьЗначениеРеквизита(ВидОтчета, "ЭтоГруппа") Тогда
			УправлениеОтчетамиКлиентУХ.СоздатьБланкСводнойТаблицыИОткрытьФорму(ВидОтчета, ВладелецФормы);
		Иначе
			ТекстСообщения = НСтр("ru = 'Выбрана группа отчетов. Операция отменена.'");
			ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
		КонецЕсли;
	Иначе
		ТекстСообщения = НСтр("ru = 'Требуется выбрать вид отчета. Операция отменена.'");
		ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
	КонецЕсли;
КонецПроцедуры
