////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ФУНКЦИИ.
//

&НаКлиенте
Процедура ПереключитьСтраницуГлавнойПанели()
	
	Элементы.ГлавнаяПанель.ТекущаяСтраница = ?(ШаблонПроцесса.Пустая(), Элементы.ПустойМаршрут, Элементы.ОбычныйРежим);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьОтборПоВладельцу()
	
	ТиповыеОтчеты_КлиентУХ.ДобавитьОтбор(Список.Отбор, "Владелец", ШаблонПроцесса);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПолеВладелец = Новый ПолеКомпоновкиДанных("Владелец");
	Для Каждого Элемент Из Список.Отбор.Элементы Цикл
		Если Элемент.ЛевоеЗначение = ПолеВладелец И Элемент.Использование Тогда
			ШаблонПроцесса = Элемент.ПравоеЗначение;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	ПереключитьСтраницуГлавнойПанели();
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ.
//
&НаКлиенте
Процедура ШаблонПроцессаПриИзменении(Элемент)
	
	ПрименитьОтборПоВладельцу();
	ПереключитьСтраницуГлавнойПанели();
	
КонецПроцедуры

&НаКлиенте
Процедура КонструкторМаршрутаНажатие(Элемент)
	
	Если ШаблонПроцесса.Пустая() Тогда
		ПоказатьПредупреждение(, Нстр("ru = 'Не выбран шаблон процесса!'"));
	Иначе
		ОткрытьФорму("Отчет.СетеваяДиаграммаШаблонаУниверсальногоПроцесса.Форма", Новый Структура("ШаблонПроцесса", ШаблонПроцесса));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьЭтапыПоТребованию(Команда)
	
	Если  НЕ Элементы.ФормаПоказатьЭтапыПоТребованию.Пометка Тогда
		ОбщегоНазначенияКлиентСерверУХ.УстановитьЭлементОтбора(Список.Отбор,"ДинамическийЭтап",Ложь,ВидСравненияКомпоновкиДанных.Равно,,Истина);	
	Иначе
		ОбщегоНазначенияКлиентСерверУХ.УстановитьЭлементОтбора(Список.Отбор,"ДинамическийЭтап",Ложь,ВидСравненияКомпоновкиДанных.Равно,,Ложь);	
	КонецЕсли;
	
	Элементы.ФормаПоказатьЭтапыПоТребованию.Пометка = НЕ  Элементы.ФормаПоказатьЭтапыПоТребованию.Пометка;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры



