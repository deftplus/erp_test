
&НаКлиенте
Процедура ЗаполнитьПоГруппе(Команда)
	
	Оповещение = Новый ОписаниеОповещения("Подключаемый_ВыборГруппыПользователей", ЭтотОбъект);
	ОткрытьФорму("Справочник.ГруппыПользователей.ФормаВыбора",Новый Структура("РежимВыбора",Истина) ,,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыборГруппыПользователей(Результат, ДополнительныеПараметры) Экспорт
	
	ГруппаПользователей = Результат;
	
	Если ГруппаПользователей <> Неопределено Тогда
		
		ОчиститьПолеИЗаполнитьТаблицуПользователей(ГруппаПользователей);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПолеИЗаполнитьТаблицуПользователей(ОбъектЗаполненияВход)
	
	Если Объект.Пользователи.Количество() > 0 Тогда
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("ОбъектЗаполнения", ОбъектЗаполненияВход);
		ТекстВопроса = НСтр("ru = 'Удалить существующие элементы?'");
		Режим = РежимДиалогаВопрос.ДаНетОтмена;
		Оповещение = Новый ОписаниеОповещения("ПодтверждениеОчисткиПоля_Завершение", ЭтаФорма, СтруктураПараметров);
		ПоказатьВопрос(Оповещение, ТекстВопроса, Режим, 0);
	Иначе
		ЗаполнитьПользователей(ОбъектЗаполненияВход);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодтверждениеОчисткиПоля_Завершение(Результат, ДопПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Объект.Пользователи.Очистить();
		
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда	
		
		Возврат;	
		
	Иначе
		// Не очищаем поле.
		
	КонецЕсли;
	
	ЗаполнитьПользователей(ДопПараметры.ОбъектЗаполнения);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьМассивИсключений()
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	ГруппыДоступаПользователиВидыОтчетовПользователи.Пользователь КАК Пользователь
	|ИЗ
	|	Справочник.ГруппыДоступаПользователиВидыОтчетов.Пользователи КАК ГруппыДоступаПользователиВидыОтчетовПользователи
	|ГДЕ
	|	НЕ ГруппыДоступаПользователиВидыОтчетовПользователи.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",Объект.Ссылка);
	
	МассивИсключений=Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Пользователь");
	
	Для Каждого Строка ИЗ Объект.Пользователи Цикл
		
		МассивИсключений.Добавить(Строка.Пользователь);
		
	КонецЦикла;
	
	Возврат МассивИсключений;
		
КонецФункции // ПолучитьМассивИсключений()

&НаСервере
Процедура ЗаполнитьПользователей(ГруппаПользователей)
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	ГруппыПользователейСостав.Пользователь КАК Пользователь
	|ИЗ
	|	Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
	|ГДЕ
	|	ГруппыПользователейСостав.Ссылка = &Ссылка
	|	И НЕ ГруппыПользователейСостав.Пользователь В (&МассивИсключений)";
	
	Запрос.УстановитьПараметр("Ссылка",ГруппаПользователей);
	Запрос.УстановитьПараметр("МассивИсключений",ПолучитьМассивИсключений());
	
	ОбщегоНазначенияУХ.ЗагрузитьВТаблицуЗначений(Запрос.Выполнить().Выгрузить(),Объект.Пользователи);
	Модифицированность=Истина;	
	
КонецПроцедуры // ЗаполнитьОрганизации()
