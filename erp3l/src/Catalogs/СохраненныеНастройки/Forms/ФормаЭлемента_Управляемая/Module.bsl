
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ РежимРедактирования Тогда
		
		РасширениеБизнесЛогикиУХКлиент.ОткрытьФормыБизнесАнализСохраненныеНастройки(Объект.НастраиваемыйОбъект, Объект.Ссылка, Отказ);
		
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РедактированиеПараметр = (Параметры.РежимРедактирования = Истина);
	ЭтоКопирование = (ЗначениеЗаполнено(Параметры.ЗначениеКопирования));
	РежимРедактирования = РедактированиеПараметр ИЛИ ЭтоКопирование;
	
	Если Параметры.Ключ.Пустая() Тогда
	
		Если Не ЗначениеЗаполнено(Объект.Владелец) Тогда
			Объект.Владелец = ОбщегоНазначенияУХ.ТекущийПользователь();
		КонецЕсли;		
	
	КонецЕсли;
	// Закешируем настройки из исходного объекта, чтобы иметь возможность сохранить их в новом объекте.
	Если ЭтоКопирование Тогда
		ЗначениеНастроек = Параметры.ЗначениеКопирования.ХранилищеНастроек.Получить();
		АдресХранилищаНастроек = ПоместитьВоВременноеХранилище(ЗначениеНастроек, ЭтаФорма);
	Иначе
		// Нет исходных настроек для копирования.
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ЭтоАдресВременногоХранилища(АдресХранилищаНастроек) Тогда
		ЗначениеНастроек = ПолучитьИзВременногоХранилища(АдресХранилищаНастроек);
		ТекущийОбъект.ХранилищеНастроек = Новый ХранилищеЗначения(ЗначениеНастроек);
	Иначе
		// Нет закешированных настроек.
	КонецЕсли;
КонецПроцедуры
