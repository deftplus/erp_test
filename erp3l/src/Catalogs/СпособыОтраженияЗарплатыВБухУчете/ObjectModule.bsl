
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ПараметрыВыбораСтатейИАналитик = Справочники.СпособыОтраженияЗарплатыВБухУчете.ПараметрыВыбораСтатейИАналитик();
	ДоходыИРасходыСервер.ОбработкаЗаполнения(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НеИспользуемыеРеквизиты = Справочники.СпособыОтраженияЗарплатыВБухУчете.НеИспользуемыеРеквизиты(ЭтотОбъект);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(
		ПроверяемыеРеквизиты,
		НеИспользуемыеРеквизиты);
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ПодбираемыеАналитики = Справочники.СпособыОтраженияЗарплатыВБухУчете.ПодбираемыеАналитики();
	
	Если ПодбираемыеАналитики.Найти(ТипЗнч(АналитикаРасходов)) <> Неопределено Тогда
		МассивНепроверяемыхРеквизитов.Добавить("АналитикаРасходов");
	КонецЕсли;
		
	Если ПодбираемыеАналитики.Найти(ТипЗнч(АналитикаРасходовВзносов)) <> Неопределено Тогда
		МассивНепроверяемыхРеквизитов.Добавить("АналитикаРасходовВзносов");
	КонецЕсли;
		
	Если ПодбираемыеАналитики.Найти(ТипЗнч(АналитикаРасходовЕНВД)) <> Неопределено Тогда
		МассивНепроверяемыхРеквизитов.Добавить("АналитикаРасходовЕНВД");
	КонецЕсли;
			
	Если ПодбираемыеАналитики.Найти(ТипЗнч(АналитикаРасходовВзносовЕНВД)) <> Неопределено Тогда
		МассивНепроверяемыхРеквизитов.Добавить("АналитикаРасходовВзносовЕНВД");
	КонецЕсли;
	
	Если ПодбираемыеАналитики.Найти(ТипЗнч(АналитикаРасходовТравматизм)) <> Неопределено Тогда
		МассивНепроверяемыхРеквизитов.Добавить("АналитикаРасходовТравматизм");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(
		ПроверяемыеРеквизиты,
		МассивНепроверяемыхРеквизитов);
	
	ПараметрыВыбораСтатейИАналитик = Справочники.СпособыОтраженияЗарплатыВБухУчете.ПараметрыВыбораСтатейИАналитик();
	ДоходыИРасходыСервер.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, ПараметрыВыбораСтатейИАналитик);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ЕстьПрочиеОперации = Справочники.СпособыОтраженияЗарплатыВБухУчете.ОтражениеПрочихОпераций(ЭтотОбъект);
	
	// очистка неиспользуемых реквизитов
	НеИспользуемыеРеквизиты = Справочники.СпособыОтраженияЗарплатыВБухУчете.НеИспользуемыеРеквизиты(ЭтотОбъект);
	
	Для Каждого Реквизит Из НеИспользуемыеРеквизиты Цикл
		ЭтотОбъект[Реквизит] = Неопределено;
	КонецЦикла;
	
	ПараметрыВыбораСтатейИАналитик = Справочники.СпособыОтраженияЗарплатыВБухУчете.ПараметрыВыбораСтатейИАналитик();
	ДоходыИРасходыСервер.ПередЗаписью(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
	// Настройка счетов учета.
	ПараметрыНастройкиСчетовУчета = Справочники.СпособыОтраженияЗарплатыВБухУчете.ПараметрыНастройкиСчетовУчета();
	НастройкаСчетовУчетаСервер.ПередЗаписью(ЭтотОбъект, ПараметрыНастройкиСчетовУчета);
	
	// установка значений по умолчанию
	Если Не ЗначениеЗаполнено(БазаРаспределенияПоСдельнымРаботам) Тогда
		БазаРаспределенияПоСдельнымРаботам = Перечисления.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда;
	КонецЕсли;
	
	Если РежимУчетаВзносов = 0 Тогда
		СтатьяРасходовВзносов = СтатьяРасходов;
		АналитикаРасходовВзносов = АналитикаРасходов;
		АналитикаАктивовПассивовВзносов = АналитикаАктивовПассивов;
		// Настройка счетов учета.
		НастройкаСчетовУчетаСервер.СкопироватьНастройкуСчетовОбъекта(ЭтотОбъект,
			ПараметрыНастройкиСчетовУчета, "НастройкаСчетовУчета", "НастройкаСчетовУчетаВзносов");
	КонецЕсли;
	
	Если ОсобыйСоответствуетОсновному Тогда
		
		СтатьяРасходовЕНВД = СтатьяРасходов;
		АналитикаРасходовЕНВД = АналитикаРасходов;
		АналитикаАктивовПассивовЕНВД = АналитикаАктивовПассивов;
		// Настройка счетов учета.
		НастройкаСчетовУчетаСервер.СкопироватьНастройкуСчетовОбъекта(ЭтотОбъект,
			ПараметрыНастройкиСчетовУчета, "НастройкаСчетовУчета", "НастройкаСчетовУчетаЕНВД");
		
		СтатьяРасходовВзносовЕНВД = СтатьяРасходовВзносов;
		АналитикаРасходовВзносовЕНВД = АналитикаРасходовВзносов;
		АналитикаАктивовПассивовВзносовЕНВД = АналитикаАктивовПассивовВзносов;
		// Настройка счетов учета.
		НастройкаСчетовУчетаСервер.СкопироватьНастройкуСчетовОбъекта(ЭтотОбъект,
			ПараметрыНастройкиСчетовУчета, "НастройкаСчетовУчетаВзносов", "НастройкаСчетовУчетаВзносовЕНВД");
		
	ИначеЕсли РежимУчетаВзносовЕНВД = 0 Тогда
		
		СтатьяРасходовВзносовЕНВД = СтатьяРасходовЕНВД;
		АналитикаРасходовВзносовЕНВД = АналитикаРасходовЕНВД;
		АналитикаАктивовПассивовВзносовЕНВД = АналитикаАктивовПассивовЕНВД;
		// Настройка счетов учета.
		НастройкаСчетовУчетаСервер.СкопироватьНастройкуСчетовОбъекта(ЭтотОбъект,
			ПараметрыНастройкиСчетовУчета, "НастройкаСчетовУчетаЕНВД", "НастройкаСчетовУчетаВзносовЕНВД");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
