#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает признак использования статей расходов устаревшего формата в способе отражения зарплаты.
//	Параметры:
//		Объект - СправочникОбъект.СпособыОтраженияЗарплатыВБухУчете - проверяемый способ отражения
//	Возвращаемое значение:
//		Булево - ИСТИНА, если в способе используются статьи расходов с вариантом распределения На прочие активы.
//
Функция ОтражениеПрочихОпераций(Объект) Экспорт
	
	ЭтоОтражениеПрочихОпераций = Ложь;
	
	МассивСтатей = Новый Массив;
	
	Если ТипЗнч(Объект.СтатьяРасходов) = Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов")
		И ЗначениеЗаполнено(Объект.СтатьяРасходов) Тогда
		МассивСтатей.Добавить(Объект.СтатьяРасходов);
	КонецЕсли;
	
	Если ТипЗнч(Объект.СтатьяРасходовЕНВД) = Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов")
		И ЗначениеЗаполнено(Объект.СтатьяРасходовЕНВД) Тогда
		МассивСтатей.Добавить(Объект.СтатьяРасходовЕНВД);
	КонецЕсли;
	
	ПараметрыСтатей = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивСтатей, "ВариантРаспределенияРасходовРегл, ВидЦенностиНДС");
	
	Для Каждого Статья Из ПараметрыСтатей Цикл
		
		Если Статья.Значение.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаПрочиеАктивы
			И Статья.Значение.ВидЦенностиНДС = Перечисления.ВидыЦенностей.ПрочиеРаботыИУслуги Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Возвращает текст запроса для получения параметров способа отражения.
//	Параметры:
//		ИмяВременнойТаблицы - Строка - имя таблицы, если требуется помещения результата во временную таблицу.
//	Возвращаемое значение:
//		Строка - текст запроса.
//
Функция ТекстЗапросаДанныхСпособов(ИмяВременнойТаблицы = "") Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СписокСпособов.СпособОтражения,
	|	ЛОЖЬ КАК ОблагаетсяЕНВД,
	|	СпособОтраженияЗарплата.ОплатаСдельныхРабот КАК ОплатаСдельныхРабот,
	|	СпособОтраженияЗарплата.СтатьяРасходов КАК СтатьяРасходов,
	|	СпособОтраженияЗарплата.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СпособОтраженияЗарплата.СтатьяРасходов.ВариантРаспределенияРасходовРегл КАК ВариантРаспределения,
	|	СпособОтраженияЗарплата.СтатьяРасходов.ПринятиеКналоговомуУчету КАК ПринятиеКналоговомуУчету,
	|	СпособОтраженияЗарплата.АналитикаРасходов КАК АналитикаРасходов,
	|	СпособОтраженияЗарплата.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчета КАК НастройкаСчетовУчета,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчета.СчетУчета КАК СчетУчета,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчета.Субконто1 КАК Субконто1,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчета.Субконто2 КАК Субконто2,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчета.Субконто3 КАК Субконто3,
	
	|	СпособОтраженияЗарплата.СтатьяРасходовВзносов КАК СтатьяРасходовВзносов,
	|	СпособОтраженияЗарплата.СтатьяРасходовВзносов.ВариантРаспределенияРасходовРегл КАК ВариантРаспределенияВзносов,
	|	СпособОтраженияЗарплата.СтатьяРасходовВзносов.ПринятиеКналоговомуУчету КАК ПринятиеКналоговомуУчетуВзносов,
	|	СпособОтраженияЗарплата.АналитикаРасходовВзносов КАК АналитикаРасходовВзносов,
	|	СпособОтраженияЗарплата.АналитикаАктивовПассивовВзносов КАК АналитикаАктивовПассивовВзносов,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносов КАК НастройкаСчетовУчетаВзносов,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносов.СчетУчета КАК СчетУчетаВзносов,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносов.Субконто1 КАК Субконто1Взносов,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносов.Субконто2 КАК Субконто2Взносов,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносов.Субконто3 КАК Субконто3Взносов,
	
	// взносы травматизм
	|	СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм КАК РежимУчетаВзносовНаТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.СтатьяРасходовТравматизм
	|		ИНАЧЕ СпособОтраженияЗарплата.СтатьяРасходовВзносов
	|	КОНЕЦ                                    КАК СтатьяРасходовТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.СтатьяРасходовТравматизм.ВариантРаспределенияРасходовРегл
	|		ИНАЧЕ СпособОтраженияЗарплата.СтатьяРасходовВзносов.ВариантРаспределенияРасходовРегл
	|	КОНЕЦ                                    КАК ВариантРаспределенияТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.СтатьяРасходовТравматизм.ПринятиеКналоговомуУчету
	|		ИНАЧЕ СпособОтраженияЗарплата.СтатьяРасходовВзносов.ПринятиеКналоговомуУчету
	|	КОНЕЦ                                    КАК ПринятиеКналоговомуУчетуТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.АналитикаРасходовТравматизм
	|		ИНАЧЕ СпособОтраженияЗарплата.АналитикаРасходовВзносов
	|	КОНЕЦ                                    КАК АналитикаРасходовТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.АналитикаАктивовПассивовТравматизм
	|		ИНАЧЕ СпособОтраженияЗарплата.АналитикаАктивовПассивовВзносов
	|	КОНЕЦ                                    КАК АналитикаАктивовПассивовТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.НастройкаСчетовУчетаТравматизм
	|		ИНАЧЕ СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносов
	|	КОНЕЦ                                    КАК НастройкаСчетовУчетаТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.НастройкаСчетовУчетаТравматизм.СчетУчета
	|		ИНАЧЕ СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносов.СчетУчета
	|	КОНЕЦ                                    КАК СчетУчетаТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.НастройкаСчетовУчетаТравматизм.Субконто1
	|		ИНАЧЕ СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносов.Субконто1
	|	КОНЕЦ                                    КАК Субконто1Травматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.НастройкаСчетовУчетаТравматизм.Субконто2
	|		ИНАЧЕ СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносов.Субконто2
	|	КОНЕЦ                                    КАК Субконто2Травматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.НастройкаСчетовУчетаТравматизм.Субконто3
	|		ИНАЧЕ СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносов.Субконто3
	|	КОНЕЦ                                    КАК Субконто3Травматизм
	
	|//ПоместитьВоВременнуюТаблицу
	|ИЗ
	|	ВтСписокСпособовОтражения КАК СписокСпособов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВБухУчете КАК СпособОтраженияЗарплата
	|		ПО СписокСпособов.СпособОтражения = СпособОтраженияЗарплата.Ссылка
	|ГДЕ
	|	СписокСпособов.СпособОтражения <> Значение(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокСпособов.СпособОтражения,
	|	ИСТИНА,
	|	СпособОтраженияЗарплата.ОплатаСдельныхРабот,
	|	СпособОтраженияЗарплата.СтатьяРасходовЕНВД,
	|	СпособОтраженияЗарплата.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СпособОтраженияЗарплата.СтатьяРасходовЕНВД.ВариантРаспределенияРасходовРегл,
	|	СпособОтраженияЗарплата.СтатьяРасходовЕНВД.ПринятиеКналоговомуУчету,
	|	СпособОтраженияЗарплата.АналитикаРасходовЕНВД,
	|	СпособОтраженияЗарплата.АналитикаАктивовПассивовЕНВД,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаЕНВД,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаЕНВД.СчетУчета,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаЕНВД.Субконто1,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаЕНВД.Субконто2,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаЕНВД.Субконто3,
	
	|	СпособОтраженияЗарплата.СтатьяРасходовВзносовЕНВД,
	|	СпособОтраженияЗарплата.СтатьяРасходовВзносовЕНВД.ВариантРаспределенияРасходовРегл,
	|	СпособОтраженияЗарплата.СтатьяРасходовВзносовЕНВД.ПринятиеКналоговомуУчету,
	|	СпособОтраженияЗарплата.АналитикаРасходовВзносовЕНВД,
	|	СпособОтраженияЗарплата.АналитикаАктивовПассивовВзносовЕНВД,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносовЕНВД,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносовЕНВД.СчетУчета,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносовЕНВД.Субконто1,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносовЕНВД.Субконто2,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносовЕНВД.Субконто3,
	
	// взносы травматизм
	|	СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.СтатьяРасходовТравматизм
	|		ИНАЧЕ СпособОтраженияЗарплата.СтатьяРасходовВзносовЕНВД
	|	КОНЕЦ                                    КАК СтатьяРасходовТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.СтатьяРасходовТравматизм.ВариантРаспределенияРасходовРегл
	|		ИНАЧЕ СпособОтраженияЗарплата.СтатьяРасходовВзносовЕНВД.ВариантРаспределенияРасходовРегл
	|	КОНЕЦ                                    КАК ВариантРаспределенияТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.СтатьяРасходовТравматизм.ПринятиеКналоговомуУчету
	|		ИНАЧЕ СпособОтраженияЗарплата.СтатьяРасходовВзносовЕНВД.ПринятиеКналоговомуУчету
	|	КОНЕЦ                                    КАК ПринятиеКналоговомуУчетуТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.АналитикаРасходовТравматизм
	|		ИНАЧЕ СпособОтраженияЗарплата.АналитикаРасходовВзносовЕНВД
	|	КОНЕЦ                                    КАК АналитикаРасходовТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.АналитикаАктивовПассивовТравматизм
	|		ИНАЧЕ СпособОтраженияЗарплата.АналитикаАктивовПассивовВзносовЕНВД
	|	КОНЕЦ                                    КАК АналитикаАктивовПассивовТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.НастройкаСчетовУчетаТравматизм
	|		ИНАЧЕ СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносовЕНВД
	|	КОНЕЦ                                    КАК НастройкаСчетовУчетаТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.НастройкаСчетовУчетаТравматизм.СчетУчета
	|		ИНАЧЕ СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносовЕНВД.СчетУчета
	|	КОНЕЦ                                    КАК СчетУчетаТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.НастройкаСчетовУчетаТравматизм.Субконто1
	|		ИНАЧЕ СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносовЕНВД.Субконто1
	|	КОНЕЦ                                    КАК Субконто1Травматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.НастройкаСчетовУчетаТравматизм.Субконто2
	|		ИНАЧЕ СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносовЕНВД.Субконто2
	|	КОНЕЦ                                    КАК Субконто2Травматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.НастройкаСчетовУчетаТравматизм.Субконто3
	|		ИНАЧЕ СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносовЕНВД.Субконто3
	|	КОНЕЦ                                    КАК Субконто3Травматизм
	
	|ИЗ
	|	ВтСписокСпособовОтражения КАК СписокСпособов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВБухУчете КАК СпособОтраженияЗарплата
	|		ПО СписокСпособов.СпособОтражения = СпособОтраженияЗарплата.Ссылка
	|ГДЕ
	|	СписокСпособов.СпособОтражения <> Значение(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка)";
	
	Если ЗначениеЗаполнено(ИмяВременнойТаблицы) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ПоместитьВоВременнуюТаблицу", "ПОМЕСТИТЬ " + ИмяВременнойТаблицы);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ПоместитьВоВременнуюТаблицу", "");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает массив блокируемых реквизитов объекта.
//	Возвращаемое значение:
//		Массив - массив реквизитов.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
	
	Результат.Добавить("НаправлениеДеятельности");
	Результат.Добавить("СтатьяРасходов");
	Результат.Добавить("АналитикаРасходов");
	Результат.Добавить("АналитикаАктивовПассивов");
	Результат.Добавить("РежимУчетаВзносов");
	Результат.Добавить("СтатьяРасходовВзносов");
	Результат.Добавить("АналитикаРасходовВзносов");
	Результат.Добавить("АналитикаАктивовПассивовВзносов");
	Результат.Добавить("ОсобыйСоответствуетОсновному");
	Результат.Добавить("СтатьяРасходовЕНВД");
	Результат.Добавить("АналитикаРасходовЕНВД");
	Результат.Добавить("АналитикаАктивовПассивовЕНВД");
	Результат.Добавить("РежимУчетаВзносовЕНВД");
	Результат.Добавить("СтатьяРасходовВзносовЕНВД");
	Результат.Добавить("АналитикаРасходовВзносовЕНВД");
	Результат.Добавить("АналитикаАктивовПассивовВзносовЕНВД");
	Результат.Добавить("ОтдельнаяСтатьяКалькуляции");
	Результат.Добавить("СтатьяКалькуляции");
	Результат.Добавить("ОтдельнаяСтатьяКалькуляцииВзносов");
	Результат.Добавить("БазаРаспределенияПоСдельнымРаботам");
	Результат.Добавить("ПринятиеКНалоговомуУчетуПоСдельнымРаботам");
	Результат.Добавить("РежимУчетаВзносовНаТравматизм");
	Результат.Добавить("СтатьяРасходовТравматизм");
	Результат.Добавить("АналитикаРасходовТравматизм");
	Результат.Добавить("АналитикаАктивовПассивовТравматизм");
	
	Результат.Добавить("ОплатаСдельныхРабот;ХарактерНачисления");
	
	Возврат Результат;
	
КонецФункции

// Проверяет использование объекта в документах Отражение зарплаты в финансовом учете и Начисление оценочных обязательств.
//	Параметры:
//		Параметры - Структура - структура, содержащая ссылку на проверяемый объект.
//		АдресХранилища - Строка - адрес, по которому следует поместить результат проверки.
//
Процедура ПроверитьИспользованиеОбъекта(Параметры, АдресХранилища) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
	|	Зарплата.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленнаяЗарплатаИВзносы КАК Зарплата
	|ГДЕ
	|	Зарплата.Ссылка.Проведен
	|	И Зарплата.СпособОтраженияЗарплатыВБухучете = &Объект
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
	|	Резервы.Ссылка
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательства КАК Резервы
	|ГДЕ
	|	Резервы.Ссылка.Проведен
	|	И Резервы.СпособОтраженияЗарплатыВБухучете = &Объект");
	
	Запрос.УстановитьПараметр("Объект", Параметры.Объект);
	Результат = Запрос.Выполнить();
	
	ПоместитьВоВременноеХранилище(Не Результат.Пустой(), АдресХранилища);
	
КонецПроцедуры

// Заполняет массивы реквизитов, зависимых от свойств документа
//
// Параметры:
//	ВидНаряда - ПеречислениеСсылка.ВидыБригадныхНарядов - вид бригадного наряда
//	ПоРаспоряжениям - Булево - признак оформления документа по распоряжениям
//	МассивВсехРеквизитов - Массив - реквизиты, которые зависят от свойств наряда
//	МассивРеквизитовОперации - Массив - реквизиты, которые используются при заданных свойствах наряда.
//
Функция НеИспользуемыеРеквизиты(Объект) Экспорт
	
	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("НаправлениеДеятельности");
	
	МассивВсехРеквизитов.Добавить("СтатьяРасходов");
	МассивВсехРеквизитов.Добавить("АналитикаРасходов");
	МассивВсехРеквизитов.Добавить("АналитикаАктивовПассивов");
	МассивВсехРеквизитов.Добавить("НастройкаСчетовУчета");
	
	МассивВсехРеквизитов.Добавить("СтатьяРасходовВзносов");
	МассивВсехРеквизитов.Добавить("АналитикаРасходовВзносов");
	МассивВсехРеквизитов.Добавить("АналитикаАктивовПассивовВзносов");
	МассивВсехРеквизитов.Добавить("НастройкаСчетовУчетаВзносов");
	
	МассивВсехРеквизитов.Добавить("СтатьяРасходовЕНВД");
	МассивВсехРеквизитов.Добавить("АналитикаРасходовЕНВД");
	МассивВсехРеквизитов.Добавить("АналитикаАктивовПассивовЕНВД");
	МассивВсехРеквизитов.Добавить("НастройкаСчетовУчетаЕНВД");
	
	МассивВсехРеквизитов.Добавить("СтатьяРасходовВзносовЕНВД");
	МассивВсехРеквизитов.Добавить("АналитикаРасходовВзносовЕНВД");
	МассивВсехРеквизитов.Добавить("АналитикаАктивовПассивовВзносовЕНВД");
	МассивВсехРеквизитов.Добавить("НастройкаСчетовУчетаВзносовЕНВД");
	
	МассивВсехРеквизитов.Добавить("ОплатаСдельныхРабот");
	МассивВсехРеквизитов.Добавить("ОтдельнаяСтатьяКалькуляции");
	МассивВсехРеквизитов.Добавить("СтатьяКалькуляции");
	МассивВсехРеквизитов.Добавить("ОтдельнаяСтатьяКалькуляцииВзносов");
	МассивВсехРеквизитов.Добавить("СтатьяКалькуляцииВзносов");
	МассивВсехРеквизитов.Добавить("БазаРаспределенияПоСдельнымРаботам");
	
	МассивВсехРеквизитов.Добавить("СтатьяРасходовТравматизм");
	МассивВсехРеквизитов.Добавить("АналитикаРасходовТравматизм");
	МассивВсехРеквизитов.Добавить("АналитикаАктивовПассивовТравматизм");
	МассивВсехРеквизитов.Добавить("НастройкаСчетовУчетаТравматизм");
	
	МассивИспользуемыхРеквизитов = Новый Массив;
	
	Если Объект.ОплатаСдельныхРабот Тогда
		МассивИспользуемыхРеквизитов.Добавить("ОплатаСдельныхРабот");
		МассивИспользуемыхРеквизитов.Добавить("ОтдельнаяСтатьяКалькуляции");
		МассивИспользуемыхРеквизитов.Добавить("ОтдельнаяСтатьяКалькуляцииВзносов");
		МассивИспользуемыхРеквизитов.Добавить("БазаРаспределенияПоСдельнымРаботам");
		
		Если Объект.ОтдельнаяСтатьяКалькуляции Тогда
			МассивИспользуемыхРеквизитов.Добавить("СтатьяКалькуляции");
		КонецЕсли;
		Если Объект.ОтдельнаяСтатьяКалькуляцииВзносов Тогда
			МассивИспользуемыхРеквизитов.Добавить("СтатьяКалькуляцииВзносов");
		КонецЕсли;
		
	Иначе
		
		МассивИспользуемыхРеквизитов.Добавить("НаправлениеДеятельности");
		МассивИспользуемыхРеквизитов.Добавить("СтатьяРасходов");
		
		Если ТипЗнч(Объект.СтатьяРасходов) = Тип("ПланВидовХарактеристикСсылка.СтатьиАктивовПассивов") Тогда
			МассивИспользуемыхРеквизитов.Добавить("АналитикаАктивовПассивов");
			МассивИспользуемыхРеквизитов.Добавить("НастройкаСчетовУчета");
		Иначе
			МассивИспользуемыхРеквизитов.Добавить("АналитикаРасходов");
		КонецЕсли;
		
		Если Объект.РежимУчетаВзносов = 1 Тогда
			
			МассивИспользуемыхРеквизитов.Добавить("СтатьяРасходовВзносов");
			
			Если ТипЗнч(Объект.СтатьяРасходовВзносов) = Тип("ПланВидовХарактеристикСсылка.СтатьиАктивовПассивов") Тогда
				МассивИспользуемыхРеквизитов.Добавить("АналитикаАктивовПассивовВзносов");
				МассивИспользуемыхРеквизитов.Добавить("НастройкаСчетовУчетаВзносов");
			Иначе
				МассивИспользуемыхРеквизитов.Добавить("АналитикаРасходовВзносов");
			КонецЕсли;
		
		КонецЕсли;
		
		Если Не Объект.ОсобыйСоответствуетОсновному Тогда
			
			МассивИспользуемыхРеквизитов.Добавить("СтатьяРасходовЕНВД");
			
			Если ТипЗнч(Объект.СтатьяРасходовЕНВД) = Тип("ПланВидовХарактеристикСсылка.СтатьиАктивовПассивов") Тогда
				МассивИспользуемыхРеквизитов.Добавить("АналитикаАктивовПассивовЕНВД");
				МассивИспользуемыхРеквизитов.Добавить("НастройкаСчетовУчетаЕНВД");
			Иначе
				МассивИспользуемыхРеквизитов.Добавить("АналитикаРасходовЕНВД");
			КонецЕсли;
			
			Если Объект.РежимУчетаВзносовЕНВД = 1 Тогда
				
				МассивИспользуемыхРеквизитов.Добавить("СтатьяРасходовВзносовЕНВД");
				
				Если ТипЗнч(Объект.СтатьяРасходовВзносовЕНВД) = Тип("ПланВидовХарактеристикСсылка.СтатьиАктивовПассивов") Тогда
					МассивИспользуемыхРеквизитов.Добавить("АналитикаАктивовПассивовВзносовЕНВД");
					МассивИспользуемыхРеквизитов.Добавить("НастройкаСчетовУчетаВзносовЕНВД");
				Иначе
					МассивИспользуемыхРеквизитов.Добавить("АналитикаРасходовВзносовЕНВД");
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Объект.РежимУчетаВзносовНаТравматизм = 1 Тогда
		
		МассивИспользуемыхРеквизитов.Добавить("СтатьяРасходовТравматизм");
		
		Если ТипЗнч(Объект.СтатьяРасходовТравматизм) = Тип("ПланВидовХарактеристикСсылка.СтатьиАктивовПассивов") Тогда
			МассивИспользуемыхРеквизитов.Добавить("АналитикаАктивовПассивовТравматизм");
			МассивИспользуемыхРеквизитов.Добавить("НастройкаСчетовУчетаТравматизм");
		Иначе
			МассивИспользуемыхРеквизитов.Добавить("АналитикаРасходовТравматизм");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОбщегоНазначенияКлиентСервер.СократитьМассив(МассивВсехРеквизитов, МассивИспользуемыхРеквизитов);
	
КонецФункции

#Область Прочее

// Возвращает параметры выбора статей для элемента справочника.
// 
Функция ПараметрыВыбораСтатейИАналитик() Экспорт
	
	МассивПараметров = Новый Массив;
	
	#Область Параметры_выбора_статьи_расходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья      = "СтатьяРасходов";
	ПараметрыВыбора.ТипСтатьи   = "ТипСтатьиРасходов";
	ПараметрыВыбора.ЗначениеПоУмолчанию = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("АналитикаАктивовПассивов");
	
	МассивПараметров.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	#Область Параметры_выбора_статьи_расходов_ЕНВД
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья      = "СтатьяРасходовЕНВД";
	ПараметрыВыбора.ТипСтатьи   = "ТипСтатьиРасходовЕНВД";
	ПараметрыВыбора.ЗначениеПоУмолчанию = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовЕНВД";
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивовЕНВД";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходовЕНВД");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовЕНВД");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("АналитикаАктивовПассивовЕНВД");
	
	МассивПараметров.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	#Область Параметры_выбора_статьи_расходов_взносов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья      = "СтатьяРасходовВзносов";
	ПараметрыВыбора.ТипСтатьи   = "ТипСтатьиРасходовВзносов";
	ПараметрыВыбора.ЗначениеПоУмолчанию = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовВзносов";
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивовВзносов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходовВзносов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовВзносов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("АналитикаАктивовПассивовВзносов");
	
	МассивПараметров.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	#Область Параметры_выбора_статьи_расходов_взносов_ЕНВД
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья      = "СтатьяРасходовВзносовЕНВД";
	ПараметрыВыбора.ТипСтатьи   = "ТипСтатьиРасходовВзносовЕНВД";
	ПараметрыВыбора.ЗначениеПоУмолчанию = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовВзносовЕНВД";
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивовВзносовЕНВД";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходовВзносовЕНВД");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовВзносовЕНВД");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("АналитикаАктивовПассивовВзносовЕНВД");
	
	МассивПараметров.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	#Область Параметры_выбора_статьи_расходов_травматизм
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья      = "СтатьяРасходовТравматизм";
	ПараметрыВыбора.ТипСтатьи   = "ТипСтатьиРасходовТравматизм";
	ПараметрыВыбора.ЗначениеПоУмолчанию = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовТравматизм";
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивовТравматизм";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходовТравматизм");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовТравматизм");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("АналитикаАктивовПассивовТравматизм");
	
	МассивПараметров.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	Возврат МассивПараметров;
	
КонецФункции

// Возвращает параметры настройки счетов учета.
// 
// Возвращаемое значение:
// 	МассивПараметров -  Массив параметров настройки счетов учета (См. НастройкаСчетовУчетаСервер.ПараметрыНастройки).
//
Функция ПараметрыНастройкиСчетовУчета() Экспорт
	
	МассивПараметров = Новый Массив;
	
	#Область НастройкаСчетаУчета
	ПараметрыНастройки = НастройкаСчетовУчетаСервер.ПараметрыНастройки();
	ПараметрыНастройки.ПутьКДанным = "Объект";
	ПараметрыНастройки.НастройкаСчетовУчета     = "НастройкаСчетовУчета";
	ПараметрыНастройки.СтатьяАктивовПассивов    = "СтатьяРасходов";
	ПараметрыНастройки.АналитикаАктивовПассивов = "АналитикаАктивовПассивов";
	ПараметрыНастройки.ТипСтатьи     = "ТипСтатьиРасходов";
	ПараметрыНастройки.Представление = "НастроитьОтражениеВРеглУчете";
	ПараметрыНастройки.Организация   = "";
	
	ЗаполнитьИсточникиПодбораСубконто(ПараметрыНастройки.ИсточникиПодбораСубконто);
	
	ПараметрыНастройки.ЭлементыФормы.Добавить("НастроитьОтражениеВРеглУчете");
	
	МассивПараметров.Добавить(ПараметрыНастройки);
	#КонецОбласти
	
	#Область НастройкаСчетаУчетаЕНВД
	ПараметрыНастройки = НастройкаСчетовУчетаСервер.ПараметрыНастройки();
	ПараметрыНастройки.ПутьКДанным = "Объект";
	ПараметрыНастройки.НастройкаСчетовУчета     = "НастройкаСчетовУчетаЕНВД";
	ПараметрыНастройки.СтатьяАктивовПассивов    = "СтатьяРасходовЕНВД";
	ПараметрыНастройки.АналитикаАктивовПассивов = "АналитикаАктивовПассивовЕНВД";
	ПараметрыНастройки.ТипСтатьи     = "ТипСтатьиРасходовЕНВД";
	ПараметрыНастройки.Представление = "НастроитьОтражениеВРеглУчетеЕНВД";
	ПараметрыНастройки.Организация = "";

	ЗаполнитьИсточникиПодбораСубконто(ПараметрыНастройки.ИсточникиПодбораСубконто);
	
	ПараметрыНастройки.ЭлементыФормы.Добавить("НастроитьОтражениеВРеглУчетеЕНВД");
	
	МассивПараметров.Добавить(ПараметрыНастройки);
	#КонецОбласти
	
	#Область НастройкаСчетаУчетаВзносов
	ПараметрыНастройки = НастройкаСчетовУчетаСервер.ПараметрыНастройки();
	ПараметрыНастройки.ПутьКДанным = "Объект";
	ПараметрыНастройки.НастройкаСчетовУчета     = "НастройкаСчетовУчетаВзносов";
	ПараметрыНастройки.СтатьяАктивовПассивов    = "СтатьяРасходовВзносов";
	ПараметрыНастройки.АналитикаАктивовПассивов = "АналитикаАктивовПассивовВзносов";
	ПараметрыНастройки.ТипСтатьи     = "ТипСтатьиРасходовВзносов";
	ПараметрыНастройки.Представление = "НастроитьОтражениеВРеглУчетеВзносов";
	ПараметрыНастройки.Организация = "";
	
	ЗаполнитьИсточникиПодбораСубконто(ПараметрыНастройки.ИсточникиПодбораСубконто);
	
	ПараметрыНастройки.ЭлементыФормы.Добавить("НастроитьОтражениеВРеглУчетеВзносов");
	
	МассивПараметров.Добавить(ПараметрыНастройки);
	#КонецОбласти
	
	#Область НастройкаСчетаУчетаВзносовЕНВД
	ПараметрыНастройки = НастройкаСчетовУчетаСервер.ПараметрыНастройки();
	ПараметрыНастройки.ПутьКДанным = "Объект";
	ПараметрыНастройки.НастройкаСчетовУчета     = "НастройкаСчетовУчетаВзносовЕНВД";
	ПараметрыНастройки.СтатьяАктивовПассивов    = "СтатьяРасходовВзносовЕНВД";
	ПараметрыНастройки.АналитикаАктивовПассивов = "АналитикаАктивовПассивовВзносовЕНВД";
	ПараметрыНастройки.ТипСтатьи     = "ТипСтатьиРасходовВзносовЕНВД";
	ПараметрыНастройки.Представление = "НастроитьОтражениеВРеглУчетеВзносовЕНВД";
	ПараметрыНастройки.Организация = "";
	
	ЗаполнитьИсточникиПодбораСубконто(ПараметрыНастройки.ИсточникиПодбораСубконто);
	
	ПараметрыНастройки.ЭлементыФормы.Добавить("НастроитьОтражениеВРеглУчетеВзносовЕНВД");
	
	МассивПараметров.Добавить(ПараметрыНастройки);
	#КонецОбласти
	
	#Область НастройкаСчетаУчетаВзносов
	ПараметрыНастройки = НастройкаСчетовУчетаСервер.ПараметрыНастройки();
	ПараметрыНастройки.ПутьКДанным = "Объект";
	ПараметрыНастройки.НастройкаСчетовУчета     = "НастройкаСчетовУчетаТравматизм";
	ПараметрыНастройки.СтатьяАктивовПассивов    = "СтатьяРасходовТравматизм";
	ПараметрыНастройки.АналитикаАктивовПассивов = "АналитикаАктивовПассивовТравматизм";
	ПараметрыНастройки.ТипСтатьи     = "ТипСтатьиРасходовТравматизм";
	ПараметрыНастройки.Представление = "НастроитьОтражениеВРеглУчетеТравматизм";
	ПараметрыНастройки.Организация = "";
	
	ЗаполнитьИсточникиПодбораСубконто(ПараметрыНастройки.ИсточникиПодбораСубконто);
	
	ПараметрыНастройки.ЭлементыФормы.Добавить("НастроитьОтражениеВРеглУчетеТравматизм");
	
	МассивПараметров.Добавить(ПараметрыНастройки);
	#КонецОбласти
	
	Возврат МассивПараметров;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьИсточникиПодбораСубконто(Список)
	Список.ЗагрузитьЗначения(ПодбираемыеАналитики());
КонецПроцедуры

Функция ПодбираемыеАналитики() Экспорт
	
	ПодбираемыеАналитики = Новый Массив;
	ПодбираемыеАналитики.Добавить(Тип("СправочникСсылка.ФизическиеЛица"));
	ПодбираемыеАналитики.Добавить(Тип("СправочникСсылка.ОбъектыЭксплуатации"));
	ПодбираемыеАналитики.Добавить(Тип("СправочникСсылка.Организации"));
	ПодбираемыеАналитики.Добавить(Тип("СправочникСсылка.СтруктураПредприятия"));
	ПодбираемыеАналитики.Добавить(Тип("ПеречислениеСсылка.ВидыОперацийПоЗарплате"));
	
	Возврат ПодбираемыеАналитики;
	
КонецФункции

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Справочники.СпособыОтраженияЗарплатыВБухУчете.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.6.25";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("b0c78fac-4e70-43bf-85da-00dcc4dc547b");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Справочники.СпособыОтраженияЗарплатыВБухУчете.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.ЗапускатьТолькоВГлавномУзле = Истина;
	Обработчик.Комментарий = НСтр("ru = 'Заполняет реквизиты:
	|- НастройкаСчетовУчетаТравматизм
	|- НастройкаСчетовУчетаВзносовЕНВД
	|- НастройкаСчетовУчетаВзносов
	|- НастройкаСчетовУчетаЕНВД
	|- НастройкаСчетовУчета';
	|en = 'Fills in attributes:
	|- НастройкаСчетовУчетаТравматизм
	|- НастройкаСчетовУчетаВзносовЕНВД
	|- НастройкаСчетовУчетаВзносов
	|- НастройкаСчетовУчетаЕНВД
	|- НастройкаСчетовУчета'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Справочники.СпособыОтраженияЗарплатыВБухУчете.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Справочники.СпособыОтраженияЗарплатыВБухУчете.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Справочники.СпособыОтраженияЗарплатыВБухУчете.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = ПустаяСсылка().Метаданные().ПолноеИмя();
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Дата УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Ссылка");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СпособыОтраженияЗарплатыВБухУчете.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СпособыОтраженияЗарплатыВБухУчете КАК СпособыОтраженияЗарплатыВБухУчете
	|ГДЕ
	|	СпособыОтраженияЗарплатыВБухУчете.УдалитьСчетУчета <> &СчетУчетаПустаяСсылка И
	|		СпособыОтраженияЗарплатыВБухУчете.НастройкаСчетовУчета = ЗНАЧЕНИЕ(Справочник.НастройкиСчетовУчетаПрочихОпераций.ПустаяСсылка)
	|	ИЛИ СпособыОтраженияЗарплатыВБухУчете.УдалитьСчетУчетаЕНВД <> &СчетУчетаПустаяСсылка И
	|		СпособыОтраженияЗарплатыВБухУчете.НастройкаСчетовУчетаЕНВД = ЗНАЧЕНИЕ(Справочник.НастройкиСчетовУчетаПрочихОпераций.ПустаяСсылка)
	|	ИЛИ СпособыОтраженияЗарплатыВБухУчете.УдалитьСчетУчетаВзносов <> &СчетУчетаПустаяСсылка И
	|		СпособыОтраженияЗарплатыВБухУчете.НастройкаСчетовУчетаВзносов = ЗНАЧЕНИЕ(Справочник.НастройкиСчетовУчетаПрочихОпераций.ПустаяСсылка)
	|	ИЛИ СпособыОтраженияЗарплатыВБухУчете.УдалитьСчетУчетаВзносовЕНВД <> &СчетУчетаПустаяСсылка И
	|		СпособыОтраженияЗарплатыВБухУчете.НастройкаСчетовУчетаВзносовЕНВД = ЗНАЧЕНИЕ(Справочник.НастройкиСчетовУчетаПрочихОпераций.ПустаяСсылка)
	|	ИЛИ СпособыОтраженияЗарплатыВБухУчете.УдалитьСчетУчетаТравматизм <> &СчетУчетаПустаяСсылка И
	|		СпособыОтраженияЗарплатыВБухУчете.НастройкаСчетовУчетаТравматизм = ЗНАЧЕНИЕ(Справочник.НастройкиСчетовУчетаПрочихОпераций.ПустаяСсылка)
	|";
	
	Запрос.УстановитьПараметр("СчетУчетаПустаяСсылка", РеглУчетКлиентСервер.СсылкаПланСчетов());
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = ПустаяСсылка().Метаданные().ПолноеИмя();
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Для Каждого Справочник Из ОбновляемыеДанные Цикл
		
		НачатьТранзакцию();
		
		Попытка
			Ссылка = Справочник.Ссылка;
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Ссылка);
			Блокировка.Заблокировать();
			
			СправочникОбъект = Ссылка.ПолучитьОбъект();
			
			Если СправочникОбъект <> Неопределено Тогда
				НастройкаСчетовУчетаСервер.ЗаполнитьНастройкиСчетовУчета(СправочникОбъект);
				НастройкаСчетовУчетаСервер.ЗаполнитьНастройкиСчетовУчета(СправочникОбъект, "ЕНВД");
				НастройкаСчетовУчетаСервер.ЗаполнитьНастройкиСчетовУчета(СправочникОбъект, "Взносов");
				НастройкаСчетовУчетаСервер.ЗаполнитьНастройкиСчетовУчета(СправочникОбъект, "ВзносовЕНВД");
				НастройкаСчетовУчетаСервер.ЗаполнитьНастройкиСчетовУчета(СправочникОбъект, "Травматизм");
			КонецЕсли;
			
			Если СправочникОбъект <> Неопределено И СправочникОбъект.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(СправочникОбъект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Ссылка);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Справочник.Ссылка);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
