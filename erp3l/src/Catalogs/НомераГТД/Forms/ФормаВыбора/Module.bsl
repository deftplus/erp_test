
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Возврат при получении формы для анализа.
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("СтранаПроисхождения", СтранаПроисхождения) Тогда
		Если Не ЗначениеЗаполнено(СтранаПроисхождения) Тогда
			Параметры.Отбор.Удалить("СтранаПроисхождения");
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
																				"СтранаПроисхождения",
																				СтранаПроисхождения,
																				ВидСравненияКомпоновкиДанных.Равно,
																				,
																				ЗначениеЗаполнено(СтранаПроисхождения));
	КонецЕсли;
	
	Параметры.Свойство("Номенклатура",     Номенклатура);
	Параметры.Свойство("ДокументПередачи", ДокументПродажиПередачи);
	
	ОтборВключен                                     = ЗначениеЗаполнено(ДокументПродажиПередачи);
	Элементы.ГруппаДокументПродажиПередачи.Видимость = ОтборВключен;
	Элементы.ОтборВключен.Видимость                  = ОтборВключен;
	
	Если ОтборВключен Тогда
		УстановитьОтборПоДокументуПродажиПередачи();
	КонецЕсли;
	
	Элементы.РНПТ.Видимость = УчетПрослеживаемыхТоваровЛокализация.ИспользоватьУчетПрослеживаемыхИмпортныхТоваров(Дата(1, 1, 1));
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборВключенПриИзменении(Элемент)
	
	УстановитьОтборПоДокументуПродажиПередачи();
	
КонецПроцедуры

&НаКлиенте
Процедура СтранаПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
																			"СтранаПроисхождения",
																			СтранаПроисхождения,
																			ВидСравненияКомпоновкиДанных.Равно,
																			,
																			ЗначениеЗаполнено(СтранаПроисхождения));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	
	Если ОтборВключен Тогда
		ОтборВключен = Ложь;
		УстановитьОтборПоДокументуПродажиПередачи();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборПоДокументуПродажиПередачи()
	
	Если ОтборВключен Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВложенныйЗапрос.Ссылка КАК Ссылка
		|ИЗ
		|	(ВЫБРАТЬ
		|		РеализацияТоваровУслугВидыЗапасов.НомерГТД КАК Ссылка
		|	ИЗ
		|		Документ.РеализацияТоваровУслуг.ВидыЗапасов КАК РеализацияТоваровУслугВидыЗапасов
		|	ГДЕ
		|		РеализацияТоваровУслугВидыЗапасов.Ссылка = &ДокументПродажиПередачи
		|		И РеализацияТоваровУслугВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура = &Номенклатура
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ОтчетОРозничныхПродажахВидыЗапасов.НомерГТД
		|	ИЗ
		|		Документ.ОтчетОРозничныхПродажах.ВидыЗапасов КАК ОтчетОРозничныхПродажахВидыЗапасов
		|	ГДЕ
		|		ОтчетОРозничныхПродажахВидыЗапасов.Ссылка = &ДокументПродажиПередачи
		|		И ОтчетОРозничныхПродажахВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура = &Номенклатура
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ПередачаТоваровМеждуОрганизациямиВидыЗапасов.НомерГТД
		|	ИЗ
		|		Документ.ПередачаТоваровМеждуОрганизациями.ВидыЗапасов КАК ПередачаТоваровМеждуОрганизациямиВидыЗапасов
		|	ГДЕ
		|		ПередачаТоваровМеждуОрганизациямиВидыЗапасов.Ссылка = &ДокументПродажиПередачи
		|		И ПередачаТоваровМеждуОрганизациямиВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура = &Номенклатура) КАК ВложенныйЗапрос";
		
		Запрос.УстановитьПараметр("Номенклатура",            Номенклатура);
		Запрос.УстановитьПараметр("ДокументПродажиПередачи", ДокументПродажиПередачи);
		
		РезультатЗапроса  = Запрос.Выполнить();
		ВыборкаНомеровГТД = РезультатЗапроса.Выбрать();
		СписокНомеровГТД  = Новый СписокЗначений;
		
		Пока ВыборкаНомеровГТД.Следующий() Цикл
			СписокНомеровГТД.Добавить(ВыборкаНомеровГТД.Ссылка);
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Ссылка", СписокНомеровГТД,
			ВидСравненияКомпоновкиДанных.ВСписке, , Истина);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Ссылка", , , , Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
