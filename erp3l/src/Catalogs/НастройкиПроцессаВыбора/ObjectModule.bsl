#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка ИЛИ Предопределенный Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаКритерия Из КритерииОценки Цикл
		Если СтрокаКритерия.МаксимальноеЗначение <> 0 Тогда
			Длинна_ = СтрокаКритерия.МаксимальноеЗначение - СтрокаКритерия.МинимальноеЗначение;
			СтрокаКритерия.К1 = ?(Длинна_=0, 1, Длинна_);
			СтрокаКритерия.К0 = - СтрокаКритерия.МинимальноеЗначение / ?(Длинна_=0, 1, Длинна_);
		Иначе
			СтрокаКритерия.К1 = 1;
			СтрокаКритерия.К0 = - СтрокаКритерия.МинимальноеЗначение;
		КонецЕсли;
		
	КонецЦикла;
	
	ИндексСтрокиПоследнегоЭтапа = ЭтапыВыбора.Количество()-1;
	
	Если ИндексСтрокиПоследнегоЭтапа >= 0 Тогда
		Если ЭтапыВыбора[ИндексСтрокиПоследнегоЭтапа].Этап = Справочники.ЭтапыОценки.ПоследующаяОценка Тогда
			ИндексСтрокиПоследнегоЭтапа = ИндексСтрокиПоследнегоЭтапа - 1;
			Если ИндексСтрокиПоследнегоЭтапа >= 0 Тогда
				ПоследнийЭтап = ЭтапыВыбора[ИндексСтрокиПоследнегоЭтапа].Этап;
			КонецЕсли;
		Иначе
			ПоследнийЭтап = ЭтапыВыбора[ИндексСтрокиПоследнегоЭтапа].Этап;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка ИЛИ Предопределенный Тогда
		Возврат;
	КонецЕсли;
	
	ВыборОбъектовУХ.ОбновитьРегистрациюОбъектовВыбора(Ссылка);
	ВыборОбъектовУХ.ОбновитьЭкспертовЭтапов(Ссылка);
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	// Проверим, что суммарный вес равен 1.
	Выгрузка = КритерииОценки.Выгрузить();
	Выгрузка.Свернуть("ЭтапВыбора", "Вес");
	Для Каждого ТекВыгрузка Из Выгрузка Цикл
		Если ТекВыгрузка.Вес <> 1 Тогда
			Отказ = Истина;
			ТекстСообщения = НСтр("ru = 'Суммарный вес критериев в этапе ""%Этап%"" составляет %Вес%. Для корректной работы требуется 1. Запись отменена.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Этап%", Строка(ТекВыгрузка.ЭтапВыбора));
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Вес%", Строка(ТекВыгрузка.Вес));
			ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
		Иначе
			// Проверка пройдена успешно.
		КонецЕсли;
	КонецЦикла;	
	
	//Проверка на корректность оценок
	Критерии=КритерииОценки.Выгрузить();
	Для Каждого СтрокаКритерии из Критерии Цикл
		Если Не СтрокаКритерии.НеИспользоватьПреобразованиеВБаллы	
			и (СтрокаКритерии.До0<>СтрокаКритерии.От1 или  СтрокаКритерии.До1<>СтрокаКритерии.От2 
			или СтрокаКритерии.До3<>СтрокаКритерии.От4 или СтрокаКритерии.До4<>СтрокаКритерии.От5) тогда
			Отказ = Истина;
			ТекстСообщения = НСтр("ru = 'По критерию ""%КритерийОценки%"" не совпадает значение конечного балла с начальным.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%КритерийОценки%", Строка(СтрокаКритерии.КритерийОценки));
			ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
			
		КонецЕсли;	
	КонецЦикла;	
	// Проверим, что существует хотя бы один эксперт с правами завершения этапа.
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("ЗакрываетЭтапОценки", Истина);
	НайденныеСтроки = ОценочнаяКомиссия.НайтиСтроки(СтруктураПоиска);
	Если НайденныеСтроки.Количество() < 1 Тогда
		Отказ = Истина;
		ТекстСообщения = НСтр("ru = 'В оценочной комиссии не назначен ни один эксперт, уполномоченный закрывать этап оценки. Запись отменена.'");
		ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
	Иначе
		// Проверка пройдена успешно.
	КонецЕсли;
КонецПроцедуры

#КонецЕсли
