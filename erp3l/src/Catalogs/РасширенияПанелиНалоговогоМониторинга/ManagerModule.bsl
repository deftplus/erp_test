
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ЗаполнитьПоставляемыеДанные() Экспорт
	Макет = Справочники.РасширенияПанелиНалоговогоМониторинга.ПолучитьМакет("ПоставляемыеДанные");
	Инд = 1;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасширенияПанелиНалоговогоМониторинга.Ссылка КАК СсылкаНаОбъект,
		|	РасширенияПанелиНалоговогоМониторинга.АлгоритмОпределенияСтатуса КАК АлгоритмОпределенияСтатуса,
		|	РасширенияПанелиНалоговогоМониторинга.АлгоритмОпределенияСтатусаНаКлиенте КАК АлгоритмОпределенияСтатусаНаКлиенте,
		|	РасширенияПанелиНалоговогоМониторинга.АлгоритмРасшифровки КАК АлгоритмРасшифровки,
		|	РасширенияПанелиНалоговогоМониторинга.ИмяПредопределенныхДанных КАК ИмяПредопределенного
		|ИЗ
		|	Справочник.РасширенияПанелиНалоговогоМониторинга КАК РасширенияПанелиНалоговогоМониторинга
		|ГДЕ
		|	РасширенияПанелиНалоговогоМониторинга.Предопределенный";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаСправочника = РезультатЗапроса.Выгрузить();

	Пока Истина Цикл 
		Инд = Инд + 1;
		ИмяПредопределенного = Макет.Область(Инд, 1, Инд, 1).Текст;
		Если ПустаяСтрока(ИмяПредопределенного) Тогда
			Прервать;
		КонецЕсли;
		СтрокаТаблицы = ТаблицаСправочника.Найти(ИмяПредопределенного, "ИмяПредопределенного");
		Если СтрокаТаблицы = Неопределено 
				ИЛИ СтрокаТаблицы.АлгоритмОпределенияСтатуса <> "" 
				ИЛИ СтрокаТаблицы.АлгоритмОпределенияСтатусаНаКлиенте <> ""
				ИЛИ СтрокаТаблицы.АлгоритмРасшифровки <> "" Тогда
			Продолжить;
		КонецЕсли;
		ОбъектЗапись = СтрокаТаблицы.СсылкаНаОбъект.ПолучитьОбъект();
		ОбъектЗапись.АлгоритмОпределенияСтатуса = Макет.Область(Инд, 2, Инд, 2).Текст;
		ОбъектЗапись.АлгоритмОпределенияСтатусаНаКлиенте = Макет.Область(Инд, 3, Инд, 3).Текст;
		ОбъектЗапись.АлгоритмРасшифровки = Макет.Область(Инд, 4, Инд, 4).Текст;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ОбъектЗапись);
	КонецЦикла;
КонецПроцедуры

#КонецЕсли