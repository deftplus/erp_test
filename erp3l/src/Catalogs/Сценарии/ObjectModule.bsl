
#Область ОбработчикиСобытийОбъекта

Процедура ПередЗаписью(Отказ)
	
	Если Не ЗначениеЗаполнено(РежимПланирования) Тогда
		 РежимПланирования = Перечисления.РежимыПланирования.ПланированиеНаИнтервал;
	КонецЕсли;	
	
	ПроверитьНастраиваемыйОтчетПоРегламенту(Отказ);
	ПроверитьРегламенты(Отказ);
		
КонецПроцедуры

#КонецОбласти

#Область ПрочиеПроцедурыИФункции

Процедура ПроверитьНастраиваемыйОтчетПоРегламенту(Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если РежимПланирования = Перечисления.РежимыПланирования.СкользящееПланирование И НЕ Ссылка.Пустая() тогда
		
		ЗапросПоНастраиваемомуОтчету = Новый Запрос("ВЫБРАТЬ
		|	НастраиваемыйОтчет.Ссылка КАК НастраиваемыйОтчет
		|ИЗ
		|	Документ.НастраиваемыйОтчет КАК НастраиваемыйОтчет
		|ГДЕ
		|	НастраиваемыйОтчет.Сценарий = &Сценарий");
		
		ЗапросПоНастраиваемомуОтчету.УстановитьПараметр("Сценарий", Ссылка);
		
		Выборка = ЗапросПоНастраиваемомуОтчету.Выполнить().Выбрать();
		
		КоличествоОшибок = Выборка.Количество();
				
		Если КоличествоОшибок = 1 тогда
			
			Если Выборка.Следующий() тогда
				
				СтрокаШаблона = Нстр("ru = 'Сценарий ""%1"" в Режиме планирования ""%2"" не должен использоваться в документах ""Экземпляр отчета""; ошибка: %3;'");
				
				Если Не ПустаяСтрока(СтрокаШаблона) тогда				
					ОбщегоНазначенияУХ.СообщитьОбОшибке(СтрШаблон(СтрокаШаблона, Ссылка, Перечисления.РежимыПланирования.СкользящееПланирование, Выборка.НастраиваемыйОтчет), Отказ,, СтатусСообщения.Важное,, Выборка.НастраиваемыйОтчет);
				КонецЕсли;
				
				Отказ = Истина;
				
			КонецЕсли;
			
		ИначеЕсли КоличествоОшибок > 1 тогда
						
			СтрокаШаблона = Нстр("ru = 'Сценарий ""%1"" в Режиме планирования ""%2"" не должен использоваться в документах ""Экземпляр отчета""; ошибка:'");
			
			Если Не ПустаяСтрока(СтрокаШаблона) тогда				
				ОбщегоНазначенияУХ.СообщитьОбОшибке(СтрШаблон(СтрокаШаблона, Ссылка, Перечисления.РежимыПланирования.СкользящееПланирование), Отказ,, СтатусСообщения.Важное,, Выборка.НастраиваемыйОтчет);
			КонецЕсли;
			
			Пока Выборка.Следующий() Цикл
				ОбщегоНазначенияУХ.СообщитьОбОшибке(Выборка.НастраиваемыйОтчет, Отказ,, СтатусСообщения.Важное,, Выборка.НастраиваемыйОтчет);
			КонецЦикла;
			
			Отказ = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ПроверитьРегламенты(Отказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если РежимПланирования = Перечисления.РежимыПланирования.СкользящееПланирование тогда
		
		Если ПериодыРегламентов.Количество() > 1 тогда
			
			СтрокаШаблона = Нстр("ru = 'В режиме ""%1"" может быть только 1 регламент'");
			
			Если Не ПустаяСтрока(СтрокаШаблона) тогда				
				ОбщегоНазначенияУХ.СообщитьОбОшибке(СтрШаблон(СтрокаШаблона, Перечисления.РежимыПланирования.СкользящееПланирование), Отказ);
			КонецЕсли;
			
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
		
КонецПроцедуры

#КонецОбласти


