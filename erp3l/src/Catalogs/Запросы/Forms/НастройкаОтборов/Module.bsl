
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастройкиОтборов_Параметр = ПолучитьИзВременногоХранилища(Параметры.ПутьКНастройкеОтборов);
	Попытка
		Для Каждого Элемент Из НастройкиОтборов_Параметр Цикл
			ЗаполнитьЗначенияСвойств(НастройкиОтборов.Добавить(), Элемент);
		КонецЦикла;
		
	ИСключение
		Отказ = Истина;
	КонецПопытки;
	
	// Сохраняем виды отбора для списков для дальнейшей работы на клиенте.
	ТипыОтбораСписков.Добавить(ВидСравненияКомпоновкиДанных.ВСписке);
	ТипыОтбораСписков.Добавить(ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии);
	ТипыОтбораСписков.Добавить(ВидСравненияКомпоновкиДанных.НеВСписке);
	ТипыОтбораСписков.Добавить(ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиОтборовПравоеЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущаяСтрока = НастройкиОтборов.НайтиПоИдентификатору(Элементы.НастройкиОтборов.ТекущаяСтрока);
	
	// Отбор по единичному значению.
	Если ТипыОтбораСписков.НайтиПоЗначению(ТекущаяСтрока.ВидСравнения) = Неопределено Тогда
		Если ЗначениеЗаполнено(ТекущаяСтрока.Владелец) Тогда
			ОткрытьФорму(ТекущаяСтрока.ИмяОбъектаМетаданных + ".ФормаВыбора", Новый Структура("Отбор", Новый Структура("Владелец", ТекущаяСтрока.Владелец)), Элемент);
		Иначе
			ОткрытьФорму(ТекущаяСтрока.ИмяОбъектаМетаданных + ".ФормаВыбора", , Элемент);
		КонецЕсли;
	// Отбор по списку.
	Иначе
		ОткрытьФорму("ОбщаяФорма.ФормаРедактированияСпискаЗначений_Управляемая", Новый Структура("ИмяСправочника, Владелец, ТекСписокЗначений", ТекущаяСтрока.ИмяОбъектаМетаданных, ТекущаяСтрока.Владелец, ТекущаяСтрока.ПравоеЗначение), Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиОтборовПравоеЗначениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("ДанныеФормыКоллекция") Тогда
		
		СтандартнаяОбработка = Ложь;
		ТекСтрока = НастройкиОтборов.НайтиПоИдентификатору(Элементы.НастройкиОтборов.ТекущаяСтрока);
		ТекСтрока.ПравоеЗначение = Новый СписокЗначений;

		Для Каждого Элемент Из ВыбранноеЗначение Цикл
			ТекСтрока.ПравоеЗначение.Добавить(Элемент.Значение);
		КонецЦикла;
	Иначе
		СтандартнаяОбработка = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИзменения(Команда)
	
	ОповеститьОВыборе(НастройкиОтборов);
	
КонецПроцедуры
