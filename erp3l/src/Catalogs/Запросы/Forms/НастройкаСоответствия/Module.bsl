
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТаблицаАналитик = ПолучитьИзВременногоХранилища(Параметры.ПутьКНастройкеСоответствия);
		
	Для Каждого Элемент Из ТаблицаАналитик Цикл
		НоваяСтрока = НастройкиСоответствия.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Элемент);
	КонецЦикла;
	
	ОбщийСписокАналитикПоказателя = Параметры.ОбщийСписокАналитикПоказателя;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиСоответствияАналитикаПоказателяНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекСтрока    = НастройкиСоответствия.НайтиПоИдентификатору(Элементы.НастройкиСоответствия.ТекущаяСтрока);
	СписокВыбора = ТекСтрока.СписокДоступныхАналитик;
	ВыбранноеЗначение = Неопределено;
	
	ДопПараметры = Новый Структура("ТекСтрока", ТекСтрока);
	Оповещение = Новый ОписаниеОповещения("Подключаемый_ОбработатьВыборИзСпискаАналитики", ЭтотОбъект, ДопПараметры);

	ПоказатьВыборИзСписка(Оповещение, СписокВыбора);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьВыборИзСпискаАналитики(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
    
    ТекСтрока = ДополнительныеПараметры.ТекСтрока;
        
    ВыбранноеЗначение = ВыбранныйЭлемент;
    
    Если ВыбранноеЗначение = Неопределено Тогда
        Возврат;
    КонецЕсли;
    
    ТекСтрока.НомерАналитикиПоказателя = ВыбранноеЗначение.Значение;
    ТекСтрока.АналитикаПОказателя      = ВыбранноеЗначение.Представление;

КонецПроцедуры

&НаКлиенте
Процедура НастройкиСоответствияАналитикаПоказателяОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИзменения(Команда)
	
	СоответствиеОтвет = Новый Соответствие;
	Для Каждого Элемент Из НастройкиСоответствия Цикл
		СоответствиеОтвет.Вставить(Элемент.НомерАналитикиИсточника, Элемент.НомерАналитикиПоказателя);
	КонецЦикла;
	
	ОповеститьОВыборе(СоответствиеОтвет);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиСоответствияАналитикаПоказателяОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущаяСтрока = НастройкиСоответствия.НайтиПоИдентификатору(Элементы.НастройкиСоответствия.ТекущаяСтрока);
	ТекущаяСтрока.НомерАналитикиПоказателя = 0;
	ТЕкущаяСТрока.АналитикаПоказателя      = "";
	
КонецПроцедуры
