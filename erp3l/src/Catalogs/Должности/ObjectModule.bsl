#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	ЗначениеЯвляетсяДолжностьюЛетногоЭкипажа = 
		ПолучитьФункциональнуюОпцию("ИспользуетсяТрудЧленовЛетныхЭкипажей") И ЯвляетсяДолжностьюЛетногоЭкипажа;
		
	ЗначениеЯвляетсяШахтерскойДолжностью =
		ПолучитьФункциональнуюОпцию("ИспользуетсяТрудШахтеров") И ЯвляетсяШахтерскойДолжностью;
		
	ЗначениеЯвляетсяФармацевтическойДолжностью = 
		ПолучитьФункциональнуюОпцию("ИспользуетсяТрудФармацевтов") И ЯвляетсяФармацевтическойДолжностью;
		
	ТекстСообщения = "";
	
	Если ЗначениеЯвляетсяДолжностьюЛетногоЭкипажа И ЗначениеЯвляетсяШахтерскойДолжностью Тогда
		ТекстСообщения = НСтр("ru = 'члена летного экипажа и шахтера';
								|en = 'flight crew member and miner'");
	КонецЕсли;
	
	Если ЗначениеЯвляетсяДолжностьюЛетногоЭкипажа И ЗначениеЯвляетсяФармацевтическойДолжностью Тогда
		Если ПустаяСтрока(ТекстСообщения) Тогда
			ТекстСообщения = НСтр("ru = 'члена летного экипажа и фармацевта';
									|en = 'flight crew member and pharmaceutist'");
		Иначе
			ТекстСообщения = СтрЗаменить(ТекстСообщения, " " + НСтр("ru = 'и';
																	|en = 'and'") + " ", ", ");
			ТекстСообщения = ТекстСообщения + " " + НСтр("ru = 'и фармацевта';
														|en = 'and pharmaceutist'");
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЯвляетсяШахтерскойДолжностью И ЗначениеЯвляетсяФармацевтическойДолжностью Тогда
		Если ПустаяСтрока(ТекстСообщения) Тогда
			ТекстСообщения = НСтр("ru = 'шахтера и фармацевта';
									|en = 'miner and pharmaceutist'");
		ИначеЕсли НЕ ЗначениеЯвляетсяДолжностьюЛетногоЭкипажа И ЗначениеЯвляетсяФармацевтическойДолжностью Тогда
			ТекстСообщения = СтрЗаменить(ТекстСообщения, " " + НСтр("ru = 'и';
																	|en = 'and'") + " ", ", ");
			ТекстСообщения = ТекстСообщения + " " + НСтр("ru = 'и фармацевта';
														|en = 'and pharmaceutist'");
		КонецЕсли;
	КонецЕсли;
		
	Если НЕ ПустаяСтрока(ТекстСообщения) Тогда
		
		ТекстСообщения = НСТр("ru = 'Должность не может одновременно являться должностью';
								|en = 'Position cannot be position at the same time'") + " " + ТекстСообщения + ".";
		ВызватьИсключение ТекстСообщения;
		
	КонецЕсли;
	
	Если НЕ ВведенаВШтатноеРасписание Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ДатаВвода");
	КонецЕсли; 

	Если НЕ ИсключенаИзШтатногоРасписания Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ДатаИсключения");
	КонецЕсли; 
	
	Если (Не ИсключенаИзШтатногоРасписания) И ПометкаУдаления Тогда
		ТекстСообщения = НСтр("ru = 'У помеченной на удаление должности нельзя снять флаг ""Исключена из штатного расписания""!';
								|en = 'Check box ""Excluded from staff list"" cannot be cleared for the position marked for deletion.'",);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, , , Отказ);
	КонецЕсли;	

	Если ЗначениеЗаполнено(ДатаИсключения) И ЗначениеЗаполнено(ДатаВвода) И ДатаВвода > ДатаИсключения Тогда
		ТекстСообщения = НСтр("ru = 'Дата ввода должности в штатное расписание не может быть больше даты исключения!';
								|en = 'Exception date cannot be less than the date when the position was included in the staff list.'",);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, , , Отказ);	
	КонецЕсли;		

	Если ИсключенаИзШтатногоРасписания Тогда 
		ПроверкаИспользованияВШтатномРасписании(Отказ);
	КонецЕсли;	
	
	ЭлектронныеТрудовыеКнижки.ПроверитьЗаполнениеТрудовойФункцииВДолжности(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПроверкаИспользованияВШтатномРасписании(Отказ)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Должность", Ссылка);
	Запрос.УстановитьПараметр("ДатаИсключения", ДатаИсключения);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ШтатноеРасписание.Наименование КАК ПозицияШтатногоРасписания,
	|	ШтатноеРасписание.Ссылка
	|ИЗ
	|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
	|ГДЕ
	|	ШтатноеРасписание.Должность = &Должность
	|	И НЕ ШтатноеРасписание.ГруппаПозицийПодразделения
	|	И НЕ(ШтатноеРасписание.Закрыта
	|				И ШтатноеРасписание.ДатаЗакрытия <= &ДатаИсключения)";
	
	Результат = Запрос.Выполнить();
		
	Если Не Результат.Пустой() Тогда
		
		ТекстСообщения = НСтр("ru = 'Должность ""%1"" не может быть исключена из штатного расписания, т.к. она используется в действующих позициях:';
								|en = 'Position ""%1"" cannot be excluded from the staff list because it is used in operating positions: '"); 
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстСообщения,
			Ссылка,
			); 
        ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, "Объект.ИсключенаИзШтатногоРасписания" , , Отказ);

		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ТекстСообщения = НСтр("ru = '- позиция ""%1""';
									|en = '- position ""%1""'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстСообщения,
				Выборка.ПозицияШтатногоРасписания);
	        ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, "Объект.ИсключенаИзШтатногоРасписания" , , Отказ);
		КонецЦикла;	
		
	КонецЕсли;	    
		
КонецПроцедуры	

Процедура ПередЗаписью(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьШтатноеРасписание") Тогда
		Если ПометкаУдаления И ВведенаВШтатноеРасписание И (Не ИсключенаИзШтатногоРасписания) Тогда
			ТекстСообщения = НСтр("ru = 'Нельзя пометить на удаление действующую должность';
									|en = 'Cannot mark an active position for deletion'",);
			ВызватьИсключение ТекстСообщения;
		КонецЕсли;	
	КонецЕсли;
	
	Если ПустаяСтрока(НаименованиеКраткое) Тогда
		НаименованиеКраткое = Наименование;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьДополнительныеСведенияКлассификатора(ДополнительныеСведения) Экспорт
	
	ЗаполнитьКатегорию = Истина;
	Для Каждого ДополнительноеСведение Из ДополнительныеСведения Цикл
		
		Если ДополнительноеСведение.Ключ = "Категория" Тогда
			
			ЗаполнитьКатегорию = Ложь;
			ОКПДТРКатегория = ДополнительноеСведение.Значение;
			
		ИначеЕсли ДополнительноеСведение.Ключ = "Код" Тогда
			
			Значение = СокрЛП(ДополнительноеСведение.Значение);
			Если Не ПустаяСтрока(Значение) Тогда
				
				ОКПДТРКЧ = Прав(Значение, 1);
				ОКПДТРКод = Лев(Значение, СтрДлина(Значение) - 1);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗаполнитьКатегорию Тогда
		ОКПДТРКатегория = "4";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли