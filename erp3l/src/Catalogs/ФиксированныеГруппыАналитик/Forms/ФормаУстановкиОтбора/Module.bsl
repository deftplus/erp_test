
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КэшируемыеЗначения = Новый Структура;
	КэшируемыеЗначения.Вставить("ВидыСравнения", МСФОВызовСервераУХ.ПолучитьСтруктуруСоЗначениямиПеречисления("ВидыСравнения"));
	
	ТипАналитики = Параметры.ТипАналитики;
	КлючОтбора = Параметры.КлючОтбора;
	ЭтаФорма.ВидСравнения = Параметры.ВидСравнения;
	ЗначениеСписок = Параметры.Значение;
	Если ЗначениеСписок.Количество() > 0 Тогда
		ЭтаФорма.Значение = ЗначениеСписок[0].Значение;
	КонецЕсли;
	
	Элементы.Значение.ОграничениеТипа = ТипАналитики;
	Элементы.ЗначениеСписокЗначение.ОграничениеТипа = ТипАналитики;
	
	ОбновитьВидимость();	
	
КонецПроцедуры

#КонецОбласти

#Область КомандыФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("ВидСравнения", 	ВидСравнения);
	ПараметрыВыбора.Вставить("КлючОтбора", 		КлючОтбора);
	Если СравнениеВСписке(ВидСравнения, КэшируемыеЗначения.ВидыСравнения) Тогда
		ПараметрыВыбора.Вставить("ЗначениеСписок", ЗначениеСписок);	
	Иначе
		
		Список = Новый СписокЗначений;
		Список.Добавить(Значение);
		ПараметрыВыбора.Вставить("ЗначениеСписок", Список);
		
	КонецЕсли;
		
	Оповестить("УстановленОтборФиксированнойАналитики", ПараметрыВыбора, ЭтаФорма);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидСравненияПриИзменении(Элемент)
	ОбновитьВидимость();
КонецПроцедуры

#КонецОбласти


#Область ВспомогательныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьВидимость()

	Если СравнениеВСписке(ВидСравнения, КэшируемыеЗначения.ВидыСравнения) Тогда
	
		Элементы.ЗначениеСписок.Видимость = Истина;
		Элементы.Значение.Видимость = Ложь;
		
	Иначе
		
		Элементы.ЗначениеСписок.Видимость = Ложь;
		Элементы.Значение.Видимость = Истина;
			
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СравнениеВСписке(ВидСравнения, ВидыСравнения)

	 Возврат (ВидСравнения = ВидыСравнения.ВСписке) 
		Или (ВидСравнения = ВидыСравнения.НеВСписке)
		Или (ВидСравнения = ВидыСравнения.ВСпискеПоИерархии)
		Или (ВидСравнения = ВидыСравнения.НеВСпискеПоИерархии);

КонецФункции
	
#КонецОбласти
