// Возвращает массив показателей, которые содержат тип показателя ТипПоказателяВход.
Функция ПолучитьМассивПоказателейСТипом(ТипПоказателяВход) Экспорт
	РезультатФункции = Новый Массив;
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоказателиОтчетов.Ссылка,
		|	ПоказателиОтчетов.ТипПоказателя
		|ИЗ
		|	Справочник.ПоказателиОтчетов КАК ПоказателиОтчетов
		|ГДЕ
		|	ПоказателиОтчетов.ТипПоказателя = &ТипПоказателя
		|	И НЕ ПоказателиОтчетов.ПометкаУдаления";
	Запрос.УстановитьПараметр("ТипПоказателя", ТипПоказателяВход);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		РезультатФункции.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
	КонецЦикла;
	Возврат РезультатФункции;
КонецФункции

// Возвращает структуру значений реквизитов из типа показателя ТипПоказателяВход.
Функция ПолучитьСтруктуруДанныхТипаПоказателя(ТипПоказателяВход) Экспорт
	РезультатФункции = Новый Структура;
	РезультатФункции.Вставить("ВидИтога",					 ТипПоказателяВход.ВидИтога);
	РезультатФункции.Вставить("ВидКурса",					 ТипПоказателяВход.ВидКурса);
	РезультатФункции.Вставить("ВидПоказателя",				 ТипПоказателяВход.ВидПоказателя);
	РезультатФункции.Вставить("ДействияПриАктуализации",	 ТипПоказателяВход.ДействияПриАктуализации);
	РезультатФункции.Вставить("НеМасштабируется",			 ТипПоказателяВход.НеМасштабируется);
	РезультатФункции.Вставить("НеФинансовый",				 ТипПоказателяВход.НеФинансовый);
	РезультатФункции.Вставить("ПересчитыватьВалютнуюСумму",	 ТипПоказателяВход.ПересчитыватьВалютнуюСумму);
	РезультатФункции.Вставить("ТипЗначения",				 ТипПоказателяВход.ТипЗначения);
	Возврат РезультатФункции;
КонецФункции

// Переносит значения реквизитов из типа показателя ТипПоказателяВход в Показатель.
Процедура ЗаполнитьДанныеПоказателяПоТипуПоказателя(Показатель, ТипПоказателяВход) Экспорт
	СтруктураДанныхТипаПоказателя = ПолучитьСтруктуруДанныхТипаПоказателя(ТипПоказателяВход);
	ЗаполнитьЗначенияСвойств(Показатель, СтруктураДанныхТипаПоказателя);
КонецПроцедуры

// Перезаписывает показатель ПоказательВход, изменяя реквизиты значениями
// из типа ТипПоказателяВход.
Функция ПеренестиДанныеТипаВПоказатель(ПоказательВход, ТипПоказателяВход) Экспорт
	РезультатФункции = Истина;
	Попытка
		ПоказательОбъект = ПоказательВход.ПолучитьОбъект();
		ЗаполнитьДанныеПоказателяПоТипуПоказателя(ПоказательОбъект, ТипПоказателяВход);
		ПоказательОбъект.Записать();
	Исключение
		ТекстСообщения = НСтр("ru = 'При переносе данных типа показателя %ТипПоказателя% в показатель %Показатель% возникли ошибки: %ОписаниеОшибки%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ТипПоказателя%", Строка(ТипПоказателяВход));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Показатель%", Строка(ПоказательВход));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%", ОписаниеОшибки());
		ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
		РезультатФункции = Ложь;
	КонецПопытки;
	Возврат РезультатФункции;
КонецФункции
