
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не СтруктураИсключаемых = Неопределено Тогда
		ТекущийОбъект.ИсключаемыеРеквизиты = Новый ХранилищеЗначения(СтруктураИсключаемых);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	СтруктураИсключаемых = ТекущийОбъект.ИсключаемыеРеквизиты.Получить();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Модифицированность = Истина;
		
		Если ПустаяСтрока(Объект.ИмяОбъекта) Тогда
			ПоказатьВводЗначения(
				Новый ОписаниеОповещения("ВыборОбъектаОбменаЗавершение", ЭтотОбъект),
				,
				"Выберите объект обмена",
				Новый ОписаниеТипов("СправочникСсылка.СП_ОбъектыОбмена")
			);
			
		Иначе
			ОткрытьФормуНастройкиСхемы();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастроитьСхему(Команда)
	
	ОткрытьФормуНастройкиСхемы();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьФормуНастройкиСхемы()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИмяОбъекта", Объект.ИмяОбъекта);
	ПараметрыФормы.Вставить("СтруктураИсключаемых", СтруктураИсключаемых);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершитьНастройкуСхемыДанных", ЭтотОбъект);
	
	ОткрытьФорму(
		"Обработка.СП_ВыборРеквизитов.Форма.Форма",
		ПараметрыФормы,
		ЭтотОбъект,
		ЭтотОбъект.УникальныйИдентификатор,
		,
		,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
	);
	
КонецПроцедуры

&НаСервере
Процедура ЗавершитьНастройкуСхемыДанных(Результат, ДопПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураИсключаемых = Результат;
	
	ТекстСхемыНовый = СП_СхемыДанных.ПолучитьСхемуДанныхОбъектаВВидеСтроки(Объект.ИмяОбъекта, СтруктураИсключаемых);
	
	Если ТекстСхемыНовый <> Объект.ТекстСхемы Тогда
		Объект.ТекстСхемы = ТекстСхемыНовый;
		Объект.ВерсияСхемы = Объект.ВерсияСхемы + 1;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборОбъектаОбменаЗавершение(ОбъектОбмена, ДопПараметры) Экспорт
	
	Если ОбъектОбмена = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ИмяОбъекта = ПолучитьИмяОбъекта(ОбъектОбмена);
	Объект.Наименование = ПолучитьПространствоИмен(Объект.ИмяОбъекта);
	
	ОткрытьФормуНастройкиСхемы();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПространствоИмен(ИмяОбъекта)
	
	Возврат СП_СхемыДанных.ПространствоИменПоИмениОбъекта(ИмяОбъекта);
	
КонецФункции

&НаСервере
Функция ПолучитьИмяОбъекта(ОбъектОбменаСсылка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ОбъектОбменаСсылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СП_ОбъектыОбмена.ИмяОбъекта
	|ИЗ
	|	Справочник.СП_ОбъектыОбмена КАК СП_ОбъектыОбмена
	|ГДЕ
	|	СП_ОбъектыОбмена.Ссылка = &Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.ИмяОбъекта;
	
КонецФункции

#КонецОбласти