
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Наименование");
	Поля.Добавить("Синоним");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Данные.Синоним) Тогда
		СтандартнаяОбработка = Ложь;
		Представление = Данные.Синоним;
	КонецЕсли;
	
КонецПроцедуры

#Область КлючевыеРеквизиты

// Функция - Получить реквизиты по типу
//
// Параметры:
//  Объект				 - <СправочникСсылка.ДокументыБД>,<СправочникОбъект.ДокументыБД>, Структура - Объект для поиска в ТЧ
//  ПолноеИмяТипДанных	 - <Строка>	 - ТипДанных
//  ИмяРеквизита		 - 	 - 
// 
// Возвращаемое значение: <СписокЗначений(Имя, Синоним, Пометка)> - список содержащий: 
//   - имя реквизита, Синоним реквизита, Пометка о соответствии переданному "ИмяРеквизита"
//
Функция ПолучитьРеквизитыПоТипу(Объект, ПолноеИмяМетаданных, ИмяРеквизита = Неопределено) Экспорт

	Результат = Новый СписокЗначений;
	
	Для Каждого Строка ИЗ Объект.Реквизиты Цикл
		
		Если НЕ СтрНайти(Строка.ТипДанных, ПолноеИмяМетаданных) > 0 Тогда			
			Продолжить;			
		КонецЕсли;
		
		Результат.Добавить(Строка.Имя, Строка.Синоним, ИмяРеквизита = Строка.Имя);
		
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#Область Обновление

Процедура ЗаполнитьКлючевыеРеквизитыМСФОПоУмолчанию() Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	т.Ссылка
	|ИЗ
	|	Справочник.ДокументыБД.Реквизиты КАК т
	|ГДЕ
	|	т.Имя В (""Сумма"", ""ДоговорКонтрагента"", ""Контрагент"")
	|	И т.Ссылка.Владелец = ЗНАЧЕНИЕ(Справочник.ТипыБазДанных.ТекущаяИБ)");
	
	ИменаРеквизитов = Новый Структура("Сумма,ДоговорКонтрагента,Контрагент", "РеквизитСуммыДокумента", "РеквизитДоговорКонтрагента", "РеквизитКонтрагент");
	ТипыРеквизитов = Новый Структура("Сумма,ДоговорКонтрагента,Контрагент", "Число", "Справочник.ДоговорыКонтрагентов", "РеквизитКонтрагент");
	
	Выборка = Запрос.Выполнить().Выбрать();	
	Пока Выборка.Следующий() Цикл
		
		ОбъектБД = Выборка.Ссылка.ПолучитьОбъект();
		ЕстьИзменение = Ложь;
		
		Для каждого ИмяРекв Из ИменаРеквизитов Цикл
		
			Если ЗначениеЗаполнено(ОбъектБД[ИмяРекв.Значение]) Тогда
				// уже заполнено
				Если ОбъектБД.Реквизиты.Найти(ОбъектБД[ИмяРекв.Значение], "Имя") = Неопределено Тогда
					ОбъектБД[ИмяРекв.Значение] = "";// неактуальный реквизит
					ЕстьИзменение = Истина;
				Иначе 
					Продолжить;	
				КонецЕсли;				
			КонецЕсли;	
			
			СтрокиРекв = ОбъектБД.Реквизиты.НайтиСтроки(Новый Структура("Имя,ТипДанных", ИмяРекв.Ключ, ТипыРеквизитов[ИмяРекв.Ключ]));
			Если СтрокиРекв.Количество() Тогда
				ОбъектБД[ИмяРекв.Значение] = ИмяРекв.Ключ;
				ЕстьИзменение = Истина;
			КонецЕсли;
		
		КонецЦикла;
		
		Если ЕстьИзменение Тогда		
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ОбъектБД);		
		КонецЕсли;
		
	КонецЦикла; 

КонецПроцедуры

#КонецОбласти

#КонецЕсли