
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтрокаДата=Параметры.ДокументБД.Реквизиты.Найти("Дата","Имя");
	
	Если НЕ СтрокаДата=Неопределено Тогда
		
		НоваяСтрока=ТаблицаРеквизитов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаДата);
		
		Если НЕ Параметры.СписокРеквизитов.НайтиПоЗначению("Дата")=Неопределено Тогда
			
			НоваяСтрока.Использовать=Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	ДокументыБДРеквизиты.Имя,
	|	ДокументыБДРеквизиты.Синоним,
	|	ДокументыБДРеквизиты.ТипДанных
	|ИЗ
	|	Справочник.ДокументыБД.Реквизиты КАК ДокументыБДРеквизиты
	|ГДЕ
	|	ДокументыБДРеквизиты.Ссылка = &Ссылка
	|	И НЕ ДокументыБДРеквизиты.Имя В
	|				(ВЫБРАТЬ
	|					ПоказателиОбъектовБД.Наименование
	|				ИЗ
	|					Справочник.ПоказателиОбъектовБД КАК ПоказателиОбъектовБД
	|				ГДЕ
	|					ПоказателиОбъектовБД.Владелец = &Ссылка)";
	
	Запрос.УстановитьПараметр("Ссылка",Параметры.ДокументБД);
	
	Результат=Запрос.Выполнить().Выбрать();
	
	ОписаниеТиповСтрокаНеограниченнойДлины=ОбщегоНазначенияУХ.ПолучитьОписаниеТиповСтроки(0);
	
	Пока Результат.Следующий() Цикл
		
		Если  Результат.Имя="Ссылка"
			ИЛИ Результат.Имя="Номер"
			ИЛИ Результат.Имя="Код"
			ИЛИ Результат.Имя="Дата" Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ТипЗначенияКолонки=ОбщегоНазначенияУХ.ПолучитьОписаниеТиповИзСтрокиТипов(Результат.ТипДанных);
		
		Если ТипЗначенияКолонки=Неопределено ИЛИ ТипЗначенияКолонки=ОписаниеТиповСтрокаНеограниченнойДлины Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		НоваяСтрока=ТаблицаРеквизитов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Результат);
		
		Если НЕ Параметры.СписокРеквизитов.НайтиПоЗначению(Результат.Имя)=Неопределено Тогда
			
			НоваяСтрока.Использовать=Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	МассивИспользование=ТаблицаРеквизитов.НайтиСтроки(Новый Структура("Использовать",Истина));
	
	СписокРеквизитов=Новый СписокЗначений;
	
	Для Каждого Строка ИЗ МассивИспользование Цикл;
		
		СписокРеквизитов.Добавить(Новый Структура("Имя,Синоним,ТипДанных",Строка.Имя,Строка.Синоним,Строка.ТипДанных));
		
	КонецЦикла;
	
	Если Параметры.ТекущийОбъект Тогда
		
		Оповестить("ИзмененСписокИспользуемыхРеквизитовТекущегоОбъекта",СписокРеквизитов);
		
	Иначе
		
		Оповестить("ИзмененСписокИспользуемыхРеквизитовИсходногоОбъекта",СписокРеквизитов);
		
	КонецЕсли;
	
	Закрыть();
		
КонецПроцедуры
