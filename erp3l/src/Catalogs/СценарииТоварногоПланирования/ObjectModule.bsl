#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 
	
	Если Не ЗначениеЗаполнено(СпособРасчетаПотребностейВМатериалах) Тогда
		СпособРасчетаПотребностейВМатериалах = ПредопределенноеЗначение("Перечисление.СпособыРасчетаМатериалов.ВероятноеПотребление");
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если (ПланЗакупокПланироватьПоСумме ИЛИ ПланПродажПланироватьПоСумме) 
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВалют")
		И Не ЗначениеЗаполнено(Валюта) Тогда
		Валюта = ДоходыИРасходыСервер.ПолучитьВалютуУправленческогоУчета(Валюта);
		Если Не ЗначениеЗаполнено(Валюта) Тогда
			ВызватьИсключение НСтр("ru = 'Не удалось заполнить поле ""Валюта"". Установите валюту управленческого учета!';
									|en = 'Failed to fill in field ""Currency"". Set management accounting currency.'");
		КонецЕсли;
	КонецЕсли;
	
	Если Периодичность = Перечисления.Периодичность.Год
		ИЛИ Периодичность = Перечисления.Периодичность.Декада Тогда
	
		ОтображатьНомерПериода = Ложь;
	КонецЕсли;
	
	//++ НЕ УТ
	Если НЕ ОтражаетсяВБюджетировании Тогда
		СценарийБюджетирования = Неопределено;
	КонецЕсли;
	//-- НЕ УТ
	
	#Область УХ_Внедрение
	Если РежимФормированияПотребностейПланомЗакупок = Перечисления.РежимыФормированияПотребностейПланомЗакупок.РегистрируютПотребности 
		И (НЕ ПолучитьФункциональнуюОпцию("ИспользоватьЦентрализованноеУправлениеЗакупками") 
			ИЛИ Периодичность <>  Константы.ПериодичностьЦентрализованныхЗакупок.Получить()) Тогда
		РежимФормированияПотребностейПланомЗакупок = Перечисления.РежимыФормированияПотребностейПланомЗакупок.НеИспользуются;
	КонецЕсли;
	#КонецОбласти
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Не ЗначениеЗаполнено(СпособРасчетаПотребностейВМатериалах) Тогда
		СпособРасчетаПотребностейВМатериалах = ПредопределенноеЗначение("Перечисление.СпособыРасчетаМатериалов.ВероятноеПотребление");
	КонецЕсли;
	
	Если НЕ (ПланЗакупокПланироватьПоСумме ИЛИ ПланПродажПланироватьПоСумме) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Валюта");
	КонецЕсли;
	
	Если НЕ ИспользоватьДляПланированияМатериалов Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СпособРасчетаПотребностейВМатериалах");
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("УправлениеТорговлей") Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ОтражаетсяВБюджетировании");
		МассивНепроверяемыхРеквизитов.Добавить("СценарийБюджетирования");
	КонецЕсли;
	
	//++ НЕ УТ
	Если НЕ ОтражаетсяВБюджетировании Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СценарийБюджетирования");
	КонецЕсли;
	//-- НЕ УТ
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	#Область УХ_Встраивание
	ВстраиваниеУХСценарииТоварногоПланирования.ОбработкаПроверкиЗаполнения(Отказ, ЭтотОбъект, ПроверяемыеРеквизиты);
	#КонецОбласти
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если (ПланЗакупокПланироватьПоСумме ИЛИ ПланПродажПланироватьПоСумме)  
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВалют")
		И Не ЗначениеЗаполнено(Валюта) Тогда
		Валюта = ДоходыИРасходыСервер.ПолучитьВалютуУправленческогоУчета(Валюта);
		Если Не ЗначениеЗаполнено(Валюта) Тогда
			ВызватьИсключение НСтр("ru = 'Не удалось заполнить поле ""Валюта"". Установите валюту управленческого учета!';
									|en = 'Failed to fill in field ""Currency"". Set management accounting currency.'");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
