&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ОбновитьСостояниеВыбораАльтернатив" Тогда
		Если ТипЗнч(Параметр) <> Тип("Структура") Тогда
			Возврат;
		КонецЕсли;
		Если Параметр.Владелец = Объект.Ссылка Тогда
			ОбновитьСостояниеВыбораАльтернатив();
		КонеЦЕсли;

	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьСостояниеВыбораАльтернатив()

	// получим состояние процесса
	ОписаниеСостояния = ВыборОбъектовУХ.ПолучитьСостояниеПроцессаВыбора(Объект.Ссылка);
	ТекущийЭтапВыбора = ОписаниеСостояния.ТекущийЭтапВыбора;
	ПроцессВыбораЗавершен = ОписаниеСостояния.ПроцессВыбораЗавершен;
	
	Элементы.ИнвестиционныеПроекты.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(ИнвестиционныеПроекты, "РазделИнвестиционнойПрограммы", Объект.Ссылка, Истина);
	
	ОбновитьСостояниеВыбораАльтернатив();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(ИнвестиционныеПроекты, "РазделИнвестиционнойПрограммы", ТекущийОбъект.Ссылка, Истина);
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПроектовНаСервере(Ссылка)
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(ИнвестиционныеПроекты, "РазделИнвестиционнойПрограммы", Ссылка, Истина);
	Иначе
		ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(ИнвестиционныеПроекты, "РазделИнвестиционнойПрограммы", Ссылка, Истина, ВидСравненияКомпоновкиДанных.НеРавно);
	КонецЕСли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнвестиционныеПроекты(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Элементы.ИнвестиционныеПроекты.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура ПодборПроектов(Команда)
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст =
			НСтр("ru = 'Необходимо сохранить раздел инвест. программы перед началом подбора!'");
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещенияЗакрытия = Новый ОписаниеОповещения("ОбновитьИнвестиционныеПроекты",
		ЭтаФорма);
	ПараметрыПодбора = Новый Структура("РазделИнвестПрограммы", Объект.Ссылка);
	ОткрытьФорму("Справочник.Проекты.Форма.ФормаВыбораДляИнвестРаздела",
		ПараметрыПодбора, ЭтаФорма, ЭтаФорма.УникальныйИдентификатор,,,
		ОписаниеОповещенияЗакрытия, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры
