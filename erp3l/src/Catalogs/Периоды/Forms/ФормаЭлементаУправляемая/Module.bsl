&НаСервереБезКонтекста
Функция УстановитьДоступныеОтборы(Родитель)
	
	ДоступныеЗначения = Новый СписокЗначений;
	
	ПериодичностьРодителя = Родитель.Периодичность;
	Для каждого ЗначениеПеречисления Из Перечисления.Периодичность Цикл
		ТекПериодичностьУжеРодительской = НЕ ОбщегоНазначенияСерверУХ.СравнитьПериодичность(ЗначениеПеречисления, ПериодичностьРодителя, Истина);
		Если ТекПериодичностьУжеРодительской Тогда
			ДоступныеЗначения.Добавить(ЗначениеПеречисления);
		Иначе
			// Периодичность шире родительской. Не добавляем в список.
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДоступныеЗначения;
	
КонецФункции

&НаСервере
Процедура ОбработатьИзменениеПараметров()
	
	Если НЕ ЗначениеЗаполнено(Объект.ДатаНачала) Тогда
		Возврат; // отказ
	ИначеЕсли НЕ ЗначениеЗаполнено(Объект.Периодичность) Тогда
		Возврат; // отказ
	КонецЕсли;
	
	Если Объект.Периодичность = Перечисления.Периодичность.Год Тогда
		ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.ДатаНачала,    НачалоГода(Объект.ДатаНачала));
		Справочники.Периоды.ОпределитьДатуОкончания(Объект.ДатаНачала, Объект.ДатаОкончания, Объект.Периодичность);
		ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.Наименование, ПредставлениеПериода(НачалоДня(Объект.ДатаНачала), КонецДня(Объект.ДатаОкончания)));
		Объект.Код = Лев(Объект.Наименование, 4);
		
	ИначеЕсли Объект.Периодичность = Перечисления.Периодичность.ДевятьМесяцев Тогда
		ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.ДатаНачала,   НачалоГода(Объект.ДатаНачала));
		НовоеНаименование = НСтр("ru = 'Девять месяцев %ДатаНачала%'");
		НовоеНаименование = СтрЗаменить(НовоеНаименование, "%ДатаНачала%", Формат(Объект.ДатаНачала, "ДФ='гггг ""г.""'"));
		ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.Наименование, НовоеНаименование);
		Справочники.Периоды.ОпределитьДатуОкончания(Объект.ДатаНачала, Объект.ДатаОкончания, Объект.Периодичность);
		
	ИначеЕсли Объект.Периодичность = Перечисления.Периодичность.Полугодие Тогда
		Если Объект.ДатаНачала < ДобавитьМесяц(НачалоГода(Объект.ДатаНачала), 6) Тогда
			ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.ДатаНачала, НачалоГода(Объект.ДатаНачала));
			НовоеНаименование = НСтр("ru = '1 полугодие %ДатаНачала%'");
			НовоеНаименование = СтрЗаменить(НовоеНаименование, "%ДатаНачала%", Формат(Объект.ДатаНачала, "ДФ='гггг ""г.""'"));
			ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.Наименование, НовоеНаименование);
		Иначе 
			ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.ДатаНачала, ДобавитьМесяц(НачалоГода(Объект.ДатаНачала), 6));
			НовоеНаименование = НСтр("ru = '2 полугодие %ДатаНачала%'");
			НовоеНаименование = СтрЗаменить(НовоеНаименование, "%ДатаНачала%", Формат(Объект.ДатаНачала, "ДФ='гггг ""г.""'"));
			ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.Наименование, НовоеНаименование);
		КонецЕсли;
		Справочники.Периоды.ОпределитьДатуОкончания(Объект.ДатаНачала, Объект.ДатаОкончания, Объект.Периодичность);
		
	ИначеЕсли Объект.Периодичность = Перечисления.Периодичность.Квартал Тогда
		ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.ДатаНачала,    НачалоКвартала(Объект.ДатаНачала));
		Справочники.Периоды.ОпределитьДатуОкончания(Объект.ДатаНачала, Объект.ДатаОкончания, Объект.Периодичность);
		ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.Наименование, ПредставлениеПериода(НачалоДня(Объект.ДатаНачала), КонецДня(Объект.ДатаОкончания)));
	ИначеЕсли Объект.Периодичность = Перечисления.Периодичность.Месяц Тогда
		ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.ДатаНачала,    НачалоМесяца(Объект.ДатаНачала));
		Справочники.Периоды.ОпределитьДатуОкончания(Объект.ДатаНачала, Объект.ДатаОкончания, Объект.Периодичность);
		ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.Наименование, ПредставлениеПериода(НачалоДня(Объект.ДатаНачала), КонецДня(Объект.ДатаОкончания)));
	ИначеЕсли Объект.Периодичность = Перечисления.Периодичность.Декада Тогда
		
		Если День(Объект.ДатаНачала) < 10 Тогда
			ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.ДатаНачала,    НачалоМесяца(Объект.ДатаНачала));
			НовоеНаименование = НСтр("ru = '1 декада. %ДатаНачала%'");
			НовоеНаименование = СтрЗаменить(НовоеНаименование, "%ДатаНачала%", Формат(Объект.ДатаНачала, "ДФ='ММММ гггг ""г.""'"));
			ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.Наименование, НовоеНаименование);
		ИначеЕсли День(Объект.ДатаНачала) < 20 Тогда
			ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.ДатаНачала,    ОбщегоНазначенияУХ.ДобавитьДень(НачалоМесяца(Объект.ДатаНачала), 10));
			НовоеНаименование = НСтр("ru = '2 декада. %ДатаНачала%'");
			НовоеНаименование = СтрЗаменить(НовоеНаименование, "%ДатаНачала%", Формат(Объект.ДатаНачала, "ДФ='ММММ гггг ""г.""'"));
			ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.Наименование, НовоеНаименование);
		Иначе 
			ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.ДатаНачала,    ОбщегоНазначенияУХ.ДобавитьДень(НачалоМесяца(Объект.ДатаНачала), 20));
			НовоеНаименование = НСтр("ru = '3 декада. %ДатаНачала%'");
			НовоеНаименование = СтрЗаменить(НовоеНаименование, "%ДатаНачала%", Формат(Объект.ДатаНачала, "ДФ='ММММ гггг ""г.""'"));
			ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.Наименование, НовоеНаименование);
		КонецЕсли;
		Справочники.Периоды.ОпределитьДатуОкончания(Объект.ДатаНачала, Объект.ДатаОкончания, Объект.Периодичность);
	ИначеЕсли Объект.Периодичность = Перечисления.Периодичность.Неделя Тогда
		
		Если День(Объект.ДатаНачала) < 7 Тогда
			ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.ДатаНачала, ?(День(НачалоНедели(Объект.ДатаНачала))>День(Объект.ДатаНачала),НачалоМесяца(Объект.ДатаНачала),НачалоНедели(Объект.ДатаНачала)));
			НовоеНаименование = НСтр("ru = '%НомерНедели% неделя. %ДатаНачала%'");
			Если ОбщегоНазначенияУХ.НачалоНеделиПоМесяцу(Объект.ДатаНачала) = НачалоМесяца(Объект.ДатаНачала) Тогда
				НовоеНаименование = СтрЗаменить(НовоеНаименование, "%НомерНедели%", НСтр("ru = '1'"));
			Иначе
				НовоеНаименование = СтрЗаменить(НовоеНаименование, "%НомерНедели%", НСтр("ru = '2'"));
			КонецЕсли;
			НовоеНаименование = СтрЗаменить(НовоеНаименование, "%ДатаНачала%", Формат(Объект.ДатаНачала, "ДФ='ММММ гггг ""г.""'"));
			ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.Наименование, НовоеНаименование);
		ИначеЕсли День(Объект.ДатаНачала) < 14 Тогда
			ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.ДатаНачала,    НачалоНедели(Объект.ДатаНачала));
			НовоеНаименование = НСтр("ru = '%НомерНедели% неделя. %ДатаНачала%'");
			Если ОбщегоНазначенияУХ.НачалоНеделиПоМесяцу(ОбщегоНазначенияУХ.ДобавитьИнтервал(Объект.ДатаНачала,Перечисления.Периодичность.Неделя,-1)) <= НачалоМесяца(Объект.ДатаНачала) Тогда
				НовоеНаименование = СтрЗаменить(НовоеНаименование, "%НомерНедели%", НСтр("ru = '2'"));
			Иначе
				НовоеНаименование = СтрЗаменить(НовоеНаименование, "%НомерНедели%", НСтр("ru = '3'"));
			КонецЕсли;
			НовоеНаименование = СтрЗаменить(НовоеНаименование, "%ДатаНачала%", Формат(Объект.ДатаНачала, "ДФ='ММММ гггг ""г.""'"));
			ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.Наименование, НовоеНаименование);
		ИначеЕсли День(Объект.ДатаНачала) < 21 Тогда
			ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.ДатаНачала,    НачалоНедели(Объект.ДатаНачала));
			НовоеНаименование = НСтр("ru = '%НомерНедели% неделя. %ДатаНачала%'");
			Если ОбщегоНазначенияУХ.НачалоНеделиПоМесяцу(ОбщегоНазначенияУХ.ДобавитьИнтервал(Объект.ДатаНачала,Перечисления.Периодичность.Неделя,-2))<=НачалоМесяца(Объект.ДатаНачала) Тогда
				НовоеНаименование = СтрЗаменить(НовоеНаименование, "%НомерНедели%", НСтр("ru = '3'"));
			Иначе
				НовоеНаименование = СтрЗаменить(НовоеНаименование, "%НомерНедели%", НСтр("ru = '4'"));
			КонецЕсли;
			НовоеНаименование = СтрЗаменить(НовоеНаименование, "%ДатаНачала%", Формат(Объект.ДатаНачала, "ДФ='ММММ гггг ""г.""'"));
			ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.Наименование, НовоеНаименование);
		ИначеЕсли День(Объект.ДатаНачала) < 28 Тогда
			ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.ДатаНачала,    НачалоНедели(Объект.ДатаНачала));
			НовоеНаименование = НСтр("ru = '%НомерНедели% неделя. %ДатаНачала%'");
			Если ОбщегоНазначенияУХ.НачалоНеделиПоМесяцу(ОбщегоНазначенияУХ.ДобавитьИнтервал(Объект.ДатаНачала,Перечисления.Периодичность.Неделя,-3))<=НачалоМесяца(Объект.ДатаНачала) Тогда
				НовоеНаименование = СтрЗаменить(НовоеНаименование, "%НомерНедели%", НСтр("ru = '4'"));
			Иначе
				НовоеНаименование = СтрЗаменить(НовоеНаименование, "%НомерНедели%", НСтр("ru = '5'"));
			КонецЕсли;
			НовоеНаименование = СтрЗаменить(НовоеНаименование, "%ДатаНачала%", Формат(Объект.ДатаНачала, "ДФ='ММММ гггг ""г.""'"));
			ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.Наименование, НовоеНаименование);
		Иначе 
			ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.ДатаНачала,    НачалоНедели(Объект.ДатаНачала));
			НовоеНаименование = НСтр("ru = '%НомерНедели% неделя. %ДатаНачала%'");
			Если ОбщегоНазначенияУХ.НачалоНеделиПоМесяцу(ОбщегоНазначенияУХ.ДобавитьИнтервал(Объект.ДатаНачала,Перечисления.Периодичность.Неделя,-4))<=НачалоМесяца(Объект.ДатаНачала) Тогда
				НовоеНаименование = СтрЗаменить(НовоеНаименование, "%НомерНедели%", НСтр("ru = '5'"));
			Иначе
				НовоеНаименование = СтрЗаменить(НовоеНаименование, "%НомерНедели%", НСтр("ru = '6'"));
			КонецЕсли;
			НовоеНаименование = СтрЗаменить(НовоеНаименование, "%ДатаНачала%", Формат(Объект.ДатаНачала, "ДФ='ММММ гггг ""г.""'"));
			ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.Наименование, НовоеНаименование);
		КонецЕсли;
		Справочники.Периоды.ОпределитьДатуОкончания(Объект.ДатаНачала, Объект.ДатаОкончания, Объект.Периодичность);
	
	Иначе
		Справочники.Периоды.ОпределитьДатуОкончания(Объект.ДатаНачала, Объект.ДатаОкончания, Объект.Периодичность);
		ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.Наименование, ПредставлениеПериода(НачалоДня(Объект.ДатаНачала), КонецДня(Объект.ДатаОкончания)));
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТолькоПросмотрПроизвольногоПериода()
	
	Элементы.Наименование.ТолькоПросмотр 	 = Не Объект.Произвольный;
	Элементы.ДатаОкончания.ТолькоПросмотр	 = Не Объект.Произвольный;
	Элементы.Периодичность.Видимость		 = НЕ Объект.Произвольный;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	
	Если НЕ Объект.Произвольный Тогда
		ОбработатьИзменениеПараметров();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодичностьПриИзменении(Элемент)
	
	ОбработатьИзменениеПараметров();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьТолькоПросмотрПроизвольногоПериода();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодичностьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = УстановитьДоступныеОтборы(Объект.Родитель);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		
		ТекОбъект = РеквизитФормыВЗначение("Объект");
		ТекОБъект.Заполнить(Неопределено);
		ЗначениеВРеквизитФормы(ТекОбъект, "Объект");
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПроизвольныйПриИзменении(Элемент)
	
	УстановитьТолькоПросмотрПроизвольногоПериода();
	
КонецПроцедуры
