// Процедура устанавливает значения и доступность флагов заполнения
//
&НаСервере
Процедура ПриИзмененииРодителя(Отказ = Ложь)
	
	ПериодичностьРодителя = Родитель.Периодичность;
	
	ЗаполнятьДевятьМесяцев = Ложь;
	ЗаполнятьПолугодия     = Ложь;
	ЗаполнятьКварталы      = Ложь;
	ЗаполнятьМесяцы        = Ложь;
	ЗаполнятьДекады        = Ложь;
	ЗаполнятьНедели        = Ложь;
	ЗаполнятьДни           = Ложь;

	Если НЕ ЗначениеЗаполнено(Родитель) Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Не определен период-родитель!'");
		Сообщение.Сообщить();
		
	ИначеЕсли ПериодичностьРодителя = Перечисления.Периодичность.День Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Период с периодичностью ""День"" является минимальным!'");
		Сообщение.Сообщить();

	ИначеЕсли ПериодичностьРодителя = Перечисления.Периодичность.Год Тогда
		ЗаполнятьДевятьМесяцев = Истина;
		ЗаполнятьПолугодия     = Истина;
		ЗаполнятьКварталы      = Истина;
		ЗаполнятьМесяцы        = Истина;
		ЗаполнятьДекады        = Истина;
		ЗаполнятьНедели        = Истина;
		ЗаполнятьДни           = Истина;
	ИначеЕсли ПериодичностьРодителя = Перечисления.Периодичность.ДевятьМесяцев Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Период девять месяцев не может содержать подчиненных периодов'");
		Сообщение.Сообщить();

	ИначеЕсли ПериодичностьРодителя = Перечисления.Периодичность.Полугодие Тогда
		ЗаполнятьКварталы  = Истина;
		ЗаполнятьМесяцы    = Истина;
		ЗаполнятьДекады    = Истина;
		ЗаполнятьНедели    = Истина;
		ЗаполнятьДни       = Истина;
	ИначеЕсли ПериодичностьРодителя = Перечисления.Периодичность.Квартал Тогда
		ЗаполнятьМесяцы    = Истина;
		ЗаполнятьДекады    = Истина;
		ЗаполнятьНедели    = Истина;
		ЗаполнятьДни       = Истина;
	ИначеЕсли ПериодичностьРодителя = Перечисления.Периодичность.Месяц Тогда
		ЗаполнятьНедели    = Истина;
		ЗаполнятьДекады    = Истина;
		ЗаполнятьДни       = Истина;
	ИначеЕсли ПериодичностьРодителя = Перечисления.Периодичность.Декада Тогда
		ЗаполнятьДни       = Истина;
	ИначеЕсли ПериодичностьРодителя = Перечисления.Периодичность.Неделя Тогда
		ЗаполнятьДни       = Истина;
	КонецЕсли;
	
	Элементы.ЗаполнятьДевятьМесяцев.Доступность = ЗаполнятьДевятьМесяцев;
	Элементы.ЗаполнятьПолугодия.Доступность     = ЗаполнятьПолугодия;
	Элементы.ЗаполнятьКварталы.Доступность      = ЗаполнятьКварталы;
	Элементы.ЗаполнятьМесяцы.Доступность        = ЗаполнятьМесяцы;
	Элементы.ЗаполнятьДекады.Доступность        = ЗаполнятьДекады;
	Элементы.ЗаполнятьНедели.Доступность        = ЗаполнятьНедели;
	Элементы.ЗаполнятьДни.Доступность           = ЗаполнятьДни;
	
КонецПроцедуры

&НаСервере
Функция Вн_СоздатьПериоды()
	
	ЗаполняемыеПериоды = Новый Массив;
	
	Если ЗаполнятьДевятьМесяцев Тогда
		ЗаполняемыеПериоды.Добавить(Перечисления.Периодичность.ДевятьМесяцев);
	КонецЕсли;
	
	Если ЗаполнятьПолугодия Тогда
		ЗаполняемыеПериоды.Добавить(Перечисления.Периодичность.Полугодие);
	КонецЕсли;
	
	Если ЗаполнятьКварталы Тогда
		ЗаполняемыеПериоды.Добавить(Перечисления.Периодичность.Квартал);
	КонецЕсли;
	
	Если ЗаполнятьМесяцы Тогда
		ЗаполняемыеПериоды.Добавить(Перечисления.Периодичность.Месяц);
	КонецЕсли;
	
	Если ЗаполнятьНедели Тогда
		ЗаполняемыеПериоды.Добавить(Перечисления.Периодичность.Неделя);
	КонецЕсли;
	
	Если ЗаполнятьДекады Тогда
		ЗаполняемыеПериоды.Добавить(Перечисления.Периодичность.Декада);
	КонецЕсли;
	
	Если ЗаполнятьДни Тогда
		ЗаполняемыеПериоды.Добавить(Перечисления.Периодичность.День);
	КонецЕсли;
	
	Если ЗаполняемыеПериоды.Количество() = 0 И НЕ ЗаполнятьДевятьМесяцев Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Не отмечена ни одна периодичность!'");
		Сообщение.Поле  = "Родитель";
		Сообщение.Сообщить();
		Возврат Ложь;
		
	КонецЕсли;

	
	Если НЕ ЗначениеЗаполнено(Родитель) Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Не определен период-родитель!'");
		Сообщение.Поле  = "Родитель";
		Сообщение.Сообщить();
		Возврат Ложь;
		
	ИначеЕсли Родитель.Периодичность = Перечисления.Периодичность.День Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Период с периодичностью ""День"" является минимальным!'");
		Сообщение.Поле  = "Родитель";
		Сообщение.Сообщить();
		Возврат Ложь;
		
	ИначеЕсли Родитель.Произвольный Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Период-родитель является произвольным.
		|Отсутствует возможность генерации произвольных периодов!'");
		Сообщение.Поле  = "Родитель";
		Сообщение.Сообщить();
		Возврат Ложь;
		
	Иначе
		
		Если ЗаполняемыеПериоды.Количество() = 0 Тогда
			Возврат Ложь;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Если Справочники.Периоды.СоздатьПериоды(Родитель, ЗаполняемыеПериоды) Тогда
			ЗафиксироватьТранзакцию();
			Возврат Истина;
		Иначе
			ОтменитьТранзакцию();
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
		
КонецФункции

&НаКлиенте
Процедура СоздатьПериоды(Команда)
	РезультатЗаполнения = Вн_СоздатьПериоды();
	Если РезультатЗаполнения Тогда
		КартинкаОповещения = БиблиотекаКартинок.Успешно32;
		ТекстОповещения = НСтр("ru = 'Создание периодов'");
		ПояснениеОповещения = НСтр("ru = 'Подпериоды для периода %ПериодРодитель% были успешно созданы'");
		ПояснениеОповещения = СтрЗаменить(ПояснениеОповещения, "%ПериодРодитель%", Строка(Родитель));
		ПоказатьОповещениеПользователя(ТекстОповещения, , ПояснениеОповещения, КартинкаОповещения);
		ОповеститьОбИзменении(Тип("СправочникСсылка.Периоды"));
	Иначе
		ТекстСообщения = НСтр("ru = 'Не удалось создать подпериоды для %ПериодРодитель%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПериодРодитель%", Строка(Родитель));
		ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Родитель = Параметры.ПериодРодитель;
	ПриИзмененииРодителя(Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура РодительПриИзменении(Элемент)
	
	ПриИзмененииРодителя();
	
КонецПроцедуры
