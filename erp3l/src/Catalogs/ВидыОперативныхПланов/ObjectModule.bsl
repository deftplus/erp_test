#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Использование = Перечисления.ИспользованиеАналитик;
	
	// Зачистить неправильные значение
	Для Каждого Аналитика Из НастройкаАналитик Цикл
		
		// Сброс группы для аналитик размещенных не в строках или расшифровке
		Если ЗначениеЗаполнено(Аналитика.ГруппаСтрок)
			И (Аналитика.Использование <> Использование.Строка 
			И Аналитика.Использование <> Использование.Уточняется) Тогда
			Аналитика.ГруппаСтрок = "";
		КонецЕсли;
		
		// Сброс значения для аналитик размещенных не в шапке или неиспользуемых аналитик.
		Если ЗначениеЗаполнено(Аналитика.ЗначениеАналитики)
			И (Аналитика.Использование <> Использование.НеИспользуется 
			И Аналитика.Использование <> Использование.КлючеваяАналитика) Тогда
			Аналитика.ЗначениеАналитики = неопределено;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ДанныеЗаполнения = неопределено Тогда
		
		ВидБюджета = ПланыВидовХарактеристик.ВидыБюджетов.БюджетДвиженияДенежныхСредств;
		Справочники.ВидыОперативныхПланов.ЗаполнитьВидОперПланаПоВидуБюджета(ЭтотОбъект);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("ВидБюджета") Тогда
			ВидБюджета = ДанныеЗаполнения.ВидБюджета;
		ИначеЕсли ДанныеЗаполнения.Свойство("Предназначение") Тогда
			Если ДанныеЗаполнения.Предназначение = Перечисления.ПредназначенияЭлементовСтруктурыОтчета.БюджетДвиженияДенежныхСредств Тогда
				ВидБюджета = ПланыВидовХарактеристик.ВидыБюджетов.БюджетДвиженияДенежныхСредств;
			ИначеЕсли ДанныеЗаполнения.Предназначение = Перечисления.ПредназначенияЭлементовСтруктурыОтчета.БюджетДоходовИРасходов Тогда
				ВидБюджета = ПланыВидовХарактеристик.ВидыБюджетов.БюджетДоходовИРасходов;
			ИначеЕсли ДанныеЗаполнения.Предназначение = Перечисления.ПредназначенияЭлементовСтруктурыОтчета.БюджетДвиженияРесурсов Тогда
				ВидБюджета = ПланыВидовХарактеристик.ВидыБюджетов.БюджетДвиженияРесурсов;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ВидБюджета) Тогда
				ДанныеЗаполнения.Вставить("ВидБюджета", ВидБюджета);
			КонецЕсли;
			
		КонецЕсли;
			
		Если ЗначениеЗаполнено(ВидБюджета) Тогда
			Справочники.ВидыОперативныхПланов.ЗаполнитьВидОперПланаПоВидуБюджета(ЭтотОбъект);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если НеИспользовать = Истина Тогда
		ПроверяемыеРеквизиты.Очистить();
		Возврат ;
	КонецЕсли;
	
	//
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	Если ИспользоватьДляВидаОпераций = Перечисления.ВидыОперацийОперативныйПлан.ВводЛимитов 
		ИЛИ ИспользоватьДляВидаОпераций = Перечисления.ВидыОперацийОперативныйПлан.Резервирование Тогда
		// Лимиты и резервы
		МассивНепроверяемыхРеквизитов.Добавить("ВидГоризонта");
		МассивНепроверяемыхРеквизитов.Добавить("Периодичность");
		МассивНепроверяемыхРеквизитов.Добавить("КоличествоПериодов");
		МассивНепроверяемыхРеквизитов.Добавить("ПервыйПериод");
	ИначеЕсли ВидГоризонта <> Перечисления.ВидыГоризонтовПланирования.ФиксированныйГоризонт Тогда
		// Планирование или любые
		МассивНепроверяемыхРеквизитов.Добавить("ПервыйПериод");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	//
	Проверки = Новый Массив;
	Аналитики = Справочники.АналитикиОперативногоПланирования;
	Использование = Перечисления.ИспользованиеАналитик;
	
	Размещение = Новый Соответствие;
	Для каждого Строка Из НастройкаАналитик Цикл
		Размещение.Вставить(Строка.Аналитика, Строка.Использование);
	КонецЦикла; 
	
	// Расположение аналитик
	ДопустимоеРазмещение = Новый Массив;
	ОписаниеОшибок = Новый Соответствие;
	
	// ВидБюджета
	МассивИспользования = Новый Массив;
	МассивИспользования.Добавить(Использование.КлючеваяАналитика);
	
	ДобавитьДопустимоеРазмещение(ДопустимоеРазмещение, Аналитики.ВидБюджета, МассивИспользования);
	
	// Валюта
	МассивИспользования = Новый Массив;
	МассивИспользования.Добавить(Использование.КлючеваяАналитика);
	
	ДобавитьДопустимоеРазмещение(ДопустимоеРазмещение, Аналитики.Валюта, МассивИспользования);
	
	// Период
	МассивИспользования = Новый Массив;
	МассивИспользования.Добавить(Использование.Колонка);
	
	ДобавитьДопустимоеРазмещение(ДопустимоеРазмещение, Аналитики.Период, МассивИспользования);
	
	// ЦФО
	Если Размещение[Аналитики.ЦФО] <> неопределено Тогда
		МассивИспользования = Новый Массив;
		МассивИспользования.Добавить(Использование.КлючеваяАналитика);
		МассивИспользования.Добавить(Использование.Строка);
		МассивИспользования.Добавить(Использование.Уточняется);
		
		ДобавитьДопустимоеРазмещение(ДопустимоеРазмещение, Аналитики.ЦФО, МассивИспользования);
	КонецЕсли;
	
	// СтатьяБюджета и Аналитики*
	Если Размещение[Аналитики.СтатьяБюджета] <> неопределено Тогда
		
		//СтатьяБюджета 
		МассивИспользования = Новый Массив;
		МассивИспользования.Добавить(Использование.КлючеваяАналитика);
		МассивИспользования.Добавить(Использование.Строка);
		МассивИспользования.Добавить(Использование.Уточняется);
		
		ДобавитьДопустимоеРазмещение(ДопустимоеРазмещение, Аналитики.СтатьяБюджета, МассивИспользования);
		
		МассивИспользования = ДопустимоеРазмещениеАналитикСтатьи(ДопустимоеРазмещение, Размещение);
		ДобавитьДопустимоеРазмещение(ДопустимоеРазмещение, Аналитики.Аналитика1, МассивИспользования);
		ДобавитьДопустимоеРазмещение(ДопустимоеРазмещение, Аналитики.Аналитика2, МассивИспользования);
		ДобавитьДопустимоеРазмещение(ДопустимоеРазмещение, Аналитики.Аналитика3, МассивИспользования);
		ДобавитьДопустимоеРазмещение(ДопустимоеРазмещение, Аналитики.Аналитика4, МассивИспользования);
		ДобавитьДопустимоеРазмещение(ДопустимоеРазмещение, Аналитики.Аналитика5, МассивИспользования);
		ДобавитьДопустимоеРазмещение(ДопустимоеРазмещение, Аналитики.Аналитика6, МассивИспользования);
		
		//
		ОписаниеОшибок.Вставить(Аналитики.Аналитика1, 
			НСтр("ru='Использование аналитики ""Аналитика 1"" зависит от аналитики ""Статья бюджета"".'"));
		ОписаниеОшибок.Вставить(Аналитики.Аналитика2, 
			НСтр("ru='Использование аналитики ""Аналитика 2"" зависит от аналитики ""Статья бюджета"".'"));
		ОписаниеОшибок.Вставить(Аналитики.Аналитика3, 
			НСтр("ru='Использование аналитики ""Аналитика 3"" зависит от аналитики ""Статья бюджета"".'"));
		ОписаниеОшибок.Вставить(Аналитики.Аналитика4, 
			НСтр("ru='Использование аналитики ""Аналитика 4"" зависит от аналитики ""Статья бюджета"".'"));
		ОписаниеОшибок.Вставить(Аналитики.Аналитика5, 
			НСтр("ru='Использование аналитики ""Аналитика 5"" зависит от аналитики ""Статья бюджета"".'"));
		ОписаниеОшибок.Вставить(Аналитики.Аналитика6, 
			НСтр("ru='Использование аналитики ""Аналитика 6"" зависит от аналитики ""Статья бюджета"".'"));
	КонецЕсли;
	
	//Приход/Расход
	Если Размещение[Аналитики.ПриходРасход] <> неопределено Тогда
		МассивИспользования = ДопустимоеРазмещениеПриходРасход(ДопустимоеРазмещение, Размещение);	
		ДобавитьДопустимоеРазмещение(ДопустимоеРазмещение, Аналитики.ПриходРасход,  МассивИспользования);
		
		ОписаниеОшибок.Вставить(Аналитики.ПриходРасход, 
			НСтр("ru='Аналитика ""Направление операции"" зависит от аналитики ""Статья бюджета"".'"));
	КонецЕсли;
	
	// Допустимое расположение договора контрагента
	Если Размещение[Аналитики.ДоговорКонтрагента] <> неопределено Тогда
		МассивИспользования = ДопустимоеРазмещениеДоговораКонтрагента(ДопустимоеРазмещение, Размещение);
		ДобавитьДопустимоеРазмещение(ДопустимоеРазмещение, Аналитики.ДоговорКонтрагента, МассивИспользования);
		ОписаниеОшибок.Вставить(Аналитики.ДоговорКонтрагента, 
			НСтр("ru='Использование аналитики ""Договор контрагента"" зависит от аналитик ""Контрагент"" и ""Организация"".'"));
	КонецЕсли;
	
	// Проверка
	Для каждого Строка Из ДопустимоеРазмещение Цикл
	
		ТекРазмещение = Размещение[Строка.Аналитика];
		Если Строка.Использование.Найти(ТекРазмещение) = неопределено Тогда
			
			// Ошибка
			Описание = ОписаниеОшибок[Строка.Аналитика];
			Если Описание = неопределено Тогда
				Описание = НСтр("ru=''");
			КонецЕсли;
			
			Если ТекРазмещение = Использование.НеИспользуется Тогда
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Аналитика ""%1"" должна использоваться! %2'"),
					Строка.Аналитика,
					Описание);
			ИначеЕсли ТекРазмещение = неопределено Тогда
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Аналитика ""%1"" не должно быть в списке используемых аналитик! %2'"),
					Строка.Аналитика,
					Описание);
			Иначе
				
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Аналитику ""%1"" нельзя использовать %2. %3'"),
					Строка.Аналитика,
					НРег(Строка(ТекРазмещение)),
					Описание);
			КонецЕсли;
			
			ВстраиваниеОПКПереопределяемый.СообщитьОбОшибке(ТекстСообщения, Отказ);
			
		КонецЕсли;
	
	КонецЦикла; 
	
	// При наличии аналитик, размещенных в расшифровке, в настройке должны быть аналитики, размещенные в строках
	ЧислоАналитикРасшифровки = НастройкаАналитик.НайтиСтроки(Новый Структура("Использование", Перечисления.ИспользованиеАналитик.Уточняется)).Количество();
	ЧислоАналитикСтроки = НастройкаАналитик.НайтиСтроки(Новый Структура("Использование", Перечисления.ИспользованиеАналитик.Строка)).Количество();
	Если ЧислоАналитикРасшифровки > 0 И ЧислоАналитикСтроки = 0 Тогда
		
		ТекстСообщения = НСтр("ru = 'В настройке есть аналитики, размещенные в расшифровке, но отсутствуют, размещенные в строках. Такая настройка не может быть записана.'");
		ВстраиваниеОПКПереопределяемый.СообщитьОбОшибке(ТекстСообщения, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДопустимоеРазмещениеАналитикСтатьи(ДопустимоеРазмещение, Размещение)
	
	Использование = Перечисления.ИспользованиеАналитик;
	Аналитики = Справочники.АналитикиОперативногоПланирования;
	
	// СтатьяБюджета
	РазмещениеСтатьи = Размещение[Аналитики.СтатьяБюджета];
	МассивИспользования = Новый Массив;
	
	Если РазмещениеСтатьи = Использование.НеИспользуется Тогда
		МассивИспользования.Добавить(Использование.НеИспользуется);
	ИначеЕсли РазмещениеСтатьи = Использование.КлючеваяАналитика Тогда
		МассивИспользования.Добавить(Использование.КлючеваяАналитика);
	ИначеЕсли РазмещениеСтатьи = Использование.Строка Тогда
		МассивИспользования.Добавить(Использование.Строка);
	ИначеЕсли РазмещениеСтатьи = Использование.Уточняется Тогда
		МассивИспользования.Добавить(Использование.Уточняется);
	ИначеЕсли РазмещениеСтатьи = неопределено Тогда
		МассивИспользования.Добавить(РазмещениеСтатьи);
	КонецЕсли; 
	
	Возврат МассивИспользования;
	
КонецФункции

Функция ДопустимоеРазмещениеДоговораКонтрагента(ДопустимоеРазмещение, Размещение)
	
	Использование = Перечисления.ИспользованиеАналитик;
	Аналитики = Справочники.АналитикиОперативногоПланирования;
	
	МассивИспользования = Новый Массив;
	
	// Договор контрагента
	РазмещениеДоговора = Размещение[Аналитики.ДоговорКонтрагента];
	Если РазмещениеДоговора = Использование.НеИспользуется Тогда
		МассивИспользования.Добавить(Использование.НеИспользуется);
		Возврат МассивИспользования;
	КонецЕсли;
	
	// Контрагент
	РазмещениеКонтрагента = Размещение[Аналитики.Контрагент];
	Если РазмещениеКонтрагента = Использование.НеИспользуется Тогда
		МассивИспользования.Добавить(Использование.НеИспользуется);
		Возврат МассивИспользования;
	КонецЕсли;
	
	// Организация
	РазмещениеОрганизации = Размещение[Аналитики.Организация];
	
	РазмещениеКО = ПолучитьМинимальноеРазмещение(РазмещениеОрганизации, РазмещениеКонтрагента);
	Если РазмещениеКО = Использование.НеИспользуется Тогда
		МассивИспользования.Добавить(Использование.НеИспользуется);
	ИначеЕсли РазмещениеКО = Использование.КлючеваяАналитика Тогда
		МассивИспользования.Добавить(Использование.КлючеваяАналитика);
		МассивИспользования.Добавить(Использование.Строка);
		МассивИспользования.Добавить(Использование.Уточняется);
	ИначеЕсли РазмещениеКО = Использование.Строка Тогда
		МассивИспользования.Добавить(Использование.Строка);
		МассивИспользования.Добавить(Использование.Уточняется);
	ИначеЕсли РазмещениеКО = Использование.Уточняется Тогда
		МассивИспользования.Добавить(Использование.Уточняется);
	ИначеЕсли НЕ ЗначениеЗаполнено(РазмещениеКО) Тогда
		МассивИспользования.Добавить(РазмещениеКО);
	КонецЕсли; 
	
	Возврат МассивИспользования;
	
КонецФункции

Функция ДопустимоеРазмещениеПриходРасход(ДопустимоеРазмещение, Размещение)
	
	Использование = Перечисления.ИспользованиеАналитик;
	Аналитики = Справочники.АналитикиОперативногоПланирования;
	
	// СтатьяБюджета
	РазмещениеСтатьи = Размещение[Аналитики.СтатьяБюджета];
	МассивИспользования = Новый Массив;
	
	Если РазмещениеСтатьи = Использование.НеИспользуется Тогда
		МассивИспользования.Добавить(Использование.НеИспользуется);
	ИначеЕсли РазмещениеСтатьи = Использование.КлючеваяАналитика Тогда
		МассивИспользования.Добавить(Использование.КлючеваяАналитика);
	ИначеЕсли РазмещениеСтатьи = Использование.Строка Тогда
		МассивИспользования.Добавить(Использование.КлючеваяАналитика);
		МассивИспользования.Добавить(Использование.Строка);
	ИначеЕсли РазмещениеСтатьи = Использование.Уточняется Тогда
		МассивИспользования.Добавить(Использование.КлючеваяАналитика);
		МассивИспользования.Добавить(Использование.Строка);
		МассивИспользования.Добавить(Использование.Уточняется);
	ИначеЕсли РазмещениеСтатьи = неопределено Тогда
		МассивИспользования.Добавить(РазмещениеСтатьи);
	КонецЕсли; 
	
	Возврат МассивИспользования;
	
КонецФункции

Функция ПолучитьМинимальноеРазмещение(РазмещениеОрганизации, РазмещениеКонтрагента)
	
	Использование = Перечисления.ИспользованиеАналитик;
	
	Если НЕ ЗначениеЗаполнено(РазмещениеОрганизации)
		ИЛИ РазмещениеОрганизации = Использование.НеИспользуется Тогда
		Возврат РазмещениеКонтрагента;
	ИначеЕсли НЕ ЗначениеЗаполнено(РазмещениеКонтрагента)
		ИЛИ РазмещениеОрганизации = Использование.НеИспользуется Тогда
		Возврат РазмещениеОрганизации;
	ИначеЕсли РазмещениеОрганизации = РазмещениеКонтрагента Тогда
		Возврат РазмещениеОрганизации;
	ИначеЕсли РазмещениеОрганизации = Использование.КлючеваяАналитика Тогда
		Возврат РазмещениеКонтрагента;
	ИначеЕсли РазмещениеКонтрагента = Использование.КлючеваяАналитика Тогда
		Возврат РазмещениеОрганизации;
	ИначеЕсли РазмещениеОрганизации = Использование.Строка Тогда
		Возврат РазмещениеКонтрагента;
	ИначеЕсли РазмещениеКонтрагента = Использование.Строка Тогда
		Возврат РазмещениеОрганизации;
	КонецЕсли;	
	
	Если РазмещениеКонтрагента = Использование.НеИспользуется Тогда
		ВызватьИсключение НСтр("ru='Для использования аналитики ""Договор"" обязательно должна быть использована аналитика ""Контрагент"".
			|В колонке ""Использование"" для аналитики ""Контрагент"" установить значение отличное от ""Не используется"" и повторите запись настройки.'");
	КонецЕсли;
	
	ВызватьИсключение НСтр("ru='Ошибка определения минимального размещения аналитик Контрагент и Договор'");
	
КонецФункции

Процедура ДобавитьДопустимоеРазмещение(ДопустимоеРазмещение, Аналитика, МассивИспользования)
	
	ДопустимоеРазмещение.Добавить(
		Новый Структура("Аналитика, Использование", Аналитика, МассивИспользования));
	
	КонецПроцедуры
	
#КонецОбласти

#КонецЕсли
