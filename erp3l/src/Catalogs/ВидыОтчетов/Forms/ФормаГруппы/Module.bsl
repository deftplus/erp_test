&НаСервере
Процедура ОбновитьРегистрБухгалтерии()
	
	МассивРегистров=УправлениеОтчетамиУХ.ПолучитьМассивРегистровБухгалтерии(Объект.ПланСчетов);	
	Элементы.РегистрБухгалтерии.СписокВыбора.Очистить();
	
	Для Каждого Регистр ИЗ МассивРегистров Цикл
		
		Элементы.РегистрБухгалтерии.СписокВыбора.Добавить(Регистр);
		
	КонецЦикла;
	
	Если Элементы.РегистрБухгалтерии.СписокВыбора.Количество()>0 Тогда
		
		Если НЕ Объект.РегистрБухгалтерии=Элементы.РегистрБухгалтерии.СписокВыбора[0].Значение Тогда	
			
			Объект.РегистрБухгалтерии=Элементы.РегистрБухгалтерии.СписокВыбора[0].Значение;
			
		КонецЕсли;
		
	КонецЕсли;
			
КонецПроцедуры // ОбновитьРегистрБухгалтерии()

&НаСервереБезКонтекста
Функция СформироватьКодПоНаименованию(Наименование)
	
	Возврат ОбщегоНазначенияУХ.СформироватьКодНаОснованииНаименования(Наименование,,"ВидыОтчетов");
	
КонецФункции // СформироватьКодПоНаименованию() 

&НаКлиенте
Процедура ПланСчетовПриИзменении(Элемент)
	
	ОбновитьРегистрБухгалтерии();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость()
	
	Элементы.ГруппаПланСчетов.Видимость		= (Не Объект.Предназначение=Перечисления.ПредназначенияЭлементовСтруктурыОтчета.ПроизвольныеДанные);		
	Элементы.РегистрБухгалтерии.Видимость	= (НЕ Элементы.РегистрБухгалтерии.СписокВыбора.Количество()=1);
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииПредназначения()
	
	Если НЕ Объект.Предназначение=Перечисления.ПредназначенияЭлементовСтруктурыОтчета.ПроизвольныеДанные Тогда
		
		Если НЕ ЗначениеЗаполнено(Объект.ПланСчетов) Тогда
			
			ПланыСчетов=Справочники.ПланыСчетовБД.Выбрать(,Справочники.ТипыБазДанных.ТекущаяИБ);
			
			Если ПланыСчетов.Следующий() Тогда
				
				Объект.ПланСчетов=ПланыСчетов.Ссылка;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Справочники.РегистрыБухгалтерииБД.ПроверитьНаличиеОписанийРегистров(,Объект.ПланСчетов,Объект.РегистрБухгалтерии);
		ОбновитьРегистрБухгалтерии();
		
	КонецЕсли;
	
	УстановитьВидимость();
		
КонецПроцедуры // ПриИзмененииПредназначения() 

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() И (Не ЗначениеЗаполнено(Параметры.ЗначениеКопирования)) Тогда
		
		Справочники.ВидыОтчетов.ЗаполнитьРеквизитыПоРодителю(Объект);
			
	КонецЕсли;
	
	Если Не Объект.Предназначение=Перечисления.ПредназначенияЭлементовСтруктурыОтчета.ПроизвольныеДанные Тогда
		ОбновитьРегистрБухгалтерии();
	КонецЕсли;
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредназначениеПриИзменении(Элемент)
	
	ПриИзмененииПредназначения();
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	// Сформируем код.
	Если НЕ ЗначениеЗаполнено(Объект.Код) Тогда
		Объект.Код = СформироватьКодПоНаименованию(Объект.Наименование);
	Иначе
		// Код уже заполнен. Не изменяем существующее значение.
	КонецЕсли;
КонецПроцедуры
