
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Источник.Пустая() Тогда
		Отказ = истина;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	КОЛИЧЕСТВО(КолонкиОтчетов.Ссылка) КАК Кол
	|ИЗ
	|	Справочник.КолонкиОтчетов КАК КолонкиОтчетов
	|ГДЕ
	|	КолонкиОтчетов.Владелец = &ОбъектКопирования
	|	И НЕ КолонкиОтчетов.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(СтрокиОтчетов.Ссылка)
	|ИЗ
	|	Справочник.СтрокиОтчетов КАК СтрокиОтчетов
	|ГДЕ
	|	СтрокиОтчетов.Владелец = &ОбъектКопирования
	|	И НЕ СтрокиОтчетов.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(ПоказателиОтчетов.Ссылка)
	|ИЗ
	|	Справочник.ПоказателиОтчетов КАК ПоказателиОтчетов
	|ГДЕ
	|	ПоказателиОтчетов.Владелец = &ОбъектКопирования
	|	И НЕ ПоказателиОтчетов.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(ГруппыРаскрытия.Ссылка)
	|ИЗ
	|	Справочник.ГруппыРаскрытия КАК ГруппыРаскрытия
	|ГДЕ
	|	ГруппыРаскрытия.Владелец = &ОбъектКопирования
	|	И НЕ ГруппыРаскрытия.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(БланкиОтчетов.Ссылка)
	|ИЗ
	|	Справочник.БланкиОтчетов КАК БланкиОтчетов
	|ГДЕ
	|	БланкиОтчетов.Владелец = &ОбъектКопирования
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(ПравилаОбработки.Ссылка)
	|ИЗ
	|	Справочник.ПравилаОбработки КАК ПравилаОбработки
	|ГДЕ
	|	ПравилаОбработки.Владелец = &ОбъектКопирования
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(ПравилаПроверки.Ссылка)
	|ИЗ
	|	Справочник.ПравилаПроверки КАК ПравилаПроверки
	|ГДЕ
	|	ПравилаПроверки.Владелец = &ОбъектКопирования");
	
	Запрос.УстановитьПараметр("ОбъектКопирования", Параметры.Источник);
	
	Количества = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.Прямой);
	
	КоличествоКолонок     = Количества.Получить(0).Получить(0);
	КоличествоСтрок       = Количества.Получить(1).Получить(0);
	КоличествоПоказателей = Количества.Получить(2).Получить(0);
	
	ДоступностьСтрок   = КоличествоСтрок <> 0;
	ДоступностьКолонок = КоличествоКолонок <> 0;
	ЕстьПоказателиОтчета = КоличествоПоказателей <> 0;
	
	СписокПараметровКопирования.Добавить("ГруппыРаскрытия", Нстр("ru = 'Группы аналитик'") + "(" + Формат( Количества.Получить(3).Получить(0), "ЧЦ=3; ЧН=0") + ")", Истина);
	СписокПараметровКопирования.Добавить("КолонкиОтчетов", Нстр("ru = 'Колонки'") + "(" + Формат(КоличествоКолонок, "ЧЦ=3; ЧН=0") + ")", Истина);
	СписокПараметровКопирования.Добавить("СтрокиОтчетов", Нстр("ru = 'Строки'") + "(" + Формат(КоличествоСтрок, "ЧЦ=3; ЧН=0") + ")", Истина);
	СписокПараметровКопирования.Добавить("ПоказателиОтчетов", Нстр("ru = 'Показатели'") + "(" + Формат(КоличествоПоказателей, "ЧЦ=3; ЧН=0") + ")", Истина);
	СписокПараметровКопирования.Добавить("БланкиОтчетов", Нстр("ru = 'Бланки отчетов'") + "(" + Формат(Количества.Получить(4).Получить(0), "ЧЦ=3; ЧН=0") + ")", Истина);
	СписокПараметровКопирования.Добавить("ПравилаОбработки", Нстр("ru = 'Правила обработки'") + "(" + Формат(Количества.Получить(5).Получить(0), "ЧЦ=3; ЧН=0") + ")", Истина);
	СписокПараметровКопирования.Добавить("ПравилаПроверки", Нстр("ru = 'Правила проверки'") + "(" + Формат(Количества.Получить(6).Получить(0), "ЧЦ=3; ЧН=0") + ")", Истина);
	
	ТекстСтрока     = "СтрокиОтчетов";
	ТекстКолонка    = "КолонкиОтчетов";
	ТекстПоказатели = "ПоказателиОтчетов";
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьПоказателей()
	
	ДоступностьПоказателей = ЕстьПоказателиОтчета
						   И СписокПараметровКопирования.НайтиПоЗначению(ТекстСтрока).Пометка
						   И СписокПараметровКопирования.НайтиПоЗначению(ТекстКолонка).Пометка;
	СтрокаПоказатель = СписокПараметровКопирования.НайтиПоЗначению(ТекстПоказатели);
	Если СтрокаПоказатель.Пометка И НЕ ДоступностьПоказателей Тогда
		СтрокаПоказатель.Пометка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте

Процедура СписокПараметровКопированияПометкаПриИзменении(Элемент)
	
	ТекСтрока = СписокПараметровКопирования.НайтиПоИдентификатору(Элементы.СписокПараметровКопирования.ТекущаяСтрока);
	Если ТекСтрока.Значение = ТекстСтрока ИЛИ ТекСтрока.Значение = ТекстКолонка Тогда
		УстановитьДоступностьПоказателей();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступностьПоказателей();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЭлементВСтруктуруОтвета(МассивОтвета,ИмяЭлемента)
	
	ЭлементСписка=СписокПараметровКопирования.НайтиПоЗначению(ИмяЭлемента);
	
	Если ЭлементСписка.Пометка Тогда
		
		МассивОтвета.Добавить(ИмяЭлемента);
		
	КонецЕсли;
		
КонецПроцедуры // ДобавитьЭлементВСтруктуру()


&НаКлиенте
Процедура ОК(Команда)
	
	МассивОтвета=Новый Массив;
	
	ДобавитьЭлементВСтруктуруОтвета(МассивОтвета,"ГруппыРаскрытия");
	ДобавитьЭлементВСтруктуруОтвета(МассивОтвета,"КолонкиОтчетов");
	ДобавитьЭлементВСтруктуруОтвета(МассивОтвета,"СтрокиОтчетов");
	ДобавитьЭлементВСтруктуруОтвета(МассивОтвета,"ПоказателиОтчетов");
	ДобавитьЭлементВСтруктуруОтвета(МассивОтвета,"БланкиОтчетов");
	ДобавитьЭлементВСтруктуруОтвета(МассивОтвета,"ПравилаОбработки");
	ДобавитьЭлементВСтруктуруОтвета(МассивОтвета,"ПравилаПроверки");
	
	Закрыть(?(МассивОтвета.Количество()>0,Новый ФиксированныйМассив(МассивОтвета),Неопределено));
	
КонецПроцедуры
