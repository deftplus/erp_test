
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСтруктуруЗначенийПоУмолчанию(ПоказательОтчета)
	
	СтруктураЗначений=Новый Структура;
	
	Если ПоказательОтчета.ТипЗначения=Перечисления.ТипыЗначенийПоказателейОтчетов.Булево Тогда
		
		СтруктураЗначений.Вставить("КонтрольноеЗначение",Истина);
		
	Иначе
		
		СтруктураЗначений.Вставить("КонтрольноеЗначение",ОбщегоНазначенияУХ.ПолучитьПустоеЗначениеПоТипуПоказателя(ПоказательОтчета.ТипЗначения));
				
	КонецЕсли;
		
	СтруктураЗначений.Вставить("ПроверятьСтрокиАналитическогоРаскрытия",ЗначениеЗаполнено(ПоказательОтчета.ГруппаРаскрытия));
	СтруктураЗначений.Вставить("ПроверятьСинтетическиеЗначения",НЕ СтруктураЗначений.ПроверятьСтрокиАналитическогоРаскрытия);
	СтруктураЗначений.Вставить("ВидСравнения",Перечисления.ВидыСравнения.Равно);
		
	Возврат СтруктураЗначений;
				
КонецФункции // ПолучитьКонтрольноеЗначениеПоУмолчанию() 

&НаСервере 
Процедура ЗаполнитьКонтролируемыеПоказатели()
	
	Объект.КонтролируемыеПоказателиОтчетов.Очистить();
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	ПоказателиОтчетов.Ссылка КАК ПоказательОтчета,
	|	ПоказателиОтчетов.ГруппаРаскрытия,
	|	ПоказателиОтчетов.Описание,
	|	ПоказателиОтчетов.ПорядокУстранения,
	|	ПоказателиОтчетов.УровеньКритичности
	|ИЗ
	|	Справочник.ПоказателиОтчетов КАК ПоказателиОтчетов
	|ГДЕ
	|	ПоказателиОтчетов.Владелец = &Владелец
	|	И ПоказателиОтчетов.Проверочный";
	
	Запрос.УстановитьПараметр("Владелец",Объект.Владелец);
	
	Результат=Запрос.Выполнить().Выбрать();
	
	Пока Результат.Следующий() Цикл
		
		НоваяСтрока=Объект.КонтролируемыеПоказателиОтчетов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Результат);
		
		СтруктураРеквизитовПоУмолчанию=ПолучитьСтруктуруЗначенийПоУмолчанию(Результат.ПоказательОтчета);
		ЗаполнитьЗначенияСвойств(НоваяСтрока,СтруктураРеквизитовПоУмолчанию);
		
	КонецЦикла;
			
КонецПроцедуры // ЗаполнитьКонтролируемыеПоказатели()

&НаКлиенте
Процедура ПоказательОтчетаПриИзменении(Элемент)
		
	СтруктураЗначенийПоУмолчанию=ПолучитьСтруктуруЗначенийПоУмолчанию(Элементы.КонтролируемыеПоказателиОтчетов.ТекущиеДанные.ПоказательОтчета);	
	ЗаполнитьЗначенияСвойств(Элементы.КонтролируемыеПоказателиОтчетов.ТекущиеДанные,СтруктураЗначенийПоУмолчанию);
			
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если Объект.КонтролируемыеПоказателиОтчетов.Количество()>0 Тогда
		
		Оповещение = Новый ОписаниеОповещения("Подключаемый_ЗаполнитьКонтролируемыеПоказатели", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, Нстр("ru = 'Строки таблицы будут очищены. Продолжить?'"), РежимДиалогаВопрос.ДаНет);
		
        Возврат;
		
	КонецЕсли;
	
	ЗаполнитьКонтролируемыеПоказатели();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗаполнитьКонтролируемыеПоказатели(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда        
        Возврат;        
    КонецЕсли;
    
   	ЗаполнитьКонтролируемыеПоказатели();

КонецПроцедуры

