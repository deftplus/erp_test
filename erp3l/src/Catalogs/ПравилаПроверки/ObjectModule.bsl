Функция ПроизвестиСинтаксическийКонтрольМодуляШаблона(МодульДляПроверки, ОписаниеОшибки) Экспорт
	
	ДокНО = Документы.НастраиваемыйОтчет.СоздатьДокумент();
	Возврат ДокНО.ПроверитьМодуль(МодульДляпроверки, ОписаниеОшибки);
	
КонецФункции

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаШаблона = Нстр("ru = 'Правило проверки: ""%1"" не может быть записано:'");
	
	ШапкаОшибки = "";
	Если Не ПустаяСтрока(СтрокаШаблона) тогда		
		ШапкаОшибки = СтрШаблон(СтрокаШаблона, ЭтотОбъект);
	КонецЕсли;
		
	Если Не ЭтоГруппа Тогда
		ПроверитьВозможностьЗаписи(Отказ, Истина, ШапкаОшибки);
	КонецЕсли;
	
	Если ЭтоНовый() И (НЕ ЗначениеЗаполнено(ТипБД)) Тогда
		
		ТипБД=Справочники.ТипыБазДанных.ТекущаяИБ;
		
	КонецЕсли;
		
КонецПроцедуры

// Проверяет возможность записи объекта.
//
Процедура ПроверитьВозможностьЗаписи(Отказ, Сообщать = Ложь, ШапкаОшибки = "") Экспорт
	
	Отказ = Ложь;
	
	Если ЭтоНовый() ИЛИ (НЕ Константы.ПроверятьУтверждениеНастроекОтчетов.Получить()) Тогда
		Возврат;
	КонецЕсли;
	
	Если Ссылка.Утверждено Тогда
		
		Если Не Утверждено Тогда
			
			Отказ = УправлениеОтчетамиУХ.ПроверитьНаличиеДокументовПоЭлементуНастройки(Ссылка,"ПравилоПроверки");
			
			Если Отказ Тогда
				
				Если Сообщать Тогда
					ОбщегоНазначенияУХ.СообщитьОбОшибке(Нстр("ru = ' - нельзя снять флаг ""Утвержден"" у правила проверки, которое
					|используют утвержденные экземпляры отчетов.'"));
				КонецЕсли;
				
				Возврат;
			КонецЕсли;
			
		Иначе
			
			Отказ = Истина;
			
			Если Сообщать Тогда
				ОбщегоНазначенияУХ.СообщитьОбОшибке(Нстр("ru = ' - нельзя вносить изменения в утвержденное правило проверки.
				| Для редактирования снимите флаг ""Утвержден""'"));
			КонецЕсли;
			
			Возврат;
			
		КонецЕсли;
		
	Иначе
		
		Отказ = Ложь;
		Возврат;
		
	КонецЕсли;		
			
КонецПроцедуры
