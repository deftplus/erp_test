#Если НаСервере Тогда
	
Функция МаксимальныйПорядокГруппы(ГруппаСтадий) Экспорт
	Порядок = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(МАКСИМУМ(СтадииПроектов.Порядок), 0) КАК МаксПорядок
		|ИЗ
		|	Справочник.СтадииПроектов КАК СтадииПроектов
		|ГДЕ
		|	СтадииПроектов.Родитель = &Родитель";
	
	Запрос.УстановитьПараметр("Родитель", ГруппаСтадий);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Порядок = ВыборкаДетальныеЗаписи.МаксПорядок;
	КонецЕсли;

	Возврат Порядок;
КонецФункции

// Направление == -1 - перемещаем вверх (уменьшаем порядок)
// Направление == +1 - перемещаем вниз (увеличиваем порядок)
// Перемещение:
//	1. Ищем ближайший элемент в нужном направлении.
//	2. Обениваем значения реквизита Порядок между Ссылка и найденным элементом.
//	3. Если ближайший элемент не найден, то ничего не делаем.
//
Процедура ПереместитьСтадию(Ссылка, Направление) Экспорт
	
	ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СтадииПроектов.Ссылка КАК Ссылка,
		|	СтадииПроектов.Порядок КАК Порядок
		|ИЗ
		|	Справочник.СтадииПроектов КАК ИзменяемаяСтадия
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтадииПроектов КАК СтадииПроектов
		|		ПО ИзменяемаяСтадия.Родитель = СтадииПроектов.Родитель
		|			И (ИзменяемаяСтадия.Ссылка = &Ссылка)
		|			И (СтадииПроектов.Ссылка <> &Ссылка
		|				И НЕ СтадииПроектов.ЭтоГруппа)
		|			И (СтадииПроектов.Порядок > ИзменяемаяСтадия.Порядок)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порядок ВОЗР";
	
	Если Направление < 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СтадииПроектов.Порядок > ИзменяемаяСтадия.Порядок", "СтадииПроектов.Порядок < ИзменяемаяСтадия.Порядок");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Порядок ВОЗР", "Порядок УБЫВ");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	// меняем стадии местами
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		
		Если ВыборкаДетальныеЗаписи.Ссылка = Null Тогда
			Возврат;
		КонецЕсли;
		
		
		
		СтадияОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		Порядок_ = СтадияОбъект.Порядок;
		СтадияОбъект.ОбменДанными.Загрузка = Истина;
		СтадияОбъект.Порядок = Ссылка.Порядок;
		СтадияОбъект.Записать();
		
		СтадияОбъект = Ссылка.ПолучитьОбъект();
		СтадияОбъект.ОбменДанными.Загрузка = Истина;
		СтадияОбъект.Порядок = Порядок_;
		СтадияОбъект.Записать();
	КонецЕсли;
	
	
	
КонецПроцедуры

#КонецЕсли