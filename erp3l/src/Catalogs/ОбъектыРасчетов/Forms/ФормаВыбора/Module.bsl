
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если НЕ ОбновлениеИнформационнойБазы.ОтложенноеОбновлениеЗавершено() Тогда
		Текст = НСтр("ru = 'Список объектов расчетов может быть неполным или неточным, т.к. не завершен переход на новую версию.';
					|en = 'The AR/AP object list can be incomplete or inaccurate since the migration to the new version is not completed.'");
		ОбщегоНазначения.СообщитьПользователю(Текст);
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("ТипРасчетов") Тогда
		Если Параметры.Отбор.ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом Тогда
			Заголовок = НСтр("ru = 'Выбор объекта расчетов с клиентом';
							|en = 'Selection of AR/AP object for settlements with customer'");
		Иначе
			Заголовок = НСтр("ru = 'Выбор объекта расчетов с поставщиком';
							|en = 'Selection of AR/AP object for settlements with vendor'");
		КонецЕсли;
		СписокОснованийПлатежа.Параметры.УстановитьЗначениеПараметра("ТипРасчетов", Параметры.Отбор.ТипРасчетов);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				СписокОбъектовРасчетов,
				"ТипРасчетов",
				Параметры.Отбор.ТипРасчетов,
				ВидСравненияКомпоновкиДанных.ВСписке,
				,
				Истина);
	КонецЕсли;
	
	УстановленоОтборов = 0;
	Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Партнер") И ЗначениеЗаполнено(Параметры.Отбор.Партнер) Тогда
		СписокПартнеров = Новый СписокЗначений;
		ПартнерыИКонтрагенты.ЗаполнитьСписокПартнераСРодителями(Параметры.Отбор.Партнер, СписокПартнеров);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			СписокОбъектовРасчетов,
			"Партнер",
			СписокПартнеров,
			ВидСравненияКомпоновкиДанных.ВСписке,
			,
			ЗначениеЗаполнено(СписокПартнеров));
			
		Если Параметры.ВыборОснованияПлатежа Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				СписокОснованийПлатежа,
				"Партнер",
				СписокПартнеров,
				ВидСравненияКомпоновкиДанных.ВСписке,
				,
				ЗначениеЗаполнено(СписокПартнеров));
		КонецЕсли;
			
		Элементы.ОтборПартнер.Заголовок = Параметры.Отбор.Партнер;
		УстановленоОтборов = УстановленоОтборов + 1;
	Иначе
		Элементы.ОтборПартнер.Видимость = Ложь;
		Элементы.ЗаголовокПартнер.Видимость = Ложь;
	КонецЕсли;
	
	Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Контрагент") Тогда
		Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов") Тогда
			Если ТипЗнч(Параметры.Отбор.Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
				Если ЗначениеЗаполнено(Параметры.Отбор.Контрагент) Тогда
					ПартнерКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Отбор.Контрагент, "Партнер");
				Иначе
					ПартнерКонтрагента = Неопределено;
				КонецЕсли;
				СписокПартнеров = Новый СписокЗначений;
				ПартнерыИКонтрагенты.ЗаполнитьСписокПартнераСРодителями(ПартнерКонтрагента, СписокПартнеров);
				Элементы.ОтборПартнер.Заголовок = ПартнерКонтрагента;
				Элементы.ОтборПартнер.Видимость = Истина;
				Элементы.ЗаголовокПартнер.Видимость = Истина;
				Параметры.Отбор.Вставить("Партнер", СписокПартнеров);
			ИначеЕсли ТипЗнч(Параметры.Отбор.Контрагент) = Тип("СправочникСсылка.Организации") Тогда
				Параметры.Отбор.Вставить("Партнер", Справочники.Партнеры.НашеПредприятие);
				Элементы.ОтборПартнер.Заголовок = Справочники.Партнеры.НашеПредприятие;
				Элементы.ОтборПартнер.Видимость = Истина;
				Элементы.ЗаголовокПартнер.Видимость = Истина;
			КонецЕсли;
			Элементы.ОтборКонтрагент.Видимость = Ложь;
			Элементы.ЗаголовокКонтрагент.Видимость = Ложь;
		Иначе
			Элементы.ОтборКонтрагент.Заголовок = Параметры.Отбор.Контрагент;
			УстановленоОтборов = УстановленоОтборов + 1;
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			СписокОбъектовРасчетов,
			"Контрагент",
			Параметры.Отбор.Контрагент,
			ВидСравненияКомпоновкиДанных.ВСписке,
			,
			ЗначениеЗаполнено(Параметры.Отбор.Контрагент));
		
		Если Параметры.ВыборОснованияПлатежа Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				СписокОснованийПлатежа,
				"Контрагент",
				Параметры.Отбор.Контрагент,
				ВидСравненияКомпоновкиДанных.ВСписке,
				,
				ЗначениеЗаполнено(Параметры.Отбор.Контрагент));
		КонецЕсли;
		
	Иначе
		Элементы.ОтборКонтрагент.Видимость = Ложь;
		Элементы.ЗаголовокКонтрагент.Видимость = Ложь;
	КонецЕсли;
	
	Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Организация") Тогда
		
		СписокОбъектовРасчетов.Параметры.УстановитьЗначениеПараметра("ПоВсемОрганизациям", Ложь);
		СписокОснованийПлатежа.Параметры.УстановитьЗначениеПараметра("ПоВсемОрганизациям", Ложь);
		
		ЦентрализованныеДоговоры = Новый Массив;
		ГоловнаяОрганизация      = Неопределено;
		ЭтоГоловнаяОрганизация   = Ложь;
		
		Если ТипЗнч(Параметры.Отбор.Организация) <> Тип("Массив") И Параметры.УчитыватьФилиалы Тогда
			
			ГоловнаяОрганизация      = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Отбор.Организация, "ГоловнаяОрганизация");
			ЭтоГоловнаяОрганизация   = Параметры.Отбор.Организация = ГоловнаяОрганизация;
			
			Если НЕ ЭтоГоловнаяОрганизация Тогда
				
				Запрос = Новый Запрос(
					"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
					|	Филиалы.Ссылка КАК Ссылка
					|ИЗ
					|	Справочник.ДоговорыКонтрагентов.Филиалы КАК Филиалы
					|ГДЕ
					|	Филиалы.Организация = &Организация
					|");
				
				Запрос.УстановитьПараметр("Организация",Параметры.Отбор.Организация);
				ЦентрализованныеДоговоры = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
				
			Иначе
				
				ГоловнаяОрганизация = Неопределено;
				
			КонецЕсли;
			
		КонецЕсли;
		
		СписокОбъектовРасчетов.Параметры.УстановитьЗначениеПараметра("ЭтоГоловнаяОрганизация",   ЭтоГоловнаяОрганизация);
		СписокОбъектовРасчетов.Параметры.УстановитьЗначениеПараметра("ГоловнаяОрганизация",      ГоловнаяОрганизация);
		СписокОбъектовРасчетов.Параметры.УстановитьЗначениеПараметра("ЦентрализованныеДоговоры", ЦентрализованныеДоговоры);
		СписокОбъектовРасчетов.Параметры.УстановитьЗначениеПараметра("Организация",              Параметры.Отбор.Организация);
		
		УстановленоОтборов = УстановленоОтборов + 1;
		
		Элементы.ОтборОрганизация.Заголовок = Параметры.Отбор.Организация;
		
		Если Параметры.ВыборОснованияПлатежа Тогда
			СписокОснованийПлатежа.Параметры.УстановитьЗначениеПараметра("ЭтоГоловнаяОрганизация",   ЭтоГоловнаяОрганизация);
			СписокОснованийПлатежа.Параметры.УстановитьЗначениеПараметра("ГоловнаяОрганизация",      ГоловнаяОрганизация);
			СписокОснованийПлатежа.Параметры.УстановитьЗначениеПараметра("ЦентрализованныеДоговоры", ЦентрализованныеДоговоры);
			СписокОснованийПлатежа.Параметры.УстановитьЗначениеПараметра("Организация",              Параметры.Отбор.Организация);
		КонецЕсли;
		
	Иначе
		Элементы.ОтборОрганизация.Видимость = Ложь;
		Элементы.ЗаголовокОрганизация.Видимость = Ложь;
		
		СписокОбъектовРасчетов.Параметры.УстановитьЗначениеПараметра("ПоВсемОрганизациям", Истина);
		СписокОбъектовРасчетов.Параметры.УстановитьЗначениеПараметра("Организация", Неопределено);
		СписокОбъектовРасчетов.Параметры.УстановитьЗначениеПараметра("ЭтоГоловнаяОрганизация", Ложь);
		СписокОбъектовРасчетов.Параметры.УстановитьЗначениеПараметра("ЦентрализованныеДоговоры", Новый Массив);
		СписокОбъектовРасчетов.Параметры.УстановитьЗначениеПараметра("ГоловнаяОрганизация", Неопределено);
		
		СписокОснованийПлатежа.Параметры.УстановитьЗначениеПараметра("ПоВсемОрганизациям", Истина);
		СписокОснованийПлатежа.Параметры.УстановитьЗначениеПараметра("Организация", Неопределено);
		СписокОснованийПлатежа.Параметры.УстановитьЗначениеПараметра("ЭтоГоловнаяОрганизация", Ложь);
		СписокОснованийПлатежа.Параметры.УстановитьЗначениеПараметра("ЦентрализованныеДоговоры", Новый Массив);
		СписокОснованийПлатежа.Параметры.УстановитьЗначениеПараметра("ГоловнаяОрганизация", Неопределено);
		
	КонецЕсли;
	
	Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("ВалютаВзаиморасчетов") И ЗначениеЗаполнено(Параметры.Отбор.ВалютаВзаиморасчетов) Тогда
		Элементы.ОтборВалютаВзаиморасчетов.Заголовок = Параметры.Отбор.ВалютаВзаиморасчетов;
		УстановленоОтборов = УстановленоОтборов + 1;
	Иначе
		Элементы.ОтборВалютаВзаиморасчетов.Видимость = Ложь;
		Элементы.ЗаголовокВалютаВзаиморасчетов.Видимость = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.РедактируемыйДокумент) Тогда
		ИсключаемыйОбъектРасчетов = ОбъектыРасчетовСервер.ПолучитьОбъектРасчетовПоСсылке(Параметры.РедактируемыйДокумент,,
			?(Параметры.Отбор.Свойство("ТипРасчетов"),Параметры.Отбор.ТипРасчетов,Неопределено));
		Если ЗначениеЗаполнено(ИсключаемыйОбъектРасчетов) Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				СписокОбъектовРасчетов,
				"ОбъектРасчетов",
				ИсключаемыйОбъектРасчетов,
				ВидСравненияКомпоновкиДанных.НеРавно,
				,
				ЗначениеЗаполнено(ИсключаемыйОбъектРасчетов));
				
			Если Параметры.ВыборОснованияПлатежа Тогда
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
					СписокОснованийПлатежа,
					"ОбъектРасчетов",
					ИсключаемыйОбъектРасчетов,
					ВидСравненияКомпоновкиДанных.НеРавно,
					,
					ЗначениеЗаполнено(ИсключаемыйОбъектРасчетов));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ТипыОснований = Новый Массив;
	Если Параметры.ВыборОснованияПлатежа И Параметры.РедактируемыйДокумент <> Неопределено Тогда
		МетаданныеДокумента = Параметры.РедактируемыйДокумент.Метаданные();
		Если МетаданныеДокумента <> Неопределено Тогда
			Если МетаданныеДокумента.ТабличныеЧасти.Найти("РасшифровкаПлатежа") = Неопределено Тогда
				ОписаниеТипов = МетаданныеДокумента.Реквизиты.ОснованиеПлатежа.Тип;
			Иначе
				ОписаниеТипов = МетаданныеДокумента.ТабличныеЧасти.РасшифровкаПлатежа.Реквизиты.ОснованиеПлатежа.Тип;
			КонецЕсли;
			ТипыОснований = ОписаниеТипов.Типы();
		КонецЕсли;
	КонецЕсли;
	СписокОснованийПлатежа.Параметры.УстановитьЗначениеПараметра("ТипыОснований", ТипыОснований);
	
	Если Параметры.ВыборОснованияПлатежа Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОснованиеПлатежа;
		Если Параметры.Свойство("ТекущаяСтрока") Тогда
			НачальноеОснованиеПлатежа = Параметры.ТекущаяСтрока;
		КонецЕсли;
	Иначе
		Элементы.ГруппаОснованиеПлатежа.Видимость = Ложь;
		Элементы.ГруппаСтраницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	КонецЕсли;
	
	Если УстановленоОтборов > 0 Тогда
		Элементы.ГруппаПредустановленныеОтборы.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Установлено отборов: %1';
																														|en = 'Active filters: %1'"), УстановленоОтборов);
	Иначе
		Элементы.ГруппаПредустановленныеОтборы.Видимость = Ложь;
	КонецЕсли;
	
	СписокОснованийПлатежа.Параметры.УстановитьЗначениеПараметра("ПодборДебиторскойЗадолженности", 
		?(Параметры.Свойство("ПодборДебиторскойЗадолженности"), Параметры.ПодборДебиторскойЗадолженности, Истина));
	
	//++ Локализация
	СписокОснованийПлатежа.ТекстЗапроса = СписокОснованийПлатежа.ТекстЗапроса + ТекстЗапросаОснованийПлатежаСФ();
	//-- Локализация
	
	ИндексТипаЗаказ = ТипыОснований.Найти(Тип("ДокументСсылка.ЗаказКлиента"));
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРасширенныеВозможностиЗаказаКлиента") 
		И ИндексТипаЗаказ <> Неопределено Тогда
		// Заказы клиентов будут добавлены отдельным запросом как счета на оплату 
		// и в основном запросе их необходимо исключить, чтобы не было дублей
		СписокОснованийПлатежа.ТекстЗапроса = СписокОснованийПлатежа.ТекстЗапроса + ТекстЗапросаЗаказы();
		ТипыОснований.Удалить(ИндексТипаЗаказ);
	КонецЕсли;
	
	Если Параметры.Свойство("ПодборДебиторскойЗадолженности") Тогда
		Элементы.ЗаголовокТипЗадолженности.Видимость = Истина;
		Элементы.ОтборЗадолженность.Заголовок = ?(Параметры.ПодборДебиторскойЗадолженности, НСтр("ru = 'дебиторской';
																								|en = 'Receivable'"), НСтр("ru = 'кредиторской';
																															|en = 'payable'"));
		СписокОбъектовРасчетов.Параметры.УстановитьЗначениеПараметра("ПодборДебиторскойЗадолженности", Параметры.ПодборДебиторскойЗадолженности);
	Иначе
		Элементы.ЗаголовокТипЗадолженности.Видимость = Ложь;
		Элементы.ОтборЗадолженность.Видимость = Ложь;
		СписокОбъектовРасчетов.Параметры.УстановитьЗначениеПараметра("ПодборДебиторскойЗадолженности", Истина);
	КонецЕсли;
	
	Если Параметры.Свойство("ВернутьСтруктуру") Тогда
		ВернутьСтруктуру = Параметры.ВернутьСтруктуру;
	КонецЕсли;
	
	ИспользоватьНесколькоВалют     = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВалют");

	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОбъектыРасчетовВалюта", "Видимость", ИспользоватьНесколькоВалют);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОснованияПлатежаВалюта", "Видимость", ИспользоватьНесколькоВалют);
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОбъект(Команда)
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОбъектРасчетов Тогда
		СтрокаТаблицы = Элементы.ОбъектыРасчетов.ТекущиеДанные;
		Если СтрокаТаблицы <> Неопределено И ЗначениеЗаполнено(СтрокаТаблицы.ОбъектРасчетов) Тогда
			ПоказатьЗначение(Неопределено, СтрокаТаблицы.ОбъектРасчетов);
		КонецЕсли;
	Иначе
		СтрокаТаблицы = Элементы.ОснованияПлатежа.ТекущиеДанные;
		Если СтрокаТаблицы <> Неопределено И ЗначениеЗаполнено(СтрокаТаблицы.ОснованиеПлатежа) Тогда
			ПоказатьЗначение(Неопределено, СтрокаТаблицы.ОснованиеПлатежа);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТолькоСОстаткомПриИзменении(Элемент)
	УстановитьОтборПоОстатку();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьОтборПоОстатку();
	Если ЗначениеЗаполнено(НачальноеОснованиеПлатежа) Тогда
		Элементы.ОснованияПлатежа.ТекущаяСтрока = НачальноеОснованиеПлатежа;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьОбъектыРасчетовПриИзменении(Элемент)
	УстановитьОтборПоОстатку();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборПоОстатку()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		СписокОбъектовРасчетов,
		"СуммаОстаток",
		0,
		ВидСравненияКомпоновкиДанных.Больше,
		,
		ТолькоСОстатком);
	
	СписокОснованийПлатежа.Параметры.УстановитьЗначениеПараметра("ОтображатьОбъектыРасчетов", ОтображатьОбъектыРасчетов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьДокумент(Команда)
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОбъектРасчетов Тогда
		СтрокаТаблицы = Элементы.ОбъектыРасчетов.ТекущиеДанные;
	Иначе
		СтрокаТаблицы = Элементы.ОснованияПлатежа.ТекущиеДанные;
	КонецЕсли;
	
	Если СтрокаТаблицы <> Неопределено Тогда
		Если ВернутьСтруктуру Тогда
			РезультатВыбора = РезультатВыбора(СтрокаТаблицы);
		Иначе
			РезультатВыбора = СтрокаТаблицы.ОбъектРасчетов;
		КонецЕсли;
		ОповеститьОВыборе(РезультатВыбора);
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция РезультатВыбора(СтрокаТаблицы)
	
	РезультатВыбора = Новый Структура;
	РезультатВыбора.Вставить("ОбъектРасчетов", СтрокаТаблицы.ОбъектРасчетов);
	РезультатВыбора.Вставить("ОснованиеПлатежа", ?(СтрокаТаблицы.Свойство("ОснованиеПлатежа"), СтрокаТаблицы.ОснованиеПлатежа, Неопределено));
	РезультатВыбора.Вставить("Организация", СтрокаТаблицы.Организация);
	РезультатВыбора.Вставить("Партнер", СтрокаТаблицы.Партнер);
	РезультатВыбора.Вставить("Контрагент", СтрокаТаблицы.Контрагент);
	РезультатВыбора.Вставить("Договор", СтрокаТаблицы.Договор);
	РезультатВыбора.Вставить("НаправлениеДеятельности", СтрокаТаблицы.НаправлениеДеятельности);
	РезультатВыбора.Вставить("ВалютаВзаиморасчетов", СтрокаТаблицы.ВалютаВзаиморасчетов);
	РезультатВыбора.Вставить("СуммаВзаиморасчетов", 0);
	РезультатВыбора.Вставить("Дата", СтрокаТаблицы.Дата);
	РезультатВыбора.Вставить("Номер", СтрокаТаблицы.Номер);
	
	
	РезультатВыбора.Вставить("СтавкаНДС", ПредопределенноеЗначение("Справочник.СтавкиНДС.ПустаяСсылка"));
	РезультатВыбора.Вставить("СуммаНДС", 0);
	РезультатВыбора.Вставить("СуммаЗаказа", СтрокаТаблицы.Сумма);
	
	РезультатВыбора.Вставить("СуммаПлатежа", СуммаПлатежа);
	РезультатВыбора.Вставить("ЭтоРасчетыСКлиентами", ЭтоРасчетыСКлиентами);
	
	Возврат РезультатВыбора;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбъектыРасчетовНомер.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбъектыРасчетовДата.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбъектыРасчетовТипСсылки.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбъектыРасчетовСумма.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбъектыРасчетовВалюта.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбъектыРасчетовСуммаОстаток.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбъектыРасчетовВалютаВзаиморасчетов.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбъектыРасчетовОрганизация.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбъектыРасчетовПартнер.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбъектыРасчетовДоговор.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбъектыРасчетовКонтрагент.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбъектыРасчетовНомерВходящегоДокумента.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбъектыРасчетовДатаВходящегоДокумента.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СсылкаНаОбъектРасчетов.Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокОбъектовРасчетов.Состояние");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = 1;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокОбъектовРасчетов.Договор");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("СписокОбъектовРасчетов.Объект");
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбъектыРасчетовТипСсылки.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокОбъектовРасчетов.ТипСсылки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;

	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Объект не указан>';
																|en = '<Object is not specified>'"));

КонецПроцедуры

&НаКлиенте
Процедура ОбъектыРасчетовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтрокаТаблицы = Элементы.ОбъектыРасчетов.ТекущиеДанные;
	Если ВернутьСтруктуру Тогда
		РезультатВыбора = РезультатВыбора(СтрокаТаблицы);
	Иначе
		РезультатВыбора = СтрокаТаблицы.ОбъектРасчетов;
	КонецЕсли;
	ОповеститьОВыборе(РезультатВыбора);
КонецПроцедуры

&НаКлиенте
Процедура ОснованияПлатежаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтрокаТаблицы = Элементы.ОснованияПлатежа.ТекущиеДанные;
	РезультатВыбора = РезультатВыбора(СтрокаТаблицы);
	ОповеститьОВыборе(РезультатВыбора);
КонецПроцедуры

//++ Локализация

&НаСервере
Функция ТекстЗапросаОснованийПлатежаСФ()
	
	Возврат "
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураВыданный.Ссылка                      КАК ОснованиеПлатежа,
	|	ОбъектыРасчетов.Ссылка                          КАК Ссылка,
	|	ОбъектыРасчетов.Ссылка                          КАК ОбъектРасчетов,
	|	ОбъектыРасчетов.Партнер                         КАК Партнер,
	|	ОбъектыРасчетов.Организация                     КАК Организация,
	|	ОбъектыРасчетов.Контрагент                      КАК Контрагент,
	|	ОбъектыРасчетов.Договор                         КАК Договор,
	|	ОбъектыРасчетов.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	ОбъектыРасчетов.ВалютаВзаиморасчетов            КАК ВалютаВзаиморасчетов,
	|	ОбъектыРасчетов.ИдентификаторПлатежа            КАК ИдентификаторПлатежа,
	|	СчетФактураВыданный.Номер                       КАК Номер,
	|	СчетФактураВыданный.Дата                        КАК Дата,
	|	СчетФактураВыданный.Дата                        КАК ДатаВходящегоДокумента,
	|	СчетФактураВыданный.Номер                       КАК НомерВходящегоДокумента,
	|	ОбъектыРасчетов.НаименованиеПервичногоДокумента КАК НаименованиеПервичногоДокумента,
	|	ОбъектыРасчетов.Сумма                           КАК Сумма,
	|	СчетФактураВыданный.Валюта                      КАК Валюта,
	|	ВЫБОР 
	|		КОГДА СчетФактураВыданный.Проведен
	|			ТОГДА 2
	|			ИНАЧЕ 0
	|	КОНЕЦ                                           КАК Состояние
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ПО СчетФактураВыданный.ДокументОснование = ОбъектыРасчетов.Объект
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК Договоры
	|		ПО (ОбъектыРасчетов.Договор = Договоры.Ссылка)
	|ГДЕ
	|	&ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|	И ОбъектыРасчетов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|	И НЕ СчетФактураВыданный.ПометкаУдаления
	|	И (ОбъектыРасчетов.Организация В (&Организация)
	|			ИЛИ НЕ &ЭтоГоловнаяОрганизация
	|				И ОбъектыРасчетов.Договор В (&ЦентрализованныеДоговоры)
	|				И (ОбъектыРасчетов.Организация = &ГоловнаяОрганизация
	|					ИЛИ ЕСТЬNULL(Договоры.РазрешаетсяПередачаОплатМеждуФилиалами, ЛОЖЬ))
	|			ИЛИ &ЭтоГоловнаяОрганизация
	|				И ОбъектыРасчетов.Организация.ГоловнаяОрганизация В (&Организация)
	|				И ОбъектыРасчетов.Организация.ДопускаютсяВзаиморасчетыЧерезГоловнуюОрганизацию
	|			ИЛИ &ПоВсемОрганизациям)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетФактураВыданный.Ссылка                      КАК ОснованиеПлатежа,
	|	ОбъектыРасчетов.Ссылка                          КАК Ссылка,
	|	ОбъектыРасчетов.Ссылка                          КАК ОбъектРасчетов,
	|	ОбъектыРасчетов.Партнер                         КАК Партнер,
	|	ОбъектыРасчетов.Организация                     КАК Организация,
	|	ОбъектыРасчетов.Контрагент                      КАК Контрагент,
	|	ОбъектыРасчетов.Договор                         КАК Договор,
	|	ОбъектыРасчетов.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	ОбъектыРасчетов.ВалютаВзаиморасчетов            КАК ВалютаВзаиморасчетов,
	|	ОбъектыРасчетов.ИдентификаторПлатежа            КАК ИдентификаторПлатежа,
	|	СчетФактураВыданный.Номер                       КАК Номер,
	|	СчетФактураВыданный.Дата                        КАК Дата,
	|	СчетФактураВыданный.Дата                        КАК ДатаВходящегоДокумента,
	|	СчетФактураВыданный.Номер                       КАК НомерВходящегоДокумента,
	|	ОбъектыРасчетов.НаименованиеПервичногоДокумента КАК НаименованиеПервичногоДокумента,
	|	ОбъектыРасчетов.Сумма                           КАК Сумма,
	|	СчетФактураВыданный.Валюта                      КАК Валюта,
	|	ВЫБОР 
	|		КОГДА СчетФактураВыданный.Проведен
	|			ТОГДА 2
	|			ИНАЧЕ 0
	|	КОНЕЦ                                           КАК Состояние
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ПО СчетФактураВыданный.Договор = ОбъектыРасчетов.Объект
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК Договоры
	|		ПО (ОбъектыРасчетов.Договор = Договоры.Ссылка)
	|ГДЕ
	|	&ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|	И ОбъектыРасчетов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|	И НЕ СчетФактураВыданный.ПометкаУдаления
	|	И (ОбъектыРасчетов.Организация В (&Организация)
	|			ИЛИ НЕ &ЭтоГоловнаяОрганизация
	|				И ОбъектыРасчетов.Договор В (&ЦентрализованныеДоговоры)
	|				И (ОбъектыРасчетов.Организация = &ГоловнаяОрганизация
	|					ИЛИ ЕСТЬNULL(Договоры.РазрешаетсяПередачаОплатМеждуФилиалами, ЛОЖЬ))
	|			ИЛИ &ЭтоГоловнаяОрганизация
	|				И ОбъектыРасчетов.Организация.ГоловнаяОрганизация В (&Организация)
	|				И ОбъектыРасчетов.Организация.ДопускаютсяВзаиморасчетыЧерезГоловнуюОрганизацию
	|			ИЛИ &ПоВсемОрганизациям)
	|";
	
КонецФункции

//-- Локализация

&НаСервере
Функция ТекстЗапросаЗаказы()
	
	Возврат "
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Заказ.Ссылка,
	|	ОбъектыРасчетов.Ссылка,
	|	ОбъектыРасчетов.Ссылка,
	|	ОбъектыРасчетов.Партнер,
	|	ОбъектыРасчетов.Организация,
	|	ОбъектыРасчетов.Контрагент,
	|	ОбъектыРасчетов.Договор,
	|	ОбъектыРасчетов.НаправлениеДеятельности,
	|	ОбъектыРасчетов.ВалютаВзаиморасчетов,
	|	ОбъектыРасчетов.ИдентификаторПлатежа,
	|	РеестрДокументов.НомерДокументаИБ,
	|	РеестрДокументов.ДатаДокументаИБ,
	|	РеестрДокументов.ДатаПервичногоДокумента,
	|	РеестрДокументов.НомерПервичногоДокумента,
	|	РеестрДокументов.НаименованиеПервичногоДокумента,
	|	РеестрДокументов.Сумма,
	|	РеестрДокументов.Валюта,
	|	ВЫБОР
	|		КОГДА Заказ.Проведен
	|			ТОГДА 2
	|		ИНАЧЕ 0
	|	КОНЕЦ
	|ИЗ
	|	Документ.ЗаказКлиента КАК Заказ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ПО (ВЫБОР
	|				КОГДА Заказ.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов)
	|					ТОГДА Заказ.Договор
	|				ИНАЧЕ Заказ.Ссылка
	|			КОНЕЦ = ОбъектыРасчетов.Объект)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК Договоры
	|		ПО (ОбъектыРасчетов.Договор = Договоры.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументов
	|		ПО (РеестрДокументов.Ссылка = Заказ.Ссылка)
	|ГДЕ
	|	НЕ Заказ.ПометкаУдаления
	|	И (ОбъектыРасчетов.Организация В (&Организация)
	|			ИЛИ НЕ &ЭтоГоловнаяОрганизация
	|				И ОбъектыРасчетов.Договор В (&ЦентрализованныеДоговоры)
	|				И (ОбъектыРасчетов.Организация = &ГоловнаяОрганизация
	|					ИЛИ ЕСТЬNULL(Договоры.РазрешаетсяПередачаОплатМеждуФилиалами, ЛОЖЬ))
	|			ИЛИ &ЭтоГоловнаяОрганизация
	|				И ОбъектыРасчетов.Организация.ГоловнаяОрганизация В (&Организация)
	|				И ОбъектыРасчетов.Организация.ДопускаютсяВзаиморасчетыЧерезГоловнуюОрганизацию
	|			ИЛИ &ПоВсемОрганизациям)
	|
	|";
	
КонецФункции

#КонецОбласти
