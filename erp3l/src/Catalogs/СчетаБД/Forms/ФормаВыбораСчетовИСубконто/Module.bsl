
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТаблицаСчетов=ПолучитьИзВременногоХранилища(Параметры.АдресТаблицыСчетов);
	
	Запрос=Новый Запрос;
	Запрос.Текст="Выбрать * Поместить ТаблицаСчетов ИЗ &ТаблицаСчетов КАК ТаблицаСчетов
	|;
	|ВЫБРАТЬ
	|	СчетаБД.Код КАК КодСчета,
	|	ТабСубконто1.Субконто1 КАК Субконто1,
	|	ТабСубконто2.Субконто2 КАК Субконто2,
	|	ТабСубконто3.Субконто3 КАК Субконто3,
	|	СчетаБД.Ссылка КАК Счет,
	|	СчетаБД.Наименование КАК НаименованиеСчета
	|Поместить ПланСчетов
	|ИЗ
	|	Справочник.СчетаБД КАК СчетаБД
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			СчетаБДВидыСубконто.ВидСубконто + ВЫБОР
	|				КОГДА СчетаБДВидыСубконто.ТолькоОбороты
	|					ТОГДА "" (об)""
	|				ИНАЧЕ """"
	|			КОНЕЦ КАК Субконто1,
	|			СчетаБДВидыСубконто.Ссылка КАК Ссылка
	|		ИЗ
	|			Справочник.СчетаБД.ВидыСубконто КАК СчетаБДВидыСубконто
	|		ГДЕ
	|			СчетаБДВидыСубконто.НомерСтроки = 1) КАК ТабСубконто1
	|		ПО СчетаБД.Ссылка = ТабСубконто1.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			СчетаБДВидыСубконто.ВидСубконто + ВЫБОР
	|				КОГДА СчетаБДВидыСубконто.ТолькоОбороты
	|					ТОГДА "" (об)""
	|				ИНАЧЕ """"
	|			КОНЕЦ КАК Субконто2,
	|			СчетаБДВидыСубконто.Ссылка КАК Ссылка
	|		ИЗ
	|			Справочник.СчетаБД.ВидыСубконто КАК СчетаБДВидыСубконто
	|		ГДЕ
	|			СчетаБДВидыСубконто.НомерСтроки = 2) КАК ТабСубконто2
	|		ПО СчетаБД.Ссылка = ТабСубконто2.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			СчетаБДВидыСубконто.ВидСубконто + ВЫБОР
	|				КОГДА СчетаБДВидыСубконто.ТолькоОбороты
	|					ТОГДА "" (об)""
	|				ИНАЧЕ """"
	|			КОНЕЦ КАК Субконто3,
	|			СчетаБДВидыСубконто.Ссылка КАК Ссылка
	|		ИЗ
	|			Справочник.СчетаБД.ВидыСубконто КАК СчетаБДВидыСубконто
	|		ГДЕ
	|			СчетаБДВидыСубконто.НомерСтроки = 3) КАК ТабСубконто3
	|		ПО СчетаБД.Ссылка = ТабСубконто3.Ссылка
	|		Левое Соединение ТаблицаСчетов КАК ТаблицаСчетов
	|		ПО СчетаБД.Ссылка = ТаблицаСчетов.Счет
	|ГДЕ
	|	СчетаБД.Владелец = &ПланСчетов
	| И НЕ СчетаБД.ПометкаУдаления
	|;
	|ВЫБРАТЬ
	|	ПланСчетов.КодСчета КАК КодСчета,
	|	ПланСчетов.Субконто1 КАК Субконто1,
	|	ПланСчетов.Субконто2 КАК Субконто2,
	|	ПланСчетов.Субконто3 КАК Субконто3,
	|	ПланСчетов.Счет КАК Счет,
	|	ПланСчетов.НаименованиеСчета КАК НаименованиеСчета,
	|	ЕСТЬNULL(ТаблицаСчетов.СчетИспользуется,ЛОЖЬ) КАК СчетИспользуется,
	|   ЕСТЬNULL(ТаблицаСчетов.Субконто1Используется,ЛОЖЬ) КАК Субконто1Используется,
	|   ЕСТЬNULL(ТаблицаСчетов.Субконто2Используется,ЛОЖЬ) КАК Субконто2Используется,
	|   ЕСТЬNULL(ТаблицаСчетов.Субконто3Используется,ЛОЖЬ) КАК Субконто3Используется
	|ИЗ ПланСчетов КАК ПланСчетов
	|ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСчетов КАК ТаблицаСчетов
	|ПО ПланСчетов.Счет = ТаблицаСчетов.Счет
	|УПОРЯДОЧИТЬ ПО
	|	ПланСчетов.КодСчета";
	
	Запрос.УстановитьПараметр("ПланСчетов",Параметры.ПланСчетов);
	Запрос.УстановитьПараметр("ТаблицаСчетов",ТаблицаСчетов);
	
	ЗначениеВРеквизитФормы(Запрос.Выполнить().Выгрузить(),"ТаблицаОСВ");
	
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьСтруктуруОтвета()
	
	СтруктураОтвета=Новый Структура;
	СтруктураОтвета.Вставить("ПланСчетов",Параметры.ПланСчетов);
	ТаблицаСчетов=РеквизитФормыВЗначение("ТаблицаОСВ");
	
	ТаблицаОтвета=Новый ТаблицаЗначений;
	
	Для Каждого Колонка ИЗ ТаблицаСчетов.Колонки Цикл
		
		ТаблицаОтвета.Колонки.Добавить(Колонка.Имя,Колонка.ТипЗначения);
		
	КонецЦикла;
	
	ВыбранныеСчета=ТаблицаОСВ.НайтиСтроки(Новый Структура("СчетИспользуется",Истина));
	
	Для Каждого Строка ИЗ ВыбранныеСчета Цикл
		
		НоваяСтрока=ТаблицаОтвета.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Строка);
		
	КонецЦикла;
	
	СтруктураОтвета.Вставить("АдресТаблицыСчетов",ПоместитьВоВременноеХранилище(ТаблицаОтвета));
	
	Возврат СтруктураОтвета;
	
КонецФункции // ПодготовитьСтруктуруОтвета() 

&НаКлиенте
Процедура ОК(Команда)
	
	Оповестить("ИзмененОтборСчетовОСВ",ПодготовитьСтруктуруОтвета());
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаОСВСчетИспользуетсяПриИзменении(Элемент)
	
	ТекущиеДанные=Элементы.ТаблицаОСВ.ТекущиеДанные;
	
	Для Индекс=1 по 3 Цикл
		
		Если НЕ ЗначениеЗаполнено(ТекущиеДанные["Субконто"+Индекс]) Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ТекущиеДанные["Субконто"+Индекс+"Используется"]=ТекущиеДанные.СчетИспользуется;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаОСВСубконто1ИспользуетсяПриИзменении(Элемент)
	
	ТекущиеДанные=Элементы.ТаблицаОСВ.ТекущиеДанные;	
	
	Если НЕ ТекущиеДанные.Субконто1Используется Тогда
		
		ТекущиеДанные.Субконто2Используется=Ложь;
		ТекущиеДанные.Субконто3Используется=Ложь;
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаОСВСубконто2ИспользуетсяПриИзменении(Элемент)
	
	ТекущиеДанные=Элементы.ТаблицаОСВ.ТекущиеДанные;	
	
	Если НЕ ТекущиеДанные.Субконто2Используется Тогда
		
		ТекущиеДанные.Субконто3Используется=Ложь;
		
	Иначе
		
		ТекущиеДанные.Субконто1Используется=Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаОСВСубконто3ИспользуетсяПриИзменении(Элемент)
	
	ТекущиеДанные=Элементы.ТаблицаОСВ.ТекущиеДанные;	
	
	Если ТекущиеДанные.Субконто3Используется Тогда
				
		ТекущиеДанные.Субконто1Используется=Истина;
		ТекущиеДанные.Субконто2Используется=Истина;
		
	КонецЕсли;
		
КонецПроцедуры
