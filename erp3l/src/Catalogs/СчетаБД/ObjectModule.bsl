
Перем ОбновитьПометкуУдаленияДляПодчиненных;
Перем ЗаписьИзФормы Экспорт;
Перем ОбновлятьОСВ Экспорт;
Перем РодительСтар;
Перем ИзмененСчетСсылка Экспорт;

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда	
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Владелец) Тогда
		Владелец = Константы.ПланСчетовПоУмолчанию.Получить();
	КонецЕсли;
	
	Если НЕ ПометкаУдаления=Ссылка.ПометкаУдаления Тогда
		 ОбновитьПометкуУдаленияДляПодчиненных=Истина;
	КонецЕсли; 
	
	Если НЕ Родитель=Ссылка.Родитель Тогда
	
		РодительСтар=Ссылка.Родитель;
		
	КонецЕсли;
		
	Валютный = СтрНайти(ПризнакиУчета,"Валютный")>0;
	
	ДополнительныеСвойства.Вставить("РодительДоИзменения", Ссылка.Родитель);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ОбновитьПометкуУдаленияДляПодчиненных Тогда
		
		ОбщегоНазначенияУХ.ПометитьСправочникПоРеквизиту("СтрокиОтчетов","СчетБД",Ссылка,ПометкаУдаления,Отказ);
		
		Если Не Отказ Тогда
			
			ОбщегоНазначенияУХ.ПометитьСправочникПоРеквизиту("ПоказателиОтчетов","СчетБД",Ссылка,ПометкаУдаления,Отказ);
			
		КонецЕсли;
		
		Если Не Отказ Тогда
			
			ОбщегоНазначенияУХ.ПометитьСправочникПоРеквизиту("ПоказателиОтчетов","КоррСчетБД",Ссылка,ПометкаУдаления,Отказ);
			
		КонецЕсли;
		
		Если Не Отказ Тогда
			
			ОбщегоНазначенияУХ.ПометитьСправочникПоРеквизиту("ГруппыРаскрытия","КоррСчетБД",Ссылка,ПометкаУдаления,Отказ);
			
		КонецЕсли;
		
		Если Не Отказ Тогда
			
			ОбщегоНазначенияУХ.ПометитьСправочникПоРеквизиту("ГруппыРаскрытия","СчетБД",Ссылка,ПометкаУдаления,Отказ);
			
		КонецЕсли;
		
		Если Не Отказ Тогда
			
			ОбщегоНазначенияУХ.ПометитьСправочникПоРеквизиту("ШаблоныПроводок","СчетДт",Ссылка,ПометкаУдаления,Отказ);
			
		КонецЕсли;
		
		Если Не Отказ Тогда
			
			ОбщегоНазначенияУХ.ПометитьСправочникПоРеквизиту("ШаблоныПроводок","СчетКт",Ссылка,ПометкаУдаления,Отказ);
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СчетСсылка) Тогда
			
			СчетОбъект=СчетСсылка.ПолучитьОбъект();
			СчетОбъект.ПометкаУдаления=ПометкаУдаления;
			СчетОбъект.ОбменДанными.Загрузка=Истина;
			СчетОбъект.Записать();
			
		КонецЕсли;
		
	КонецЕсли;
		
	Если ЗначениеЗаполнено(Родитель) 
		И (НЕ Родитель.ГруппирующийСчет)
		И ЗаписьИзФормы Тогда
		
		РодительОбъект=Родитель.ПолучитьОбъект();
		РодительОбъект.ГруппирующийСчет=Истина;
		
		Попытка
			
			РодительОбъект.Записать();
			
		Исключение
			
			ОбщегоНазначенияУХ.СообщитьОбОшибке(СтрШаблон(Нстр("ru = 'Не удалось обновить признак ""Группирующий счет"" у счета %1, являющегося родителем счета %2
			|%3
			|Счет не может быть записан.'"), РодительОбъект.Код, Код, 
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())),Отказ,,СтатусСообщения.Внимание);
			
		КонецПопытки;
			
	КонецЕсли; 
	
	Если НЕ РодительСтар=Неопределено Тогда 
		
		ОбработатьСменуРодителя(Отказ);
			
	КонецЕсли;
					
КонецПроцедуры

Процедура ОбработатьСменуРодителя(Отказ)
	
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ ПЕРВЫЕ 1
	|	СчетаБД.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СчетаБД КАК СчетаБД
	|ГДЕ
	|	СчетаБД.Ссылка В ИЕРАРХИИ(&РодительСтар)
	|	И СчетаБД.Ссылка <> &РодительСтар";
	
	Запрос.УстановитьПараметр("РодительСтар",РодительСтар);
	
	Результат=Запрос.Выполнить().Выбрать();
	
	Если НЕ Результат.Следующий() Тогда
		
		РодительСтарОбъект=РодительСтар.ПолучитьОбъект();
		РодительСтарОбъект.ГруппирующийСчет=Ложь;
		
		Попытка
			
			РодительСтарОбъект.ОбменДанными.Загрузка=Истина;
			РодительСтарОбъект.Записать();
			
		Исключение
			
			ОбщегоНазначенияУХ.СообщитьОбОшибке(СтрШаблон(Нстр("ru = 'Не удалось обновить признак ""Группирующий счет"" у счета %1, являвшегося родителем счета %2
			|%3
			|Счет не может быть записан.'"), РодительСтарОбъект.Код, Код, 
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())),Отказ,,СтатусСообщения.Внимание);
			
		КонецПопытки;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СчетСсылка) 
		И Владелец.ПланСчетовМСФО
		И ИзмененСчетСсылка=Неопределено Тогда
		
		Если ЗначениеЗаполнено(Родитель) Тогда
			
			НовыйСчетРодитель=Родитель.СчетСсылка;
			
		Иначе
			
			НовыйСчетРодитель=ПланыСчетов.МСФО.ПустаяСсылка();
			
		КонецЕсли;
		
		СчетОбъект=СчетСсылка.ПолучитьОбъект();
		СчетОбъект.Родитель=НовыйСчетРодитель;
		СчетОбъект.ОбменДанными.Загрузка=Истина;
		
		Попытка
			
			СчетОбъект.Записать();
			
		Исключение
			
			ОбщегоНазначенияУХ.СообщитьОбОшибке(СтрШаблон(Нстр("ru = 'Не удалось изменить родителя у плана счетов МСФО %1: 
			|%2
			|Счет не может быть записан.'"), СчетОбъект.Код,ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())),Отказ,,СтатусСообщения.Внимание);
			
		КонецПопытки;
		
	КонецЕсли;
			
		
КонецПроцедуры // ОбработатьСменуРодителя() 

ОбновитьПометкуУдаленияДляПодчиненных=Ложь;
ЗаписьИзФормы=Истина;

