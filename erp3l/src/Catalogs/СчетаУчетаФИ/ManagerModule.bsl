
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция ПолучитьСчета() Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СчетаУчетаФИ.Ссылка КАК Ссылка,
	|	СчетаУчетаФИ.ЭтоГруппа КАК ЭтоГруппа,
	|	СчетаУчетаФИ.Наименование КАК Наименование,
	|	СчетаУчетаФИ.Забалансовый КАК Забалансовый,
	|	СчетаУчетаФИ.РазделПланаСчетов КАК РазделПланаСчетов,
	|	СчетаУчетаФИ.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных,
	|	СчетаУчетаФИ.Родитель.ИмяПредопределенныхДанных КАК Родитель,
	|	СчетаУчетаФИ.Код КАК Код,
	|	СчетаУчетаФИ.Описание КАК Описание
	|ИЗ
	|	Справочник.СчетаУчетаФИ КАК СчетаУчетаФИ
	|ГДЕ
	|	НЕ СчетаУчетаФИ.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетаУчетаФИ.Код");
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#Область ОбновлениеПредопреленных

Процедура ОбновитьПредопределенные() Экспорт

	КэшРазделПланаСчетов = Неопределено;
	ТабДок = Справочники.СчетаУчетаФИ.ПолучитьМакет("ЗаполнениеПредопределенных");
	
	Для НомерСтроки = 2 По ТабДок.ВысотаТаблицы Цикл
		
		ИмяПредопределенного = ТабДок.Область(НомерСтроки, 1).Текст;
		ПредопределенныйЭлемент = Справочники.СчетаУчетаФИ[ИмяПредопределенного];
		
		Если ПредопределенныйЭлемент.ЭтоГруппа Тогда
			Продолжить;
		КонецЕсли;
		
		Объект = ПредопределенныйЭлемент.ПолучитьОбъект();
		
		Объект.РазделПланаСчетов	= РазделПланаСчетовИзТекста(СокрЛП(ТабДок.Область(НомерСтроки, 2).Текст), КэшРазделПланаСчетов);
		Объект.Забалансовый 		= ЗначениеИзТекста(ТабДок.Область(НомерСтроки, 3).Текст);
		Объект.Описание 			= СокрЛП(ТабДок.Область(НомерСтроки, 4).Текст);
		
		Объект.Записать();			
		
	КонецЦикла;

КонецПроцедуры

Функция ЗначениеИзТекста(ТекстОбласти)
	Возврат ?(ТекстОбласти = "Истина", Истина, ?(ТекстОбласти = "Ложь", Ложь, Неопределено));
КонецФункции

Функция РазделПланаСчетовИзТекста(ТекстОбласти, КэшРазделПланаСчетов = Неопределено)

	Если КэшРазделПланаСчетов = Неопределено Тогда
	
		КэшРазделПланаСчетов = Новый Соответствие;
		Выборка = Справочники.РазделыПланаСчетов.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Выборка.Предопределенный Тогда
				КэшРазделПланаСчетов.Вставить(Выборка.ИмяПредопределенныхДанных, Выборка.Ссылка);
			КонецЕсли;			
		КонецЦикла;
	
	КонецЕсли;
	
	Возврат КэшРазделПланаСчетов.Получить(ТекстОбласти);

КонецФункции

#КонецОбласти

#КонецЕсли
