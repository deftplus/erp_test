
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Предопределенный Тогда
		
		ТолькоПросмотр = Истина;
		Элементы.ТипФИ.ТолькоПросмотр = Истина;
		Элементы.Задолженность.ТолькоПросмотр = Истина;
		Элементы.ВозвратПоГрафику.ТолькоПросмотр = Истина;
		Элементы.ПереводЗадолженностиИзДолгосрочной.ТолькоПросмотр = Истина;
		
	КонецЕсли;
	
	Если Не ИдентификацияПродуктаУХКлиентСервер.ЭтоУправлениеХолдингом() Тогда //в БМ ДКЗ и МКЗ не доступны
		Элементы.Задолженность.Видимость = Ложь;
		Если Объект.Задолженность Тогда
			Объект.Задолженность = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ТипФИ 								= (Объект.Лизинг = Истина);
	ПереводЗадолженностиИзДолгосрочной 	= Объект.ПереводВКраткосрочную;
	ВозвратПоГрафику 					= Объект.ВозвратПоГрафику;
	Задолженность 						= Объект.Задолженность;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура АктивОбязательствоПриИзменении(Элемент)
	УправлениеФормой(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура СпособУчетаПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроизводныйФИПриИзменении(Элемент)
	УправлениеФормой(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ТипФИПриИзменении(Элемент)
	
	Объект.Лизинг = (ТипФИ = 1);
	УправлениеФормой(ЭтаФорма);	
	
КонецПроцедуры

&НаКлиенте
Процедура ПереводЗадолженностиИзДолгосрочнойПриИзменении(Элемент)
		
	Объект.ПереводВКраткосрочную = (ПереводЗадолженностиИзДолгосрочной = 1);
	УправлениеФормой(ЭтаФорма);	

КонецПроцедуры

&НаКлиенте
Процедура ВозвратПоГрафикуПриИзменении(Элемент)
	
	Объект.ВозвратПоГрафику = (ВозвратПоГрафику = 1);
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ВидСтавкиРасчетаПроцентовПриИзменении(Элемент)
	УправлениеФормой(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ЗадолженностьПриИзменении(Элемент)
	Объект.Задолженность = (Задолженность = 1);	
	УправлениеФормой(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ГрафикиРасчетовСоответствуютНСБУПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеПроцедурыФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если Объект.СпособУчета = ПредопределенноеЗначение("Перечисление.СпособыУчетаФинансовыхИнструментов.ПоСправедливойСтоимости") Тогда
		
		Элементы.СтраницыСпособУчета.ТекущаяСтраница = Элементы.ГруппаСправедливаяСтоимость;
				
		Элементы.СпособУчетаПереоценки.ТолькоПросмотр = Объект.ПроизводныйФИ;
		Если Объект.ПроизводныйФИ Тогда
			ЧерезПрибыльУбыток = ПредопределенноеЗначение("Перечисление.СпособыУчетаПереоценкиФИ.ЧерезПрибыльУбыток");
			ОбщегоНазначенияУХ.УстановитьНовоеЗначение(Объект.СпособУчетаПереоценки, ЧерезПрибыльУбыток);		
		КонецЕсли;
		
		Элементы.АктивОбязательствоПФИ.ТолькоПросмотр = Объект.ПроизводныйФИ;
		Если Объект.ПроизводныйФИ Тогда
			Объект.АктивОбязательство = ПредопределенноеЗначение("Перечисление.АктивОбязательствоОбъектовФинансовогоХарактера.ПустаяСсылка");
		КонецЕсли;
		
	Иначе
		
		Элементы.АктивОбязательствоПФИ.ТолькоПросмотр = Ложь;
		
		ЭтоАктив = Объект.АктивОбязательство = ПредопределенноеЗначение("Перечисление.АктивОбязательствоОбъектовФинансовогоХарактера.Актив");
		ПредставлениеДКЗ = ?(ЭтоАктив, НСтр("ru = 'Торговый инcтрумент (дебиторская задолженность)'"), НСтр("ru = 'Торговый инструмент (кредиторская задолженность)'"));
		Элементы.Задолженность.СписокВыбора[1].Представление = ПредставлениеДКЗ;
		
		Элементы.СтраницыСпособУчета.ТекущаяСтраница = Элементы.ГруппаАмортизированнаяСтоимость;
		
		ВидыСтавок = ПолучитьВидыСтавок(Объект.Лизинг, Объект.Задолженность);
		СписокВыбораСтавка = Элементы.ВидСтавкиРасчетаПроцентов.СписокВыбора;		
		СписокВыбораСтавка.Очистить();
		Для каждого ВидСтавки Из ВидыСтавок Цикл
			СписокВыбораСтавка.Добавить(ВидСтавки.Значение, ВидСтавки.Представление);
		КонецЦикла;
		
		ВидЭСП = ПредопределенноеЗначение("Перечисление.ВидыСтавокОбъектовФинансовогоХарактера.ЭффективнаяСтавка");
		
		Если Объект.Лизинг Тогда
			
			Объект.ГрафикиРасчетовСоответствуютНСБУ = Ложь;
			Элементы.ГрафикиРасчетовСоответствуютНСБУ.ТолькоПросмотр = Истина;
			
			Форма.ВозвратПоГрафику = 0;
			Объект.ВозвратПоГрафику = Ложь;
			
		ИначеЕсли Объект.Задолженность Тогда
			
			Форма.ВозвратПоГрафику = 1;
			Объект.ВозвратПоГрафику = Истина;
			Объект.ПорядокУчетаДисконта = Истина;
			
		КонецЕсли;
				
		Элементы.ПорядокУчетаДисконта.ТолькоПросмотр 		= Объект.Задолженность Или (Объект.ВидСтавкиРасчетаПроцентов = ВидЭСП);		
		Элементы.ВидСтавкиРасчетаПроцентов.ТолькоПросмотр 	= Объект.ГрафикиРасчетовСоответствуютНСБУ;
		Элементы.ВозвратПоГрафику.ТолькоПросмотр 			= Объект.ГрафикиРасчетовСоответствуютНСБУ Или Объект.Лизинг Или Объект.Задолженность;
		
		Если Объект.ГрафикиРасчетовСоответствуютНСБУ Тогда		
			Объект.ВидСтавкиРасчетаПроцентов = ПредопределенноеЗначение("Перечисление.ВидыСтавокОбъектовФинансовогоХарактера.СтавкаПоДоговору");
			Объект.ВозвратПоГрафику = Истина;
			Форма.ВозвратПоГрафику = Объект.ВозвратПоГрафику;
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьВидыСтавок(Лизинг = Ложь, Задолженность = Ложь)

	ВидыСтавок = Новый СписокЗначений;
	
	Если Лизинг Тогда
		
		ВидыСтавок.Добавить(Перечисления.ВидыСтавокОбъектовФинансовогоХарактера.ЭффективнаяСтавка, НСтр("ru = 'Подразумеваемая ставка'"));		
		ВидыСтавок.Добавить(Перечисления.ВидыСтавокОбъектовФинансовогоХарактера.СтавкаПоДоговору, НСтр("ru = 'Ставка по договору'"));
		ВидыСтавок.Добавить(Перечисления.ВидыСтавокОбъектовФинансовогоХарактера.РыночнаяСтавка, НСтр("ru = 'Вмененная ставка'"));
		
	Иначе
		
		Если Не Задолженность Тогда
			ВидыСтавок.Добавить(Перечисления.ВидыСтавокОбъектовФинансовогоХарактера.ЭффективнаяСтавка, НСтр("ru = 'Эффективная ставка'"));
		КонецЕсли;		
		ВидыСтавок.Добавить(Перечисления.ВидыСтавокОбъектовФинансовогоХарактера.СтавкаПоДоговору, НСтр("ru = 'Ставка по договору'"));
		ВидыСтавок.Добавить(Перечисления.ВидыСтавокОбъектовФинансовогоХарактера.РыночнаяСтавка, НСтр("ru = 'Рыночная ставка'"));
	
	КонецЕсли;

	Возврат ВидыСтавок;
	
КонецФункции

#КонецОбласти
