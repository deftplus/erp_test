// Управляет доступностью элементов формы.
&НаСервере
Процедура УправлениеДоступностью()
	// Доступность таблицы типов итоговых документов.
	Элементы.ТипыИтоговыхДокументов.Доступность = Объект.ИспользуетсяИтоговыйДокумент;
	// Видимость Способа выбора поставщика.
	ЭтоМероприятиеЗакупочнаяПроцедура = (Объект.ВидМероприятия = Перечисления.ВидыМероприятий.ЗакупочнаяПроцедура);
	Элементы.СпособВыбораПоставщика.Видимость = ЭтоМероприятиеЗакупочнаяПроцедура;
	// Доступность Вида мероприятия.
	ЕстьВидМероприятия = ЗначениеЗаполнено(Объект.ВидМероприятия);
	ОбъектЗаписан = ЗначениеЗаполнено(Объект.Ссылка);
	Если ЭтоМероприятиеЗакупочнаяПроцедура Тогда
		Элементы.ВидМероприятия.Видимость = Ложь;
	Иначе
		Элементы.ВидМероприятия.ТолькоПросмотр = ((ЕстьВидМероприятия) И (ОбъектЗаписан));
	КонецЕсли;	
КонецПроцедуры		// УправлениеДоступностью()

&НаКлиенте
Процедура ИспользуетсяИтоговыйДокументПриИзменении(Элемент)
	УправлениеДоступностью();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УправлениеДоступностью();
КонецПроцедуры

&НаКлиенте
Процедура ВидМероприятияПриИзменении(Элемент)
	УправлениеДоступностью();
КонецПроцедуры

&НаКлиенте
Процедура ВидМероприятияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	МассивВыбора = Новый Массив;
	МассивВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыМероприятий.Инициатива"));
	МассивВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыМероприятий.КонтрольноеМероприятие"));
	МассивВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВидыМероприятий.РеакцияНаРиск"));
	Элемент.СписокВыбора.ЗагрузитьЗначения(МассивВыбора);
КонецПроцедуры

&НаКлиенте
Процедура ВсеОбязательные(Команда)
	Для Каждого ТекТипыИтоговыхДокументов Из Объект.ТипыИтоговыхДокументов Цикл
		ТекТипыИтоговыхДокументов.Обязательный = Истина;
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОбязательные(Команда)
	Для Каждого ТекТипыИтоговыхДокументов Из Объект.ТипыИтоговыхДокументов Цикл
		ТекТипыИтоговыхДокументов.Обязательный = Ложь;
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура ТипыИтоговыхДокументовТипИтоговогоДокументаПриИзменении(Элемент)
	ТекДанные = Элементы.ТипыИтоговыхДокументов.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		Если СокрЛП(ТекДанные.ПредставлениеДокумента) = "" Тогда
			НовоеПредставление = Строка(ТекДанные.ТипИтоговогоДокумента);
			ТекДанные.ПредставлениеДокумента = НовоеПредставление;
		Иначе
			// Представление заполнено. Не изменяем существующее.
		КонецЕсли;
	Иначе
		// Строка не выбрана. Ничего не делаем.
	КонецЕсли;
КонецПроцедуры
