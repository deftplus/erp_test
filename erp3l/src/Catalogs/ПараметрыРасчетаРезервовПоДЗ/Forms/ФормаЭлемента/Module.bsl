
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипПараметраРасчетаРезерва = Объект.ТипПараметраРасчетаРезервов;
	
	ТипыПараметровРасчетаРезервов = МСФОВызовСервераУХ.ПолучитьСтруктуруСоЗначениямиПеречисления("ТипыПараметровРасчетаРезерва");
	КэшируемыеЗначения = Новый ФиксированнаяСтруктура("ТипыПараметровРасчетаРезервов", ТипыПараметровРасчетаРезервов);
	
	ОбновитьВидимостьСтраниц(ЭтаФорма);
	ОбновитьОтборы();
	
	Если Объект.Предопределенный Тогда
	
		Элементы.Алгоритм.ТолькоПросмотр = Истина;
		Элементы.ТипПараметраРасчетаРезерва.ТолькоПросмотр = Истина;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ОбновленыПризнакиРезервов" Тогда
	    Элементы.ПериодическиеПризнаки.Обновить();	
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура ТипПризнакаРезерваПриИзменении(Элемент)
	
	Объект.ТипПараметраРасчетаРезервов = ТипПараметраРасчетаРезерва;
	Модифицированность = Истина;
	
	ОбновитьВидимостьСтраниц(ЭтаФорма);
	ОбновитьОтборы();

КонецПроцедуры

&НаКлиенте
Процедура ГрупповоеДобавление(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда
		
		ПоказатьПредупреждение(, НСтр("ru = 'Перед измененением групп признаков необходимо записать объект!'"), 20);
		Возврат;
		
	КонецЕсли;
				
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ПараметрРасчетаРезервов", 	Объект.Ссылка);  
	ПараметрыОткрытия.Вставить("Период", 					НачалоМесяца(ТекущаяДата()));
	
	ОткрытьФорму("РегистрСведений.ПризнакиЗадолженности.Форма.ГрупповаяУстановкаПризнаковОбъектов", ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппыПризнаковПередНачаломИзменения(Элемент, Отказ)
	
	Если Объект.Ссылка.Пустая() Тогда
		
		Отказ = Истина;
		ПоказатьПредупреждение(, НСтр("ru = 'Перед измененением групп признаков необходимо записать объект!'"), 20);		
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьВидимостьСтраниц(ЭтаФорма)

	Если ЭтаФорма.Объект.ТипПараметраРасчетаРезервов = ЭтаФорма.КэшируемыеЗначения.ТипыПараметровРасчетаРезервов.Вычисляемый Тогда
	 
		ЭтаФорма.Элементы.СтраницаФормулаРасчета.Видимость = Истина;
		ЭтаФорма.Элементы.СтраницаПериодическиеПризнаки.Видимость = Ложь;
		
	Иначе
				
		ЭтаФорма.Элементы.СтраницаФормулаРасчета.Видимость = Ложь;
		ЭтаФорма.Элементы.СтраницаПериодическиеПризнаки.Видимость = Истина;		
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтборы()
	
	Если Объект.ТипПараметраРасчетаРезервов = КэшируемыеЗначения.ТипыПараметровРасчетаРезервов.Вычисляемый Тогда
		ТиповыеОтчетыУХ.ДобавитьОтбор(ГруппыПризнаков.КомпоновщикНастроек, "Владелец", Объект.Ссылка);
	Иначе 
		ТиповыеОтчетыУХ.ДобавитьОтбор(ПериодическиеПризнаки.КомпоновщикНастроек, "ПараметрРасчетаРезервов", Объект.Ссылка);
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти


