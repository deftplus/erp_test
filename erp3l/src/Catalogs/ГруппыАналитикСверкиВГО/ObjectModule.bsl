#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
// Возвращает структуру с двумя массивами "Аналитики" и "Ресурсы".
// Каждый описывает свою табличную часть.
// Данные для заполнения определяются по СпособуЭлиминации
//
Функция ПолучитьПоляДляДобавления() Экспорт
	
	мАналитикДляДобавления = Новый Массив;
	мРесурсовДляДобавления = Новый Массив;
	мДобавляемыхРеквизитов = Новый Структура("Аналитики, Ресурсы", мАналитикДляДобавления, мРесурсовДляДобавления);
	
	Если НЕ ЗначениеЗаполнено(СпособЭлиминации) Тогда
		Возврат мДобавляемыхРеквизитов;
	КонецЕсли;
	
	ОписаниеЭлиминации = ЭлиминацияВГОУХ.ПолучитьОписаниеСпособаЭлиминации(
		СпособЭлиминации, 
		Владелец);
	
	Если ТипИсточникаДанныхВГО = Перечисления.ТипИсточникаДанныхВГО.Основной 
		ИЛИ НЕ ЗначениеЗаполнено(ТипИсточникаДанныхВГО) Тогда
		
		мАналитик = ОписаниеЭлиминации.Аналитики;
		мПоказателей = ОписаниеЭлиминации.Показатели;
		
	Иначе
		
		ОписаниеЭлиминации = ОписаниеЭлиминации.ДопИсточникиДанных[ТипИсточникаДанныхВГО];
		Если НЕ ЗначениеЗаполнено(ОписаниеЭлиминации) Тогда
			Возврат мДобавляемыхРеквизитов;
		КонецЕсли;
		
		мАналитик = ОписаниеЭлиминации.Аналитики;
		мПоказателей = ОписаниеЭлиминации.Показатели;
		
	КонецЕсли;
	
	Для Каждого Аналитика_ Из мАналитик Цикл
		Если Аналитики.Найти(Аналитика_.ТипДляЭлиминации, "ТипДляЭлиминации") = Неопределено Тогда
			мАналитикДляДобавления.Добавить(Аналитика_);
		КонецЕсли;
	КонецЦИкла;
	
	Для Каждого Показатель_ Из мПоказателей Цикл
		Если Ресурсы.Найти(Показатель_.ТипДляЭлиминации, "ТипДляЭлиминации") = Неопределено Тогда
			мРесурсовДляДобавления.Добавить(Показатель_);
		КонецЕсли;
	КонецЦИкла;
	
	мДобавляемыхРеквизитов.Аналитики = мАналитикДляДобавления;
	мДобавляемыхРеквизитов.Ресурсы = мРесурсовДляДобавления;
	
	Возврат мДобавляемыхРеквизитов;
	
КонецФункции

// Заполняет табличные части "Аналитики" и "Ресурсы" данными соответствующими СпособуЭлиминации
Процедура ЗаполнитьНедостающиеПоля() Экспорт
	
	ОписаниеПолей = ПолучитьПоляДляДобавления();
	
	Для Каждого ДобавляемыйОбъект Из ОписаниеПолей.Аналитики Цикл
		НоваяСтрока = Аналитики.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ДобавляемыйОбъект);
		НоваяСтрока.ДопАналитикаСверки = НоваяСтрока.ИспользоватьДляСверки И СверкаВГОВызовСервераУХ.ЭтоДопАналитикаСверки(НоваяСтрока.ТипДляЭлиминации);
	КонецЦикла;
		
	Для Каждого ДобавляемыйОбъект Из ОписаниеПолей.Ресурсы Цикл
		НоваяСтрока = Ресурсы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ДобавляемыйОбъект);
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() И ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Если ДанныеЗаполнения.Свойство("СпособЭлиминации") И ЗначениеЗаполнено(ДанныеЗаполнения.СпособЭлиминации) Тогда
			Наименование = "Аналитики: " + ДанныеЗаполнения.СпособЭлиминации;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли
