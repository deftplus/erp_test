#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбновлениеПредопределенных() Экспорт

	ТабДок = ПолучитьМакет("СчетаФИ");
	
	Таб = Новый ТаблицаЗначений;
	Таб.Колонки.Добавить("ИмяВидСчетаФИ");
	Таб.Колонки.Добавить("ВидФИ");
	Таб.Колонки.Добавить("Актив");
	Таб.Колонки.Добавить("КодСчета");
	Таб.Колонки.Добавить("ВидимостьВДокументах");
		
	Для каждого ОбластьВидФИ Из ТабДок.Области Цикл
		
		Мас = СтрРазделить(ОбластьВидФИ.Имя, "_");
		
		ТекВидФИ = Перечисления.ВидыФинансовыхИнструментов[Мас[0]];
		ТекАктив = ?(Мас.Количество() > 1, Мас[1] = "Актив", Ложь);
		
		Для НомерСтроки = ОбластьВидФИ.Верх По ОбластьВидФИ.Низ Цикл
			
			НоваяСтрока = Таб.Добавить();
			НоваяСтрока.ИмяВидСчетаФИ			= ТабДок.Область(НомерСтроки,1).Текст;
			НоваяСтрока.ВидФИ 					= ТекВидФИ;
			НоваяСтрока.Актив 					= ТекАктив;
			НоваяСтрока.КодСчета 				= ТабДок.Область(НомерСтроки,2).Текст;
			НоваяСтрока.ВидимостьВДокументах	= ТабДок.Область(НомерСтроки,3).Текст;
			
		КонецЦикла;
	
	КонецЦикла;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Ссылка,
	|	Хозрасчетный.Код КАК Код
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Код В(&Код)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыСчетовФИ.Ссылка КАК Ссылка,
	|	ВидыСчетовФИ.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных
	|ИЗ
	|	Справочник.ВидыСчетовФИ КАК ВидыСчетовФИ");
	Запрос.УстановитьПараметр("Код", Таб.ВыгрузитьКолонку("КодСчета"));
	Результат = Запрос.ВыполнитьПакет();
	
	СчетаПоКоду = Результат[0].Выгрузить();
	СчетаПоКоду.Индексы.Добавить("Код");
	
	Таб.Индексы.Добавить("ИмяВидСчетаФИ");
	Выборка = Результат[1].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Объект = Выборка.Ссылка.ПолучитьОбъект();
		Объект.ВидыФИ.Очистить();
		
		СтрокиВидыФИ = Таб.НайтиСтроки(Новый Структура("ИмяВидСчетаФИ", Выборка.ИмяПредопределенныхДанных));
		Для каждого СтрокаВидФИ Из СтрокиВидыФИ Цикл
			
			Если СтрокаВидФИ.КодСчета = "" Тогда
				Продолжить;			
			КонецЕсли;
			
			СтрокаТаб = Объект.ВидыФИ.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаб, СтрокаВидФИ);
			СтрокаТаб.Счет = СчетаПоКоду.Найти(СтрокаВидФИ.КодСчета, "Код").Ссылка;
		
		КонецЦикла;
		
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли