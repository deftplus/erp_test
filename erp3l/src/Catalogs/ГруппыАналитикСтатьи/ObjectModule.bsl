#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

// Процедура - обработчик события объекта "ПередЗаписью".
//
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Валютная И НЕ ЗначениеЗаполнено(ВидАналитикиВалютаДт) Тогда
		ВидАналитикиВалютаДт=ПланыВидовХарактеристик.ВидыСубконтоКорпоративные.СправочникВалюты;
	КонецЕсли;
	
	КоличествоАналитикСтатьи = ОбщегоНазначения.ВызватьФункциюКонфигурации(
		"АналитикиСтатейБюджетовУХКлиентСервер.КоличествоАналитикСтатьи");
	
	ОпределитьКоличествоАналитик(КоличествоАналитикСтатьи);
	
	ОпределенВидАналитики = Ложь;
	Для Инд = 1 По КоличествоАналитикСтатьи Цикл
		Если ЗначениеЗаполнено(ЭтотОбъект["ВидАналитики" + Инд]) Тогда
			ОпределенВидАналитики = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ВидАналитикиВалютаДт) Тогда
		ОпределенВидАналитики = Истина;
	КонецЕсли;
	
	Если НЕ ОпределенВидАналитики Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не определены виды аналитики.'"),,,,Отказ);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОпределитьКоличествоАналитик(Знач КоличествоАналитикСтатьи = неопределено) Экспорт
	
	Если КоличествоАналитикСтатьи = неопределено Тогда
		КоличествоАналитикСтатьи = ОбщегоНазначения.ВызватьФункциюКонфигурации(
			"АналитикиСтатейБюджетовУХКлиентСервер.КоличествоАналитикСтатьи");
	КонецЕсли;
	
	ЧислоАналитик = 0;
	Для Инд = 1 По КоличествоАналитикСтатьи Цикл
		Если ЗначениеЗаполнено(ЭтотОбъект["ВидАналитики" + Инд]) Тогда
			ЧислоАналитик = ЧислоАналитик+1;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли 

