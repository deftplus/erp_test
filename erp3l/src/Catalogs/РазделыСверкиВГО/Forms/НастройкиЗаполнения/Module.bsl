
#Область ОбработкаОсновныхСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СпособЭлиминации = Параметры.СпособЭлиминации;
	
	ОбновитьПоляКолонкиСверки 			= Истина;
	ОбновитьСверяемыеИсточникиДанных 	= Истина;
	ОбновитьИсточникиСебестоимости 		= Истина;
	ОбновитьФСД 						= Истина;
	ОбновитьПравилаИмпортаФСД 			= Истина;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаСобытийЭлементовФормы

&НаКлиенте
Процедура Заполнить(Команда)
	
	Результат = новый Структура(
		"СпособЭлиминации,
		|ОбновитьПоляКолонкиСверки,
		|ОбновитьСверяемыеИсточникиДанных,
		|ОбновитьИсточникиСебестоимости,
		|ОбновитьФСД,
		|ОбновитьПравилаИмпортаФСД");
	
	ЗаполнитьЗначенияСвойств(Результат, ЭтотОбъект);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособЭлиминацииПриИзменении(Элемент)
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	ЕстьСебестоимость = Ложь;
	Если Форма.СпособЭлиминации = ПредопределенноеЗначение("Перечисление.СпособыЭлиминации.ЭлиминацияЗапасов") Тогда
		ЕстьСебестоимость = Истина;
	ИначеЕсли Форма.СпособЭлиминации = ПредопределенноеЗначение("Перечисление.СпособыЭлиминации.ЭлиминацияОСиНМА") Тогда	
		ЕстьСебестоимость = Истина;
	КонецЕсли;
	
	Элементы.ОбновитьИсточникиСебестоимости.ТолькоПросмотр = Не ЕстьСебестоимость;
	Если Не ЕстьСебестоимость Тогда
		Форма.ОбновитьИсточникиСебестоимости = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


