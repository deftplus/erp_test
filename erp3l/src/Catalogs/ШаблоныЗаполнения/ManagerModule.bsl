#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Возвращает наименование по умолчанию для шаблона по объекту СсылкаНаОбъектВход.
Функция ПолучитьНаименованиеШаблонаПоУмолчанию(СсылкаНаОбъектВход) Экспорт
	// Заполнение по шаблону.
	РезультатФункции = "";
	МетаданныеОбъекта = СсылкаНаОбъектВход.Метаданные();
	Если ОбщегоНазначения.ЭтоДокумент(МетаданныеОбъекта) Тогда
		РезультатФункции = НСтр("ru = '(Шаблон) [%ИмяОбъектаМетаданных%] %Номер%'");
		РезультатФункции = СтрЗаменить(РезультатФункции, "%ИмяОбъектаМетаданных%", УправлениеШаблонамиЗаполненияУХ.ИмяОбъектаМетаданных(СсылкаНаОбъектВход));
		РезультатФункции = СтрЗаменить(РезультатФункции, "%Номер%", Строка(СсылкаНаОбъектВход.Номер));
	ИначеЕсли ОбщегоНазначения.ЭтоСправочник(МетаданныеОбъекта) Тогда
		РезультатФункции = НСтр("ru = '(Шаблон) [%ИмяОбъектаМетаданных%] %Наименование%'");
		РезультатФункции = СтрЗаменить(РезультатФункции, "%ИмяОбъектаМетаданных%", УправлениеШаблонамиЗаполненияУХ.ИмяОбъектаМетаданных(СсылкаНаОбъектВход));
		РезультатФункции = СтрЗаменить(РезультатФункции, "%Наименование%", Строка(СсылкаНаОбъектВход.Наименование));
	Иначе
		РезультатФункции = НСтр("ru = '(Шаблон) [%ИмяОбъектаМетаданных%] '");
		РезультатФункции = СтрЗаменить(РезультатФункции, "%ИмяОбъектаМетаданных%", УправлениеШаблонамиЗаполненияУХ.ИмяОбъектаМетаданных(СсылкаНаОбъектВход));
	КонецЕсли;	
	// Проверка на существование имени.
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ШаблоныЗаполнения.Ссылка,
		|	ШаблоныЗаполнения.Наименование
		|ИЗ
		|	Справочник.ШаблоныЗаполнения КАК ШаблоныЗаполнения
		|ГДЕ
		|	ШаблоныЗаполнения.ПометкаУдаления = ЛОЖЬ";
	РезультатЗапроса = Запрос.Выполнить();
	Выгрузка = РезультатЗапроса.Выгрузить();
	МассивНаименований = Выгрузка.ВыгрузитьКолонку("Наименование");
	ИсходныйВариант = РезультатФункции;
	Счетчик = 2;
	Пока (МассивНаименований.Найти(РезультатФункции) <> Неопределено) И (Счетчик < 10000) Цикл
		РезультатФункции = ИсходныйВариант + "_" + Строка(Счетчик);
		Счетчик = Счетчик + 1;
	КонецЦикла;
	Возврат РезультатФункции;
КонецФункции
#КонецЕсли
