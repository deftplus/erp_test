// Формирует новый шаблон по образу ОбъектРодительВход и выбранным реквизитам 
// СписокРеквизитовВход, СписокТабличныхЧастейВход. Возвращает ссылку на созданный шаблон.
&НаСервере
Функция СоздатьШаблон(ОбъектРодительВход, СписокРеквизитовВход, СписокТабличныхЧастейВход)
	РезультатФункции = Справочники.ШаблоныЗаполнения.ПустаяСсылка();
	НачатьТранзакцию();
	ЕстьОшибки = Ложь;
	Попытка 
		НовыйШаблон = Справочники.ШаблоныЗаполнения.СоздатьЭлемент();
		НовыйШаблон.ЗаполнитьИзОбъекта(НаименованиеШаблона, ОбъектРодительВход, СписокРеквизитовВход, СписокТабличныхЧастейВход, АналитикаОтбора);
		НовыйШаблон.Записать();
		РезультатФункции = НовыйШаблон.Ссылка;
	Исключение
		ТекстСообщения = НСтр("ru = 'Не удалось создать шаблон по объекту %ОбъектРодитель%: %ОписаниеОшибки%'");
		ЕстьОшибки = Истина;
	КонецПопытки;
	Если ЕстьОшибки Тогда
		ОтменитьТранзакцию();
	Иначе
		ЗафиксироватьТранзакцию();
	КонецЕсли;
	Возврат РезультатФункции;
КонецФункции

&НаКлиенте
Процедура ПеренестиВШаблонЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		// Пользователь отказался. Не открываем.
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ПоказатьЗначение(, ДополнительныеПараметры);
	Иначе
		// Неизвестный вариант ответа. Ничего не делаем.
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбъектРодитель	 = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ОбъектРодитель", Неопределено);
	АналитикаОтбора	 = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "АналитикаОтбора", Неопределено);
	Если ОбъектРодитель <> Неопределено Тогда
		НаименованиеШаблона = Справочники.ШаблоныЗаполнения.ПолучитьНаименованиеШаблонаПоУмолчанию(ОбъектРодитель);
		// Заполнение таблицы реквизитов.
		ТаблицаРеквизитовРезультат = УправлениеШаблонамиЗаполненияУХ.ПолучитьТаблицуРеквизитовОбъекта(ОбъектРодитель);
		ТаблицаРеквизитов.Загрузить(ТаблицаРеквизитовРезультат);
		// Заполнение таблицы табличных частей.
		ТаблицаТабличныхЧастейРезультат = УправлениеШаблонамиЗаполненияУХ.ПолучитьТаблицуТабличныхЧастейОбъекта(ОбъектРодитель);
		ТабличныеЧастиСвертка = ТаблицаТабличныхЧастейРезультат.Скопировать();
		ТабличныеЧастиСвертка.Колонки.Добавить("КоличествоСтрок");
		Для Каждого ТекТабличныеЧастиСвертка Из ТабличныеЧастиСвертка Цикл
			ТекТабличныеЧастиСвертка.КоличествоСтрок = 1;
		КонецЦикла;
		ТабличныеЧастиСвертка.Свернуть("ТабличнаяЧасть, СинонимТабличнойЧасти", "КоличествоСтрок");
		ТаблицаТабличныхЧастей.Загрузить(ТабличныеЧастиСвертка);
	Иначе
		ТекстСообщения = НСтр("ru = 'Не удалось получить объект для заполнения.'");
		ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВШаблон(Команда)
	// Формируем список выбранных реквизитов.
	СписокРеквизитовДляПереноса = Новый СписокЗначений;
	Для Каждого ТекТаблицаРеквизитов Из ТаблицаРеквизитов Цикл
		Если ТекТаблицаРеквизитов.Использование Тогда
			СписокРеквизитовДляПереноса.Добавить(ТекТаблицаРеквизитов.НаименованиеРеквизита);
		Иначе
			Продолжить;
		КонецЕсли;
	КонецЦикла;
	// Формируем список выбранных табличных частей.
	СписокТабличныхЧастейДляПереноса = Новый СписокЗначений;
	Для Каждого ТекТаблицаТабличныхЧастей Из ТаблицаТабличныхЧастей Цикл
		Если ТекТаблицаТабличныхЧастей.Использование Тогда
			СписокТабличныхЧастейДляПереноса.Добавить(ТекТаблицаТабличныхЧастей.ТабличнаяЧасть);
		Иначе
			Продолжить;
		КонецЕсли;
	КонецЦикла;
	// Переносим списки в форму создания шаблона.
	Если (СписокРеквизитовДляПереноса.Количество() = 0) И ((СписокТабличныхЧастейДляПереноса.Количество() = 0)) Тогда
		ТекстСообщения = НСтр("ru = 'Не выбран ни один реквизит в шаблоне. Операция отменена.'");
		ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
	Иначе
		СозданныйШаблон = СоздатьШаблон(ОбъектРодитель, СписокРеквизитовДляПереноса, СписокТабличныхЧастейДляПереноса);
		Если ЗначениеЗаполнено(СозданныйШаблон) Тогда
			ЭтаФорма.ТолькоПросмотр = Истина;
			ТекстВопроса = НСтр("ru = 'Шаблон %НаименованиеШаблона% создан. Открыть его?'");
			ТекстВопроса = СтрЗаменить(ТекстВопроса, "%НаименованиеШаблона%", Строка(СозданныйШаблон));
			ОписаниеОповещения = Новый ОписаниеОповещения("ПеренестиВШаблонЗавершение", ЭтотОбъект, СозданныйШаблон);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Иначе
			// Пустой объект. Не открываем.
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьВсеРеквизиты(Команда)
	Для Каждого ТекТаблицаРеквизитов Из ТаблицаРеквизитов Цикл
		ТекТаблицаРеквизитов.Использование = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СнятьВыделениеРеквизитов(Команда)
	Для Каждого ТекТаблицаРеквизитов Из ТаблицаРеквизитов Цикл
		ТекТаблицаРеквизитов.Использование = Ложь;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьВсеТабличныеЧасти(Команда)
	Для Каждого ТекТаблицаТабличныхЧастей Из ТаблицаТабличныхЧастей Цикл
		ТекТаблицаТабличныхЧастей.Использование = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СнятьВыделениеТабличныхЧастей(Команда)
	Для Каждого ТекТаблицаТабличныхЧастей Из ТаблицаТабличныхЧастей Цикл
		ТекТаблицаТабличныхЧастей.Использование = Ложь;
	КонецЦикла; 
КонецПроцедуры
