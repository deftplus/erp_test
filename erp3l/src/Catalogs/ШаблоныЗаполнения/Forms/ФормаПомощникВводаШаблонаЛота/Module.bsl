// Выполняет считывание реквизитов из объекта ОбъектРодительВход.
&НаСервере
Процедура ЗагрузитьРеквизитыИзОбъектаРодителя(ОбъектРодительВход)
	Если ТипЗнч(ОбъектРодительВход) = Тип("СправочникСсылка.Лоты") Тогда
		ТаблицаРеквизитовРезультат = УправлениеШаблонамиЗаполненияУХ.ПолучитьТаблицуРеквизитовОбъекта(ОбъектРодительВход);
		Для Каждого ТекТаблицаРеквизитовРезультат Из ТаблицаРеквизитовРезультат Цикл
			Попытка
				ИмяРеквизита = ТекТаблицаРеквизитовРезультат.НаименованиеРеквизита;
				ЭтаФорма[ИмяРеквизита] = ТекТаблицаРеквизитовРезультат.ЗначениеРеквизита;
			Исключение
				ТекстСообщения = НСтр("ru = 'Во время получения значения реквизита %СинонимРеквизита% возникли ошибки: %ОписаниеОшибки%'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%СинонимРеквизита%", Строка(ТекТаблицаРеквизитовРезультат.СинонимРеквизита));
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%", ОписаниеОшибки());
				ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
			КонецПопытки;
		КонецЦикла;
	Иначе
		// Неизвестный вариант. Ничего не делаем.
	КонецЕсли;
КонецПроцедуры

// Выполняет считывание табличных частей из объекта ОбъектРодительВход.
&НаСервере
Процедура ЗагрузитьТабличныеЧастиИзОбъектаРодителя(ОбъектРодительВход)
	Если ТипЗнч(ОбъектРодительВход) = Тип("СправочникСсылка.Лоты") Тогда
		ТаблицаТабличныхЧастейРезультат = УправлениеШаблонамиЗаполненияУХ.ПолучитьТаблицуТабличныхЧастейОбъекта(ОбъектРодительВход);
		ТаблицаТабличныхЧастейРезультат.Сортировать("ТабличнаяЧасть Возр, НомерСтрокиТаблицы возр");
		ПоследнийНомерСтроки = -1;
		Для каждого ТекТабРеквизиты Из ТаблицаТабличныхЧастейРезультат Цикл
			Если ТекТабРеквизиты.ТабличнаяЧасть <> "" Тогда
				Попытка
					ТекНомерСтроки = ТекТабРеквизиты.НомерСтрокиТаблицы;
					Если ТекНомерСтроки <> ПоследнийНомерСтроки Тогда
						НоваяСтрока = ЭтаФорма[ТекТабРеквизиты.ТабличнаяЧасть].Добавить();	
					Иначе
						// Пользуемся уже существующей строкой.
					КонецЕсли;
					НоваяСтрока[ТекТабРеквизиты.НаименованиеРеквизита] = ТекТабРеквизиты.ЗначениеРеквизита;
					ПоследнийНомерСтроки = ТекНомерСтроки;
				Исключение
					ТекстСообщения = НСтр("ru = 'Во время считывания табличной части %ТабличнаяЧасть% возникли ошибки: %ОписаниеОшибки%'");
					ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ТабличнаяЧасть%", Строка(ТекТабРеквизиты.СинонимТабличнойЧасти));
					ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%", ОписаниеОшибки());
					ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
				КонецПопытки;
			Иначе
				// Это реквизит объекта.
			КонецЕсли;
		КонецЦикла;
	Иначе
		// Неизвестный вариант. Ничего не делаем.
	КонецЕсли;
КонецПроцедуры

// Добавляет в таблицу реквизитов шаблона ТаблицаВход новый реквизит из поля формы
// ИмяРеквизитаФормыВход с именем НаименованиеРеквизитаВход.
&НаСервере
Процедура ДобавитьЗначениеРеквизитаВТаблицу(ТаблицаВход, ИмяРеквизитаФормыВход, НаименованиеРеквизитаВход);
	НоваяСтрока = ТаблицаВход.Добавить();
	НоваяСтрока.НаименованиеРеквизита	 = НаименованиеРеквизитаВход;
	НоваяСтрока.ТабличнаяЧасть			 = "";
	НоваяСтрока.НомерСтрокиТаблицы		 = 0;
	НоваяСтрока.ЗначениеРеквизита		 = ЭтаФорма[ИмяРеквизитаФормыВход];
	НоваяСтрока.Комментарий				 = "";
КонецПроцедуры

// Добавляет в таблицу реквизитов шаблона ТаблицаВход значения реквизитов и табличных частей формы.
&НаСервере
Процедура ПеренестиРеквизитыФормыВТаблицу(ТаблицаВход)
	// Заполнение реквизитов.
	ДобавитьЗначениеРеквизитаВТаблицу(ТаблицаВход, "ПредметДоговора",									 "ПредметДоговора");
	ДобавитьЗначениеРеквизитаВТаблицу(ТаблицаВход, "ПояснениеУсловияПоставки",							 "ПояснениеУсловияПоставки");
	ДобавитьЗначениеРеквизитаВТаблицу(ТаблицаВход, "УсловияПоставкиИнкотермс",							 "УсловияПоставкиИнкотермс");
	ДобавитьЗначениеРеквизитаВТаблицу(ТаблицаВход, "АдресПредоставленияДокументации",					 "АдресПредоставленияДокументации");
	ДобавитьЗначениеРеквизитаВТаблицу(ТаблицаВход, "РазмерОплатыЗаПредоставлениеДокументации",			 "РазмерОплатыЗаПредоставлениеДокументации");
	ДобавитьЗначениеРеквизитаВТаблицу(ТаблицаВход, "УсловияОплаты",										 "УсловияОплаты");
	ДобавитьЗначениеРеквизитаВТаблицу(ТаблицаВход, "АдресПриемаДокументации",							 "АдресПриемаДокументации");
	ДобавитьЗначениеРеквизитаВТаблицу(ТаблицаВход, "АдресРассмотренияЗаявок",							 "АдресРассмотренияЗаявок");
	ДобавитьЗначениеРеквизитаВТаблицу(ТаблицаВход, "КоличествоМестСОбязанностьюЗаключитьДоговор",		 "КоличествоМестСОбязанностьюЗаключитьДоговор");
	ДобавитьЗначениеРеквизитаВТаблицу(ТаблицаВход, "ДатаПриказаНазначенияЗакупочнойКомиссии",			 "ДатаПриказаНазначенияЗакупочнойКомиссии");
	ДобавитьЗначениеРеквизитаВТаблицу(ТаблицаВход, "ОбщиеТребования",									 "ОбщиеТребования");
	ДобавитьЗначениеРеквизитаВТаблицу(ТаблицаВход, "ОписаниеГрафикаПоставки",							 "ОписаниеГрафикаПоставки");
	ДобавитьЗначениеРеквизитаВТаблицу(ТаблицаВход, "ТребованияКОформлениюДокументации",					 "ТребованияКОформлениюДокументации");
	ДобавитьЗначениеРеквизитаВТаблицу(ТаблицаВход, "ПорядокПредоставленияДокументации",					 "ПорядокПредоставленияДокументации");
	ДобавитьЗначениеРеквизитаВТаблицу(ТаблицаВход, "СрокИПорядокОплатыЗаПредоставлениеДокументации",	 "СрокИПорядокОплатыЗаПредоставлениеДокументации");
	ДобавитьЗначениеРеквизитаВТаблицу(ТаблицаВход, "ИсточникФинансирования",							 "ИсточникФинансирования");
	ДобавитьЗначениеРеквизитаВТаблицу(ТаблицаВход, "EMailПриема",										 "EMailПриема");
	ДобавитьЗначениеРеквизитаВТаблицу(ТаблицаВход, "ГрафикРаботы",										 "ГрафикРаботы");
	ДобавитьЗначениеРеквизитаВТаблицу(ТаблицаВход, "АдресПодведенияИтогов",								 "АдресПодведенияИтогов");
	ДобавитьЗначениеРеквизитаВТаблицу(ТаблицаВход, "МинимальноеКоличествоЗаявок",						 "МинимальноеКоличествоЗаявок");
	ДобавитьЗначениеРеквизитаВТаблицу(ТаблицаВход, "НомерПриказаНазначенияЗакупочнойКомиссии",			 "НомерПриказаНазначенияЗакупочнойКомиссии");
	// Заполнение табличных частей.
	Счетчик = 1;
	Для Каждого ТекЗакупочнаяКомиссия Из ЗакупочнаяКомиссия Цикл
		// Организация.
		НоваяСтрока = ТаблицаВход.Добавить();
		НоваяСтрока.НаименованиеРеквизита	 = "Организация";
		НоваяСтрока.ТабличнаяЧасть			 = "ЗакупочнаяКомиссия";
		НоваяСтрока.НомерСтрокиТаблицы		 = Счетчик;
		НоваяСтрока.ЗначениеРеквизита		 = ТекЗакупочнаяКомиссия.Организация;
		НоваяСтрока.Комментарий				 = "";
		// Должность.
		НоваяСтрока = ТаблицаВход.Добавить();
		НоваяСтрока.НаименованиеРеквизита	 = "Должность";
		НоваяСтрока.ТабличнаяЧасть			 = "ЗакупочнаяКомиссия";
		НоваяСтрока.НомерСтрокиТаблицы		 = Счетчик;
		НоваяСтрока.ЗначениеРеквизита		 = ТекЗакупочнаяКомиссия.Должность;
		НоваяСтрока.Комментарий				 = "";
		// Роль.
		НоваяСтрока = ТаблицаВход.Добавить();
		НоваяСтрока.НаименованиеРеквизита	 = "Роль";
		НоваяСтрока.ТабличнаяЧасть			 = "ЗакупочнаяКомиссия";
		НоваяСтрока.НомерСтрокиТаблицы		 = Счетчик;
		НоваяСтрока.ЗначениеРеквизита		 = ТекЗакупочнаяКомиссия.Роль;
		НоваяСтрока.Комментарий				 = "";
		// Сотрудник.
		НоваяСтрока = ТаблицаВход.Добавить();
		НоваяСтрока.НаименованиеРеквизита	 = "Сотрудник";
		НоваяСтрока.ТабличнаяЧасть			 = "ЗакупочнаяКомиссия";
		НоваяСтрока.НомерСтрокиТаблицы		 = Счетчик;
		НоваяСтрока.ЗначениеРеквизита		 = ТекЗакупочнаяКомиссия.Сотрудник;
		НоваяСтрока.Комментарий				 = "";
		Счетчик = Счетчик + 1;
	КонецЦикла;

КонецПроцедуры

// Распределяет значения из таблицы реквизитов шаблона ТаблицаВход по
// реквизитам формы.
&НаСервере
Процедура ПеренестиТаблицуВРеквизитыФормы(ТаблицаВход)
	// Перенос реквизитов.
	Попытка
		СтруктуруПоиска = Новый Структура;
		СтруктуруПоиска.Вставить("ТабличнаяЧасть", "");
		МассивРеквизитовШаблона = ТаблицаВход.НайтиСтроки(СтруктуруПоиска);
		Для Каждого ТекМассивРеквизитовШаблона Из МассивРеквизитовШаблона Цикл
			ЭтаФорма[ТекМассивРеквизитовШаблона.НаименованиеРеквизита] = ТекМассивРеквизитовШаблона.ЗначениеРеквизита;
		КонецЦикла;
	Исключение
		ТекстСообщения = НСтр("ru = 'Не удалось считать значения реквизитов: %ОписаниеОшибки%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%", ОписаниеОшибки());
		ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
	КонецПопытки;
	// Перенос табличных частей.
	ТаблицаВход.Сортировать("ТабличнаяЧасть Возр, НомерСтрокиТаблицы возр");
	ПоследнийНомерСтроки = -1;
	Для каждого ТекТаблицаВход Из ТаблицаВход Цикл
		Если ТекТаблицаВход.ТабличнаяЧасть = "ЗакупочнаяКомиссия" Тогда
			Попытка	
				ТекНомерСтроки = ТекТаблицаВход.НомерСтрокиТаблицы;
				Если ТекНомерСтроки <> ПоследнийНомерСтроки Тогда
					НоваяСтрока = ЭтаФорма[ТекТаблицаВход.ТабличнаяЧасть].Добавить();	
				Иначе
					// Пользуемся уже существующей строкой.
				КонецЕсли;
				НоваяСтрока[ТекТаблицаВход.НаименованиеРеквизита] = ТекТаблицаВход.ЗначениеРеквизита;
				ПоследнийНомерСтроки = ТекНомерСтроки;  
			Исключение
				ТекстСообщения = НСтр("ru = 'Не удалось считать значения табличной части %ТабличнаяЧасть%: %ОписаниеОшибки%'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%", ОписаниеОшибки());
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ТабличнаяЧасть%", Строка(ТекТаблицаВход.ТабличнаяЧасть));
				ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
			КонецПопытки;
		Иначе
			// Неизвестная табличная часть. Пропускаем.
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПеренестиТаблицуВРеквизитыФормы(Объект.РеквизитыШаблона);
	Иначе	
		Объект.Назначение = Перечисления.НазначенияШаблонов.Справочник;
		Объект.ИмяРодителя = "Лоты";
		Объект.ОбъектРодитель = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ОбъектРодитель", Неопределено);
		Объект.АналитикаОтбора = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "АналитикаОтбора", Справочники.Организации.ПустаяСсылка());
		Объект.Наименование = Справочники.ШаблоныЗаполнения.ПолучитьНаименованиеШаблонаПоУмолчанию(Объект.ОбъектРодитель);
		ЗагрузитьРеквизитыИзОбъектаРодителя(Объект.ОбъектРодитель);
		ЗагрузитьТабличныеЧастиИзОбъектаРодителя(Объект.ОбъектРодитель);
		ЭтаФорма.Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	// Инициализация.
	ТаблицаРеквизитовОбъекта = ТекущийОбъект.РеквизитыШаблона;
	ТаблицаРеквизитовОбъекта.Очистить();
	// Заполнение реквизитов.
	ПеренестиРеквизитыФормыВТаблицу(ТаблицаРеквизитовОбъекта);
КонецПроцедуры
