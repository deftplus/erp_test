// Заполняет таблицу вариантов заполнения шаблонов согласно данным информационной базы.
&НаСервере
Процедура ЗаполнитьТаблицуВариантов()
	ТаблицаВариантовЗаполнения.Очистить();
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ВариантыЗаполненияШаблонов.Ссылка КАК Ссылка,
		|	ВариантыЗаполненияШаблонов.Назначение,
		|	ВариантыЗаполненияШаблонов.ИмяРодителя
		|ИЗ
		|	Справочник.ВариантыЗаполненияШаблонов КАК ВариантыЗаполненияШаблонов
		|ГДЕ
		|	ВариантыЗаполненияШаблонов.Назначение = &Назначение
		|	И ВариантыЗаполненияШаблонов.ИмяРодителя = &ИмяРодителя
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка";
	Запрос.УстановитьПараметр("ИмяРодителя", "НастройкиПроцессаВыбора");
	Запрос.УстановитьПараметр("Назначение", Перечисления.НазначенияШаблонов.Справочник);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Выбрано = Ложь;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НоваяСтрока = ТаблицаВариантовЗаполнения.Добавить();
		Если НЕ Выбрано Тогда
			НоваяСтрока.Использование = Истина;
			Выбрано = Истина;
		Иначе
			НоваяСтрока.Использование = Ложь;
		КонецЕсли;
		НоваяСтрока.ВариантЗаполненияШаблона = ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла;
КонецПроцедуры

// Возвращает выбранный пользователем элемент варианта заполнения шаблона.
&НаКлиенте
Функция ПолучитьВыбранныйВариант()
	РезультатФункции = ПредопределенноеЗначение("Справочник.ВариантыЗаполненияШаблонов.ПустаяСсылка");
	Найдено = Ложь;
	Для Каждого ТекТаблицаВариантовЗаполнения Из ТаблицаВариантовЗаполнения Цикл
		Если ТекТаблицаВариантовЗаполнения.Использование Тогда
			Если НЕ Найдено Тогда
				РезультатФункции = ТекТаблицаВариантовЗаполнения.ВариантЗаполненияШаблона;
				Найдено = Истина;
			Иначе
				РезультатФункции = ПредопределенноеЗначение("Справочник.ВариантыЗаполненияШаблонов.ПустаяСсылка");
				Прервать;			// Два значения.
			КонецЕсли;
		Иначе
			Продолжить;		// Выполняем поиск далее.
		КонецЕсли;
	КонецЦикла;
	Возврат РезультатФункции;
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	НастройкаПроцессаВыбора = Параметры.НастройкаПроцессаВыбора;
	НаименованиеШаблона = Справочники.ШаблоныЗаполнения.ПолучитьНаименованиеШаблонаПоУмолчанию(НастройкаПроцессаВыбора);
	ЗаполнитьТаблицуВариантов();
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаВариантовЗаполненияИспользованиеПриИзменении(Элемент)
	ТекДанные = Элементы.ТаблицаВариантовЗаполнения.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		Для Каждого ТекТаблицаВариантовЗаполнения Из ТаблицаВариантовЗаполнения Цикл
			ВариантыСовпадают = (ТекТаблицаВариантовЗаполнения.ВариантЗаполненияШаблона = ТекДанные.ВариантЗаполненияШаблона);
			ТекТаблицаВариантовЗаполнения.Использование = ВариантыСовпадают;
		КонецЦикла;
	Иначе
		// Строка не выбрана. Ничего не делаем.
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СоздатьШаблон(Команда)
	ВыбранныйВариант = ПолучитьВыбранныйВариант();
	Если ЗначениеЗаполнено(ВыбранныйВариант) Тогда
		СтруктураВыбора = Новый Структура;
		СтруктураВыбора.Вставить("ВыбранныйВариант", ВыбранныйВариант);
		СтруктураВыбора.Вставить("Наименование", НаименованиеШаблона);
		ОповеститьОВыборе(СтруктураВыбора);
	Иначе
		ТекстСообщения = НСтр("ru = 'Не удалось определить выбранный вариант заполнения шаблона. Операция отменена.'");
		ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
	КонецЕсли;
КонецПроцедуры
