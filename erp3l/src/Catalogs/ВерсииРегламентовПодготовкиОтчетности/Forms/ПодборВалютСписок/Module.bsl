&НаКлиенте
Перем ФормаВыбораВалют;

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если ПроверитьЗаполненностьВалют() Тогда
		ОповеститьОВыборе(Новый ФиксированнаяСтруктура("ДополнительныеВалюты, ИдентификаторСтроки", ДополнительныеВалюты, ИдентификаторСтроки));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Если ЗначениеЗаполнено(Параметры.ДополнительныеВалюты) Тогда
		
		Для Каждого ТекВалюта ИЗ Параметры.ДополнительныеВалюты Цикл
			
			НоваяСтрока = ДополнительныеВалюты.Добавить();
			НоваяСтрока.Валюта = ТекВалюта.Значение;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ИдентификаторСтроки = Параметры.ИдентификаторСтроки;
	ОсновнаяВалюта = Параметры.ОсновнаяВалюта;
	ДополнительныеВалюты.Сортировать("Валюта Возр");
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполненностьВалют()
	
	Отказ = Ложь;
	
	Для Каждого Строка Из ДополнительныеВалюты Цикл
		
		Если НЕ ЗначениеЗаполнено(Строка.Валюта) Тогда
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст = НСтр("ru = 'Пустое поле валюты!'");
			СообщениеПользователю.Поле = "ДополнительныеВалюты[" +ДополнительныеВалюты.Индекс(Строка) +  "].Валюта";
			СообщениеПользователю.Сообщить();
			Элементы.ДополнительныеВалюты.ТекущаяСтрока = Строка.ПолучитьИдентификатор();
			
			Отказ = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат НЕ Отказ;
		
КонецФункции

&НаКлиенте
Процедура ДополнительныеВалютыВалютаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ДополнительныеВалюты.ТекущиеДанные;
	ТекущаяВалюта = ТекущиеДанные.Валюта;
	
	Элемент.СписокВыбора.ЗагрузитьЗначения(ПолучитьМассивВалют(ТекущаяВалюта));
	
	Если ЗначениеЗаполнено(ТекущаяВалюта) тогда		
		Если Элемент.ТекстРедактирования = "" тогда   //нужно для отображения текущего значения в ячейке
			ТекущиеДанные.Валюта = ТекущаяВалюта;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьМассивВалют(ТекущаяВалюта = неопределено)

	МассивВалют = Новый Массив;
	
	Если ЗначениеЗаполнено(ТекущаяВалюта) тогда		
		МассивВалют.Добавить(ТекущаяВалюта);
	КонецЕсли;
	
	ВыборкаВалют = Справочники.Валюты.Выбрать();
	ДополнительныеВалютыТаблица = РеквизитФормыВЗначение("ДополнительныеВалюты");
	
	Пока ВыборкаВалют.Следующий() Цикл
		Если ВыборкаВалют.Ссылка <> ОсновнаяВалюта
			И ДополнительныеВалютыТаблица.Найти(ВыборкаВалют.Ссылка, "Валюта") = неопределено тогда
			
			МассивВалют.Добавить(ВыборкаВалют.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивВалют;
	
КонецФункции

&НаКлиенте
Процедура КомандаПодбор(Команда)
		
	ПараметрыФормы = Новый Структура("РежимВыбора", Истина);
	ФормаВыбораВалют = ПолучитьФорму("Справочник.Валюты.Форма.ФормаСписка", ПараметрыФормы, Элементы.ДополнительныеВалюты, Истина);
			
	ФормаВыбораВалют.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ФормаВыбораВалют.ЗакрыватьПриВыборе = Ложь;
	ФормаВыбораВалют.ЗакрыватьПриЗакрытииВладельца = Истина;
		
	ЭлементОтбора = ФормаВыбораВалют.Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	ЭлементОтбора.Использование = Истина;
		
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Ссылка");
	ЭлементОтбора.ПравоеЗначение = ПолучитьМассивВалют();
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("Подключаемый_ЗакрытиеФормыПодбора", ЭтотОбъект);
	ФормаВыбораВалют.ОписаниеОповещенияОЗакрытии = ОповещениеОЗакрытии;
	ФормаВыбораВалют.Открыть();
			
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗакрытиеФормыПодбора(Результат, ДополнительныеПараметры) Экспорт
	
	ФормаВыбораВалют = неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеВалютыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	НоваяСтрока = ДополнительныеВалюты.Добавить();
	НоваяСтрока.Валюта = ВыбранноеЗначение;
	
	Если ТипЗнч(ФормаВыбораВалют) = Тип("ФормаКлиентскогоПриложения")
		И ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Валюты") тогда
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаВыбораВалют, "Список") тогда
			Если ТипЗнч(ФормаВыбораВалют.Список) = Тип("ДинамическийСписок") тогда
				Если ФормаВыбораВалют.Список.Отбор.Элементы.Количество() = 1 тогда
					
					МассивВалют = ФормаВыбораВалют.Список.Отбор.Элементы[0].ПравоеЗначение;
					
					Если ТипЗнч(МассивВалют) = Тип("Массив") тогда
						
						ИндексВыбраннойВалюты = МассивВалют.Найти(ВыбранноеЗначение);
						
						Если ИндексВыбраннойВалюты <> неопределено тогда
							МассивВалют.Удалить(ИндексВыбраннойВалюты);
						КонецЕсли;
						
						ФормаВыбораВалют.Список.Отбор.Элементы[0].ПравоеЗначение = МассивВалют;
												
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры
