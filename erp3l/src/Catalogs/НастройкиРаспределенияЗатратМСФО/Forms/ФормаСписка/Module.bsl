
&НаКлиенте
Процедура КомандаСкопироватьНастройкиВНовыйПериод(Команда)
	
	Перем Параметры_;
	
	Параметры_ = Новый Структура;
	ОписаниеОповещения_ = Новый ОписаниеОповещения("СкопироватьЗаписиПоВыбраннымНастройкам", ЭтаФорма);
	
	ТекДанные = Элементы.Список.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		ИскСвойство = Неопределено;
		Если ТекДанные.Свойство("ПериодСценария", ИскСвойство) Тогда
			Параметры_.Вставить("ПериодСценария", ИскСвойство);
		КонецЕсли;
		ИскСвойство = Неопределено;
		Если ТекДанные.Свойство("Сценарий", ИскСвойство) Тогда
			Параметры_.Вставить("Сценарий", ИскСвойство);
		КонецЕсли;
		ИскСвойство = Неопределено;
		Если ТекДанные.Свойство("Организация", ИскСвойство) Тогда
			Параметры_.Вставить("Организация", ИскСвойство);
		КонецЕсли;
	КонецЕсли;
	ОткрытьФорму("Справочник.НастройкиРаспределенияЗатратМСФО.Форма.ФормаВыбораПериодаИСценария", Параметры_, ЭтаФорма,,ЭтаФорма.Окно,,ОписаниеОповещения_, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьЗаписиПоВыбраннымНастройкам(Результат, ДопПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СкопироватьЗаписиПоВыбраннымНастройкамНаСервере(Результат);
	
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьИнтерфейс", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьИнтерфейс()
	ОбновитьИнтерфейс();
КонецПроцедуры

&НаСервере
Процедура СкопироватьЗаписиПоВыбраннымНастройкамНаСервере(Результат)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НастройкаРаспределенияЗатратМСФО.Ссылка
	|ИЗ
	|	Справочник.НастройкиРаспределенияЗатратМСФО КАК НастройкаРаспределенияЗатратМСФО
	|ГДЕ
	|	НастройкаРаспределенияЗатратМСФО.Сценарий = &Сценарий
	|	И НастройкаРаспределенияЗатратМСФО.ПериодСценария = &ПериодСценария
	|	И НастройкаРаспределенияЗатратМСФО.Организация = &Организация
	|	И НЕ НастройкаРаспределенияЗатратМСФО.ПометкаУдаления");
	
	Запрос.УстановитьПараметр("Сценарий", 		Результат.СценарийИсточник);
	Запрос.УстановитьПараметр("ПериодСценария", Результат.ПериодИсточник);
	Запрос.УстановитьПараметр("Организация", 	Результат.ОрганизацияИсточник);
		
	Выборка = Запрос.Выполнить().Выбрать();	
	Пока Выборка.Следующий() Цикл
		
		Объект = Справочники.НастройкиРаспределенияЗатратМСФО.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(Объект, Выборка.Ссылка, , "Код,Родитель,Владелец");
		
		Объект.Сценарий 		= Результат.СценарийПриемник;
		Объект.ПериодСценария 	= Результат.ПериодПриемник;
		Объект.Организация 		= Результат.ОрганизацияПриемник;
		
		Объект.Записать();
		
	КонецЦикла; 
		
КонецПроцедуры