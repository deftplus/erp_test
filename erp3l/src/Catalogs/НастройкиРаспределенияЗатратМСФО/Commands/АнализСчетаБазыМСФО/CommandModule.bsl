
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПараметрыФормы = ПолучитьПараметры(ПараметрКоманды);
	ОткрытьФорму("Отчет.ОСВМСФО.Форма", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, 
					ПараметрыВыполненияКоманды.Уникальность, , );
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПараметры(ПараметрКоманды) Экспорт

	ПериодОтчета = Неопределено;
	Если ЗначениеЗаполнено(ПараметрКоманды.ПериодСценария) Тогда
		ПериодОтчета = Новый СтандартныйПериод(ПараметрКоманды.ПериодСценария.ДатаНачала, ПараметрКоманды.ПериодСценария.ДатаОкончания);
	КонецЕсли;
	
	Отбор = Новый Структура;	
	ДобавитьОтборОтчета(Отбор, "Период", 		ПериодОтчета);
	ДобавитьОтборОтчета(Отбор, "Организация", 	ПараметрКоманды.Организация);
	ДобавитьОтборОтчета(Отбор, "Сценарий", 		ПараметрКоманды.Сценарий);
	
	Если ПараметрКоманды.СпособЗакрытия = ПредопределенноеЗначение("Перечисление.СпособыЗакрытияСчетовЗатрат.ПропорциональноОборотуНсбуДтКт") Тогда
		
		ДобавитьОтборОтчета(Отбор, "КорСчет", 					ПараметрКоманды.СчетИсточник.СчетСсылка);
		ДобавитьОтборОтчета(Отбор, "КорНаправлениеДеятельности",ПараметрКоманды.СчетИсточникНаправлениеДеятельности);
		ДобавитьОтборОтчета(Отбор, "КорПодразделение", 			ПараметрКоманды.СчетИсточникПодразделение);
		ДобавитьОтборОтчета(Отбор, "КорСубконто1", 				ПараметрКоманды.СчетИсточникСубконто1);
		ДобавитьОтборОтчета(Отбор, "КорСубконто2", 				ПараметрКоманды.СчетИсточникСубконто2);
		ДобавитьОтборОтчета(Отбор, "КорСубконто3", 				ПараметрКоманды.СчетИсточникСубконто3);
		
	КонецЕсли;
	
	ДобавитьОтборОтчета(Отбор, "Счет", 						ПараметрКоманды.СчетПриемник.СчетСсылка);
	ДобавитьОтборОтчета(Отбор, "НаправлениеДеятельности", 	ПараметрКоманды.СчетПриемникНаправлениеДеятельности);
	ДобавитьОтборОтчета(Отбор, "Подразделение", 			ПараметрКоманды.СчетПриемникПодразделение);
	ДобавитьОтборОтчета(Отбор, "Субконто1", 				ПараметрКоманды.СчетПриемникСубконто1);
	ДобавитьОтборОтчета(Отбор, "Субконто2", 				ПараметрКоманды.СчетПриемникСубконто2);
	ДобавитьОтборОтчета(Отбор, "Субконто3", 				ПараметрКоманды.СчетПриемникСубконто3);
	
	Группировки = Новый Массив;
	Если Не Отбор.Свойство("Организация") Тогда
		Группировки.Добавить("Организация");
	КонецЕсли;
	Группировки.Добавить("Счет");
	Группировки.Добавить("КорСчет");
	Группировки.Добавить("ТипОперации");
	Отбор.Вставить("Группировка", Группировки);
	
	Возврат Новый Структура("Отбор,КлючВарианта,СформироватьПриОткрытии,КлючПользовательскихНастроек", 
							Отбор, "АнализСчета", Истина, "");

КонецФункции

Функция ДобавитьОтборОтчета(Отбор, ИмяРеквизита, ЗначениеРеквизита = Неопределено)

	Если Не ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
		Возврат Неопределено;
	КонецЕсли;

	Отбор.Вставить(ИмяРеквизита, ЗначениеРеквизита);
	
КонецФункции

