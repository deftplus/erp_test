
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИсходныйТекст = ВРЕГ(Символы.ПС + СтрЗаменить(Параметры.ИсходныйТекст, " ", ""));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПоказателиОтчетов.Код,
	|	ПоказателиОтчетов.Ссылка,
	|	ПоказателиОтчетов.Наименование
	|ИЗ
	|	Справочник.ПоказателиОтчетов КАК ПоказателиОтчетов
	|ГДЕ
	|	ПоказателиОтчетов.Владелец = &ВидОтчета
	|	И (НЕ ПоказателиОтчетов.ПометкаУдаления)";
	
	Запрос.УстановитьПараметр("ВидОтчета", Параметры.ВидОтчета);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ПредставлениеПоказателя = "Показатели." + СокрЛП(Выборка.Код) + "=";
		
		Если СтрНайти(ИсходныйТекст, Символы.ПС + ВРЕГ(ПредставлениеПоказателя)) = 0 Тогда
			
			НоваяСтрока                         = Показатели.Добавить();
			НоваяСтрока.Пометка                 = Истина;
			НоваяСтрока.Код                     = Выборка.Код;
			НоваяСтрока.Наименование            = Выборка.Наименование;
			НоваяСтрока.ПредставлениеПоказателя = ПредставлениеПоказателя + Символы.ПС;
			
		КонецЕсли;
	КонецЦикла;
	
	Если Показатели.Количество() = 0 Тогда
		
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = Нстр("ru = 'Отсутствуют показатели для вставки!'");
		СообщениеПользователю.Сообщить();
		
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	МестоВставки = 1;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ТекстДляВставки = "";
	
	ВыделенныеСтроки = Показатели.НайтиСтроки(Новый Структура("Пометка", Истина));
	Для Каждого Элемент Из ВыделенныеСтроки Цикл
		ТекстДляВставки = ТекстДляВставки + Элемент.ПредставлениеПоказателя;
	КонецЦикла;
	
	Оповестить("ВставитьПоказатели", Новый Структура("МестоВставки, ТекстДляВставки", МестоВставки, ТекстДляВставки));
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьВсе(Команда)
	
	Для Каждого Строка Из Показатели Цикл
		Строка.Пометка = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВыделение(Команда)
	
	Для Каждого Строка Из Показатели Цикл
		Строка.Пометка = Ложь;
	КонецЦикла;

КонецПроцедуры
