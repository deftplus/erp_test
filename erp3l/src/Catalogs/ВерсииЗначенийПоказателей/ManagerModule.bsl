Процедура ВыполнитьПереносЗаписейРегистров(Версия,МаскаИмениИсходный,МаскаИмениНовый,Отказ) Экспорт
	
	Если НЕ ПроверитьВозможностьИзмененияДанныхВерсии(Версия,МаскаИмениИсходный) Тогда
		
		Отказ=Истина;	
		Возврат;
		
	КонецЕсли;
		
	НачатьТранзакцию();
	
	Если НЕ ВыполнитьПереносИзРегистраВРегистр(Версия,МаскаИмениИсходный,МаскаИмениНовый,"Синтетика") Тогда
		
		Отказ=Истина;
		ОтменитьТранзакцию();
		Возврат;
		
	КонецЕсли;
	
	Если НЕ ВыполнитьПереносИзРегистраВРегистр(Версия,МаскаИмениИсходный,МаскаИмениНовый,"Валютные") Тогда
		
		Отказ=Истина;
		ОтменитьТранзакцию();
		Возврат;
		
	КонецЕсли;
	
	Если НЕ ВыполнитьПереносИзРегистраВРегистр(Версия,МаскаИмениИсходный,МаскаИмениНовый,"Нечисловые") Тогда
		
		Отказ=Истина;
		ОтменитьТранзакцию();
		Возврат;
		
	КонецЕсли;
	
	Для Индекс=1 По ПараметрыСеанса.ЧислоДопАналитик Цикл
		
		Если НЕ ВыполнитьПереносИзРегистраВРегистр(Версия,МаскаИмениИсходный,МаскаИмениНовый,Индекс) Тогда
			
			Отказ=Истина;
			ОтменитьТранзакцию();
			Возврат;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
			
КонецПроцедуры // ВыполнитьПереносЗаписейРегистров()

Функция ВыполнитьПереносИзРегистраВРегистр(Версия, МаскаИмениИсходный,МаскаИмениНовый,ТекущийПостфикс)
	
	МассивЗаписейИсходный=РегистрыСведений[МаскаИмениИсходный+ТекущийПостфикс].СоздатьНаборЗаписей();
	МассивЗаписейНовый=РегистрыСведений[МаскаИмениНовый+ТекущийПостфикс].СоздатьНаборЗаписей();
		
	МассивЗаписейИсходный.Отбор.Версия.Установить(Версия);
	МассивЗаписейНовый.Отбор.Версия.Установить(Версия);
	
	МассивЗаписейИсходный.Прочитать();
	
	Для Каждого Строка ИЗ МассивЗаписейИсходный Цикл
		
		НоваяСтрока=МассивЗаписейНовый.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Строка);
		
	КонецЦикла;
	
	МассивЗаписейИсходный.Очистить();
	
	Попытка
		
		МассивЗаписейИсходный.Записать();
		МассивЗаписейНовый.Записать(Истина);
		
		Возврат Истина;
		
	Исключение
		
		СтрокаШаблона = НСтр("ru = 'Перенос данных по версии значений показатетей %1 не выполнен.'");
		Если ЗначениеЗаполнено(СтрокаШаблона) тогда
			ОбщегоНазначенияУХ.СообщитьОбОшибке(СтрШаблон(СтрокаШаблона, Версия) + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),,, СтатусСообщения.Важное);
		КонецЕсли;
		
		Возврат Ложь;
		
	КонецПопытки;	
	
КонецФункции // ВыполнитьПереносИзРегистраВРегистр() 

Функция ПроверитьВозможностьИзмененияДанныхВерсии(Версия,МаскаИмени="") Экспорт
	
	Если ПустаяСтрока(МаскаИмени) Тогда
		
		МаскаИмени="ЗначенияПоказателейОтчетов";
		
	КонецЕсли;
		
	АналитикВидаОтчета=ОбщегоНазначенияУХ.ВернутьКоличествоАналитикНаУровнеОтчета(Версия.ВидОтчета);
	
	ТаблицаСинтетики=?(АналитикВидаОтчета=0,МаскаИмени+"Синтетика",МаскаИмени+АналитикВидаОтчета);
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	ЗначенияПоказателейОтчетовСинтетика.Показатель
	|ИЗ
	|	РегистрСведений."+ТаблицаСинтетики+" КАК ЗначенияПоказателейОтчетовСинтетика
	|ГДЕ
	|	ЗначенияПоказателейОтчетовСинтетика.Версия = &Версия
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗначенияПоказателейОтчетовНечисловые.Показатель
	|ИЗ
	|	РегистрСведений.ЗначенияПоказателейОтчетовНечисловые КАК ЗначенияПоказателейОтчетовНечисловые
	|ГДЕ
	|	ЗначенияПоказателейОтчетовНечисловые.Версия = &Версия";
	
	Запрос.УстановитьПараметр("Версия",Версия);
	
	Отказ=Ложь;
	
	ОбъектРасчета=Новый Структура("Организация,ПериодОтчета,Сценарий,Проект,ВидОтчета");
	ЗаполнитьЗначенияСвойств(ОбъектРасчета,Версия);
	
	УправлениеОтчетамиУХ.ПроверитьВозможностьЗаписи(ОбъектРасчета,Запрос.Выполнить().Выгрузить(),Отказ);
	
	Возврат НЕ Отказ;
		
КонецФункции // ПроверитьВозможностьИзмененияДанныхВерсии()

Функция УдалитьЗаписиВерсии(Версия,ПроверятьВозможностьИзмененияДанных=Истина) Экспорт
	
	Если ПроверятьВозможностьИзмененияДанных И (НЕ ПроверитьВозможностьИзмененияДанныхВерсии(Версия)) Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Для Инд=1 По ПараметрыСеанса.ЧислоДопАналитик Цикл
		
		НаборЗаписей = РегистрыСведений["ЗначенияПоказателейОтчетов"+Инд].СоздатьНаборЗаписей(); 	
		НаборЗаписей.Отбор.Версия.Установить(Версия);
		НаборЗаписей.Записать(Истина);
		
	КонецЦикла;
	
	НаборЗаписей = РегистрыСведений.ЗначенияПоказателейОтчетовСинтетика.СоздатьНаборЗаписей(); 	
	НаборЗаписей.Отбор.Версия.Установить(Версия);
	НаборЗаписей.Записать(Истина);

	
	НаборЗаписей = РегистрыСведений.ЗначенияПоказателейОтчетовВалютные.СоздатьНаборЗаписей(); 	
	НаборЗаписей.Отбор.Версия.Установить(Версия);
	НаборЗаписей.Записать(Истина);

	
	НаборЗаписей = РегистрыСведений.ЗначенияПоказателейОтчетовНечисловые.СоздатьНаборЗаписей(); 	
	НаборЗаписей.Отбор.Версия.Установить(Версия);
	НаборЗаписей.Записать(Истина);
	
	НаборЗаписей = РегистрыСведений.КомментарииЗначенийПоказателей.СоздатьНаборЗаписей(); 	
	НаборЗаписей.Отбор.Версия.Установить(Версия);
	НаборЗаписей.Записать(Истина);
	
	Возврат Истина;	
	
КонецФункции // УдалитьЗаписиВерсии()