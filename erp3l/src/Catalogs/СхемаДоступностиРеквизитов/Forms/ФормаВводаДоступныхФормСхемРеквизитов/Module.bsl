// Возвращает списокдоступных форм по метаданным объекта БД ОбъектБдВход.
&НаСервере
Функция ПолучитьСписокФормОбъектаБд(ОбъектБдВход)
	// Инициализация.
	РезультатФункции = Новый СписокЗначений; 
	// Получим набор метаданных для обхода форм.
	НаименованиеОбъектаБд = ОбъектБдВход.Наименование;
	Если ТипЗнч(ОбъектБД) = Тип("СправочникСсылка.СправочникиБД") Тогда
		НаборМетаданных = Метаданные.Справочники[НаименованиеОбъектаБд];	
	ИначеЕсли ТипЗнч(ОбъектБД) = Тип("СправочникСсылка.ДокументыБД") Тогда
		НаборМетаданных = Метаданные.Документы[НаименованиеОбъектаБд];	
	Иначе
		НаборМетаданных = Неопределено;
	КонецЕсли;
	// Добавим все формы из полученных метаданных.
	Если НаборМетаданных <> Неопределено Тогда
		Для Каждого ФормаОбъекта Из НаборМетаданных.Формы Цикл
			РезультатФункции.Добавить(СокрЛП(ФормаОбъекта.Имя));
		КонецЦикла;	
	Иначе
		РезультатФункции = Новый СписокЗначений;
	КонецЕсли;
	Возврат РезультатФункции;		
КонецФункции		// ПолучитьСписокФормОбъектаБд()	

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// Параметр СписокДоступныхФормСхемРеквизитов.
	Для Каждого ТекСписокДоступныхФормСхемРеквизитов Из Параметры.СписокДоступныхФормСхемРеквизитов Цикл
		НоваяСтрока = ТаблицаДоступныхФормСхемРеквизитов.Добавить();
		НоваяСтрока.ИмяДоступнойФормы = ТекСписокДоступныхФормСхемРеквизитов.Значение;
	КонецЦикла;	                                  
	// Параметр ОбъектБД.
	ОбъектБД = Параметры.ОбъектБД;
КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
	МассивРезультат = Новый Массив;
	Для Каждого ТекТаблицаДоступныхФормСхемРеквизитов Из ТаблицаДоступныхФормСхемРеквизитов Цикл
		МассивРезультат.Добавить(ТекТаблицаДоступныхФормСхемРеквизитов.ИмяДоступнойФормы);
	КонецЦикла;	
	МассивРезультат = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивРезультат);
	МассивРезультат = ОбщегоНазначенияКлиентСерверУХ.СортироватьМассив(МассивРезультат);
	СписокРезультат = Новый СписокЗначений;
	СписокРезультат.ЗагрузитьЗначения(МассивРезультат);
	Закрыть(СписокРезультат);
КонецПроцедуры

&НаКлиенте
Процедура СписокФорм_ПослеВыбора(ЗначениеВыбора, ДопПараметры) Экспорт
	Если ТипЗнч(ЗначениеВыбора) = Тип("ЭлементСпискаЗначений") Тогда
		НовоеИмяФормы = ЗначениеВыбора.Значение;
		Если ТипЗнч(НовоеИмяФормы) = Тип("Строка") Тогда
			Если СокрЛП(НовоеИмяФормы) <> "" Тогда
				ТекДанные = Элементы.ТаблицаДоступныхФормСхемРеквизитов.ТекущиеДанные;
				Если ТекДанные <> Неопределено Тогда
					ТекДанные.ИмяДоступнойФормы = СокрЛП(НовоеИмяФормы);
				Иначе
					// Строка не выбрана. Ничего не делаем.
				КонецЕсли;
			Иначе
				ТекстСообщения = НСтр("ru = 'Не удалось получить имя формы.'");
				ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
			КонецЕсли;
		Иначе
			// Неизвестный вариант. Пропускаем.
		КонецЕсли;
	Иначе
		// Пользователь отказался. Ничего не делаем.
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДоступныхФормСхемРеквизитовИмяДоступнойФормыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СписокФорм = ПолучитьСписокФормОбъектаБд(ОбъектБД);	
	Оповещение = Новый ОписаниеОповещения("СписокФорм_ПослеВыбора", ЭтотОбъект);
	ПоказатьВыборИзСписка(Оповещение, СписокФорм);
КонецПроцедуры
