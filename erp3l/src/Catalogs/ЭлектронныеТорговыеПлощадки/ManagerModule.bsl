#Если НаСервере Тогда
	
	
// Возвращает имя текущей обработки для вызова функций интеграции указанной ЭТП.
// Параметры:
//	ЭТП - СправочникСсылка.ЭлектронныеТорговыеПлощадки.
//	ВерсияОбработки - Число, Неопределено. Необязательный. Если задано, то возвращает требуемую версию,
//		иначе возвращает версию выбранную в настройках ЭТП (см. реквизит "ВерсияОбработки").
//	ВнешняяОбработка - Булево, в переменную будет записано значение, внешняя-ли это обработка.
// Возвращает:
//	Строка, имя обработкиесли удалось определить обработку для ЭТП.
//	Неопределено, если имя определить не удалось.
//
Функция ПолучитьИмяОбработкиИнтеграции(ЭТП, ЭтоВнешняяОбработка, ВерсияОбработки=Неопределено) Экспорт
	Если НЕ ЗначениеЗаполнено(ЭТП) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Попытка
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ЭлектронныеТорговыеПлощадкиВерсииОбработки.ЭтоДопОбработка,
			|	ЭлектронныеТорговыеПлощадкиВерсииОбработки.ИмяОбработки,
			|	ЭлектронныеТорговыеПлощадкиВерсииОбработки.ДопОбработка
			|ИЗ
			|	Справочник.ЭлектронныеТорговыеПлощадки КАК ЭлектронныеТорговыеПлощадки
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭлектронныеТорговыеПлощадки.ВерсииОбработки КАК ЭлектронныеТорговыеПлощадкиВерсииОбработки
			|		ПО (ЭлектронныеТорговыеПлощадкиВерсииОбработки.Ссылка = ЭлектронныеТорговыеПлощадки.Ссылка)
			|			И (ЭлектронныеТорговыеПлощадкиВерсииОбработки.ВерсияОбработки = ЭлектронныеТорговыеПлощадки.ВерсияОбработки)
			|			И (ЭлектронныеТорговыеПлощадкиВерсииОбработки.НеИспользуется = ЛОЖЬ)
			|ГДЕ
			|	ЭлектронныеТорговыеПлощадки.Ссылка = &ЭТП";
			
		Запрос.УстановитьПараметр("ЭТП", ЭТП);
		
		Если ЗначениеЗаполнено(ВерсияОбработки) Тогда
			Запрос.УстановитьПараметр("ВерсияОбработки", ВерсияОбработки);
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ЭлектронныеТорговыеПлощадкиВерсииОбработки.ВерсияОбработки = ЭлектронныеТорговыеПлощадки.ВерсияОбработки", "И (ЭлектронныеТорговыеПлощадкиВерсииОбработки.ВерсияОбработки = &ВерсияОбработки)");
		КонецЕсли;                                   
		
		РезультатЗапроса = Запрос.Выполнить();
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			ЭтоВнешняяОбработка = ВыборкаДетальныеЗаписи.ЭтоДопОбработка;
			Если ЭтоВнешняяОбработка Тогда
				Возврат ДополнительныеОтчетыИОбработки.ПодключитьВнешнююОбработку(ВыборкаДетальныеЗаписи.ДопОбработка);
			Иначе
				Возврат ВыборкаДетальныеЗаписи.ИмяОбработки;
			КонецЕсли;
		КонецЕсли;
	
	Исключение
		Инфо = ИнформацияОбОшибке();
		ИнтеграцияЦУЗсЭТПСерверУХ.ЗаписатьОшибкуИнтеграцииВЖурналаРегистрации(ПодробноеПредставлениеОшибки(Инфо), ЭТП.Наименование)
	КонецПопытки;
	
	Возврат Неопределено;
КонецФункции

// Возвращает текущую обработку для вызова функций интеграции указанной ЭТП.
// Параметры:
//	ЭТП - СправочникСсылка.ЭлектронныеТорговыеПлощадки.
//	ВерсияОбработки - Число, Неопределено. Необязательный. Если задано, то возвращает требуемую версию,
//		иначе возвращает версию выбранную в настройках ЭТП (см. реквизит "ВерсияОбработки").
// Возвращает:
//	ОбработкаОбъект, если удалось определить обработку для ЭТП.
//	Неопределено, если определить не удалось.
//
Функция ПолучитьОбработкуИнтеграции(ЭТП, ВерсияОбработки=Неопределено) Экспорт
	Если НЕ ЗначениеЗаполнено(ЭТП) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Попытка
		ЭтоВнешняяОбработка = Неопределено;
		ИмяОбработки = ПолучитьИмяОбработкиИнтеграции(ЭТП, ЭтоВнешняяОбработка, ВерсияОбработки);
		Если ИмяОбработки = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Возврат Обработки[ИмяОбработки].Создать();
	
	Исключение
		Инфо = ИнформацияОбОшибке();
		ИнтеграцияЦУЗсЭТПСерверУХ.ЗаписатьОшибкуИнтеграцииВЖурналаРегистрации(ПодробноеПредставлениеОшибки(Инфо), ЭТП.Наименование)
	КонецПопытки;
	
	Возврат Неопределено;
КонецФункции

// Возвращает ссылку на ЭТП-ЕИС
Функция СсылкаНаЕИС() Экспорт
	Возврат Справочники.ЭлектронныеТорговыеПлощадки.НайтиПоНаименованию("new.zakupky.gov.ru", Истина);
КонецФункции


#КонецЕсли