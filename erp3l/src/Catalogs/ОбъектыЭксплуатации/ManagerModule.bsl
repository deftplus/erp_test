
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

//++ НЕ УТКА

// Возвращает структуру по умолчанию передаваемую в качестве параметра в процедуру проверки "ПроверитьЗаполнение".
// 
// Возвращаемое значение:
// 	Структура - Описание:
//      * ПараметрыПроверкиУзлов - Структура -:
//           ** Форма - Неопределено -
//           ** СообщатьОшибки - Булево -
//           ** ПотокОшибок - Структура -
//      * ПроверятьУзлы - Булево -
//      * ИспользоватьУзлыОбъектовЭксплуатации - Булево -
//      * Форма - ФормаКлиентскогоПриложения - с реквизитами:
//           ** Свойства_ОписаниеДополнительныхРеквизитов - ДанныеФормыКоллекция - с колонками:
//                  *** Наименование - Строка -
//      * СообщатьОшибки - Булево -
//      * ПотокОшибок - Структура -
Функция ПараметрыПроверкиЗаполнения() Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("ПотокОшибок", Новый Структура);
	ОбъектыЭксплуатации.ИнициализироватьПотокОшибок(Параметры.ПотокОшибок);
	
	Параметры.Вставить("СообщатьОшибки", Истина);
	Параметры.Вставить("Форма", Неопределено);
	
	Параметры.Вставить("ИспользоватьУзлыОбъектовЭксплуатации", ПолучитьФункциональнуюОпцию("ИспользоватьУзлыОбъектовЭксплуатации"));
	
	Параметры.Вставить("ПроверятьУзлы", Истина);
	Параметры.Вставить("ПараметрыПроверкиУзлов", Справочники.УзлыОбъектовЭксплуатации.ПараметрыПроверкиЗаполнения());
	Параметры.ПараметрыПроверкиУзлов.Вставить("СообщатьОшибки", Ложь);
	
	Возврат Параметры;
	
КонецФункции

// Возвращает описание реквизитов объекта эксплуатации
//
// Возвращаемое значение:
//		ТаблицаЗначений - Таблица с описанием реквизитов элемента справочника объектов эксплуатации.
//
Функция СоставРеквизитовОбъектаЭксплуатации() Экспорт
	
	// Описание формата возвращаемой таблицы реквизитов
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Имя", Новый ОписаниеТипов("Строка"));
	Таблица.Колонки.Добавить("Синоним", Новый ОписаниеТипов("Строка"));
	Таблица.Колонки.Добавить("ОбязателенДляЗаполнения", Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("ТолькоПросмотрОбязательности", Новый ОписаниеТипов("Булево"));
	
	// Значения заполняемые по-умолчанию в строку каждого из реквизитов
	ЗначенияПоУмолчанию = Новый Структура;
	ЗначенияПоУмолчанию.Вставить("Имя", "");
	ЗначенияПоУмолчанию.Вставить("Синоним", "");
	ЗначенияПоУмолчанию.Вставить("ОбязателенДляЗаполнения", "Ложь");
	ЗначенияПоУмолчанию.Вставить("ТолькоПросмотрОбязательности", "Ложь");
	
	// Описания реквизитов, со значениями параметров отличающихся от "по-умолчанию"
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("ДатаСведений", Новый Структура("ТолькоПросмотрОбязательности", Истина));
	СтруктураРеквизитов.Вставить("Статус", Новый Структура("ТолькоПросмотрОбязательности", Истина));
	СтруктураРеквизитов.Вставить("Класс", Новый Структура("ТолькоПросмотрОбязательности", Истина));
	СтруктураРеквизитов.Вставить("Подкласс", Новый Структура("ОбязателенДляЗаполнения, ТолькоПросмотрОбязательности", Ложь, Истина));
	
	// Список реквизитов, которые должны быть исключены: служебные, не используемые, предназначенные для удаления, булевы признаки
	// 		или любые другие не предназначенные для интерактивной настройки пользователем.
	СтопЛист = Новый Структура(
		"ГруппаОСМеждународныйУчет,
		|Комментарий,
		|ОбъектБытовогоНазначения,
		|ОбъектБытовогоНазначения,
		|ДатаСведений,
		|ЗарегистрированоВРеестреСистемыПлатон,
		|УдалитьАмортизационнаяГруппа,
		|УдалитьНаправлениеДеятельности,
		|УдалитьНедвижимоеИмущество,
		|УдалитьРеквизитНедвижимоеИмуществоОбновлен");
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьМеждународныйФинансовыйУчет") Тогда
		СтопЛист.Удалить("ГруппаОСМеждународныйУчет");
	КонецЕсли;
	
	// Получение таблицы реквизитов из описания метаданных
	СтруктураСправочника = Метаданные.Справочники.ОбъектыЭксплуатации;
	Для Каждого Реквизит Из СтруктураСправочника.Реквизиты Цикл
		
		Если СтопЛист.Свойство(Реквизит.Имя) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаРеквизита = Таблица.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаРеквизита, ЗначенияПоУмолчанию);
		ЗаполнитьЗначенияСвойств(СтрокаРеквизита, Реквизит);
		
		СтрокаРеквизита.ОбязателенДляЗаполнения = (Реквизит.ПроверкаЗаполнения = ПроверкаЗаполнения.ВыдаватьОшибку);
		
		Если СтруктураРеквизитов.Свойство(Реквизит.Имя) Тогда
			ЗаполнитьЗначенияСвойств(СтрокаРеквизита, СтруктураРеквизитов[Реквизит.Имя]);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Таблица;
	
КонецФункции

// Выполняет проверку заполнения переданного объекта проверки по заданным параметрам.
//
// Параметры:
// 		ОбъектПроверки - ДанныеФормыСтруктура, СправочникОбъект.ОбъектыЭксплуатации, СправочникСсылка.ОбъектыЭксплуатации - Объект, ссылка или массив ссылок на объекты эксплуатации, которые необходимо проверить на корректность заполнения.
// 		ПараметрыПроверки - см. Справочники.ОбъектыЭксплуатации.ПараметрыПроверкиЗаполнения
// 		Отказ - Булево - Возвращаемый параметр, признак наличия ошибок при проверки.
//
Процедура ПроверитьЗаполнениеОбъектаЭксплуатации(ОбъектПроверки, ПараметрыПроверки, Отказ=Ложь) Экспорт
	
	КэшированныеЗначения = Неопределено;
	ОбъектыЭксплуатации.ИнициализироватьКэшированныеЗначенияПроверкиЗаполнения(КэшированныеЗначения);
	
	ТипОбъекта = ТипЗнч(ОбъектПроверки);
	
	МассивОбъектовЭксплуатации = Новый Массив;
	
	Если ТипОбъекта = Тип("СправочникСсылка.ОбъектыЭксплуатации")
		Или ТипОбъекта = Тип("Массив") Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ОбъектыЭксплуатации", ОбъектПроверки);
		
		СформироватьЗапросПроверкиЗаполнения(Запрос.Текст);
		
		Результат = Запрос.Выполнить();
		
		Если Результат.Пустой() Тогда
			Возврат;
		КонецЕсли;
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ПроверитьОбъектЭксплуатации(
				Выборка,
				ПараметрыПроверки,
				КэшированныеЗначения);
			
			МассивОбъектовЭксплуатации.Добавить(Выборка.Ссылка);
			
		КонецЦикла;
		
	Иначе
		
		ПроверитьОбъектЭксплуатации(
			ОбъектПроверки,
			ПараметрыПроверки,
			КэшированныеЗначения);
		
		МассивОбъектовЭксплуатации.Добавить(ОбъектПроверки.Ссылка);
		
	КонецЕсли;
	
	ПараметрыПроверки.Вставить("ОтказПроверкиУзлов", Ложь);
	
	Если ПараметрыПроверки.ИспользоватьУзлыОбъектовЭксплуатации
		И ПараметрыПроверки.ПроверятьУзлы Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ОбъектыЭксплуатации", МассивОбъектовЭксплуатации);
		СформироватьЗапросПроверкиЗаполненияУзлов(Запрос.Текст);
		
		Результат = Запрос.Выполнить();
		
		Если Не Результат.Пустой() Тогда
			
			Выборка = Результат.Выбрать();
			
			Пока Выборка.Следующий() Цикл
				
				Справочники.УзлыОбъектовЭксплуатации.ПроверитьЗаполнение(
					Выборка,
					ПараметрыПроверки.ПараметрыПроверкиУзлов,
					ПараметрыПроверки.ОтказПроверкиУзлов, // Отказ
					КэшированныеЗначения);
				
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыПроверки.ПотокОшибок.СписокОшибок.Количество() <> 0 Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если ПараметрыПроверки.ПараметрыПроверкиУзлов.ПотокОшибок.СписокОшибок.Количество() <> 0 Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если ПараметрыПроверки.СообщатьОшибки Тогда
		ОбъектыЭксплуатации.СообщитьОшибкиПроверкиЗаполнения(ПараметрыПроверки.ПотокОшибок, Отказ);
	КонецЕсли;
	
КонецПроцедуры

// Выполняет разблокировку объекта эксплуатации для разрешения возможности интерактивного редактирования.
//
// Параметры:
//	ОбъектЭксплуатации - СправочникСсылка.ОбъектыЭксплуатации - Ссылка на элемент справочника
//	Отказ - Булево - Возвращаемый параметр, признак наличия ошибок во время выполнения.
//
Процедура РазблокироватьОбъектЭксплуатации(ОбъектЭксплуатации, Отказ=Ложь) Экспорт
	
	ДанныеЗаполнения = Новый Структура("Статус", Перечисления.СтатусыОбъектовЭксплуатации.Редактируется);
	
	ОбъектСправочника = ОбъектЭксплуатации.ПолучитьОбъект();
	
	ЗаполнитьЗначенияСвойств(ОбъектСправочника, ДанныеЗаполнения);
	Попытка
		ОбъектСправочника.Записать();
	Исключение
		Отказ = Истина;
		ВызватьИсключение НСтр("ru = 'Ошибка при попытке записи объекта эксплуатации:';
								|en = 'An error occurred while trying to save asset:'") + " " + ИнформацияОбОшибке().Описание;
	КонецПопытки;
	
КонецПроцедуры

//-- НЕ УТКА

// Формирует сведения об учете основного средства
//
// Параметры:
//  ОсновноеСредство - СправочникСсылка.ОбъектыЭксплуатации	- Основное средство, для которого нужно получить сведения.
//  ОтборОрганизация - СправочникСсылка.Организации - Организация, для которой нужно получить сведения.
// 
// Возвращаемое значение:
//  Структура - Содержит сведения об учете.
//
Функция СведенияОбУчете(ОсновноеСредство, ОтборОрганизация = Неопределено) Экспорт

	СведенияОбУчете = ОбъектыЭксплуатацииЛокализация.СведенияОбУчете(ОсновноеСредство, ОтборОрганизация);
	
	Если СведенияОбУчете = Неопределено Тогда
		
		Если НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.МестонахождениеОС)
			ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПервоначальныеСведенияОС)
			ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПорядокУчетаОС)
			ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПорядокУчетаОСУУ)
			ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПараметрыАмортизацииОСУУ) Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаВводаВЭксплуатациюБУ,
		|	ЕСТЬNULL(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаВводаВЭксплуатациюУУ,
		|	НЕОПРЕДЕЛЕНО КАК ДокументВводаВЭксплуатациюБУ,
		|	ЕСТЬNULL(ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ, НЕОПРЕДЕЛЕНО) КАК ДокументВводаВЭксплуатациюУУ,
		|	ЕСТЬNULL(ПервоначальныеСведенияОС.ДокументСнятияСУчетаУУ, НЕОПРЕДЕЛЕНО) КАК ДокументСнятияСУчетаУУ,
		|	ЕСТЬNULL(ПервоначальныеСведенияОС.ДокументСнятияСУчетаУУ.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаСнятияСУчетаУУ,
		|	ЕСТЬNULL(ПорядокУчетаОССрезПоследних.ГруппаФинансовогоУчета, НЕОПРЕДЕЛЕНО) КАК ГруппаФинансовогоУчета,
		|	ЕСТЬNULL(ПорядокУчетаОССрезПоследних.ГруппаФинансовогоУчета.Представление, """") КАК ГруппаФинансовогоУчетаПредставление,
		|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.НеПринятоКУчету) КАК СостояниеБУ,
		|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.НеПринятоКУчету) КАК СостояниеНУ,
		|	ЕСТЬNULL(ПорядокУчетаОСУУСрезПоследних.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияОС.НеПринятоКУчету)) КАК СостояниеУУ,
		|	ЕСТЬNULL(ПараметрыАмортизацииОСУУСрезПоследних.ЛиквидационнаяСтоимость, 0) КАК ЛиквидационнаяСтоимость,
		|	ЕСТЬNULL(ПараметрыАмортизацииОСУУСрезПоследних.ЛиквидационнаяСтоимостьРегл, 0) КАК ЛиквидационнаяСтоимостьРегл,
		|	ЕСТЬNULL(ПараметрыАмортизацииОСУУСрезПоследних.СрокИспользования, 0) КАК СрокИспользованияУУ,
		|	0 КАК СрокИспользованияБУ,
		|	0 КАК СрокИспользованияНУ,
		|	МестонахождениеОС.Период КАК ПериодСведенийОМестонахождении,
		|	МестонахождениеОС.Организация КАК Организация,
		|	ЕСТЬNULL(МестонахождениеОС.Организация.Представление, """") КАК ОрганизацияПредставление,
		|	МестонахождениеОС.МОЛ КАК МОЛ,
		|	ЕСТЬNULL(МестонахождениеОС.МОЛ.Представление, """") КАК МОЛПредставление,
		|	МестонахождениеОС.Местонахождение КАК Подразделение,
		|	ЕСТЬNULL(МестонахождениеОС.Местонахождение.Представление, """") КАК ПодразделениеПредставление,
		|	МестонахождениеОС.АдресМестонахождения КАК АдресМестонахождения,
		|	МестонахождениеОС.ОсновноеСредство.ТипОС КАК ТипОС,
		|	ЛОЖЬ КАК ПолученоВАрендуОтДругойОрганизации,
		|	НЕ АрендованныеОС.ОсновноеСредство ЕСТЬ NULL КАК ПолученВАренду,
		|	НЕ ПереданныеВАрендуОС.ОсновноеСредство ЕСТЬ NULL КАК ПереданВАренду,
		|	ЕСТЬNULL(АрендованныеОС.Партнер, ЕСТЬNULL(ПереданныеВАрендуОС.Партнер, НЕОПРЕДЕЛЕНО)) КАК Партнер,
		|	ЕСТЬNULL(АрендованныеОС.Партнер.Представление, ЕСТЬNULL(ПереданныеВАрендуОС.Партнер.Представление, """")) КАК ПартнерПредставление,
		|	ЕСТЬNULL(АрендованныеОС.Контрагент, ЕСТЬNULL(ПереданныеВАрендуОС.Контрагент, НЕОПРЕДЕЛЕНО)) КАК Контрагент,
		|	ЕСТЬNULL(АрендованныеОС.Контрагент.Представление, ЕСТЬNULL(ПереданныеВАрендуОС.Контрагент.Представление, """")) КАК КонтрагентПредставление,
		|	ЕСТЬNULL(АрендованныеОС.Договор, ЕСТЬNULL(ПереданныеВАрендуОС.Договор, НЕОПРЕДЕЛЕНО)) КАК Договор,
		|	ЕСТЬNULL(АрендованныеОС.Договор.Представление, ЕСТЬNULL(ПереданныеВАрендуОС.Договор.Представление, """")) КАК ДоговорПредставление
		|ИЗ
		|	РегистрСведений.МестонахождениеОС.СрезПоследних(, ОсновноеСредство = &Ссылка) КАК МестонахождениеОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(, ОсновноеСредство = &Ссылка) КАК ПервоначальныеСведенияОС
		|		ПО (ПервоначальныеСведенияОС.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство)
		|			И (ПервоначальныеСведенияОС.Организация = МестонахождениеОС.Организация)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС.СрезПоследних(, ОсновноеСредство = &Ссылка) КАК ПорядокУчетаОССрезПоследних
		|		ПО (ПорядокУчетаОССрезПоследних.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(, ОсновноеСредство = &Ссылка) КАК ПорядокУчетаОСУУСрезПоследних
		|		ПО (ПорядокУчетаОСУУСрезПоследних.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство)
		|			И (ПорядокУчетаОСУУСрезПоследних.Организация = МестонахождениеОС.Организация)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСУУ.СрезПоследних(, ОсновноеСредство = &Ссылка) КАК ПараметрыАмортизацииОСУУСрезПоследних
		|		ПО (ПараметрыАмортизацииОСУУСрезПоследних.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство)
		|			И (ПараметрыАмортизацииОСУУСрезПоследних.Организация = МестонахождениеОС.Организация)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПереданныеВАрендуОС.СрезПоследних(, ОсновноеСредство = &Ссылка) КАК ПереданныеВАрендуОС
		|		ПО (ПереданныеВАрендуОС.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство)
		|			И (ПереданныеВАрендуОС.Организация = МестонахождениеОС.Организация)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АрендованныеОС.СрезПоследних(, ОсновноеСредство = &Ссылка) КАК АрендованныеОС
		|		ПО (АрендованныеОС.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство)";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Ссылка", ОсновноеСредство);
		Результат = Запрос.Выполнить();
		
		Если Результат.Пустой() Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		СведенияОбУчете = Новый Структура;
		Для каждого КолонкаЗапроса Из Результат.Колонки Цикл
			СведенияОбУчете.Вставить(КолонкаЗапроса.Имя);
		КонецЦикла; 
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(СведенияОбУчете, Выборка);
		
	КонецЕсли; 
	
	Возврат СведенияОбУчете;

КонецФункции

// Возвращает сведения об основном средстве.
//
// Параметры:
//  ОсновноеСредствоИлиСписок - Массив, СправочникСсылка.ОбъектыЭксплуатации - Основное средство для которого нужно получить сведения.
//  Период - Дата - На какую дату нужно получить сведения.
// 
// Возвращаемое значение:
//  Соответствие, Структура - Содержит сведения об основном средстве.
//
Функция ПервоначальныеСведения(ОсновноеСредствоИлиСписок, Период = '000101010000') Экспорт
	
	ПервоначальныеСведения = ОбъектыЭксплуатацииЛокализация.ПервоначальныеСведения(ОсновноеСредствоИлиСписок, Период);
	
	Если ПервоначальныеСведения <> Неопределено Тогда
		Возврат ПервоначальныеСведения;
	КонецЕсли; 
		
	ПервоначальныеСведения = Новый Структура;
	ПервоначальныеСведения.Вставить("Организация", Неопределено);
	ПервоначальныеСведения.Вставить("Местонахождение", Неопределено);
	ПервоначальныеСведения.Вставить("МОЛ", Неопределено);
	ПервоначальныеСведения.Вставить("НаправлениеДеятельности", Неопределено);
	ПервоначальныеСведения.Вставить("ДатаВводаВЭксплуатациюБУ", '000101010000');
	ПервоначальныеСведения.Вставить("ДатаВводаВЭксплуатациюНУ", '000101010000');
	ПервоначальныеСведения.Вставить("ДатаВводаВЭксплуатациюУУ", '000101010000');
	ПервоначальныеСведения.Вставить("ДокументВводаВЭксплуатациюБУ", Неопределено);
	ПервоначальныеСведения.Вставить("ДокументВводаВЭксплуатациюНУ", Неопределено);
	ПервоначальныеСведения.Вставить("ДокументВводаВЭксплуатациюУУ", Неопределено);
	ПервоначальныеСведения.Вставить("СостояниеБУ", Перечисления.СостоянияОС.НеПринятоКУчету);
	ПервоначальныеСведения.Вставить("СостояниеНУ", Перечисления.СостоянияОС.НеПринятоКУчету);
	ПервоначальныеСведения.Вставить("СостояниеУУ", Перечисления.СостоянияОС.НеПринятоКУчету);
	ПервоначальныеСведения.Вставить("ЛиквидационнаяСтоимость", 0);
	ПервоначальныеСведения.Вставить("ЛиквидационнаяСтоимостьРегл", 0);
	
	Если НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.МестонахождениеОС)
		ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПервоначальныеСведенияОС)
		ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПорядокУчетаОСУУ) Тогда
		
		Возврат ?(ТипЗнч(ОсновноеСредствоИлиСписок) = Тип("Массив"), Новый Соответствие, ПервоначальныеСведения);
	КонецЕсли; 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МестонахождениеОС.ОсновноеСредство КАК ОсновноеСредство,
	|	МестонахождениеОС.Организация КАК Организация,
	|	МестонахождениеОС.Местонахождение КАК Местонахождение,
	|	МестонахождениеОС.МОЛ КАК МОЛ,
	|	ПорядокУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ КАК ДатаВводаВЭксплуатациюУУ,
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ КАК ДатаВводаВЭксплуатациюБУ,
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ КАК ДатаВводаВЭксплуатациюНУ,
	|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ КАК ДокументВводаВЭксплуатациюУУ,
	|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ КАК ДокументВводаВЭксплуатациюБУ,
	|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ КАК ДокументВводаВЭксплуатациюНУ,
	|	ЕСТЬNULL(ПорядокУчетаОСУУ.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияОС.НеПринятоКУчету)) КАК СостояниеУУ,
	|	ЕСТЬNULL(ПорядокУчетаОСУУ.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияОС.НеПринятоКУчету)) КАК СостояниеБУ,
	|	ЕСТЬNULL(ПорядокУчетаОСУУ.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияОС.НеПринятоКУчету)) КАК СостояниеНУ,
	|	ЕСТЬNULL(ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимость, 0) КАК ЛиквидационнаяСтоимость,
	|	ЕСТЬNULL(ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимостьРегл, 0) КАК ЛиквидационнаяСтоимостьРегл
	|ИЗ
	|	РегистрСведений.МестонахождениеОС.СрезПоследних(&Период, ОсновноеСредство В (&СписокОС)) КАК МестонахождениеОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(&Период, ОсновноеСредство В (&СписокОС)) КАК ПервоначальныеСведенияОС
	|		ПО МестонахождениеОС.ОсновноеСредство = ПервоначальныеСведенияОС.ОсновноеСредство
	|			И МестонахождениеОС.Организация = ПервоначальныеСведенияОС.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС.СрезПоследних(&Период, ОсновноеСредство В (&СписокОС)) КАК ПорядокУчетаОС
	|		ПО МестонахождениеОС.ОсновноеСредство = ПорядокУчетаОС.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(&Период, ОсновноеСредство В (&СписокОС)) КАК ПорядокУчетаОСУУ
	|		ПО МестонахождениеОС.ОсновноеСредство = ПорядокУчетаОСУУ.ОсновноеСредство
	|			И МестонахождениеОС.Организация = ПорядокУчетаОСУУ.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСУУ.СрезПоследних(&Период, ОсновноеСредство В (&СписокОС)) КАК ПараметрыАмортизацииОСУУ
	|		ПО МестонахождениеОС.ОсновноеСредство = ПараметрыАмортизацииОСУУ.ОсновноеСредство
	|			И МестонахождениеОС.Организация = ПараметрыАмортизацииОСУУ.Организация";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Период", Период);
	
	СписокОС = ?(ТипЗнч(ОсновноеСредствоИлиСписок) = Тип("Массив"), ОсновноеСредствоИлиСписок, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОсновноеСредствоИлиСписок));
	Запрос.УстановитьПараметр("СписокОС", СписокОС);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат ?(ТипЗнч(ОсновноеСредствоИлиСписок) = Тип("Массив"), Новый Соответствие, ПервоначальныеСведения);
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	Если ТипЗнч(ОсновноеСредствоИлиСписок) = Тип("Массив") Тогда
		Результат = Новый Соответствие;
		Пока Выборка.Следующий() Цикл
			ПервоначальныеСведенияОС = ОбщегоНазначения.СкопироватьРекурсивно(ПервоначальныеСведения);
			ЗаполнитьЗначенияСвойств(ПервоначальныеСведенияОС, Выборка);
			Результат.Вставить(Выборка.ОсновноеСредство, ПервоначальныеСведенияОС);
		КонецЦикла;
	Иначе
		Результат = ПервоначальныеСведения;
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли; 
	
	Возврат Результат;

КонецФункции

// Возвращает имена блокируемых реквизитов для механизма блокирования реквизитов БСП
//
// Возвращаемое значение:
// 		Массив - имена блокируемых реквизитов.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить("ГруппаОС");
	Результат.Добавить("ШифрПоЕНАОФ");
	Результат.Добавить("КодПоОКОФ");
	Результат.Добавить("ГруппаОСМеждународныйУчет");
	
	Возврат Результат;
	
КонецФункции

// Возвращает показатель наработки объекта эксплуатации.
//
// Параметры:
//  ОбъектЭксплуатации	 - СправочникСсылка.ОбъектыЭксплуатации	 - Объект эксплуатации.
//  Дата				 - Дата									 - Дата.
// 
// Возвращаемое значение:
//  СправочникСсылка.ПоказателиНаработки - Показатель наработки объекта эксплуатации.
//
Функция ПоказательНаработки(ОбъектЭксплуатации, Дата) Экспорт

	ТекстЗапроса = ОбъектыЭксплуатацииЛокализация.ТекстЗапросаПоказательНаработки();
	
	Если ТекстЗапроса = Неопределено Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПорядокУчетаОС.ПоказательНаработки КАК ПоказательНаработки
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОС.СрезПоследних(&Дата, ОсновноеСредство = &Объект) КАК ПорядокУчетаОС"
	КонецЕсли; 
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Объект", ОбъектЭксплуатации);
	Запрос.УстановитьПараметр("ИспользуетсяУправлениеВНА_2_4", ВнеоборотныеАктивы.ИспользуетсяУправлениеВНА_2_4(Дата));
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Результат.Пустой() Тогда
		Возврат Справочники.ПоказателиНаработки.ПустаяСсылка();
	КонецЕсли;
		
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
		
	Возврат Выборка.ПоказательНаработки;

КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Возвращает реквизиты объекта, которые разрешается редактировать
// с помощью обработки группового изменения реквизитов.
//
// Возвращаемое значение:
//  Массив - список имен реквизитов объекта.
Функция РеквизитыРедактируемыеВГрупповойОбработке() Экспорт
	
	РедактируемыеРеквизиты = Новый Массив;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеРемонтами") Тогда
		РедактируемыеРеквизиты.Добавить("ДатаВыпуска");
		РедактируемыеРеквизиты.Добавить("Изготовитель");
		РедактируемыеРеквизиты.Добавить("Модель");
		РедактируемыеРеквизиты.Добавить("РемонтирующееПодразделение");
		РедактируемыеРеквизиты.Добавить("ЭксплуатирующееПодразделение");
		РедактируемыеРеквизиты.Добавить("СтатьяРасходов");
	КонецЕсли;
	
	Возврат РедактируемыеРеквизиты;

КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	ОбработкаВыполнена = ВнеоборотныеАктивыВызовСервера.ОбработкаПолученияФормы_ОбъектыЭксплуатации(
							ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка);
	
	Если ВидФормы = "ФормаВыбора" И НЕ ОбработкаВыполнена Тогда
		
		ВыбраннаяФорма = "ФормаВыбора2_4";
		СтандартнаяОбработка = Ложь;

	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ДанныеВыбора = ВнеоборотныеАктивыВызовСервера.ДанныеВыбораОбъектовЭксплуатации(Параметры, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция ЕстьПраваНаЧтениеСведений() Экспорт

	Если НЕ ВнеоборотныеАктивы.ИспользуетсяУправлениеВНА_2_4()
		ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.МестонахождениеОС)
		ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПервоначальныеСведенияОС)
		ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПорядокУчетаОСУУ)
		ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.СтоимостьОС)
		ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.АмортизацияОС)
		ИЛИ НЕ ОбъектыЭксплуатацииЛокализация.ЕстьПраваНаЧтениеСведений() Тогда
		Возврат Ложь;
	КонецЕсли;

	Возврат Истина;
	
КонецФункции

Функция ПредставлениеСведенийОбУчете(СведенияОбУчете, СтоимостьИАмортизация, ВключитьНезаполненные = Истина) Экспорт

	СведенияПринятКУчету1 = Новый Массив;
	СведенияПринятКУчету2 = Новый Массив;
	СведенияПринятКУчету3 = Новый Массив;
	СведенияСрокИспользования1 = Новый Массив;
	СведенияСрокИспользования2 = Новый Массив;
	СведенияСрокИспользования3 = Новый Массив;
	СведенияЛиквидационнаяСтоимость = Новый Массив;
	СведенияЛиквидационнаяСтоимостьРегл = Новый Массив;
	СведенияСнятСУчета1 = Новый Массив;
	СведенияСнятСУчета2 = Новый Массив;
	СведенияСнятСУчета3 = Новый Массив;
	СведенияГФУ = Новый Массив;
	
	// Аренда
	СведенияПартнер = Новый Массив;
	СведенияКонтрагент = Новый Массив;
	СведенияДоговор = Новый Массив;
	
	СведенияВосстановительнаяСтоимость = Новый Массив;
	СведенияНакопленнаяАмортизация = Новый Массив;
	СведенияОбесценение = Новый Массив;
	СведенияОстаточнаяСтоимость = Новый Массив;
	
	СведенияМестонахождениеОрганизация = Новый Массив;
	СведенияМестонахождениеПодразделение = Новый Массив;
	СведенияМестонахождениеМОЛ = Новый Массив;
	СведенияМестонахождениеАдрес = Новый Массив;
	Период = '000101010000';
	
	Если СведенияОбУчете <> Неопределено Тогда
		ВалютаРегл = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(СведенияОбУчете.Организация);
	Иначе
		ВалютаРегл = ЗначениеНастроекПовтИсп.БазоваяВалютаПоУмолчанию();
	КонецЕсли; 

	ВалютаУпр = Константы.ВалютаУправленческогоУчета.Получить();
		
	ВалютаРеглПредставление = Строка(ВалютаРегл);
	ВалютаУпрПредставление = Строка(ВалютаУпр);
	
	ДоступенВыборОтраженияВУчетах = 
		ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА();
	
	#Область ОбщиеСведения
	
	Если СведенияОбУчете <> Неопределено Тогда
		
		Если СведенияОбУчете.СостояниеУУ = Перечисления.СостоянияОС.ПринятоКУчету
			ИЛИ СведенияОбУчете.СостояниеБУ = Перечисления.СостоянияОС.ПринятоКУчету
			ИЛИ СведенияОбУчете.СостояниеНУ = Перечисления.СостоянияОС.ПринятоКУчету
			ИЛИ СведенияОбУчете.СостояниеУУ = Перечисления.СостоянияОС.СнятоСУчета
			ИЛИ СведенияОбУчете.СостояниеБУ = Перечисления.СостоянияОС.СнятоСУчета
			ИЛИ СведенияОбУчете.СостояниеНУ = Перечисления.СостоянияОС.СнятоСУчета Тогда
			
			Если ЗначениеЗаполнено(СведенияОбУчете.ДатаВводаВЭксплуатациюУУ) 
					И ЗначениеЗаполнено(СведенияОбУчете.ДатаВводаВЭксплуатациюБУ)
					И ЗначениеЗаполнено(СведенияОбУчете.ДатаВводаВЭксплуатациюНУ)
					И СведенияОбУчете.ДатаВводаВЭксплуатациюУУ = СведенияОбУчете.ДатаВводаВЭксплуатациюБУ
					И СведенияОбУчете.ДатаВводаВЭксплуатациюУУ = СведенияОбУчете.ДатаВводаВЭксплуатациюНУ
				ИЛИ ЗначениеЗаполнено(СведенияОбУчете.ДатаВводаВЭксплуатациюУУ) 
					И НЕ ДоступенВыборОтраженияВУчетах Тогда
				
				СведенияПринятКУчету1.Добавить(НСтр("ru = 'Дата принятия к учету:';
													|en = 'Date of recognition:'"));
				СведенияПринятКУчету1.Добавить(" ");
				ДатаДокумента = Формат(СведенияОбУчете.ДатаВводаВЭксплуатациюУУ, "ДЛФ=D");
				Если ЗначениеЗаполнено(СведенияОбУчете.ДокументВводаВЭксплуатациюУУ) Тогда
					СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.ДокументВводаВЭксплуатациюУУ);
				Иначе
					СсылкаНаОбъект = Неопределено;
				КонецЕсли;
				СведенияПринятКУчету1.Добавить(Новый ФорматированнаяСтрока(ДатаДокумента,,,, СсылкаНаОбъект));
				
			Иначе
				
				Если ДоступенВыборОтраженияВУчетах
					И (СведенияОбУчете.СостояниеУУ <> Перечисления.СостоянияОС.СнятоСУчета 
						ИЛИ ЗначениеЗаполнено(СведенияОбУчете.ДатаВводаВЭксплуатациюБУ)) Тогда
						
					СведенияПринятКУчету1.Добавить(НСтр("ru = 'Дата принятия в бух. учете:';
														|en = 'Date of recognition in accounting:'"));
					СведенияПринятКУчету1.Добавить(" ");
					Если ЗначениеЗаполнено(СведенияОбУчете.ДатаВводаВЭксплуатациюБУ) Тогда
						ДатаДокумента = Формат(СведенияОбУчете.ДатаВводаВЭксплуатациюБУ, "ДЛФ=D");
						Если ЗначениеЗаполнено(СведенияОбУчете.ДокументВводаВЭксплуатациюБУ) Тогда
							СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.ДокументВводаВЭксплуатациюБУ);
						Иначе
							СсылкаНаОбъект = Неопределено;
						КонецЕсли;
						СведенияПринятКУчету1.Добавить(Новый ФорматированнаяСтрока(ДатаДокумента,,,, СсылкаНаОбъект));
					Иначе	
						СведенияПринятКУчету1.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'принять к учету';
																						|en = 'recognize'"),,,, "#Создать"));
					КонецЕсли; 
				КонецЕсли;
				
				Если ДоступенВыборОтраженияВУчетах
					И (СведенияОбУчете.СостояниеУУ <> Перечисления.СостоянияОС.СнятоСУчета 
						ИЛИ ЗначениеЗаполнено(СведенияОбУчете.ДатаВводаВЭксплуатациюНУ))
					И (СведенияОбУчете.ТипОС = Перечисления.ТипыОС.ОбъектЭксплуатации
						ИЛИ СведенияОбУчете.ТипОС = Перечисления.ТипыОС.ГрупповоеОС) Тогда
						
					СведенияПринятКУчету2.Добавить(НСтр("ru = 'Дата принятия в нал. учете:';
														|en = 'Date of recognition in tax accounting:'"));
					СведенияПринятКУчету2.Добавить(" ");
					Если ЗначениеЗаполнено(СведенияОбУчете.ДатаВводаВЭксплуатациюНУ) Тогда
						ДатаДокумента = Формат(СведенияОбУчете.ДатаВводаВЭксплуатациюНУ, "ДЛФ=D");
						Если ЗначениеЗаполнено(СведенияОбУчете.ДокументВводаВЭксплуатациюНУ) Тогда
							СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.ДокументВводаВЭксплуатациюНУ);
						Иначе
							СсылкаНаОбъект = Неопределено;
						КонецЕсли;
						СведенияПринятКУчету2.Добавить(Новый ФорматированнаяСтрока(ДатаДокумента,,,, СсылкаНаОбъект));
					Иначе	
						СведенияПринятКУчету2.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'принять к учету';
																						|en = 'recognize'"),,,, "#Создать"));
					КонецЕсли; 
				КонецЕсли;
				
				Если ЗначениеЗаполнено(СведенияОбУчете.ДатаВводаВЭксплуатациюУУ)
					ИЛИ ОбъектыЭксплуатацииЛокализация.МожноПринятьКУчету(СведенияОбУчете) Тогда
					
					СведенияПринятКУчету3.Добавить(НСтр("ru = 'Дата принятия в упр. учете:';
														|en = 'Date of recognition in man. accounting:'"));
					СведенияПринятКУчету3.Добавить(" ");
					Если ЗначениеЗаполнено(СведенияОбУчете.ДатаВводаВЭксплуатациюУУ) Тогда
						ДатаДокумента = Формат(СведенияОбУчете.ДатаВводаВЭксплуатациюУУ, "ДЛФ=D");
						Если ЗначениеЗаполнено(СведенияОбУчете.ДокументВводаВЭксплуатациюУУ) Тогда
							СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.ДокументВводаВЭксплуатациюУУ);
						Иначе
							СсылкаНаОбъект = Неопределено;
						КонецЕсли;
						СведенияПринятКУчету3.Добавить(Новый ФорматированнаяСтрока(ДатаДокумента,,,, СсылкаНаОбъект));
					Иначе
						СведенияПринятКУчету3.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'принять к учету';
																						|en = 'recognize'"),,,, "#Создать"));
					КонецЕсли; 
				КонецЕсли; 
				
			КонецЕсли;
			
			Если СведенияОбУчете.ЛиквидационнаяСтоимостьРегл <> 0 Тогда
				СведенияСтрокой = СтрШаблон(НСтр("ru = 'Ликвидационная стоимость: %1 %2';
												|en = 'Residual value: %1 %2'"), 
											Строка(СведенияОбУчете.ЛиквидационнаяСтоимостьРегл),
											ВалютаРеглПредставление);
				СведенияЛиквидационнаяСтоимостьРегл.Добавить(СведенияСтрокой);
			КонецЕсли; 
			
			Если СведенияОбУчете.ЛиквидационнаяСтоимость <> 0
				И (ВалютаРегл <> ВалютаУпр
					ИЛИ СведенияОбУчете.ЛиквидационнаяСтоимость = 0) Тогда
				СведенияСтрокой = СтрШаблон(НСтр("ru = 'Ликвидационная стоимость: %1 %2';
												|en = 'Residual value: %1 %2'"), 
											Строка(СведенияОбУчете.ЛиквидационнаяСтоимость),
											ВалютаУпрПредставление);
				СведенияЛиквидационнаяСтоимость.Добавить(СведенияСтрокой);
			КонецЕсли; 
			
			СрокИспользованияУУ = ВнеоборотныеАктивыКлиентСервер.ПредставлениеКоличестваМесяцевСтрокой(СведенияОбУчете.СрокИспользованияУУ, Ложь);
			СрокИспользованияБУ = ВнеоборотныеАктивыКлиентСервер.ПредставлениеКоличестваМесяцевСтрокой(СведенияОбУчете.СрокИспользованияБУ, Ложь);
			СрокИспользованияНУ = ВнеоборотныеАктивыКлиентСервер.ПредставлениеКоличестваМесяцевСтрокой(СведенияОбУчете.СрокИспользованияНУ, Ложь);
			
			Если СведенияОбУчете.СрокИспользованияБУ = СведенияОбУчете.СрокИспользованияНУ
					И СведенияОбУчете.СрокИспользованияБУ = СведенияОбУчете.СрокИспользованияУУ 
					И СведенияОбУчете.СрокИспользованияУУ <> 0
				ИЛИ СведенияОбУчете.СрокИспользованияУУ <> 0
					И СведенияОбУчете.СрокИспользованияБУ = 0
					И СведенияОбУчете.СрокИспользованияБУ = 0 Тогда
				
				СведенияСрокИспользования1.Добавить(СтрШаблон(НСтр("ru = 'Срок использования: %1';
																	|en = 'Useful life: %1'"), СрокИспользованияУУ));
				
			ИначеЕсли СведенияОбУчете.СрокИспользованияБУ <> 0
				И СведенияОбУчете.СрокИспользованияУУ = 0
				И (СведенияОбУчете.СрокИспользованияНУ = 0
					ИЛИ СведенияОбУчете.СрокИспользованияНУ = СведенияОбУчете.СрокИспользованияБУ) Тогда
					
				СведенияСрокИспользования1.Добавить(СтрШаблон(НСтр("ru = 'Срок использования: %1';
																	|en = 'Useful life: %1'"), СрокИспользованияБУ));
				
			ИначеЕсли СведенияОбУчете.СрокИспользованияНУ <> 0
				И СведенияОбУчете.СрокИспользованияУУ = 0
				И (СведенияОбУчете.СрокИспользованияБУ = 0
					ИЛИ СведенияОбУчете.СрокИспользованияБУ = СведенияОбУчете.СрокИспользованияНУ) Тогда				
					
				СведенияСрокИспользования1.Добавить(СтрШаблон(НСтр("ru = 'Срок использования: %1';
																	|en = 'Useful life: %1'"), СрокИспользованияНУ));
				
			Иначе
				
				Если СведенияОбУчете.СрокИспользованияБУ <> 0 Тогда
					СведенияСрокИспользования1.Добавить(СтрШаблон(НСтр("ru = 'Срок использования (БУ):  %1';
																		|en = 'Useful life (AC): %1'"), СрокИспользованияБУ));
				КонецЕсли;
				Если СведенияОбУчете.СрокИспользованияНУ <> 0 Тогда
					СведенияСрокИспользования2.Добавить(СтрШаблон(НСтр("ru = 'Срок использования (НУ):  %1';
																		|en = 'Useful life (TA): %1'"), СрокИспользованияНУ));
				КонецЕсли;
				Если СведенияОбУчете.СрокИспользованияУУ <> 0 Тогда
					СведенияСрокИспользования3.Добавить(СтрШаблон(НСтр("ru = 'Срок использования (УУ):  %1';
																		|en = 'Useful life (MA): %1'"), СрокИспользованияУУ));
				КонецЕсли;
				
			КонецЕсли;
				
			//
			Если ЗначениеЗаполнено(СведенияОбУчете.ДатаСнятияСУчетаУУ) 
					И ЗначениеЗаполнено(СведенияОбУчете.ДатаСнятияСУчетаБУ)
					И ЗначениеЗаполнено(СведенияОбУчете.ДатаСнятияСУчетаНУ)
					И СведенияОбУчете.ДатаСнятияСУчетаУУ = СведенияОбУчете.ДатаСнятияСУчетаБУ
					И СведенияОбУчете.ДатаСнятияСУчетаУУ = СведенияОбУчете.ДатаСнятияСУчетаНУ
				ИЛИ ЗначениеЗаполнено(СведенияОбУчете.ДатаСнятияСУчетаУУ) 
					И НЕ ДоступенВыборОтраженияВУчетах Тогда
			
				СведенияСнятСУчета1.Добавить(НСтр("ru = 'Дата снятия с учета:';
													|en = 'Deregistration date:'"));
				СведенияСнятСУчета1.Добавить(" ");
				ДатаДокумента = Формат(СведенияОбУчете.ДатаСнятияСУчетаУУ, "ДЛФ=D");
				Если ЗначениеЗаполнено(СведенияОбУчете.ДокументСнятияСУчетаУУ) Тогда
					СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.ДокументСнятияСУчетаУУ);
					СведенияСнятСУчета1.Добавить(Новый ФорматированнаяСтрока(ДатаДокумента,,,, СсылкаНаОбъект));
				Иначе
					СведенияСнятСУчета1.Добавить(ДатаДокумента);
				КонецЕсли;
				
			Иначе
				
				Если ДоступенВыборОтраженияВУчетах
					И ЗначениеЗаполнено(СведенияОбУчете.ДатаСнятияСУчетаБУ) Тогда
						
					СведенияСнятСУчета1.Добавить(НСтр("ru = 'Дата снятия в бух. учете:';
														|en = 'Derecognition in accounting date:'"));
					СведенияСнятСУчета1.Добавить(" ");
					ДатаДокумента = Формат(СведенияОбУчете.ДатаСнятияСУчетаБУ, "ДЛФ=D");
					Если ЗначениеЗаполнено(СведенияОбУчете.ДокументСнятияСУчетаБУ) Тогда
						СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.ДокументСнятияСУчетаБУ);
					Иначе
						СсылкаНаОбъект = Неопределено;
					КонецЕсли;
					СведенияСнятСУчета1.Добавить(Новый ФорматированнаяСтрока(ДатаДокумента,,,, СсылкаНаОбъект));
				КонецЕсли;
				
				Если ДоступенВыборОтраженияВУчетах
					И ЗначениеЗаполнено(СведенияОбУчете.ДатаСнятияСУчетаНУ) Тогда
						
					СведенияСнятСУчета2.Добавить(НСтр("ru = 'Дата снятия в нал. учете:';
														|en = 'Derecognition in tax accounting date:'"));
					СведенияСнятСУчета2.Добавить(" ");
					ДатаДокумента = Формат(СведенияОбУчете.ДатаСнятияСУчетаНУ, "ДЛФ=D");
					Если ЗначениеЗаполнено(СведенияОбУчете.ДокументСнятияСУчетаНУ) Тогда
						СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.ДокументСнятияСУчетаНУ);
					Иначе
						СсылкаНаОбъект = Неопределено;
					КонецЕсли;
					СведенияСнятСУчета2.Добавить(Новый ФорматированнаяСтрока(ДатаДокумента,,,, СсылкаНаОбъект));
				КонецЕсли;
				
				Если ЗначениеЗаполнено(СведенияОбУчете.ДатаСнятияСУчетаУУ) Тогда
					
					СведенияСнятСУчета3.Добавить(НСтр("ru = 'Дата снятия в упр. учете:';
														|en = 'Derecognition in man. accounting date:'"));
					СведенияСнятСУчета3.Добавить(" ");
					ДатаДокумента = Формат(СведенияОбУчете.ДатаСнятияСУчетаУУ, "ДЛФ=D");
					Если ЗначениеЗаполнено(СведенияОбУчете.ДокументСнятияСУчетаУУ) Тогда
						СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.ДокументСнятияСУчетаУУ);
					Иначе
						СсылкаНаОбъект = Неопределено;
					КонецЕсли;
					СведенияСнятСУчета3.Добавить(Новый ФорматированнаяСтрока(ДатаДокумента,,,, СсылкаНаОбъект));
				КонецЕсли; 
				
			КонецЕсли; 
			
			//
			СведенияГФУ.Добавить(НСтр("ru = 'Группа финансового учета:';
										|en = 'Financial accounting group:'"));
			СведенияГФУ.Добавить(" ");
			Если ЗначениеЗаполнено(СведенияОбУчете.ГруппаФинансовогоУчета) Тогда
				СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.ГруппаФинансовогоУчета);
			Иначе
				СсылкаНаОбъект = Неопределено;
			КонецЕсли;
			СведенияГФУ.Добавить(Новый ФорматированнаяСтрока(СведенияОбУчете.ГруппаФинансовогоУчетаПредставление,,,, СсылкаНаОбъект));
		
			// Сведения об аренде
			Если ЗначениеЗаполнено(СведенияОбУчете.Партнер) Тогда
				ТекстНадписи = ?(СведенияОбУчете.ПолученВАренду, НСтр("ru = 'Арендодатель:';
																		|en = 'Lessor:'"), НСтр("ru = 'Арендатор:';
																									|en = 'Lessee:'"));
				СведенияПартнер.Добавить(ТекстНадписи);
				СведенияПартнер.Добавить(" ");
				СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.Партнер);
				СведенияПартнер.Добавить(Новый ФорматированнаяСтрока(СведенияОбУчете.ПартнерПредставление,,,, СсылкаНаОбъект));
			КонецЕсли; 

			Если ЗначениеЗаполнено(СведенияОбУчете.Контрагент) Тогда
				Если НЕ ЗначениеЗаполнено(СведенияОбУчете.Партнер) Тогда
					ТекстНадписи = ?(СведенияОбУчете.ПолученВАренду, НСтр("ru = 'Арендодатель:';
																			|en = 'Lessor:'"), НСтр("ru = 'Арендатор:';
																										|en = 'Lessee:'"));
				Иначе
					ТекстНадписи = НСтр("ru = 'Контрагент:';
										|en = 'Counterparty:'");
				КонецЕсли;
				СведенияКонтрагент.Добавить(ТекстНадписи);
				СведенияКонтрагент.Добавить(" ");
				СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.Контрагент);
				СведенияКонтрагент.Добавить(Новый ФорматированнаяСтрока(СведенияОбУчете.КонтрагентПредставление,,,, СсылкаНаОбъект));
			КонецЕсли; 

			Если ЗначениеЗаполнено(СведенияОбУчете.Договор) Тогда
				СведенияДоговор.Добавить(НСтр("ru = 'Договор:';
												|en = 'Contract:'"));
				СведенияДоговор.Добавить(" ");
				СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.Договор);
				СведенияДоговор.Добавить(Новый ФорматированнаяСтрока(СведенияОбУчете.ДоговорПредставление,,,, СсылкаНаОбъект));
			КонецЕсли; 
		
		ИначеЕсли СведенияОбУчете.СостояниеБУ = Перечисления.СостоянияОС.ПринятоКЗабалансовомуУчету
			ИЛИ СведенияОбУчете.СостояниеУУ = Перечисления.СостоянияОС.ПринятоКЗабалансовомуУчету Тогда
			
			Если СведенияОбУчете.ПолученоВАрендуОтДругойОрганизации Тогда
				СведенияПринятКУчету1.Добавить(НСтр("ru = 'Дата принятия к учету:';
													|en = 'Date of recognition:'"));
			Иначе
				СведенияПринятКУчету1.Добавить(НСтр("ru = 'Дата принятия к забалансовому учету:';
													|en = 'Date of entering in off-balance accounting:'"));
			КонецЕсли;
			СведенияПринятКУчету1.Добавить(" ");
			ДатаДокумента = Формат(СведенияОбУчете.ДатаВводаВЭксплуатациюБУ, "ДЛФ=D");
			Если ЗначениеЗаполнено(СведенияОбУчете.ДокументВводаВЭксплуатациюБУ) Тогда
				СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.ДокументВводаВЭксплуатациюБУ);
			Иначе
				СсылкаНаОбъект = Неопределено;
			КонецЕсли;
			СведенияПринятКУчету1.Добавить(Новый ФорматированнаяСтрока(ДатаДокумента,,,, СсылкаНаОбъект));
			
			// Сведения об аренде
			Если ЗначениеЗаполнено(СведенияОбУчете.Партнер) Тогда
				СведенияПартнер.Добавить(НСтр("ru = 'Арендодатель:';
												|en = 'Lessor:'"));
				СведенияПартнер.Добавить(" ");
				СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.Партнер);
				СведенияПартнер.Добавить(Новый ФорматированнаяСтрока(СведенияОбУчете.ДоговорПредставление,,,, СсылкаНаОбъект));
			КонецЕсли; 

			Если ЗначениеЗаполнено(СведенияОбУчете.Контрагент) Тогда
				Если НЕ ЗначениеЗаполнено(СведенияОбУчете.Партнер) Тогда
					ТекстНадписи = ?(СведенияОбУчете.ПолученВАренду, НСтр("ru = 'Арендодатель:';
																			|en = 'Lessor:'"), НСтр("ru = 'Арендатор:';
																										|en = 'Lessee:'"));
				Иначе
					ТекстНадписи = НСтр("ru = 'Контрагент:';
										|en = 'Counterparty:'");
				КонецЕсли;
				СведенияКонтрагент.Добавить(ТекстНадписи);
				СведенияКонтрагент.Добавить(" ");
				СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.Контрагент);
				СведенияКонтрагент.Добавить(Новый ФорматированнаяСтрока(СведенияОбУчете.ДоговорПредставление,,,, СсылкаНаОбъект));
			КонецЕсли; 

			Если ЗначениеЗаполнено(СведенияОбУчете.Договор) Тогда
				СведенияДоговор.Добавить(НСтр("ru = 'Договор:';
												|en = 'Contract:'"));
				СведенияДоговор.Добавить(" ");
				СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.Договор);
				СведенияДоговор.Добавить(Новый ФорматированнаяСтрока(СведенияОбУчете.ДоговорПредставление,,,, СсылкаНаОбъект));
			КонецЕсли; 
			
		КонецЕсли; 
		
	Иначе
		СведенияПринятКУчету1.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Принять к учету';
																		|en = 'recognize'"),,,, "#Создать"));
	КонецЕсли;
	
	#КонецОбласти
	
	#Область Местонахождение
	
	Если СведенияОбУчете <> Неопределено Тогда
		
		Период = СведенияОбУчете.ПериодСведенийОМестонахождении; 
		
		СведенияМестонахождениеОрганизация.Добавить(СтрШаблон(НСтр("ru = 'Организация: %1';
																	|en = 'Company: %1'"), СведенияОбУчете.ОрганизацияПредставление));
		СведенияМестонахождениеПодразделение.Добавить(СтрШаблон(НСтр("ru = 'Подразделение: %1';
																	|en = 'Department: %1'"), СведенияОбУчете.ПодразделениеПредставление));
		
		СведенияМестонахождениеМОЛ.Добавить(НСтр("ru = 'МОЛ:';
												|en = 'Responsible employee:'"));
		СведенияМестонахождениеМОЛ.Добавить(" ");
		Если ЗначениеЗаполнено(СведенияОбУчете.МОЛ) Тогда
			СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(СведенияОбУчете.МОЛ);
			СведенияМестонахождениеМОЛ.Добавить(Новый ФорматированнаяСтрока(СведенияОбУчете.МОЛПредставление,,,, СсылкаНаОбъект));
		ИначеЕсли ВключитьНезаполненные Тогда
			СведенияМестонахождениеМОЛ.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'не указан';
																				|en = 'not specified'"),, ЦветаСтиля.НезаполненноеПолеТаблицы));
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СведенияОбУчете.АдресМестонахождения) Тогда
			СведенияМестонахождениеАдрес.Добавить(НСтр("ru = 'Адрес:';
														|en = 'Branch ID:'"));
			СведенияМестонахождениеАдрес.Добавить(" ");
			СведенияМестонахождениеАдрес.Добавить(СведенияОбУчете.АдресМестонахождения);
			СведенияМестонахождениеАдрес.Добавить(" ");
			СведенияМестонахождениеАдрес.Добавить(Новый ФорматированнаяСтрока(БиблиотекаКартинок.ПоказатьНаКарте,,,, СведенияОбУчете.АдресМестонахождения));
		ИначеЕсли ВключитьНезаполненные Тогда
			СведенияМестонахождениеАдрес.Добавить(НСтр("ru = 'Адрес:';
														|en = 'Branch ID:'"));
			СведенияМестонахождениеАдрес.Добавить(" ");
			СведенияМестонахождениеАдрес.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'не указан';
																					|en = 'not specified'"),, ЦветаСтиля.НезаполненноеПолеТаблицы));
		КонецЕсли; 
		
	КонецЕсли; 
	
	#КонецОбласти
	
	#Область Суммы
	
	Если НЕ ДоступенВыборОтраженияВУчетах 
		И ВалютаРегл = ВалютаУпр
		И СведенияОбУчете <> Неопределено Тогда
		
		ПредставлениеСуммы = ВнеоборотныеАктивыСлужебный.ПредставлениеСуммы(
			СтоимостьИАмортизация.Стоимость, НСтр("ru = 'Восстановительная стоимость:';
													|en = 'Replacement cost:'"), ВалютаУпр);
		СведенияВосстановительнаяСтоимость.Добавить(ПредставлениеСуммы);
		
		ПредставлениеСуммы = ВнеоборотныеАктивыСлужебный.ПредставлениеСуммы(
			СтоимостьИАмортизация.Амортизация, НСтр("ru = 'Накопленная амортизация:';
													|en = 'Accumulated depreciation:'"), ВалютаУпр);
		СведенияНакопленнаяАмортизация.Добавить(ПредставлениеСуммы);
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьОбесценениеВНА") Тогда
			ПредставлениеСуммы = ВнеоборотныеАктивыСлужебный.ПредставлениеСуммы(
				СтоимостьИАмортизация.ОбесценениеУпр, НСтр("ru = 'Обесценение:';
															|en = 'Impairment:'"), ВалютаУпр);
			СведенияОбесценение.Добавить(ПредставлениеСуммы);
		КонецЕсли;
		
		ПредставлениеСуммы = ВнеоборотныеАктивыСлужебный.ПредставлениеСуммы(
			СтоимостьИАмортизация.Стоимость - СтоимостьИАмортизация.Амортизация - СтоимостьИАмортизация.ОбесценениеУпр,
			НСтр("ru = 'Остаточная стоимость:';
				|en = 'Net book value:'"), ВалютаУпр);
		СведенияОстаточнаяСтоимость.Добавить(ПредставлениеСуммы);
		
	КонецЕсли;
	
	#КонецОбласти
	
	ПредставлениеСведений = Новый Структура;
	ПредставлениеСведений.Вставить("Период", Период);
	ПредставлениеСведений.Вставить("СведенияПринятКУчету1", СведенияПринятКУчету1);
	ПредставлениеСведений.Вставить("СведенияПринятКУчету2", СведенияПринятКУчету2);
	ПредставлениеСведений.Вставить("СведенияПринятКУчету3", СведенияПринятКУчету3);
	ПредставлениеСведений.Вставить("СведенияСрокИспользования1", СведенияСрокИспользования1);
	ПредставлениеСведений.Вставить("СведенияСрокИспользования2", СведенияСрокИспользования2);
	ПредставлениеСведений.Вставить("СведенияСрокИспользования3", СведенияСрокИспользования3);
	ПредставлениеСведений.Вставить("СведенияЛиквидационнаяСтоимость", СведенияЛиквидационнаяСтоимость);
	ПредставлениеСведений.Вставить("СведенияЛиквидационнаяСтоимостьРегл", СведенияЛиквидационнаяСтоимостьРегл);
	ПредставлениеСведений.Вставить("СведенияСнятСУчета1", СведенияСнятСУчета1);
	ПредставлениеСведений.Вставить("СведенияСнятСУчета2", СведенияСнятСУчета2);
	ПредставлениеСведений.Вставить("СведенияСнятСУчета3", СведенияСнятСУчета3);
	ПредставлениеСведений.Вставить("СведенияГФУ", СведенияГФУ);
	ПредставлениеСведений.Вставить("СведенияПартнер", СведенияПартнер);
	ПредставлениеСведений.Вставить("СведенияКонтрагент", СведенияКонтрагент);
	ПредставлениеСведений.Вставить("СведенияДоговор", СведенияДоговор);
	ПредставлениеСведений.Вставить("СведенияМестонахождениеОрганизация", СведенияМестонахождениеОрганизация);
	ПредставлениеСведений.Вставить("СведенияМестонахождениеПодразделение", СведенияМестонахождениеПодразделение);
	ПредставлениеСведений.Вставить("СведенияМестонахождениеМОЛ", СведенияМестонахождениеМОЛ);
	ПредставлениеСведений.Вставить("СведенияМестонахождениеАдрес", СведенияМестонахождениеАдрес);
	ПредставлениеСведений.Вставить("СведенияВосстановительнаяСтоимость", СведенияВосстановительнаяСтоимость);
	ПредставлениеСведений.Вставить("СведенияНакопленнаяАмортизация", СведенияНакопленнаяАмортизация);
	ПредставлениеСведений.Вставить("СведенияОбесценение", СведенияОбесценение);
	ПредставлениеСведений.Вставить("СведенияОстаточнаяСтоимость", СведенияОстаточнаяСтоимость);
	
	Возврат ПредставлениеСведений;

КонецФункции

// Возвращает владельца узла или компонента амортизации по состоянию на дату.
// 
// Параметры:
//  УзелКомпонент - СправочникСсылка.ОбъектыЭксплуатации - узел или компонент амортизации
//  ДатаСведений - Дата - Дата сведений, если не указано, то на текущую дату.
// 
// Возвращаемое значение:
//  СправочникСсылка.ОбъектыЭксплуатации - Владелец узла или компонента амортизации.
//
Функция ВладелецУзлаКомпонентаАмортизации(УзелКомпонент, ДатаСведений = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(УзелКомпонент) Тогда
		Возврат ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УзелКомпонент", УзелКомпонент);
	Запрос.УстановитьПараметр("Период", ?(ДатаСведений = Неопределено, ТекущаяДатаСеанса(), ДатаСведений));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УзлыКомпоненты.ОсновноеСредство КАК ОсновноеСредство
	|ИЗ
	|	РегистрСведений.УзлыКомпонентыАмортизации.СрезПоследних(&Период, УзелКомпонент = &УзелКомпонент) КАК УзлыКомпоненты";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ОсновноеСредство;
	Иначе
		Возврат ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОписаниеЗапросаДляВыбора(Параметры, УстановитьВсеОтборы = Ложь, МассивОбъектов = Неопределено) Экспорт

	Если Параметры.Свойство("ВладелецУзлаКомпонентаОС") Тогда
		ДатаСведений = Неопределено;
		Параметры.Свойство("ДатаСведений", ДатаСведений);
		МассивОбъектов = ПолучитьМассивУзловКомпонентовАмортизацииПоВладельцу(Параметры.ВладелецУзлаКомпонентаОС, ДатаСведений);
	КонецЕсли;

	ОписаниеЗапросаДляВыбора = ОбъектыЭксплуатацииЛокализация.ОписаниеЗапросаДляВыбора(Параметры, УстановитьВсеОтборы, МассивОбъектов);
	
	Если ОписаниеЗапросаДляВыбора <> Неопределено Тогда
		Возврат ОписаниеЗапросаДляВыбора;
	КонецЕсли;
	
	Если Параметры.Свойство("Контекст") Тогда
		ДоступныеКонтексты = Новый Структура(Параметры.Контекст);
	Иначе
		ДоступныеКонтексты = Новый Структура("УУ"); // По умолчанию доступны все контексты.
	КонецЕсли;
	
	ОтборСписка = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "Отбор", Новый Структура);
	
	Если ОтборСписка.Свойство("ОтражатьВРеглУчете")
		И НЕ ОтборСписка.ОтражатьВРеглУчете
		И ДоступныеКонтексты.Свойство("БУ") Тогда
		ДоступныеКонтексты.Удалить("БУ"); // Доступен выбор отражения в учете и в регл. учете нет отражения.
	КонецЕсли; 
	
	Если ОтборСписка.Свойство("ОтражатьВУпрУчете")
		И НЕ ОтборСписка.ОтражатьВУпрУчете
		И ДоступныеКонтексты.Свойство("УУ") Тогда
		ДоступныеКонтексты.Удалить("УУ"); // Доступен выбор отражения в учете и в упр. учете нет отражения.
	КонецЕсли; 
	
	Если НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.МестонахождениеОС) Тогда
		ОтборСписка.Удалить("Подразделение");
		ОтборСписка.Удалить("Организация");
		ОтборСписка.Удалить("МОЛ");
	КонецЕсли;
	
	Если ОтборСписка.Свойство("Организация")
		И НЕ ЗначениеЗаполнено(ОтборСписка.Организация) Тогда
		ОтборСписка.Удалить("Организация");
	КонецЕсли; 
	
	Если ОтборСписка.Свойство("Подразделение")
		И НЕ ЗначениеЗаполнено(ОтборСписка.Подразделение) Тогда
		ОтборСписка.Удалить("Подразделение");
	КонецЕсли; 
	
	Если ОтборСписка.Свойство("МОЛ")
		И НЕ ЗначениеЗаполнено(ОтборСписка.МОЛ) Тогда
		ОтборСписка.Удалить("МОЛ");
	КонецЕсли; 
	
	Если ОтборСписка.Свойство("Договор")
		И НЕ ЗначениеЗаполнено(ОтборСписка.Договор) Тогда
		ОтборСписка.Удалить("Договор");
	КонецЕсли; 
	
	ЭтоВыборУзлаКомпонента = Ложь;
	ЭтоВыборОС = Ложь;
	Если ОтборСписка.Свойство("ТипОС")
		И (ОтборСписка.ТипОС = Перечисления.ТипыОС.Узел
		Или ОтборСписка.ТипОС = Перечисления.ТипыОС.КомпонентАмортизации) Тогда
		ЭтоВыборУзлаКомпонента = Истина;
	ИначеЕсли ОтборСписка.Свойство("ТипОС")
		И ОтборСписка.ТипОС = Перечисления.ТипыОС.ОбъектЭксплуатации Тогда
		ЭтоВыборОС = Истина;
	КонецЕсли;
	
	ИсключитьКомпоненты = Ложь;
	Если ОтборСписка.Свойство("Владелец")
		И ТипЗнч(ОтборСписка.Владелец) = Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов")
		И ДоступныеКонтексты.Свойство("БУ") Тогда
		ИсключитьКомпоненты = Истина; // Компоненты амортизации запрещены для выбора в качестве аналитики расходов
	КонецЕсли; 
	
	ДополнительныеПоля = "";
	ТекстОтборы = "";
	ПараметрыЗапроса = Новый Структура;
	ДоступныеПоля = Новый Массив;
	НеобходимыеТаблицы = Новый Структура;
	
	Если ДоступныеКонтексты.Свойство("УУ")
		И ПравоДоступа("Чтение", Метаданные.РегистрыСведений.МестонахождениеОС) Тогда
		
		ПолеОрганизация = "ЕСТЬNULL(МестонахождениеОС.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))";
		ПолеМОЛ = "ЕСТЬNULL(МестонахождениеОС.МОЛ, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка))";
		ПолеПодразделение = "ЕСТЬNULL(МестонахождениеОС.Местонахождение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))";
		ПолеАдресМестонахождения = "ЕСТЬNULL(МестонахождениеОС.АдресМестонахождения, """""""")";
		
		НеобходимыеТаблицы.Вставить("МестонахождениеОС");
		
	Иначе
		
		ПолеОрганизация = "ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)";
		ПолеМОЛ = "ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)";
		ПолеПодразделение = "ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)";
		ПолеАдресМестонахождения = """""";
		
	КонецЕсли;
	
	Если ДоступныеКонтексты.Свойство("УУ")
		И ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПорядокУчетаОС) Тогда
		
		ПолеГФУ = "ЕСТЬNULL(ПорядокУчетаОС.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаВнеоборотныхАктивов.ПустаяСсылка))";
		
		НеобходимыеТаблицы.Вставить("ПорядокУчетаОС");
		
	Иначе
		
		ПолеГФУ = "ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаВнеоборотныхАктивов.ПустаяСсылка)";
		
	КонецЕсли;
	
	Если (ДоступныеКонтексты.Свойство("БУ") ИЛИ ДоступныеКонтексты.Свойство("УУ"))
		И ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПервоначальныеСведенияОС)
		И ПравоДоступа("Чтение", Метаданные.РегистрыСведений.МестонахождениеОС) Тогда
		
		ПолеДатаПринятияКУчетуРегл = "ДАТАВРЕМЯ(1, 1, 1)";
		ПолеДатаПринятияКУчетуУпр = "ЕСТЬNULL(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ, ДАТАВРЕМЯ(1, 1, 1))";
		
		НеобходимыеТаблицы.Вставить("ПервоначальныеСведенияОС");
		
	Иначе
		ПолеДатаПринятияКУчетуРегл = "ДАТАВРЕМЯ(1, 1, 1)";
		ПолеДатаПринятияКУчетуУпр = "ДАТАВРЕМЯ(1, 1, 1)";
	КонецЕсли;
	
	Если (ДоступныеКонтексты.Свойство("БУ") ИЛИ ДоступныеКонтексты.Свойство("УУ"))
		И ОтборСписка.Свойство("Договор")
		И ПравоДоступа("Чтение", Метаданные.РегистрыСведений.АрендованныеОС) Тогда
		
		ПолеДоговор = "ЕСТЬNULL(АрендованныеОС.Договор, НЕОПРЕДЕЛЕНО)";
		
		НеобходимыеТаблицы.Вставить("АрендованныеОС");
		
	Иначе
		ПолеДоговор = "НЕОПРЕДЕЛЕНО";
	КонецЕсли;
	
	Если Параметры.Свойство("РеквизитыКоторыеДолжныСовпадать")
			И СтрНайти(Параметры.РеквизитыКоторыеДолжныСовпадать, "СрокИспользованияУУ") <> 0 
		ИЛИ ОтборСписка.Свойство("СрокИспользованияУУ") Тогда
		
		ПолеСрокИспользованияУУ = "ЕСТЬNULL(ПараметрыАмортизацииОСУУ.СрокИспользования, 0)";
		
		НеобходимыеТаблицы.Вставить("ПараметрыАмортизацииОСУУ");
		
	Иначе
		
		ПолеСрокИспользованияУУ = "0";
		
	КонецЕсли;
	
	Если Параметры.Свойство("ИспользоватьОтборПоОстаточномуСПИ") И Параметры.Свойство("ДатаСведений")
		И ЗначениеЗаполнено(Параметры.ДатаСведений) Тогда
		
		ПолеОстаточныйСрокИспользованияУУ = 
			"ВЫБОР 
			|	КОГДА ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ ЕСТЬ NULL
			|		ТОГДА 0
			|	КОГДА ПараметрыАмортизацииОСУУ.СрокИспользования - РАЗНОСТЬДАТ(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ, НАЧАЛОПЕРИОДА(&ДатаСведений, ДЕНЬ), МЕСЯЦ) < 0
			|		ТОГДА 0
			|	ИНАЧЕ ПараметрыАмортизацииОСУУ.СрокИспользования - РАЗНОСТЬДАТ(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ, НАЧАЛОПЕРИОДА(&ДатаСведений, ДЕНЬ), МЕСЯЦ)
			|	КОНЕЦ";
		
		НеобходимыеТаблицы.Вставить("ПервоначальныеСведенияОС");
		НеобходимыеТаблицы.Вставить("ПараметрыАмортизацииОСУУ");
		
	Иначе
		
		ПолеОстаточныйСрокИспользованияУУ = "0";
		
	КонецЕсли;
	
	Если Параметры.Свойство("РегистрацияНаработки")
		ИЛИ Параметры.Свойство("УстановкаНаработки")
		ИЛИ Параметры.Свойство("ИсточникПоказателяНаработки") Тогда
		НеобходимыеТаблицы.Вставить("ПараметрыАмортизацииОСУУ");
	КонецЕсли;
				
	ДополнительныеПоля = ДополнительныеПоля + "
		|,%1 КАК Организация
		|,%2 КАК МОЛ
		|,%3 КАК Подразделение
		|,%4 КАК АдресМестонахождения
		|,%5 КАК ГруппаФинансовогоУчета
		|,%6 КАК СрокИспользованияУУ
		|,0 КАК СрокИспользованияБУ
		|,0 КАК СрокИспользованияНУ
		|,НЕОПРЕДЕЛЕНО КАК НалогообложениеНДС
		|,%7 КАК ДатаПринятияКУчетуРегл
		|,%8 КАК ДатаПринятияКУчетуУпр
		|,%9 КАК Договор
		|,%10 КАК ОстаточныйСрокИспользованияУУ
		|,0 КАК ОстаточныйСрокИспользованияБУ
		|,0 КАК ОстаточныйСрокИспользованияНУ";
	
	ДополнительныеПоля = СтрШаблон(
		ДополнительныеПоля, 
		ПолеОрганизация, 
		ПолеМОЛ, 
		ПолеПодразделение, 
		ПолеАдресМестонахождения, 
		ПолеГФУ,
		ПолеСрокИспользованияУУ,
		ПолеДатаПринятияКУчетуРегл,
		ПолеДатаПринятияКУчетуУпр,
		ПолеДоговор,
		ПолеОстаточныйСрокИспользованияУУ);
	
	Если ДоступныеКонтексты.Свойство("УУ")
		И ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПорядокУчетаОСУУ) Тогда
		
		ДополнительныеПоля = ДополнительныеПоля + "
			|,ВЫБОР 
			|	КОГДА НЕ СправочникОбъектыЭксплуатации.ЭтоГруппа 
			|		ТОГДА ЕСТЬNULL(ПорядокУчетаОСУУ.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияОС.НеПринятоКУчету))
			|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПустаяСсылка)
			|	КОНЕЦ КАК СостояниеУУ";
		
		НеобходимыеТаблицы.Вставить("ПорядокУчетаОСУУ");
	Иначе
		
		ДополнительныеПоля = ДополнительныеПоля + ",ВЫБОР 
			|	КОГДА НЕ СправочникОбъектыЭксплуатации.ЭтоГруппа 
			|		ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияОС.НеПринятоКУчету)
			|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПустаяСсылка)
			|	КОНЕЦ КАК СостояниеУУ";
		
	КонецЕсли;
	
	Если ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПорядокУчетаОСУУ) Тогда
		
		Если ОтборСписка.Свойство("Состояние") Тогда
				
			Если ДоступныеКонтексты.Свойство("УУ") Тогда
				
				ТекстУсловия = "ЕСТЬNULL(ПорядокУчетаОСУУ.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияОС.НеПринятоКУчету)) В(&Состояние)";
				ВнеоборотныеАктивыСлужебный.ДобавитьТекстОтбора(ТекстОтборы, ТекстУсловия);
			
				Если ТипЗнч(ОтборСписка.Состояние) = Тип("ФиксированныйМассив") Тогда
					ПараметрыЗапроса.Вставить("Состояние", Новый Массив(ОтборСписка.Состояние));
				Иначе
					ПараметрыЗапроса.Вставить("Состояние", ОтборСписка.Состояние);
				КонецЕсли;

			КонецЕсли;
			
			Параметры.Отбор.Удалить("Состояние");
			
		Иначе
			
			Если ДоступныеКонтексты.Свойство("УУ") Тогда
				
				ТекстУсловия = 
				"(ЕСТЬNULL(ПорядокУчетаОСУУ.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияОС.НеПринятоКУчету)) = &Состояние
				|		ИЛИ &Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПустаяСсылка))";
				ВнеоборотныеАктивыСлужебный.ДобавитьТекстОтбора(ТекстОтборы, ТекстУсловия);
				
				ПараметрыЗапроса.Вставить("Состояние", Перечисления.СостоянияОС.ПустаяСсылка());
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли; 
	
	Если ЭтоВыборОС Тогда
		
		ТекстУсловия = "СправочникОбъектыЭксплуатации.ТипОС = ЗНАЧЕНИЕ(Перечисление.ТипыОС.ОбъектЭксплуатации)";
		ВнеоборотныеАктивыСлужебный.ДобавитьТекстОтбора(ТекстОтборы, ТекстУсловия);
			
	ИначеЕсли ИсключитьКомпоненты Тогда
		
		ТекстУсловия = "НЕ СправочникОбъектыЭксплуатации.ТипОС = ЗНАЧЕНИЕ(Перечисление.ТипыОС.КомпонентАмортизации)";
		ВнеоборотныеАктивыСлужебный.ДобавитьТекстОтбора(ТекстОтборы, ТекстУсловия);
			
	КонецЕсли;
	
	Если УстановитьВсеОтборы Тогда
		
		Для каждого КлючИЗначение Из ОтборСписка Цикл
			
			Если НЕ ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
				Продолжить;
			КонецЕсли;
			
			Если КлючИЗначение.Ключ = "Организация" Тогда
				ПутьКПолю = ПолеОрганизация;
			ИначеЕсли КлючИЗначение.Ключ = "Подразделение" Тогда
				ПутьКПолю = ПолеПодразделение;
			ИначеЕсли КлючИЗначение.Ключ = "МОЛ" Тогда
				ПутьКПолю = ПолеМОЛ;
			ИначеЕсли КлючИЗначение.Ключ = "СрокИспользованияУУ" Тогда
				ПутьКПолю = ПолеСрокИспользованияУУ;
			ИначеЕсли КлючИЗначение.Ключ = "Договор" Тогда
				ПутьКПолю = ПолеДоговор;
			ИначеЕсли КлючИЗначение.Ключ = "НаправлениеДеятельности" Тогда
				ПутьКПолю = "ЕСТЬNULL(ПорядокУчетаОС.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))";
			ИначеЕсли КлючИЗначение.Ключ = "Статус"
				ИЛИ КлючИЗначение.Ключ = "ТипОС" Тогда
				ПутьКПолю = "СправочникОбъектыЭксплуатации." + КлючИЗначение.Ключ;
			Иначе
				Продолжить;
			КонецЕсли;
			
			ЭтоМассив = ТипЗнч(КлючИЗначение.Значение) = Тип("ФиксированныйМассив") 
						ИЛИ ТипЗнч(КлючИЗначение.Значение) = Тип("Массив");
						
			ТекстУсловия = ПутьКПолю + ?(ЭтоМассив, " В (&" + КлючИЗначение.Ключ + ")", " = &" + КлючИЗначение.Ключ);
			ВнеоборотныеАктивыСлужебный.ДобавитьТекстОтбора(ТекстОтборы, ТекстУсловия);
						
			ПараметрыЗапроса.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
			
		КонецЦикла; 
		
	КонецЕсли; 
	
	ТекстСоединения = "";
	
	Если НеобходимыеТаблицы.Свойство("МестонахождениеОС")
		ИЛИ НеобходимыеТаблицы.Свойство("ПервоначальныеСведенияОС")
		ИЛИ НеобходимыеТаблицы.Свойство("ПорядокУчетаОСУУ") Тогда
		
		Если ЭтоВыборУзлаКомпонента Тогда
			ТекстСоединения = ТекстСоединения + "
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УзлыКомпонентыАмортизации.СрезПоследних(&ПАРАМЕТРЫ_СРЕЗАПОСЛЕДНИХ) КАК УзлыКомпоненты
			|		ПО СправочникОбъектыЭксплуатации.Ссылка = УзлыКомпоненты.УзелКомпонент
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС.СрезПоследних(&ПАРАМЕТРЫ_СРЕЗАПОСЛЕДНИХ) КАК МестонахождениеОС
			|		ПО УзлыКомпоненты.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство";
		Иначе
			ТекстСоединения = ТекстСоединения + "
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС.СрезПоследних(&ПАРАМЕТРЫ_СРЕЗАПОСЛЕДНИХ) КАК МестонахождениеОС
			|		ПО СправочникОбъектыЭксплуатации.Ссылка = МестонахождениеОС.ОсновноеСредство";
		КонецЕсли;
		
	КонецЕсли;
	
	Если НеобходимыеТаблицы.Свойство("ПервоначальныеСведенияОС") Тогда
		ТекстСоединения = ТекстСоединения + "
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(&ПАРАМЕТРЫ_СРЕЗАПОСЛЕДНИХ) КАК ПервоначальныеСведенияОС
		|		ПО ПервоначальныеСведенияОС.Организация = МестонахождениеОС.Организация
		|			И ПервоначальныеСведенияОС.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство";
	КонецЕсли;
	
	Если НеобходимыеТаблицы.Свойство("ПорядокУчетаОСУУ") Тогда
		ТекстСоединения = ТекстСоединения + "
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(&ПАРАМЕТРЫ_СРЕЗАПОСЛЕДНИХ) КАК ПорядокУчетаОСУУ
		|		ПО СправочникОбъектыЭксплуатации.Ссылка = ПорядокУчетаОСУУ.ОсновноеСредство
		|			И ПорядокУчетаОСУУ.Организация = МестонахождениеОС.Организация";
	КонецЕсли;
	
	Если НеобходимыеТаблицы.Свойство("ПараметрыАмортизацииОСУУ") Тогда
		ТекстСоединения = ТекстСоединения + "
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСУУ.СрезПоследних(&ПАРАМЕТРЫ_СРЕЗАПОСЛЕДНИХ) КАК ПараметрыАмортизацииОСУУ
		|		ПО СправочникОбъектыЭксплуатации.Ссылка = ПараметрыАмортизацииОСУУ.ОсновноеСредство
		|			И ПараметрыАмортизацииОСУУ.Организация = МестонахождениеОС.Организация";
	КонецЕсли;
	
	Если НеобходимыеТаблицы.Свойство("АрендованныеОС") Тогда
		ТекстСоединения = ТекстСоединения + "
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АрендованныеОС.СрезПоследних(&ПАРАМЕТРЫ_СРЕЗАПОСЛЕДНИХ) КАК АрендованныеОС
		|		ПО СправочникОбъектыЭксплуатации.Ссылка = АрендованныеОС.ОсновноеСредство";
	КонецЕсли;
	
	Если Параметры.Свойство("СтрокаПоиска") Тогда
		
		ТекстУсловия = 
			"(СправочникОбъектыЭксплуатации.Наименование ПОДОБНО &СтрокаПоиска
			|			ИЛИ СправочникОбъектыЭксплуатации.ИнвентарныйНомер ПОДОБНО &СтрокаПоиска)
			|	И НЕ СправочникОбъектыЭксплуатации.ЭтоГруппа
			|	И НЕ СправочникОбъектыЭксплуатации.ПометкаУдаления";
		ВнеоборотныеАктивыСлужебный.ДобавитьТекстОтбора(ТекстОтборы, ТекстУсловия);
			
		ПараметрыЗапроса.Вставить("СтрокаПоиска", "%" + Параметры.СтрокаПоиска + "%");
		
	КонецЕсли;
	
	Если МассивОбъектов <> Неопределено Тогда
		
		ТекстУсловия = 
			"СправочникОбъектыЭксплуатации.Ссылка В(&МассивОбъектов)
			|	И НЕ СправочникОбъектыЭксплуатации.ЭтоГруппа
			|	И НЕ СправочникОбъектыЭксплуатации.ПометкаУдаления";
		ВнеоборотныеАктивыСлужебный.ДобавитьТекстОтбора(ТекстОтборы, ТекстУсловия);

		ПараметрыЗапроса.Вставить("МассивОбъектов", МассивОбъектов);
		
	КонецЕсли; 
	
	Если Параметры.Свойство("РегистрацияНаработки")
		ИЛИ Параметры.Свойство("УстановкаНаработки")
		ИЛИ Параметры.Свойство("ИсточникПоказателяНаработки") Тогда
		
		ТекстУсловия = "";
		Если ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеРемонтами") Тогда
			
			//++ НЕ УТКА
			ПараметрыЗапроса.Вставить(
				"РегистрацияНаработки",
				?(Параметры.Свойство("РегистрацияНаработки"), Параметры.РегистрацияНаработки, Ложь));
	
			ПараметрыЗапроса.Вставить(
				"УстановкаНаработки",
				?(Параметры.Свойство("УстановкаНаработки"), Параметры.УстановкаНаработки, Ложь));
	
			ПараметрыЗапроса.Вставить(
				"ИсточникПоказателяНаработки",
				?(Параметры.Свойство("ИсточникПоказателяНаработки"), Параметры.ИсточникПоказателяНаработки, Ложь));
	
			ПараметрыЗапроса.Вставить(
				"ПоказательНаработки",
				?(Параметры.Свойство("ПоказательНаработки"), Параметры.ПоказательНаработки, Неопределено));

			ТекстУсловия = "СправочникОбъектыЭксплуатации.Класс В
			|	(ВЫБРАТЬ
			|		ПараметрыПоказателейКласса.Ссылка
			|	ИЗ
			|		Справочник.КлассыОбъектовЭксплуатации.ПоказателиНаработки КАК ПараметрыПоказателейКласса
			|	ГДЕ
			|		НЕ ПараметрыПоказателейКласса.Ссылка.ПометкаУдаления
			|		И ВЫБОР
			|			КОГДА &РегистрацияНаработки
			|				ТОГДА НЕ ПараметрыПоказателейКласса.РегистрироватьОтИсточника
			|			КОГДА &УстановкаНаработки
			|				ТОГДА ИСТИНА
			|			КОГДА &ИсточникПоказателяНаработки
			|				ТОГДА ПараметрыПоказателейКласса.ПоказательНаработки = &ПоказательНаработки
			|			ИНАЧЕ ИСТИНА
			|		КОНЕЦ)";
			//-- НЕ УТКА
			
		Иначе
			
			ТекстУсловия = "ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)";
			
		КонецЕсли;
			
		ВнеоборотныеАктивыСлужебный.ДобавитьТекстОтбора(ТекстОтборы, ТекстУсловия);
		
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ТекстОтборы) Тогда
		ТекстОтборы = "
		|ГДЕ
		|" + ТекстОтборы;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 10
	|	СправочникОбъектыЭксплуатации.Ссылка,
	|	СправочникОбъектыЭксплуатации.ПометкаУдаления,
	|	СправочникОбъектыЭксплуатации.Родитель,
	|	СправочникОбъектыЭксплуатации.ЭтоГруппа,
	|	СправочникОбъектыЭксплуатации.Код,
	|	СправочникОбъектыЭксплуатации.Наименование,
	|	СправочникОбъектыЭксплуатации.ТипОС,
	|	СправочникОбъектыЭксплуатации.ИнвентарныйНомер,
	|	СправочникОбъектыЭксплуатации.ГруппаОСМеждународныйУчет,
	|	СправочникОбъектыЭксплуатации.ДатаВыпуска,
	|	СправочникОбъектыЭксплуатации.ЗаводскойНомер,
	|	СправочникОбъектыЭксплуатации.Изготовитель,
	|	СправочникОбъектыЭксплуатации.Класс,
	|	СправочникОбъектыЭксплуатации.Модель,
	|	СправочникОбъектыЭксплуатации.НаименованиеПолное,
	|	СправочникОбъектыЭксплуатации.НомерПаспорта,
	|	СправочникОбъектыЭксплуатации.ОбъектБытовогоНазначения,
	|	СправочникОбъектыЭксплуатации.Подкласс,
	|	СправочникОбъектыЭксплуатации.Расположение,
	|	СправочникОбъектыЭксплуатации.РемонтирующееПодразделение,
	|	СправочникОбъектыЭксплуатации.СерийныйНомер,
	|	СправочникОбъектыЭксплуатации.Статус,
	|	ЕСТЬNULL(ПорядокУчетаОС.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
	|	СправочникОбъектыЭксплуатации.Комментарий
	|	,&ДОПОЛНИТЕЛЬНЫЕ_ПОЛЯ
	|ИЗ
	|	Справочник.ОбъектыЭксплуатации КАК СправочникОбъектыЭксплуатации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС.СрезПоследних(&ПАРАМЕТРЫ_СРЕЗАПОСЛЕДНИХ) КАК ПорядокУчетаОС
	|		ПО СправочникОбъектыЭксплуатации.Ссылка = ПорядокУчетаОС.ОсновноеСредство";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&ДОПОЛНИТЕЛЬНЫЕ_ПОЛЯ", ДополнительныеПоля);
	
	ТекстЗапроса = ТекстЗапроса + Символы.ПС + ТекстСоединения;
	ТекстЗапроса = ТекстЗапроса + Символы.ПС + ТекстОтборы;
	
	Если НЕ Параметры.Свойство("СтрокаПоиска") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РАЗРЕШЕННЫЕ ПЕРВЫЕ 10", "");
	КонецЕсли;

	ПараметрыСрезаПоследних = "";
	Если Параметры.Свойство("ДатаСведений") Тогда
		ПараметрыСрезаПоследних = "&ДатаСведений";
		ПараметрыЗапроса.Вставить("ДатаСведений", КонецДня(Параметры.ДатаСведений));
	КонецЕсли; 
	Если Параметры.Свойство("ТекущийРегистратор") Тогда
		ПараметрыСрезаПоследних = ПараметрыСрезаПоследних 
			+ ",Регистратор <> &ТекущийРегистратор";
		ПараметрыЗапроса.Вставить("ТекущийРегистратор", Параметры.ТекущийРегистратор);
	КонецЕсли; 
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПАРАМЕТРЫ_СРЕЗАПОСЛЕДНИХ", ПараметрыСрезаПоследних);
	
	ОписаниеЗапросаДляВыбора = Новый Структура("ТекстЗапроса,ПараметрыЗапроса,ДоступныеПоля", ТекстЗапроса, ПараметрыЗапроса, ДоступныеПоля);

	
	Возврат ОписаниеЗапросаДляВыбора;
	
КонецФункции

// Возвращает массив узлов и компонентов амортизации по ОС-Владельцу
//
// Параметры:
//  ОсновноеСредство  - СправочникСсылка.ОбъектыЭксплуатации - ОС-Владелец или узел/компонент амортизации
//
// Возвращаемое значение:
//   Массив   - Массив подчиненныз узлов и компонентов амотризации
//
Функция ПолучитьМассивУзловКомпонентовАмортизацииПоВладельцу(ОсновноеСредство, ДатаСведений = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	&ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ ВтОСВладелец
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	УзлыКомпонентыАмортизации.ОсновноеСредство
		|ИЗ
		|	РегистрСведений.УзлыКомпонентыАмортизации.СрезПоследних(&ДатаСведений, УзелКомпонент = &ОсновноеСредство) КАК
		|		УзлыКомпонентыАмортизации
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	УзлыКомпоненты.УзелКомпонент КАК УзелКомпонент
		|ПОМЕСТИТЬ ВтУзлыОсновныхСредств
		|ИЗ
		|	РегистрСведений.УзлыКомпонентыАмортизации КАК УзлыКомпоненты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОСВладелец КАК ОСВладелец
		|		ПО УзлыКомпоненты.ОсновноеСредство = ОСВладелец.ОсновноеСредство
		|ГДЕ
		|	УзлыКомпоненты.УзелКомпонент <> &ОсновноеСредство //Если передан не владелец, а узел, то исключаем сам узел из выборки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	УзелКомпонент
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УзлыКомпоненты.УзелКомпонент КАК УзелКомпонент
		|ИЗ
		|	РегистрСведений.УзлыКомпонентыАмортизации.СрезПоследних(&ДатаСведений, УзелКомпонент В
		|		(ВЫБРАТЬ
		|			ВтУзлыОсновныхСредств.УзелКомпонент КАК УзелКомпонент
		|		ИЗ
		|			ВтУзлыОсновныхСредств КАК ВтУзлыОсновныхСредств)) КАК УзлыКомпоненты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОСВладелец КАК ОСВладелец
		|		ПО УзлыКомпоненты.ОсновноеСредство = ОСВладелец.ОсновноеСредство";
	
	Запрос.УстановитьПараметр("ОсновноеСредство", ОсновноеСредство);
	Запрос.УстановитьПараметр("ДатаСведений",     ДатаСведений);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("УзелКомпонент");

КонецФункции

Функция ИсторияИзмененияРеквизита(ОсновноеСредство, ИмяРеквизита) Экспорт

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДокументыПоОС.Дата КАК Дата,
	|	ДокументыПоОС.Ссылка КАК Документ
	|ИЗ
	|	РегистрСведений.ДокументыПоОС КАК ДокументыПоОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровОС2_4 КАК ИзменениеПараметровОС
	|		ПО (ИзменениеПараметровОС.Ссылка = ДокументыПоОС.Ссылка)
	|ГДЕ
	|	ДокументыПоОС.ОсновноеСредство = &ОсновноеСредство
	|	И ДокументыПоОС.ТипСсылки = &ТипСсылки
	|	И ДокументыПоОС.НомерЗаписи = 0
	|	И ДокументыПоОС.Проведен
	|	И НЕ ДокументыПоОС.ДополнительнаяЗапись
	|	И (ИзменениеПараметровОС.НаправлениеДеятельностиФлаг
	|				И &ИмяРеквизита = ""НаправлениеДеятельности""
	|			ИЛИ ИзменениеПараметровОС.АмортизационнаяГруппаФлаг
	|				И &ИмяРеквизита = ""АмортизационнаяГруппа""
	|			ИЛИ ИзменениеПараметровОС.НедвижимоеИмуществоФлаг
	|				И &ИмяРеквизита = ""НедвижимоеИмущество"")
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументыПоОС.Дата";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ОсновноеСредство", ОсновноеСредство);
	Запрос.УстановитьПараметр("ИмяРеквизита", ИмяРеквизита);
	Запрос.УстановитьПараметр("ТипСсылки", ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Тип("ДокументСсылка.ИзменениеПараметровОС2_4")));
	
	УстановитьПривилегированныйРежим(Истина);
	ИсторияИзменений = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);

	Возврат ИсторияИзменений;
	
КонецФункции

//++ НЕ УТКА

// Вызывается при изменении класса объекта эксплуатации.
// 
// Параметры:
// 	Объект - СправочникОбъект.ОбъектыЭксплуатации -
Процедура ПриИзмененииКласса(Объект) Экспорт
	
	Объект.Подкласс = Справочники.ПодклассыОбъектовЭксплуатации.ПустаяСсылка();
	
КонецПроцедуры

#Область ПроверкаЗаполненияОбъектовЭксплуатации

Процедура СформироватьЗапросПроверкиЗаполнения(ТекстЗапроса)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ОбъектыЭксплуатации.*
		|ИЗ
		|	Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
		|ГДЕ
		|	ОбъектыЭксплуатации.Ссылка В (&ОбъектыЭксплуатации)";
	
КонецПроцедуры

Процедура СформироватьЗапросПроверкиЗаполненияУзлов(ТекстЗапроса)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	УзлыОбъектовЭксплуатации.*
		|ИЗ
		|	Справочник.УзлыОбъектовЭксплуатации КАК УзлыОбъектовЭксплуатации
		|ГДЕ
		|	УзлыОбъектовЭксплуатации.Владелец В(&ОбъектыЭксплуатации)
		|	И УзлыОбъектовЭксплуатации.Владелец <> ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка)
		|	И НЕ УзлыОбъектовЭксплуатации.ПометкаУдаления
		|	И УзлыОбъектовЭксплуатации.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыОбъектовЭксплуатации.ВЭксплуатации), ЗНАЧЕНИЕ(Перечисление.СтатусыОбъектовЭксплуатации.ПустаяСсылка))";
	
КонецПроцедуры

Функция ПолучитьОбязательныеРеквизиты(Класс, КэшированныеЗначения)
	
	ОбязательныеРеквизиты = КэшированныеЗначения.ОбязательныеРеквизиты.Получить(Класс);
	
	Если ОбязательныеРеквизиты = Неопределено Тогда
		
		Запрос = Новый Запрос;
		
		Если ЗначениеЗаполнено(Класс) Тогда
			Запрос.Текст =
				"ВЫБРАТЬ
				|	РеквизитыДляКонтроляОбъектов.Имя,
				|	РеквизитыДляКонтроляОбъектов.Синоним
				|ИЗ
				|	Справочник.КлассыОбъектовЭксплуатации.РеквизитыДляКонтроля КАК Реквизиты
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РеквизитыОбъектов КАК РеквизитыДляКонтроляОбъектов
				|		ПО Реквизиты.ИмяРеквизита = РеквизитыДляКонтроляОбъектов.Имя
				|ГДЕ
				|	Реквизиты.Ссылка = &Класс
				|	И Реквизиты.ОбязателенДляЗаполнения";
		Иначе
			Запрос.Текст =
				"ВЫБРАТЬ
				|	Реквизиты.Имя,
				|	Реквизиты.Синоним
				|ИЗ
				|	РеквизитыОбъектов КАК Реквизиты
				|ГДЕ
				|	Реквизиты.ОбязателенДляЗаполнения";
			
		КонецЕсли;
		Запрос.МенеджерВременныхТаблиц = КэшированныеЗначения.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Класс", Класс);
		
		ОбязательныеРеквизиты = Запрос.Выполнить().Выгрузить();
		
		КэшированныеЗначения.ОбязательныеРеквизиты.Вставить(Класс, ОбязательныеРеквизиты);
		
	КонецЕсли;
	
	Возврат ОбязательныеРеквизиты;
	
КонецФункции

Функция ПолучитьОбязательныеДопРеквизиты(Класс, КэшированныеЗначения)
	
	ОбязательныеДопРеквизиты = КэшированныеЗначения.ОбязательныеДопРеквизиты.Получить(Класс);
	
	Если ОбязательныеДопРеквизиты = Неопределено Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ДопРеквизиты.Свойство
			|ИЗ
			|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК ДопРеквизиты
			|ГДЕ
			|	ДопРеквизиты.Ссылка = ВЫРАЗИТЬ(&Класс КАК Справочник.КлассыОбъектовЭксплуатации).НаборСвойств
			|	И (ДопРеквизиты.Свойство.ЗаполнятьОбязательно)
			|	И НЕ ДопРеквизиты.Свойство В
			|				(ВЫБРАТЬ
			|					ОбщиеДопРеквизитыОбъектов.Свойство
			|				ИЗ
			|					ОбщиеДопРеквизитыОбъектов КАК ОбщиеДопРеквизитыОбъектов)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ДопРеквизиты.Свойство
			|ИЗ
			|	ОбщиеДопРеквизитыОбъектов КАК ДопРеквизиты
			|ГДЕ
			|	ДопРеквизиты.ЗаполнятьОбязательно");
		
		Запрос.МенеджерВременныхТаблиц = КэшированныеЗначения.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Класс", Класс);
		
		ОбязательныеДопРеквизиты = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Свойство");
		
		КэшированныеЗначения.ОбязательныеДопРеквизиты.Вставить(Класс, ОбязательныеДопРеквизиты);
		
	КонецЕсли;
	
	Возврат ОбязательныеДопРеквизиты;
	
КонецФункции

Функция ПолучитьПараметрыУчетаНаработок(Класс, КэшированныеЗначения)
	
	ПараметрыУчетаНаработок = КэшированныеЗначения.ПараметрыУчетаНаработок.Получить(Класс);
	
	Если ПараметрыУчетаНаработок = Неопределено Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	Параметры.ПоказательНаработки КАК ПоказательНаработки,
			|	Параметры.РассчитыватьОстаточныйРесурс КАК ЗаполнятьНазначенныйРесурс,
			|	Параметры.РегистрироватьОтИсточника КАК ЗаполнятьИсточник
			|ИЗ
			|	Справочник.КлассыОбъектовЭксплуатации.ПоказателиНаработки КАК Параметры
			|ГДЕ
			|	Параметры.Ссылка = &Класс
			|	И (Параметры.РегистрироватьОтИсточника
			|			ИЛИ Параметры.РассчитыватьОстаточныйРесурс)");
		Запрос.МенеджерВременныхТаблиц = КэшированныеЗначения.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Класс", Класс);
		
		ПараметрыУчетаНаработок = Запрос.Выполнить().Выгрузить();
		
		КэшированныеЗначения.ПараметрыУчетаНаработок.Вставить(Класс, ПараметрыУчетаНаработок);
		
	КонецЕсли;
	
	Возврат ПараметрыУчетаНаработок;
	
КонецФункции

Процедура ПроверитьОбъектЭксплуатации(ОбъектПроверки, ПараметрыПроверки, КэшированныеЗначения)
	
	ПроверитьРеквизиты(ОбъектПроверки, ПараметрыПроверки, КэшированныеЗначения);
	ПроверитьДопРеквизиты(ОбъектПроверки, ПараметрыПроверки, КэшированныеЗначения);
	ПроверитьРабочиеЦентры(ОбъектПроверки, ПараметрыПроверки, КэшированныеЗначения);
	ПроверитьПараметрыУчетаНаработок(ОбъектПроверки, ПараметрыПроверки, КэшированныеЗначения);
	
КонецПроцедуры

Процедура ПроверитьРеквизиты(ОбъектПроверки, ПараметрыПроверки, КэшированныеЗначения)
	
	ОбязательныеРеквизиты = ПолучитьОбязательныеРеквизиты(ОбъектПроверки.Класс, КэшированныеЗначения);
	
	Для Каждого Реквизит Из ОбязательныеРеквизиты Цикл
		
		Если Не ЗначениеЗаполнено(ОбъектПроверки[СокрЛП(Реквизит.Имя)]) Тогда
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Поле ""%1"" не заполнено.';
					|en = '""%1"" is required.'"),
				СокрЛП(Реквизит.Синоним));
			
			ОбъектыЭксплуатации.ДобавитьОшибкуПользователю(
				ПараметрыПроверки.ПотокОшибок,
				ОбъектПроверки.Ссылка,
				"Объект."+Реквизит.Имя,
				ТекстОшибки,
				"Реквизиты",
				0,
				ТекстОшибки);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьДопРеквизиты(ОбъектПроверки, ПараметрыПроверки, КэшированныеЗначения)
	
	Если ПараметрыПроверки.Форма = Неопределено Тогда
		
		ОбязательныеДопРеквизиты = ПолучитьОбязательныеДопРеквизиты(ОбъектПроверки.Класс, КэшированныеЗначения);
		
		ТаблицаДопРеквизитов = ОбъектПроверки.ДополнительныеРеквизиты.Выгрузить();
		
		Для Каждого Свойство Из ОбязательныеДопРеквизиты Цикл
			
			Если ТаблицаДопРеквизитов.Найти(Свойство, "Свойство") = Неопределено Тогда
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Поле ""%1"" не заполнено.';
						|en = 'Field %1 is required.'"),
					Свойство);
				
				ОбъектыЭксплуатации.ДобавитьОшибкуПользователю(
					ПараметрыПроверки.ПотокОшибок,
					ОбъектПроверки.Ссылка,
					"ГруппаДополнительныеРеквизиты",
					ТекстОшибки,
					"ДопРеквизиты",
					0,
					ТекстОшибки);
				
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		Если Не (ПараметрыПроверки.Форма.Свойства_ИспользоватьСвойства И ПараметрыПроверки.Форма.Свойства_ИспользоватьДопРеквизиты) Тогда
			Возврат;
		КонецЕсли;
		
		ТаблицаДопРеквизитов = ПараметрыПроверки.Форма.Свойства_ОписаниеДополнительныхРеквизитов;
		
		Для Каждого Строка Из ТаблицаДопРеквизитов Цикл
			Если Строка.ЗаполнятьОбязательно Тогда
				
				Если НЕ ЗначениеЗаполнено(ПараметрыПроверки.Форма[Строка.ИмяРеквизитаЗначение]) Тогда
					
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Поле ""%1"" не заполнено.';
							|en = 'Field %1 is required.'"),
						Строка.Наименование);
					
					ОбъектыЭксплуатации.ДобавитьОшибкуПользователю(
						ПараметрыПроверки.ПотокОшибок,
						ОбъектПроверки.Ссылка,
						Строка.ИмяРеквизитаЗначение,
						ТекстОшибки,
						"ДопРеквизиты",
						0,
						ТекстОшибки);
					
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьРабочиеЦентры(ОбъектПроверки, ПараметрыПроверки, КэшированныеЗначения)
	
	РабочиеЦентры = ОбъектПроверки.РабочиеЦентры.Выгрузить();
	Для ТекИндекс=0 По РабочиеЦентры.Количество()-1 Цикл
		
		Строка = РабочиеЦентры[ТекИндекс];
		
		Если Не ЗначениеЗаполнено(Строка["РабочийЦентр"]) Тогда
			
			ОбъектыЭксплуатации.ДобавитьОшибкуПользователю(
				ПараметрыПроверки.ПотокОшибок,
				ОбъектПроверки.Ссылка,
				"Объект.РабочиеЦентры[%1].РабочийЦентр",
				НСтр("ru = 'Не заполнено поле ""Рабочий центр""';
					|en = '""Operation center"" is required'"),
				"РабочиеЦентры",
				ТекИндекс,
				НСтр("ru = 'Не заполнена колонка ""Рабочий центр"" в строке %1 списка ""Рабочие центры""';
					|en = 'The ""Operation center"" column in line %1 of the ""Operation centers"" list is not filled in'"));
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьПараметрыУчетаНаработок(ОбъектПроверки, ПараметрыПроверки, КэшированныеЗначения)
	
	Если ПараметрыПроверки.Форма = Неопределено Тогда
		
		ПараметрыУчетаНаработки = ПолучитьПараметрыУчетаНаработок(ОбъектПроверки.Класс, КэшированныеЗначения);
		
		ТаблицаПараметров = ОбъектПроверки.ПараметрыУчетаНаработок.Выгрузить(); 
		
		Для Каждого НастройкаПоказателя Из ПараметрыУчетаНаработки Цикл
			
			Строка = ТаблицаПараметров.Найти(НастройкаПоказателя.ПоказательНаработки, "ПоказательНаработки");
			
			Если Строка = Неопределено Или (НастройкаПоказателя.ЗаполнятьНазначенныйРесурс И Не ЗначениеЗаполнено(Строка.НазначенныйРесурс)) Тогда
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Назначенный ресурс показателя наработки ""%1"" не заполнен';
						|en = 'Specify lifetime for the ""%1"" asset activity measure'"),
					НастройкаПоказателя.ПоказательНаработки);
				
				ОбъектыЭксплуатации.ДобавитьОшибкуПользователю(
					ПараметрыПроверки.ПотокОшибок,
					ОбъектПроверки.Ссылка,
					"ПараметрыУчетаНаработок",
					ТекстОшибки,
					"ПараметрыУчетаНаработок",
					0,
					ТекстОшибки);
				
			КонецЕсли;
			
			Если Строка = Неопределено Или (НастройкаПоказателя.ЗаполнятьИсточник И Не ЗначениеЗаполнено(Строка.ИсточникПараметра)) Тогда
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Источник текущих значений показателя наработки ""%1"" не заполнен';
						|en = 'Specify the source of the current values of ""%1"" asset activity'"),
					НастройкаПоказателя.ПоказательНаработки);
				
				ОбъектыЭксплуатации.ДобавитьОшибкуПользователю(
					ПараметрыПроверки.ПотокОшибок,
					ОбъектПроверки.Ссылка,
					"ПараметрыУчетаНаработок",
					ТекстОшибки,
					"ПараметрыУчетаНаработок",
					0,
					ТекстОшибки);
				
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		Если ПараметрыПроверки.Форма.ПараметрыУчетаНаработок.Количество()=0 Тогда
			Возврат;
		КонецЕсли;
		
		Для ТекИндекс=0 По ПараметрыПроверки.Форма.ПараметрыУчетаНаработок.Количество()-1 Цикл
			
			Строка = ПараметрыПроверки.Форма.ПараметрыУчетаНаработок[ТекИндекс];
			
			Если Строка.ЗаполнятьНазначенныйРесурс И Не ЗначениеЗаполнено(Строка.НазначенныйРесурс) Тогда
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Назначенный ресурс показателя наработки ""%1"" не заполнен';
						|en = 'Specify lifetime for the ""%1"" asset activity measure'"),
					Строка.ПоказательНаработки);
				
				ОбъектыЭксплуатации.ДобавитьОшибкуПользователю(
					ПараметрыПроверки.ПотокОшибок,
					ОбъектПроверки.Ссылка,
					"ПараметрыУчетаНаработок[%1].НазначенныйРесурс",
					ТекстОшибки,
					"ПараметрыУчетаНаработок",
					ТекИндекс,
					ТекстОшибки);
				
			КонецЕсли;
			Если Строка.ЗаполнятьИсточник И Не ЗначениеЗаполнено(Строка.ИсточникПараметра) Тогда
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Источник текущих значений показателя наработки ""%1"" не заполнен';
						|en = 'Specify the source of the current values of ""%1"" asset activity'"),
					Строка.ПоказательНаработки);
				
				ОбъектыЭксплуатации.ДобавитьОшибкуПользователю(
					ПараметрыПроверки.ПотокОшибок,
					ОбъектПроверки.Ссылка,
					"ПараметрыУчетаНаработок[%1].ИсточникПараметра",
					ТекстОшибки,
					"ПараметрыУчетаНаработок",
					ТекИндекс,
					ТекстОшибки);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

//-- НЕ УТКА

#Область ПодключаемыеКоманды

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	Если ПравоДоступа("Просмотр", Метаданные.Обработки.ПечатьЭтикетокОбъектовЭксплуатации) Тогда
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Обработчик = "ВнеоборотныеАктивыКлиент.ПечатьЭтикетокОбъектовЭксплуатации";
		КомандаПечати.Идентификатор = "ЭтикеткаОбъектовЭксплуатации";
		КомандаПечати.Представление = НСтр("ru = 'Этикетки';
											|en = 'Labels'");
		
	КонецЕсли;
	
	ОбъектыЭксплуатацииЛокализация.ДобавитьКомандыПечати(КомандыПечати);
	
КонецПроцедуры

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Команда = Документы.ПринятиеКУчетуОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСпискаСоСведениями";
		Команда.РежимЗаписи = "";
	КонецЕсли;
	
	Команда = Документы.ПринятиеКУчетуУзловКомпонентовАмортизации.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСпискаСоСведениями";
		Команда.РежимЗаписи = "";
	КонецЕсли;
	
	Команда = Документы.ИзменениеПараметровОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСпискаСоСведениями";
		Команда.РежимЗаписи = "";
	КонецЕсли;
	
	Команда = Документы.МодернизацияОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСпискаСоСведениями";
		Команда.РежимЗаписи = "";
	КонецЕсли;
	
	Команда = Документы.ПереоценкаОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСпискаСоСведениями";
		Команда.РежимЗаписи = "";
	КонецЕсли;
	
	Команда = Документы.ПеремещениеОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСпискаСоСведениями";
		Команда.РежимЗаписи = "";
	КонецЕсли;
	
	Команда = Документы.ПодготовкаКПередачеОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСпискаСоСведениями";
		Команда.РежимЗаписи = "";
	КонецЕсли;
	
	Команда = Документы.СписаниеОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСпискаСоСведениями";
		Команда.РежимЗаписи = "";
	КонецЕсли;
	
	Команда = Документы.НаработкаОбъектовЭксплуатации.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСписка, ФормаСпискаСоСведениями";
		Команда.РежимЗаписи = "";
	КонецЕсли;
	
	Команда = Документы.РазукомплектацияОС.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСпискаСоСведениями";
		Команда.РежимЗаписи = "";
	КонецЕсли;
	
	Команда = Документы.ОбъединениеОС.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСпискаСоСведениями";
		Команда.РежимЗаписи = "";
		Команда.МножественныйВыбор = Истина;
	КонецЕсли;
	
	Команда = Документы.КорректировкаСтоимостиИАмортизацииОС.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСпискаСоСведениями";
		Команда.РежимЗаписи = "";
		Команда.МножественныйВыбор = Истина;
	КонецЕсли;
	
	//++ НЕ УТКА
	Команда = Документы.ЗаказНаРемонт.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда

		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСписка";
		Команда.РежимЗаписи = "";
		
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(
			Команда, 
			"ТипОС",
			Перечисления.ТипыОС.ОбъектЭксплуатации);
		
	КонецЕсли;
	
	Команда = Документы.УстановкаЗначенийНаработки.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСписка";
		Команда.РежимЗаписи = "";
	КонецЕсли;
	//-- НЕ УТКА
	
	ОбъектыЭксплуатацииЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);
	
КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	Команда = Отчеты.СправкаРасчетАмортизацииОС_МУ.ДобавитьКомандуОтчетаПоОС(КомандыОтчетов);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСпискаСоСведениями";
	КонецЕсли;
	
	Команда = Отчеты.СтруктураЗатратНаКапитализацию.ДобавитьКомандуОтчета(КомандыОтчетов);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСпискаСоСведениями";
	КонецЕсли;
	
	//++ НЕ УТКА
	
	Команда = Отчеты.КарточкаОбъектаЭксплуатации.ДобавитьКомандуОтчета(КомандыОтчетов);
	Если Команда <> Неопределено Тогда
		
		Команда.ВидимостьВФормах = "ФормаЭлемента, ФормаСписка";
		
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(
			Команда, 
			"ТипОС",
			Перечисления.ТипыОС.ОбъектЭксплуатации);
			
	КонецЕсли;
	
	//-- НЕ УТКА

	ОбъектыЭксплуатацииЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);
	
КонецПроцедуры

#КонецОбласти

#Область Печать

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	ОбъектыЭксплуатацииЛокализация.Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Справочники.ОбъектыЭксплуатации.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.7.273";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Многопоточный = Истина;
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("0a97bfe9-77f9-4f51-970e-40c12228274b");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Справочники.ОбъектыЭксплуатации.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Обновляет справочник ""Объекты эксплуатации"":
	|- очищает неиспользуемые реквизиты
	|- заполняет новый реквизит ""Тип ОС""';
	|en = 'Updates the ""Assets"" catalog:
	|- clears unused attributes
	|- fills in the new ""Fixed asset type"" attribute'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Константы.ИспользоватьРеглУчет.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ОбъектыЭксплуатации.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Справочники.ОбъектыЭксплуатации.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Справочники.ОбъектыЭксплуатации.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = "Справочник.ОбъектыЭксплуатации";
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Ссылка");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();

	СписокЗапросов = Новый Массив;
	
	ОбъектыЭксплуатацииЛокализация.ДополнитьТекстЗапросаРегистрацииДанныхКОбработке(СписокЗапросов);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ОбновляемыйОбъект.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ОбъектыЭксплуатации КАК ОбновляемыйОбъект
	|ГДЕ
	|	ОбновляемыйОбъект.ТипОС = ЗНАЧЕНИЕ(Перечисление.ТипыОС.ПустаяСсылка)
	|	И НЕ ОбновляемыйОбъект.ЭтоГруппа";
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	Если СписокЗапросов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ИспользоватьРеглУчет", Константы.ИспользоватьРеглУчет.Получить());
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "Справочник.ОбъектыЭксплуатации";
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;
	
	ДопПараметры = Новый Структура;
	
	Для Каждого Выборка Из ОбновляемыеДанные Цикл
 		
 		НачатьТранзакцию();
		
 		Попытка
			
 			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("Справочник.ОбъектыЭксплуатации");
 			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			
 			Блокировка.Заблокировать();
 			
			ДанныеОбъекта = Выборка.Ссылка.ПолучитьОбъект(); // СправочникОбъект.ОбъектыЭксплуатации
			Если ДанныеОбъекта = Неопределено Тогда
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
				ЗафиксироватьТранзакцию();
 				Продолжить;
			КонецЕсли;
			
			Если НЕ ДанныеОбъекта.ЭтоГруппа
				И НЕ ЗначениеЗаполнено(ДанныеОбъекта.ТипОС) Тогда
				ДанныеОбъекта.ТипОС = Перечисления.ТипыОС.ОбъектЭксплуатации;
			КонецЕсли;
			
			ОбъектыЭксплуатацииЛокализация.ОбработатьДанныеДляПереходаНаНовуюВерсию(ДанныеОбъекта, ДопПараметры);
			
			Если ДанныеОбъекта.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДанныеОбъекта);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
 		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru = 'Не удалось обработать объект: %Ссылка% по причине: %Причина%';
									|en = 'Cannot process the object: %Ссылка%. Reason: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Выборка.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
									УровеньЖурналаРегистрации.Предупреждение,
									Выборка.Ссылка.Метаданные(),
									Выборка.Ссылка,
									ТекстСообщения);
									
 		КонецПопытки;
 
 	КонецЦикла;
 		
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
