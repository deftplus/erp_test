
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Отбор.Свойство("Владелец") И ЗначениеЗаполнено(Параметры.Отбор.Владелец) Тогда
		
		ТипБД=Параметры.Отбор.Владелец;
		
		Если ТипБД.ВерсияПлатформы=Перечисления.ПлатформыВнешнихИнформационныхБаз.ADO Тогда
			
			ОбщегоНазначенияУХ.СообщитьОбОшибке(НСтр("ru = 'Для выбранного типа информационной базы доступны только таблицы ADO'"), Отказ,,СтатусСообщения.Информация);
			Возврат;
			
		КонецЕсли;
		
		ОбновитьОтборПоВладельцу();
		
		Элементы.ТипБД.Доступность=Ложь;
		
	Иначе
		
				
		Элементы.ТипБД.Доступность=Истина;
		ТипБД=Справочники.ТипыБазДанных.ТекущаяИБ;
		ОбновитьОтборПоВладельцу();
		
		Если Параметры.Свойство("Контролируемые") Тогда
			Элементы.ТипБД.Доступность=Ложь;
			ОбщегоНазначенияКлиентСерверУХ.УстановитьЭлементОтбора(Список.Отбор, "Контролируемый", Истина, ВидСравненияКомпоновкиДанных.Равно,,Истина);	
		КонецЕсли;
	
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтборПоВладельцу()
	
	ОбщегоНазначенияКлиентСерверУХ.УстановитьЭлементОтбора(Список.Отбор,"Владелец",ТипБД,ВидСравненияКомпоновкиДанных.Равно,,Истина);
		
	Если ТипБД=Справочники.ТипыБазДанных.ТекущаяИБ Тогда
		
		Элементы.СписокЗагрузитьИЗВИБ.Заголовок = НСтр("ru = 'Обновить по данным текущей ИБ'");
		
	Иначе
		
		Элементы.СписокЗагрузитьИЗВИБ.Заголовок = НСтр("ru = 'Обновить по данным внешней ИБ'");
		
	КонецЕсли;
	
	Если ТипБД=Справочники.ТипыБазДанных.ТекущаяИБ Тогда
		
		Заголовок = НСтр("ru = 'Планы видов характеристик текущей информационной базы'");
		Элементы.Соответствие.Видимость=Ложь;
		Элементы.Ссылка.Заголовок=Нстр("ru = 'Планы видов характеристик текущей ИБ'");
		
	Иначе
		
		Заголовок = НСтр("ru = 'Планы видов характеристик внешней информационной базы'");
		Элементы.Соответствие.Видимость=Истина;
		Элементы.Ссылка.Заголовок = НСтр("ru = 'Планы видов характеристик внешней ИБ'");
		
	КонецЕсли;
			
КонецПроцедуры // ОбновитьОтборПоВладельцу() 

&НаСервереБезКонтекста
Процедура ЗаполнитьСписокПВХ(ТипБД)
	
	ОбщегоНазначенияУХ.ЗаполнитьСписокПлановВидовХарактеристикБД(ТипБД);
		
КонецПроцедуры // ЗаполнитьСписокСправочников()


/////////////////////////////////////////////////////////////////////////////////

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка=Ложь;
	
	Если Поле.Имя="Соответствие" Тогда
		
		СтандартнаяОбработка=Ложь;
		
		ТекСоответствие=Элементы.Список.ТекущиеДанные.Соответствие;
		
		Если НЕ ЗначениеЗаполнено(ТекСоответствие) Тогда
			
			СтруктураСоответствие=Новый Структура;
			СтруктураСоответствие.Вставить("ОписаниеОбъектаВИБ",Элементы.Список.ТекущиеДанные.Ссылка);
			СтруктураСоответствие.Вставить("Владелец",ТипБД);
			СтруктураСоответствие.Вставить("ТипОбъектаВИБ","ПланВидовХарактеристик");
			
			ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", СтруктураСоответствие);
			Оповещение = Новый ОписаниеОповещения("Подключаемый_ОбновитьСписок", ЭтотОбъект);
			ОткрытьФорму("Справочник.СоответствиеВнешнимИБ.ФормаОбъекта", 
						ПараметрыФормы, ЭтаФорма,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
		Иначе

			ОткрытьФорму("Справочник.СоответствиеВнешнимИБ.ФормаОбъекта",Новый Структура("Ключ",ТекСоответствие));
			
		КонецЕсли;
		
	Иначе
		
		ОткрытьФорму("Справочник.ПланыВидовХарактеристикБД.ФормаОбъекта",Новый Структура("Ключ",Элементы.Список.ТекущиеДанные.Ссылка));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьСписок(Результат, ДополнительныеПараметры) Экспорт
    
    Элементы.Список.Обновить();

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИЗВИБ(Команда)
	
	Если НЕ ЗначениеЗаполнено(ТипБД) Тогда
		
		ПоказатьПредупреждение(, НСтр("ru = 'Не указан тип внешней информационной базы'"));
		Возврат;
		
	Иначе
		
		ЗаполнитьСписокПВХ(ТипБД);
		Элементы.Список.Обновить();
		
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура ТипБДПриИзменении(Элемент)
	
	ОбновитьОтборПоВладельцу();
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдалениеОтсутствующие(Команда)
	ОбщегоНазначенияУХ.ПометитьНаУдалениеОтсутствующие("Справочник.ПланыВидовХарактеристикБД");
	Элементы.Список.Обновить();
КонецПроцедуры
