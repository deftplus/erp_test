#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


Функция ПолучитьДляСтрокиПлана(СтрокаПланаОбъект) Экспорт
	Ссылка = 
		Справочники.ИдентификаторыСтрокПлановЗакупок.ПустаяСсылка();
	Если НЕ ЗначениеЗаполнено(СтрокаПланаОбъект.ИдентификаторСтрокиПланаЗакупок) Тогда
		Если ЗначениеЗаполнено(СтрокаПланаОбъект.ДокументОснование) 
				И ЗначениеЗаполнено(
					СтрокаПланаОбъект.ДокументОснование.ИдентификаторыСтрокПлановЗакупок) Тогда
			Возврат СтрокаПланаОбъект.ДокументОснование.ИдентификаторыСтрокПлановЗакупок;
		КонецЕсли;
		ИдентификаторОбъект =
			Справочники.ИдентификаторыСтрокПлановЗакупок.СоздатьЭлемент();
	Иначе
		Если Справочники.ИдентификаторыСтрокПлановЗакупок.ЗаполненыКлючевыеРеквизиты(
								СтрокаПланаОбъект.ИдентификаторСтрокиПланаЗакупок) Тогда
			Возврат СтрокаПланаОбъект.ИдентификаторСтрокиПланаЗакупок;
		КонецЕсли;
		ИдентификаторОбъект = 
			СтрокаПланаОбъект.ИдентификаторСтрокиПланаЗакупок.ПолучитьОбъект();
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(
		ИдентификаторОбъект,
		СтрокаПланаОбъект,
		"НомерВГПЗ,ПериодЗакупок,ОрганизацияДляЗаключенияДоговора");
	ИдентификаторОбъект.ИнновационныйПланЗакупок =
		СтрокаПланаОбъект.ЗакупкаИнновационнойВысокотехнологичнойПродукцииИЛекарственныхСредств;
	ИдентификаторОбъект.Записать();
	Возврат ИдентификаторОбъект.Ссылка;
КонецФункции

// Проверяет, что заполнены ключевые реквизиты объекта
//  с точки зрения идентификатора строки плана закупок.
//
// Параметры:
//  Объект - СправочникСсылка.ИдентификаторыСтрокПлановЗакупок |
//			СправочникСсылка.Лоты | 
//			ДокументСсылка.СтрокаПланаЗакупок |
//			СправочникОбъект.ИдентификаторыСтрокПлановЗакупок |
//			СправочникОбъект.Лоты | 
//			ДокументОбъект.СтрокаПланаЗакупок - объект у которого
//		нужно проверить заполнение ключевых реквизитов.
// 
// Возвращаемое значение:
//   Булево - Истина - реквизиты заполнены.
//		Ложь - хотябы один реквизит не заполнен.
//
Функция ЗаполненыКлючевыеРеквизиты(Объект) Экспорт
	Если НЕ ЗначениеЗаполнено(Объект) Тогда
		Возврат Ложь;
	КонецЕсли;
	ИменаКлючей = СтрРазделить(ИменаКлючевыхРеквизитов(), ",");
	Для Каждого ИмяРеквизита Из ИменаКлючей Цикл
		Если ТипЗнч(Объект[ИмяРеквизита]) <> Тип("Булево")
				И НЕ ЗначениеЗаполнено(Объект[ИмяРеквизита]) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	Возврат Истина;
КонецФункции

Функция ИменаКлючевыхРеквизитов() Экспорт
	Возврат "НомерВГПЗ,ПериодЗакупок,ОрганизацияДляЗаключенияДоговора,ИнновационныйПланЗакупок";
КонецФункции


#КонецЕсли