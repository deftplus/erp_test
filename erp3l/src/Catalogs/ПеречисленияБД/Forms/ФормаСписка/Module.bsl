
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Отбор.Свойство("Владелец") И ЗначениеЗаполнено(Параметры.Отбор.Владелец) Тогда
		
		ТипБД=Параметры.Отбор.Владелец;
		
		Если ТипБД.ВерсияПлатформы=Перечисления.ПлатформыВнешнихИнформационныхБаз.ADO Тогда
			
			ОбщегоНазначенияУХ.СообщитьОбОшибке(Нстр("ru = 'Для выбранного типа информационной базы доступны только таблицы ADO'"),Отказ,,СтатусСообщения.Информация);
			Возврат;
			
		КонецЕсли;

		ОбновитьОтборПоВладельцу();	
		Элементы.ТипБД.Доступность=Ложь;
		
	Иначе
		
		Элементы.ТипБД.Доступность=Истина;
		ТипБД=Справочники.ТипыБазДанных.ТекущаяИБ;			
		ОбновитьОтборПоВладельцу();
					
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтборПоВладельцу()
	
	ОбщегоНазначенияКлиентСерверУХ.УстановитьЭлементОтбора(Список.Отбор,"Владелец",ТипБД,ВидСравненияКомпоновкиДанных.Равно,,Истина);
	
	Если ТипБД=Справочники.ТипыБазДанных.ТекущаяИБ Тогда
		
		Элементы.СписокЗагрузитьИЗВИБ.Заголовок=Нстр("ru = 'Обновить по данным текущей ИБ'");
		
	Иначе
		
		Элементы.СписокЗагрузитьИЗВИБ.Заголовок=Нстр("ru = 'Обновить по данным внешней ИБ;'")
		
	КонецЕсли;
		
КонецПроцедуры // ОбновитьОтборПоВладельцу() 

&НаСервереБезКонтекста
Процедура ЗаполнитьСписокПеречислений(ТипБД)
	
	ОбщегоНазначенияУХ.ЗаполнитьСписокПеречисленийБД(ТипБД);
		
КонецПроцедуры // ЗаполнитьСписокСправочников()


/////////////////////////////////////////////////////////////////////////////////

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка=Ложь;
	
	Если Поле.Имя="Соответствие" Тогда
		
		ТекСоответствие=Элементы.Список.ТекущиеДанные.Соответствие;
		
		Если НЕ ЗначениеЗаполнено(ТекСоответствие) Тогда
			
			СтруктураСоответствие=Новый Структура;
			СтруктураСоответствие.Вставить("ОписаниеОбъектаВИБ",Элементы.Список.ТекущиеДанные.Ссылка);
			СтруктураСоответствие.Вставить("Владелец",ТипБД);
			СтруктураСоответствие.Вставить("ТипОбъектаВИБ","Перечисление");
			
			ПараметрыФормы = Новый Структура("ЗначенияЗаполнения",СтруктураСоответствие);
			Оповещение = Новый ОписаниеОповещения("СписокВыборЗавершение", ЭтотОбъект);
			ОткрытьФорму("Справочник.СоответствиеВнешнимИБ.ФормаОбъекта", 
							ПараметрыФормы,,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
			
		Иначе

			ОткрытьФорму("Справочник.СоответствиеВнешнимИБ.ФормаОбъекта", Новый Структура("Ключ",ТекСоответствие));
			
		КонецЕсли;	
		
	Иначе
		
		ОткрытьФорму("Справочник.ПеречисленияБД.ФормаОбъекта",Новый Структура("Ключ",Элементы.Список.ТекущиеДанные.Ссылка));
		
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗавершение(Результат, ДополнительныеПараметры) Экспорт    
    Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИЗВИБ(Команда)
	
	Если НЕ ЗначениеЗаполнено(ТипБД) Тогда
		
		ПоказатьПредупреждение(, Нстр("ru = 'Не указан тип внешней информационной базы.'"));
		Возврат;
		
	Иначе
		
		ЗаполнитьСписокПеречислений(ТипБД);
		Элементы.Список.Обновить();
		
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура ТипБДПриИзменении(Элемент)
	
	ОбновитьОтборПоВладельцу();
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдалениеОтсутствующие(Команда)
	ОбщегоНазначенияУХ.ПометитьНаУдалениеОтсутствующие("Справочник.ПеречисленияБД");
	Элементы.Список.Обновить();
КонецПроцедуры

