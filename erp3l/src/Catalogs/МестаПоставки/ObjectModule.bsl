#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Склады") Тогда
		Наименование = "Склад: " + ДанныеЗаполнения.Наименование;
		Склад = ДанныеЗаполнения;
		Вид = Перечисления.ВидыМестПоставки.Склад;
	КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	ОбновитьПредставлениеАдресовВОбъекте();
КонецПроцедуры

Процедура ОбновитьПредставлениеАдресовВОбъекте() Экспорт
	АдресСтрокой = 
		УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформации(
			Адрес);
КонецПроцедуры

// Возвращает массив складов, привязанные к месту поставки МестоПоставкиВход.
Функция СкладыМестаПоставок(МестоПоставкиВход)
	РезультатФункции = Новый Массив;
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	МестаПоставкиДляСкладов.Склад КАК Склад,
		|	МестаПоставкиДляСкладов.МестоПоставки КАК МестоПоставки
		|ИЗ
		|	РегистрСведений.МестаПоставкиДляСкладов КАК МестаПоставкиДляСкладов
		|ГДЕ
		|	МестаПоставкиДляСкладов.МестоПоставки = &МестоПоставки";
	Запрос.УстановитьПараметр("МестоПоставки", МестоПоставкиВход);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		РезультатФункции.Добавить(ВыборкаДетальныеЗаписи.Склад);
	КонецЦикла;
	Возврат РезультатФункции;	
КонецФункции		 // СкладыМестаПоставок()

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	// Проверка, что указан хотя бы один  склад
	Если ЗначениеЗаполнено(Ссылка) Тогда
		МассивСкладов = СкладыМестаПоставок(Ссылка);
	Иначе
		МассивСкладов = Новый Массив;
	КонецЕсли;
	Если Вид = Перечисления.ВидыМестПоставки.Склад Тогда
		МассивСкладов.Добавить(Склад);
	Иначе
		// Не добавляем склад для других вариантов.
	КонецЕсли;
	МассивСкладов = ОбщегоНазначенияКлиентСерверУХ.УдалитьПустыеЭлементыМассива(МассивСкладов);
	Если МассивСкладов.Количество() = 0 Тогда
		Отказ = Истина;
		ТекстСообщения = НСтр("ru = 'Для места поставки ""%МестоПоставки%"" не указан ни один склад. Запись отменена.'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%МестоПоставки%", Строка(Ссылка));
		ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
	Иначе
		// Проверка пройдена успешно.
	КонецЕсли;
КонецПроцедуры

#КонецЕсли
