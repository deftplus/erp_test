#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
// Создать место поставки с видом Склад по
//  данным переданного склада.
//
// Параметры:
//  Склад - СправочникСсылка.Склады - склад.
// 
// Возвращаемое значение:
//   - СправочникСсылка.МестаПоставки.
//
Функция СоздатьПоСкладу(Склад) Экспорт
	МестоОбъект = Справочники.МестаПоставки.СоздатьЭлемент();
	МестоОбъект.Заполнить(Склад);
	МестоОбъект.Записать();
	Возврат МестоОбъект.Ссылка;
КонецФункции

Функция СкладыМестаПоставки(МестоПоставки) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	МестаПоставки.Склад КАК Ссылка
		|ИЗ
		|	Справочник.МестаПоставки КАК МестаПоставки
		|ГДЕ
		|	МестаПоставки.Ссылка = &МестоПоставки
		|	И МестаПоставки.Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	МестаПоставкиДляСкладов.Склад
		|ИЗ
		|	РегистрСведений.МестаПоставкиДляСкладов КАК МестаПоставкиДляСкладов
		|ГДЕ
		|	МестаПоставкиДляСкладов.МестоПоставки = &МестоПоставки";
	
	Запрос.УстановитьПараметр("МестоПоставки", МестоПоставки);
	РезультатЗапроса = Запрос.Выполнить();
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
КонецФункции

Функция МестаПоставкиСклада(Склад) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	МестаПоставки.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.МестаПоставки КАК МестаПоставки
		|ГДЕ
		|	МестаПоставки.Склад = &Склад
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	МестаПоставкиДляСкладов.МестоПоставки
		|ИЗ
		|	РегистрСведений.МестаПоставкиДляСкладов КАК МестаПоставкиДляСкладов
		|ГДЕ
		|	МестаПоставкиДляСкладов.Склад = &Склад";
	Запрос.УстановитьПараметр("Склад", Склад);
	РезультатЗапроса = Запрос.Выполнить();
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
КонецФункции

#Область ОбработчикиОбновленияИнформационнойБазы

Процедура СформироватьМестаПоставкиПоСуществующимСкладам() экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Склады.Ссылка КАК Склад
	|ИЗ
	|	Справочник.Склады КАК Склады
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.МестаПоставки КАК МестаПоставки
	|		ПО Склады.Ссылка = МестаПоставки.Склад
	|ГДЕ
	|	МестаПоставки.Ссылка ЕСТЬ NULL
	|	И Склады.ЭтоГруппа = ЛОЖЬ";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выб = Результат.Выбрать();
	Пока Выб.Следующий() Цикл
		Справочники.МестаПоставки.СоздатьПоСкладу(Выб.Склад);
	КонецЦикла; 

КонецПроцедуры
 
	
#КонецОбласти 


#КонецЕсли