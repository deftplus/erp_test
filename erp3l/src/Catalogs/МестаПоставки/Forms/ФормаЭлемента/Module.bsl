
#Область ОбработкаОсновныхСобытийФормы


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ВидКонтактнойИнформацииАдреса = Новый Структура;
	ВидКонтактнойИнформацииАдреса.Вставить("Тип", Перечисления.ТипыКонтактнойИнформации.Адрес);
	ВидКонтактнойИнформацииАдреса.Вставить("АдресТолькоРоссийский", Ложь);
	ВидКонтактнойИнформацииАдреса.Вставить("ВключатьСтрануВПредставление", Ложь);
	ВидКонтактнойИнформацииАдреса.Вставить("СкрыватьНеактуальныеАдреса", Ложь);
	
	ПредставлениеАдреса = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформации(Объект.Адрес);
	КомментарийАдреса  = УправлениеКонтактнойИнформацией.КомментарийКонтактнойИнформации(Объект.Адрес);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимость();
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(Склады, "МестоПоставки", Объект.Ссылка, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(Склады, "МестоПоставки", Объект.Ссылка, Истина);
КонецПроцедуры


#КонецОбласти

#Область ОбработкаСобытийЭлементовФормы


&НаКлиенте
Процедура ВидПриИзменении(Элемент)
	УстановитьВидимость();
КонецПроцедуры

&НаКлиенте
Процедура АдресНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	УправлениеКонтактнойИнформациейКлиент.НачалоВыбора(ЭтаФорма, Элемент, ЭтаФорма.Модифицированность, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеАдресаПриИзменении(Элемент)
	Текст = Элемент.ТекстРедактирования;
	Если ПустаяСтрока(Текст) Тогда
		ПредставлениеАдреса = "";
		КомментарийАдреса   = "";
		Объект.Адрес        = "";
		Возврат;
	КонецЕсли;
	// Формируем внутренние значения полей по тексту и параметрам
	ПредставлениеАдреса = Текст;
	Объект.Адрес = ЗначенияПолейКонтактнойИнформацииСервер(
		Текст,
		ВидКонтактнойИнформацииАдреса);
	ОбработатьИзменениеАдреса(Новый Структура("КонтактнаяИнформация", Объект.Адрес),
							  Неопределено)
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеАдресаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	// Если представление было изменено в поле и сразу нажата
	// кнопка выбора, то необходимо привести данные в соответствие
	// и сбросить внутренние поля для повторного репарсинга
	Если Элемент.ТекстРедактирования <> ПредставлениеАдреса Тогда
		ПредставлениеАдреса = Элемент.ТекстРедактирования;
		Объект.Адрес = "";
	КонецЕсли;
	// Данные для редактирования
	ОповещениеИзмененияАдреса = Новый ОписаниеОповещения("ОбработатьИзменениеАдреса", ЭтаФорма);
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ВидКонтактнойИнформации", ВидКонтактнойИнформацииАдреса);
	ПараметрыОткрытия.Вставить("ЗначенияПолей", Объект.Адрес);
	ПараметрыОткрытия.Вставить("Представление", ПредставлениеАдреса	);
	ПараметрыОткрытия.Вставить("Комментарий", КомментарийАдреса);
	// Переопределямый заголовок формы,
	// по умолчнию отобразятся данные по ВидКонтактнойИнформации
	ПараметрыОткрытия.Вставить("Заголовок", НСтр("ru='Адрес '"));
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(
		ПараметрыОткрытия, Элемент, ОповещениеИзмененияАдреса);
КонецПроцедуры

// Ожидает на вход структуру с полем КонтактнаяИнформация.
//
&НаКлиенте
Процедура ОбработатьИзменениеАдреса(Результат, ДопПараметры) Экспорт
	Если ЗначениеЗаполнено(Результат) 
			И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(
										Результат, "КонтактнаяИнформация") Тогда
		Если НЕ ЗначениеЗаполнено(Объект.КодПоОКАТО) Тогда
			Объект.КодПоОКАТО = ПолучитьКодОКАТОПоАдресу(Результат.КонтактнаяИнформация);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеАдресаОчистка(Элемент, СтандартнаяОбработка)
	ПредставлениеАдреса = "";
	КомментарийАдреса   = "";
	Объект.Адрес        = "";
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеАдресаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	ПредставлениеАдреса = ВыбранноеЗначение.Представление;
	КомментарийАдреса   = ВыбранноеЗначение.Комментарий;
	Объект.Адрес        = ВыбранноеЗначение.КонтактнаяИнформация;
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыНаКлиенте


&НаКлиенте
Процедура УстановитьВидимость()
	Элементы.Склад.Видимость = (Объект.Вид = ПредопределенноеЗначение("Перечисление.ВидыМестПоставки.Склад"));
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыНаСервере


&НаСервереБезКонтекста
Функция ПолучитьКодОКАТОПоАдресу(АдресXML)
	КодыАдреса = АдресныйКлассификатор.КодыАдреса(АдресXML);
	Возврат КодыАдреса.OKATO;
КонецФункции

&НаСервереБезКонтекста
Функция ЗначенияПолейКонтактнойИнформацииСервер(
										Знач Представление,
										Знач ВидКонтактнойИнформации)
	Возврат УправлениеКонтактнойИнформацией.КонтактнаяИнформацияXMLПоПредставлению(
		Представление, 
		ВидКонтактнойИнформации);
КонецФункции


#КонецОбласти
