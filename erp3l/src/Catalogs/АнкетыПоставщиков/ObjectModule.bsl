#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	
Процедура ПередЗаписью(Отказ)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	1 КАК КодОшибки
		|ИЗ
		|	Справочник.АнкетыПоставщиков КАК АнкетыПоставщиков
		|ГДЕ
		|	АнкетыПоставщиков.ИНН = &ИНН
		|	И АнкетыПоставщиков.КПП = &КПП
		|	И АнкетыПоставщиков.Ссылка <> &Ссылка
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 2
		|	2
		|ИЗ
		|	Справочник.АнкетыПоставщиков КАК АнкетыПоставщиков
		|ГДЕ
		|	АнкетыПоставщиков.РегистрационныйНомер = &РегистрационныйНомер
		|	И АнкетыПоставщиков.СтранаРегистрации = &СтранаРегистрации
		|	И АнкетыПоставщиков.Ссылка <> &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 3
		|	3
		|ИЗ
		|	Справочник.АнкетыПоставщиков КАК АнкетыПоставщиков
		|ГДЕ
		|	АнкетыПоставщиков.АдресЭП = &АдресЭП
		|	И АнкетыПоставщиков.Ссылка <> &Ссылка";
	
	Запрос.УстановитьПараметр("ИНН", ИНН);
	Запрос.УстановитьПараметр("КПП", КПП);
	Запрос.УстановитьПараметр("РегистрационныйНомер", РегистрационныйНомер);
	Запрос.УстановитьПараметр("СтранаРегистрации", СтранаРегистрации);
	Запрос.УстановитьПараметр("АдресЭП", АдресЭП);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаИзЗапроса = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.Прямой);
	Пока ВыборкаИзЗапроса.Следующий() Цикл
		Сообщение = Новый СообщениеПользователю;
		Если ВыборкаИзЗапроса.КодОшибки = 1 Тогда
			ТекстСообщения = НСтр("ru = 'Запрос на регистрацию поставщика с ИНН/КПП %ИНН%/%КПП% уже был зарегистрирован.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИНН%", Строка(ИНН));
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%КПП%", Строка(КПП));
			Сообщение.Текст = ТекстСообщения;
		ИначеЕсли ВыборкаИзЗапроса.КодОшибки = 2 Тогда
			ТекстСообщения = НСтр("ru = 'Запрос на регистрацию поставщика с регистрационным номером %РегистрационныйНомер% (страна регистрации %СтранаРегистрации%) уже был зарегистрирован.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%РегистрационныйНомер%", Строка(РегистрационныйНомер));
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%СтранаРегистрации%", Строка(СтранаРегистрации));
			Сообщение.Текст = ТекстСообщения;
		ИначеЕсли ВыборкаИзЗапроса.КодОшибки = 3 Тогда
			ТекстСообщения = НСтр("ru = 'Запрос на регистрацию поставщика с e-mail %АдресЭП% уже был зарегистрирован.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%АдресЭП%", Строка(АдресЭП));
			Сообщение.Текст = ТекстСообщения;
		КонецЕсли;
		
		Сообщение.Сообщить();
		Отказ = Истина;
	КонецЦикла;
	ОбновитьПредставлениеАдресовВОбъекте();
КонецПроцедуры
	
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если (ЮридическоеФизическоеЛицо =
				Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
			ИЛИ НЕ ФизЛицоНаСпецрежиме422ФЗ Тогда
		ПроверяемыеРеквизиты.Добавить("РегистрационныйНомер");		
	КонецЕсли;
	Если Справочники.АнкетыПоставщиков.ПроверитьКорректностьИмениПользователяИБ(ВнешнийПользователь, ПользовательИБИмя) Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Некорректное имя пользователя.'");
		Сообщение.Поле = "ПользовательИБИмя";
		Сообщение.Сообщить();
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	ПользовательИБВременныйПароль = АккредитацияПоставщиковУХ.СоздатьПарольВнешнегоПользователя();
	
	ЭтоВнешнийПользователь = ПользователиКлиентСервер.ЭтоСеансВнешнегоПользователя();
	Если НЕ ЭтоВнешнийПользователь Тогда
		Если НЕ ЗначениеЗаполнено(Ответственный) Тогда
			Ответственный = Пользователи.ТекущийПользователь();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ОбновитьПредставлениеАдресовВОбъекте() Экспорт
	ФактическийАдресСтрокой = 
		УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформации(ФактическийАдрес);
	ЮридическийАдресСтрокой = 
		УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформации(ЮридическийАдрес);
КонецПроцедуры

#КонецЕсли

