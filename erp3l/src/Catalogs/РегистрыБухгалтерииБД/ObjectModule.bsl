Перем Используется77; // Признак использования обращения к ВИБ на платформе 7.7
Перем ТекСоединениеВИБ Экспорт; // Текущее соединение с внешней информационной базой
Перем РегистрБД;
Перем ТекущаяИБ;
Перем мОписаниеТиповСтрока;
Перем мОписаниеТиповБулево;
Перем мОписаниеТиповТЗ;
Перем мОписаниеТиповСписокЗначений;

Процедура ЗаполнитьРеквизитыОбъекта(Кэш = Неопределено, ЕстьИзменения = Истина) Экспорт
	
	Если Не ПроверитьНаименование() Тогда		
		Возврат;		
	КонецЕсли;
	
	ТекущаяИБ = (Владелец = Справочники.ТипыБазДанных.ТекущаяИБ);
	
	Если НЕ ТекущаяИБ Тогда
		
		Если ТекСоединениеВИБ = Неопределено Тогда
			
			ТекСоединениеВИБ = ОбщегоНазначенияУХ.ПолучитьСоединениеСВИБПоУмолчанию(Владелец, 1);
			
			Если ТекСоединениеВИБ = Неопределено Тогда
				
				Возврат;
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		ТекСоединениеВИБ = Обработки.РаботаСМетаданнымиУХ.Создать();
		
	КонецЕсли;
	
	СтруктураОписания = РаботаСОбъектамиМетаданныхУХ.ПолучитьСтруктуруОписанияРегистраБухгалтерииБД(
		Наименование, ТекСоединениеВИБ, Использование77(), Кэш);
	Если НЕ ЗначениеЗаполнено(СтруктураОписания.Регистраторы) Тогда
		СтруктураОписания.Регистраторы = "";
	КонецЕсли;	
	
	Если СтруктураОписания.Свойство("ТекстОшибки") Тогда		
		ОбщегоНазначенияУХ.СообщитьОбОшибке(СтруктураОписания.ТекстОшибки, Истина,, СтатусСообщения.Внимание);
		Возврат;		
	КонецЕсли;
	
	РаботаСОбъектамиМетаданныхУХ.ЗаполнитьИзмененныеРеквизиты(ЭтотОбъект, СтруктураОписания, "Реквизиты,Измерения,Ресурсы", ЕстьИзменения);
	РаботаСОбъектамиМетаданныхУХ.ЗагрузитьИзмененнуюТабЧасть(Ресурсы, СтруктураОписания.Ресурсы, ЕстьИзменения);
	РаботаСОбъектамиМетаданныхУХ.ЗагрузитьИзмененнуюТабЧасть(Измерения, СтруктураОписания.Измерения, ЕстьИзменения);
	РаботаСОбъектамиМетаданныхУХ.ЗагрузитьИзмененнуюТабЧасть(Реквизиты, СтруктураОписания.Реквизиты, ЕстьИзменения);

	ИмяПланаСчетов = СтруктураОписания.ИмяПланаСчетов;	
	НовыйПланСчетов = ОбщегоНазначенияУХ.ПолучитьОписаниеОбъектаБД(ТекСоединениеВИБ, "ПланСчетов", ИмяПланаСчетов, Владелец, Ложь);
	ОбщегоНазначенияКлиентСерверУХ.УстановитьНовоеЗначение(ЭтотОбъект.ПланСчетов, НовыйПланСчетов, ЕстьИзменения);
		
КонецПроцедуры // ЗаполнитьРеквизитыОбъекта()
	
Функция ПроверитьНаименование()
	
	ЕстьОшибки=Ложь;
	
	Если НЕ ЗначениеЗаполнено(Наименование) Тогда
		
		ОбщегоНазначенияУХ.СообщитьОбОшибке(Нстр("ru = 'Для обрабатываемого плана счетов внешней информационной базы не указано наименование.'"),ЕстьОшибки,,СтатусСообщения.Внимание);
		
	КонецЕсли;
	
	Возврат НЕ ЕстьОшибки;
			
КонецФункции // ПроверитьНаименование()

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	Иначе	
		Отказ = НЕ ПроверитьНаименование();
	КонецЕсли;
		
КонецПроцедуры

Функция Использование77() Экспорт
		
	Если Используется77 = Неопределено Тогда
		Используется77 = (Владелец.ВерсияПлатформы = Перечисления.ПлатформыВнешнихИнформационныхБаз.Предприятие77);	
	КонецЕсли;
	
	Возврат Используется77;
	
КонецФункции

