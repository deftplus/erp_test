
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция ПолучитьСчетУчета(ВидСчета, ВидРазницы) Экспорт

	Если ВидСчета = Перечисления.ВидыСчетовБД.Пассивный Тогда
	
		Если ВидРазницы = Перечисления.ВидыРазниц.ПР Тогда		
			Возврат Справочники.ФиксированныеСчетаУчетаБД.ПостоянноеНалоговоеОбязательство.Счет;
		Иначе 
			Возврат Справочники.ФиксированныеСчетаУчетаБД.ОтложенноеНалоговоеОбязательство.Счет;
		КонецЕсли;
		
	Иначе 
		
		Если ВидРазницы = Перечисления.ВидыРазниц.ПР Тогда		
			Возврат Справочники.ФиксированныеСчетаУчетаБД.ПостоянныйНалоговыйАктив.Счет;
		Иначе 
			Возврат Справочники.ФиксированныеСчетаУчетаБД.ОтложенныйНалоговыйАктив.Счет;
		КонецЕсли;
		
	КонецЕсли;

КонецФункции

Процедура ПодставитьВидРазницы(ТекстЗапроса, ПолеВидРазницы = "т.ВидРазницы", ВидРазницыДляЗамены = "&ЭффектОтРазницы") Экспорт

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ВидРазницыДляЗамены, ТекстЗапроса_ПолеВидРазницы(ПолеВидРазницы));

КонецПроцедуры

Функция ТекстЗапроса_ПолеВидРазницы(ПутьВидРазницы = "т.ВидРазницы")

	ВыражениеПоля = 
	"ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(&ВидРазницы КАК Справочник.ВидыВременныхРазниц).ВидСчета = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовБД.Активный)
	|				И ВЫРАЗИТЬ(&ВидРазницы КАК Справочник.ВидыВременныхРазниц).ВидРазницы = ЗНАЧЕНИЕ(Перечисление.ВидыРазниц.ВР)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыАктивовИОбязательствПБУ18.ОНА)
	|		КОГДА ВЫРАЗИТЬ(&ВидРазницы КАК Справочник.ВидыВременныхРазниц).ВидСчета = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовБД.Пассивный)
	|				И ВЫРАЗИТЬ(&ВидРазницы КАК Справочник.ВидыВременныхРазниц).ВидРазницы = ЗНАЧЕНИЕ(Перечисление.ВидыРазниц.ВР)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыАктивовИОбязательствПБУ18.ОНО)
	|		КОГДА ВЫРАЗИТЬ(&ВидРазницы КАК Справочник.ВидыВременныхРазниц).ВидСчета = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовБД.Активный)
	|				И ВЫРАЗИТЬ(&ВидРазницы КАК Справочник.ВидыВременныхРазниц).ВидРазницы = ЗНАЧЕНИЕ(Перечисление.ВидыРазниц.ПР)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыАктивовИОбязательствПБУ18.ПНА)
	|		КОГДА ВЫРАЗИТЬ(&ВидРазницы КАК Справочник.ВидыВременныхРазниц).ВидСчета = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовБД.Пассивный)
	|				И ВЫРАЗИТЬ(&ВидРазницы КАК Справочник.ВидыВременныхРазниц).ВидРазницы = ЗНАЧЕНИЕ(Перечисление.ВидыРазниц.ПР)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыАктивовИОбязательствПБУ18.ПНО)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыАктивовИОбязательствПБУ18.ПустаяСсылка)
	|	КОНЕЦ";
	
	Возврат СтрЗаменить(ВыражениеПоля, "ВЫРАЗИТЬ(&ВидРазницы КАК Справочник.ВидыВременныхРазниц)", ПутьВидРазницы);

КонецФункции

#КонецЕсли

