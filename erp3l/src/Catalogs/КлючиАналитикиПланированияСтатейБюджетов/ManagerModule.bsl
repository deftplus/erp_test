#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ЗаменаДублейКлючейАналитики

Процедура ЗаменитьДублиКлючейАналитики() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеСправочника.Ссылка КАК Ссылка,
	|	ДанныеСправочника.ПометкаУдаления КАК ПометкаУдаления,
	|	Аналитика.КлючАналитики КАК КлючАналитики
	|ИЗ
	|	Справочник.КлючиАналитикиПланированияСтатейБюджетов КАК ДанныеСправочника
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаПланированияСтатейБюджетов КАК ДанныеРегистра
	|		ПО ДанныеСправочника.Ссылка = ДанныеРегистра.КлючАналитики
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаПланированияСтатейБюджетов КАК Аналитика
	|		ПО ДанныеСправочника.СтатьяБюджета = Аналитика.СтатьяБюджета
	|			И ДанныеСправочника.Аналитика1 = Аналитика.Аналитика1
	|			И ДанныеСправочника.Аналитика2 = Аналитика.Аналитика2
	|			И ДанныеСправочника.Аналитика3 = Аналитика.Аналитика3
	|			И ДанныеСправочника.Аналитика4 = Аналитика.Аналитика4
	|			И ДанныеСправочника.Аналитика5 = Аналитика.Аналитика5
	|			И ДанныеСправочника.Аналитика6 = Аналитика.Аналитика6
	|			И ДанныеСправочника.ПриходРасход = Аналитика.ПриходРасход
	|ГДЕ
	|	ДанныеРегистра.КлючАналитики ЕСТЬ NULL";
	
	// Сформируем соответствие ключей аналитики.
	СоответствиеАналитик = Новый Соответствие;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
	
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			СоответствиеАналитик.Вставить(Выборка.Ссылка, Выборка.КлючАналитики);
			
			Если Не Выборка.ПометкаУдаления Тогда
				СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
				Попытка
					СправочникОбъект.УстановитьПометкуУдаления(Истина, Ложь);
				Исключение
					ВызватьИсключение;
				КонецПопытки;
			КонецЕсли;

		КонецЦикла;
		Исключения = Новый Массив;
		ОбщегоНазначенияОПК.ЗаменитьСсылки(СоответствиеАналитик);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли