

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ПодготовитьФормуНаСервере(ЭтаФорма);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда	
		ПодготовитьФормуНаСервере(ЭтаФорма);	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура СчетПриИзменении(Элемент)
	МСФОКлиентСерверУХ.ОбновитьСубконтоСчета(ЭтаФорма, Элемент.Имя);
КонецПроцедуры

#Область ВспомогательныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ПодготовитьФормуНаСервере(Форма)
	
	Если НЕ Форма.Объект.Предопределенный Тогда
	
		Форма.Элементы.Родитель.ТолькоПросмотр = Ложь;
		Форма.Элементы.Код.ТолькоПросмотр = Ложь;		
	
	КонецЕсли;
	
	Форма.КэшируемыеЗначения = Новый Структура;	
	Форма.КэшируемыеЗначения.Вставить("ИменаСубконто", 		Справочники.ФиксированныеСчетаУчетаБД.ПолучитьИменаСубконто());	
	Форма.КэшируемыеЗначения.Вставить("КлючевыеСубконто", 	ПолучитьКлючевыеСубконто(Форма.Объект.Родитель));
	
	ПланСчетовБД = МСФОКлиентСерверУХ.ПланСчетовПараметровУчета(Форма.Объект.Счет);

	ПараметрыВыбораИсточник = Новый Массив;
	ПараметрыВыбораИсточник.Добавить(Новый ПараметрВыбора("Владелец", ПланСчетовБД));
	ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораИсточник);	
	
	Для каждого ИмяСчета Из Форма.КэшируемыеЗначения.ИменаСубконто Цикл
		Форма.Элементы[ИмяСчета.Ключ].ПараметрыВыбора = ПараметрыВыбора;
		МСФОКлиентСерверУХ.ОбновитьСубконтоСчета(Форма, ИмяСчета.Ключ);
	КонецЦикла;
	
	ЕстьУП = ПолучитьФункциональнуюОпцию("ИспользоватьУчетныеПолитикиМСФО");
	
	Форма.Элементы.ГруппаСчет.Видимость = Не ЕстьУП;
	Форма.Элементы.ФиксированныеСчетаУчетаБД.Видимость = ЕстьУП;
	
	КомпоновщикСписка = Форма.ФиксированныеСчетаУчетаБД.КомпоновщикНастроек;
	КомпоновщикСписка.Настройки.Отбор.Элементы.Получить(0).ПравоеЗначение = Форма.Объект.Ссылка;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКлючевыеСубконто(Родитель)
	
	Если Родитель.Пустая() Тогда
		
		Возврат новый Соответствие;
		
	ИначеЕсли Родитель = Справочники.ФиксированныеСчетаУчетаБД.КонсолидационныеПоправки 
		Или Родитель.ПринадлежитЭлементу(Справочники.ФиксированныеСчетаУчетаБД.КонсолидационныеПоправки) Тогда
	
		Возврат МСФОКлиентСерверУХ.ПолучитьКлючевыеСубконтоИнвестиций();
		
	Иначе
		
		Возврат Новый Соответствие;
	
	КонецЕсли;	

КонецФункции

#КонецОбласти
