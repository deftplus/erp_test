
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере(Параметры.ЗначениеКопирования);
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
		
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	ПодготовитьФормуНаСервере();
		
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ДокВексель = РеквизитФормыВЗначение("ПараметрыЦеннойБумаги");
	Если Не ДокВексель.ПроверитьЗаполнение() Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ПараметрыЦеннойБумаги", РеквизитФормыВЗначение("ПараметрыЦеннойБумаги"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
		
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ЗначениеВРеквизитФормы(ТекущийОбъект.ПараметрыЦеннойБумаги.ПолучитьОбъект(), "ПараметрыЦеннойБумаги");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФормаВекселяПриИзменении(Элемент)
	УправлениеФормой(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ВекселедательПриИзменении(Элемент)
	Объект.Эмитент = ПараметрыЦеннойБумаги.Векселедатель;
	
	Если ПараметрыЦеннойБумаги.ФормаВекселя = ПредопределенноеЗначение("Перечисление.ФормаВекселя.Простой") Тогда 
		ПараметрыЦеннойБумаги.Должник = ПараметрыЦеннойБумаги.Векселедатель;
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ДолжникПриИзменении(Элемент)
	УправлениеФормой(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура СрокПлатежаПриИзменении(Элемент)
	
	Если ПараметрыЦеннойБумаги.СрокПлатежаПоВекселю = ПредопределенноеЗначение("Перечисление.СрокиПлатежаПоВекселю.НаОпределенныйДень") Тогда
		ПараметрыЦеннойБумаги.ПредусмотренПроцентныйДоход = Ложь;
	КонецЕсли;
	ЗаполнитьДатыВекселя(ЭтаФорма);
	РассчитатьСуммуПроцентов(ЭтаФорма);
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыЦеннойБумагиПредусмотренПроцентныйДоходПриИзменении(Элемент)
	Объект.ДисконтнаяЦБ = Не ПараметрыЦеннойБумаги.ПредусмотренПроцентныйДоход;
	ЗаполнитьДатыВекселя(ЭтаФорма);	
	РассчитатьСуммуПроцентов(ЭтаФорма);
	УправлениеФормой(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ВалютаНоминалаПриИзменении(Элемент)
	Объект.ВалютаНоминала = ПараметрыЦеннойБумаги.ВалютаНоминала;
	Объект.ВалютаКотировки = ПараметрыЦеннойБумаги.ВалютаНоминала;
	
КонецПроцедуры

&НаКлиенте
Процедура НачислятьПроцентыСДатыСоставленияПриИзменении(Элемент)
	
	ЗаполнитьДатыВекселя(ЭтаФорма);	
	РассчитатьСуммуПроцентов(ЭтаФорма);
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаВыпускаПриИзменении(Элемент)
	
	Объект.ДатаВыпуска = ПараметрыЦеннойБумаги.ДатаСоставления;
	
	ЗаполнитьДатыВекселя(ЭтаФорма);
	РассчитатьСуммуПроцентов(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура УказанаДатаПредъявленияНеРанееПриИзменении(Элемент)
	
	ЗаполнитьДатыВекселя(ЭтаФорма);
	РассчитатьСуммуПроцентов(ЭтаФорма);
	
	УправлениеФормой(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыЦеннойБумагиДатаНачалаНачисленияПроцентовПриИзменении(Элемент)
	
	ЗаполнитьДатыВекселя(ЭтаФорма);
	РассчитатьСуммуПроцентов(ЭтаФорма);
	
	
КонецПроцедуры

&НаКлиенте
Процедура НоминалПриИзменении(Элемент)
	Объект.Номинал = ПараметрыЦеннойБумаги.Номинал;
	РассчитатьСуммуПроцентов(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ПараметрыЦеннойБумагиДатаПредъявленияНеРанееПриИзменении(Элемент)
	
	ЗаполнитьДатыВекселя(ЭтаФорма);
	РассчитатьСуммуПроцентов(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцентнаяСтавкаПриИзменении(Элемент)
	РассчитатьСуммуПроцентов(ЭтаФорма);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
//Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере(ЗначениеКопирования = Неопределено)
	
	Если ЗначениеЗаполнено(Объект.ПараметрыЦеннойБумаги) И ТипЗнч(Объект.ПараметрыЦеннойБумаги) = Тип("ДокументСсылка.ПараметрыЦеннойБумагиВексель") Тогда
		
		ДокВексель = Объект.ПараметрыЦеннойБумаги.ПолучитьОбъект();
		
	ИначеЕсли ЗначениеЗаполнено(ЗначениеКопирования) Тогда
		
		ДокВексель = ЗначениеКопирования.ПараметрыЦеннойБумаги.Скопировать();
				
	Иначе
		
		ДокВексель = Документы.ПараметрыЦеннойБумагиВексель.СоздатьДокумент();
		Если ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения) Тогда
			ЗаполнитьЗначенияСвойств(ДокВексель, Параметры.ЗначенияЗаполнения);
		КонецЕсли;
		
		ДокВексель.Заполнить(Объект);
				
	КонецЕсли;
		
	ЗначениеВРеквизитФормы(ДокВексель, "ПараметрыЦеннойБумаги");
	
	УправлениеФормой(ЭтаФорма);
	
	ЦенныеБумагиФормыКлиентСервер.УстановитьЗаголовокФормы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Объект = Форма.Объект;
	ПараметрыЦеннойБумаги = Форма.ПараметрыЦеннойБумаги;
	Элементы = Форма.Элементы;
	
	// Группа "Стороны"
	ЭтоПереводнойВексель = (ПараметрыЦеннойБумаги.ФормаВекселя = ПредопределенноеЗначение("Перечисление.ФормаВекселя.Переводной"));
	Элементы.Векселедатель.Заголовок = ?(ЭтоПереводнойВексель, НСтр("ru = 'Векселедатель'"), НСтр("ru = 'Векселедатель и плательщик'"));
	Элементы.Должник.Видимость = ЭтоПереводнойВексель;
	Если ЭтоПереводнойВексель Тогда
		ТекстЗаголовкаСвернутогоОтображения = СтрШаблон(НСтр("ru = 'Стороны: векселедатель %1, плательщик %2)'"), ПараметрыЦеннойБумаги.Векселедатель, ПараметрыЦеннойБумаги.Должник);
	Иначе
		ТекстЗаголовкаСвернутогоОтображения = СтрШаблон(НСтр("ru = 'Стороны: Векселедатель %1'"), ПараметрыЦеннойБумаги.Векселедатель);
	КонецЕсли;
	Элементы.ГруппаСтороны.ЗаголовокСвернутогоОтображения = ТекстЗаголовкаСвернутогоОтображения;
	
	// Группа "Платеж"
	ЭтоПлатежПоПредъявлении = (ПараметрыЦеннойБумаги.СрокПлатежаПоВекселю = ПредопределенноеЗначение("Перечисление.СрокиПлатежаПоВекселю.ПоПредъявлении"));
	
	УказанаДатаПредъявления = (ПараметрыЦеннойБумаги.СпособУказанияДатыПредъявления = ПредопределенноеЗначение("Перечисление.СпособыУказанияДатыПредъявленияВекселя.НеРанееУказаннойДаты"));
	НачислятьПроцентыСДатыПредъявления = (ПараметрыЦеннойБумаги.ВариантДатыНачалаНачисленияПроцентов = ПредопределенноеЗначение("Перечисление.ВариантыДатыНачалаНачисленияПроцентовПоВекселю.СДатыВозможногоПредъявления"));
	
	Элементы.ПараметрыЦеннойБумагиПредусмотренПроцентныйДоход.ТолькоПросмотр = Не ЭтоПлатежПоПредъявлении;
	Элементы.ОтсрочкаПлатежа.Видимость = ЭтоПлатежПоПредъявлении;
	Элементы.ГруппаПлатежНеРанее.Видимость = ЭтоПлатежПоПредъявлении;
	Элементы.ДатаПлатежа.Видимость = Не ЭтоПлатежПоПредъявлении;
	Элементы.ПараметрыЦеннойБумагиДатаПредъявленияНеРанее.Видимость = УказанаДатаПредъявления;
	Элементы.ГруппаПроценты.Видимость = ПараметрыЦеннойБумаги.ПредусмотренПроцентныйДоход;
	Элементы.ПараметрыЦеннойБумагиДатаНачалаНачисленияПроцентов.Видимость = Не НачислятьПроцентыСДатыПредъявления;
	
	ТекстыЗаголовкаСвернутогоОтображения = Новый Массив;
	ТекстыЗаголовкаСвернутогоОтображения.Добавить(СтрШаблон("Платеж: %1 %2", ПараметрыЦеннойБумаги.Номинал, ПараметрыЦеннойБумаги.ВалютаНоминала));
	Если ЭтоПлатежПоПредъявлении Тогда
		ТекстыЗаголовкаСвернутогоОтображения.Добавить(НСтр("ru = ' по предъявлении'"));
		Если УказанаДатаПредъявления Тогда
			ТекстыЗаголовкаСвернутогоОтображения.Добавить(СтрШаблон(НСтр("ru = ' не ранее %1'"),Формат(ПараметрыЦеннойБумаги.ДатаПредъявленияНеРанее,"ДФ=dd.MM.yyyy")));
		КонецЕсли;
		Если ПараметрыЦеннойБумаги.ПредусмотренПроцентныйДоход Тогда
			ТекстыЗаголовкаСвернутогоОтображения.Добавить(СтрШаблон(НСтр("ru = '; процентная ставка %1%% годовых'"),ПараметрыЦеннойБумаги.ПроцентнаяСтавка));
		КонецЕсли;
	Иначе
		ТекстыЗаголовкаСвернутогоОтображения.Добавить(СтрШаблон(НСтр("ru = ' в дату %1'"), Формат(ПараметрыЦеннойБумаги.ДатаПлатежа,"ДФ=dd.MM.yyyy")));
	КонецЕсли;

	Элементы.ГруппаПлатеж.ЗаголовокСвернутогоОтображения = СтрСоединить(ТекстыЗаголовкаСвернутогоОтображения)
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьДатыВекселя(Форма)
	
	ПараметрыЦеннойБумаги = Форма.ПараметрыЦеннойБумаги;
	
	Если ПараметрыЦеннойБумаги.СпособУказанияДатыПредъявления <> ПредопределенноеЗначение("Перечисление.СпособыУказанияДатыПредъявленияВекселя.НеРанееУказаннойДаты") Тогда
		ПараметрыЦеннойБумаги.ДатаПредъявленияНеРанее = ПараметрыЦеннойБумаги.ДатаСоставления;
	КонецЕсли;
	
	Если ПараметрыЦеннойБумаги.ВариантДатыНачалаНачисленияПроцентов = ПредопределенноеЗначение("Перечисление.ВариантыДатыНачалаНачисленияПроцентовПоВекселю.СДатыВозможногоПредъявления") Тогда
		ПараметрыЦеннойБумаги.ДатаНачалаНачисленияПроцентов = ПараметрыЦеннойБумаги.ДатаПредъявленияНеРанее + 86400;
	КонецЕсли;
	
	ПараметрыЦеннойБумаги.ДатаОкончанияНачисленияПроцентов = ПараметрыЦеннойБумаги.ДатаПредъявленияНеРанее + 365 * 86400;
	
	Если ДеньГода(КонецГода(ПараметрыЦеннойБумаги.ДатаПредъявленияНеРанее)) = 366 Тогда
		ДатаПроверки = Дата(Год(ПараметрыЦеннойБумаги.ДатаПредъявленияНеРанее),2,29);
	ИначеЕсли ДеньГода(КонецГода(ПараметрыЦеннойБумаги.ДатаОкончанияНачисленияПроцентов)) = 366 Тогда
		ДатаПроверки = Дата(Год(ПараметрыЦеннойБумаги.ДатаОкончанияНачисленияПроцентов),2,29);
	Иначе
		ДатаПроверки = Дата(1,1,1);
	КонецЕсли;
	
	Если ДатаПроверки >= ПараметрыЦеннойБумаги.ДатаПредъявленияНеРанее И ДатаПроверки <= ПараметрыЦеннойБумаги.ДатаОкончанияНачисленияПроцентов Тогда
		ПараметрыЦеннойБумаги.ДатаОкончанияНачисленияПроцентов = ПараметрыЦеннойБумаги.ДатаОкончанияНачисленияПроцентов + 86400; 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьСуммуПроцентов(Форма)
	
	ПараметрыЦеннойБумаги = Форма.ПараметрыЦеннойБумаги;
	
	Если ПараметрыЦеннойБумаги.ПредусмотренПроцентныйДоход Тогда
		ПараметрыЦеннойБумаги.МаксимальнаяСуммаПроцентов = ФинансоваяМатематикаКлиентСервер.СуммаПроцентовЗаПериод(
			ПараметрыЦеннойБумаги.ДатаНачалаНачисленияПроцентов,
			ПараметрыЦеннойБумаги.ДатаОкончанияНачисленияПроцентов,
			ПараметрыЦеннойБумаги.Номинал,
			ПараметрыЦеннойБумаги.ПроцентнаяСтавка);
	Иначе
		ПараметрыЦеннойБумаги.МаксимальнаяСуммаПроцентов = 0;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СтандартныеПодсистемы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти
