
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КэшируемыеЗначения = Новый Структура;
	КэшируемыеЗначения.Вставить("ПараметрыГрафика", ФинансовыеИнструментыФормыКлиентСервер.ПолучитьПараметрыГрафикаЦБ());
	
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ПодготовитьФормуНаСервере();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Не РеквизитФормыВЗначение("ВерсияФИ").ПроверитьЗаполнение() Тогда
		Отказ = Истина;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.ДисконтнаяЦБ = ТипЦБ;
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ПараметрыЦеннойБумаги", РеквизитФормыВЗначение("ВерсияФИ"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
		
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ЗначениеВРеквизитФормы(ТекущийОбъект.ПараметрыЦеннойБумаги.ПолучитьОбъект(), "ВерсияФИ")
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьГрафик(Команда)
		
	ПараметрыГрафика = Новый Структура("ФИ,ДатаНачала", Объект.Ссылка, Объект.ДатаВыпуска);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПараметрыГрафика", 	ПараметрыГрафика);
	ПараметрыФормы.Вставить("АдресТаблицыГрафика", 	ПолучитьАдресГрафика());
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ЗагрузитьГрафикЗавершение", ЭтаФорма, ПараметрыГрафика);
	
	ОткрытьФорму("Обработка.ЗагрузкаГрафикаФИ.Форма.Форма", ПараметрыФормы, ЭтаФорма,,,, ОповещениеОЗакрытии);
		
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммыПроцентов(Команда)
	ЗаполнитьГрафикНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ИсторияВерсий(Команда)
	ПараметрыОткрытия = Новый Структура("Отбор", Новый Структура("ФинансовыйИнструмент", Объект.Ссылка));
	ОткрытьФорму("Документ.ВерсияГрафикаЦеннойБумаги.ФормаСписка", ПараметрыОткрытия, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьПроцентныеСтавки(Команда)
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ИсходныеДанные", ВерсияФИ);
	
	ДополнительныеДанные = Новый Структура;
	ДополнительныеДанные.Вставить("ДатаНачалаДействия", Объект.ДатаВыпуска);
	СтруктураПараметров.Вставить("ДополнительныеДанные", ДополнительныеДанные);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("НадписьРедактироватьПроцентныеСтавкиПродолжение", ЭтотОбъект);
	
	ОткрытьФорму("Обработка.РедактированиеГрафика.Форма.ФормаРедактированияПроцентныхСтавок", СтруктураПараметров, ЭтотОбъект,,,, ОписаниеОповещения);
		
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьСуммуГрафика(Команда)
	
	ТекДанные = Элементы.ВерсияЦБГрафик.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоОблигация = Объект.ВидФинансовогоИнструмента = ПредопределенноеЗначение("Перечисление.ВидыФинансовыхИнструментов.Облигация");
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ДатаРасшифровки", ТекДанные.Дата);
	Если Элементы.ВерсияЦБГрафик.ТекущийЭлемент = Элементы.ВерсияЦБГрафикПроцентыНачислено Тогда
		СтруктураПараметров.Вставить("ЗаголовокРасшифровки", ?(ЭтоОблигация, НСтр("ru = 'Начисление купонов'"), НСтр("ru = 'Начисление процентов'")));
	ИначеЕсли Элементы.ВерсияЦБГрафик.ТекущийЭлемент = Элементы.ВерсияЦБГрафикПроцентыУплачено Тогда
		СтруктураПараметров.Вставить("ЗаголовокРасшифровки", ?(ЭтоОблигация, НСтр("ru = 'Уплата купонов'"), НСтр("ru = 'Уплата процентов'")));
	Иначе
		Возврат;
	КонецЕсли;
	
	СтруктураПараметров.Вставить("ТекущееПоле", СтрЗаменить(Элементы.ВерсияЦБГрафик.ТекущийЭлемент.Имя,Элементы.ВерсияЦБГрафик.Имя, ""));
	СтруктураПараметров.Вставить("СтрокаГрафика", Элементы.ВерсияЦБГрафик.ТекущаяСтрока);
	СтруктураПараметров.Вставить("ДокументОбъект", ВерсияФИ);
	
	ОткрытьФорму("Обработка.РедактированиеГрафика.Форма.ФормаРасшифровкиРассчитанныхСумм", СтруктураПараметров);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура ВидФинансовогоИнструментаПриИзменении(Элемент)
	ПодготовитьФормуНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ФормаВекселяПриИзменении(Элемент)
	УправлениеФормой(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура БиржевойПриИзменении(Элемент)
	УправлениеФормой(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ДатаВыпускаПриИзменении(Элемент)
	ЗаполнитьГрафикНаСервере();	
КонецПроцедуры

&НаКлиенте
Процедура СрокПогашенияПриИзменении(Элемент)
	ЗаполнитьГрафикНаСервере();	
КонецПроцедуры

&НаКлиенте
Процедура НоминалПриИзменении(Элемент)
	ЗаполнитьГрафикНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДатаОтсчетаПроцентныхПериодовПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипПроцентнойСтавкиПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеИзменениямиСтавкиПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура РучноеУправлениеИзменениямиСтавкиПлавающаяПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ТипЦБПриИзменении(Элемент)
	УправлениеФормой(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ВерсияЦБГрафикПриИзменении(Элемент)
	ВерсияЦБГрафикПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЦенаВыпускаПриИзменении(Элемент)
	УправлениеФормой(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ВерсияЦБГрафикПриАктивизацииЯчейки(Элемент)
	
	Элементы.РасшифроватьСуммуГрафика.Доступность = (Элемент.ТекущийЭлемент = Элементы.ВерсияЦБГрафикПроцентыНачислено 
		ИЛИ Элемент.ТекущийЭлемент = Элементы.ВерсияЦБГрафикПроцентыУплачено);
	
	КонецПроцедуры
	
#КонецОбласти

#Область СтандартныеПодсистемы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроцентныеСтавки

&НаКлиенте
Процедура НадписьРедактироватьПроцентныеСтавкиПродолжение(Результат, Параметры) Экспорт
	
	Если ЭтоАдресВременногоХранилища(Результат) Тогда
		ЗагрузитьТаблицуПроцентныхСтавок(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьТаблицуПроцентныхСтавок(АдресХранилища)
	
	ВерсияФИ.ПроцентныеСтавки.Загрузить(ПолучитьИзВременногоХранилища(АдресХранилища));
	
КонецПроцедуры

#КонецОбласти

#Область График

&НаСервере
Процедура ЗаполнитьГрафикНаСервере()
	
	Если (Объект.Номинал = 0) Или Не ЗначениеЗаполнено(Объект.ДатаВыпуска) Или Не ЗначениеЗаполнено(Объект.СрокПогашения) Тогда
		ВерсияФИ.График.Очистить();
		Возврат;
	КонецЕсли;
	
	Объект.ДисконтнаяЦБ = ТипЦБ > 1;
	
	ЗаполнитьОсновнойДолгЦБ = Новый Структура("ДатаВыпуска,ДатаПогашения,ОсновнойДолгПолучение,ОсновнойДолгВозврат",
													Объект.ДатаВыпуска, 
													Объект.СрокПогашения, 
													Объект.Номинал, 
													?(Объект.ДисконтнаяЦБ, Объект.ЦенаВыпуска, Объект.Номинал)
												);
											
	СтрукутураДействий = Новый Структура("ЗаполнитьОсновнойДолгЦБ,ЗаполнитьПроценты,СвернутьГрафик,РассчитатьСальдо", ЗаполнитьОсновнойДолгЦБ);
	Если Не Объект.ДисконтнаяЦБ Тогда
		СтрукутураДействий.Вставить("РассчитатьПроценты");
	КонецЕсли;
	
	КэшируемыеЗначения.ПараметрыГрафика.Вставить("Финансовыйинструмент", Объект);
	
	ВерсияФИ.График.Загрузить(ФинансовыеИнструментыФормыВызовСервера.ПолучитьГрафик(СтрукутураДействий, ВерсияФИ, КэшируемыеЗначения.ПараметрыГрафика));
	
	ФинансовыеИнструментыФормыКлиентСервер.ОбновитьИтогиФормаЦБ(ЭтаФорма, ВерсияФИ.График);
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьДатуВерсии(Форма)
	
	ВерсияФИ.Дата = ТекущаяДата();
	
КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаГрафика 

&НаКлиенте
Процедура ЗагрузитьГрафикЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт

	Если РезультатЗакрытия = Неопределено ИЛИ РезультатЗакрытия = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьГрафикСервер(РезультатЗакрытия);

КонецПроцедуры

&НаСервере
Функция ПолучитьАдресГрафика()
	
	ИменаКолонок = "Дата,ОсновнойДолгПолучение,ОсновнойДолгВозврат,ПроцентыНачислено,ПроцентыУплачено";
	
	ТаблицаГрафик = ВерсияФИ.График.Выгрузить(,ИменаКолонок);
	ПредставленияКолонок = Справочники.ОперацииГрафиковДоговоров.ПолучитьПараметрыПоВидуФИ(Объект.ВидФинансовогоИнструмента, Истина);
	
	Для каждого КолонкаГрафика Из ТаблицаГрафик.Колонки Цикл
		
		Если ПредставленияКолонок.Свойство(КолонкаГрафика.Имя, КолонкаГрафика.Заголовок) 
			И Не ЗначениеЗаполнено(КолонкаГрафика.Заголовок) Тогда
			КолонкаГрафика.Заголовок = КолонкаГрафика.Имя;
		КонецЕсли;
				
	КонецЦикла;
		
	Возврат ПоместитьВоВременноеХранилище(ТаблицаГрафик);
	
КонецФункции

&НаСервере
Процедура ЗагрузитьГрафикСервер(АдресГрафика)
	
	ДанныеГрафика = ПолучитьИзВременногоХранилища(АдресГрафика);	
	ВерсияФИ.График.Загрузить(ДанныеГрафика.График);
		
	ФинансовыеИнструментыФормыВызовСервера.ПриИзмененииГрафикаНаФорме(ЭтаФорма, ВерсияФИ);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПересчитатьИтогиСтрокиГрафика(СтрокаГрафика)

	СтрокаГрафика.ОсновнойДолгОстаток = СтрокаГрафика.ОсновнойДолгПолучение - СтрокаГрафика.ОсновнойДолгВозврат;
	СтрокаГрафика.ПроцентыОстаток = СтрокаГрафика.ПроцентыНачислено - СтрокаГрафика.ПроцентыУплачено;

КонецПроцедуры

&НаСервере
Процедура ВерсияЦБГрафикПриИзмененииНаСервере()
	ВерсияФИОбъект = РеквизитФормыВЗначение("ВерсияФИ");
	ФинансовыеИнструментыФормыВызовСервера.ПриИзмененииГрафикаНаФорме(ЭтаФорма, ВерсияФИОбъект);
	ЗначениеВРеквизитФормы(ВерсияФИОбъект, "ВерсияФИ");
КонецПроцедуры

#КонецОбласти

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
		
	ЭтоОблигация = Объект.ВидФинансовогоИнструмента = ПредопределенноеЗначение("Перечисление.ВидыФинансовыхИнструментов.Облигация");
	
	Объект.ДисконтнаяЦБ = Форма.ТипЦБ > 0;
		
	ВидимостьСтороныСоглашения(Форма);
	УправлениеФормой_Условия(Форма, ЭтоОблигация);
	ВидимостьРЦБ(Форма);
 	ВидимостьПроценты(Форма, ЭтоОблигация);
	ВидимостьГрафик(Форма, ЭтоОблигация);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВидимостьГрафик(Форма, ЭтоОблигация)
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	ЕстьГрафик = Не Объект.ДисконтнаяЦБ И ЭтоОблигация;
	
	Элементы.ГруппаГрафик.Видимость = ЕстьГрафик;
	Элементы.ГруппаСтраницы.ОтображениеСтраниц = ?(ЕстьГрафик, ОтображениеСтраницФормы.ЗакладкиСверху, ОтображениеСтраницФормы.Нет);
	
	ВидФИ = Объект.ВидФинансовогоИнструмента;
	ФинансовыеИнструментыФормыКлиентСервер.УстановитьЗаголовкиГрафика(Форма, ВидФИ, Истина, "ВерсияЦБГрафик", "ВерсияЦБГрафикПроценты");	
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВидимостьПроценты(Форма, ЭтоОблигация)
	
	Элементы = Форма.Элементы;	
	Объект = Форма.Объект;
	ВерсияФИ = Форма.ВерсияФИ;
	
	Элементы.ГруппаПроценты.Видимость = Не Объект.ДисконтнаяЦБ И (ЭтоОблигация);
	Элементы.ГруппаПроценты.Заголовок = ?(ЭтоОблигация, НСтр("ru = 'Купоны'"), НСтр("ru = 'Проценты'"));
	
	Если Объект.ДисконтнаяЦБ Тогда		
		ВерсияФИ.ТипПроцентнойСтавки = ПредопределенноеЗначение("Перечисление.ТипыСтавокДляРасчетаПериодическихОпераций.ПустаяСсылка");
		Форма.СтавкаДоходностиПоДисконту = ПолучитьЭСП(Форма);
	КонецЕсли;	
		
	Элементы.ТипПроцентнойСтавки.Видимость = ЭтоОблигация;
	Элементы.БазаДляРасчетаПроцентов.Видимость = ЭтоОблигация;
	Элементы.ПереносДатСНерабочихДней.Видимость = ЭтоОблигация;
	Элементы.ИзменяетсяДлительностьПроцентногоПериодаПриПереносе.Видимость = ЭтоОблигация;
	
	ОблигацияКупонная = ЭтоОблигация И Не Объект.ДисконтнаяЦБ;
	
	ВерсияФИ.ВыплачиватьПроцентыПериодически = ЭтоОблигация И Не Объект.ДисконтнаяЦБ;
	Элементы.ГруппаПериодичностьПроцентов.Видимость = ЭтоОблигация;
	
	Элементы.ДатаОкончанияПервогоПроцентногоПериода.Видимость = (ВерсияФИ.ДатаОтсчетаПроцентныхПериодов = ПредопределенноеЗначение("Перечисление.ВидыДатОтсчетаПериодичности.ДатаПервогоПогашения"));
	
	ЭтоФиксированнаяСтавка = (ВерсияФИ.ТипПроцентнойСтавки = ПредопределенноеЗначение("Перечисление.ТипыСтавокДляРасчетаПериодическихОпераций.Фиксированная"));
	Элементы.ГруппаФиксированнаяСтавка.Видимость = ЭтоФиксированнаяСтавка;
	Элементы.ГруппаПлавающаяСтавка.Видимость = НЕ ЭтоФиксированнаяСтавка;
	
	Элементы.ГруппаФиксированнаяСтавкаСтавка.Видимость = НЕ ВерсияФИ.РучноеУправлениеИзменениямиСтавки;
	Элементы.РедактироватьПроцентныеСтавкиФиксированная.Видимость = ВерсияФИ.РучноеУправлениеИзменениямиСтавки;
	Элементы.РедактироватьПроцентныеСтавкиПлавающая.Видимость = ВерсияФИ.РучноеУправлениеИзменениямиСтавки;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьЭСП(Форма)
	
	Объект = Форма.Объект;

	ДенежныйПоток = Новый Массив;
	ДенежныйПоток.Добавить(-Объект.ЦенаВыпуска);
	ДенежныйПоток.Добавить(Объект.Номинал);
	
	ДатыПотока = Новый Массив;
	ДатыПотока.Добавить(Объект.ДатаВыпуска);
	ДатыПотока.Добавить(Объект.СрокПогашения);

	Возврат ФинансоваяМатематикаКлиентСервер.ПолучитьЭффективнуюСтавкуПроцента(ДенежныйПоток, ДатыПотока, 0) * 100;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ВидимостьРЦБ(Форма)
	
	Элементы = Форма.Элементы;
	
	Объект = Форма.Объект;
	
	Элементы.Биржа.Видимость = Объект.ЭтоБиржевойРынок;
	Элементы.БиржевойКод.Видимость = Объект.ЭтоБиржевойРынок;
	Элементы.Котировка.Видимость = Объект.ЭтоБиржевойРынок;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой_Условия(Форма, ЭтоОблигация)

	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	Элементы.ЦенаВыпуска.Видимость = Объект.ДисконтнаяЦБ;
	Элементы.Количество.Видимость = ЭтоОблигация;
	Элементы.СрокПогашения.Видимость = ЭтоОблигация;
	Элементы.СрокПогашения.Заголовок = ?(ЭтоОблигация, НСтр("ru = 'Срок погашения'"), НСтр("ru = 'Срок погашения не ранее'"));
	Элементы.СтавкаДоходностиПоДисконту.Видимость = Объект.ДисконтнаяЦБ;
	
	Элементы.ГруппаПериодичностьПроцентов.Заголовок = ?(ЭтоОблигация, НСтр("ru = 'Купонные периоды'"), НСтр("ru = 'Процентные периоды'"));
	Элементы.ГруппаПроцентнаяСтавка.Заголовок = ?(ЭтоОблигация, НСтр("ru = 'Ставка купонов'"), НСтр("ru = 'Процентная ставка'"));
	
	ЗаголовокСтавка = ?(ЭтоОблигация, НСтр("ru = 'Ставка купона'"), НСтр("ru = 'Процентная ставка'"));
	Элементы.ПроцентнаяСтавка.Заголовок = ЗаголовокСтавка;
	Элементы.ИндикативнаяСтавка.Заголовок = ЗаголовокСтавка;
	
	ОбновитьОтображениеТипЦБ(Форма, ЭтоОблигация);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВидимостьСтороныСоглашения(Форма)

	Элементы = Форма.Элементы;
	Объект = Форма.Объект;

	ЭтоОблигация = Объект.ВидФинансовогоИнструмента = ПредопределенноеЗначение("Перечисление.ВидыФинансовыхИнструментов.Облигация");
	
	Элементы.ПервыйПолучатель.Заголовок = ?(Объект.ЭтоБиржевойРынок > 0, НСтр("ru = 'Андеррайтер'"), НСтр("ru = 'Кредитор'"));
	
	Элементы.ПервыйПолучатель.Видимость = ЭтоОблигация;
 		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьОтображениеПроценты(Форма)
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьОтображениеТипЦБ(Форма, ЭтоОблигация)

	Элементы = Форма.Элементы;
	
	Элементы.ТипЦБ.Видимость = ЭтоОблигация;
	Если Элементы.ТипЦБ.Видимость Тогда
		
		Элементы.ТипЦБ.Заголовок = НСтр("ru = 'Способ выплаты дохода'");
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	ТипЦБ = Объект.ДисконтнаяЦБ;
	
	ДокВерсия = Неопределено;
	
	Если ЗначениеЗаполнено(Объект.ПараметрыЦеннойБумаги) Тогда
		
		ДокВерсия = Объект.ПараметрыЦеннойБумаги.ПолучитьОбъект();
		
	Иначе
		ИмяДокумента = ЦенныеБумагиКлиентСерверПовтИсп.ПолучитьИмяДокумента(Объект.ВидФинансовогоИнструмента);
		Если ЗначениеЗаполнено(ИмяДокумента) Тогда
			ДокВерсия = Документы[ИмяДокумента].СоздатьДокумент();
			ДокВерсия.Заполнить(Объект);
		КонецЕсли;
	КонецЕсли;
	
	Если ДокВерсия <> Неопределено Тогда
		ЗначениеВРеквизитФормы(ДокВерсия, "ВерсияФИ");
	КонецЕсли;
	
	ЭтаФорма.ЗаблокироватьДанныеФормыДляРедактирования();
	УправлениеФормой(ЭтаФорма);	
	ЦенныеБумагиФормыКлиентСервер.УстановитьЗаголовокФормы(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти
