// ++ УХ_Интеграция
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПриКопировании(ОбъектКопирования)
	ПараметрыЦеннойБумаги = Неопределено;
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
КонецПроцедуры
	
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ВидФинансовогоИнструмента = Перечисления.ВидыФинансовыхИнструментов.Вексель Тогда
		
		// Исключим реквизиты, заполняемые методом ЗаполнитьРеквизитыСправочникаПоДокументу.
		МассивНепроверяемыхРеквизитов.Добавить("ВалютаНоминала");
		МассивНепроверяемыхРеквизитов.Добавить("Номинал");
		МассивНепроверяемыхРеквизитов.Добавить("Эмитент");
		МассивНепроверяемыхРеквизитов.Добавить("ДатаВыпуска");
		МассивНепроверяемыхРеквизитов.Добавить("СрокПогашения");
		МассивНепроверяемыхРеквизитов.Добавить("ПервыйПолучатель");
		МассивНепроверяемыхРеквизитов.Добавить("Должник");
		
	ИначеЕсли ВидФинансовогоИнструмента <> Перечисления.ВидыФинансовыхИнструментов.Облигация Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("СрокПогашения");
		
	КонецЕсли;
	
	Если ЭтоГруппа Или Не ЭтоБиржевойРынок Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Биржа");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ПараметрыЦеннойБумаги") Тогда
		// Есть документ с данными. Установим ссылку, заполним реквизиты справочника, 
		// синхронизируем пометку удаления
		Если ЗначениеЗаполнено(ДополнительныеСвойства.ПараметрыЦеннойБумаги.Ссылка) Тогда
			ПараметрыЦеннойБумаги = ДополнительныеСвойства.ПараметрыЦеннойБумаги.Ссылка;
		Иначе
			ПараметрыЦеннойБумаги = ОбщегоНазначения.МенеджерОбъектаПоСсылке(ДополнительныеСвойства.ПараметрыЦеннойБумаги.Ссылка).ПолучитьСсылку();
			ДополнительныеСвойства.ПараметрыЦеннойБумаги.УстановитьСсылкуНового(ПараметрыЦеннойБумаги);
		КонецЕсли;
		
		ЗаполнитьРеквизитыСправочникаПоДокументу(ДополнительныеСвойства.ПараметрыЦеннойБумаги);
		
		ДополнительныеСвойства.ПараметрыЦеннойБумаги.ПометкаУдаления = ПометкаУдаления;
		
	ИначеЕсли Не ЗначениеЗаполнено(ПараметрыЦеннойБумаги) Тогда
		// Создаем новый документ, заполняем его по объекту. Устанавливаем ссылку.
		ИмяДокумента = ЦенныеБумагиКлиентСерверПовтИсп.ПолучитьИмяДокумента(ВидФинансовогоИнструмента);
		Если ЗначениеЗаполнено(ИмяДокумента)Тогда
			МенеджерДокумента = Документы[ИмяДокумента];
			ДокументПараметры = МенеджерДокумента.СоздатьДокумент();
			ДокументПараметры.Заполнить(ЭтотОбъект);
			ПараметрыЦеннойБумаги = МенеджерДокумента.ПолучитьСсылку();
			ДокументПараметры.УстановитьСсылкуНового(ПараметрыЦеннойБумаги);
			ДополнительныеСвойства.Вставить("ПараметрыЦеннойБумаги", ДокументПараметры);
		КонецЕсли;
	Иначе
		// Документ есть, но он не был получен ранее. Синхронизируем только пометку удаления.
		Если ПометкаУдаления <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ПометкаУдаления") Тогда
			ДокументПараметры = ПараметрыЦеннойБумаги.ПолучитьОбъект();
			ДокументПараметры.ПометкаУдаления = ПометкаУдаления;
			ДополнительныеСвойства.Вставить("ПараметрыЦеннойБумаги", ДокументПараметры);
		КонецЕсли;
	КонецЕсли;
	
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ПараметрыЦеннойБумаги") Тогда
		ДополнительныеСвойства.ПараметрыЦеннойБумаги.ФинансовыйИнструмент = Ссылка; // для надежности.
		Если Не ДополнительныеСвойства.ПараметрыЦеннойБумаги.ПометкаУдаления Тогда
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
		ИначеЕсли ДополнительныеСвойства.ПараметрыЦеннойБумаги.Проведен Тогда
			РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения;
		Иначе
			РежимЗаписи = РежимЗаписиДокумента.Запись;
		КонецЕсли;
		ДополнительныеСвойства.ПараметрыЦеннойБумаги.Записать(РежимЗаписи);
	КонецЕсли;
	
КонецПроцедуры

#Область СлужебныеПроцедурыИФункции
Процедура ЗаполнитьРеквизитыСправочникаПоДокументу(ДокументПараметры)
	
	Если ТипЗнч(ДокументПараметры.Ссылка) = Тип("ДокументСсылка.ПараметрыЦеннойБумагиВексель") Тогда
		
		ВалютаНоминала		= ДокументПараметры.ВалютаНоминала;
		Номинал				= ДокументПараметры.Номинал;
		Эмитент				= ДокументПараметры.Векселедатель;
		ДисконтнаяЦБ		= Не ДокументПараметры.ПредусмотренПроцентныйДоход;
		ДатаВыпуска			= ДокументПараметры.ДатаСоставления;
		СрокПогашения		= ДокументПараметры.ДатаОкончанияНачисленияПроцентов;
		ПервыйПолучатель	= ДокументПараметры.ПервыйПолучатель;
		Должник				= ДокументПараметры.Должник;
	ИначеЕсли ТипЗнч(ДокументПараметры.Ссылка) = Тип("ДокументСсылка.ВерсияГрафикаЦеннойБумаги") Тогда
		
	КонецЕсли;
	
КонецПроцедуры
	
#КонецОбласти

#КонецЕсли

// -- УХ_Интеграция