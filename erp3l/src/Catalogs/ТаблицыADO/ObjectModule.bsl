
Перем ТекСоединениеВИБ Экспорт;

Процедура ЗаполнитьРеквизитыОбъекта() Экспорт
	
	Если Не ПроверитьНаименование() Тогда
		
		Возврат;
		
	КонецЕсли;
	
	СтрокаПодключение=УправлениеСоединениямиВИБУХ.ПолучитьСтрокуСоединенияADO(Владелец.ВИБПоУмолчанию,,ИмяФайла);
	
	ПоляТаблицы = УправлениеСоединениямиВИБУХ.ВернутьКолонкиТаблицы(Наименование,СтрокаПодключение, Ложь);
	
	Реквизиты.Очистить();
	
	Для Каждого ПолеТаблицы Из ПоляТаблицы Цикл
		
		РеквизитТаблицы = Реквизиты.Добавить();
		РеквизитТаблицы.Имя = ПолеТаблицы.Имя;
		РеквизитТаблицы.ТипЗначения = ПолеТаблицы.ОписаниеТипов;
		
	КонецЦикла;
	
	ТекТаблицаСвязей = УправлениеСоединениямиВИБУХ.ВернутьТаблицуСвязей(Наименование,СтрокаПодключение);
	СвязанныеТаблицы.Очистить();
	
	Для Каждого СвязаннаяТаблица Из ТекТаблицаСвязей Цикл
		
		ТекущаяТаблица = Справочники.ТаблицыADO.НайтиПоНаименованию(СвязаннаяТаблица.СвязаннаяТаблица, ,Владелец);
		Если ЗначениеЗаполнено(ТекущаяТаблица) Тогда
			НоваяСвязь = СвязанныеТаблицы.Добавить();
			НоваяСвязь.СвязаннаяТаблица = ТекущаяТаблица.Ссылка;
			НоваяСвязь.ПолеТекущейТаблицы = СвязаннаяТаблица.КолонкаТекущейТаблицы;
			НоваяСвязь.ПолеСвязаннойТаблицы = СвязаннаяТаблица.КолонкаСвязаннойТаблицы;
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры

Функция ПроверитьНаименование()
	
	ЕстьОшибки=Ложь;
	
	Если НЕ ЗначениеЗаполнено(Наименование) Тогда
		
		ОбщегоНазначенияУХ.СообщитьОбОшибке(Нстр("ru = 'Для обрабатываемой таблицы соединения по ADO не указано наименование.'"),ЕстьОшибки,,СтатусСообщения.Внимание);
		
	КонецЕсли;
	
	Возврат НЕ ЕстьОшибки;
			
КонецФункции // ПроверитьНаименование()

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если НЕ (ЭтоНовый() ИЛИ Ссылка.Имя=Имя) Тогда
		
		Отказ=НЕ Справочники.ТаблицыADO.ЗаменитьИмяТаблицыADO(Ссылка,Ссылка.Имя,Имя);
		
	КонецЕсли;
	
КонецПроцедуры
