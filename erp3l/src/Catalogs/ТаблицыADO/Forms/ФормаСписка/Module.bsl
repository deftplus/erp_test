
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Отбор.Свойство("Владелец") И ЗначениеЗаполнено(Параметры.Отбор.Владелец) Тогда
		
		ТипБД=Параметры.Отбор.Владелец;
		
		Если НЕ ТипБД.ВерсияПлатформы=Перечисления.ПлатформыВнешнихИнформационныхБаз.ADO Тогда
			
			ОбщегоНазначенияУХ.СообщитьОбОшибке(Нстр("ru = 'Выбранный тип внешней информационной базы не использует подключение по ADO'"),Отказ,,СтатусСообщения.Информация);
			Возврат;
			
		КонецЕсли;
		
		ОбновитьОтборПоВладельцу();
		
		Элементы.ТипБД.Доступность=Ложь;
		
	Иначе
		
		Элементы.ТипБД.Доступность=Истина;
		
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТипыБазДанных.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ТипыБазДанных КАК ТипыБазДанных
		|ГДЕ
		|	ТипыБазДанных.ВерсияПлатформы = ЗНАЧЕНИЕ(Перечисление.ПлатформыВнешнихИнформационныхБаз.ADO)";
		
		Результат=Запрос.Выполнить().Выбрать();
		
		Если Результат.Следующий() Тогда
			
			ТипБД=Результат.Ссылка;
			ОбновитьОтборПоВладельцу();
			
		КонецЕсли;
			
	КонецЕсли;
	
	Элементы.ИмяФайла.Видимость=(ТипБД.ВИБПоУмолчанию.ТипХранилищаДанныхADO=Перечисления.ТипыХранилищДанныхADO.XLS
		ИЛИ ТипБД.ВИБПоУмолчанию.ТипХранилищаДанныхADO=Перечисления.ТипыХранилищДанныхADO.MSAccess);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтборПоВладельцу()
	
	ПолеВладелец = Новый ПолеКомпоновкиДанных("Владелец");
	
	Для Каждого СтрОтбор ИЗ Список.Отбор.Элементы Цикл
		
		Если СтрОтбор.ЛевоеЗначение=ПолеВладелец Тогда
			
			СтрОтбор.ПравоеЗначение=ТипБД;
			
		КонецЕсли;
		
	КонецЦикла;
		
	Элементы.СписокИмпортироватьДанные.Видимость=Истина;
	
КонецПроцедуры // ОбновитьОтборПоВладельцу() 

&НаСервереБезКонтекста
Процедура ЗаполнитьСписокТаблиц(ТипБД)
	
	Справочники.ТаблицыADO.ОбновитьТаблицыADO(ТипБД);
		
КонецПроцедуры // ЗаполнитьСписокСправочников()


/////////////////////////////////////////////////////////////////////////////////

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка=Ложь;
	
	Если Поле.Имя="Ссылка" ИЛИ Поле.Имя="ИмяФайла" Тогда
		
		ОткрытьФорму("Справочник.ТаблицыADO.Форма.ФормаЭлемента", Новый Структура("Ключ",Элементы.Список.ТекущиеДанные.Ссылка));
		
	ИначеЕсли Поле.Имя="Соответствие" Тогда
		
		ТекСоответствие=Элементы.Список.ТекущиеДанные.Соответствие;
		
		Если НЕ ЗначениеЗаполнено(ТекСоответствие) Тогда
			
			СтруктураСоответствие=Новый Структура;
			СтруктураСоответствие.Вставить("ОписаниеОбъектаВИБ",Элементы.Список.ТекущиеДанные.Ссылка);
			СтруктураСоответствие.Вставить("Владелец",ТипБД);
			СтруктураСоответствие.Вставить("ТипОбъектаВИБ","ТаблицаADO");
			
			ПараметрыФормы = Новый Структура("ЗначенияЗаполнения",СтруктураСоответствие);
			Оповещение = Новый ОписаниеОповещения("Подключаемый_ОбновитьСписок", ЭтотОбъект);
			ОткрытьФорму("Справочник.СоответствиеВнешнимИБ.ФормаОбъекта", 
							ПараметрыФормы, ЭтаФорма,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
		Иначе

			ОткрытьФорму("Справочник.СоответствиеВнешнимИБ.ФормаОбъекта",Новый Структура("Ключ",ТекСоответствие));
			
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьСписок(Результат, ДополнительныеПараметры) Экспорт
    
    Элементы.Список.Обновить();

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИЗADO(Команда)
	
	Если НЕ ЗначениеЗаполнено(ТипБД) Тогда
		
		ПоказатьПредупреждение(, Нстр("ru = 'Не указан тип внешней информационной базы.'"));
		Возврат;
		
	Иначе
		
		ЗаполнитьСписокТаблиц(ТипБД);
		Элементы.Список.Обновить();
		
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура ТипБДПриИзменении(Элемент)
	
	ОбновитьОтборПоВладельцу();
	
КонецПроцедуры

&НаКлиенте
Процедура ИмпортироватьДанные(Команда)
	
	Если Элементы.Список.ТекущиеДанные=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("Обработка.ОбменНСИ.Форма.Форма",Новый Структура("НастройкаСоответствия,ИмпортДанных",Элементы.Список.ТекущиеДанные.Соответствие,Истина));
	
КонецПроцедуры

&НаКлиенте
Процедура ТипБДНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка=Ложь;
	
	СтруктураОтбора=Новый Структура;
	СтруктураОтбора.Вставить("ВерсияПлатформы",ПредопределенноеЗначение("Перечисление.ПлатформыВнешнихИнформационныхБаз.ADO"));
	
	ОткрытьФорму("Справочник.ТипыБазДанных.ФормаВыбора",Новый Структура("Отбор",СтруктураОтбора),Элемент);
		
КонецПроцедуры

&НаКлиенте
Процедура ПросмотрДанных(Команда)
	
	Если Элементы.Список.ТекущиеДанные=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПараметров=Новый Структура;
	СтруктураПараметров.Вставить("ТаблицаADO",Элементы.Список.ТекущиеДанные.Ссылка);
	СтруктураПараметров.Вставить("ТипБД",ТипБД);
	СтруктураПараметров.Вставить("РежимВыбора",Ложь);
	
	ОткрытьФорму("ОбщаяФорма.ФормаПросмотраТаблицыADO",СтруктураПараметров);
	
КонецПроцедуры
