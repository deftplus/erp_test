
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НаименованиеСправочника=Параметры.НаименованиеСправочника;
	Использование77=Параметры.Использование77;
	
	ИспользуемаяИБ=Параметры.ТипБД.ВИБПоУмолчанию;
	
	ТипМетаДанных=Параметры.ТипМетаДанных;
	СправочникБД=Параметры.СправочникБД;
	
	СтруктураПараметров=Новый Структура;
	СтруктураПараметров.Вставить("Использование77",			Параметры.Использование77);
	СтруктураПараметров.Вставить("Код",						Параметры.Код);
	СтруктураПараметров.Вставить("Наименование",			Параметры.Наименование);
	СтруктураПараметров.Вставить("НаименованиеСправочника",	Параметры.НаименованиеСправочника);
	СтруктураПараметров.Вставить("ТипБД",					Параметры.ТипБД);
	СтруктураПараметров.Вставить("ТипМетаДанных",			Параметры.ТипМетаДанных);
	СтруктураПараметров.Вставить("СправочникБД",			Параметры.СправочникБД);
	СтруктураПараметров.Вставить("ВнешняяИнформационнаяБаза",Параметры.ВнешняяИнформационнаяБаза);
		
	Если ТипМетаДанных = "Справочник" ИЛИ ТипМетаДанных="ПланСчетов" ИЛИ ТипМетаДанных="ПланВидовХарактеристик"  Тогда
		
		ДанныеСправочника = РасширениеИнтеграцииУХ.ЗаполнитьДеревоЗначенийДляСправочникаВИБ(СтруктураПараметров);
		
	ИначеЕсли ТипМетаДанных = "Перечисление" Тогда
		
		ДанныеСправочника = РасширениеИнтеграцииУХ.ЗаполнитьДеревоЗначенийДляПеречисленияВИБ(СтруктураПараметров);
		
	КонецЕсли;
	
	ДеревоЗначений=ДанныеСправочника.ДеревоЗначений;
	КодНеИспользуется=ДанныеСправочника.КодНеИспользуется;
		
	Заголовок=ТипМетаДанных+": "+СправочникБД;
	
	АдресДереваОбъектов=ПоместитьВоВременноеХранилище(ДеревоЗначений,ЭтаФорма.УникальныйИдентификатор);
	
	Для Каждого ТекЭлемент ИЗ Параметры.ТекСписокЗначений Цикл
		НоваяСтрока=ТабличноеПолеЗначенияВИБ.Добавить();
		НоваяСтрока.Код=ТекЭлемент.Значение;
		НоваяСтрока.Наименование=ТекЭлемент.Представление;
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьРедактирование(Команда)
	
	СписокОбъектов=Новый СписокЗначений;
	
	Для Каждого Строка Из ТабличноеПолеЗначенияВИБ Цикл
		
		СписокОбъектов.Добавить(Строка.Код, Строка.Наименование, НЕ КодНеИспользуется);
		
	КонецЦикла;

	Закрыть(СписокОбъектов);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка=Ложь;
	
	СтруктураДанные=Новый Структура;
	СтруктураДанные.Вставить("НаименованиеСправочника",НаименованиеСправочника);
	СтруктураДанные.Вставить("АдресДереваОбъектов",АдресДереваОбъектов);
	СтруктураДанные.Вставить("Код",Элементы.ТабличноеПолеЗначенияВИБ.ТекущиеДанные.Код);
	СтруктураДанные.Вставить("Наименование",Элементы.ТабличноеПолеЗначенияВИБ.ТекущиеДанные.Наименование);
	СтруктураДанные.Вставить("ТипМетаДанных",ТипМетаДанных);
	СтруктураДанные.Вставить("КодНеИспользуется",КодНеИспользуется);
	
	ДанныеОбъектаВИБ = Неопределено;

	Оповещение = Новый ОписаниеОповещения("НаименованиеНачалоВыбораЗавершение", ЭтотОбъект);
	ОткрытьФорму("Справочник.ИсточникиДанныхДляРасчетов.Форма.ФормаВыбораЗначенийВИБ", 
					СтруктураДанные,,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
		
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    ДанныеОбъектаВИБ=Результат;
    
    Если ТипЗнч(ДанныеОбъектаВИБ)=Тип("СписокЗначений") Тогда
        
        Элементы.ТабличноеПолеЗначенияВИБ.ТекущиеДанные.Код = ДанныеОбъектаВИБ[0].Значение;
        Элементы.ТабличноеПолеЗначенияВИБ.ТекущиеДанные.Наименование = ДанныеОбъектаВИБ[0].Представление;
        
    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПодборЭлементов(Команда)
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("НаименованиеСправочника",	НаименованиеСправочника);
	СтруктураДанные.Вставить("АдресДереваОбъектов",		АдресДереваОбъектов);
	СтруктураДанные.Вставить("ТипМетаДанных",			ТипМетаДанных);
	СтруктураДанные.Вставить("СправочникБД",			СправочникБД);
	
	Если НЕ Элементы.ТабличноеПолеЗначенияВИБ.ТекущиеДанные = Неопределено Тогда
		
		СтруктураДанные.Вставить("Код",				Элементы.ТабличноеПолеЗначенияВИБ.ТекущиеДанные.Код);
		СтруктураДанные.Вставить("Наименование",	Элементы.ТабличноеПолеЗначенияВИБ.ТекущиеДанные.Наименование);
		
	КонецЕсли;

	СтруктураДанные.Вставить("НеЗакрыватьПриВыборе", Истина);
	
	ОткрытьФорму("Справочник.ИсточникиДанныхДляРасчетов.Форма.ФормаВыбораЗначенийВИБ",
					СтруктураДанные,,,,,, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение)=Тип("СписокЗначений") Тогда
		
		СтруктураПоиска=Новый Структура;
		СтруктураПоиска.Вставить("Код",ВыбранноеЗначение[0].Значение);
		СтруктураПоиска.Вставить("Наименование",ВыбранноеЗначение[0].Представление);
		
		МассивСуществующих=ТабличноеПолеЗначенияВИБ.НайтиСтроки(СтруктураПоиска);
		
		Если МассивСуществующих.Количество()=0 Тогда
			
			НоваяСтрока=ТабличноеПолеЗначенияВИБ.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,СтруктураПоиска);
			
		КонецЕсли;
		
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия="ВыбранЭлементВИБ" Тогда
		
		СтруктураПоиска=Новый Структура;
			
		СтруктураПоиска.Вставить("Код",Параметр[0].Значение);
		СтруктураПоиска.Вставить("Наименование",Параметр[0].Представление);
			
		МассивСуществующих=ТабличноеПолеЗначенияВИБ.НайтиСтроки(СтруктураПоиска);
		
		Если МассивСуществующих.Количество()=0 Тогда
			
			НоваяСтрока=ТабличноеПолеЗначенияВИБ.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,СтруктураПоиска);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

