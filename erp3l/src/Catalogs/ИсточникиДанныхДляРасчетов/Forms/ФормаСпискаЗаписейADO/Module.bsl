
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипБД=Параметры.ТипБД;
	
	ТаблицаADO=Справочники.ТаблицыADO.НайтиПоНаименованию(Параметры.СвязаннаяТаблица,Истина,,ТипБД);
	
	Если Не ЗначениеЗаполнено(ТаблицаADO) Тогда
		
		Справочники.ТаблицыADO.ОбновитьТаблицыADO(ТипБД);
		ТаблицаADO=Справочники.ТаблицыADO.НайтиПоНаименованию(Параметры.СвязаннаяТаблица,Истина,,ТипБД);
		
		Если Не ЗначениеЗаполнено(ТаблицаADO) Тогда
			
			СтрокаШаблона = НСтр("ru = 'Не найдена таблица %1 в подключении %2'");
			
			Если Не ПустаяСтрока(СтрокаШаблона) тогда
				ОбщегоНазначенияУХ.СообщитьОбОшибке(СтрШаблон(СтрокаШаблона, Параметры.СвязаннаяТаблица, ТипБД),,, СтатусСообщения.Внимание);
			КонецЕсли;
						
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;

	ПодготовитьТаблицуДляЗаполнения();
	
	Если ТипЗнч(Параметры.СписокСтрок)=Тип("СписокЗначений") Тогда
		
		Для Каждого Строка ИЗ Параметры.СписокСтрок Цикл
			
			НоваяСтрока=ДанныеТаблицы.Добавить();
			
			Для Каждого Колонка Из СписокКолонок Цикл
				
				НоваяСтрока[Колонка.Значение]=Строка.Значение["["+Колонка.Представление+"]"];
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
			
КонецПроцедуры

&НаСервере
Процедура ПодготовитьТаблицуДляЗаполнения()
	
	РеквизитыКДобавлению=Новый Массив;
	РеквизитыКУдалению=Новый Массив;
	
	Если СписокКолонок.Количество()>0 Тогда
		
		Для Каждого Колонка ИЗ СписокКолонок Цикл	
			
			РеквизитыКУдалению.Добавить("ДанныеТаблицы."+Колонка.Значение);	
			ЭтаФорма.Элементы.Удалить(ЭтаФорма.Элементы[Колонка.Значение]);
			
		КонецЦикла;
		
		СписокКолонок.Очистить();
				
	КонецЕсли;
	
	Для Каждого Колонка ИЗ ТаблицаADO.Реквизиты Цикл
		
		ТекущийТип=ОбщегоНазначенияУХ.ПреобразоватьТипИзСтроки(Колонка.ТипЗначения);
		
		Если ТекущийТип=Неопределено Тогда
			ТекущийТип=Тип("Строка");
		КонецЕсли;
		
		МассивТипов=Новый Массив;
		МассивТипов.Добавить(ТекущийТип);
		
		Попытка
			
			Если СписокКолонок.НайтиПоЗначению(Колонка.ВнутреннееПредставление)=Неопределено Тогда
				
				СписокКолонок.Добавить(Колонка.ВнутреннееПредставление,Колонка.Имя);
				
				РеквизитыКДобавлению.Добавить(Новый РеквизитФормы(Колонка.ВнутреннееПредставление,
				Новый ОписаниеТипов(МассивТипов),
				"ДанныеТаблицы",
				Колонка.Имя,
				Ложь));
				
			КонецЕсли;
			
		Исключение
			
		КонецПопытки;
		
	КонецЦикла;
	
	ИзменитьРеквизиты(РеквизитыКДобавлению,РеквизитыКУдалению);
	
	Для Каждого Колонка ИЗ ТаблицаADO.Реквизиты Цикл
		
		ТекущийТип=ОбщегоНазначенияУХ.ПреобразоватьТипИзСтроки(Колонка.ТипЗначения);
		
		Если ТекущийТип=Неопределено Тогда
			ТекущийТип=Тип("Строка");
		КонецЕсли;
		
		МассивТипов=Новый Массив;
		МассивТипов.Добавить(ТекущийТип);
		
		Если ЭтаФорма.Элементы.Найти(Колонка.ВнутреннееПредставление)=Неопределено Тогда
			
			Попытка
				
				ЭтаФорма.Элементы.Добавить(Колонка.ВнутреннееПредставление,Тип("ПолеФормы"),ЭтаФорма.Элементы.ДанныеТаблицы);	
				ЭтаФорма.Элементы[Колонка.ВнутреннееПредставление].ПутьКДанным="ДанныеТаблицы."+Колонка.ВнутреннееПредставление;
				ЭтаФорма.Элементы[Колонка.ВнутреннееПредставление].Вид=ВидПоляФормы.ПолеВвода;
				ЭтаФорма.Элементы[Колонка.ВнутреннееПредставление].Заголовок=Колонка.Имя;
				ЭтаФорма.Элементы[Колонка.ВнутреннееПредставление].ТолькоПросмотр=Истина;
				ЭтаФорма.Элементы[Колонка.ВнутреннееПредставление].ДоступныеТипы=Новый ОписаниеТипов(МассивТипов);
				
			Исключение
				
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;
			
КонецПроцедуры // ПодготовитьТаблицуДляЗаполнения()

&НаКлиенте
Процедура ДанныеТаблицыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ=Истина;
	СтруктураПараметров=Новый Структура;
	СтруктураПараметров.Вставить("ТаблицаADO",ТаблицаADO);
	СтруктураПараметров.Вставить("ТипБД",ТипБД);
	СтруктураПараметров.Вставить("РежимВыбора",Истина);
	СтруктураПараметров.Вставить("МножественныйВыбор",Истина);
	
	СписокСтрок = Неопределено;

	Оповещение = Новый ОписаниеОповещения("ДанныеТаблицыПередНачаломДобавленияЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ФормаПросмотраТаблицыADO", СтруктураПараметров,,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
		
КонецПроцедуры

&НаКлиенте
Процедура ДанныеТаблицыПередНачаломДобавленияЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    СписокСтрок=Результат;
    
    Если ТипЗнч(СписокСтрок)=Тип("СписокЗначений") Тогда
        
        Для Каждого Строка Из СписокСтрок Цикл
            
            СтруктураПоиска=Новый Структура;
            
            Для Каждого Колонка ИЗ СписокКолонок Цикл
                
                СтруктураПоиска.Вставить(Колонка.Значение,Строка.Значение["["+Колонка.Представление+"]"]);
                
            КонецЦикла;
            
            МассивСтрок=ДанныеТаблицы.НайтиСтроки(СтруктураПоиска);
            
            Если МассивСтрок.Количество()=0 Тогда
                
                НоваяСтрока=ДанныеТаблицы.Добавить();
                ЗаполнитьЗначенияСвойств(НоваяСтрока,СтруктураПоиска);
                
            КонецЕсли;
            
        КонецЦикла;
        
    КонецЕсли;

КонецПроцедуры
 
&НаКлиенте
Процедура Записать(Команда)
	
	СписокСтрок=Новый СписокЗначений;
	
	Для Каждого Строка ИЗ ДанныеТаблицы Цикл
		
		ДанныеСтроки=Новый Соответствие;
		
		Для Каждого Колонка ИЗ СписокКолонок Цикл
			
			ДанныеСтроки.Вставить("["+Колонка.Представление+"]",Строка[Колонка.Значение]);
			
		КонецЦикла;
		
		СписокСтрок.Добавить(ДанныеСтроки);
		
	КонецЦикла;
	
	Оповестить("ИзмененОтборЗаписейADO",СписокСтрок);
	Закрыть();
						
КонецПроцедуры
