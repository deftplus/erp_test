&НаКлиенте
Процедура ИзменитьРазделИнвестПрограммыПроекта(Проект, Раздел=Неопределено)
	
	мПроекты = ПроектыДляОтмены.НайтиСтроки(Новый Структура("Проект", Проект));
	Если мПроекты.Количество() = 0 Тогда
		// регистрируем первоначальный раздел, для возможной отмены
		НоваяСтрока = ПроектыДляОтмены.Добавить();
		НоваяСтрока.Проект = Проект;
	Иначе
		НоваяСтрока = Неопределено;
	КонецЕсли;
	
	СтарыйРаздел = ИзменитьРазделИнвестПрограммыПроектаНаСервере(Проект, Раздел);
	
	Если НоваяСтрока <> Неопределено Тогда
		НоваяСтрока.ИсходныйРаздел = СтарыйРаздел;
	КонецЕсли;
	
	Элементы.ПроектыДляВыбора.Обновить();
	Элементы.ВыбранныеПроекты.Обновить();
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИзменитьРазделИнвестПрограммыПроектаНаСервере(Проект, РазделИнвестПрограммы)
	
	СтарыйРаздел = Проект.РазделИнвестиционнойПрограммы;
	
	ПроектОбъект = Проект.ПолучитьОбъект();
	ПроектОбъект.РазделИнвестиционнойПрограммы = РазделИнвестПрограммы;
	ПроектОбъект.Записать();
	
	Возврат СтарыйРаздел;
КонецФункции

&НаКлиенте
Процедура ПринятьИзменения(Команда)
	ЭтаФорма.Закрыть();
КонецПроцедуры

&НаСервере
Процедура ОтменитьИЗакрытьНаСервере()
	Для Каждого Строка_ Из ПроектыДляОтмены Цикл
		ИзменитьРазделИнвестПрограммыПроектаНаСервере(Строка_.Проект, Строка_.ИсходныйРаздел)
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьИЗакрыть(Команда)
	ОтменитьИЗакрытьНаСервере();
	ЭтаФорма.Закрыть();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	РазделИнвестиционнойПрограммы = Параметры.РазделИнвестПрограммы;
	Если НЕ ЗначениеЗаполнено(РазделИнвестиционнойПрограммы) Тогда 
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(ВыбранныеПроекты, "РазделИнвестиционнойПрограммы", РазделИнвестиционнойПрограммы, Истина);
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(ПроектыДляВыбора, "РазделИнвестиционнойПрограммы", Справочники.РазделыИнвестиционныхПрограмм.ПустаяСсылка(), Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеПроектыПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	ИзменитьРазделИнвестПрограммыПроекта(ПараметрыПеретаскивания.Значение[0], РазделИнвестиционнойПрограммы);
КонецПроцедуры

&НаКлиенте
Процедура ПроектыДляВыбораПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	ИзменитьРазделИнвестПрограммыПроекта(ПараметрыПеретаскивания.Значение[0], Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ПроектыДляВыбораВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ТекДанные = Элементы.ПроектыДляВыбора.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьРазделИнвестПрограммыПроекта(ТекДанные.Ссылка, РазделИнвестиционнойПрограммы);
КонецПроцедуры

