
&НаКлиенте
Процедура ОткрытьОтчет(Команда)
	
	ТекСтрока = Элементы.Список.ТекущиеДанные;
	Если ТекСтрока <> Неопределено Тогда
		// Проверим, что выбрана не группа, и откроем форму выбранного отчета.
		Если НЕ ТекСтрока.ЭтоГруппа Тогда
			Если ТекСтрока.ВидПроизвольногоОтчета = 0 
				ИЛИ ТекСтрока.ВидПроизвольногоОтчета = 1 Тогда
				СтруктураПараметровФормы = Новый Структура;
				СтруктураПараметровФормы.Вставить("Ключ", ТекСтрока.Ссылка);
				СтруктураПараметровФормы.Вставить("ФормироватьПриОткрытии", Истина);
				ОткрытьФорму("Справочник.ПроизвольныеОтчеты.Форма.ФормаОтображенияОтчетаУправляемая", СтруктураПараметровФормы);	
			Иначе
				ПараметрыФормы = Новый Структура("Бланк,Ключ",ТекСтрока.БланкИсточник,ТекСтрока.Ссылка);	   		
				ОткрытьФорму("Обработка.АналитическийБланк.Форма.ФормаОтчета", ПараметрыФормы,,Истина);		
			КонецЕсли;		
		Иначе
			ТекстСообщения = НСтр("ru = 'Невозможно открыть группу отчетов. Операция отменена.'");
			ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Владелец = ОбщегоНазначенияУХ.ПолучитьЗначениеПеременной("глТекущийПользователь");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	МоиОтчеты = Ложь;
	УстановитьОтборПоОтчетамПользователя();
	
КонецПроцедуры

Процедура УстановитьОтборПоОтчетамПользователя()
	
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить(Владелец);
	СписокЗначений.Добавить(ПредопределенноеЗначение("Справочник.ГруппыПользователей.ВсеПользователи"));
	ТиповыеОтчетыУХ.ДобавитьОтбор(Список.Отбор, Новый ПолеКомпоновкиДанных("Владелец"), СписокЗначений, ВидСравненияКомпоновкиДанных.ВСписке);
	
КонецПроцедуры

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	
	УстановитьОтборПоОтчетамПользователя();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовуюАналитическуюПанель(Команда)
	
	СписокОтчетов = новый СписокЗначений;
	
	Для Каждого ВыделенныйЭлемент Из Элементы.Список.ВыделенныеСтроки Цикл
		
		СписокОтчетов.Добавить(ВыделенныйЭлемент);
		
	КонецЦикла;
	
	ОткрытьФорму("Справочник.ПанелиОтчетов.Форма.ФормаНастройки_Управляемая", Новый Структура("СписокНачальногоЗаполнения", СписокОтчетов));
	
КонецПроцедуры
