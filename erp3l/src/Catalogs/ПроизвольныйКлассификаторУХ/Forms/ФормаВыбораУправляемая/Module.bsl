
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ОтборыВСписке) Тогда
		Для Каждого Элемент Из Параметры.ОтборыВСписке Цикл
			НовыйОтбор = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			НовыйОтбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(Элемент.Ключ);
			НовыйОтбор.ПравоеЗначение = Новый СписокЗначений;
			
			НовыйОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
			
			Для Каждого ЭлементОтбора Из Элемент.Значение Цикл
				НовыйОтбор.ПравоеЗначение.Добавить(ЭлементОтбора);
			КонецЦикла;
			НовыйОтбор.Использование = Истина;
		КонецЦикла;
	КонецЕсли;
	
	ОтборСчетИсточник = Неопределено;
	Если (Параметры.ОтборНомерСубконто > 0) И Параметры.Отбор.Свойство("ОтборСчетИсточник", ОтборСчетИсточник) И ЗначениеЗаполнено(ОтборСчетИсточник) Тогда
		
		СвойстваСчета = УчетМСФОПовтИспУХ.ПолучитьСвойстваСчета(ОтборСчетИсточник);		
		Владелец = СвойстваСчета["ВидСубконто" + Параметры.ОтборНомерСубконто];
		
		Параметры.Отбор.Вставить("Владелец", Владелец);
		ТиповыеОтчетыУХ.ДобавитьОтбор(Список.Отбор, Новый ПолеКомпоновкиДанных("Владелец"), Владелец, ВидСравненияКомпоновкиДанных.Равно);
		
	КонецЕсли;
	
	Параметры.Отбор.Свойство("Владелец", Владелец);	
	// Если отбор по владельцу передан из внешней формы, заблокируем изменение отбора.
	Если ЗначениеЗаполнено(Владелец) Тогда
		Элементы.Владелец.ТолькоПросмотр = Истина;
	Иначе
		// Не изменяем доступность.
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьОтборПоВладельцу();
	
КонецПроцедуры

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	
	УстановитьОтборПоВладельцу();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ФУНКЦИИ.
//
&НаКлиенте
Процедура УстановитьОтборПоВладельцу()
	
	Если НЕ Владелец.Пустая() Тогда
		ТиповыеОтчеты_КлиентУХ.ДобавитьОтбор(Список.Отбор, "Владелец", Владелец);
	Иначе
		ТиповыеОтчеты_КлиентУХ.УдалитьОтбор(Список.Отбор, "Владелец");
	КонецЕсли;
	
КонецПроцедуры
