
#Область ОбработкаОсновныхСобытийФормы


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПриСозданииЧтенииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ПриСозданииЧтенииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.РеквизитыДляКонтроля.Загрузить(РеквизитыДляКонтроля.Выгрузить());
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ПриСозданииЧтенииНаСервере();
	Элементы.СтраницаОписания.Картинка =
		ОбщегоНазначенияКлиентСервер.КартинкаКомментария(Объект.Описание);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ПриИзмененииНазначенияВНА",, Объект.Ссылка);
КонецПроцедуры


#КонецОбласти

#Область ОбработкаСобытийЭлементовФормы

&НаКлиенте
Процедура КлассВНАПриИзменении(Элемент)
	ПриСозданииЧтенииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыНаКлиенте


#КонецОбласти

#Область СлужебныеПроцедурыНаСервере

// Обработчик при создании и чтении на сервере
//
&НаСервере
Процедура ПриСозданииЧтенииНаСервере()
	ЗаполнитьРеквизитыДляКонтроля(Объект.КлассВНА, Объект.РеквизитыДляКонтроля, РеквизитыДляКонтроля);
КонецПроцедуры

// Заполняет таблицу ТЗРеквизитыДляКонтроля реквизитов
// для контроля ВНА.
//
&НаСервереБезКонтекста
Процедура ЗаполнитьРеквизитыДляКонтроля(КлассВНА,
				ТЧРеквизитыДляКонтроля, ТЗРеквизитыДляКонтроля)
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(НастройкиМетаданных.ИмяРеквизита КАК СТРОКА(255)) КАК ИмяРеквизита,
		|	ВЫРАЗИТЬ(НастройкиМетаданных.Синоним КАК СТРОКА) КАК СинонимРеквизита,
		|	ВЫРАЗИТЬ(НастройкиМетаданных.Видимость КАК БУЛЕВО) КАК Видимость
		|ПОМЕСТИТЬ НастройкиМетаданных
		|ИЗ
		|	&НастройкиМетаданных КАК НастройкиМетаданных
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(НастройкаТекущие.ИмяРеквизита КАК СТРОКА(255)) КАК ИмяРеквизита,
		|	ВЫРАЗИТЬ(НастройкаТекущие.Видимость КАК БУЛЕВО) КАК Видимость
		|ПОМЕСТИТЬ НастройкиТекущие
		|ИЗ
		|	&НастройкиТекущие КАК НастройкаТекущие
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НастройкиМетаданных.ИмяРеквизита КАК ИмяРеквизита,
		|	НастройкиМетаданных.СинонимРеквизита КАК СинонимРеквизита,
		|	ЕСТЬNULL(НастройкиТекущие.Видимость, НастройкиМетаданных.Видимость) КАК Видимость
		|ИЗ
		|	НастройкиМетаданных КАК НастройкиМетаданных
		|		ЛЕВОЕ СОЕДИНЕНИЕ НастройкиТекущие КАК НастройкиТекущие
		|		ПО НастройкиМетаданных.ИмяРеквизита = НастройкиТекущие.ИмяРеквизита");
	
	Запрос.УстановитьПараметр(
		"НастройкиТекущие",
		ТЧРеквизитыДляКонтроля.Выгрузить(,"ИмяРеквизита, Видимость"));
	Запрос.УстановитьПараметр(
		"НастройкиМетаданных",
		Справочники.НазначенияВНА.ЭлементыФормыВНАДляУправленияВидимостью(КлассВНА));
	
	Выборка = Запрос.Выполнить().Выбрать();
	ТЗРеквизитыДляКонтроля.Очистить();
	
	Пока Выборка.Следующий() Цикл
		СтрокаРеквизита = ТЗРеквизитыДляКонтроля.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаРеквизита, Выборка);
		
		СтрокаРеквизита.ИмяРеквизита = СокрЛП(Выборка.ИмяРеквизита);
	КонецЦикла;
КонецПроцедуры


#КонецОбласти

