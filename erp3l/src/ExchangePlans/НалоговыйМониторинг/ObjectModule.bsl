
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Перем БезусловноВыгружаемыеМетаданные;
Перем МассивПолныхОграничений;
Перем СоответствиеОграниченийДанных;

#Область ОбработчикиСобытий

Процедура ПриОтправкеДанныхПодчиненному(ЭлементДанных, ОтправкаЭлемента, СозданиеНачальногоОбраза)
	
	НалоговыйМониторинг.ПриОтправкеДанныхПодчиненному(ЭтотОбъект, ЭлементДанных, ОтправкаЭлемента, СозданиеНачальногоОбраза);
	
	Если ОтправкаЭлемента = ОтправкаЭлементаДанных.Игнорировать Тогда
		Возврат;
	КонецЕсли;
	
	Если МассивПолныхОграничений = Неопределено Тогда
		ЗаполнитьОграничения(СписокНеВыгружаемыхРеквизитов, МассивПолныхОграничений, СоответствиеОграниченийДанных);
	КонецЕсли;
	
	ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(ЭлементДанных));
	
	Если ОбъектМетаданных = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ПолноеИмя = ОбъектМетаданных.ПолноеИмя();
	Если МассивПолныхОграничений.Найти(ПолноеИмя) <> Неопределено Тогда
		ОтправкаЭлемента = ОтправкаЭлементаДанных.Игнорировать;
		Возврат;
	КонецЕсли;
	
	МассивОграниченийДанных = СоответствиеОграниченийДанных.Получить(ПолноеИмя);
	Если МассивОграниченийДанных <> Неопределено Тогда
		Для каждого Ограничение Из МассивОграниченийДанных Цикл
			МассивПодстрок = СтрРазделить(Ограничение, ".");
			Если МассивПодстрок[0] = "Справочник" ИЛИ МассивПодстрок[0] = "Документ" Тогда
				Если МассивПодстрок[2] = "Реквизиты" Тогда
					ЭлементДанных[МассивПодстрок[3]] = Неопределено;
				ИначеЕсли МассивПодстрок[2] = "ТабличныеЧасти" Тогда
					Если МассивПодстрок.Количество() = 5 Тогда
						Для каждого Стр Из ЭлементДанных[МассивПодстрок[3]] Цикл
							Стр[МассивПодстрок[4]] = Неопределено;
						КонецЦикла;
					Иначе
						ЭлементДанных[МассивПодстрок[3]].Очистить();
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли МассивПодстрок[0] = "РегистрСведений" ИЛИ МассивПодстрок[0] = "РегистрНакопления" ИЛИ МассивПодстрок[0] = "РегистрБухгалтерии" Тогда
				Если МассивПодстрок[2] = "Реквизиты" ИЛИ МассивПодстрок[2] = "Ресурсы" Тогда
					Для каждого ЭлементНабора Из ЭлементДанных Цикл
						ЭлементНабора[МассивПодстрок[3]] = Неопределено;
					КонецЦикла;
				КонецЕсли;
			Иначе
				// 
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОтправкеДанныхУзлаПодчиненному(ЭлементДанных, Игнорировать)
	Если ЭлементДанных.Ссылка <> ПланыОбмена.НалоговыйМониторинг.ЭтотУзел() Тогда
		ЭлементДанных.ДатаВыгрузки = ТекущаяДата();
	КонецЕсли;
КонецПроцедуры

Процедура ПриПолученииДанныхОтГлавного(ЭлементДанных, ПолучениеЭлемента, ОтправкаНазад)
	Если ПланыОбмена.НалоговыйМониторинг.ЭтотУзел().РегистрироватьИзменения Тогда
		НомерВыгрузки = ПланыОбмена.НалоговыйМониторинг.ЭтотУзел().НомерПринятого; 
		ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(ЭлементДанных));
		Если Метаданные.Справочники.Содержит(ОбъектМетаданных) Тогда
			ТекстТипа = "Справочник: " + ОбъектМетаданных.Представление()
		ИначеЕсли Метаданные.Документы.Содержит(ОбъектМетаданных) Тогда
			ТекстТипа = "Документ: " + ОбъектМетаданных.Представление()
		Иначе
			Возврат;
		КонецЕсли;
		НовыйМенеджер = РегистрыСведений.ИзмененияОбъектовНалоговогоМониторинга.СоздатьМенеджерЗаписи();
		НовыйМенеджер.Объект = ЭлементДанных.Ссылка;
		НовыйМенеджер.НомерВыгрузки = НомерВыгрузки;
		НовыйМенеджер.Записать(Истина);
	КонецЕсли;
КонецПроцедуры

Процедура ПриПолученииДанныхОтПодчиненного(ЭлементДанных, ПолучениеЭлемента, ОтправкаНазад)
	Если ЭтотОбъект.ПравилаПолученияИзмененийНаГлавномУзле = 0 Тогда
		ПолучениеЭлемента = ПолучениеЭлементаДанных.Игнорировать;
	ИначеЕсли ЭтотОбъект.ПравилаПолученияИзмененийНаГлавномУзле = 1 Тогда
		МассивОбъектов = НалоговыйМониторингВызовСервераПовтИсп.ПолучитьМассивОбъектовИсключенийДляРегистрацииОбмена();
		ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(ЭлементДанных));
		Если МассивОбъектов.Найти(ОбъектМетаданных) = Неопределено Тогда
			ПолучениеЭлемента = ПолучениеЭлементаДанных.Игнорировать;
		КонецЕсли;
	ИначеЕсли ЭтотОбъект.ПравилаПолученияИзмененийНаГлавномУзле = 2 Тогда
		ПолучениеЭлемента = ПолучениеЭлементаДанных.Авто;
	Иначе
		ВызватьИсключение "Неизвестное правило обмена";
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьОграничения(ТаблицаОграничений, Полные, ПоРеквизитам)
	Полные = Новый Массив;
	ПоРеквизитам = Новый Соответствие;
	Для каждого Строка Из ТаблицаОграничений Цикл
		МассивПодстрок = СтрРазделить(Строка.НеВыгружаемыйРеквизит, ".");
		Если МассивПодстрок.Количество() > 2 Тогда
			Ключ = МассивПодстрок[0] + "." + МассивПодстрок[1];
			Значение = ПоРеквизитам.Получить(Ключ);
			Если Значение = Неопределено Тогда
				Значение = Новый Массив;
			КонецЕсли;
			Значение.Добавить(Строка.НеВыгружаемыйРеквизит);
			ПоРеквизитам.Вставить(Ключ, Значение);
		Иначе
			Полные.Добавить(Строка.НеВыгружаемыйРеквизит);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#КонецЕсли
