
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	График = ДействующийГрафик(ПараметрКоманды);
	
	Если ЗначениеЗаполнено(График) Тогда
		
		ПоказатьЗначение(,График);
		
	Иначе
		
		ПараметрыФормы = СтруктураПараметровПредметаРасчетов(ПараметрКоманды);
		Если ПараметрыФормы.Свойство("ИмяФормыОткрытия") Тогда
			ОткрытьФорму(ПараметрыФормы.ИмяФормыОткрытия, ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, 
				ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка);
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Данный вид договора не предусматривает ведение графика'"));
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ДействующийГрафик(ПредметРасчетов)
	
	Возврат РаботаСДоговорамиКонтрагентовУХ.ДействующийГрафик(ПредметРасчетов)
	
КонецФункции

&НаСервере
Функция СтруктураПараметровПредметаРасчетов(ПредметРасчетов)
	
	Результат = Новый Структура;
	
	Если ТипЗнч(ПредметРасчетов) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		РеквизитыОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПредметРасчетов,"ВидДоговораУХ,Организация");

		ИмяДокумента = УправлениеДоговорамиУХКлиентСерверПовтИсп.ПолучитьИмяДокументаПоВидуДоговора(РеквизитыОбъекта.ВидДоговораУХ);
		Если ИмяДокумента <> Неопределено Тогда
			Результат.Вставить("ИмяФормыОткрытия", СтрШаблон("Документ.%1.ФормаОбъекта", ИмяДокумента)); 
		КонецЕсли;
	
		Если УправлениеДоговорамиУХКлиентСерверПовтИсп.ЭтоДоговорФинансовогоИнструмента(РеквизитыОбъекта.ВидДоговораУХ) Тогда
			Результат.Вставить("ЗначенияЗаполнения", Новый Структура("ФинансовыйИнструмент,Организация", ПредметРасчетов, РеквизитыОбъекта.Организация));
		Иначе
			Результат.Вставить("ЗначенияЗаполнения", Новый Структура("ОбъектРасчетов,Организация", ПредметРасчетов, РеквизитыОбъекта.Организация));
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

