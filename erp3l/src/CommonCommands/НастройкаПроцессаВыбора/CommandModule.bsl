
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	Если НЕ ЗначениеЗаполнено(ПараметрКоманды) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Объект не записан. Команда не может быть выполнена'");
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ПараметрКоманды) = Тип("СправочникСсылка.Лоты") Тогда
		МетодОценнки = ОбщегоНазначенияУХ.ПолучитьЗначениеРеквизита(ПараметрКоманды, "МетодОценкиПредложенийПоставщиков");
		Если МетодОценнки = ПредопределенноеЗначение("Перечисление.МетодыОценкиПредложенийПоставщиков.ПоЦенам") Тогда
			ТекстСообщения = НСтр("ru = 'Для метода оценки по ценам не требуется предварительная настройка. Перейдите в протокол выбора победителя для оценки предложения.'");
			ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
		Иначе
			ОткрытьФормуНастройки(ПараметрКоманды, ПараметрыВыполненияКоманды);
		КонецЕсли;
	Иначе
		ОткрытьФормуНастройки(ПараметрКоманды, ПараметрыВыполненияКоманды);
	КонецЕсли;
КонецПроцедуры

// Выполняет открытие формы НастройкиПроцессаВыбора для текущего объекта.
&НаКлиенте
Процедура ОткрытьФормуНастройки(ПараметрКоманды, ПараметрыВыполненияКоманды)
		НастройкаПроцессаВыбора = ВыборОбъектовВызовСервераУХ.НастройкаПроцессаВыбораВладельца(ПараметрКоманды, Истина);
		ПараметрыФормы = Новый Структура("Ключ, ВладелецОбъектов", НастройкаПроцессаВыбора, ПараметрКоманды);
		ОткрытьФорму("Справочник.НастройкиПроцессаВыбора.ФормаОбъекта", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды.Уникальность, ПараметрыВыполненияКоманды.Окно, ПараметрыВыполненияКоманды.НавигационнаяСсылка);
КонецПроцедуры		// ОткрытьФормуНастройки()