#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает текст запроса динамического списка полуфабрикатов, используется в форме документа "План производства"
//
// Параметры:
//  СписокОтборов	 - Структура	 - список предопределенных отборов динамического списка
// 
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТекстЗапросаДинамическогоСписокПолуфабрикатов(СписокОтборов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПланыПроизводства.Регистратор КАК Регистратор,
	|	ПланыПроизводства.НомерСтроки КАК НомерСтроки,
	|
	|	ПланыПроизводства.ПланПроизводства КАК ПланПроизводства,
	|	ПланыПроизводства.Период           КАК Период,
	|	ПланыПроизводства.Номенклатура     КАК Номенклатура,
	|	ПланыПроизводства.Характеристика   КАК Характеристика,
	|	ПланыПроизводства.Назначение       КАК Назначение,
	|	ПланыПроизводства.Спецификация     КАК Спецификация,
	|
	|	ПланыПроизводства.ДатаВыпускаПолуфабриката КАК ДатаВыпускаПолуфабриката,
	|	ПланыПроизводства.ДатаЗапускаВыпуска КАК ДатаЗапускаВыпуска,
	|
	|	ПланыПроизводства.Количество КАК Количество,
	|	ПланыПроизводства.КЗаказу КАК КЗаказу,
	|
	|	ПланыПроизводства.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ВЫБОР
	|		КОГДА ПланыПроизводства.Номенклатура.ИспользованиеХарактеристик В (
	|												ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры),
	|												ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры),
	|												ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                          КАК ХарактеристикиИспользуются,
	|
	|	ПланыПроизводства.РучнаяКорректировка КАК РучнаяКорректировка,
	|	ПланыПроизводства.ДатаКорректировки   КАК ДатаКорректировки,
	|	ПланыПроизводства.АвторКорректировки  КАК АвторКорректировки,
	|	ПланыПроизводства.Комментарий         КАК Комментарий
	|ИЗ
	|	РегистрНакопления.ПланыПроизводства КАК ПланыПроизводства
	|ГДЕ
	|	ПланыПроизводства.ПланПроизводства = &ПланПроизводства И ПланыПроизводства.ЭтоПолуфабрикат
	|	И &ЕстьОтборПоВхождениямПолуфабриката";
	
	Если СписокОтборов.Свойство("ЕстьОтборПоВхождениямПолуфабриката") И СписокОтборов.ЕстьОтборПоВхождениямПолуфабриката Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПланыПроизводства.Регистратор КАК Регистратор,
		|	ПланыПроизводства.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВтОтборПоВхождениямПолуфабриката
		|ИЗ
		|	РегистрНакопления.ПланыПроизводства КАК ПланыПроизводства
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПланыПотребленияМатериалов КАК ПланыПотребленияМатериалов
		|		ПО ПланыПроизводства.ПланПроизводства = ПланыПотребленияМатериалов.ПланПроизводства
		|		 И ПланыПроизводства.Период           = ПланыПотребленияМатериалов.ДатаПроизводства
		|		 И ПланыПроизводства.Спецификация     = ПланыПотребленияМатериалов.СпецификацияПродукции
		|		 И ПланыПроизводства.Назначение       = ПланыПотребленияМатериалов.НазначениеПродукции
		|		 И ПланыПроизводства.Характеристика   = ПланыПотребленияМатериалов.ХарактеристикаПродукции
		|ГДЕ
		|	ПланыПотребленияМатериалов.ПланПроизводства = &ПланПроизводства
		|	И ПланыПотребленияМатериалов.Период         = &ДатаВыпускаПолуфабриката
		|	И ПланыПотребленияМатериалов.Назначение     = &Назначение
		|	И ПланыПотребленияМатериалов.Номенклатура   = &Номенклатура
		|	И ПланыПотребленияМатериалов.Характеристика = &Характеристика
		|" + "
		|;
		|" + ТекстЗапроса;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЕстьОтборПоВхождениямПолуфабриката", "
			|(ПланыПроизводства.Регистратор, ПланыПроизводства.НомерСтроки) 
			|В 
			|(ВЫБРАТЬ
			|		ТаблицаОтбора.Регистратор КАК Регистратор,
			|		ТаблицаОтбора.НомерСтроки КАК НомерСтроки
			|	ИЗ
			|		ВтОтборПоВхождениямПолуфабриката КАК ТаблицаОтбора
			|)");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЕстьОтборПоВхождениямПолуфабриката", "ИСТИНА");
	КонецЕсли;
	
	Возврат ТекстЗапроса
	
КонецФункции

// Рассчитывает количество полуфабрикатов, используется при отображении количества строк в динамическом списке полуфабрикатов
//
// Параметры:
//  ПланПроизводства - ДокументСсылка.ПланПроизводства - ссылка на документ план производства
// 
// Возвращаемое значение:
//  Число - количество строк 
//
Функция РассчитатьКоличествоПолуфабрикатов(ПланПроизводства) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(*) КАК КоличествоСтрок
	|ИЗ
	|	РегистрНакопления.ПланыПроизводства КАК ПланыПроизводства
	|ГДЕ
	|	ПланыПроизводства.ПланПроизводства = &ПланПроизводства
	|	И ПланыПроизводства.ЭтоПолуфабрикат
	|");
	Запрос.УстановитьПараметр("ПланПроизводства", ПланПроизводства);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.КоличествоСтрок;
	
КонецФункции

// Возвращает данные регистра по заданному плану.
//
// Параметры:
//  Ссылка					 - ДокументСсылка.ПланПроизводства - план, данные которого необходимо прочитать.
//  ПолучитьПродукцию		 - Булево - Истина, если требуется получить план выпуска готовой продукции.
//  ПолучитьПолуфабрикаты	 - Булево - Истина, если требуется получить план выпуска производимых полуфабрикатов.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - данные регистра.
//
Функция ПрочитатьПланПроизводства(Ссылка, ПолучитьПродукцию = Истина, ПолучитьПолуфабрикаты = Истина) Экспорт
	
	Если НЕ ПолучитьПродукцию И НЕ ПолучитьПолуфабрикаты Тогда
		ТекстИсключения = НСтр("ru = 'Ошибка чтения данных плана производства. В процедуру переданы некорректные параметры.';
								|en = 'An error occurred while reading production plan data. Invalid parameters were passed to the procedure.'");
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПланыПроизводства.*
		|ИЗ
		|	РегистрНакопления.ПланыПроизводства КАК ПланыПроизводства
		|ГДЕ
		|	ПланыПроизводства.Регистратор = &Ссылка
		|	И &ПродукцияПолуфабрикат";
		
	Если ПолучитьПродукцию И ПолучитьПолуфабрикаты Тогда
		ТекстУсловия = "ИСТИНА";
	ИначеЕсли ПолучитьПродукцию Тогда
		ТекстУсловия = "ПланыПроизводства.ЭтоПолуфабрикат = ЛОЖЬ";
	ИначеЕсли ПолучитьПолуфабрикаты Тогда
		ТекстУсловия = "ПланыПроизводства.ЭтоПолуфабрикат = ИСТИНА";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПродукцияПолуфабрикат", ТекстУсловия);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Возвращает список продукции, потребляющей заданный полуфабрикат.
//
// Параметры:
//  ПланПроизводства - ДокументСсылка.ПланПроизводства			 - план производства
//  ДатаПотребности	 - Дата										 - дата потребности (дата производства, дата выпуска полуфабриката)
//  Назначение		 - СправочникСсылка.Назначения				 - назначение
//  Номенклатура	 - СправочникСсылка.Номенклатура			 - номенклатура
//  Характеристика	 - СправочникСсылка.ХарактеристикиНоменклатуры	 - характеристика
// 
// Возвращаемое значение:
//  ТаблицаЗначений - данные о потребителях полуфабриката.
//
Функция ПолуфабрикатВходитВПродукцию(ПланПроизводства, ДатаПотребности, Назначение, Номенклатура, Характеристика) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПланыПроизводства.Номенклатура КАК Номенклатура,
	|	ПланыПроизводства.Характеристика КАК Характеристика,
	|	ПланыПроизводства.Назначение КАК Назначение,
	|	ПланыПроизводства.Спецификация КАК Спецификация
	|ИЗ
	|	РегистрНакопления.ПланыПроизводства КАК ПланыПроизводства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПланыПотребленияМатериалов КАК ПланыПотребленияМатериалов
	|		ПО ПланыПроизводства.ПланПроизводства = ПланыПотребленияМатериалов.ПланПроизводства
	|		 И ПланыПроизводства.Период           = ПланыПотребленияМатериалов.ДатаПроизводства
	|		 И ПланыПроизводства.Спецификация     = ПланыПотребленияМатериалов.СпецификацияПродукции
	|		 И ПланыПроизводства.Назначение       = ПланыПотребленияМатериалов.НазначениеПродукции
	|		 И ПланыПроизводства.Характеристика   = ПланыПотребленияМатериалов.ХарактеристикаПродукции
	|ГДЕ
	|	ПланыПотребленияМатериалов.ПланПроизводства = &ПланПроизводства
	|	И ПланыПотребленияМатериалов.Период         = &ДатаПотребности
	|	И ПланыПотребленияМатериалов.Назначение     = &Назначение
	|	И ПланыПотребленияМатериалов.Номенклатура   = &Номенклатура
	|	И ПланыПотребленияМатериалов.Характеристика = &Характеристика
	|	И ( ПланыПотребленияМатериалов.ЭтоПолуфабрикат
	|		И НЕ ПланыПроизводства.ЭтоПолуфабрикат )
	|");
	Запрос.УстановитьПараметр("ПланПроизводства", ПланПроизводства);
	Запрос.УстановитьПараметр("ДатаПотребности",  ДатаПотребности);
	Запрос.УстановитьПараметр("Назначение",       Назначение);
	Запрос.УстановитьПараметр("Номенклатура",     Номенклатура);
	Запрос.УстановитьПараметр("Характеристика",   Характеристика);
	
	Результат = Запрос.Выполнить().Выгрузить();
	Возврат Результат;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Подразделение)
	|	И ЗначениеРазрешено(Сценарий)
	|	И ЗначениеРазрешено(ВидПлана)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ НЕ УТКА

#Область Корректировка

Процедура СохранитьРучныеКорректировкиПриЗаписи(Регистратор, МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СтарыеДвижения.ПланПроизводства КАК ПланПроизводства,
	|	СтарыеДвижения.Номенклатура КАК Номенклатура,
	|	СтарыеДвижения.Характеристика КАК Характеристика,
	|	СтарыеДвижения.Назначение КАК Назначение,
	|	СтарыеДвижения.Спецификация КАК Спецификация,
	|	ВЫБОР
	|		КОГДА СтарыеДвижения.ТипПроизводственногоПроцесса = ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Разборка)
	|			ТОГДА СтарыеДвижения.Период
	|		ИНАЧЕ СтарыеДвижения.ДатаЗапускаВыпуска
	|	КОНЕЦ КАК ДатаЗапуска,
	|	ВЫБОР
	|		КОГДА СтарыеДвижения.ТипПроизводственногоПроцесса = ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Разборка)
	|			ТОГДА СтарыеДвижения.ДатаЗапускаВыпуска
	|		ИНАЧЕ СтарыеДвижения.Период
	|	КОНЕЦ КАК ДатаВыпуска,
	|	СтарыеДвижения.ДатаВыпускаПолуфабриката КАК ДатаВыпускаПолуфабриката,
	|	СтарыеДвижения.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
	|	СтарыеДвижения.Количество КАК Количество,
	|	СтарыеДвижения.КЗаказу КАК КЗаказу,
	|	СтарыеДвижения.ДатаКорректировки КАК ДатаКорректировки,
	|	СтарыеДвижения.АвторКорректировки КАК АвторКорректировки,
	|	СтарыеДвижения.Комментарий КАК Комментарий
	|ИЗ
	|	СписокКорректировокПередЗаписью КАК СтарыеДвижения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПланыПроизводства КАК НовыеДвижения
	|		ПО НовыеДвижения.Регистратор = &Регистратор
	|			И СтарыеДвижения.Период = НовыеДвижения.Период
	|			И СтарыеДвижения.Сценарий = НовыеДвижения.Сценарий
	|			И СтарыеДвижения.Номенклатура = НовыеДвижения.Номенклатура
	|			И СтарыеДвижения.Характеристика = НовыеДвижения.Характеристика
	|			И СтарыеДвижения.Подразделение = НовыеДвижения.Подразделение
	|			И СтарыеДвижения.ПланПроизводства = НовыеДвижения.ПланПроизводства
	|			И СтарыеДвижения.Спецификация = НовыеДвижения.Спецификация
	|			И СтарыеДвижения.Назначение = НовыеДвижения.Назначение
	|			И СтарыеДвижения.ДатаВыпускаПолуфабриката = НовыеДвижения.ДатаВыпускаПолуфабриката
	|			И СтарыеДвижения.ДатаЗапускаВыпуска = НовыеДвижения.ДатаЗапускаВыпуска
	|			И СтарыеДвижения.ЭтоПолуфабрикат = НовыеДвижения.ЭтоПолуфабрикат
	|			И СтарыеДвижения.РучнаяКорректировка = НовыеДвижения.РучнаяКорректировка
	|ГДЕ
	|	СтарыеДвижения.РучнаяКорректировка И НовыеДвижения.Номенклатура ЕСТЬ NULL");
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	РегистрыСведений.ОтменаКорректировокПланаПроизводства.ЗаписатьКорректировки(Запрос.Выполнить().Выгрузить(), Регистратор);
	
КонецПроцедуры

#КонецОбласти

//-- НЕ УТКА

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыНакопления.ПланыПроизводства.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.3.10";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("53b1f59f-6cef-4472-96fd-9e9056bfd4ee");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.ПланыПроизводства.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Изменяет регистратор движений - вместо документа ""План производства"" устанавливается ""Регистратор плана производства"".
	|Заполняет реквизит ""Исходная потребность"".
	|Заполняет реквизит ""Вид плана"".
	|Заполняет ресурс ""К заказу"".
	|Отмечает к пересчету планы с измененным состоянием замещения';
	|en = 'Changes the movements recorder - sets ""Production plan recorder"" instead of the ""Production plan"" document.
	|Fills in the ""Initial demand"" attribute. 
	|Fills in the ""Plan profile"" attribute.
	|Fills in the ""To order"" resource
	|Marks the plans with changed substitution status for recalculation'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.РегистраторПланаПроизводства.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.ПланыПотребленияМатериалов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.ПланыПроизводства.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыНакопления.ПланыПроизводства.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПланПроизводства.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ПланыПотребленияМатериалов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ТекстЗапроса = Документы.ПланПроизводства.ТекстЗапросаДанныеКОбработке();
	
	ТекстЗапроса = ТекстЗапроса + "
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПланыПроизводства.ПланПроизводства КАК Ссылка
		|ИЗ
		|	РегистрНакопления.ПланыПроизводства КАК ПланыПроизводства
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЗамещающиеИЗамещенныеПланыПроизводства КАК ЗамещающиеИЗамещенныеПланыПроизводства
		|		ПО ПланыПроизводства.ПланПроизводства = ЗамещающиеИЗамещенныеПланыПроизводства.ЗамещенныйПлан
		|ГДЕ
		|	(ПланыПроизводства.Регистратор ССЫЛКА Документ.ПланПроизводства
		|	И ПланыПроизводства.Сценарий.ИспользоватьДляПланированияМатериалов)
		|	ИЛИ ПланыПроизводства.ВидПлана = ЗНАЧЕНИЕ(Справочник.ВидыПланов.ПустаяСсылка)
		|	ИЛИ (ПланыПроизводства.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПланов.Отменен))
		|	ИЛИ (ЗамещающиеИЗамещенныеПланыПроизводства.ЗамещенныйПлан ЕСТЬ NULL
		|	И ПланыПроизводства.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПланов.Утвержден)
		|	И ПланыПроизводства.Количество <> 0
		|	И ПланыПроизводства.КЗаказу = 0)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Данные = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Данные);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрНакопления.ПланыПроизводства";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуСсылокДляОбработки(Параметры.Очередь, "Документ.ПланПроизводства", МенеджерВременныхТаблиц);
	
	Если НЕ Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	Если НЕ Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СсылкиДляОбработки.Ссылка КАК Ссылка,
	|	Документ.ВерсияДанных КАК ВерсияДанных,
	|	Документ.Проведен КАК Проведен,
	|	Документ.ВидПлана КАК ВидПлана,
	|	Документ.Статус КАК Статус,
	|	Документ.Сценарий КАК Сценарий,
	|	Документ.Сценарий.ИспользоватьДляПланированияМатериалов КАК ИспользоватьДляПланированияМатериалов,
	|	Документ.Сценарий.Периодичность КАК Периодичность
	|ПОМЕСТИТЬ ПланыПроизводстваДляОбработки
	|ИЗ
	|	&СсылкиДляОбработки КАК СсылкиДляОбработки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПланПроизводства КАК Документ
	|		ПО СсылкиДляОбработки.Ссылка = Документ.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПланыПроизводства.ПланПроизводства КАК Ссылка,
	|	ПланыПроизводства.Регистратор ССЫЛКА Документ.ПланПроизводства
	|		И ПланыПроизводства.Сценарий.ИспользоватьДляПланированияМатериалов КАК ЗаменитьРегистратор,
	|	ПланыПроизводства.ПланПроизводства.Замещающий
	|		И ПланыПроизводства.Сценарий.ИспользоватьДляПланированияМатериалов
	|		И НЕ ПланыПроизводства.ЭтоПолуфабрикат
	|		И ВЫБОР ПланыПроизводства.Сценарий.Периодичность
	|			КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
	|				ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводства.Период, НЕДЕЛЯ)
	|			КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Декада)
	|				ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводства.Период, ДЕКАДА)
	|			КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|				ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводства.Период, МЕСЯЦ)
	|			КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
	|				ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводства.Период, КВАРТАЛ)
	|			КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Полугодие)
	|				ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводства.Период, ПОЛУГОДИЕ)
	|			КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Год)
	|				ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводства.Период, ГОД)
	|			ИНАЧЕ ПланыПроизводства.Период
	|		КОНЕЦ = ЗамещениеПланов.ЗамещенныйПериод
	|		И ПланыПроизводства.Количество <> 0 КАК Заместить
	|ПОМЕСТИТЬ ВТПланыДляОбработки
	|ИЗ
	|	РегистрНакопления.ПланыПроизводства КАК ПланыПроизводства
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗамещениеПланов КАК ЗамещениеПланов
	|		ПО ПланыПроизводства.ПланПроизводства = ЗамещениеПланов.ЗамещенныйПлан
	|ГДЕ
	|	(ПланыПроизводства.ПланПроизводства В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						СсылкиДляОбработки.Ссылка
	|					ИЗ
	|						&СсылкиДляОбработки КАК СсылкиДляОбработки)
	|				И (ПланыПроизводства.ПланПроизводства.Замещающий
	|					И ПланыПроизводства.Сценарий.ИспользоватьДляПланированияМатериалов
	|					И НЕ ПланыПроизводства.ЭтоПолуфабрикат
	|					И ВЫБОР ПланыПроизводства.Сценарий.Периодичность
	|						КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
	|							ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводства.Период, НЕДЕЛЯ)
	|						КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Декада)
	|							ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводства.Период, ДЕКАДА)
	|						КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|							ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводства.Период, МЕСЯЦ)
	|						КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
	|							ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводства.Период, КВАРТАЛ)
	|						КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Полугодие)
	|							ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводства.Период, ПОЛУГОДИЕ)
	|						КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Год)
	|							ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводства.Период, ГОД)
	|						ИНАЧЕ ПланыПроизводства.Период
	|					КОНЕЦ = ЗамещениеПланов.ЗамещенныйПериод
	|					И ПланыПроизводства.Количество <> 0)
	|			ИЛИ ПланыПроизводства.Регистратор ССЫЛКА Документ.ПланПроизводства
	|				И ПланыПроизводства.Сценарий.ИспользоватьДляПланированияМатериалов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланыПроизводстваДляОбработки.Ссылка КАК Ссылка,
	|	ПланыПроизводстваДляОбработки.Статус КАК Статус,
	|	ПланыПроизводстваДляОбработки.ВидПлана КАК ВидПлана,
	|	ПланыПроизводстваДляОбработки.Сценарий КАК Сценарий,
	|	ПланыПроизводстваДляОбработки.Периодичность КАК Периодичность,
	|	ВЫБОР
	|		КОГДА ВТПланыДляОбработки.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ЕСТЬNULL(ВТПланыДляОбработки.Заместить, ЛОЖЬ)
	|	КОНЕЦ КАК ПолныйПересчет,
	|	ВЫБОР
	|		КОГДА ВТПланыДляОбработки.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ЕСТЬNULL(ВТПланыДляОбработки.ЗаменитьРегистратор, ЛОЖЬ)
	|	КОНЕЦ КАК ЗаменитьРегистратор
	|ИЗ
	|	ПланыПроизводстваДляОбработки КАК ПланыПроизводстваДляОбработки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПланыДляОбработки КАК ВТПланыДляОбработки
	|		ПО (ВТПланыДляОбработки.Ссылка = ПланыПроизводстваДляОбработки.Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланыПроизводстваДляОбработки.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(РегистраторПланаПроизводства.Ссылка, ПланыПроизводстваДляОбработки.Ссылка) КАК Регистратор
	|ИЗ
	|	ПланыПроизводстваДляОбработки КАК ПланыПроизводстваДляОбработки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РегистраторПланаПроизводства КАК РегистраторПланаПроизводства
	|		ПО ПланыПроизводстваДляОбработки.Ссылка = РегистраторПланаПроизводства.ПланПроизводства
	|			И (РегистраторПланаПроизводства.Проведен)";
	
	ТекстЗапросаЗаменыДвижений = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПланыПроизводства.Номенклатура КАК Номенклатура,
	|	ПланыПроизводства.Характеристика КАК Характеристика,
	|	ПланыПроизводства.ПланПроизводства КАК ПланПроизводства,
	|	ПланыПроизводства.Назначение КАК Назначение,
	|	ПланыПроизводства.ДатаВыпускаПолуфабриката КАК ДатаВыпускаПолуфабриката
	|ПОМЕСТИТЬ ВТКорректировки
	|ИЗ
	|	РегистрНакопления.ПланыПроизводства КАК ПланыПроизводства
	|ГДЕ
	|	ПланыПроизводства.Регистратор = &Регистратор
	|	И ПланыПроизводства.РучнаяКорректировка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПланПроизводства,
	|	Номенклатура,
	|	Назначение,
	|	Характеристика,
	|	ДатаВыпускаПолуфабриката
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланыПотребленияМатериаловОбороты.Номенклатура КАК Номенклатура,
	|	ПланыПотребленияМатериаловОбороты.Характеристика КАК Характеристика,
	|	ПланыПотребленияМатериаловОбороты.Назначение КАК Назначение,
	|	ПланыПотребленияМатериаловОбороты.ПланПроизводства КАК ПланПроизводства,
	|	ПланыПотребленияМатериаловОбороты.Период КАК Период,
	|	ПланыПотребленияМатериаловОбороты.КоличествоОборот КАК Количество,
	|	ВЫБОР
	|		КОГДА ПланыПотребленияМатериаловОбороты.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПланов.Утвержден)
	|				И ПланыПотребленияМатериаловОбороты.КоличествоОборот <> 0
	|				И ПланыПотребленияМатериаловОбороты.КЗаказуОборот = 0
	|			ТОГДА ПланыПотребленияМатериаловОбороты.КоличествоОборот
	|		ИНАЧЕ ПланыПотребленияМатериаловОбороты.КЗаказуОборот
	|	КОНЕЦ КАК КЗаказу
	|ПОМЕСТИТЬ ВТИсходнаяПотребность
	|ИЗ
	|	РегистрНакопления.ПланыПотребленияМатериалов.Обороты(
	|			,
	|			,
	|			День,
	|			(Номенклатура, Характеристика, Назначение, ПланПроизводства) В
	|				(ВЫБРАТЬ
	|					ВТКорректировки.Номенклатура,
	|					ВТКорректировки.Характеристика,
	|					ВТКорректировки.Назначение,
	|					ВТКорректировки.ПланПроизводства
	|				ИЗ
	|					ВТКорректировки)) КАК ПланыПотребленияМатериаловОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКорректировки КАК ВТКорректировки
	|		ПО ПланыПотребленияМатериаловОбороты.Номенклатура = ВТКорректировки.Номенклатура
	|			И ПланыПотребленияМатериаловОбороты.Характеристика = ВТКорректировки.Характеристика
	|			И ПланыПотребленияМатериаловОбороты.ПланПроизводства = ВТКорректировки.ПланПроизводства
	|			И ПланыПотребленияМатериаловОбороты.Назначение = ВТКорректировки.Назначение
	|			И ПланыПотребленияМатериаловОбороты.Период = ВТКорректировки.ДатаВыпускаПолуфабриката
	|ГДЕ
	|	ВТКорректировки.Номенклатура ЕСТЬ НЕ NULL 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Назначение,
	|	ПланПроизводства,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланыПроизводства.Период КАК Период,
	|	ВЫБОР &Периодичность
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
	|			ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводства.Период, НЕДЕЛЯ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Декада)
	|			ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводства.Период, ДЕКАДА)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|			ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводства.Период, МЕСЯЦ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
	|			ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводства.Период, КВАРТАЛ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Полугодие)
	|			ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводства.Период, ПОЛУГОДИЕ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Год)
	|			ТОГДА НАЧАЛОПЕРИОДА(ПланыПроизводства.Период, ГОД)
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(ПланыПроизводства.Период, ДЕНЬ)
	|	КОНЕЦ КАК НачалоПериода,
	|	ПланыПроизводства.Регистратор КАК Регистратор,
	|	ПланыПроизводства.НомерСтроки КАК НомерСтроки,
	|	ПланыПроизводства.Активность КАК Активность,
	|	ПланыПроизводства.Сценарий КАК Сценарий,
	|	&ВидПлана КАК ВидПлана,
	|	ПланыПроизводства.Статус КАК Статус,
	|	ПланыПроизводства.Номенклатура КАК Номенклатура,
	|	ПланыПроизводства.Характеристика КАК Характеристика,
	|	ПланыПроизводства.Подразделение КАК Подразделение,
	|	ПланыПроизводства.ПланПроизводства КАК ПланПроизводства,
	|	ПланыПроизводства.Спецификация КАК Спецификация,
	|	ПланыПроизводства.Назначение КАК Назначение,
	|	ПланыПроизводства.ДатаВыпускаПолуфабриката КАК ДатаВыпускаПолуфабриката,
	|	ПланыПроизводства.ДатаЗапускаВыпуска КАК ДатаЗапускаВыпуска,
	|	ПланыПроизводства.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
	|	ПланыПроизводства.ТипПроизводственногоПроцесса КАК ТипПроизводственногоПроцесса,
	|	ПланыПроизводства.РазделительРасчета КАК РазделительРасчета,
	|	ПланыПроизводства.Количество КАК Количество,
	|	ПланыПроизводства.КЗаказу КАК КЗаказу,
	|	ИСТИНА КАК ПовторноеРазузлование,
	|	ПланыПроизводства.РучнаяКорректировка КАК РучнаяКорректировка,
	|	ПланыПроизводства.ДатаКорректировки КАК ДатаКорректировки,
	|	ПланыПроизводства.АвторКорректировки КАК АвторКорректировки,
	|	ВЫБОР
	|		КОГДА ПланыПроизводства.РучнаяКорректировка
	|			ТОГДА ЕСТЬNULL(ВТИсходнаяПотребность.Количество, ПланыПроизводства.ИсходнаяПотребность)
	|		ИНАЧЕ ПланыПроизводства.ИсходнаяПотребность
	|	КОНЕЦ КАК ИсходнаяПотребность,
	|	ВЫБОР
	|		КОГДА ПланыПроизводства.РучнаяКорректировка
	|			ТОГДА ЕСТЬNULL(ВТИсходнаяПотребность.КЗаказу, ПланыПроизводства.ИсходнаяПотребностьКЗаказу)
	|		ИНАЧЕ ПланыПроизводства.ИсходнаяПотребностьКЗаказу
	|	КОНЕЦ КАК ИсходнаяПотребностьКЗаказу,
	|	ПланыПроизводства.Комментарий КАК Комментарий
	|ПОМЕСТИТЬ ПланыПроизводства
	|ИЗ
	|	РегистрНакопления.ПланыПроизводства КАК ПланыПроизводства
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИсходнаяПотребность КАК ВТИсходнаяПотребность
	|		ПО ПланыПроизводства.Номенклатура = ВТИсходнаяПотребность.Номенклатура
	|			И ПланыПроизводства.Характеристика = ВТИсходнаяПотребность.Характеристика
	|			И ПланыПроизводства.ПланПроизводства = ВТИсходнаяПотребность.ПланПроизводства
	|			И ПланыПроизводства.Назначение = ВТИсходнаяПотребность.Назначение
	|			И ПланыПроизводства.ДатаВыпускаПолуфабриката = ВТИсходнаяПотребность.Период
	|ГДЕ
	|	ПланыПроизводства.Регистратор = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗамещениеПланов.ЗамещенныйПлан КАК ЗамещенныйПлан,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗамещениеПланов.СтатусЗамещения = ЗНАЧЕНИЕ(Перечисление.СтатусыПланов.Утвержден)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЗамещатьКЗаказу,
	|	ЗамещениеПланов.ЗамещенныйПериод КАК ЗамещенныйПериод
	|ПОМЕСТИТЬ ЗамещениеПланов
	|ИЗ
	|	РегистрСведений.ЗамещениеПланов КАК ЗамещениеПланов
	|ГДЕ
	|	ЗамещениеПланов.ЗамещенныйПлан = &ПланПроизводства
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗамещениеПланов.ЗамещенныйПлан,
	|	ЗамещениеПланов.ЗамещенныйПериод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПланыПроизводства.Период КАК Период,
	|	ПланыПроизводства.НачалоПериода КАК НачалоПериода,
	|	ПланыПроизводства.Регистратор КАК Регистратор,
	|	ПланыПроизводства.НомерСтроки КАК НомерСтроки,
	|	ПланыПроизводства.Активность КАК Активность,
	|	ПланыПроизводства.Сценарий КАК Сценарий,
	|	ПланыПроизводства.ВидПлана КАК ВидПлана,
	|	ПланыПроизводства.Статус КАК Статус,
	|	ПланыПроизводства.Номенклатура КАК Номенклатура,
	|	ПланыПроизводства.Характеристика КАК Характеристика,
	|	ПланыПроизводства.Подразделение КАК Подразделение,
	|	ПланыПроизводства.ПланПроизводства КАК ПланПроизводства,
	|	ПланыПроизводства.Спецификация КАК Спецификация,
	|	ПланыПроизводства.Назначение КАК Назначение,
	|	ПланыПроизводства.ДатаВыпускаПолуфабриката КАК ДатаВыпускаПолуфабриката,
	|	ПланыПроизводства.ДатаЗапускаВыпуска КАК ДатаЗапускаВыпуска,
	|	ПланыПроизводства.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
	|	ПланыПроизводства.ТипПроизводственногоПроцесса КАК ТипПроизводственногоПроцесса,
	|	ПланыПроизводства.РазделительРасчета КАК РазделительРасчета,
	|	ПланыПроизводства.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА ПланыПроизводства.КЗаказу <> 0
	|			ТОГДА ПланыПроизводства.КЗаказу
	|		КОГДА ПланыПроизводства.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПланов.Утвержден)
	|			ТОГДА ПланыПроизводства.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КЗаказу,
	|	ПланыПроизводства.ПовторноеРазузлование КАК ПовторноеРазузлование,
	|	ПланыПроизводства.РучнаяКорректировка КАК РучнаяКорректировка,
	|	ПланыПроизводства.ДатаКорректировки КАК ДатаКорректировки,
	|	ПланыПроизводства.АвторКорректировки КАК АвторКорректировки,
	|	ПланыПроизводства.ИсходнаяПотребность КАК ИсходнаяПотребность,
	|	ПланыПроизводства.ИсходнаяПотребностьКЗаказу КАК ИсходнаяПотребностьКЗаказу,
	|	ПланыПроизводства.Комментарий КАК Комментарий
	|ИЗ
	|	ПланыПроизводства КАК ПланыПроизводства
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗамещениеПланов КАК ЗамещениеПланов
	|		ПО ПланыПроизводства.НачалоПериода = ЗамещениеПланов.ЗамещенныйПериод";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СсылкиДляОбработки", Результат.ИмяВременнойТаблицы);
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ВыборкаПоДокументам = МассивРезультатов[МассивРезультатов.ВГраница()-1].Выбрать();
	ТаблицаПоРегистраторам = МассивРезультатов[МассивРезультатов.ВГраница()].Выгрузить();
	
	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра + ".НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", ВыборкаПоДокументам.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ПланыПотребленияМатериалов");
			ЭлементБлокировки.УстановитьЗначение("ПланПроизводства", ВыборкаПоДокументам.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			
			ЭлементБлокировки = Блокировка.Добавить("Документ.РегистраторПланаПроизводства");
			ЭлементБлокировки.УстановитьЗначение("ПланПроизводства", ВыборкаПоДокументам.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			
			Блокировка.Заблокировать();
			
			СтрокиРегистраторы = ТаблицаПоРегистраторам.НайтиСтроки(Новый Структура("Ссылка", ВыборкаПоДокументам.Ссылка));
			
			Для Каждого СтрокаРегистратор Из СтрокиРегистраторы Цикл
				
				Если ВыборкаПоДокументам.Статус = Перечисления.СтатусыПланов.Отменен Тогда
					
					НаборЗаписейСтарый = РегистрыНакопления.ПланыПроизводства.СоздатьНаборЗаписей();
					Если ВыборкаПоДокументам.ЗаменитьРегистратор Тогда
						НаборЗаписейСтарый.Отбор.Регистратор.Установить(ВыборкаПоДокументам.Ссылка);
					Иначе
						НаборЗаписейСтарый.Отбор.Регистратор.Установить(СтрокаРегистратор.Регистратор);
					КонецЕсли;
					ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписейСтарый);
					
				Иначе
					ЗапросЗаменыДвижений = Новый Запрос();
					ЗапросЗаменыДвижений.Текст = ТекстЗапросаЗаменыДвижений;
					Если ВыборкаПоДокументам.ЗаменитьРегистратор Тогда
						ЗапросЗаменыДвижений.УстановитьПараметр("Регистратор", ВыборкаПоДокументам.Ссылка);
					Иначе
						ЗапросЗаменыДвижений.УстановитьПараметр("Регистратор", СтрокаРегистратор.Регистратор);
					КонецЕсли;
					ЗапросЗаменыДвижений.УстановитьПараметр("ВидПлана", ВыборкаПоДокументам.ВидПлана);
					ЗапросЗаменыДвижений.УстановитьПараметр("ПолныйПересчет", ВыборкаПоДокументам.ПолныйПересчет);
					ЗапросЗаменыДвижений.УстановитьПараметр("Периодичность", ВыборкаПоДокументам.Периодичность);
					ЗапросЗаменыДвижений.УстановитьПараметр("ПланПроизводства", ВыборкаПоДокументам.Ссылка);
					
					РезультатДвижения = ЗапросЗаменыДвижений.Выполнить();
					
					Если Не РезультатДвижения.Пустой() 
						И ВыборкаПоДокументам.ЗаменитьРегистратор Тогда
						
						НаборЗаписейНовый = РегистрыНакопления.ПланыПроизводства.СоздатьНаборЗаписей();
						НаборЗаписейНовый.Отбор.Регистратор.Установить(СтрокаРегистратор.Регистратор);
						НаборЗаписейНовый.Загрузить(РезультатДвижения.Выгрузить());
						ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписейНовый);
						
						НаборЗаписейСтарый = РегистрыНакопления.ПланыПроизводства.СоздатьНаборЗаписей();
						НаборЗаписейСтарый.Отбор.Регистратор.Установить(ВыборкаПоДокументам.Ссылка);
						ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписейСтарый);
						
					ИначеЕсли Не РезультатДвижения.Пустой() Тогда
						
						НаборЗаписейНовый = РегистрыНакопления.ПланыПроизводства.СоздатьНаборЗаписей();
						НаборЗаписейНовый.Отбор.Регистратор.Установить(СтрокаРегистратор.Регистратор);
						НаборЗаписейНовый.Загрузить(РезультатДвижения.Выгрузить());
						ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписейНовый);
						
					КонецЕсли;
				КонецЕсли;
				
			КонецЦикла;
			
			Если ВыборкаПоДокументам.ПолныйПересчет Тогда
				РегистрыСведений.ОчередьРасчетаПланаПроизводства.ДобавитьЗадание(ВыборкаПоДокументам.Сценарий, ВыборкаПоДокументам.Ссылка, Истина);
			КонецЕсли;
			
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ВыборкаПоДокументам.Ссылка);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), ВыборкаПоДокументам.Ссылка);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
