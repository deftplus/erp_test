#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОписаниеПеременных
	
Перем мПериод          Экспорт; // Период движений
Перем мТаблицаДвижений Экспорт; // Таблица движений

#КонецОбласти 

#Область ПрограммныйИнтерфейс

// Выполняет движения по регистру.
//
// Параметры:
//  Нет.
//
Процедура ВыполнитьДвижения() Экспорт

	Загрузить(мТаблицаДвижений);

КонецПроцедуры // ВыполнитьДвижения()

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеСерверОПК.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	БлокироватьДляИзменения = Истина;

	// Текущее состояние набора помещается во временную таблицу "ДвиженияЛимитыПоБюджетамПередЗаписью",
	// чтобы при записи получить изменение нового набора относительно текущего.
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = РегистрыНакопления.ЛимитыПоБюджетам.ПолучитьТекстЗапросаТекущиеДвижения();
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеСерверОПК.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;

	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	
	"ВЫБРАТЬ
	|	Таблица.Предназначение,
	|	Таблица.ПериодЛимитирования,
	|	Таблица.СтатьяБюджета,
	|	Таблица.Аналитика1,
	|	Таблица.Аналитика2,
	|	Таблица.Аналитика3,
	|	Таблица.Аналитика4,
	|	Таблица.Аналитика5,
	|	Таблица.Аналитика6,
	|	Таблица.ЦФО,
	|	Таблица.Проект,
	|	Таблица.Валюта,
	|	Таблица.ДокументРезервирования,
	|	Таблица.ДокументПланирования,
	|	Таблица.Лимит,
	|	Таблица.Корректировка,
	|	Таблица.Зарезервировано,
	|	Таблица.Заявлено,
	|	Таблица.Исполнено,
	|	Таблица.КОбеспечению
	|ПОМЕСТИТЬ ВТ_ДвиженияЛимитыПоБюджетам
	|ИЗ
	|	РегистрНакопления.ЛимитыПоБюджетам КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор"
	+ ОбщегоНазначенияОПК.ТекстРазделителяЗапросовПакета()
	+ РегистрыНакопления.ЛимитыПоБюджетам.ПолучитьТекстЗапросаТаблицаИзменений();
	
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияЛимитыПоБюджетамИзменение", Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

