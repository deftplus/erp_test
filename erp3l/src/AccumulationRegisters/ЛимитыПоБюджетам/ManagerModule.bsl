#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Возвращает текст запроса, который формирует таблицу ранее записанных движений регистра по документу
// Сохраняет во временную таблицу
//		ДвиженияЛимитыПоБюджетамПередЗаписью	- Ранее записанные движения регистра (все измерения и ресурсы)
Функция ПолучитьТекстЗапросаТекущиеДвижения() экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Таблица.Период КАК Период,
	|	Таблица.Предназначение КАК Предназначение,
	|	Таблица.ПериодЛимитирования КАК ПериодЛимитирования,
	|	Таблица.СтатьяБюджета КАК СтатьяБюджета,
	|	Таблица.Аналитика1 КАК Аналитика1,
	|	Таблица.Аналитика2 КАК Аналитика2,
	|	Таблица.Аналитика3 КАК Аналитика3,
	|	Таблица.Аналитика4 КАК Аналитика4,
	|	Таблица.Аналитика5 КАК Аналитика5,
	|	Таблица.Аналитика6 КАК Аналитика6,
	|	Таблица.ЦФО КАК ЦФО,
	|	Таблица.Проект КАК Проект,
	|	Таблица.Валюта КАК Валюта,
	|	Таблица.ДокументРезервирования КАК ДокументРезервирования,
	|	Таблица.ДокументПланирования КАК ДокументПланирования,
	|	Таблица.Лимит КАК ЛимитПередЗаписью,
	|	Таблица.Корректировка КАК КорректировкаПередЗаписью,
	|	Таблица.Зарезервировано КАК ЗарезервированоПередЗаписью,
	|	Таблица.Заявлено КАК ЗаявленоПередЗаписью,
	|	Таблица.Исполнено КАК ИсполненоПередЗаписью,
	|	Таблица.КОбеспечению КАК КОбеспечениюПередЗаписью
	|ПОМЕСТИТЬ ДвиженияЛимитыПоБюджетамПередЗаписью
	|ИЗ
	|	РегистрНакопления.ЛимитыПоБюджетам КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И (НЕ &ЭтоНовый)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса, который формирует таблицу изменения движений регистра по документу
// Должны существовать временные таблицы:
//		ДвиженияЛимитыПоБюджетамПередЗаписью	- Ранее записанные движения регистра (все измерения и ресурсы)
//		ВТ_ДвиженияЛимитыПоБюджетам				- Новые движения регистра (все измерения и ресурсы)
Функция ПолучитьТекстЗапросаТаблицаИзменений() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаИзменений.Предназначение КАК Предназначение,
	|	ТаблицаИзменений.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ТаблицаИзменений.СтатьяБюджета КАК СтатьяБюджета,
	|	ТаблицаИзменений.Аналитика1 КАК Аналитика1,
	|	ТаблицаИзменений.Аналитика2 КАК Аналитика2,
	|	ТаблицаИзменений.Аналитика3 КАК Аналитика3,
	|	ТаблицаИзменений.Аналитика4 КАК Аналитика4,
	|	ТаблицаИзменений.Аналитика5 КАК Аналитика5,
	|	ТаблицаИзменений.Аналитика6 КАК Аналитика6,
	|	ТаблицаИзменений.ЦФО КАК ЦФО,
	|	ТаблицаИзменений.Проект КАК Проект,
	|	ТаблицаИзменений.Валюта КАК Валюта,
	|	ТаблицаИзменений.ДокументРезервирования КАК ДокументРезервирования,
	|	ТаблицаИзменений.ДокументПланирования КАК ДокументПланирования,
	|	СУММА(ТаблицаИзменений.ЛимитИзменение) КАК ЛимитИзменение,
	|	СУММА(ТаблицаИзменений.КорректировкаИзменение) КАК КорректировкаИзменение,
	|	СУММА(ТаблицаИзменений.ЗарезервированоИзменение) КАК ЗарезервированоИзменение,
	|	СУММА(ТаблицаИзменений.ЗаявленоИзменение) КАК ЗаявленоИзменение,
	|	СУММА(ТаблицаИзменений.ИсполненоИзменение) КАК ИсполненоИзменение,
	|	СУММА(ТаблицаИзменений.КОбеспечениюИзменение) КАК КОбеспечениюИзменение
	|ПОМЕСТИТЬ ДвиженияЛимитыПоБюджетамИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Предназначение КАК Предназначение,
	|		Таблица.ПериодЛимитирования КАК ПериодЛимитирования,
	|		Таблица.СтатьяБюджета КАК СтатьяБюджета,
	|		Таблица.Аналитика1 КАК Аналитика1,
	|		Таблица.Аналитика2 КАК Аналитика2,
	|		Таблица.Аналитика3 КАК Аналитика3,
	|		Таблица.Аналитика4 КАК Аналитика4,
	|		Таблица.Аналитика5 КАК Аналитика5,
	|		Таблица.Аналитика6 КАК Аналитика6,
	|		Таблица.ЦФО КАК ЦФО,
	|		Таблица.Проект КАК Проект,
	|		Таблица.Валюта КАК Валюта,
	|		Таблица.ДокументРезервирования КАК ДокументРезервирования,
	|		Таблица.ДокументПланирования КАК ДокументПланирования,
	|		-Таблица.ЛимитПередЗаписью КАК ЛимитИзменение,
	|		-Таблица.КорректировкаПередЗаписью КАК КорректировкаИзменение,
	|		-Таблица.ЗарезервированоПередЗаписью КАК ЗарезервированоИзменение,
	|		-Таблица.ЗаявленоПередЗаписью КАК ЗаявленоИзменение,
	|		-Таблица.ИсполненоПередЗаписью КАК ИсполненоИзменение,
	|		-Таблица.КОбеспечениюПередЗаписью КАК КОбеспечениюИзменение
	|	ИЗ
	|		ДвиженияЛимитыПоБюджетамПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Предназначение,
	|		Таблица.ПериодЛимитирования,
	|		Таблица.СтатьяБюджета,
	|		Таблица.Аналитика1,
	|		Таблица.Аналитика2,
	|		Таблица.Аналитика3,
	|		Таблица.Аналитика4,
	|		Таблица.Аналитика5,
	|		Таблица.Аналитика6,
	|		Таблица.ЦФО,
	|		Таблица.Проект,
	|		Таблица.Валюта,
	|		Таблица.ДокументРезервирования,
	|		Таблица.ДокументПланирования,
	|		Таблица.Лимит,
	|		Таблица.Корректировка,
	|		Таблица.Зарезервировано,
	|		Таблица.Заявлено,
	|		Таблица.Исполнено,
	|		Таблица.КОбеспечению
	|	ИЗ
	|		ВТ_ДвиженияЛимитыПоБюджетам КАК Таблица) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Предназначение,
	|	ТаблицаИзменений.ПериодЛимитирования,
	|	ТаблицаИзменений.СтатьяБюджета,
	|	ТаблицаИзменений.Аналитика1,
	|	ТаблицаИзменений.Аналитика2,
	|	ТаблицаИзменений.Аналитика3,
	|	ТаблицаИзменений.Аналитика4,
	|	ТаблицаИзменений.Аналитика5,
	|	ТаблицаИзменений.Аналитика6,
	|	ТаблицаИзменений.ЦФО,
	|	ТаблицаИзменений.Проект,
	|	ТаблицаИзменений.Валюта,
	|	ТаблицаИзменений.ДокументРезервирования,
	|	ТаблицаИзменений.ДокументПланирования
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ТаблицаИзменений.ЛимитИзменение) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.КорректировкаИзменение) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.ЗарезервированоИзменение) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.ЗаявленоИзменение) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.ИсполненоИзменение) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.КОбеспечениюИзменение) <> 0)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецЕсли
