#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ПодразделениеДиспетчер)
	|	И ЗначениеРазрешено(Сценарий)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыНакопления.ПланыЗанятостиТрудовыхРесурсов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.3.10";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("a347e660-c244-4309-9951-1ff0fc7d5057");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.ПланыЗанятостиТрудовыхРесурсов.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Изменяет регистратор движений - вместо документа ""План производства"" устанавливается ""Регистратор плана производства"".
	|Заполняет измерения ""Спецификация продукции"" и ""Назначение продукции"".
	|Отменяет движения в статусе ""Отменен""';
	|en = 'Changes the movements recorder - sets the ""Production plan recorder"" instead of the ""Production plan"" document.
	|Fills in the ""Product BOM"" and ""Product purpose"" dimensions
	|Cancels movements in the ""Canceled"" status.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.РегистраторПланаПроизводства.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.ПланыЗанятостиТрудовыхРесурсов.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыНакопления.ПланыЗанятостиТрудовыхРесурсов.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПланПроизводства.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПланыЗанятостиТрудовыхРесурсов.Регистратор КАК Ссылка
		|ИЗ
		|	РегистрНакопления.ПланыЗанятостиТрудовыхРесурсов КАК ПланыЗанятостиТрудовыхРесурсов
		|ГДЕ
		|	ПланыЗанятостиТрудовыхРесурсов.Регистратор ССЫЛКА Документ.ПланПроизводства
		|	ИЛИ ПланыЗанятостиТрудовыхРесурсов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПланов.Отменен)");
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = "РегистрНакопления.ПланыЗанятостиТрудовыхРесурсов";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры,
		Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"),
		ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт

	ПолноеИмяРегистра = "РегистрНакопления.ПланыЗанятостиТрудовыхРесурсов";
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПолноеИмяРегистра;
	
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьРегистраторыРегистраДляОбработки(
		Параметры.Очередь,
		,
		ПолноеИмяРегистра);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ПланыЗанятостиТрудовыхРесурсов.Период КАК Период,
		|	ПланыЗанятостиТрудовыхРесурсов.Регистратор КАК Регистратор,
		|	ПланыЗанятостиТрудовыхРесурсов.НомерСтроки КАК НомерСтроки,
		|	ПланыЗанятостиТрудовыхРесурсов.Активность КАК Активность,
		|	ПланыЗанятостиТрудовыхРесурсов.Сценарий КАК Сценарий,
		|	ПланыЗанятостиТрудовыхРесурсов.Статус КАК Статус,
		|	ПланыЗанятостиТрудовыхРесурсов.ВидРабот КАК ВидРабот,
		|	ПланыЗанятостиТрудовыхРесурсов.ПодразделениеДиспетчер КАК ПодразделениеДиспетчер,
		|	ПланыЗанятостиТрудовыхРесурсов.ПодразделениеИсполнитель КАК ПодразделениеИсполнитель,
		|	ПланыЗанятостиТрудовыхРесурсов.ПланПроизводства КАК ПланПроизводства,
		|	ПланыЗанятостиТрудовыхРесурсов.ДатаПроизводства КАК ДатаПроизводства,
		|	ПланыЗанятостиТрудовыхРесурсов.УдалитьСпецификация КАК СпецификацияПродукции,
		|	ПланыЗанятостиТрудовыхРесурсов.УдалитьНазначениеПродукции КАК НазначениеПродукции,
		|	ПланыЗанятостиТрудовыхРесурсов.НоменклатураПродукции КАК НоменклатураПродукции,
		|	ПланыЗанятостиТрудовыхРесурсов.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ПланыЗанятостиТрудовыхРесурсов.РазделительРасчета КАК РазделительРасчета,
		|	ПланыЗанятостиТрудовыхРесурсов.Количество КАК Количество,
		|	ПланыЗанятостиТрудовыхРесурсов.КоличествоНаЕдиницуПродукции КАК КоличествоНаЕдиницуПродукции,
		|	ПланыЗанятостиТрудовыхРесурсов.УдалитьСпецификация КАК УдалитьСпецификация,
		|	ПланыЗанятостиТрудовыхРесурсов.УдалитьНазначениеПродукции КАК УдалитьНазначениеПродукции
		|ИЗ
		|	РегистрНакопления.ПланыЗанятостиТрудовыхРесурсов КАК ПланыЗанятостиТрудовыхРесурсов
		|ГДЕ
		|	ПланыЗанятостиТрудовыхРесурсов.Регистратор = &Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	РегистраторПланаПроизводства.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.РегистраторПланаПроизводства КАК РегистраторПланаПроизводства
		|ГДЕ
		|	РегистраторПланаПроизводства.ПланПроизводства = &Регистратор
		|	И РегистраторПланаПроизводства.Проведен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДокументПланПроизводства.Статус КАК Статус
		|ИЗ
		|	Документ.ПланПроизводства КАК ДокументПланПроизводства
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РегистраторПланаПроизводства КАК РегистраторПланаПроизводства
		|		ПО РегистраторПланаПроизводства.ПланПроизводства = ДокументПланПроизводства.Ссылка
		|ГДЕ
		|	(ДокументПланПроизводства.Ссылка = &Регистратор
		|			ИЛИ РегистраторПланаПроизводства.Ссылка = &Регистратор)");
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра + ".НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", Выборка.Регистратор);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			ЭлементБлокировки = Блокировка.Добавить("Документ.РегистраторПланаПроизводства");
			Если ТипЗнч(Выборка.Регистратор) = Тип("ДокументСсылка.ПланПроизводства") Тогда
				ЭлементБлокировки.УстановитьЗначение("ПланПроизводства", Выборка.Регистратор);
			Иначе
				ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Регистратор);
			КонецЕсли;
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			
			Блокировка.Заблокировать();
			
			Запрос.УстановитьПараметр("Регистратор", Выборка.Регистратор);
			МассивРезультатов = Запрос.ВыполнитьПакет();
			
			ВыборкаСтатус = МассивРезультатов[2].Выбрать();
			
			Если ВыборкаСтатус.Следующий() 
				И ВыборкаСтатус.Статус = Перечисления.СтатусыПланов.Отменен Тогда
				
				НаборЗаписейСтарый = РегистрыНакопления.ПланыЗанятостиТрудовыхРесурсов.СоздатьНаборЗаписей();
				НаборЗаписейСтарый.Отбор.Регистратор.Установить(Выборка.Регистратор);
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписейСтарый);
				
			ИначеЕсли Не МассивРезультатов[0].Пустой()
				И Не МассивРезультатов[1].Пустой() Тогда
				
				НаборЗаписейНовый = РегистрыНакопления.ПланыЗанятостиТрудовыхРесурсов.СоздатьНаборЗаписей();
				НаборЗаписейНовый.Отбор.Регистратор.Установить(МассивРезультатов[1].Выгрузить()[0].Ссылка);
				НаборЗаписейНовый.Загрузить(МассивРезультатов[0].Выгрузить());
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписейНовый);
				
				НаборЗаписейСтарый = РегистрыНакопления.ПланыЗанятостиТрудовыхРесурсов.СоздатьНаборЗаписей();
				НаборЗаписейСтарый.Отбор.Регистратор.Установить(Выборка.Регистратор);
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписейСтарый);
				
			Иначе
				
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Регистратор, ДополнительныеПараметры);
				
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Выборка.Регистратор);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
