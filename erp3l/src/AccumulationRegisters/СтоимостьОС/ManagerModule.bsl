#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

//++ НЕ УТКА

// Заполняет параметры отражения движений регистра в финансовом учете
//
// Возвращаемое значение:
// 	см. МеждународныйУчетПоДаннымОстаточныхФинансовыхРегистров.ПараметрыОтраженияДвиженийВФинансовомУчете
//
Функция ПараметрыОтраженияДвиженийВФинансовомУчете() Экспорт
	
	ПараметрыОтражения = МеждународныйУчетПоДаннымОстаточныхФинансовыхРегистров.ПараметрыОтраженияДвиженийВФинансовомУчете();
	ПараметрыОтражения.ПутьКДаннымОбъектНастройки = "ГруппаФинансовогоУчета";
	ПараметрыОтражения.ПутьКДаннымНаправлениеДеятельности = "НаправлениеДеятельности";
	ПараметрыОтражения.ПутьКДаннымПодразделение = "Подразделение";
	ПараметрыОтражения.ПутьКДаннымМестоУчета = "Подразделение";
	ПараметрыОтражения.РесурсыУпр.Добавить("Стоимость");
	ПараметрыОтражения.РесурсыУпр.Добавить("ПриростСтоимости");
	ПараметрыОтражения.РесурсыРегл.Добавить("СтоимостьРегл");
	ПараметрыОтражения.РесурсыРегл.Добавить("СтоимостьЦФ");
	ПараметрыОтражения.РесурсыРегл.Добавить("ПриростСтоимостиРегл");
	
	МетаданныеРегистра = СоздатьНаборЗаписей().Метаданные();
	МеждународныйУчетПоДаннымОстаточныхФинансовыхРегистров.ЗаполнитьПараметрыОтраженияПоМетаданнымРегистра(ПараметрыОтражения, МетаданныеРегистра);
	
	Возврат ПараметрыОтражения;
	
КонецФункции

// Определяет источники уточнения счета, доступные в регистре и их свойства.
//
//
// Параметры:
//  СвойстваИсточника - Строка - "ИмяПоля" - имя атрибута регистра накопления, из которого планируется получать источник
//                               уточнения счета.
//
// Возвращаемое значение:
//  Соответствие - Ключ - название источника уточнения счета. 
//                 Значение - структура свойств источника уточнения счета.
//
Функция ИсточникиУточненияСчета(СвойстваИсточника) Экспорт
	
	Возврат ВнеоборотныеАктивыСлужебный.ИсточникиУточненияСчетаСтоимость(СвойстваИсточника);
	
КонецФункции

// Определяет источники подразделений регистра и их свойства.
//
//
// Возвращаемое значение:
//  Соответствие - Ключ - имя источника. 
//                 Значение - структура свойств источника. 
//
Функция ИсточникиПодразделений() Экспорт

	Возврат ВнеоборотныеАктивыСлужебный.ИсточникиПодразделенийСтоимость();

КонецФункции

// Определяет источники направлений регистра и их свойства.
//
//
// Возвращаемое значение:
//  Соответствие - Ключ - имя источника. 
//                 Значение - структура свойств источника. 
//
Функция ИсточникиНаправлений() Экспорт

	Возврат ВнеоборотныеАктивыСлужебный.ИсточникиНаправлений();

КонецФункции

// Определяет источники заполнения субконто.
//
//
// Возвращаемое значение:
//  Массив - массив атрибутов регистра.
//
Функция ИсточникиСубконто() Экспорт

	МассивСубконто = Новый Массив;
	МассивСубконто.Добавить("ОсновноеСредство");
	МассивСубконто.Добавить("СтатьяРасходов");
	МассивСубконто.Добавить("Контрагент");

	Возврат Новый Структура("СубконтоДт, СубконтоКт", МассивСубконто, МассивСубконто);

КонецФункции

// Определяет показатели в валюте регистра.
//
//
// Параметры:
//  СвойстваПоказателей - Строка - "ИсточникВалюты" - источник валюты для показателя регистра.
//
// Возвращаемое значение:
//  Соответствие - Ключ - имя показателя.
//                 Значение - структура свойств показателя.
//
Функция ПоказателиВВалюте(СвойстваПоказателей) Экспорт

	ПоказателиВВалюте = Новый Соответствие;
	Возврат ПоказателиВВалюте;

КонецФункции

// Определяет показатели в валюте регистра.
//
//
// Возвращаемое значение:
//  Соответствие - Ключ - имя показателя.
//                 Значение - структура свойств показателя.
//
Функция ПоказателиКоличества() Экспорт

	ПоказателиКоличества = Новый Соответствие;
	Возврат ПоказателиКоличества;

КонецФункции

//-- НЕ УТКА

// Определяет показатели регистра.
//
//
// Параметры:
//  Свойства - Структура - содержащая ключи СвойстваПоказателей, СвойстваРесурсов.
//
// Возвращаемое значение:
//  Соответствие - Ключ - имя показателя.
//                 Значение - структура свойств показателя.
//
Функция Показатели(Свойства) Экспорт

	Показатели = Новый Соответствие;
	
	СвойстваПоказателей = Свойства.СвойстваПоказателей;
	СвойстваРесурсов = Свойства.СвойстваРесурсов;
	
	МассивРесурсов = Новый Массив;
	МассивРесурсов.Добавить(Новый Структура(СвойстваРесурсов, "Стоимость", "ВалютаУпр"));
	МассивРесурсов.Добавить(Новый Структура(СвойстваРесурсов, "СтоимостьРегл", "ВалютаРегл"));//СтоимостьРегл = СтоимостьРегл + СтоимостьЦФ
	Показатели.Вставить(Перечисления.ПоказателиАналитическихРегистров.Сумма, Новый Структура(СвойстваПоказателей, МассивРесурсов));
	
	Возврат Показатели;
	
КонецФункции

// Собирает структуру из текстов запросов для дальнейшей проверки даты запрета.
// 
// Параметры:
// 	Запрос - Запрос - Запрос по проверке даты запрета, можно установить параметры
// Возвращаемое значение:
// 	Структура - см. ЗакрытиеМесяцаСервер.ИнициализироватьСтруктуруТекстовЗапросов
Функция ТекстЗапросаКонтрольДатыЗапрета(Запрос) Экспорт
	ИмяРегистра = Метаданные.РегистрыНакопления.СтоимостьОС.Имя;
	ИмяТаблицыИзменений = "СтоимостьОСИзменение"; 
	СтруктураТекстовЗапросов = ПроведениеДокументов.ШаблонТекстЗапросаКонтрольДатыЗапрета(Запрос, 
		ИмяРегистра, 
		ИмяТаблицыИзменений, 
		"ФинансовыйКонтур");
	Возврат СтруктураТекстовЗапросов

КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыНакопления.СтоимостьОС.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.6.32";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("0bf67f31-4424-49e5-ace8-4a813be9f07f");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.СтоимостьОС.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Обновляет движения документов по регистру накопления ""Стоимость ОС"":
	|- заполняет новый ресуср ""Не учитываемая стоимость (НУ)""';
	|en = 'Updates document register records for ""Fixed assets cost"" accumulation register:
	|- populates the new resource ""Disregarded cost (TA)""'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.ВводОстатковВнеоборотныхАктивов2_4.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ИзменениеПараметровОС2_4.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПеремещениеОС2_4.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПереоценкаОС2_4.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПодготовкаКПередачеОС2_4.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПринятиеКУчетуОС2_4.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.РазукомплектацияОС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.СписаниеОС2_4.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.СтоимостьОС.ПолноеИмя());
	//++ Локализация
	Читаемые.Добавить(Метаданные.Документы.РегламентнаяОперация.ПолноеИмя());
	//-- Локализация
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыНакопления.СтоимостьОС.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыНакопления.СтоимостьОС.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ВводОстатковВнеоборотныхАктивов2_4.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ИзменениеПараметровОС2_4.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПеремещениеОС2_4.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПереоценкаОС2_4.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПодготовкаКПередачеОС2_4.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ПринятиеКУчетуОС2_4.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.РазукомплектацияОС.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.СписаниеОС2_4.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

КонецПроцедуры

// Процедура регистрации данных для обработчика обновления ОбработатьДанныеДляПереходаНаВерсию.
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра  = "РегистрНакопления.СтоимостьОС";
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Период УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Период УБЫВ");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра();

	//++ Локализация
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ДанныеРегистра.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ ОбновляемыеДанныеТаблицаОС
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаОС.Ссылка КАК Регистратор,
	|		ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство
	|	ИЗ
	|		Документ.ВводОстатковВнеоборотныхАктивов2_4.ОС КАК ТаблицаОС
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК ДанныеРегистра
	|			ПО ДанныеРегистра.Регистратор = ТаблицаОС.Ссылка
	|				И ДанныеРегистра.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|				И ДанныеРегистра.НеУчитываемаяСтоимостьНУ <> 0
	|				И ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	ГДЕ
	|		ТаблицаОС.КорректировкаСтоимостиАрендованногоИмуществаНУ <> 0
	|		И ТаблицаОС.Ссылка.Проведен
	|		И ДанныеРегистра.Регистратор ЕСТЬ NULL
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ДанныеДокумента.Ссылка КАК Регистратор,
	|		ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство
	|	ИЗ
	|		Документ.ПринятиеКУчетуОС2_4 КАК ДанныеДокумента
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС2_4.ОС КАК ТаблицаОС
	|			ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный КАК ДанныеРегистраХозрасчетный
	|			ПО ДанныеРегистраХозрасчетный.Регистратор = ДанныеДокумента.Ссылка
	|				И ДанныеРегистраХозрасчетный.СчетДт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КорректировкаСтоимостиАрендованногоИмущества)
	|				И ДанныеРегистраХозрасчетный.СуммаНУДт <> 0
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК ДанныеРегистра
	|			ПО ДанныеРегистра.Регистратор = ТаблицаОС.Ссылка
	|				И ДанныеРегистра.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|				И ДанныеРегистра.НеУчитываемаяСтоимостьНУ <> 0
	|				И ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	ГДЕ
	|		ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуПредметовАренды)
	|		И ДанныеДокумента.Проведен
	|		И ДанныеРегистра.Регистратор ЕСТЬ NULL
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		РасчетРасходовПоАренде.Регистратор КАК Регистратор,
	|		РасчетРасходовПоАренде.ОсновноеСредство КАК ОсновноеСредство
	|	ИЗ
	|		РегистрСведений.РасчетРасходовПоАренде КАК РасчетРасходовПоАренде
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК ДанныеРегистра
	|			ПО ДанныеРегистра.Регистратор = РасчетРасходовПоАренде.Регистратор
	|				И ДанныеРегистра.НеУчитываемаяСтоимостьНУ <> 0
	|				И ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	ГДЕ
	|		РасчетРасходовПоАренде.Регистратор ССЫЛКА Документ.РегламентнаяОперация
	|		И (РасчетРасходовПоАренде.СуммаПлатежаНУ <> 0
	|			ИЛИ РасчетРасходовПоАренде.КорректировкаАмортизацииНУ <> 0)
	|		И ДанныеРегистра.Регистратор ЕСТЬ NULL
	|
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ЗаписиРегистра.Регистратор КАК Регистратор,
	|		ЗаписиРегистра.ОсновноеСредство КАК ОсновноеСредство
	|	ИЗ
	|		РегистрНакопления.СтоимостьОС КАК ЗаписиРегистра
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов2_4.ОС КАК ТаблицаОС
	|			ПО ТаблицаОС.Ссылка = ЗаписиРегистра.Регистратор
	|			И ТаблицаОС.ОсновноеСредство = ЗаписиРегистра.ОсновноеСредство
	|	ГДЕ
	|		ЗаписиРегистра.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
	|		И (ТаблицаОС.РезервПереоценкиСтоимости - ТаблицаОС.РезервПереоценкиАмортизации > 0
	|				ИЛИ ТаблицаОС.РезервПереоценкиСтоимостиРегл - ТаблицаОС.РезервПереоценкиАмортизацииРегл > 0)
	|
	|		И НЕ ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				РегистрНакопления.СтоимостьОС КАК ЗаписиРегистраПроверка
	|			ГДЕ
	|				ЗаписиРегистраПроверка.Регистратор = ЗаписиРегистра.Регистратор
	|				И (ЗаписиРегистраПроверка.ПриростСтоимости <> 0
	|					ИЛИ ЗаписиРегистраПроверка.ПриростСтоимостиРегл <> 0))
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ЗаписиРегистра.Регистратор КАК Регистратор,
	|		ЗаписиРегистра.ОсновноеСредство КАК ОсновноеСредство
	|	ИЗ
	|		РегистрНакопления.СтоимостьОС КАК ЗаписиРегистра
	|	ГДЕ
	|		ЗаписиРегистра.Регистратор ССЫЛКА Документ.ПереоценкаОС2_4
	|
	|		И (ЗаписиРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				И ЗаписиРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.УвеличениеСтоимостиОС)
	|				И (ЗаписиРегистра.СтоимостьРегл <> 0 ИЛИ ЗаписиРегистра.Стоимость <> 0)
	|
	|			ИЛИ ЗаписиРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				И ЗаписиРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.УменьшениеВеличиныДооценкиСтоимостиОС)
	|				И (ЗаписиРегистра.РезервПереоценкиСтоимости > 0 ИЛИ ЗаписиРегистра.РезервПереоценкиСтоимостиРегл > 0))
	|
	|		И НЕ ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				РегистрНакопления.СтоимостьОС КАК ЗаписиРегистраПроверка
	|			ГДЕ
	|				ЗаписиРегистраПроверка.Регистратор = ЗаписиРегистра.Регистратор
	|				И (ЗаписиРегистраПроверка.ПриростСтоимости <> 0
	|					ИЛИ ЗаписиРегистраПроверка.ПриростСтоимостиРегл <> 0))
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ЗаписиРегистра.Регистратор КАК Регистратор,
	|		ЗаписиРегистра.ОсновноеСредство КАК ОсновноеСредство
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ЗаписиРегистра.Регистратор КАК Регистратор,
	|			ЗаписиРегистра.ОсновноеСредство КАК ОсновноеСредство
	|		ИЗ
	|			(ВЫБРАТЬ
	|				ЗаписиРегистра.Регистратор КАК Регистратор,
	|				ЗаписиРегистра.ОсновноеСредство КАК ОсновноеСредство,
	|				ЗаписиРегистра.РезервПереоценкиСтоимости КАК РезервПереоценкиСтоимости,
	|				ЗаписиРегистра.РезервПереоценкиСтоимостиРегл КАК РезервПереоценкиСтоимостиРегл
	|			ИЗ
	|				РегистрНакопления.СтоимостьОС КАК ЗаписиРегистра
	|			ГДЕ
	|				ЗаписиРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				И (ЗаписиРегистра.РезервПереоценкиСтоимости <> 0
	|						ИЛИ ЗаписиРегистра.РезервПереоценкиСтоимостиРегл <> 0)
	|				И (ЗаписиРегистра.Регистратор ССЫЛКА Документ.ИзменениеПараметровОС2_4
	|						ИЛИ ЗаписиРегистра.Регистратор ССЫЛКА Документ.ПеремещениеОС2_4
	|						ИЛИ ЗаписиРегистра.Регистратор ССЫЛКА Документ.ПодготовкаКПередачеОС2_4
	|						ИЛИ ЗаписиРегистра.Регистратор ССЫЛКА Документ.РазукомплектацияОС
	|						ИЛИ ЗаписиРегистра.Регистратор ССЫЛКА Документ.СписаниеОС2_4)
	|
	|			ОБЪЕДИНИТЬ ВСЕ
	|
	|			ВЫБРАТЬ
	|				ЗаписиРегистра.Регистратор КАК Регистратор,
	|				ЗаписиРегистра.ОсновноеСредство КАК ОсновноеСредство,
	|				-ЗаписиРегистра.РезервПереоценкиАмортизации КАК РезервПереоценкиАмортизации,
	|				-ЗаписиРегистра.РезервПереоценкиАмортизацииРегл КАК РезервПереоценкиАмортизацииРегл
	|			ИЗ
	|				РегистрНакопления.АмортизацияОС КАК ЗаписиРегистра
	|			ГДЕ
	|				ЗаписиРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				И (ЗаписиРегистра.РезервПереоценкиАмортизации <> 0
	|						ИЛИ ЗаписиРегистра.РезервПереоценкиАмортизацииРегл <> 0)
	|				И (ЗаписиРегистра.Регистратор ССЫЛКА Документ.ИзменениеПараметровОС2_4
	|						ИЛИ ЗаписиРегистра.Регистратор ССЫЛКА Документ.ПеремещениеОС2_4
	|						ИЛИ ЗаписиРегистра.Регистратор ССЫЛКА Документ.ПодготовкаКПередачеОС2_4
	|						ИЛИ ЗаписиРегистра.Регистратор ССЫЛКА Документ.РазукомплектацияОС
	|						ИЛИ ЗаписиРегистра.Регистратор ССЫЛКА Документ.СписаниеОС2_4)
	|
	|			) КАК ЗаписиРегистра
	|
	|		СГРУППИРОВАТЬ ПО
	|			ЗаписиРегистра.Регистратор,
	|			ЗаписиРегистра.ОсновноеСредство
	|
	|		ИМЕЮЩИЕ
	|			СУММА(ЗаписиРегистра.РезервПереоценкиСтоимости) > 0
	|				ИЛИ СУММА(ЗаписиРегистра.РезервПереоценкиСтоимостиРегл) > 0
	|		) КАК ЗаписиРегистра
	|	ГДЕ
	|		НЕ ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				РегистрНакопления.СтоимостьОС КАК ЗаписиРегистраПроверка
	|			ГДЕ
	|				ЗаписиРегистраПроверка.Регистратор = ЗаписиРегистра.Регистратор
	|				И (ЗаписиРегистраПроверка.ПриростСтоимости <> 0
	|					ИЛИ ЗаписиРегистраПроверка.ПриростСтоимостиРегл <> 0))
	|
	|	) КАК ДанныеРегистра
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Регистратор
	|ИЗ
	|	(ВЫБРАТЬ
	|		Регистратор.Ссылка КАК Регистратор
	|	ИЗ
	|		ОбновляемыеДанныеТаблицаОС КАК ТаблицаОС
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ДокументыПоОС.Ссылка КАК Регистратор
	|	ИЗ
	|		РегистрСведений.ДокументыПоОС КАК ДокументыПоОС
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументов
	|			ПО РеестрДокументов.Ссылка = ДокументыПоОС.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК ДанныеРегистра
	|			ПО ДанныеРегистра.Регистратор = ДокументыПоОС.Ссылка
	|				И ДанныеРегистра.НеУчитываемаяСтоимостьНУ <> 0
	|				И ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	ГДЕ
	|		ДокументыПоОС.ТипСсылки В (&ПрочиеДокументы)
	|		И РеестрДокументов.Проведен
	|		И ДанныеРегистра.Регистратор ЕСТЬ NULL
	|		И ДокументыПоОС.ОсновноеСредство В (ВЫБРАТЬ Т.ОсновноеСредство ИЗ ОбновляемыеДанныеТаблицаОС КАК Т)
	|
	|	) КАК ДанныеРегистра";
	
	ПрочиеДокументы = Новый Массив;
	ПрочиеДокументы.Добавить(ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ИзменениеПараметровОС2_4"));
	ПрочиеДокументы.Добавить(ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ПеремещениеОС2_4"));
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ПрочиеДокументы", ПрочиеДокументы);
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяРегистра);
	//-- Локализация
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрНакопления.СтоимостьОС";
	ИмяРегистра = "СтоимостьОС";
	МетаданныеРегистра = Метаданные.РегистрыНакопления.СтоимостьОС;
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяРегистра);
		Возврат;
	КонецЕсли;
	
	//++ Локализация
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЧитаемыеДанные = Новый Массив;
	ЧитаемыеДанные.Добавить("Документ.ВводОстатковВнеоборотныхАктивов2_4");
	ЧитаемыеДанные.Добавить("Документ.ИзменениеПараметровОС2_4");
	ЧитаемыеДанные.Добавить("Документ.ПеремещениеОС2_4");
	ЧитаемыеДанные.Добавить("Документ.ПринятиеКУчетуОС2_4");
	ЧитаемыеДанные.Добавить("Документ.РегламентнаяОперация");
	ВТЗаблокированныеСсылки = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияСсылок(
								Параметры.Очередь, ЧитаемыеДанные, МенеджерВременныхТаблиц);
								
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ОбновляемыеДанные.Регистратор
	|ПОМЕСТИТЬ ВсеОбновляемые
	|ИЗ
	|	&ОбновляемыеДанные КАК ОбновляемыеДанные
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеОбновляемые.Регистратор
	|ПОМЕСТИТЬ ОбновляемыеДанные
	|ИЗ
	|	ВсеОбновляемые КАК ВсеОбновляемые
	|ГДЕ
	|	НЕ Регистратор В
	|		(ВЫБРАТЬ
	|			Т.Ссылка
	|		ИЗ
	|			ВТЗаблокировано КАК Т)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВсеОбновляемые";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТЗаблокировано", ВТЗаблокированныеСсылки.ИмяВременнойТаблицы);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ОбновляемыеДанные", ОбновляемыеДанные);
	Запрос.Выполнить();
	
	// Обновление через получение данных из документа
	#Область ПростоеОбновление
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОбновляемыеДанные.Регистратор
	|ИЗ
	|	ОбновляемыеДанные КАК ОбновляемыеДанные
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ОбновляемыеДанные.Регистратор) = ТИП(Документ.ВводОстатковВнеоборотныхАктивов2_4)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
	
		ВнеоборотныеАктивы.ПолучитьДвиженияИЗаписатьПриОбновленииИБ(
			ИмяРегистра,
			ПолноеИмяРегистра,
			Выборка.Регистратор,
			МетаданныеРегистра,
			Параметры);
	
	КонецЦикла;
	#КонецОбласти
	
	// Обновление путем явного формирования таблиц
	#Область СложноеОбновление
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтоимостьОС.Регистратор,
	|	СтоимостьОС.Период,
	|	СтоимостьОС.ОсновноеСредство,
	|	СтоимостьОС.Подразделение,
	|	СтоимостьОС.ГруппаФинансовогоУчета,
	|	СтоимостьОС.НаправлениеДеятельности
	|ПОМЕСТИТЬ ОбновляемыеДанныеТаблицаОС
	|ИЗ
	|	РегистрНакопления.СтоимостьОС КАК СтоимостьОС
	|ГДЕ
	|	СтоимостьОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И (СтоимостьОС.Регистратор ССЫЛКА Документ.ИзменениеПараметровОС2_4
	|		ИЛИ СтоимостьОС.Регистратор ССЫЛКА Документ.ПеремещениеОС2_4)
	|	И СтоимостьОС.Регистратор В
	|			(ВЫБРАТЬ
	|				ОбновляемыеДанные.Регистратор
	|			ИЗ
	|				ОбновляемыеДанные КАК ОбновляемыеДанные)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.Регистратор КАК Регистратор,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	МАКСИМУМ(ДанныеРегистра.Период) КАК ПериодСреза
	|ПОМЕСТИТЬ втМестонахождениеОС_Период
	|ИЗ
	|	РегистрСведений.РасчетРасходовПоАренде КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС КАК ДанныеРегистра
	|		ПО ДанныеРегистра.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|			И ДанныеРегистра.Активность
	|			И (ДанныеРегистра.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
	|					И ДанныеРегистра.Период < ТаблицаОС.ПериодРасчета
	|				ИЛИ НЕ ДанныеРегистра.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
	|					И ДанныеРегистра.Период < НАЧАЛОПЕРИОДА(ТаблицаОС.ПериодРасчета, МЕСЯЦ))
	|ГДЕ
	|	ТаблицаОС.Регистратор В
	|		(ВЫБРАТЬ
	|			ОбновляемыеДанные.Регистратор
	|		ИЗ
	|			ОбновляемыеДанные КАК ОбновляемыеДанные)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОС.Регистратор,
	|	ТаблицаОС.ОсновноеСредство
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПериодСреза
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра_Период.Регистратор,
	|	ДанныеРегистра_Период.ОсновноеСредство,
	|	МАКСИМУМ(ДанныеРегистра.Регистратор) КАК РегистраторСреза
	|ПОМЕСТИТЬ втМестонахождениеОС_Регистратор
	|ИЗ
	|	втМестонахождениеОС_Период КАК ДанныеРегистра_Период
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС КАК ДанныеРегистра
	|		ПО ДанныеРегистра.ОсновноеСредство = ДанныеРегистра_Период.ОсновноеСредство
	|			И ДанныеРегистра.Период = ДанныеРегистра_Период.ПериодСреза
	|			И ДанныеРегистра.Активность
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистра_Период.Регистратор,
	|	ДанныеРегистра_Период.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	РегистраторСреза
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра_Регистратор.Регистратор КАК Регистратор,
	|	ДанныеРегистра_Регистратор.ОсновноеСредство,
	|	ДанныеРегистра.Местонахождение КАК Местонахождение
	|ПОМЕСТИТЬ втМестонахождениеОС
	|ИЗ
	|	втМестонахождениеОС_Регистратор КАК ДанныеРегистра_Регистратор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС КАК ДанныеРегистра
	|		ПО ДанныеРегистра.ОсновноеСредство = ДанныеРегистра_Регистратор.ОсновноеСредство
	|			И ДанныеРегистра.Регистратор = ДанныеРегистра_Регистратор.РегистраторСреза
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втМестонахождениеОС_Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втМестонахождениеОС_Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОС.Регистратор КАК Регистратор,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	МАКСИМУМ(ДанныеРегистра.Период) КАК ПериодСреза
	|ПОМЕСТИТЬ втПорядокУчетаОС_Период
	|ИЗ
	|	РегистрСведений.РасчетРасходовПоАренде КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС КАК ДанныеРегистра
	|		ПО ДанныеРегистра.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|			И ДанныеРегистра.Активность
	|			И (ДанныеРегистра.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
	|					И ДанныеРегистра.Период < ТаблицаОС.ПериодРасчета
	|				ИЛИ НЕ ДанныеРегистра.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
	|					И ДанныеРегистра.Период < НАЧАЛОПЕРИОДА(ТаблицаОС.ПериодРасчета, МЕСЯЦ))
	|ГДЕ
	|	ТаблицаОС.Регистратор В
	|		(ВЫБРАТЬ
	|			ОбновляемыеДанные.Регистратор
	|		ИЗ
	|			ОбновляемыеДанные КАК ОбновляемыеДанные)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОС.Регистратор,
	|	ТаблицаОС.ОсновноеСредство
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПериодСреза
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра_Период.Регистратор КАК Регистратор,
	|	ДанныеРегистра_Период.ОсновноеСредство КАК ОсновноеСредство,
	|	МАКСИМУМ(ДанныеРегистра.Регистратор) КАК РегистраторСреза
	|ПОМЕСТИТЬ втПорядокУчетаОС_Регистратор
	|ИЗ
	|	втПорядокУчетаОС_Период КАК ДанныеРегистра_Период
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС КАК ДанныеРегистра
	|		ПО ДанныеРегистра.ОсновноеСредство = ДанныеРегистра_Период.ОсновноеСредство
	|			И ДанныеРегистра.Период = ДанныеРегистра_Период.ПериодСреза
	|			И ДанныеРегистра.Активность
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистра_Период.Регистратор,
	|	ДанныеРегистра_Период.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	РегистраторСреза
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра_Регистратор.Регистратор КАК Регистратор,
	|	ДанныеРегистра_Регистратор.ОсновноеСредство КАК ОсновноеСредство,
	|	ДанныеРегистра.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ДанныеРегистра.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ втПорядокУчетаОС
	|ИЗ
	|	втПорядокУчетаОС_Регистратор КАК ДанныеРегистра_Регистратор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС КАК ДанныеРегистра
	|		ПО ДанныеРегистра.ОсновноеСредство = ДанныеРегистра_Регистратор.ОсновноеСредство
	|			И ДанныеРегистра.Регистратор = ДанныеРегистра_Регистратор.РегистраторСреза
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втПорядокУчетаОС_Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втПорядокУчетаОС_Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Движения.Регистратор КАК Регистратор,
	|	Движения.ОсновноеСредство КАК ОсновноеСредство,
	|	СУММА(Движения.НеУчитываемаяСтоимостьНУ) КАК НеУчитываемаяСтоимостьНУ
	|
	|ПОМЕСТИТЬ втНеУчитываемаяСтоимостьНУ
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОбновляемыеДанныеТаблицаОС.Регистратор КАК Регистратор,
	|		ОбновляемыеДанныеТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|		ТаблицаОС.КорректировкаСтоимостиАрендованногоИмуществаНУ КАК НеУчитываемаяСтоимостьНУ
	|	ИЗ
	|		ОбновляемыеДанныеТаблицаОС КАК ОбновляемыеДанныеТаблицаОС
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов2_4.ОС КАК ТаблицаОС
	|			ПО ТаблицаОС.ОсновноеСредство = ОбновляемыеДанныеТаблицаОС.ОсновноеСредство
	|				И ТаблицаОС.Ссылка.Дата <= ОбновляемыеДанныеТаблицаОС.Период
	|				И ТаблицаОС.Ссылка <> ОбновляемыеДанныеТаблицаОС.Регистратор
	|				И ТаблицаОС.КорректировкаСтоимостиАрендованногоИмуществаНУ <> 0
	|				И ТаблицаОС.Ссылка.Проведен
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ОбновляемыеДанныеТаблицаОС.Регистратор КАК Регистратор,
	|		ОбновляемыеДанныеТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|		ТаблицаОС.УдалитьКорректировкаСтоимостиНУ КАК НеУчитываемаяСтоимостьНУ
	|	ИЗ
	|		ОбновляемыеДанныеТаблицаОС КАК ОбновляемыеДанныеТаблицаОС
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК ТаблицаОС
	|			ПО ТаблицаОС.ОсновноеСредство = ОбновляемыеДанныеТаблицаОС.ОсновноеСредство
	|				И ТаблицаОС.Период <= ОбновляемыеДанныеТаблицаОС.Период
	|				И ТаблицаОС.Регистратор <> ОбновляемыеДанныеТаблицаОС.Регистратор
	|				И ТаблицаОС.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
	|				И ТаблицаОС.УдалитьКорректировкаСтоимостиНУ <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ОбновляемыеДанныеТаблицаОС.Регистратор КАК Регистратор,
	|		ОбновляемыеДанныеТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|		- РасчетРасходовПоАренде.СуммаПлатежаНУ - РасчетРасходовПоАренде.КорректировкаАмортизацииНУ КАК НеУчитываемаяСтоимостьНУ
	|	ИЗ
	|		ОбновляемыеДанныеТаблицаОС КАК ОбновляемыеДанныеТаблицаОС
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РасчетРасходовПоАренде КАК РасчетРасходовПоАренде
	|			ПО РасчетРасходовПоАренде.ОсновноеСредство = ОбновляемыеДанныеТаблицаОС.ОсновноеСредство
	|				И НАЧАЛОПЕРИОДА(РасчетРасходовПоАренде.ПериодРасчета, МЕСЯЦ) <= ОбновляемыеДанныеТаблицаОС.Период
	|				И РасчетРасходовПоАренде.Регистратор <> ОбновляемыеДанныеТаблицаОС.Регистратор
	|				И (РасчетРасходовПоАренде.СуммаПлатежаНУ <> 0
	|					ИЛИ РасчетРасходовПоАренде.КорректировкаАмортизацииНУ <> 0)
	|
	|	) КАК Движения
	|	
	|СГРУППИРОВАТЬ ПО
	|	Движения.Регистратор,
	|	Движения.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РезервДооценки.Регистратор КАК Регистратор,
	|	РезервДооценки.ОсновноеСредство КАК ОсновноеСредство,
	|	РезервДооценки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	РезервДооценки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	РезервДооценки.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|
	|	ВЫБОР
	|		КОГДА СУММА(РезервДооценки.Сумма) > 0
	|			ТОГДА СУММА(РезервДооценки.Сумма)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПриростСтоимости,
	|
	|	ВЫБОР
	|		КОГДА СУММА(РезервДооценки.СуммаРегл) > 0
	|			ТОГДА СУММА(РезервДооценки.СуммаРегл)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПриростСтоимостиРегл
	|
	|ПОМЕСТИТЬ втРезервДооценки
	|ИЗ
	|	(ВЫБРАТЬ
	|		СтоимостьОС.Регистратор КАК Регистратор,
	|		СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство,
	|		СтоимостьОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|		СтоимостьОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		СтоимостьОС.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|		СтоимостьОС.РезервПереоценкиСтоимости КАК Сумма,
	|		СтоимостьОС.РезервПереоценкиСтоимостиРегл КАК СуммаРегл
	|	ИЗ
	|		РегистрНакопления.СтоимостьОС КАК СтоимостьОС
	|	ГДЕ
	|		СтоимостьОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И (СтоимостьОС.РезервПереоценкиСтоимости > 0
	|			ИЛИ СтоимостьОС.РезервПереоценкиСтоимостиРегл > 0)
	|		И СтоимостьОС.Регистратор В
	|				(ВЫБРАТЬ
	|					ОбновляемыеДанные.Регистратор
	|				ИЗ
	|					ОбновляемыеДанные КАК ОбновляемыеДанные)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		АмортизацияОС.Регистратор,
	|		АмортизацияОС.ОсновноеСредство,
	|		АмортизацияОС.ГруппаФинансовогоУчета,
	|		АмортизацияОС.НаправлениеДеятельности,
	|		АмортизацияОС.ИдентификаторФинЗаписи,
	|		-АмортизацияОС.РезервПереоценкиАмортизации,
	|		-АмортизацияОС.РезервПереоценкиАмортизацииРегл
	|	ИЗ
	|		РегистрНакопления.АмортизацияОС КАК АмортизацияОС
	|	ГДЕ
	|		АмортизацияОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И (АмортизацияОС.РезервПереоценкиАмортизации > 0
	|			ИЛИ АмортизацияОС.РезервПереоценкиАмортизацииРегл > 0)
	|		И АмортизацияОС.Регистратор В
	|				(ВЫБРАТЬ
	|					ОбновляемыеДанные.Регистратор
	|				ИЗ
	|					ОбновляемыеДанные КАК ОбновляемыеДанные)) КАК РезервДооценки
	|
	|СГРУППИРОВАТЬ ПО
	|	РезервДооценки.Регистратор,
	|	РезервДооценки.ОсновноеСредство,
	|	РезервДооценки.ГруппаФинансовогоУчета,
	|	РезервДооценки.НаправлениеДеятельности,
	|	РезервДооценки.ИдентификаторФинЗаписи
	|
	|ИМЕЮЩИЕ
	|	(СУММА(РезервДооценки.Сумма) > 0
	|		ИЛИ СУММА(РезервДооценки.СуммаРегл) > 0)
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// ИзменениеПараметровОС2_4
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)    КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                    КАК Регистратор,
	|	ДанныеДокумента.Дата                      КАК Период,
	|	ДанныеДокумента.Организация               КАК Организация,
	|
	|	ОбновляемыеДанныеТаблицаОС.ОсновноеСредство         КАК ОсновноеСредство,
	|	ОбновляемыеДанныеТаблицаОС.Подразделение            КАК Подразделение,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|			ТОГДА ДанныеДокумента.ГруппаФинансовогоУчета
	|		ИНАЧЕ ОбновляемыеДанныеТаблицаОС.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.НаправлениеДеятельностиФлаг
	|			ТОГДА ДанныеДокумента.НаправлениеДеятельности
	|		ИНАЧЕ ОбновляемыеДанныеТаблицаОС.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|
	|	0     КАК ПриростСтоимости,
	|	0     КАК ПриростСтоимостиРегл,
	|
	|	втНеУчитываемаяСтоимостьНУ.НеУчитываемаяСтоимостьНУ КАК НеУчитываемаяСтоимостьНУ,
	|
	|	ДанныеДокумента.Организация КАК КорОрганизация,
	|	ОбновляемыеДанныеТаблицаОС.Подразделение КАК КорПодразделение,
	|	ОбновляемыеДанныеТаблицаОС.ГруппаФинансовогоУчета КАК КорГруппаФинансовогоУчета,
	|	ОбновляемыеДанныеТаблицаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	
	|	ДанныеДокументаТаблицаОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровСтоимостиОС) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровСтоимостиОС) КАК НастройкаХозяйственнойОперации
	|ПОМЕСТИТЬ втТаблицыДвижений	
	|ИЗ
	|	Документ.ИзменениеПараметровОС2_4 КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбновляемыеДанныеТаблицаОС КАК ОбновляемыеДанныеТаблицаОС
	|		ПО ОбновляемыеДанныеТаблицаОС.Регистратор = ДанныеДокумента.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровОС2_4.ОС КАК ДанныеДокументаТаблицаОС
	|		ПО ДанныеДокументаТаблицаОС.Ссылка = ОбновляемыеДанныеТаблицаОС.Регистратор
	|			И ДанныеДокументаТаблицаОС.ОсновноеСредство = ОбновляемыеДанныеТаблицаОС.ОсновноеСредство
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНеУчитываемаяСтоимостьНУ КАК втНеУчитываемаяСтоимостьНУ
	|		ПО втНеУчитываемаяСтоимостьНУ.Регистратор = ОбновляемыеДанныеТаблицаОС.Регистратор
	|			И втНеУчитываемаяСтоимостьНУ.ОсновноеСредство = ОбновляемыеДанныеТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|		ИЛИ ДанныеДокумента.НаправлениеДеятельностиФлаг
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)    КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                    КАК Регистратор,
	|	ДанныеДокумента.Дата                      КАК Период,
	|	ДанныеДокумента.Организация               КАК Организация,
	|
	|	ОбновляемыеДанныеТаблицаОС.ОсновноеСредство         КАК ОсновноеСредство,
	|	ОбновляемыеДанныеТаблицаОС.Подразделение            КАК Подразделение,
	|
	|	ОбновляемыеДанныеТаблицаОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ОбновляемыеДанныеТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|
	|	0     КАК ПриростСтоимости,
	|	0     КАК ПриростСтоимостиРегл,
	|
	|	втНеУчитываемаяСтоимостьНУ.НеУчитываемаяСтоимостьНУ КАК НеУчитываемаяСтоимостьНУ,
	|
	|	ДанныеДокумента.Организация КАК КорОрганизация,
	|	ОбновляемыеДанныеТаблицаОС.Подразделение КАК КорПодразделение,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|			ТОГДА ДанныеДокумента.ГруппаФинансовогоУчета
	|		ИНАЧЕ ОбновляемыеДанныеТаблицаОС.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК КорГруппаФинансовогоУчета,
	|	
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|			ТОГДА ДанныеДокумента.ГруппаФинансовогоУчета
	|		ИНАЧЕ ОбновляемыеДанныеТаблицаОС.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК КорНаправлениеДеятельности,
	|
	|	ДанныеДокументаТаблицаОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровСтоимостиОС) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровСтоимостиОС) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	Документ.ИзменениеПараметровОС2_4 КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбновляемыеДанныеТаблицаОС КАК ОбновляемыеДанныеТаблицаОС
	|		ПО ОбновляемыеДанныеТаблицаОС.Регистратор = ДанныеДокумента.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровОС2_4.ОС КАК ДанныеДокументаТаблицаОС
	|		ПО ДанныеДокументаТаблицаОС.Ссылка = ОбновляемыеДанныеТаблицаОС.Регистратор
	|			И ДанныеДокументаТаблицаОС.ОсновноеСредство = ОбновляемыеДанныеТаблицаОС.ОсновноеСредство
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНеУчитываемаяСтоимостьНУ КАК втНеУчитываемаяСтоимостьНУ
	|		ПО втНеУчитываемаяСтоимостьНУ.Регистратор = ОбновляемыеДанныеТаблицаОС.Регистратор
	|			И втНеУчитываемаяСтоимостьНУ.ОсновноеСредство = ОбновляемыеДанныеТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|		ИЛИ ДанныеДокумента.НаправлениеДеятельностиФлаг
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// ПеремещениеОС2_4
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)    КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                    КАК Регистратор,
	|	ДанныеДокумента.Дата                      КАК Период,
	|	ДанныеДокумента.ОрганизацияПолучатель     КАК Организация,
	|
	|	ОбновляемыеДанныеТаблицаОС.ОсновноеСредство  КАК ОсновноеСредство,
	|	ДанныеДокумента.ПодразделениеПолучатель      КАК Подразделение,
	|
	|	ОбновляемыеДанныеТаблицаОС.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|	ОбновляемыеДанныеТаблицаОС.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|
	|	0     КАК ПриростСтоимости,
	|	0     КАК ПриростСтоимостиРегл,
	|
	|	втНеУчитываемаяСтоимостьНУ.НеУчитываемаяСтоимостьНУ КАК НеУчитываемаяСтоимостьНУ,
	|
	|	ДанныеДокумента.Организация КАК КорОрганизация,
	|	ДанныеДокумента.Подразделение КАК КорПодразделение,
	|	ОбновляемыеДанныеТаблицаОС.НаправлениеДеятельности  КАК КорНаправлениеДеятельности,
	|	ОбновляемыеДанныеТаблицаОС.ГруппаФинансовогоУчета   КАК КорГруппаФинансовогоУчета,
	|	
	|	ДанныеДокументаТаблицаОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПеремещениеОСвПодразделениеВыделенноеНаБаланс) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	Документ.ПеремещениеОС2_4 КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбновляемыеДанныеТаблицаОС КАК ОбновляемыеДанныеТаблицаОС
	|		ПО ОбновляемыеДанныеТаблицаОС.Регистратор = ДанныеДокумента.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПеремещениеОС2_4.ОС КАК ДанныеДокументаТаблицаОС
	|		ПО ДанныеДокументаТаблицаОС.Ссылка = ОбновляемыеДанныеТаблицаОС.Регистратор
	|			И ДанныеДокументаТаблицаОС.ОсновноеСредство = ОбновляемыеДанныеТаблицаОС.ОсновноеСредство
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНеУчитываемаяСтоимостьНУ КАК втНеУчитываемаяСтоимостьНУ
	|		ПО втНеУчитываемаяСтоимостьНУ.Регистратор = ОбновляемыеДанныеТаблицаОС.Регистратор
	|			И втНеУчитываемаяСтоимостьНУ.ОсновноеСредство = ОбновляемыеДанныеТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	(ДанныеДокумента.Организация <> ДанныеДокумента.ОрганизацияПолучатель
	|		ИЛИ ДанныеДокумента.Подразделение <> ДанныеДокумента.ПодразделениеПолучатель)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)    КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                    КАК Регистратор,
	|	ДанныеДокумента.Дата                      КАК Период,
	|	ДанныеДокумента.Организация               КАК Организация,
	|
	|	ОбновляемыеДанныеТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|
	|	ОбновляемыеДанныеТаблицаОС.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|	ОбновляемыеДанныеТаблицаОС.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|
	|	0     КАК ПриростСтоимости,
	|	0     КАК ПриростСтоимостиРегл,
	|
	|	втНеУчитываемаяСтоимостьНУ.НеУчитываемаяСтоимостьНУ КАК НеУчитываемаяСтоимостьНУ,
	|
	|	ДанныеДокумента.ОрганизацияПолучатель КАК КорОрганизация,
	|	ДанныеДокумента.ПодразделениеПолучатель КАК КорПодразделение,
	|	ОбновляемыеДанныеТаблицаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	ОбновляемыеДанныеТаблицаОС.ГруппаФинансовогоУчета КАК КорГруппаФинансовогоУчета,
	|	
	|	ДанныеДокументаТаблицаОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПеремещениеОСвПодразделениеВыделенноеНаБаланс) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	Документ.ПеремещениеОС2_4 КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбновляемыеДанныеТаблицаОС КАК ОбновляемыеДанныеТаблицаОС
	|		ПО ОбновляемыеДанныеТаблицаОС.Регистратор = ДанныеДокумента.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПеремещениеОС2_4.ОС КАК ДанныеДокументаТаблицаОС
	|		ПО ДанныеДокументаТаблицаОС.Ссылка = ОбновляемыеДанныеТаблицаОС.Регистратор
	|			И ДанныеДокументаТаблицаОС.ОсновноеСредство = ОбновляемыеДанныеТаблицаОС.ОсновноеСредство
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНеУчитываемаяСтоимостьНУ КАК втНеУчитываемаяСтоимостьНУ
	|		ПО втНеУчитываемаяСтоимостьНУ.Регистратор = ОбновляемыеДанныеТаблицаОС.Регистратор
	|			И втНеУчитываемаяСтоимостьНУ.ОсновноеСредство = ОбновляемыеДанныеТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	(ДанныеДокумента.Организация <> ДанныеДокумента.ОрганизацияПолучатель
	|		ИЛИ ДанныеДокумента.Подразделение <> ДанныеДокумента.ПодразделениеПолучатель)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// ПринятиеКУчетуОС2_4
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)    КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                    КАК Регистратор,
	|	ДанныеДокумента.Дата                      КАК Период,
	|	ДанныеДокумента.Организация               КАК Организация,
	|
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ДанныеДокумента.Местонахождение КАК Подразделение,
	|
	|	ДанныеДокумента.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|
	|	0     КАК ПриростСтоимости,
	|	0     КАК ПриростСтоимостиРегл,
	|
	|	ДанныеРегистраХозрасчетный.СуммаНУДт КАК НеУчитываемаяСтоимостьНУ,
	|
	|	НЕОПРЕДЕЛЕНО КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО КАК КорГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО КАК КорНаправлениеДеятельности,
	|	
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуПредметовАренды) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПринятиеКУчетуПредметовАренды) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	Документ.ПринятиеКУчетуОС2_4 КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС2_4.ОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(,,СчетДт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КорректировкаСтоимостиАрендованногоИмущества),,) КАК ДанныеРегистраХозрасчетный
	|		ПО ДанныеРегистраХозрасчетный.Регистратор = ТаблицаОС.Ссылка
	|			И ДанныеРегистраХозрасчетный.СчетДт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КорректировкаСтоимостиАрендованногоИмущества)
	|			И ДанныеРегистраХозрасчетный.СубконтоДт1 = ТаблицаОС.ОсновноеСредство
	|			И ДанныеРегистраХозрасчетный.СуммаНУДт <> 0
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка В
	|		(ВЫБРАТЬ
	|			ОбновляемыеДанные.Регистратор
	|		ИЗ
	|			ОбновляемыеДанные КАК ОбновляемыеДанные)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// РегламентнаяОперация
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)    КАК ВидДвижения,
	|	РасчетРасходовПоАренде.Регистратор        КАК Регистратор,
	|	РасчетРасходовПоАренде.ПериодРасчета      КАК Период,
	|	РасчетРасходовПоАренде.Организация        КАК Организация,
	|
	|	РасчетРасходовПоАренде.ОсновноеСредство КАК ОсновноеСредство,
	|	втМестонахождениеОС.Местонахождение КАК Подразделение,
	|
	|	втПорядокУчетаОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	втПорядокУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|
	|	0     КАК ПриростСтоимости,
	|	0     КАК ПриростСтоимостиРегл,
	|
	|	РасчетРасходовПоАренде.СуммаПлатежаНУ + РасчетРасходовПоАренде.КорректировкаАмортизацииНУ КАК НеУчитываемаяСтоимостьНУ,
	|
	|	НЕОПРЕДЕЛЕНО КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО КАК КорГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО КАК КорНаправлениеДеятельности,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПризнаниеВНУАрендныхПлатежей) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	РегистрСведений.РасчетРасходовПоАренде КАК РасчетРасходовПоАренде
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПорядокУчетаОС КАК втПорядокУчетаОС
	|		ПО втПорядокУчетаОС.Регистратор = РасчетРасходовПоАренде.Регистратор
	|			И втПорядокУчетаОС.ОсновноеСредство = РасчетРасходовПоАренде.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втМестонахождениеОС КАК втМестонахождениеОС
	|		ПО втМестонахождениеОС.Регистратор = РасчетРасходовПоАренде.Регистратор
	|			И втМестонахождениеОС.ОсновноеСредство = РасчетРасходовПоАренде.ОсновноеСредство
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// ИзменениеПараметровОС2_4
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)    КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                    КАК Регистратор,
	|	ДанныеДокумента.Дата                      КАК Период,
	|	ДанныеДокумента.Организация               КАК Организация,
	|	втРезервДооценки.ОсновноеСредство         КАК ОсновноеСредство,
	|	НЕОПРЕДЕЛЕНО                              КАК Подразделение,
	|	втРезервДооценки.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|	втРезервДооценки.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|
	|	втРезервДооценки.ПриростСтоимости         КАК ПриростСтоимости,
	|	втРезервДооценки.ПриростСтоимостиРегл     КАК ПриростСтоимостиРегл,
	|
	|	0                                         КАК НеУчитываемаяСтоимостьНУ,
	|
	|	ДанныеДокумента.Организация               КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО                              КАК КорПодразделение,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|			ТОГДА ДанныеДокумента.ГруппаФинансовогоУчета
	|		ИНАЧЕ втРезервДооценки.ГруппаФинансовогоУчета
	|	КОНЕЦ                                     КАК КорГруппаФинансовогоУчета,
	|	
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.НаправлениеДеятельностиФлаг
	|			ТОГДА ДанныеДокумента.НаправлениеДеятельности
	|		ИНАЧЕ втРезервДооценки.НаправлениеДеятельности
	|	КОНЕЦ                                     КАК КорНаправлениеДеятельности,
	|
	|	втРезервДооценки.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровСтоимостиОС) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровСтоимостиОС) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	втРезервДооценки КАК втРезервДооценки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровОС2_4 КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = втРезервДооценки.Регистратор
	|ГДЕ
	|	ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|		ИЛИ ДанныеДокумента.НаправлениеДеятельностиФлаг
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)    КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                    КАК Регистратор,
	|	ДанныеДокумента.Дата                      КАК Период,
	|	ДанныеДокумента.Организация               КАК Организация,
	|	втРезервДооценки.ОсновноеСредство         КАК ОсновноеСредство,
	|	НЕОПРЕДЕЛЕНО                              КАК Подразделение,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|			ТОГДА ДанныеДокумента.ГруппаФинансовогоУчета
	|		ИНАЧЕ втРезервДооценки.ГруппаФинансовогоУчета
	|	КОНЕЦ                                     КАК ГруппаФинансовогоУчета,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.НаправлениеДеятельностиФлаг
	|			ТОГДА ДанныеДокумента.НаправлениеДеятельности
	|		ИНАЧЕ втРезервДооценки.НаправлениеДеятельности
	|	КОНЕЦ                                     КАК НаправлениеДеятельности,
	|
	|	втРезервДооценки.ПриростСтоимости         КАК ПриростСтоимости,
	|	втРезервДооценки.ПриростСтоимостиРегл     КАК ПриростСтоимостиРегл,
	|
	|	0                                         КАК НеУчитываемаяСтоимостьНУ,
	|
	|	ДанныеДокумента.Организация               КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО                              КАК КорПодразделение,
	|	втРезервДооценки.ГруппаФинансовогоУчета   КАК КорГруппаФинансовогоУчета,
	|	втРезервДооценки.НаправлениеДеятельности  КАК КорНаправлениеДеятельности,
	|	
	|	втРезервДооценки.ИдентификаторФинЗаписи   КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровСтоимостиОС) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровСтоимостиОС) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	втРезервДооценки КАК втРезервДооценки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровОС2_4 КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = втРезервДооценки.Регистратор
	|ГДЕ
	|	ДанныеДокумента.ГруппаФинансовогоУчетаФлаг
	|		ИЛИ ДанныеДокумента.НаправлениеДеятельностиФлаг
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// ПеремещениеОС2_4
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)    КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                    КАК Регистратор,
	|	ДанныеДокумента.Дата                      КАК Период,
	|	ДанныеДокумента.Организация               КАК Организация,
	|	втРезервДооценки.ОсновноеСредство         КАК ОсновноеСредство,
	|	НЕОПРЕДЕЛЕНО                              КАК Подразделение,
	|	втРезервДооценки.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|	втРезервДооценки.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|
	|	втРезервДооценки.ПриростСтоимости         КАК ПриростСтоимости,
	|	втРезервДооценки.ПриростСтоимостиРегл     КАК ПриростСтоимостиРегл,
	|
	|	0                                         КАК НеУчитываемаяСтоимостьНУ,
	|
	|	ДанныеДокумента.ОрганизацияПолучатель     КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО                              КАК КорПодразделение,
	|	втРезервДооценки.ГруппаФинансовогоУчета   КАК КорГруппаФинансовогоУчета,
	|	втРезервДооценки.НаправлениеДеятельности  КАК КорНаправлениеДеятельности,
	|	
	|	втРезервДооценки.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПеремещениеОСвПодразделениеВыделенноеНаБаланс) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	втРезервДооценки КАК втРезервДооценки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПеремещениеОС2_4 КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = втРезервДооценки.Регистратор
	|ГДЕ
	|	ДанныеДокумента.Организация <> ДанныеДокумента.ОрганизацияПолучатель
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)    КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                    КАК Регистратор,
	|	ДанныеДокумента.Дата                      КАК Период,
	|	ДанныеДокумента.ОрганизацияПолучатель     КАК Организация,
	|	втРезервДооценки.ОсновноеСредство         КАК ОсновноеСредство,
	|	НЕОПРЕДЕЛЕНО                              КАК Подразделение,
	|
	|	втРезервДооценки.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|	втРезервДооценки.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|
	|	втРезервДооценки.ПриростСтоимости         КАК ПриростСтоимости,
	|	втРезервДооценки.ПриростСтоимостиРегл     КАК ПриростСтоимостиРегл,
	|
	|	0                                         КАК НеУчитываемаяСтоимостьНУ,
	|
	|	ДанныеДокумента.Организация               КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО                              КАК КорПодразделение,
	|	втРезервДооценки.ГруппаФинансовогоУчета   КАК КорГруппаФинансовогоУчета,
	|	втРезервДооценки.НаправлениеДеятельности  КАК КорНаправлениеДеятельности,
	|	
	|	втРезервДооценки.ИдентификаторФинЗаписи   КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПеремещениеОСвПодразделениеВыделенноеНаБаланс) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	втРезервДооценки КАК втРезервДооценки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПеремещениеОС2_4 КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = втРезервДооценки.Регистратор
	|ГДЕ
	|	ДанныеДокумента.Организация <> ДанныеДокумента.ОрганизацияПолучатель
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// ПодготовкаКПередачеОС2_4
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)    КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                    КАК Регистратор,
	|	ДанныеДокумента.Дата                      КАК Период,
	|	ДанныеДокумента.Организация               КАК Организация,
	|	втРезервДооценки.ОсновноеСредство         КАК ОсновноеСредство,
	|	НЕОПРЕДЕЛЕНО                              КАК Подразделение,
	|	втРезервДооценки.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|	втРезервДооценки.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|
	|	втРезервДооценки.ПриростСтоимости         КАК ПриростСтоимости,
	|	втРезервДооценки.ПриростСтоимостиРегл     КАК ПриростСтоимостиРегл,
	|
	|	0                                         КАК НеУчитываемаяСтоимостьНУ,
	|
	|	НЕОПРЕДЕЛЕНО                              КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО                              КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО                              КАК КорГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                              КАК КорНаправлениеДеятельности,
	|	
	|	втРезервДооценки.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРезерваПереоценкиСтоимостиОС) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеРезерваПереоценкиСтоимостиОС) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	втРезервДооценки КАК втРезервДооценки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПодготовкаКПередачеОС2_4 КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = втРезервДооценки.Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// СписаниеОС2_4
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)    КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                    КАК Регистратор,
	|	ДанныеДокумента.Дата                      КАК Период,
	|	ДанныеДокумента.Организация               КАК Организация,
	|	втРезервДооценки.ОсновноеСредство         КАК ОсновноеСредство,
	|	НЕОПРЕДЕЛЕНО                              КАК Подразделение,
	|	втРезервДооценки.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|	втРезервДооценки.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|
	|	втРезервДооценки.ПриростСтоимости         КАК ПриростСтоимости,
	|	втРезервДооценки.ПриростСтоимостиРегл     КАК ПриростСтоимостиРегл,
	|
	|	0                                         КАК НеУчитываемаяСтоимостьНУ,
	|
	|	НЕОПРЕДЕЛЕНО                              КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО                              КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО                              КАК КорГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                              КАК КорНаправлениеДеятельности,
	|	
	|	втРезервДооценки.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРезерваПереоценкиСтоимостиОС) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеРезерваПереоценкиСтоимостиОС) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	втРезервДооценки КАК втРезервДооценки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС2_4 КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = втРезервДооценки.Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// РазукомплектацияОС
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)    КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                    КАК Регистратор,
	|	ДанныеДокумента.Дата                      КАК Период,
	|	ДанныеДокумента.Организация               КАК Организация,
	|	втРезервДооценки.ОсновноеСредство         КАК ОсновноеСредство,
	|	НЕОПРЕДЕЛЕНО                              КАК Подразделение,
	|	втРезервДооценки.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|	втРезервДооценки.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|
	|	втРезервДооценки.ПриростСтоимости         КАК ПриростСтоимости,
	|	втРезервДооценки.ПриростСтоимостиРегл     КАК ПриростСтоимостиРегл,
	|
	|	0                                         КАК НеУчитываемаяСтоимостьНУ,
	|
	|	НЕОПРЕДЕЛЕНО                              КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО                              КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО                              КАК КорГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                              КАК КорНаправлениеДеятельности,
	|	
	|	ДанныеДокумента.ИдентификаторДокумента КАК ИдентификаторФинЗаписи,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВыделениеСтоимостиОСПриРазукомплектации) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	втРезервДооценки КАК втРезервДооценки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РазукомплектацияОС КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = втРезервДооценки.Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)    КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                    КАК Регистратор,
	|	ДанныеДокумента.Дата                      КАК Период,
	|	ДанныеДокумента.Организация               КАК Организация,
	|	ТаблицаОС.ОсновноеСредство                КАК ОсновноеСредство,
	|	НЕОПРЕДЕЛЕНО                              КАК Подразделение,
	|	втРезервДооценки.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|	втРезервДооценки.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|
	|	втРезервДооценки.ПриростСтоимости         КАК ПриростСтоимости,
	|	втРезервДооценки.ПриростСтоимостиРегл     КАК ПриростСтоимостиРегл,
	|
	|	0                                         КАК НеУчитываемаяСтоимостьНУ,
	|
	|	НЕОПРЕДЕЛЕНО                              КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО                              КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО                              КАК КорГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                              КАК КорНаправлениеДеятельности,
	|	
	|	ДанныеДокумента.ИдентификаторДокумента КАК ИдентификаторФинЗаписи,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВыделениеСтоимостиОСПриРазукомплектации) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	втРезервДооценки КАК втРезервДооценки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РазукомплектацияОС КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = втРезервДооценки.Регистратор
	|			И ДанныеДокумента.ОсновноеСредство = втРезервДооценки.ОсновноеСредство
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// ПереоценкаОС2_4
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)    КАК ВидДвижения,
	|	Строки.Регистратор                        КАК Регистратор,
	|	Строки.Период                             КАК Период,
	|	Строки.Организация                        КАК Организация,
	|	Строки.ОсновноеСредство                   КАК ОсновноеСредство,
	|	НЕОПРЕДЕЛЕНО                              КАК Подразделение,
	|	Строки.ГруппаФинансовогоУчета             КАК ГруппаФинансовогоУчета,
	|	Строки.НаправлениеДеятельности            КАК НаправлениеДеятельности,
	|
	|	Строки.Стоимость                          КАК ПриростСтоимости,
	|	Строки.СтоимостьРегл                      КАК ПриростСтоимостиРегл,
	|
	|	0                                         КАК НеУчитываемаяСтоимостьНУ,
	|
	|	НЕОПРЕДЕЛЕНО                              КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО                              КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО                              КАК КорГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                              КАК КорНаправлениеДеятельности,
	|	
	|	Строки.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.УвеличениеСтоимостиОС) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.УвеличениеСтоимостиОС) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	РегистрНакопления.СтоимостьОС КАК Строки
	|ГДЕ
	|	Строки.Регистратор В (ВЫБРАТЬ ОбновляемыеДанные.Регистратор ИЗ ОбновляемыеДанные КАК ОбновляемыеДанные)
	|	И ТИПЗНАЧЕНИЯ(Строки.Регистратор) = ТИП(Документ.ПереоценкаОС2_4)
	|	И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И Строки.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.УвеличениеСтоимостиОС)
	|	И (Строки.СтоимостьРегл <> 0 ИЛИ Строки.Стоимость <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)    КАК ВидДвижения,
	|	Строки.Регистратор                        КАК Регистратор,
	|	Строки.Период                             КАК Период,
	|	Строки.Организация                        КАК Организация,
	|	Строки.ОсновноеСредство                   КАК ОсновноеСредство,
	|	НЕОПРЕДЕЛЕНО                              КАК Подразделение,
	|	Строки.ГруппаФинансовогоУчета             КАК ГруппаФинансовогоУчета,
	|	Строки.НаправлениеДеятельности            КАК НаправлениеДеятельности,
	|
	|	Строки.Амортизация                        КАК ПриростСтоимости,
	|
	|	ВЫБОР 
	|		КОГДА Строки.НачислятьИзнос 
	|			ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияРегл 
	|	КОНЕЦ КАК ПриростСтоимостиРегл,
	|
	|	0                                         КАК НеУчитываемаяСтоимостьНУ,
	|
	|	НЕОПРЕДЕЛЕНО                              КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО                              КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО                              КАК КорГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                              КАК КорНаправлениеДеятельности,
	|	
	|	Строки.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.УвеличениеНакопленнойАмортизацииОС) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.УвеличениеНакопленнойАмортизацииОС) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	РегистрНакопления.АмортизацияОС КАК Строки
	|ГДЕ
	|	Строки.Регистратор В (ВЫБРАТЬ ОбновляемыеДанные.Регистратор ИЗ ОбновляемыеДанные КАК ОбновляемыеДанные)
	|	И ТИПЗНАЧЕНИЯ(Строки.Регистратор) = ТИП(Документ.ПереоценкаОС2_4)
	|	И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И Строки.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.УвеличениеНакопленнойАмортизацииОС)
	|	И (Строки.Амортизация <> 0
	|		ИЛИ НЕ Строки.НачислятьИзнос И Строки.АмортизацияРегл <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)    КАК ВидДвижения,
	|	Строки.Регистратор                        КАК Регистратор,
	|	Строки.Период                             КАК Период,
	|	Строки.Организация                        КАК Организация,
	|	Строки.ОсновноеСредство                   КАК ОсновноеСредство,
	|	НЕОПРЕДЕЛЕНО                              КАК Подразделение,
	|	Строки.ГруппаФинансовогоУчета             КАК ГруппаФинансовогоУчета,
	|	Строки.НаправлениеДеятельности            КАК НаправлениеДеятельности,
	|
	|	Строки.РезервПереоценкиСтоимости          КАК ПриростСтоимости,
	|	Строки.РезервПереоценкиСтоимостиРегл      КАК ПриростСтоимостиРегл,
	|
	|	0                                         КАК НеУчитываемаяСтоимостьНУ,
	|
	|	НЕОПРЕДЕЛЕНО                              КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО                              КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО                              КАК КорГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                              КАК КорНаправлениеДеятельности,
	|	
	|	Строки.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.УменьшениеВеличиныДооценкиСтоимостиОС) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.УменьшениеВеличиныДооценкиСтоимостиОС) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	РегистрНакопления.СтоимостьОС КАК Строки
	|ГДЕ
	|	Строки.Регистратор В (ВЫБРАТЬ ОбновляемыеДанные.Регистратор ИЗ ОбновляемыеДанные КАК ОбновляемыеДанные)
	|	И ТИПЗНАЧЕНИЯ(Строки.Регистратор) = ТИП(Документ.ПереоценкаОС2_4)
	|	И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И Строки.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.УменьшениеВеличиныДооценкиСтоимостиОС)
	|	И (Строки.РезервПереоценкиСтоимости > 0
	|			ИЛИ Строки.РезервПереоценкиСтоимостиРегл > 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)    КАК ВидДвижения,
	|	Строки.Регистратор                        КАК Регистратор,
	|	Строки.Период                             КАК Период,
	|	Строки.Организация                        КАК Организация,
	|	Строки.ОсновноеСредство                   КАК ОсновноеСредство,
	|	НЕОПРЕДЕЛЕНО                              КАК Подразделение,
	|	Строки.ГруппаФинансовогоУчета             КАК ГруппаФинансовогоУчета,
	|	Строки.НаправлениеДеятельности            КАК НаправлениеДеятельности,
	|
	|	Строки.РезервПереоценкиАмортизации          КАК ПриростСтоимости,
	|
	|	ВЫБОР 
	|		КОГДА Строки.НачислятьИзнос 
	|			ТОГДА 0 
	|		ИНАЧЕ Строки.РезервПереоценкиАмортизацииРегл 
	|	КОНЕЦ КАК ПриростСтоимостиРегл,
	|
	|	0                                         КАК НеУчитываемаяСтоимостьНУ,
	|
	|	НЕОПРЕДЕЛЕНО                              КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО                              КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО                              КАК КорГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО                              КАК КорНаправлениеДеятельности,
	|	
	|	Строки.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.УменьшениеВеличиныДооценкиНакопленнойАмортизацииОС) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.УменьшениеВеличиныДооценкиНакопленнойАмортизацииОС) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	РегистрНакопления.АмортизацияОС КАК Строки
	|ГДЕ
	|	Строки.Регистратор В (ВЫБРАТЬ ОбновляемыеДанные.Регистратор ИЗ ОбновляемыеДанные КАК ОбновляемыеДанные)
	|	И ТИПЗНАЧЕНИЯ(Строки.Регистратор) = ТИП(Документ.ПереоценкаОС2_4)
	|	И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И Строки.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.УменьшениеВеличиныДооценкиНакопленнойАмортизацииОС)
	|	И (Строки.РезервПереоценкиАмортизации <> 0
	|		ИЛИ НЕ Строки.НачислятьИзнос 
	|			И Строки.РезервПереоценкиАмортизацииРегл <> 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицыДвижений.ВидДвижения КАК ВидДвижения,
	|	ТаблицыДвижений.Регистратор КАК Регистратор,
	|	ТаблицыДвижений.Период КАК Период,
	|	ТаблицыДвижений.Организация КАК Организация,
	|
	|	ТаблицыДвижений.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицыДвижений.Подразделение КАК Подразделение,
	|	ТаблицыДвижений.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ТаблицыДвижений.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|
	|	ТаблицыДвижений.ПриростСтоимости КАК ПриростСтоимости,
	|	ТаблицыДвижений.ПриростСтоимостиРегл КАК ПриростСтоимостиРегл,
	|	ТаблицыДвижений.НеУчитываемаяСтоимостьНУ КАК НеУчитываемаяСтоимостьНУ,
	|
	|	ТаблицыДвижений.КорОрганизация КАК КорОрганизация,
	|	ТаблицыДвижений.КорПодразделение КАК КорПодразделение,
	|	ТаблицыДвижений.КорГруппаФинансовогоУчета КАК КорГруппаФинансовогоУчета,
	|	ТаблицыДвижений.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	
	|	ТаблицыДвижений.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ТаблицыДвижений.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втТаблицыДвижений КАК ТаблицыДвижений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Выборка обновляемых данных, по которым нет таблиц для движений
	|ВЫБРАТЬ
	|	НЕОПРЕДЕЛЕНО КАК ВидДвижения,
	|	ОбновляемыеДанные.Регистратор КАК Регистратор,
	|	НЕОПРЕДЕЛЕНО КАК Период,
	|	НЕОПРЕДЕЛЕНО КАК Организация,
	|
	|	НЕОПРЕДЕЛЕНО КАК ОсновноеСредство,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|
	|	НЕОПРЕДЕЛЕНО КАК ПриростСтоимости,
	|	НЕОПРЕДЕЛЕНО КАК ПриростСтоимостиРегл,
	|	НЕОПРЕДЕЛЕНО КАК НеУчитываемаяСтоимостьНУ,
	|
	|	НЕОПРЕДЕЛЕНО КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО КАК КорГруппаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО КАК КорНаправлениеДеятельности,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ОбновляемыеДанные КАК ОбновляемыеДанные
	|ГДЕ
	|	НЕ ОбновляемыеДанные.Регистратор В (ВЫБРАТЬ Т.Регистратор ИЗ втТаблицыДвижений КАК Т)
	|	И ТИПЗНАЧЕНИЯ(ОбновляемыеДанные.Регистратор) <> ТИП(Документ.ВводОстатковВнеоборотныхАктивов2_4)
	|
	|ИТОГИ ПО 
	|	Регистратор";	
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Результат = Запрос.Выполнить();
	ВыборкаРегистратор = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаРегистратор.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ОбщегоНазначения.ИмяТаблицыПоСсылке(ВыборкаРегистратор.Регистратор));
			ЭлементБлокировки.УстановитьЗначение("Ссылка", ВыборкаРегистратор.Регистратор);

			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра + ".НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", ВыборкаРегистратор.Регистратор);

			Блокировка.Заблокировать();
			
			НаборЗаписей = РегистрыНакопления.СтоимостьОС.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаРегистратор.Регистратор);
			НаборЗаписей.Прочитать();
			
			// Добавление новых записей
			Выборка = ВыборкаРегистратор.Выбрать();
			Пока Выборка.Следующий() Цикл
			
				Если НЕ ЗначениеЗаполнено(Выборка.Период) Тогда
					Продолжить
				КонецЕсли;
				
				ЗаписьРегистра = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(ЗаписьРегистра, Выборка);
				
			КонецЦикла;
			
			Если НаборЗаписей.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
			КонецЕсли;

			ЗафиксироватьТранзакцию();
			
		Исключение

			ОтменитьТранзакцию();

			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось обработать записи регистра ""Стоимость ОС"": %1 по причине: %2';
											|en = 'Cannot process ""TA cost"" entries: %1. Reason: %2'"),
										Строка(Выборка.Регистратор),
										ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
										
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Предупреждение, 
				Выборка.Регистратор.Метаданные(), 
				Выборка.Регистратор, 
				ТекстСообщения);

		КонецПопытки;
		
	КонецЦикла;
	#КонецОбласти
	
	//-- Локализация
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
