#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Добавляет в набор записей регистра движения заказа поставщику.
// Все реквизиты запроса должны соотвествтовать реквизитам регистра.
// Количество должно быть в базовой единице измерения.
// Сумма в валюте учета закупок.
//
Процедура ДобавитьЗаказПоставщику(НаборЗаписей,
								ВыборкаЗапроса) Экспорт
	ДобавитьЗаписи(НаборЗаписей, 
					ВыборкаЗапроса, 
					ВидДвиженияНакопления.Приход);
КонецПроцедуры

// Добавляет в набор записей регистра движения закрытия (списания)
// зарегистрированного заказа поставщику.
// Все реквизиты запроса должны соотвествтовать реквизитам регистра.
// Количество должно быть в базовой единице измерения.
// Сумма в валюте учета закупок.
//
Процедура ЗакрытьЗаказПоставщику(НаборЗаписей,
									ВыборкаЗапроса) Экспорт
	ДобавитьЗаписи(НаборЗаписей, 
					ВыборкаЗапроса, 
					ВидДвиженияНакопления.Расход);	
КонецПроцедуры

// Записывает данные из выборки запроса в набор записей
//  регистра. Выборка запроса должна содеражать поля
//  "Количество" и "Сумма".
//
// Параметры:
//  НаборЗаписей - РегистрНакопленияНаборЗаписей.ЗаказыПоставщикамУХ.
//  ВыборкаЗапроса - ВыборкаИзРезультатаЗапроса - должна содержать
//		одноименные поля для заполнения измерений, а также поля
//		"Количество" и "Сумма". Количество должно быть в базовой
//		единице измерения. Сумма в валюте учета закупок.
//	ВидДвижения - ВидДвиженияНакопления - вид движения.
//
Процедура ДобавитьЗаписи(НаборЗаписей,
							ВыборкаЗапроса,
							ВидДвижения)
	Пока ВыборкаЗапроса.Следующий() Цикл
		Движение = НаборЗаписей.Добавить();
		Движение.ВидДвижения = ВидДвижения;
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаЗапроса);
	КонецЦикла;	
КонецПроцедуры

#КонецЕсли