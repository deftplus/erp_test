#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	
// Добавляет в набор записей регистра движения увеличивающие 
// потребность в номенклатуре. Количество и сумма потребности 
// беруться из колонок таблицы "Количество" и "Сумма".
// Количество должно быть в базовой единице измерения.
// Сумма в валюте учета закупок.
//
Процедура ДобавитьПотребность(НаборЗаписей,
								ВыборкаЗапроса) Экспорт
	ДобавитьЗаписи(НаборЗаписей, ВыборкаЗапроса, "Потребность", 1);
КонецПроцедуры

// Записывает в набор записей регистра данные из запроса
// со знаком минус в ресурсы количество и сумма потребности.
// Количество и сумма в обеспечении не изменяются.
// Количество должно быть в базовой единице измерения.
// Сумма в валюте учета закупок.
//
Процедура СторнироватьПотребность(НаборЗаписей,
									ВыборкаЗапроса) Экспорт
	ДобавитьЗаписи(НаборЗаписей, ВыборкаЗапроса, "Потребность", -1);	
КонецПроцедуры

// Записывает в набор записей регистра данные из запроса
// со знаком плюс количество и сумма в обеспечении.
// Количество должно быть в базовой единице измерения.
// Сумма в валюте учета закупок.
//
Процедура ДобавитьОбеспечениеПотребности(НаборЗаписей,
											ВыборкаЗапроса) Экспорт
	ДобавитьЗаписи(НаборЗаписей, ВыборкаЗапроса, "ВОбеспечении", 1);
КонецПроцедуры

// Записывает в набор записей регистра данные из из запроса
// со знаком минус в количество и сумму обеспечения.
// Количество должно быть в базовой единице измерения.
// Сумма в валюте учета закупок.
//
Процедура СторнироватьОбеспечениеПотребности(НаборЗаписей,
											ВыборкаЗапроса) Экспорт
	ДобавитьЗаписи(НаборЗаписей, ВыборкаЗапроса, "ВОбеспечении", -1);	
КонецПроцедуры

// Записывает в набор записей регистра данные из запроса
// со знаком плюс количество и сумма в договоре.
// Количество должно быть в базовой единице измерения.
// Сумма в валюте учета закупок.
//
Процедура ДобавитьРазмещениеВДоговоре(НаборЗаписей,
											ВыборкаЗапроса) Экспорт
	ДобавитьЗаписи(НаборЗаписей, ВыборкаЗапроса, "ВОбеспечении", 1);
КонецПроцедуры

// Записывает в набор записей регистра данные из из запроса
// со знаком минус в количество и сумму обеспечения.
// Количество должно быть в базовой единице измерения.
// Сумма в валюте учета закупок.
//
Процедура СторнироватьРазмещениеВДоговоре(НаборЗаписей,
											ВыборкаЗапроса) Экспорт
	ДобавитьЗаписи(НаборЗаписей, ВыборкаЗапроса, "ВОбеспечении", -1);	
КонецПроцедуры

Процедура ДобавитьПотребностьВнеПлана(НаборЗаписей,
											ВыборкаЗапроса) Экспорт
	ДобавитьЗаписи(НаборЗаписей, ВыборкаЗапроса, "ВнеПлана", 1);
КонецПроцедуры

Процедура СторнироватьПотребностьВнеПлана(НаборЗаписей,
											ВыборкаЗапроса) Экспорт
	ДобавитьЗаписи(НаборЗаписей, ВыборкаЗапроса, "ВнеПлана", -1);	
КонецПроцедуры

// Заполняет набор записей регистра из выборки запроса.
//
// Параметры:
//  НаборЗаписей - РегистрНакопленияНаборЗаписей.ПотребностиВНоменклатуре.
//  ВыборкаЗапроса - ВыборкаИзРезультатаЗапроса - для заполнения набора.
//
Процедура ЗаполнитьНаборЗаписей(НаборЗаписей, ВыборкаЗапроса) Экспорт
	Пока ВыборкаЗапроса.Следующий() Цикл
		Движение = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаЗапроса);
	КонецЦикла;	
КонецПроцедуры

// Записывает данные из выборки запроса в набор записей
//  регистра. Выборка запроса должна содеражать поля
//  "Количество" и "Сумма". В зависимости от аргументов
//  процедуры они будут записаны, либо в потребность,
//  либо в обеспечение.
//
// Параметры:
//  НаборЗаписей - РегистрНакопленияНаборЗаписей.ПотребностиВНоменклатуре.
//  ВыборкаЗапроса - ВыборкаИзРезультатаЗапроса - должна содержать
//		одноименные поля для заполнения измерений, а также поля
//		"Количество" и "Сумма". Количество должно быть в базовой
//		единице измерения. Сумма в валюте учета закупок.
//	СуффиксРесурса - Строка: "Потребность" | "ВОбеспечении"
//		- в зависимости от суффикса, количество и сумма будут
//		записаны в разные ресурсы регистра.
//	Знак - Число: -1 | +1 - знак с которым данные будут записаны в регистр.
//
Процедура ДобавитьЗаписи(НаборЗаписей,
							ВыборкаЗапроса,
							СуффиксРесурса,
							Знак)
	Пока ВыборкаЗапроса.Следующий() Цикл
		Движение = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаЗапроса);
		Движение["Количество" + СуффиксРесурса] =
			Знак * ВыборкаЗапроса.Количество;
		Движение["Сумма" + СуффиксРесурса] = 
			Знак * ВыборкаЗапроса.Сумма;
		Движение.Коэффициент = ВыборкаЗапроса.Коэффициент;
	КонецЦикла;	
КонецПроцедуры


#КонецЕсли