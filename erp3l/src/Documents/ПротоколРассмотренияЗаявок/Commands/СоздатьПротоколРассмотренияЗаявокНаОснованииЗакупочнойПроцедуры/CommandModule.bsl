
// Возвращает документ Протокол рассмотрения заявок по закупочной процедуре ЗакупочнаяПроцедураВход.
&НаСервере
Функция НайтиПротоколРассмотренияЗаявокПоЗакупочнойПроцедуре(ЗакупочнаяПроцедураВход)
	РезультатФункции = Документы.ПротоколРассмотренияЗаявок.ПустаяСсылка();
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ЗакупочнаяПроцедура", ЗакупочнаяПроцедураВход);
	СтруктураОтбора.Вставить("ПометкаУдаления", Ложь);
	мДокументов = ЦентрализованныеЗакупкиУХ.ПолучитьДокументыПоОтбору("ПротоколРассмотренияЗаявок", СтруктураОтбора);
	Если мДокументов.Количество() > 0 Тогда
		ПерваяСтрока = мДокументов[0];
		РезультатФункции = ПерваяСтрока.Ссылка;
	Иначе
		РезультатФункции = Документы.ПротоколРассмотренияЗаявок.ПустаяСсылка();
	КонецЕсли;
	Возврат РезультатФункции;
КонецФункции		 // НайтиПротоколРассмотренияЗаявокПоЗакупочнойПроцедуре()

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	// Проверим, что способ выбора поставщика позволяет вводить данный документ.
	РазрешенныеСпособы = Новый Массив;
	РазрешенныеСпособы.Добавить(ПредопределенноеЗначение("Перечисление.СпособыВыбораПоставщика.Конкурс"));
	РазрешенныеСпособы.Добавить(ПредопределенноеЗначение("Перечисление.СпособыВыбораПоставщика.Аукцион"));
	ТекСпособ = ОбщегоНазначенияУХ.ПолучитьЗначениеРеквизита(ПараметрКоманды, "СпособВыбораПоставщика");
	СпособРазрешен = (РазрешенныеСпособы.Найти(ТекСпособ) <> Неопределено);
	Если СпособРазрешен Тогда
		НайденныйПротокол = НайтиПротоколРассмотренияЗаявокПоЗакупочнойПроцедуре(ПараметрКоманды);
		Если ЗначениеЗаполнено(НайденныйПротокол) Тогда
			// Откроем существующий документ.
			ПоказатьЗначение(, НайденныйПротокол);
		Иначе
			// Откроем форму нового документа.
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Основание", ПараметрКоманды);
			ОткрытьФорму("Документ.ПротоколРассмотренияЗаявок.ФормаОбъекта",
			ПараметрыФормы, 
			ПараметрыВыполненияКоманды.Источник, 
			ПараметрыВыполненияКоманды.Уникальность, 
			ПараметрыВыполненияКоманды.Окно, 
			ПараметрыВыполненияКоманды.НавигационнаяСсылка);
		КонецЕсли;
	Иначе
		ТекстСообщения = НСтр("ru = 'По способу выбора поставщика %Способ% не требуется ввод документа Протокол рассмотрения заявок. Операция отменена.'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Способ%", Строка(ТекСпособ));
		ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
	КонецЕсли;
КонецПроцедуры
