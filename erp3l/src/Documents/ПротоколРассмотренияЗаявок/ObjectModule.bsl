#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Если НЕ Отменен
				И ПредложенияПоставщиков.Количество() = 0 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru='Не указаны поставщики!'"),
				,
				"Объект.ПротоколВскрытияКонвертов",
				,
				Отказ);
		КонецЕсли;
	КонецЕсли;
	мДокументов = ЦентрализованныеЗакупкиУХ.ПолучитьДокументыПоОтбору(
		"ПротоколРассмотренияЗаявок",
		Новый Структура("ЗакупочнаяПроцедура, Проведен", ЗакупочнаяПроцедура, Истина),
		Ссылка);
	Если мДокументов.Количество() > 0 Тогда
		ТекстСообщения = НСтр("ru = 'По закупочной процедуре ""%ЗакупочнаяПроцедура%"" уже введен протокол рассмотрения заявок. Операция отменена.'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ЗакупочнаяПроцедура%", Строка(ЗакупочнаяПроцедура));
		ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения,	, , СтатусСообщения.Важное);
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ДанныеЗаполнения <> Неопределено Тогда
		Если ТипДанныхЗаполнения = Тип("СправочникСсылка.ЗакупочныеПроцедуры") Тогда
			Если НЕ Документы.ПротоколВскрытияКонвертов.МожноВвестиНаОснованииЗакупки(ДанныеЗаполнения) Тогда
				ТекстИсключения = НСтр("ru = 'Для закупки со способом выбора поставщика ""%СпособВыбораПоставщика%"" запрещено вводить протокол рассмотрения заявок'");
				ТекстИсключения = СтрЗаменить(ТекстИсключения, "%СпособВыбораПоставщика%", Строка(ДанныеЗаполнения.СпособВыбораПоставщика));
				ВызватьИсключение ТекстИсключения;
			КонецЕсли;
			ЗаполнитьИзЗакупки(ДанныеЗаполнения);
			ПротоколВскрытияКонвертов = ЦентрализованныеЗакупкиУХ.ПолучитьДокументЗакупочнойПроцедуры(
				"ПротоколВскрытияКонвертов", ДанныеЗаполнения);
		ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ПротоколВскрытияКонвертов") Тогда
			Если НЕ Документы.ПротоколВскрытияКонвертов.МожноВвестиНаОснованииПротоколаВскрытияКонвертов(ДанныеЗаполнения) Тогда
				ТекстИсключения = НСтр("ru = 'Протокол рассмотрения заявок запрещено вводить на основании протокола вскрытия конвертов'");
				ВызватьИсключение ТекстИсключения;
			КонецЕсли;
			ЗаполнитьИзЗакупки(ДанныеЗаполнения.ЗакупочнаяПроцедура);
			ПротоколВскрытияКонвертов = ДанныеЗаполнения;
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если Отменен Тогда
		ПроверяемыеРеквизиты.Добавить("ПричинаОтмены");
	Иначе
		ПроверяемыеРеквизиты.Добавить("РезультатРаботыКомиссии");
	КонецЕсли;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Ответственный = Пользователи.ТекущийПользователь();
	УИД_ЕИС = "";
	РегистрационныйНомерЕИС = 0;
КонецПроцедуры

Процедура ЗаполнитьИзЗакупки(ЗакупочнаяПроцедураСсылка)
	ЗакупочнаяПроцедура = ЗакупочнаяПроцедураСсылка;
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗакупочнаяПроцедураСсылка);
	ЗаполнитьПредложениямиПоставщиковПоУмолчанию();
КонецПроцедуры

// Возвращает массив ссылок предложений поставщика по закупочной процедуре ЗакупочнаяПроцедураВход.
Функция ПолучитьПредложенияПоставщикаЗакупочнойПроцедуры(ЗакупочнаяПроцедураВход)
	РезультатФункции = Новый Массив;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ПредложениеПоставщика.Ссылка КАК Ссылка,
	|	ПредложениеПоставщика.Лот КАК Лот
	|ИЗ
	|	Документ.ПредложениеПоставщика КАК ПредложениеПоставщика
	|ГДЕ
	|	НЕ ПредложениеПоставщика.ПометкаУдаления
	|	И ПредложениеПоставщика.Лот.Владелец = &ЗакупочнаяПроцедура
	|	И ПредложениеПоставщика.Проведен";
	Запрос.УстановитьПараметр("ЗакупочнаяПроцедура", ЗакупочнаяПроцедураВход);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		РезультатФункции.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
	КонецЦикла;
	Возврат РезультатФункции;
КонецФункции		// ПолучитьПредложенияПоставщикаЗакупочнойПроцедуры()

// Заполняем, только если указан Лот.
Процедура ЗаполнитьПредложениямиПоставщиковПоУмолчанию()
	Если НЕ ЗначениеЗаполнено(ЗакупочнаяПроцедура) Тогда
		Возврат;
	КонецЕсли;
	ПредложенияПоставщиков.Очистить();
	мПредложений = ПолучитьПредложенияПоставщикаЗакупочнойПроцедуры(ЗакупочнаяПроцедура);
	Для Каждого ПредолжениеПоставщика Из мПредложений Цикл
		Документы.ПротоколРассмотренияЗаявок.ДобавитьСтрокуПредложенияПоставщика(
			ПредложенияПоставщиков, ПредолжениеПоставщика);
	КонецЦикла;

КонецПроцедуры


#КонецЕсли