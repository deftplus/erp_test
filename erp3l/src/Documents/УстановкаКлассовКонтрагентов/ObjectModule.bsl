
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	ДатаНачала = ТекущаяДатаСеанса();
	Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	ДатаНачала = ТекущаяДатаСеанса();
	Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	// регистр КлассыКонтрагентов
	Движения.КлассыКонтрагентов.Записывать = Истина;
	Для Каждого ТекСтрокаОценки Из Оценки Цикл
		Движение = Движения.КлассыКонтрагентов.Добавить();
		Движение.Период				= ДатаНачала;
		Движение.Контрагент			= ТекСтрокаОценки.Контрагент;
		Движение.РольКонтрагента	= РольКонтрагента;
		Движение.СтепеньРиска		= ТекСтрокаОценки.СтепеньРиска;
		Движение.СтепеньДоходности	= ТекСтрокаОценки.СтепеньДоходности;
		Движение.Задолженность		= ТекСтрокаОценки.Задолженность;
		Движение.КонтролироватьЗадолженность = ТекСтрокаОценки.КонтролироватьЗадолженность;
		Если Не РольКонтрагента = Перечисления.РолиКонтрагентов.Банки Тогда
			Движение.ПроцентАванса		= ТекСтрокаОценки.ПроцентАванса;
			Движение.СрокОтсрочки		= ТекСтрокаОценки.СрокОтсрочки;
		КонецЕсли;
		Движение.Комментарий		= ТекСтрокаОценки.Комментарий;
	КонецЦикла;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	// Проверка дубликатов контрагентов
	СоответствиеПоиска = Новый Соответствие;
	Для Каждого ТекОценки Из Оценки Цикл
		ТекКонтрагент = ТекОценки.Контрагент;
		Если СоответствиеПоиска[ТекКонтрагент] <> Неопределено Тогда
			ТекстСообщения = НСтр("ru = 'В табличной части найден дублирующийся контрагент %Контрагент%. Проведение отменено.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Контрагент%", Строка(ТекКонтрагент));
			ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения, Отказ);
		Иначе
			СоответствиеПоиска.Вставить(ТекКонтрагент, Истина);  	//Значение соответствия не имеет значения для функционирования алгоритма
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецЕсли