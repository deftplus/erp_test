#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ОбработкаПроведения(Отказ, Режим)

	РодителиНоменклатуры = Новый Соответствие;
	
	Если РешениеПоДокументу Тогда
		Движения.АккредитованыеПоставщики.Записывать = Истина;
		Движения.СостоянияАккредитованныхПоставщиков.Записывать = Истина;
	
		Движение = Движения.АккредитованыеПоставщики.Добавить();
		Движение.Период = Дата;
		Движение.Организация = Организация;
		Движение.Контрагент = АнкетаПоставщика.Контрагент;
		Движение.АнкетаПоставщика = АнкетаПоставщика;
		Движение.ДатаНачала = Дата;
		
		Если МораторийНаРаботуСПоставщиком Тогда
			Движение.Состояние = Перечисления.СостоянияАккредитацииПоставщиков.ЛишенАккредитации;
			Движение.ДатаОкончания = ДатаМоратория;
		Иначе
			Движение.Состояние = Перечисления.СостоянияАккредитацииПоставщиков.НеАккредитован;
			Движение.ДатаОкончания = Дата;
		КонецЕсли;
		
		// Записываем общее состояние в целом по холдингу
		АккредитацияПоставщиковУХ.ДобавитьДвижениеСостояниеАккредитацииПоставщика(
			Движения.СостоянияАккредитованныхПоставщиков,
			АнкетаПоставщика,
			Дата,
			Организация,
			НЕ РешениеПоДокументу,
			Дата);
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Отбор_ = Новый Соответствие;
	Отбор_.Вставить("Ссылка", Новый Структура("Значение,ВидСравнения", Ссылка, "<>"));
	
	ДругойДокумент = АккредитацияПоставщиковУХ.ПолучитьДокументыАккредитации(
		"ЛишениеАккредитации",
		Организация,
		АнкетаПоставщика,
		Дата,
		Отбор_);
		
	Если ЗначениеЗаполнено(ДругойДокумент) Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		ТекстСообщения = НСтр("ru = 'Уже есть документ лишения аккредитации поставщика ""%АнкетаПоставщика%"" от %Дата%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%АнкетаПоставщика%", Строка(АнкетаПоставщика));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Дата%", Строка(ДругойДокумент.Дата));
		Сообщение.Текст = ТекстСообщения;
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		ТипЗаполнения = ТипЗнч(ДанныеЗаполнения);
		
		Если ТипЗаполнения = Тип("Структура") Тогда
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
			
		Иначе
			Если НЕ ЦентрализованныеЗакупкиУХ.ОбъектУтвержден(ДанныеЗаполнения.Ссылка) Тогда
				ВызватьИсключение НСтр("ru = 'Ввод на основании можно делать только на основании утвержденного объекта.'");
			КонецЕсли;
			Если Документы.ТипВсеСсылки().СодержитТип(ТипЗаполнения) Тогда
				Если НЕ ДанныеЗаполнения.Проведен Тогда
					ВызватьИсключение НСтр("ru = 'Ввод на основании можно делать только на основании проведенного документа.'");
				КонецЕсли;
				Если ТипЗаполнения = Тип("ДокументСсылка.АккредитацияПоставщика") И
					ДанныеЗаполнения.РешениеПоДокументу <> Перечисления.ВидыРешенийПоДокументуАккредитации.ПоложительноеРешение Тогда
					ВызватьИсключение НСтр("ru = 'Ввод на основании можно делать только на основании Аккредитации с положительным решением.'");
				КонецЕсли;
			КонецЕсли;
			
			Если ТипЗаполнения = Тип("СправочникСсылка.АнкетыПоставщиков") Тогда
				АнкетаПоставщика = ДанныеЗаполнения;
			ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.АккредитацияПоставщика") Тогда
				АнкетаПоставщика = ДанныеЗаполнения.АнкетаПоставщика;
				Организация = ДанныеЗаполнения.Организация;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РешениеПоДокументу) Тогда
		РешениеПоДокументу = Перечисления.ВидыРешенийПоДокументуАккредитации.РешениеНеПринято;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(АнкетаПоставщика) Тогда
		Если ЗначениеЗаполнено(Организация) Тогда
			ДанныеАккредитации = АккредитацияПоставщиковУХ.ПолучитьДанныеАккредитацииПоставщика(Организация, АнкетаПоставщика, Дата);
			флАккредитован = (ДанныеАккредитации <> Неопределено)
				И (ДанныеАккредитации.Состояние = Перечисления.СостоянияАккредитацииПоставщиков.Аккредитован);
		Иначе
			ДанныеАккредитации = АккредитацияПоставщиковУХ.ПолучитьОписаниеСтатусаАккредитацииПоставщика(АнкетаПоставщика, Дата);
			флАккредитован = ДанныеАккредитации.Аккредитован;
		КонецЕсли;
		Если НЕ флАккредитован Тогда
			ТекстИсключения = НСтр("ru = 'Поставщик ""%АнкетаПоставщика%"" не аккредитован.'");
			ТекстИсключения = СтрЗаменить(ТекстИсключения, "%АнкетаПоставщика%", Строка(АнкетаПоставщика));
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если МораторийНаРаботуСПоставщиком Тогда
		ПроверяемыеРеквизиты.Добавить("ДатаМоратория");
	КонецЕсли;
	Если РешениеПоДокументу Тогда
		ПроверяемыеРеквизиты.Добавить("ОбоснованиеРешения");
	КонецЕсли;
КонецПроцедуры

#КонецЕсли
