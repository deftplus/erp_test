#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаПлатежныеПозицииКЗакрытию(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаДвиженияПоОперПланам(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСерверОПК.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	//
	ПодготовитьТаблицуЛимитовПоБюджетам(ДокументСсылка, Запрос, ДополнительныеСвойства.ТаблицыДляДвижений);

КонецПроцедуры

#Область СтандартныеПодсистемыКоманды

// Добавляет команду создания объекта на основании.
// 
// Параметры:
// 	КомандыСозданияНаОсновании - ТаблицаЗначений - перечень команд
// Возвращаемое значение:
// 	Неопределено, СтрокаТаблицыЗначений - Добавленная Команда.
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.ЗакрытиеПлатежнойПозиции;
	
	Если ПравоДоступа("Добавление", МетаданныеДокумента) Тогда
		
		КомандаСоздатьНаОсновании						= КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер				= МетаданныеДокумента.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление			= ОбщегоНазначения.ПредставлениеОбъекта(МетаданныеДокумента);
		КомандаСоздатьНаОсновании.РежимЗаписи			= "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Заполняет список команд создания на основании.
// 
// Параметры:
// 	КомандыСозданияНаОсновании - ТаблицаЗначений - перечень команд
// 	Параметры - Структура - параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	// Для этого документа это не надо, но для других возможно потребуется
	//Параметры.Вставить("ФормаОплаты", Перечисления.ФормыОплаты.Безналичная);
	//Параметры.Вставить("ПриходРасход", Перечисления.ВидыДвиженийПриходРасход.Расход);
	
	ПлатежныеПозиции.ПереопределитьПолучаемуюФормуДокументаДДС(Документы.ЗакрытиеПлатежнойПозиции, 
		ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Реквизиты = ЗаявкиНаОперацииПовтИсп.РеквизитыДокументаЗаявка(ДокументСсылка.ЗаявкаНаОперацию.Метаданные().ПолноеИмя());
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Дата КАК Дата,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ЗаявкаНаОперацию КАК ЗаявкаНаОперацию,
	|	ДанныеДокумента.ЗаявкаНаОперацию.ДокументПланирования КАК ДокументРезервирования,
	|	&ДатаНачала КАК ОжидаемаяДатаВыполнения,
	|	ДанныеДокумента.ПриходРасход КАК ПриходРасход,
	|	ДанныеДокумента.ИдентификаторПозиции КАК ИдентификаторПозиции,
	|	ДанныеДокумента.Ответственный КАК Ответственный
	|ИЗ
	|	Документ.ЗакрытиеПлатежнойПозиции КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДатаНачала", "ДанныеДокумента.ЗаявкаНаОперацию."+Реквизиты.ПланируемаяДатаОперации.ИмяРеквизита);
	
	ТаблицаРеквизитов = Запрос.Выполнить().Выгрузить();
	Реквизиты = ТаблицаРеквизитов[0];
	
	Для Каждого Колонка Из ТаблицаРеквизитов.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	// Параметры Оперативного планирования
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПараметрыОперативногоПланированияСрезПоследних.Предназначение КАК Предназначение,
	|	ПараметрыОперативногоПланированияСрезПоследних.ПериодичностьОперативногоПланирования КАК ПериодичностьОП
	|ИЗ
	|	РегистрСведений.ПараметрыОперативногоПланирования КАК ПараметрыОперативногоПланированияСрезПоследних
	|ГДЕ
	|	Предназначение = ЗНАЧЕНИЕ(Перечисление.ПредназначенияЭлементовСтруктурыОтчета.БюджетДвиженияДенежныхСредств)";
	ТаблицаРеквизитов = Запрос.Выполнить().Выгрузить();
	Реквизиты = ТаблицаРеквизитов[0];
	
	Для Каждого Колонка Из ТаблицаРеквизитов.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
КонецПроцедуры

Процедура ТекстЗапросаПлатежныеПозицииКЗакрытию(Запрос, ТекстыЗапроса, Регистры)
	
	Если НЕ ПроведениеСерверОПК.ЕстьТаблицаЗапроса("ВТ_ПлатежныеПозицииКЗакрытию", ТекстыЗапроса) Тогда
		ТекстЗапроса_ВТ_ПлатежныеПозицииКЗакрытию(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ИмяРегистра = "СостоянияИсполненияДокументовПланирования";
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Дата КАК Период,
	|	ВТ_ПлатежныеПозицииКЗакрытию.ОсновнаяПозиция КАК ОсновнаяПозиция,
	|	ВТ_ПлатежныеПозицииКЗакрытию.ЗаявкаНаОперацию КАК ЗаявкаНаОперацию,
	|	ВТ_ПлатежныеПозицииКЗакрытию.ПриходРасход КАК ПриходРасход,
	|	ВТ_ПлатежныеПозицииКЗакрытию.ИдентификаторПозиции КАК ИдентификаторПозиции,
	|	ВТ_ПлатежныеПозицииКЗакрытию.ИдентификаторВстречнойПозиции КАК ИдентификаторВстречнойПозиции,
	|	ВТ_ПлатежныеПозицииКЗакрытию.СостояниеИсполнения КАК СостояниеИсполнения
	|ИЗ
	|	ВТ_ПлатежныеПозицииКЗакрытию КАК ВТ_ПлатежныеПозицииКЗакрытию";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапроса_ВТ_ПлатежныеПозицииКЗакрытию(Запрос, ТекстыЗапроса)

	ИмяВременнойТаблицы = "ВТ_ПлатежныеПозицииКЗакрытию";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ИСТИНА КАК ОсновнаяПозиция,
	|	РазмещениеЗаявок.ЗаявкаНаОперацию КАК ЗаявкаНаОперацию,
	|	РазмещениеЗаявок.ПриходРасход КАК ПриходРасход,
	|	РазмещениеЗаявок.ИдентификаторПозиции КАК ИдентификаторПозиции,
	|	РазмещениеЗаявок.ИдентификаторВстречнойПозиции КАК ИдентификаторВстречнойПозиции,
	|	СостоянияИсполненияДокументовПланированияСрезПоследних.СостояниеИсполнения КАК СостояниеИсполнения
	|ПОМЕСТИТЬ ВТ_ПлатежныеПозицииКЗакрытию
	|ИЗ
	|	РегистрСведений.РазмещениеЗаявок КАК РазмещениеЗаявок
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияИсполненияДокументовПланирования.СрезПоследних(, ) КАК СостоянияИсполненияДокументовПланированияСрезПоследних
	|		ПО РазмещениеЗаявок.ЗаявкаНаОперацию = СостоянияИсполненияДокументовПланированияСрезПоследних.ДокументПланирования
	|			И РазмещениеЗаявок.ИдентификаторПозиции = СостоянияИсполненияДокументовПланированияСрезПоследних.ИдентификаторПозиции
	|ГДЕ
	|	РазмещениеЗаявок.ИдентификаторПозиции = &ИдентификаторПозиции
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЛОЖЬ,
	|	РазмещениеЗаявок.ЗаявкаНаОперацию,
	|	РазмещениеЗаявок.ПриходРасход,
	|	РазмещениеЗаявок.ИдентификаторПозиции,
	|	РазмещениеЗаявок.ИдентификаторВстречнойПозиции,
	|	СостоянияИсполненияДокументовПланированияСрезПоследних.СостояниеИсполнения
	|ИЗ
	|	РегистрСведений.РазмещениеЗаявок КАК РазмещениеЗаявок
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияИсполненияДокументовПланирования.СрезПоследних(, ) КАК СостоянияИсполненияДокументовПланированияСрезПоследних
	|		ПО РазмещениеЗаявок.ЗаявкаНаОперацию = СостоянияИсполненияДокументовПланированияСрезПоследних.ДокументПланирования
	|			И РазмещениеЗаявок.ИдентификаторПозиции = СостоянияИсполненияДокументовПланированияСрезПоследних.ИдентификаторПозиции
	|ГДЕ
	|	РазмещениеЗаявок.ИдентификаторВстречнойПозиции = &ИдентификаторПозиции";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяВременнойТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаДвиженияПоОперПланам(Запрос, ТекстыЗапроса, Регистры)
	
	Если НЕ ПроведениеСерверОПК.ЕстьТаблицаЗапроса("ВТ_ТаблицаДвиженияОперации", ТекстыЗапроса) Тогда
		ТекстЗапроса_ВТ_ТаблицаДвиженияОперации(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если НЕ ПроведениеСерверОПК.ЕстьТаблицаЗапроса("ВТ_ТаблицаПлановССуммамиЛимитирования", ТекстыЗапроса) Тогда
		ТекстЗапроса_ВТ_ПлановССуммамиЛимитирования(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ИмяРегистра = "ОперативныйПланПоБюджетам";
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Дата КАК Период,
	|	ВТ_ТаблицаДвиженияОперации.Предназначение КАК Предназначение,
	|	ВТ_ТаблицаДвиженияОперации.ДокументПланирования КАК ДокументПланирования,
	|	ВТ_ТаблицаДвиженияОперации.ПериодОтчета КАК ПериодОтчета,
	|	ВТ_ТаблицаДвиженияОперации.ЦФО КАК ЦФО,
	|	ВТ_ТаблицаДвиженияОперации.Проект КАК Проект,
	|	ВТ_ТаблицаДвиженияОперации.ПриходРасход КАК ПриходРасход,
	|	ВТ_ТаблицаДвиженияОперации.СтатьяДвиженияДенежныхСредств КАК СтатьяБюджета,
	|	ВТ_ТаблицаДвиженияОперации.Аналитика1 КАК Аналитика1,
	|	ВТ_ТаблицаДвиженияОперации.Аналитика2 КАК Аналитика2,
	|	ВТ_ТаблицаДвиженияОперации.Аналитика3 КАК Аналитика3,
	|	ВТ_ТаблицаДвиженияОперации.Аналитика4 КАК Аналитика4,
	|	ВТ_ТаблицаДвиженияОперации.Аналитика5 КАК Аналитика5,
	|	ВТ_ТаблицаДвиженияОперации.Аналитика6 КАК Аналитика6,
	|	ВТ_ТаблицаДвиженияОперации.Организация КАК Организация,
	|	ВТ_ТаблицаДвиженияОперации.Контрагент КАК Контрагент,
	|	ВТ_ТаблицаДвиженияОперации.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВТ_ТаблицаДвиженияОперации.Валюта КАК Валюта,
	|	ВТ_ТаблицаДвиженияОперации.Заявлено КАК Заявлено
	|ИЗ
	|	ВТ_ТаблицаДвиженияОперации КАК ВТ_ТаблицаДвиженияОперации";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапроса_ВТ_ТаблицаДвиженияОперации(Запрос, ТекстыЗапроса)

	ИмяВременнойТаблицы = "ВТ_ТаблицаДвиженияОперации";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Предназначение КАК Предназначение,
	|	Запрос.ДокументПланирования КАК ДокументПланирования,
	|	Запрос.ДокументРезервирования КАК ДокументРезервирования,
	|	Запрос.ДатаНачала КАК ДатаНачала,
	|	Периоды.Ссылка КАК ПериодОтчета,
	|	Запрос.ЦФО КАК ЦФО,
	|	Запрос.Проект КАК Проект,
	|	Запрос.ПриходРасход КАК ПриходРасход,
	|	Запрос.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	Запрос.Аналитика1 КАК Аналитика1,
	|	Запрос.Аналитика2 КАК Аналитика2,
	|	Запрос.Аналитика3 КАК Аналитика3,
	|	Запрос.Аналитика4 КАК Аналитика4,
	|	Запрос.Аналитика5 КАК Аналитика5,
	|	Запрос.Аналитика6 КАК Аналитика6,
	|	Запрос.Организация КАК Организация,
	|	Запрос.Контрагент КАК Контрагент,
	|	Запрос.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Запрос.Заявлено КАК Заявлено,
	|	Запрос.Валюта КАК Валюта
	|ПОМЕСТИТЬ ВТ_ТаблицаДвиженияОперации
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДвиженияБюджетированияПоПозициям.ЗаявкаНаОперацию КАК ДокументПланирования,
	|		ДвиженияБюджетированияПоПозициям.ЗаявкаНаОперацию.ДокументПланирования КАК ДокументРезервирования,
	|		ВЫБОР
	|			КОГДА ДвиженияБюджетированияПоПозициям.ЗаявкаНаОперацию ССЫЛКА Документ.ЗаявкаНаРасходованиеДенежныхСредств
	|				ТОГДА ДвиженияБюджетированияПоПозициям.ЗаявкаНаОперацию.ЖелательнаяДатаПлатежа
	|			ИНАЧЕ ДвиженияБюджетированияПоПозициям.ЗаявкаНаОперацию.ДатаНачала
	|		КОНЕЦ КАК ДатаНачала,
	|		ДвиженияБюджетированияПоПозициям.ЦФО КАК ЦФО,
	|		ДвиженияБюджетированияПоПозициям.Проект КАК Проект,
	|		ВТ_ПлатежныеПозицииКЗакрытию.ПриходРасход КАК ПриходРасход,
	|		ДвиженияБюджетированияПоПозициям.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|		ДвиженияБюджетированияПоПозициям.Аналитика1 КАК Аналитика1,
	|		ДвиженияБюджетированияПоПозициям.Аналитика2 КАК Аналитика2,
	|		ДвиженияБюджетированияПоПозициям.Аналитика3 КАК Аналитика3,
	|		ДвиженияБюджетированияПоПозициям.Аналитика4 КАК Аналитика4,
	|		ДвиженияБюджетированияПоПозициям.Аналитика5 КАК Аналитика5,
	|		ДвиженияБюджетированияПоПозициям.Аналитика6 КАК Аналитика6,
	|		ДвиженияБюджетированияПоПозициям.ЗаявкаНаОперацию.Организация КАК Организация,
	|		ДвиженияБюджетированияПоПозициям.ЗаявкаНаОперацию.Контрагент КАК Контрагент,
	|		ДвиженияБюджетированияПоПозициям.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|		СУММА(-ДвиженияБюджетированияПоПозициям.Сумма) КАК Заявлено,
	|		ДвиженияБюджетированияПоПозициям.ВалютаВзаиморасчетов КАК Валюта
	|	ИЗ
	|		ВТ_ПлатежныеПозицииКЗакрытию КАК ВТ_ПлатежныеПозицииКЗакрытию
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДвиженияБюджетированияПоПозициям КАК ДвиженияБюджетированияПоПозициям
	|			ПО ВТ_ПлатежныеПозицииКЗакрытию.ЗаявкаНаОперацию = ДвиженияБюджетированияПоПозициям.ЗаявкаНаОперацию
	|				И ВТ_ПлатежныеПозицииКЗакрытию.ИдентификаторПозиции = ДвиженияБюджетированияПоПозициям.ИдентификаторПозиции
	|				И (ВТ_ПлатежныеПозицииКЗакрытию.ОсновнаяПозиция = ИСТИНА)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДвиженияБюджетированияПоПозициям.Аналитика3,
	|		ДвиженияБюджетированияПоПозициям.Аналитика1,
	|		ДвиженияБюджетированияПоПозициям.Аналитика6,
	|		ДвиженияБюджетированияПоПозициям.ЗаявкаНаОперацию,
	|		ДвиженияБюджетированияПоПозициям.ДоговорКонтрагента,
	|		ДвиженияБюджетированияПоПозициям.ВалютаВзаиморасчетов,
	|		ДвиженияБюджетированияПоПозициям.Аналитика2,
	|		ДвиженияБюджетированияПоПозициям.Аналитика5,
	|		ДвиженияБюджетированияПоПозициям.ЦФО,
	|		ДвиженияБюджетированияПоПозициям.Проект,
	|		ВТ_ПлатежныеПозицииКЗакрытию.ПриходРасход,
	|		ДвиженияБюджетированияПоПозициям.Аналитика4,
	|		ДвиженияБюджетированияПоПозициям.СтатьяДвиженияДенежныхСредств,
	|		ДвиженияБюджетированияПоПозициям.ЗаявкаНаОперацию.ДокументПланирования,
	|		ВЫБОР
	|			КОГДА ДвиженияБюджетированияПоПозициям.ЗаявкаНаОперацию ССЫЛКА Документ.ЗаявкаНаРасходованиеДенежныхСредств
	|				ТОГДА ДвиженияБюджетированияПоПозициям.ЗаявкаНаОперацию.ЖелательнаяДатаПлатежа
	|			ИНАЧЕ ДвиженияБюджетированияПоПозициям.ЗаявкаНаОперацию.ДатаНачала
	|		КОНЕЦ,
	|		ДвиженияБюджетированияПоПозициям.ЗаявкаНаОперацию.Организация,
	|		ДвиженияБюджетированияПоПозициям.ЗаявкаНаОперацию.Контрагент) КАК Запрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Периоды КАК Периоды
	|		ПО (&ПериодичностьОП = Периоды.Периодичность)
	|			И (Периоды.Произвольный = ЛОЖЬ)
	|			И Запрос.ДатаНачала >= Периоды.ДатаНачала
	|			И Запрос.ДатаНачала <= Периоды.ДатаОкончания";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяВременнойТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапроса_ВТ_ПлановССуммамиЛимитирования(Запрос, ТекстыЗапроса)

	ИмяВременнойТаблицы = "ВТ_ТаблицаПлановССуммамиЛимитирования";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Дата КАК Период,
	|	ВТ_ТаблицаДвиженияОперации.Предназначение КАК Предназначение,
	|	ВТ_ТаблицаДвиженияОперации.ДокументПланирования КАК ДокументПланирования,
	|	ВТ_ТаблицаДвиженияОперации.ДокументРезервирования КАК ДокументРезервирования,
	|	ВТ_ТаблицаДвиженияОперации.ДатаНачала КАК ДатаНачала,
	|	ВТ_ТаблицаДвиженияОперации.ПериодОтчета КАК ПериодОтчета,
	|	ВТ_ТаблицаДвиженияОперации.ЦФО КАК ЦФО,
	|	ВТ_ТаблицаДвиженияОперации.Проект КАК Проект,
	|	ВТ_ТаблицаДвиженияОперации.ПриходРасход КАК ПриходРасход,
	|	ВТ_ТаблицаДвиженияОперации.СтатьяДвиженияДенежныхСредств КАК СтатьяБюджета,
	|	ВТ_ТаблицаДвиженияОперации.Аналитика1 КАК Аналитика1,
	|	ВТ_ТаблицаДвиженияОперации.Аналитика2 КАК Аналитика2,
	|	ВТ_ТаблицаДвиженияОперации.Аналитика3 КАК Аналитика3,
	|	ВТ_ТаблицаДвиженияОперации.Аналитика4 КАК Аналитика4,
	|	ВТ_ТаблицаДвиженияОперации.Аналитика5 КАК Аналитика5,
	|	ВТ_ТаблицаДвиженияОперации.Аналитика6 КАК Аналитика6,
	|	ВТ_ТаблицаДвиженияОперации.Организация КАК Организация,
	|	ВТ_ТаблицаДвиженияОперации.Контрагент КАК Контрагент,
	|	ВТ_ТаблицаДвиженияОперации.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВТ_ТаблицаДвиженияОперации.Валюта КАК Валюта,
	|	0 КАК Лимит,
	|	0 КАК Зарезервировано,
	|	ВТ_ТаблицаДвиженияОперации.Заявлено КАК Заявлено,
	|	0 КАК Исполнено
	|ПОМЕСТИТЬ ВТ_ТаблицаПлановССуммамиЛимитирования
	|ИЗ
	|	ВТ_ТаблицаДвиженияОперации КАК ВТ_ТаблицаДвиженияОперации";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяВременнойТаблицы);
	
КонецПроцедуры

Процедура ПодготовитьТаблицуЛимитовПоБюджетам(ДокументСсылка, Запрос, ТаблицыДляДвижений)
	
	ТаблицаЛимитов = КонтрольЛимитовУХ.ПолучитьТаблицуЛимитов(Запрос, Запрос.Параметры.ОжидаемаяДатаВыполнения);
	
	ТаблицыДляДвижений.Вставить("ТаблицаЛимитыПоБюджетам", ТаблицаЛимитов);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли 

