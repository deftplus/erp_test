#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("ОсновныеСредства");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("СебестоимостьИПартионныйУчет");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("ОборотныеРегистрыУправленческогоУчета");
	МеханизмыДокумента.Добавить("ОбесценениеВНА");
	
	АмортизацияОСЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура:
//     * Ключ - Строка - Имя таблицы.
//     * Значение - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ, ДопПараметры);
		
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаАмортизацияОС(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаОбесценениеВНА(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
		
		АмортизацияОСЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
		
	КонецЕсли;
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

#Область Команды

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	Отчеты.СправкаРасчетАмортизацииОС_МУ.ДобавитьКомандуОтчетаПоДокументуАмортизацииОС(КомандыОтчетов);
	
	АмортизацияОСЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	АмортизацияОСЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	АмортизацияОСЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Представление = СтрШаблон(НСтр("ru = 'Амортизация ОС %1 за %2';
									|en = 'The %1 Fixed assets — Depreciation charge for %2'"), Данные.Номер, Формат(Данные.Дата, НСтр("ru = 'ДФ=''ММММ гггг''';
																											|en = 'DF=''MMMM yyyy'''")));
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет расчет амортизации в заданном месяце и записывает его с помощью проведения документа "Амортизация ОС".
//
// Параметры:
//  ИсходныеПараметрыРасчета - см. ВнеоборотныеАктивы.НовыеПараметрыРасчетаАмортизации.
//  АдресХранилища - Строка - Адрес временного хранилища.
//
Процедура РассчитатьАмортизацию(ИсходныеПараметрыРасчета, АдресХранилища) Экспорт
	
	ОписаниеЗамера = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("ЗакрытиеМесяца.НачислениеАмортизацииОС");
	
	ПараметрыРасчета = ВнеоборотныеАктивыСлужебный.СкопироватьПараметрыРасчетаАмортизации(ИсходныеПараметрыРасчета);
	ВнеоборотныеАктивыСлужебный.ДополнитьПараметрамиУчетаВнеоборотныхАктивов(ПараметрыРасчета);
	
	Если ПараметрыРасчета.МенеджерВременныхТаблиц = Неопределено Тогда
		ПараметрыРасчета.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		ИсходныеПараметрыРасчета.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
	КонецЕсли;
	
	Попытка
		
		ПодготовитьПараметрыДляРасчетаАмортизации(ПараметрыРасчета);
		
		ВыполнитьРасчетАмортизации(ПараметрыРасчета);
		
		ЗарегистрироватьОшибкиРасчетаАмортизации(ПараметрыРасчета);
	
		ОбщегоНазначенияУТ.УничтожитьВременныеТаблицы(ПараметрыРасчета.МенеджерВременныхТаблиц,, ПараметрыРасчета.ИспользуемыеТаблицы);
		
		ЗаписатьРасчетАмортизации(ПараметрыРасчета);
	
	Исключение
		
		ПараметрыРасчета.ЕстьОшибки = Истина;
		ПараметрыРасчета.ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
			ПараметрыРасчета.ЭтапРасчета, 
			ПараметрыРасчета.Период, 
			ПараметрыРасчета.ПакетыАмортизации.ВыгрузитьКолонку("Организация"),
			ПараметрыРасчета.ТекстОшибки);
			
	КонецПопытки; 
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, ПараметрыРасчета.КоличествоОбработанныхДанных / 100);
	
	Если ЭтоАдресВременногоХранилища(АдресХранилища) Тогда
		РезультатРасчета = Новый Структура;
		РезультатРасчета.Вставить("ЕстьОшибки", ПараметрыРасчета.ЕстьОшибки);
		РезультатРасчета.Вставить("ТекстОшибки", ПараметрыРасчета.ТекстОшибки);
		ПоместитьВоВременноеХранилище(РезультатРасчета, АдресХранилища);
	КонецЕсли;
	
КонецПроцедуры

// Определяет номер пакета амортизации.
//
// Параметры:
//  Организация	 - СправочникСсылка.Организации - Организация.
//
// Возвращаемое значение:
//  Число - Номер пакета.
//
Функция НомерПакетаПоУмолчанию(Организация) Экспорт

	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат 0;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(ПакетыАмортизацииОС.НомерПакета) КАК НомерПакета,
	|	ЕСТЬNULL(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПакетыАмортизацииОС.НомерПакета), 0) КАК КоличествоНомеров
	|ИЗ
	|	РегистрСведений.ПакетыАмортизацииОС КАК ПакетыАмортизацииОС
	|ГДЕ
	|	ПакетыАмортизацииОС.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ПакетыАмортизацииОС.Организация";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	НомерПакета = 0;
	Если Выборка.Следующий() Тогда
		Если Выборка.КоличествоНомеров = 1 Тогда
			НомерПакета = Выборка.НомерПакета;
		КонецЕсли; 
	КонецЕсли;

	Возврат НомерПакета;
	
КонецФункции

// Добавляет в список запросов формирование временной таблицы НачисленнаяАмортизация.
//  Используется для универсального начисления амортизации.
//  Созданная временная таблица может использоваться для формирования движений по регистрам.
//
// Параметры:
//  СписокЗапросов - Массив, СписокЗначений - Список запросов.
//  ТекстПодзапроса	- Строка - Текст запроса, содержащий результат расчета амортизации.
//  ПроведениеДокумента - Булево - Истина, если процедура вызывается при проведении документа.
//
Процедура ТекстЗапросаВтНачисленнаяАмортизация(СписокЗапросов, ТекстПодзапроса, ПроведениеДокумента = Ложь) Экспорт
	
	ИмяТаблицы = "НачисленнаяАмортизация";
	
	ТекстЗапроса = АмортизацияОСЛокализация.ТекстЗапросаВтНачисленнаяАмортизация();
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.ОсновноеСредство) КАК ВидВНА,
		|	НачисленнаяАмортизация.ПериодРасчета КАК ПериодРасчета,
		|	НачисленнаяАмортизация.Организация КАК Организация,
		|	ЕСТЬNULL(ПакетыАмортизацииОС.НомерПакета, 0) КАК НомерПакета,
		|	НачисленнаяАмортизация.Подразделение КАК Подразделение,
		|	НачисленнаяАмортизация.КорПодразделение КАК КорПодразделение,
		|	НачисленнаяАмортизация.ОсновноеСредство КАК ОсновноеСредство,
		|	НачисленнаяАмортизация.ЕГДС КАК ЕГДС,
		|	НачисленнаяАмортизация.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
		|	НачисленнаяАмортизация.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	НачисленнаяАмортизация.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
		|	НачисленнаяАмортизация.СтатьяРасходов КАК СтатьяРасходов,
		|	НачисленнаяАмортизация.АналитикаРасходов КАК АналитикаРасходов,
		|	ЛОЖЬ КАК ПередаватьРасходыВДругуюОрганизацию,
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК ОрганизацияПолучательРасходов,
		|	ЛОЖЬ КАК НачислятьИзнос,
		|	НачисленнаяАмортизация.ЭтоДоначисление КАК ЭтоДоначисление,
		|	ЛОЖЬ КАК ЭтоДоначислениеБУ,
		|	ЛОЖЬ КАК ЭтоАмортизационнаяПремия,
		|	МАКСИМУМ(НачисленнаяАмортизация.РесурсВыработанныйЗаПрошлыйПериод) КАК РесурсВыработанныйЗаПрошлыйПериод,
		|	МАКСИМУМ(НачисленнаяАмортизация.РесурсВыработанныйЗаТекущийПериод) КАК РесурсВыработанныйЗаТекущийПериод,
		|	0 КАК РесурсВыработанныйЗаПрошлыйПериодБУ,
		|	0 КАК РесурсВыработанныйЗаТекущийПериодБУ,
		|	МАКСИМУМ(НачисленнаяАмортизация.СтоимостьДляВычисленияАмортизации) КАК СтоимостьДляВычисленияАмортизации,
		|	МАКСИМУМ(НачисленнаяАмортизация.СтоимостьДляВычисленияАмортизацииРегл) КАК СтоимостьДляВычисленияАмортизацииРегл,
		|	МАКСИМУМ(НачисленнаяАмортизация.ОстатокОбесцененияРегл) КАК ОстатокОбесцененияРегл,
		|	МАКСИМУМ(НачисленнаяАмортизация.ОстатокОбесцененияУпр) КАК ОстатокОбесцененияУпр,
		|	МАКСИМУМ(НачисленнаяАмортизация.ПорядокУчетаУУ) КАК ПорядокУчетаУУ,
		|	НЕОПРЕДЕЛЕНО КАК ПорядокУчетаБУ,
		|	МАКСИМУМ(НачисленнаяАмортизация.ВариантПроводокПоОбесценениюВНА) КАК ВариантПроводокПоОбесценениюВНА,
		|
		|	&ИдентификаторСтрокиТаблицы КАК ИдентификаторСтроки,
		|
		|	СУММА(НачисленнаяАмортизация.СуммаУУ) КАК СуммаУУ,
		|	СУММА(НачисленнаяАмортизация.СуммаБУ) КАК СуммаБУ,
		|	0 КАК СуммаНУ,
		|	0 КАК СуммаПР,
		|	0 КАК СуммаВР,
		|	0 КАК СуммаЦФ,
		|	0 КАК СуммаНУЦФ,
		|	0 КАК СуммаПРЦФ,
		|	0 КАК СуммаВРЦФ,
		|
		|	СУММА(НачисленнаяАмортизация.ОбесценениеУУ) КАК ОбесценениеУУ,
		|	СУММА(НачисленнаяАмортизация.ОбесценениеБУ) КАК ОбесценениеБУ,
		|	0 КАК ОбесценениеЦФ
		|
		|ПОМЕСТИТЬ НачисленнаяАмортизация
		|
		|ИЗ
		|	&ТекстПодзапроса КАК НачисленнаяАмортизация
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыАмортизацииОС КАК ПакетыАмортизацииОС
		|		ПО ПакетыАмортизацииОС.Организация = НачисленнаяАмортизация.Организация
		|			И ПакетыАмортизацииОС.ОсновноеСредство = НачисленнаяАмортизация.ОсновноеСредство
		|
		|СГРУППИРОВАТЬ ПО
		|	НачисленнаяАмортизация.ПериодРасчета,
		|	НачисленнаяАмортизация.Организация,
		|	НачисленнаяАмортизация.ЕГДС,
		|	ЕСТЬNULL(ПакетыАмортизацииОС.НомерПакета, 0),
		|	НачисленнаяАмортизация.Подразделение,
		|	НачисленнаяАмортизация.КорПодразделение,
		|	НачисленнаяАмортизация.ГруппаФинансовогоУчета,
		|	НачисленнаяАмортизация.НаправлениеДеятельности,
		|	НачисленнаяАмортизация.КорНаправлениеДеятельности,
		|	НачисленнаяАмортизация.ОсновноеСредство,
		|	НачисленнаяАмортизация.СтатьяРасходов,
		|	НачисленнаяАмортизация.АналитикаРасходов,
		|	НачисленнаяАмортизация.ЭтоДоначисление,
		|	&ГруппировкаИдентификатора";
	
	КонецЕсли; 
	
	Если ПроведениеДокумента Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИдентификаторСтрокиТаблицы", "НачисленнаяАмортизация.ИдентификаторСтроки");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ГруппировкаИдентификатора",  "НачисленнаяАмортизация.ИдентификаторСтроки");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИдентификаторСтрокиТаблицы", "АВТОНОМЕРЗАПИСИ()");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ГруппировкаИдентификатора",  "ИСТИНА");
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПодзапроса", "(" + ТекстПодзапроса + ")");
	
	Если ТипЗнч(СписокЗапросов) = Тип("СписокЗначений") Тогда
		СписокЗапросов.Добавить(ТекстЗапроса, ИмяТаблицы);
	Иначе
		СписокЗапросов.Добавить(ТекстЗапроса);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	Возврат ИсточникиДанных;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.АмортизацияОС2_4";
	
	ВЗапросеЕстьИсточник = Истина;
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		ВЗапросеЕстьИсточник = Ложь;
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры)
	
	МенеджерВременныхТаблиц = Неопределено;
	
	Если ДопПараметры <> Неопределено
		И ДопПараметры.Свойство("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц) Тогда
		
		МенеджерВременныхТаблиц = ?(МенеджерВременныхТаблиц = Неопределено,
									Новый МенеджерВременныхТаблиц,
									МенеджерВременныхТаблиц);
		
	Иначе
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	КонецЕсли;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка           КАК Ссылка,
	|	ДанныеДокумента.Дата             КАК Период,
	|	ДанныеДокумента.Номер            КАК Номер,
	|	ДанныеДокумента.Проведен         КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления  КАК ПометкаУдаления,
	|	ДанныеДокумента.Комментарий      КАК Комментарий,
	|	ДанныеДокумента.Ответственный    КАК Ответственный,
	|	ДанныеДокумента.Организация      КАК Организация,
	|	ДанныеДокумента.НомерПакета      КАК НомерПакета
	|ИЗ
	|	Документ.АмортизацияОС2_4 КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.АмортизацияОС2_4"));
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.АмортизацияОС);
	ЗначенияПараметровПроведения.Вставить("СтатьяАП_ОС", ПланыВидовХарактеристик.СтатьиАктивовПассивов.ОсновныеСредства);

	ЗначенияПараметровПроведения.Вставить(
		"ДоначислятьКорректировкуОбесценения", 
		Перечисления.ВариантыПроводокПоОбесценениюВНА.ДоначислятьКорректировкуОбесценения);
	
	ЗначенияПараметровПроведения.Вставить(
		"СторнироватьКорректировкуОбесценения", 
		Перечисления.ВариантыПроводокПоОбесценениюВНА.СторнироватьКорректировкуОбесценения);

	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ТекстЗапросаТаблицаАмортизацияОС(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "АмортизацияОС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстыЗапросаОбъединение = Новый Массив;
	
	#Область Амортизация // Дт 91 - Кт 02.ам
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	&Ссылка                                         КАК Регистратор,
	|	НачисленнаяАмортизация.ПериодРасчета            КАК Период,
	|	НачисленнаяАмортизация.Организация              КАК Организация,
	|	НачисленнаяАмортизация.Подразделение            КАК Подразделение,
	|	НачисленнаяАмортизация.ОсновноеСредство         КАК ОсновноеСредство,
	|	НачисленнаяАмортизация.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|
	|	НачисленнаяАмортизация.СуммаУУ    КАК Амортизация,
	|	НачисленнаяАмортизация.СуммаБУ    КАК АмортизацияРегл,
	|	НачисленнаяАмортизация.СуммаНУ    КАК АмортизацияНУ,
	|	НачисленнаяАмортизация.СуммаПР    КАК АмортизацияПР,
	|	НачисленнаяАмортизация.СуммаВР    КАК АмортизацияВР,
	|	НачисленнаяАмортизация.СуммаЦФ    КАК АмортизацияЦФ,
	|	НачисленнаяАмортизация.СуммаНУЦФ  КАК АмортизацияНУЦФ,
	|	НачисленнаяАмортизация.СуммаПРЦФ  КАК АмортизацияПРЦФ,
	|	НачисленнаяАмортизация.СуммаВРЦФ  КАК АмортизацияВРЦФ,
	|
	|	ВЫБОР 
	|		КОГДА НачисленнаяАмортизация.ПередаватьРасходыВДругуюОрганизацию
	|				И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА НачисленнаяАмортизация.ОрганизацияПолучательРасходов
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	КОНЕЦ КАК КорОрганизация,
	|	НачисленнаяАмортизация.КорПодразделение              КАК КорПодразделение,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	НачисленнаяАмортизация.СтатьяРасходов                КАК КорСтатьяРасходов,
	|	НачисленнаяАмортизация.АналитикаРасходов             КАК КорАналитикаРасходов,
	|	НачисленнаяАмортизация.НачислятьИзнос                КАК НачислятьИзнос,
	|
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаПрошлыйПериодБУ КАК РесурсВыработанныйЗаПрошлыйПериодБУ,
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаТекущийПериодБУ КАК РесурсВыработанныйЗаТекущийПериодБУ,
	|
	|	НачисленнаяАмортизация.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	НачисленнаяАмортизация.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|
	|	НачисленнаяАмортизация.ОстатокОбесцененияРегл КАК ОстатокОбесцененияРегл,
	|	НачисленнаяАмортизация.ОстатокОбесцененияУпр  КАК ОстатокОбесцененияУпр,
	|
	|	НачисленнаяАмортизация.ЭтоДоначисление                   КАК ЭтоДоначисление,
	|	НачисленнаяАмортизация.ЭтоДоначислениеБУ                 КАК ЭтоДоначислениеБУ,
	|
	|	НачисленнаяАмортизация.ПорядокУчетаУУ                    КАК ПорядокУчетаУУ,
	|	НачисленнаяАмортизация.ПорядокУчетаБУ                    КАК ПорядокУчетаБУ,
	|	
	|	НачисленнаяАмортизация.ИдентификаторСтроки               КАК ИдентификаторФинЗаписи,
	|
	|	ВЫБОР 
	|		КОГДА НачисленнаяАмортизация.ПередаватьРасходыВДругуюОрганизацию
	|				И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.АмортизацияОСвДругуюОрганизацию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.АмортизацияОС)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|
	|	ВЫБОР 
	|		КОГДА НачисленнаяАмортизация.ПередаватьРасходыВДругуюОрганизацию
	|				И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.АмортизацияОСвДругуюОрганизацию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.АмортизацияОС)
	|	КОНЕЦ КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|
	|	И (НачисленнаяАмортизация.СуммаУУ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаБУ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаНУ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаПР <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаВР <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаЦФ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаНУЦФ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаПРЦФ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаВРЦФ <> 0)";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область КорректировкаОбесценения // Дт 02.об - Кт 02.ам
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	&Ссылка                                         КАК Регистратор,
	|	НачисленнаяАмортизация.ПериодРасчета            КАК Период,
	|	НачисленнаяАмортизация.Организация              КАК Организация,
	|	НачисленнаяАмортизация.Подразделение            КАК Подразделение,
	|	НачисленнаяАмортизация.ОсновноеСредство         КАК ОсновноеСредство,
	|	НачисленнаяАмортизация.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|
	|	НачисленнаяАмортизация.ОбесценениеУУ            КАК Амортизация,
	|	НачисленнаяАмортизация.ОбесценениеБУ            КАК АмортизацияРегл,
	|	0                                               КАК АмортизацияНУ,
	|	0                                               КАК АмортизацияПР,
	|	НачисленнаяАмортизация.ОбесценениеБУ            КАК АмортизацияВР,
	|	НачисленнаяАмортизация.ОбесценениеЦФ            КАК АмортизацияЦФ,
	|	0                                               КАК АмортизацияНУЦФ,
	|	0                                               КАК АмортизацияПРЦФ,
	|	НачисленнаяАмортизация.ОбесценениеЦФ            КАК АмортизацияВРЦФ,
	|
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)        КАК КорОрганизация,
	|	НачисленнаяАмортизация.КорПодразделение              КАК КорПодразделение,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	НачисленнаяАмортизация.СтатьяРасходов                КАК КорСтатьяРасходов,
	|	НачисленнаяАмортизация.АналитикаРасходов             КАК КорАналитикаРасходов,
	|	НачисленнаяАмортизация.НачислятьИзнос                КАК НачислятьИзнос,
	|
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаПрошлыйПериодБУ КАК РесурсВыработанныйЗаПрошлыйПериодБУ,
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаТекущийПериодБУ КАК РесурсВыработанныйЗаТекущийПериодБУ,
	|
	|	НачисленнаяАмортизация.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	НачисленнаяАмортизация.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|
	|	НачисленнаяАмортизация.ОстатокОбесцененияРегл КАК ОстатокОбесцененияРегл,
	|	НачисленнаяАмортизация.ОстатокОбесцененияУпр  КАК ОстатокОбесцененияУпр,
	|
	|	НачисленнаяАмортизация.ЭтоДоначисление                   КАК ЭтоДоначисление,
	|	НачисленнаяАмортизация.ЭтоДоначислениеБУ                 КАК ЭтоДоначислениеБУ,
	|
	|	НачисленнаяАмортизация.ПорядокУчетаУУ                    КАК ПорядокУчетаУУ,
	|	НачисленнаяАмортизация.ПорядокУчетаБУ                    КАК ПорядокУчетаБУ,
	|	
	|	НачисленнаяАмортизация.ИдентификаторСтроки               КАК ИдентификаторФинЗаписи,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаОбесцененияОС) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КорректировкаОбесцененияОС) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|
	|	И НачисленнаяАмортизация.ВариантПроводокПоОбесценениюВНА = &ДоначислятьКорректировкуОбесценения
	|
	|	И (НачисленнаяАмортизация.ОбесценениеУУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеБУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеЦФ <> 0)";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапросаОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Процедура ТекстЗапросаТаблицаОбесценениеВНА(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ОбесценениеВНА";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапросаОбъединение = Новый Массив;
	
	#Область СторноОбесценения // Дт 91 - Кт 02.об (сторно)
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	&Ссылка                                         КАК Регистратор,
	|	НачисленнаяАмортизация.ПериодРасчета            КАК Период,
	|	НачисленнаяАмортизация.ОсновноеСредство         КАК ВнеоборотныйАктив,
	|	НачисленнаяАмортизация.Организация              КАК Организация,
	|	НачисленнаяАмортизация.Подразделение            КАК Подразделение,
	|	НачисленнаяАмортизация.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|	НачисленнаяАмортизация.ЕГДС                     КАК ЕГДС,
	|	НачисленнаяАмортизация.ИдентификаторСтроки      КАК ИдентификаторФинЗаписи,
	|
	|	ВЫБОР 
	|		КОГДА НачисленнаяАмортизация.ПередаватьРасходыВДругуюОрганизацию
	|				И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноОбесцененияОСВДругуюОрганизацию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноОбесцененияОС)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|
	|	ВЫБОР 
	|		КОГДА НачисленнаяАмортизация.ПередаватьРасходыВДругуюОрганизацию
	|				И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СторноОбесцененияОСВДругуюОрганизацию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СторноОбесцененияОС)
	|	КОНЕЦ КАК НастройкаХозяйственнойОперации,
	|
	|	-(НачисленнаяАмортизация.ОбесценениеБУ + НачисленнаяАмортизация.ОбесценениеЦФ) КАК ОбесценениеРегл,
	|	-НачисленнаяАмортизация.ОбесценениеУУ КАК ОбесценениеУпр
	|	
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|
	|	И НачисленнаяАмортизация.ВариантПроводокПоОбесценениюВНА = &СторнироватьКорректировкуОбесценения
	|
	|	И (НачисленнаяАмортизация.ОбесценениеУУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеБУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеЦФ <> 0)";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область КорректировкаОбесценения // Дт 02.об - Кт 02.ам
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	&Ссылка                                         КАК Регистратор,
	|	НачисленнаяАмортизация.ПериодРасчета            КАК Период,
	|	НачисленнаяАмортизация.ОсновноеСредство         КАК ВнеоборотныйАктив,
	|	НачисленнаяАмортизация.Организация              КАК Организация,
	|	НачисленнаяАмортизация.Подразделение            КАК Подразделение,
	|	НачисленнаяАмортизация.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|	НачисленнаяАмортизация.ЕГДС                     КАК ЕГДС,
	|	НачисленнаяАмортизация.ИдентификаторСтроки      КАК ИдентификаторФинЗаписи,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаОбесцененияОС) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КорректировкаОбесцененияОС) КАК НастройкаХозяйственнойОперации,
	|
	|	НачисленнаяАмортизация.ОбесценениеБУ + НачисленнаяАмортизация.ОбесценениеЦФ КАК ОбесценениеРегл,
	|	НачисленнаяАмортизация.ОбесценениеУУ КАК ОбесценениеУпр
	|	
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|
	|	И НачисленнаяАмортизация.ВариантПроводокПоОбесценениюВНА = &ДоначислятьКорректировкуОбесценения
	|
	|	И (НачисленнаяАмортизация.ОбесценениеУУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеБУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеЦФ <> 0)
	|";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапросаОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПартииПрочихРасходов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&Ответственный                          КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	ЛОЖЬ                                    КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО                            КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО                            КАК ИсправляемыйДокумент,
	|	&Период                                 КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО                            КАК Приоритет";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	СписокЗапросовОбъединение = Новый Массив;
	
	СписокЗапросовОбъединение.Добавить(РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы());
	СписокЗапросовОбъединение.Добавить(ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеНачисленнойАмортизацииВПрочихРасходах());
	СписокЗапросовОбъединение.Добавить(ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеОбесцененияВПрочихРасходах());
	
	АмортизацияОСЛокализация.ДополнитьТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(СписокЗапросовОбъединение);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтПартииПрочихРасходов";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаВтПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПартииПрочихРасходов";
	
	СписокЗапросовОбъединение = Новый Массив;
	СписокЗапросовОбъединение.Добавить(РегистрыНакопления.ПартииПрочихРасходов.ТекстОписаниеВтИсходныеПартииПрочихРасходов());
	СписокЗапросовОбъединение.Добавить(ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеНачисленнойАмортизацииВПартияхПрочихРасходов());
	СписокЗапросовОбъединение.Добавить(ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеОбесцененияВПартияхПрочихРасходах());
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияПоПрочимАктивамПассивам";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	СписокЗапросовОбъединение = Новый Массив;
	
	#Область Амортизация
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НачисленнаяАмортизация.ПериодРасчета                 КАК Период,
	|	НачисленнаяАмортизация.Организация                   КАК Организация,
	|	НачисленнаяАмортизация.Подразделение                 КАК Подразделение,
	|	НачисленнаяАмортизация.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыПоВыделенномуИмуществу) КАК Статья,
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов КАК Аналитика,
	|	НЕОПРЕДЕЛЕНО                                         КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	НачисленнаяАмортизация.СуммаБУ КАК СуммаРегл,
	|	НачисленнаяАмортизация.СуммаПР КАК ПостояннаяРазница,
	|	НачисленнаяАмортизация.СуммаВР КАК ВременнаяРазница,
	|	НачисленнаяАмортизация.СуммаУУ КАК СуммаУпр,
	|	НачисленнаяАмортизация.СуммаУУ КАК СуммаСНДС,
	|	НачисленнаяАмортизация.СуммаУУ КАК СуммаБезНДС,
	|
	|	НачисленнаяАмортизация.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.АмортизацияОСвДругуюОрганизацию) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|	И НачисленнаяАмортизация.ПередаватьРасходыВДругуюОрганизацию
	|	И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И НЕ НачисленнаяАмортизация.НачислятьИзнос
	|
	|	И (НачисленнаяАмортизация.СуммаБУ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаПР <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаВР <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаУУ <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НачисленнаяАмортизация.ПериодРасчета                  КАК Период,
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов  КАК Организация,
	|	НачисленнаяАмортизация.КорПодразделение               КАК Подразделение,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыПоВыделенномуИмуществу) КАК Статья,
	|	НачисленнаяАмортизация.Организация                    КАК Аналитика,
	|	НЕОПРЕДЕЛЕНО                                          КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|
	|	НачисленнаяАмортизация.СуммаБУ КАК СуммаРегл,
	|	НачисленнаяАмортизация.СуммаПР КАК ПостояннаяРазница,
	|	НачисленнаяАмортизация.СуммаВР КАК ВременнаяРазница,
	|	НачисленнаяАмортизация.СуммаУУ КАК СуммаУпр,
	|	НачисленнаяАмортизация.СуммаУУ КАК СуммаСНДС,
	|	НачисленнаяАмортизация.СуммаУУ КАК СуммаБезНДС,
	|
	|	НачисленнаяАмортизация.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.АмортизацияОСизДругойОрганизации) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|	И НачисленнаяАмортизация.ПередаватьРасходыВДругуюОрганизацию
	|	И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И НЕ НачисленнаяАмортизация.НачислятьИзнос
	|
	|	И (НачисленнаяАмортизация.СуммаБУ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаПР <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаВР <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаУУ <> 0)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область СторноОбесценения
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НачисленнаяАмортизация.ПериодРасчета                 КАК Период,
	|	НачисленнаяАмортизация.Организация                   КАК Организация,
	|	НачисленнаяАмортизация.Подразделение                 КАК Подразделение,
	|	НачисленнаяАмортизация.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыПоВыделенномуИмуществу) КАК Статья,
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов КАК Аналитика,
	|	НЕОПРЕДЕЛЕНО                                         КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	-НачисленнаяАмортизация.ОбесценениеБУ КАК СуммаРегл,
	|	0 КАК ПостояннаяРазница,
	|	-НачисленнаяАмортизация.ОбесценениеБУ КАК ВременнаяРазница,
	|	-НачисленнаяАмортизация.ОбесценениеУУ КАК СуммаУпр,
	|	-НачисленнаяАмортизация.ОбесценениеУУ КАК СуммаСНДС,
	|	-НачисленнаяАмортизация.ОбесценениеУУ КАК СуммаБезНДС,
	|
	|	НачисленнаяАмортизация.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СторноОбесцененияОСВДругуюОрганизацию) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|	И НачисленнаяАмортизация.ПередаватьРасходыВДругуюОрганизацию
	|	И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И НЕ НачисленнаяАмортизация.НачислятьИзнос
	|
	|	И НачисленнаяАмортизация.ВариантПроводокПоОбесценениюВНА = &СторнироватьКорректировкуОбесценения
	|
	|	И (НачисленнаяАмортизация.ОбесценениеУУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеБУ <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НачисленнаяАмортизация.ПериодРасчета                  КАК Период,
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов  КАК Организация,
	|	НачисленнаяАмортизация.КорПодразделение               КАК Подразделение,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыПоВыделенномуИмуществу) КАК Статья,
	|	НачисленнаяАмортизация.Организация                    КАК Аналитика,
	|	НЕОПРЕДЕЛЕНО                                          КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|
	|	-НачисленнаяАмортизация.ОбесценениеБУ КАК СуммаРегл,
	|	0 КАК ПостояннаяРазница,
	|	-НачисленнаяАмортизация.ОбесценениеБУ КАК ВременнаяРазница,
	|	-НачисленнаяАмортизация.ОбесценениеУУ КАК СуммаУпр,
	|	-НачисленнаяАмортизация.ОбесценениеУУ КАК СуммаСНДС,
	|	-НачисленнаяАмортизация.ОбесценениеУУ КАК СуммаБезНДС,
	|
	|	НачисленнаяАмортизация.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СторноОбесцененияОСизДругойОрганизации) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|	И НачисленнаяАмортизация.ПередаватьРасходыВДругуюОрганизацию
	|	И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И НЕ НачисленнаяАмортизация.НачислятьИзнос
	|
	|	И НачисленнаяАмортизация.ВариантПроводокПоОбесценениюВНА = &СторнироватьКорректировкуОбесценения
	|
	|	И (НачисленнаяАмортизация.ОбесценениеУУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеБУ <> 0)
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	АмортизацияОСЛокализация.ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(СписокЗапросовОбъединение);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
	Возврат ТекстЗапроса;

КонецФункции

Процедура ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапросаОбъединение = Новый Массив;
	
	#Область СторноОбесценения // Дт 91 - Кт 02.об (сторно)
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НачисленнаяАмортизация.ПериодРасчета КАК Период,
	|	НачисленнаяАмортизация.Организация КАК Организация,
	|
	|	НачисленнаяАмортизация.Подразделение КАК Подразделение,
	|	НачисленнаяАмортизация.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС КАК Статья,
	|	НачисленнаяАмортизация.ОсновноеСредство КАК АналитикаАктивовПассивов,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|
	|	НачисленнаяАмортизация.КорПодразделение КАК КорПодразделение,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	НачисленнаяАмортизация.СтатьяРасходов КАК КорСтатья,
	|	НачисленнаяАмортизация.АналитикаРасходов КАК КорАналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК КорАналитикаАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО КАК КорГруппаФинансовогоУчета,
	|
	|	-НачисленнаяАмортизация.ОбесценениеУУ КАК Сумма,
	|	-НачисленнаяАмортизация.ОбесценениеУУ КАК СуммаУпр,
	|	-(НачисленнаяАмортизация.ОбесценениеБУ + НачисленнаяАмортизация.ОбесценениеЦФ) КАК СуммаРегл,
	|
	|	ВЫБОР 
	|		КОГДА НачисленнаяАмортизация.ПередаватьРасходыВДругуюОрганизацию
	|				И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноОбесцененияОСВДругуюОрганизацию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноОбесцененияОС)
	|	КОНЕЦ КАК ХозяйственнаяОперация
	|
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|
	|	И НачисленнаяАмортизация.ВариантПроводокПоОбесценениюВНА = &СторнироватьКорректировкуОбесценения
	|
	|	И (НачисленнаяАмортизация.ОбесценениеУУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеБУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеЦФ <> 0)";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область КорректировкаОбесценения // Дт 02.об - Кт 02.ам
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НачисленнаяАмортизация.ПериодРасчета КАК Период,
	|	НачисленнаяАмортизация.Организация КАК Организация,
	|
	|	НачисленнаяАмортизация.Подразделение КАК Подразделение,
	|	НачисленнаяАмортизация.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС КАК Статья,
	|	НачисленнаяАмортизация.ОсновноеСредство КАК АналитикаАктивовПассивов,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|
	|	НачисленнаяАмортизация.Подразделение КАК КорПодразделение,
	|	НачисленнаяАмортизация.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО КАК КорАналитикаРасходов,
	|	НачисленнаяАмортизация.ОсновноеСредство КАК КорАналитикаАктивовПассивов,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета КАК КорГруппаФинансовогоУчета,
	|
	|	НачисленнаяАмортизация.ОбесценениеУУ КАК Сумма,
	|	НачисленнаяАмортизация.ОбесценениеУУ КАК СуммаУпр,
	|	НачисленнаяАмортизация.ОбесценениеБУ + НачисленнаяАмортизация.ОбесценениеЦФ КАК СуммаРегл,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаОбесцененияОС) КАК ХозяйственнаяОперация
	|
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|
	|	И НачисленнаяАмортизация.ВариантПроводокПоОбесценениюВНА = &ДоначислятьКорректировкуОбесценения
	|
	|	И (НачисленнаяАмортизация.ОбесценениеУУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеБУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеЦФ <> 0)";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапросаОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат АмортизацияОСЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат АмортизацияОСЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

#КонецОбласти

#Область РасчетАмортизации

Процедура ПодготовитьПараметрыДляРасчетаАмортизации(ПараметрыРасчета)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
	ВнеоборотныеАктивы.УстановитьПараметрыЗапросаДляРасчетаАмортизации(Запрос, ПараметрыРасчета);
	
	Запрос.УстановитьПараметр("Дата", КонецМесяца(ПараметрыРасчета.Период));
	Запрос.УстановитьПараметр("БазоваяВалюта", ЗначениеНастроекПовтИсп.БазоваяВалютаПоУмолчанию());

	//
	СписокЗапросов = Новый Массив;

	ТекстЗапросаПакетыАмортизации(СписокЗапросов);
	ТекстЗапросаСписокВсехОС(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаСписокОбъектовУчета(СписокЗапросов, ПараметрыРасчета);
	
	ТекстЗапросаПараметрыРасчетаАмортизации(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаПроверкаПараметровРасчета(СписокЗапросов, ПараметрыРасчета);

	Запрос.Текст = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());

	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры

Процедура ВыполнитьРасчетАмортизации(ПараметрыРасчета)
	
	ИспользуемыеТаблицы = ОбщегоНазначенияУТ.СписокВременныхТаблиц(ПараметрыРасчета.МенеджерВременныхТаблиц);

	АмортизацияОСЛокализация.ВыполнитьРасчетАмортизации(ПараметрыРасчета);

	ДоначислитьАмортизациюУУЗаПрошлыйМесяц(ПараметрыРасчета);
	РассчитатьАмортизациюУУЗаТекущийМесяц(ПараметрыРасчета);
	ОбъединитьРезультатыРасчета(ПараметрыРасчета);

	ПолучитьКоличествоОбработанныхДанных(ПараметрыРасчета);
	
	ОбщегоНазначенияУТ.ДобавитьИдентификаторыВоВременнуюТаблицу("НачисленнаяАмортизация", ПараметрыРасчета.МенеджерВременныхТаблиц);
	
	Для Каждого ИмяТаблицы Из СтрРазделить(ПараметрыРасчета.ИспользуемыеТаблицы, ",") Цикл
		ИспользуемыеТаблицы.Добавить(ИмяТаблицы);
	КонецЦикла;

	ОбщегоНазначенияУТ.УничтожитьВременныеТаблицы(ПараметрыРасчета.МенеджерВременныхТаблиц,, ИспользуемыеТаблицы);

КонецПроцедуры

Процедура ДоначислитьАмортизациюУУЗаПрошлыйМесяц(ПараметрыРасчета)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
	ВнеоборотныеАктивы.УстановитьПараметрыЗапросаДляРасчетаАмортизации(Запрос, ПараметрыРасчета);

	// Расчет за прошлый месяц до изменения параметров.
	// И получение данных для расчета с учетом изменения параметров.
	СписокЗапросов = Новый Массив;
	
	ТекстЗапросаДоначислениеАмортизацииБезИзмененийУУ(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаДоначислениеАмортизацииВыборкаИзмененийУУ(СписокЗапросов, ПараметрыРасчета);
	
	Запрос.Текст = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	РезультатЗапроса = Запрос.Выполнить();
	
	АмортизацияИзменений = РассчитатьАмортизациюПриИзмененииПараметров(РезультатЗапроса, Неопределено);
	
	// Объединение всех расчетов
	СписокЗапросов = Новый Массив;
	
	ТекстЗапросаДоначислениеАмортизацииУУ(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаДоначислениеАмортизацииУУ_Сгруппированная(СписокЗапросов, ПараметрыРасчета);
	
	Запрос.Текст = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	Запрос.УстановитьПараметр("АмортизацияИзменений_УУ", АмортизацияИзменений);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
КонецПроцедуры

Процедура РассчитатьАмортизациюУУЗаТекущийМесяц(ПараметрыРасчета)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
	ВнеоборотныеАктивы.УстановитьПараметрыЗапросаДляРасчетаАмортизации(Запрос, ПараметрыРасчета);

	// Расчет за текущий месяц до изменения параметров
	// И получение данных для расчета с учетом изменения параметров
	СписокЗапросов = Новый Массив;
	
	ТекстЗапросаРасчетАмортизацииБезИзмененийУУ(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаРасчетАмортизацииВыборкаИзмененийУУ(СписокЗапросов, ПараметрыРасчета);
	
	Запрос.Текст = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	АмортизацияИзменений = РассчитатьАмортизациюПриИзмененииПараметров(
								РезультатЗапроса[РезультатЗапроса.ВГраница()], 
								РезультатЗапроса[РезультатЗапроса.ВГраница()-1].Выгрузить());
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СписокОС.Период КАК Период,
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.Подразделение КАК Подразделение,
	|	СписокОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	СписокОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОС.СтатьяРасходов КАК СтатьяРасходов,
	|	СписокОС.АналитикаРасходов КАК АналитикаРасходов,
	|	СписокОС.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	СписокОС.СуммаУУ КАК СуммаУУ,
	|	СписокОС.СуммаБУ КАК СуммаБУ,
	|	СписокОС.ОбесценениеУУ КАК ОбесценениеУУ,
	|	СписокОС.ОбесценениеБУ КАК ОбесценениеБУ,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокОС.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	СписокОС.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|	СписокОС.РесурсНазначенный КАК РесурсНазначенный,
	|	СписокОС.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	СписокОС.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|	СписокОС.ОстатокОбесцененияРегл КАК ОстатокОбесцененияРегл,
	|	СписокОС.ОстатокОбесцененияУпр КАК ОстатокОбесцененияУпр
	|ПОМЕСТИТЬ РасчетАмортизацииСУчетомИзмененийУУ
	|ИЗ
	|	&АмортизацияИзменений_УУ КАК СписокОС
	|ГДЕ
	|	СписокОС.ЭтоИзменения";

	Запрос.УстановитьПараметр("АмортизацияИзменений_УУ", АмортизацияИзменений);
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ОбъединитьРезультатыРасчета(ПараметрыРасчета)
	
	СписокЗапросов = Новый Массив;
	
	ТекстЗапросаРасчетАмортизации_УУ(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаНачисленнаяАмортизация(СписокЗапросов, ПараметрыРасчета);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
	ВнеоборотныеАктивы.УстановитьПараметрыЗапросаДляРасчетаАмортизации(Запрос, ПараметрыРасчета);
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПолучитьКоличествоОбработанныхДанных(ПараметрыРасчета)
	
	ТекстЗапроса = ТекстЗапросаКоличествоОбработанныхДанных(ПараметрыРасчета);

	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	ПараметрыРасчета.КоличествоОбработанныхДанных = Выборка.КоличествоДанных;
	
КонецПроцедуры

// Выполняет расчет амортизации при изменении параметров на дату их изменения.
// 
// Параметры:
// 	РезультатЗапроса - Неопределено, РезультатЗапроса - Описание
// 	ТаблицаИсточникИПолучатель - ТаблицаЗначений - 
// Возвращаемое значение:
// 	ТаблицаЗначений - 
Функция РассчитатьАмортизациюПриИзмененииПараметров(РезультатЗапроса, ТаблицаИсточникИПолучатель) Экспорт

	АмортизацияИзменений = РезультатЗапроса.Выгрузить();
	
	Если АмортизацияИзменений.Количество() = 0 Тогда
		Возврат АмортизацияИзменений;
	КонецЕсли;
	
	АмортизацияИзменений.Сортировать("ЭтоИзменения,Период,Приоритет");
	
	Если ТаблицаИсточникИПолучатель <> Неопределено Тогда
		ТаблицаИсточникИПолучатель.Индексы.Добавить("ОсновноеСредствоПолучатель,Ссылка");
	КонецЕсли; 
	
	// Хранит параметры расчета, которые использовались при предыдущем расчете.
	ЗначенияПараметровРасчетаИзменений = Новый Соответствие;
	
	Для каждого ДанныеСтроки Из АмортизацияИзменений Цикл
		
		ЗначенияПараметров = ЗначенияПараметровРасчетаИзменений.Получить(ДанныеСтроки.ОсновноеСредство);
		
		Если ЗначенияПараметров = Неопределено Тогда
			
			ЗначенияПараметров = Новый Структура;
			
			// Стоимость и амортизация на дату расчета.
			// Может измениться при переоценке, разукомплектации и т.п.
			ЗначенияПараметров.Вставить("СтоимостьУпр", 0);
			ЗначенияПараметров.Вставить("СтоимостьРегл", 0);
			ЗначенияПараметров.Вставить("АмортизацияУпр", 0);
			ЗначенияПараметров.Вставить("АмортизацияРегл", 0);
			
			// Накопленная амортизация c начала месяца на дату расчета.
			// Используется для прекращения расчета, если она превышает остаточную стоимость.
			ЗначенияПараметров.Вставить("АмортизацияЗаМесяцУпр", 0);  
			ЗначенияПараметров.Вставить("АмортизацияЗаМесяцРегл", 0);
			
			// Накопленное обесценение c начала месяца на дату расчета.
			// Используется для прекращения расчета, если она превышает остаточную сумму обесценения.
			ЗначенияПараметров.Вставить("ОбесценениеЗаМесяцУпр", 0);  
			ЗначенияПараметров.Вставить("ОбесценениеЗаМесяцРегл", 0);
			
			ЗначенияПараметров.СтоимостьУпр = ДанныеСтроки.Стоимость;
			ЗначенияПараметров.СтоимостьРегл = ДанныеСтроки.СтоимостьРегл;
			ЗначенияПараметров.АмортизацияУпр = ДанныеСтроки.Амортизация;
			ЗначенияПараметров.АмортизацияРегл = ДанныеСтроки.АмортизацияРегл;
			
			ЗначенияПараметровРасчетаИзменений.Вставить(ДанныеСтроки.ОсновноеСредство, ЗначенияПараметров);
			
		КонецЕсли;
		
		УвеличитьСтоимостьИАмортизациюПриРазукомплектацииИОбъединении(
			ДанныеСтроки, 
			ЗначенияПараметровРасчетаИзменений, 
			ТаблицаИсточникИПолучатель);
		
		Если ДанныеСтроки.ЭтоИзменения Тогда

			Если ТипЗнч(ДанныеСтроки.Регистратор) = Тип("ДокументСсылка.ПереоценкаОС2_4") Тогда
				
				// Нужно расчитать остаточную стоимость так как это делает переоценка.
				
				Если ДанныеСтроки.Стоимость <> 0 Тогда
					КоэффициентУпр = ДанныеСтроки.СтоимостьИзменение / ДанныеСтроки.Стоимость;
					ЗначенияПараметров.СтоимостьУпр = ДанныеСтроки.СтоимостьИзменение;
					ЗначенияПараметров.АмортизацияУпр = Окр(ДанныеСтроки.Амортизация * КоэффициентУпр, 2);
					ЗначенияПараметров.АмортизацияЗаМесяцУпр = Окр(ЗначенияПараметров.АмортизацияЗаМесяцУпр * КоэффициентУпр, 2);
				КонецЕсли; 

				Если ДанныеСтроки.СтоимостьРегл <> 0 Тогда
					КоэффициентРегл = ДанныеСтроки.СтоимостьРеглИзменение / ДанныеСтроки.СтоимостьРегл;
					ЗначенияПараметров.СтоимостьРегл = ДанныеСтроки.СтоимостьРеглИзменение;
					ЗначенияПараметров.АмортизацияРегл = Окр(ДанныеСтроки.АмортизацияРегл * КоэффициентРегл, 2);
					ЗначенияПараметров.АмортизацияЗаМесяцРегл = Окр(ЗначенияПараметров.АмортизацияЗаМесяцРегл * КоэффициентРегл, 2);
				КонецЕсли; 
				
			ИначеЕсли ТипЗнч(ДанныеСтроки.Регистратор) = Тип("ДокументСсылка.СписаниеОС2_4")
				ИЛИ ТипЗнч(ДанныеСтроки.Регистратор) = Тип("ДокументСсылка.РазукомплектацияОС")
					И НЕ ДанныеСтроки.ПолученПриРазукомплектацииОбъединении Тогда
				
				Если ДанныеСтроки.НачислятьАмортизацию Тогда
					
					// Частичная разукомплектация или частичное списание.
					
					// Уменьшение стоимости на сумму списания.
					ЗначенияПараметров.СтоимостьУпр  = ЗначенияПараметров.СтоимостьУпр  + ДанныеСтроки.СтоимостьИзменение;
					ЗначенияПараметров.СтоимостьРегл = ЗначенияПараметров.СтоимостьРегл + ДанныеСтроки.СтоимостьРеглИзменение;
					
					// Уменьшение амортизации пропорционально списанной стоимости.
					Если ДанныеСтроки.Стоимость <> 0 Тогда
						
						ДоляСписания = ?(ДанныеСтроки.Стоимость <> 0, Окр(-ДанныеСтроки.СтоимостьИзменение / ДанныеСтроки.Стоимость, 16), 1);
						
						ЗначенияПараметров.АмортизацияУпр = 
							ЗначенияПараметров.АмортизацияУпр 
							- ЗначенияПараметров.АмортизацияУпр * ДоляСписания;
							
						ЗначенияПараметров.АмортизацияЗаМесяцУпр = 
							ЗначенияПараметров.АмортизацияЗаМесяцУпр 
							- ЗначенияПараметров.АмортизацияЗаМесяцУпр * ДоляСписания;
							
					КонецЕсли; 
						
					Если ДанныеСтроки.СтоимостьРегл <> 0 Тогда
						
						ДоляСписания = ?(ДанныеСтроки.СтоимостьРегл <> 0, Окр(-ДанныеСтроки.СтоимостьРеглИзменение / ДанныеСтроки.СтоимостьРегл, 16), 1);
						
						ЗначенияПараметров.АмортизацияРегл = 
							ЗначенияПараметров.АмортизацияРегл 
							- ДанныеСтроки.АмортизацияРегл * ДоляСписания;
						
						ЗначенияПараметров.АмортизацияЗаМесяцРегл = 
							ЗначенияПараметров.АмортизацияЗаМесяцРегл 
							- ЗначенияПараметров.АмортизацияЗаМесяцРегл * ДоляСписания;
							
					КонецЕсли; 
					
				Иначе	
					
					// Полная разукомплектация или полное списание.
					ЗначенияПараметров.СтоимостьУпр = 0;
					ЗначенияПараметров.СтоимостьРегл = 0;
					ЗначенияПараметров.АмортизацияУпр = 0;
					ЗначенияПараметров.АмортизацияРегл = 0;
					
				КонецЕсли; 
					
			ИначеЕсли ТипЗнч(ДанныеСтроки.Регистратор) = Тип("ДокументСсылка.КорректировкаСтоимостиИАмортизацииОС") Тогда
				
				ЗначенияПараметров.СтоимостьУпр = ДанныеСтроки.СтоимостьИзменение;
				ЗначенияПараметров.СтоимостьРегл = ДанныеСтроки.СтоимостьРеглИзменение;
				ЗначенияПараметров.АмортизацияУпр = ДанныеСтроки.АмортизацияИзменение;
				ЗначенияПараметров.АмортизацияРегл = ДанныеСтроки.АмортизацияРеглИзменение;
				
				ЗначенияПараметров.АмортизацияЗаМесяцУпр = 0;
				ЗначенияПараметров.АмортизацияЗаМесяцРегл = 0;
				
			Иначе
				
				ЗначенияПараметров.СтоимостьУпр  = ЗначенияПараметров.СтоимостьУпр  + ДанныеСтроки.СтоимостьИзменение;
				ЗначенияПараметров.СтоимостьРегл = ЗначенияПараметров.СтоимостьРегл + ДанныеСтроки.СтоимостьРеглИзменение;
				
			КонецЕсли; 
			
			// Стоимость, которая используется для расчета амортизации.
			ДанныеСтроки.СтоимостьДляВычисленияАмортизации = ЗначенияПараметров.СтоимостьУпр - ЗначенияПараметров.АмортизацияУпр;
			ДанныеСтроки.СтоимостьДляВычисленияАмортизацииРегл = ЗначенияПараметров.СтоимостьРегл - ЗначенияПараметров.АмортизацияРегл;
			
			// Расчет суммы амортизации по формуле.
			ДанныеСтроки.СуммаУУ = РассчитатьСуммуАмортизацииУУ(
										ДанныеСтроки, 
										ДанныеСтроки.СтоимостьДляВычисленияАмортизации, 
										ДанныеСтроки.ЛиквидационнаяСтоимость, 
										ДанныеСтроки.ОстатокОбесцененияУпр, 
										ЗначенияПараметров.АмортизацияЗаМесяцУпр);
			
			ДанныеСтроки.СуммаБУ = РассчитатьСуммуАмортизацииУУ(
										ДанныеСтроки, 
										ДанныеСтроки.СтоимостьДляВычисленияАмортизацииРегл, 
										ДанныеСтроки.ЛиквидационнаяСтоимостьРегл, 
										ДанныеСтроки.ОстатокОбесцененияРегл, 
										ЗначенияПараметров.АмортизацияЗаМесяцРегл);
										
			// Расчет суммы обесценения по формуле.
			ДанныеСтроки.ОбесценениеУУ = РассчитатьСуммуОбесценения(
										ДанныеСтроки, 
										ДанныеСтроки.ОстатокОбесцененияУпр,
										ЗначенияПараметров.ОбесценениеЗаМесяцУпр);
			
			ДанныеСтроки.ОбесценениеБУ = РассчитатьСуммуОбесценения(
										ДанныеСтроки, 
										ДанныеСтроки.ОстатокОбесцененияРегл,
										ЗначенияПараметров.ОбесценениеЗаМесяцРегл);
										
		КонецЕсли;
		
		// Накопленная амортизация за месяц.
		ЗначенияПараметров.АмортизацияЗаМесяцУпр  = ЗначенияПараметров.АмортизацияЗаМесяцУпр  + ДанныеСтроки.СуммаУУ;
		ЗначенияПараметров.АмортизацияЗаМесяцРегл = ЗначенияПараметров.АмортизацияЗаМесяцРегл + ДанныеСтроки.СуммаБУ;
		
		// Накопленное обесценение за месяц.
		ЗначенияПараметров.ОбесценениеЗаМесяцУпр  = ЗначенияПараметров.ОбесценениеЗаМесяцУпр  + ДанныеСтроки.ОбесценениеУУ;
		ЗначенияПараметров.ОбесценениеЗаМесяцРегл = ЗначенияПараметров.ОбесценениеЗаМесяцРегл + ДанныеСтроки.ОбесценениеБУ;
		
	КонецЦикла; 
	
	Возврат АмортизацияИзменений;

КонецФункции

Процедура УвеличитьСтоимостьИАмортизациюПриРазукомплектацииИОбъединении(ДанныеСтроки, ЗначенияПараметровРасчетаИзменений, ТаблицаИсточникИПолучатель)

	Если ТаблицаИсточникИПолучатель = Неопределено
		ИЛИ НЕ ДанныеСтроки.ПолученПриРазукомплектацииОбъединении Тогда
		Возврат;
	КонецЕсли; 
	
	СтруктураПоиска = Новый Структура("ОсновноеСредствоПолучатель,Ссылка", ДанныеСтроки.ОсновноеСредство, ДанныеСтроки.Регистратор);
	ИсходныеОС = ТаблицаИсточникИПолучатель.НайтиСтроки(СтруктураПоиска);
	
	Если ИсходныеОС.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаИсходноеОС Из ИсходныеОС Цикл
	
		ЗначенияПараметров = ЗначенияПараметровРасчетаИзменений.Получить(СтрокаИсходноеОС.ОсновноеСредствоИсточник);
		Если ЗначенияПараметров = Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		
		Если СтрокаИсходноеОС.СтоимостьСписанияУпр <> 0 Тогда
			
			УвеличениеСтоимостиУпр  = СтрокаИсходноеОС.СтоимостьСписанияУпр;
			УвеличениеСтоимостиРегл = СтрокаИсходноеОС.СтоимостьСписанияРегл;
			
			Если ЗначенияПараметров.СтоимостьУпр <> 0 Тогда
				УвеличениеАмортизацииУпр = 
					(ЗначенияПараметров.АмортизацияУпр + ЗначенияПараметров.АмортизацияЗаМесяцУпр)
						* СтрокаИсходноеОС.СтоимостьСписанияУпр 
						/ ЗначенияПараметров.СтоимостьУпр;
					
				УвеличениеАмортизацииУпр = Окр(УвеличениеАмортизацииУпр, 2);
			Иначе
				УвеличениеАмортизацииУпр = 0;
			КонецЕсли;
			
			Если ЗначенияПараметров.СтоимостьРегл <> 0 Тогда
				УвеличениеАмортизацииРегл = 
					(ЗначенияПараметров.АмортизацияРегл + ЗначенияПараметров.АмортизацияЗаМесяцРегл)
						* СтрокаИсходноеОС.СтоимостьСписанияРегл 
						/ ЗначенияПараметров.СтоимостьРегл;
			Иначе
				УвеличениеАмортизацииРегл = 0;
			КонецЕсли;
		
		Иначе
			
			УвеличениеСтоимостиУпр = ЗначенияПараметров.СтоимостьУпр;
			УвеличениеСтоимостиРегл = ЗначенияПараметров.СтоимостьРегл;
			УвеличениеАмортизацииУпр = ЗначенияПараметров.АмортизацияУпр + ЗначенияПараметров.АмортизацияЗаМесяцУпр;
			УвеличениеАмортизацииРегл = ЗначенияПараметров.АмортизацияРегл + ЗначенияПараметров.АмортизацияЗаМесяцРегл;
			
		КонецЕсли;
		
		ДанныеСтроки.Стоимость = ДанныеСтроки.Стоимость + УвеличениеСтоимостиУпр;
		ДанныеСтроки.СтоимостьРегл = ДанныеСтроки.СтоимостьРегл + УвеличениеСтоимостиРегл;
		ДанныеСтроки.Амортизация = ДанныеСтроки.Амортизация + УвеличениеАмортизацииУпр;
		ДанныеСтроки.АмортизацияРегл = ДанныеСтроки.АмортизацияРегл + УвеличениеАмортизацииРегл;
			
	КонецЦикла; 
	
	ЗначенияПараметров = ЗначенияПараметровРасчетаИзменений.Получить(ДанныеСтроки.ОсновноеСредство);
	
	ЗначенияПараметров.СтоимостьУпр = ДанныеСтроки.Стоимость;
	ЗначенияПараметров.СтоимостьРегл = ДанныеСтроки.СтоимостьРегл;
	ЗначенияПараметров.АмортизацияУпр = ДанныеСтроки.Амортизация;
	ЗначенияПараметров.АмортизацияРегл = ДанныеСтроки.АмортизацияРегл;
	
КонецПроцедуры
 
Функция РассчитатьСуммуАмортизацииУУ(ДанныеСтроки, ОстаточнаяСтоимость, ЛиквидационнаяСтоимость, Обесценение, НачисленнаяАмортизацияЗаМесяц)

	СуммаУУ = 0;
	
	ОстаточнаяСтоимостьПослеНачисленияАмортизации =
		(ОстаточнаяСтоимость
			- НачисленнаяАмортизацияЗаМесяц 
			- ЛиквидационнаяСтоимость);
				
	Если НЕ ДанныеСтроки.НачислятьАмортизацию 
		ИЛИ ОстаточнаяСтоимостьПослеНачисленияАмортизации <= 0
		ИЛИ ДанныеСтроки.РесурсНазначенный <= 0 Тогда
		
		СуммаУУ = 0;
		
	ИначеЕсли (ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод + ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод) >= ДанныеСтроки.РесурсНазначенный Тогда
		
		СуммаУУ = ОстаточнаяСтоимость - ЛиквидационнаяСтоимость;
		
	ИначеЕсли ДанныеСтроки.МетодНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииОС.Линейный
		ИЛИ ДанныеСтроки.МетодНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции
		ИЛИ ДанныеСтроки.МетодНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка
			И (ДанныеСтроки.РесурсНазначенный
				- ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод
				- ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод) <= 12 Тогда
		
		СуммаУУ = (ОстаточнаяСтоимость - ЛиквидационнаяСтоимость - Обесценение)
					* (ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод)
					/ (ДанныеСтроки.РесурсНазначенный - ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод);
		
	ИначеЕсли ДанныеСтроки.МетодНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка Тогда
		
		СуммаУУ = ОстаточнаяСтоимость
					* ДанныеСтроки.КоэффициентУскорения
					* ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод
					/ (ДанныеСтроки.РесурсНазначенный - ДанныеСтроки.КоэффициентУскорения 
							*(ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод - ДанныеСтроки.ОтработанныхМесяцевПолныхЛет));
	КонецЕсли; 
	
	Если СуммаУУ > ОстаточнаяСтоимостьПослеНачисленияАмортизации
		ИЛИ (ОстаточнаяСтоимостьПослеНачисленияАмортизации - СуммаУУ) < ВнеоборотныеАктивыСлужебный.МинимальнаяОстаточнаяСтоимость() Тогда
		СуммаУУ = Макс(ОстаточнаяСтоимостьПослеНачисленияАмортизации, 0);
	КонецЕсли;

	Возврат Окр(СуммаУУ, 2);
	
КонецФункции

Функция РассчитатьСуммуОбесценения(ДанныеСтроки, ОстатокОбесценение, НачисленноеОбесценениеЗаМесяц) Экспорт

	СуммаОбесценения = 0;
	
	ОстатокОбесценениеПослеНачисления = ОстатокОбесценение - НачисленноеОбесценениеЗаМесяц;
				
	Если НЕ ДанныеСтроки.НачислятьАмортизацию 
		ИЛИ ОстатокОбесценениеПослеНачисления <= 0
		ИЛИ ДанныеСтроки.РесурсНазначенный <= 0 Тогда
		
		СуммаОбесценения = 0;
		
	ИначеЕсли (ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод + ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод) >= ДанныеСтроки.РесурсНазначенный Тогда
		
		СуммаОбесценения = ОстатокОбесценение;
		
	ИначеЕсли ДанныеСтроки.МетодНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииОС.Линейный
		ИЛИ ДанныеСтроки.МетодНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции Тогда
		
		СуммаОбесценения = ОстатокОбесценение
					* (ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод)
					/ (ДанныеСтроки.РесурсНазначенный - ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод);
		
	КонецЕсли; 
	
	Если СуммаОбесценения > ОстатокОбесценениеПослеНачисления
		ИЛИ (ОстатокОбесценениеПослеНачисления - СуммаОбесценения) < ВнеоборотныеАктивыСлужебный.МинимальнаяОстаточнаяСтоимость() Тогда
		СуммаОбесценения = Макс(ОстатокОбесценениеПослеНачисления, 0);
	КонецЕсли;

	Возврат Окр(СуммаОбесценения, 2);
	
КонецФункции
 
Процедура ЗарегистрироватьОшибкиРасчетаАмортизации(ПараметрыРасчета)
		
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПроверкаПараметровРасчета.ОбъектУчета КАК ОбъектУчета,
	|	ПроверкаПараметровРасчета.Организация КАК Организация,
	|	ВЫРАЗИТЬ(ПроверкаПараметровРасчета.ОбъектУчета КАК Справочник.ОбъектыЭксплуатации).Наименование КАК ОбъектУчетаПредставление,
	|	ВЫРАЗИТЬ(ПроверкаПараметровРасчета.ОбъектУчета КАК Справочник.ОбъектыЭксплуатации).ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	МАКСИМУМ(ПроверкаПараметровРасчета.НеУказанСрокИспользованияУУ) КАК НеУказанСрокИспользованияУУ,
	|	МАКСИМУМ(ПроверкаПараметровРасчета.НеУказанРесурсВыработанный) КАК НеУказанРесурсВыработанный,
	|	МАКСИМУМ(ПроверкаПараметровРасчета.ОбъемНаработкиВыработан) КАК ОбъемНаработкиВыработан,
	|	МАКСИМУМ(ПроверкаПараметровРасчета.НеУказанОбъемНаработки) КАК НеУказанОбъемНаработки,
	|	МАКСИМУМ(ПроверкаПараметровРасчета.НевернаяДатаРазукомплектации) КАК НевернаяДатаРазукомплектации,
	|	МАКСИМУМ(ПроверкаПараметровРасчета.РазукомплектацияИПредварительнаяСтоимость) КАК РазукомплектацияИПредварительнаяСтоимость,
	|	МАКСИМУМ(ПроверкаПараметровРасчета.ОбъединениеИПредварительнаяСтоимость) КАК ОбъединениеИПредварительнаяСтоимость
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПроверкаПараметровРасчета.Организация КАК Организация,
	|		ПроверкаПараметровРасчета.ОбъектУчета КАК ОбъектУчета,
	|		ПроверкаПараметровРасчета.НеУказанСрокИспользованияУУ КАК НеУказанСрокИспользованияУУ,
	|		ЛОЖЬ КАК НеУказанРесурсВыработанный,
	|		ПроверкаПараметровРасчета.НеУказанОбъемНаработки КАК НеУказанОбъемНаработки,
	|		ПроверкаПараметровРасчета.ОбъемНаработкиВыработан КАК ОбъемНаработкиВыработан,
	|		ЛОЖЬ КАК НевернаяДатаРазукомплектации,
	|		ЛОЖЬ КАК РазукомплектацияИПредварительнаяСтоимость,
	|		ЛОЖЬ КАК ОбъединениеИПредварительнаяСтоимость
	|	ИЗ
	|		ПроверкаПараметровРасчета_УУ КАК ПроверкаПараметровРасчета
	|	ГДЕ
	|		ПроверкаПараметровРасчета.Организация В(&СписокОрганизаций)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПроверкаПараметровРасчета.Организация КАК Организация,
	|		ПроверкаПараметровРасчета.ОбъектУчета КАК ОбъектУчета,
	|		ПроверкаПараметровРасчета.НеУказанСрокИспользованияУУ КАК НеУказанСрокИспользованияУУ,
	|		ПроверкаПараметровРасчета.НеУказанРесурсВыработанный КАК НеУказанРесурсВыработанный,
	|		ПроверкаПараметровРасчета.НеУказанОбъемНаработки КАК НеУказанОбъемНаработки,
	|		ЛОЖЬ КАК ОбъемНаработкиВыработан,
	|		ЛОЖЬ КАК НевернаяДатаРазукомплектации,
	|		ЛОЖЬ КАК РазукомплектацияИПредварительнаяСтоимость,
	|		ЛОЖЬ КАК ОбъединениеИПредварительнаяСтоимость
	|	ИЗ
	|		ПроверкаПараметровРасчетаИзменения_УУ КАК ПроверкаПараметровРасчета
	|	ГДЕ
	|		ПроверкаПараметровРасчета.Организация В(&СписокОрганизаций)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РазукомплектацияОС.Ссылка.Организация КАК Организация,
	|		РазукомплектацияОС.ОсновноеСредство КАК ОбъектУчета,
	|		ЛОЖЬ КАК НеУказанСрокИспользованияУУ,
	|		ЛОЖЬ КАК НеУказанРесурсВыработанный,
	|		ЛОЖЬ КАК НеУказанОбъемНаработки,
	|		ЛОЖЬ КАК ОбъемНаработкиВыработан,
	|		ИСТИНА КАК НевернаяДатаРазукомплектации,
	|		ЛОЖЬ КАК РазукомплектацияИПредварительнаяСтоимость,
	|		ЛОЖЬ КАК ОбъединениеИПредварительнаяСтоимость
	|	ИЗ
	|		Документ.РазукомплектацияОС.ОС КАК РазукомплектацияОС
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОбъединениеОС.ОС КАК ОбъединениеОС
	|			ПО (ОбъединениеОС.ОсновноеСредство = РазукомплектацияОС.ОсновноеСредство)
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&КонецМесяца) КАК УчетнаяПолитика
	|			ПО УчетнаяПолитика.Организация = РазукомплектацияОС.Ссылка.Организация.ГоловнаяОрганизация
	|	ГДЕ
	|		ЕСТЬNULL(УчетнаяПолитика.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)
	|		И РазукомплектацияОС.Ссылка.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
	|		И ОбъединениеОС.Ссылка.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
	|		И РазукомплектацияОС.Ссылка.Дата >= ОбъединениеОС.Ссылка.Дата
	|		И ОбъединениеОС.Ссылка.Проведен
	|		И ОбъединениеОС.Ссылка.ОтражатьВУпрУчете
	|		И РазукомплектацияОС.Ссылка.Проведен
	|		И РазукомплектацияОС.Ссылка.ОтражатьВУпрУчете
	|		И РазукомплектацияОС.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				СписокОС.ОбъектУчета
	|			ИЗ
	|				ПакетыАмортизации КАК СписокОС)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РазукомплектацияОС.Организация КАК Организация,
	|		РазукомплектацияОС.ОсновноеСредство КАК ОбъектУчета,
	|		ЛОЖЬ КАК НеУказанСрокИспользованияУУ,
	|		ЛОЖЬ КАК НеУказанРесурсВыработанный,
	|		ЛОЖЬ КАК НеУказанОбъемНаработки,
	|		ЛОЖЬ КАК ОбъемНаработкиВыработан,
	|		ЛОЖЬ КАК НевернаяДатаРазукомплектации,
	|		ИСТИНА КАК РазукомплектацияИПредварительнаяСтоимость,
	|		ЛОЖЬ КАК ОбъединениеИПредварительнаяСтоимость
	|	ИЗ
	|		Документ.РазукомплектацияОС КАК РазукомплектацияОС
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС2_4.ОС КАК ПринятиеКУчетуОС2_4
	|			ПО (ПринятиеКУчетуОС2_4.ОсновноеСредство = РазукомплектацияОС.ОсновноеСредство)
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.МодернизацияОС2_4.ОС КАК МодернизацияОС2_4
	|			ПО (МодернизацияОС2_4.ОсновноеСредство = РазукомплектацияОС.ОсновноеСредство)
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&КонецМесяца) КАК УчетнаяПолитика
	|			ПО УчетнаяПолитика.Организация = РазукомплектацияОС.Организация.ГоловнаяОрганизация
	|	ГДЕ
	|		ЕСТЬNULL(УчетнаяПолитика.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)
	|		И РазукомплектацияОС.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
	|		И (ПринятиеКУчетуОС2_4.Ссылка.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
	|				И ПринятиеКУчетуОС2_4.Ссылка.Проведен
	|				И ПринятиеКУчетуОС2_4.Ссылка.ОтражатьВУпрУчете
	|			ИЛИ МодернизацияОС2_4.Ссылка.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
	|				И МодернизацияОС2_4.Ссылка.Проведен
	|				И МодернизацияОС2_4.Ссылка.ОтражатьВУпрУчете)
	|		И РазукомплектацияОС.Проведен
	|		И РазукомплектацияОС.ОтражатьВУпрУчете
	|		И РазукомплектацияОС.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				СписокОС.ОбъектУчета
	|			ИЗ
	|				ПакетыАмортизации КАК СписокОС)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОбъединениеОС.Ссылка.Организация КАК Организация,
	|		ОбъединениеОС.ОсновноеСредство КАК ОбъектУчета,
	|		ЛОЖЬ КАК НеУказанСрокИспользованияУУ,
	|		ЛОЖЬ КАК НеУказанРесурсВыработанный,
	|		ЛОЖЬ КАК НеУказанОбъемНаработки,
	|		ЛОЖЬ КАК ОбъемНаработкиВыработан,
	|		ЛОЖЬ КАК НевернаяДатаРазукомплектации,
	|		ЛОЖЬ КАК РазукомплектацияИПредварительнаяСтоимость,
	|		ИСТИНА КАК ОбъединениеИПредварительнаяСтоимость
	|	ИЗ
	|		Документ.ОбъединениеОС.ОС КАК ОбъединениеОС
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС2_4.ОС КАК ПринятиеКУчетуОС2_4
	|			ПО (ПринятиеКУчетуОС2_4.ОсновноеСредство = ОбъединениеОС.ОсновноеСредство)
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.МодернизацияОС2_4.ОС КАК МодернизацияОС2_4
	|			ПО (МодернизацияОС2_4.ОсновноеСредство = ОбъединениеОС.ОсновноеСредство)
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&КонецМесяца) КАК УчетнаяПолитика
	|			ПО УчетнаяПолитика.Организация = ОбъединениеОС.Ссылка.Организация.ГоловнаяОрганизация
	|	ГДЕ
	|		ЕСТЬNULL(УчетнаяПолитика.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)
	|		И ОбъединениеОС.Ссылка.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
	|		И (ПринятиеКУчетуОС2_4.Ссылка.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
	|				И ПринятиеКУчетуОС2_4.Ссылка.Проведен
	|				И ПринятиеКУчетуОС2_4.Ссылка.ОтражатьВУпрУчете
	|			ИЛИ МодернизацияОС2_4.Ссылка.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
	|				И МодернизацияОС2_4.Ссылка.Проведен
	|				И МодернизацияОС2_4.Ссылка.ОтражатьВУпрУчете)
	|		И ОбъединениеОС.Ссылка.Проведен
	|		И ОбъединениеОС.Ссылка.ОтражатьВУпрУчете
	|		И ОбъединениеОС.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				СписокОС.ОбъектУчета
	|			ИЗ
	|				ПакетыАмортизации КАК СписокОС)
	|
	|) КАК ПроверкаПараметровРасчета
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверкаПараметровРасчета.ОбъектУчета,
	|	ПроверкаПараметровРасчета.Организация";
		
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("СписокОрганизаций", ПараметрыРасчета.СписокОрганизаций);
	ВнеоборотныеАктивы.УстановитьПараметрыЗапросаДляРасчетаАмортизации(Запрос, ПараметрыРасчета);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ШаблонОбъемНаработки = НСтр("ru = 'Для основного средства ""%1"" (инв. №%2) в управленческом учете не определен предполагаемый объем наработки (продукции, работ).';
								|en = 'Estimated asset activity value (of products, works) is not defined for the fixed asset ""%1"" (inv. No.%2) in management accounting.'");
	
	ШаблонОбъемНаработкиВыработан = НСтр("ru = 'Для основного средства ""%1"" (инв. №%2) предполагаемый объем наработки полностью выработан.
												|Если была произведена модернизация после полной выработки ресурса, то необходимо оформить документ ""Изменение параметров ОС"" и увеличить предполагаемый объем наработки или приостановить начисление амортизации в управленческом учете.';
												|en = 'Estimated asset activity is fully generated for the fixed asset ""%1"" (inv. No.%2).
												|If a modernization has been performed after the resource has been fully generated, then it is necessary to register a document ""Adjust fixed asset financial details"" and increase the estimated running time or suspend depreciation charge in management accounting.'");

	ШаблонСрокИспользованияУУ = НСтр("ru = 'Для основного средства ""%1"" (инв. №%2) в управленческом учете не определен срок использования, необходимый для вычисления амортизации.
									 |Если была произведена модернизация после окончания срока использования, то следует оформить документ ""Изменение параметров ОС"" и указать новый срок (первоначальный + срок, на который произошло увеличение после модернизации) или приостановить начисление амортизации.';
									 |en = 'Useful life necessary to depreciation calculation for the fixed asset ""%1"" (inv. No.%2) is not defined in management accounting.
									 |If a modernization has been performed after the end of the useful life, then you should register a document ""Adjust fixed asset financial details"" and indicate a new due date (initial + due date for which the increase occurred after the renovation) or suspend depreciation charge.'");
	
	ШаблонРесурсВыработанный = НСтр("ru = 'Для основного средства ""%1"" (инв. №%2) не удалось определить выработанный ресурс в управленческом учете.';
									|en = 'Cannot define the generated resource in management accounting for the fixed asset ""%1"" (inv. No. %2).'");
	
	ШаблонНевернаяДатаРазукомплектации = НСтр("ru = 'Для основного средства ""%1"" (инв. №%2) дата и время разукомплектации должны быть раньше, чем дата и время объединения.';
												|en = 'For the fixed asset ""%1"" (Inv. No.%2), the date and time of splitting must be before the date and time of the combining.'");
	
	ШаблонОбъединениеИПредварительнаяСтоимость = НСтр("ru = 'Не поддерживается объединение основного средства ""%1"" (инв. №%2) в упр. учете, т.к. в этом же месяце оформлено принятие к учету или модернизация.
                                                       |Для обхода ограничения можно оформить объединение в упр. учете в следующем месяце.';
                                                       |en = 'Merge of the fixed asset ""%1"" (Inv. No.%2) in manag. accounting is not supported, because it was recognized or modernized in the same month.
                                                       |To circumvent the restriction, you can register the merge in manag. accounting in the next month.'");
	
	ШаблонРазукомплектацияИПредварительнаяСтоимость = НСтр("ru = 'Не поддерживается разукомплектация основного средства ""%1"" (инв. №%2) в упр. учете, т.к. в этом же месяце оформлено принятие к учету или модернизация.
                                                       |Для обхода ограничения можно оформить разукомплектацию в упр. учете в следующем месяце.';
                                                       |en = 'Split of the fixed asset ""%1"" (Inv. No.%2) in manag. accounting is not supported, because it was recognized or modernized in the same month.
                                                       |To circumvent the restriction, you can register the splitting in manag. accounting in the next month.'");
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.НеУказанОбъемНаработки Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонОбъемНаработки, Выборка.ОбъектУчетаПредставление, СокрЛП(Выборка.ИнвентарныйНомер));
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРасчета.ЭтапРасчета, 
				ПараметрыРасчета.Период,
				Выборка.Организация,
				ТекстОшибки,,
				Выборка.ОбъектУчета,
				ПараметрыРасчета.ЕстьОшибки);
				
		КонецЕсли; 
		
		Если Выборка.ОбъемНаработкиВыработан Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонОбъемНаработкиВыработан, Выборка.ОбъектУчетаПредставление, СокрЛП(Выборка.ИнвентарныйНомер));
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРасчета.ЭтапРасчета, 
				ПараметрыРасчета.Период,
				Выборка.Организация,
				ТекстОшибки,,
				Выборка.ОбъектУчета,
				ПараметрыРасчета.ЕстьОшибки);
				
		КонецЕсли; 
		
		Если Выборка.НеУказанРесурсВыработанный Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонРесурсВыработанный, Выборка.ОбъектУчетаПредставление, СокрЛП(Выборка.ИнвентарныйНомер));
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРасчета.ЭтапРасчета, 
				ПараметрыРасчета.Период,
				Выборка.Организация,
				ТекстОшибки,,
				Выборка.ОбъектУчета,
				ПараметрыРасчета.ЕстьОшибки);
				
		КонецЕсли; 
		
		Если Выборка.НеУказанСрокИспользованияУУ Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонСрокИспользованияУУ, Выборка.ОбъектУчетаПредставление, СокрЛП(Выборка.ИнвентарныйНомер));
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРасчета.ЭтапРасчета, 
				ПараметрыРасчета.Период,
				Выборка.Организация,
				ТекстОшибки,,
				Выборка.ОбъектУчета,
				ПараметрыРасчета.ЕстьОшибки);
				
		КонецЕсли; 
	
		Если Выборка.НевернаяДатаРазукомплектации Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонНевернаяДатаРазукомплектации, Выборка.ОбъектУчетаПредставление, СокрЛП(Выборка.ИнвентарныйНомер));
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРасчета.ЭтапРасчета, 
				ПараметрыРасчета.Период,
				Выборка.Организация,
				ТекстОшибки,,
				Выборка.ОбъектУчета,
				ПараметрыРасчета.ЕстьОшибки);
				
		КонецЕсли; 
		
		Если Выборка.ОбъединениеИПредварительнаяСтоимость Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонОбъединениеИПредварительнаяСтоимость, Выборка.ОбъектУчетаПредставление, СокрЛП(Выборка.ИнвентарныйНомер));
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРасчета.ЭтапРасчета, 
				ПараметрыРасчета.Период,
				Выборка.Организация,
				ТекстОшибки,,
				Выборка.ОбъектУчета,
				ПараметрыРасчета.ЕстьОшибки);
				
		КонецЕсли; 
		
		Если Выборка.РазукомплектацияИПредварительнаяСтоимость Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонРазукомплектацияИПредварительнаяСтоимость, Выборка.ОбъектУчетаПредставление, СокрЛП(Выборка.ИнвентарныйНомер));
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРасчета.ЭтапРасчета, 
				ПараметрыРасчета.Период,
				Выборка.Организация,
				ТекстОшибки,,
				Выборка.ОбъектУчета,
				ПараметрыРасчета.ЕстьОшибки);
				
		КонецЕсли; 
		
	КонецЦикла;
	
	АмортизацияОСЛокализация.ЗарегистрироватьОшибкиРасчетаАмортизации(ПараметрыРасчета);
	
КонецПроцедуры

Процедура ЗаписатьРасчетАмортизации(ПараметрыРасчета)

	Если НЕ ПараметрыРасчета.ЗаписатьДанные Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
	
		// Нужно заблокировать документы, чтобы не создать дублей
		Блокировка = Новый БлокировкаДанных;
		
		ЭлементБлокировки = Блокировка.Добавить("Документ.АмортизацияОС2_4");
		ЭлементБлокировки.УстановитьЗначение("Дата", Новый Диапазон(НачалоМесяца(ПараметрыРасчета.Период),КонецМесяца(ПараметрыРасчета.Период)));
		ЭлементБлокировки.ИсточникДанных = ПараметрыРасчета.ПакетыАмортизации;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Организация", "Организация");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("НомерПакета", "НомерПакета");
		
		Блокировка.Заблокировать(); 
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	НомераПакетовСФилиалами.Организация,
		|	НомераПакетовСФилиалами.НомерПакета,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ЕСТЬNULL(РегламентныйДокумент.Проведен, ЛОЖЬ) = ИСТИНА
		|				ТОГДА РегламентныйДокумент.Ссылка
		|			ИНАЧЕ ЗНАЧЕНИЕ(Документ.АмортизацияОС2_4.ПустаяСсылка)
		|		КОНЕЦ) КАК СсылкаПроведен,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ЕСТЬNULL(РегламентныйДокумент.Проведен, НЕОПРЕДЕЛЕНО) = ЛОЖЬ
		|				ТОГДА РегламентныйДокумент.Ссылка
		|			ИНАЧЕ ЗНАЧЕНИЕ(Документ.АмортизацияОС2_4.ПустаяСсылка)
		|		КОНЕЦ) КАК СсылкаНеПроведен,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ЕСТЬNULL(РегламентныйДокумент.ПометкаУдаления, ЛОЖЬ) = ИСТИНА
		|				ТОГДА РегламентныйДокумент.Ссылка
		|			ИНАЧЕ ЗНАЧЕНИЕ(Документ.АмортизацияОС2_4.ПустаяСсылка)
		|		КОНЕЦ) КАК СсылкаУдален
		|ИЗ
		|	НомераПакетовСФилиалами КАК НомераПакетовСФилиалами
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АмортизацияОС2_4 КАК РегламентныйДокумент
		|		ПО (РегламентныйДокумент.Организация = НомераПакетовСФилиалами.Организация)
		|			И (РегламентныйДокумент.НомерПакета = НомераПакетовСФилиалами.НомерПакета)
		|			И (РегламентныйДокумент.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца)
		|ГДЕ
		|	НомераПакетовСФилиалами.Организация В(&СписокОрганизаций)
		|
		|СГРУППИРОВАТЬ ПО
		|	НомераПакетовСФилиалами.Организация,
		|	НомераПакетовСФилиалами.НомерПакета";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(ПараметрыРасчета.Период));
		Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(ПараметрыРасчета.Период));
		Запрос.УстановитьПараметр("СписокОрганизаций", ПараметрыРасчета.СписокОрганизаций);
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.НомерПакета = ПараметрыРасчета.НомерПакета Тогда
				
				// Расчет выполняется из формы документа.
				Если ЗначениеЗаполнено(ПараметрыРасчета.Ссылка) Тогда
					ДокументОбъект = ПараметрыРасчета.Ссылка.ПолучитьОбъект();
				Иначе 
					ДокументОбъект = ВнеоборотныеАктивыСлужебный.ПолучитьРегламентныйДокумент(
						Выборка.СсылкаПроведен, Неопределено, Неопределено, Документы.АмортизацияОС2_4);
				КонецЕсли;
				
				ДокументОбъект.НомерПакета   = Выборка.НомерПакета;
				ДокументОбъект.Организация   = Выборка.Организация;
				ДокументОбъект.Комментарий   = ПараметрыРасчета.Комментарий;
				ДокументОбъект.Ответственный = ПараметрыРасчета.Ответственный;
				
			Иначе	
				
				ДокументОбъект = ВнеоборотныеАктивыСлужебный.ПолучитьРегламентныйДокумент(
					Выборка.СсылкаПроведен, Выборка.СсылкаНеПроведен, Выборка.СсылкаУдален, Документы.АмортизацияОС2_4);
					
				ДокументОбъект.НомерПакета   = Выборка.НомерПакета;
				ДокументОбъект.Организация   = Выборка.Организация;
				ДокументОбъект.Ответственный = Пользователи.ТекущийПользователь();
				
			КонецЕсли; 
				
			ДокументОбъект.Дата = КонецМесяца(ПараметрыРасчета.Период);
			
			ДокументОбъект.ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", ПараметрыРасчета.МенеджерВременныхТаблиц);
			
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			
			ВнеоборотныеАктивыСлужебный.УничтожитьВременныеТаблицыПослеЗаписиРасчетаАмортизации(
				ПараметрыРасчета.МенеджерВременныхТаблиц, ПараметрыРасчета);
			
		КонецЦикла; 
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстОшибки;
	КонецПопытки; 
	
КонецПроцедуры

#Область ТекстыЗапросовДляРасчетаАмортизации

Процедура ТекстЗапросаПакетыАмортизации(СписокЗапросов)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПакетыАмортизации.Организация КАК Организация,
	|	ПакетыАмортизации.НомерПакета КАК НомерПакета
	|ПОМЕСТИТЬ НомераПакетов
	|ИЗ
	|	&ПакетыАмортизации КАК ПакетыАмортизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НомераПакетов.Организация КАК Организация,
	|	НомераПакетов.НомерПакета КАК НомерПакета
	|ПОМЕСТИТЬ НомераПакетовСФилиалами
	|ИЗ
	|	НомераПакетов КАК НомераПакетов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Филиалы.Ссылка,
	|	НомераПакетов.НомерПакета
	|ИЗ
	|	НомераПакетов КАК НомераПакетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Филиалы
	|		ПО (Филиалы.ГоловнаяОрганизация = НомераПакетов.Организация)
	|			И (НЕ Филиалы.ПометкаУдаления)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	НомерПакета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПакетыАмортизации.Организация КАК Организация,
	|	ВЫРАЗИТЬ(ПакетыАмортизации.Организация КАК Справочник.Организации).ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ПакетыАмортизации.ОбъектУчета КАК ОбъектУчета
	|ПОМЕСТИТЬ ПакетыАмортизации
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПакетыАмортизации.Организация КАК Организация,
	|		ПакетыАмортизации.ОсновноеСредство КАК ОбъектУчета
	|	ИЗ
	|		НомераПакетовСФилиалами КАК НомераПакетов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыАмортизацииОС КАК ПакетыАмортизации
	|			ПО (ПакетыАмортизации.Организация = НомераПакетов.Организация)
	|				И (ПакетыАмортизации.НомерПакета = НомераПакетов.НомерПакета)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РазукомплектацияОС.Организация,
	|		РазукомплектацияОС.ОсновноеСредство
	|	ИЗ
	|		Документ.РазукомплектацияОС КАК РазукомплектацияОС
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|			ПО (ТаблицаОС.Ссылка = РазукомплектацияОС.Ссылка)
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&КонецМесяца) КАК УчетнаяПолитика
	|			ПО УчетнаяПолитика.Организация = РазукомплектацияОС.Организация.ГоловнаяОрганизация
	|	ГДЕ
	|		ЕСТЬNULL(УчетнаяПолитика.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)
	|		И РазукомплектацияОС.Дата МЕЖДУ &НачалоПредыдущегоМесяца И &КонецМесяца
	|		И РазукомплектацияОС.Проведен
	|		И ТаблицаОС.ОсновноеСредство В
	|				(ВЫБРАТЬ
	|					ПакетыАмортизации.ОсновноеСредство
	|				ИЗ
	|					НомераПакетовСФилиалами КАК НомераПакетов
	|						ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыАмортизацииОС КАК ПакетыАмортизации
	|						ПО ПакетыАмортизации.Организация = НомераПакетов.Организация
	|							И ПакетыАмортизации.НомерПакета = НомераПакетов.НомерПакета)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОбъединениеОС.Организация,
	|		ТаблицаОС.ОсновноеСредство
	|	ИЗ
	|		Документ.ОбъединениеОС КАК ОбъединениеОС
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОбъединениеОС.ОС КАК ТаблицаОС
	|			ПО (ТаблицаОС.Ссылка = ОбъединениеОС.Ссылка)
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&КонецМесяца) КАК УчетнаяПолитика
	|			ПО УчетнаяПолитика.Организация = ОбъединениеОС.Организация.ГоловнаяОрганизация
	|	ГДЕ
	|		ЕСТЬNULL(УчетнаяПолитика.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)
	|		И ОбъединениеОС.Дата МЕЖДУ &НачалоПредыдущегоМесяца И &КонецМесяца
	|		И ОбъединениеОС.Проведен
	|		И ОбъединениеОС.ОсновноеСредство В
	|				(ВЫБРАТЬ
	|					ПакетыАмортизации.ОсновноеСредство
	|				ИЗ
	|					НомераПакетовСФилиалами КАК НомераПакетов
	|						ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыАмортизацииОС КАК ПакетыАмортизации
	|						ПО ПакетыАмортизации.Организация = НомераПакетов.Организация
	|							И ПакетыАмортизации.НомерПакета = НомераПакетов.НомерПакета)) КАК ПакетыАмортизации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета,
	|	Организация";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаСписокОбъектовУчета(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = АмортизацияОСЛокализация.ТекстЗапросаСписокОбъектовУчета(ПараметрыРасчета);
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СписокОС.НачалоМесяца                        КАК НачалоМесяца,
		|	СписокОС.КонецМесяца                         КАК КонецМесяца,
		|	СписокОС.Организация                         КАК Организация,
		|	СписокОС.ОсновноеСредство                    КАК ОсновноеСредство,
		|	СписокОС.СпособОтраженияРасходовУУ           КАК СпособОтраженияРасходов,
		|	ЕСТЬNULL(ПакетыАмортизацииОС.НомерПакета, 0) КАК НомерПакета,
		|
		|	ЕСТЬNULL(УчетнаяПолитика.ВариантПроводокПоОбесценениюВНА, ЗНАЧЕНИЕ(Перечисление.ВариантыПроводокПоОбесценениюВНА.ДоначислятьКорректировкуОбесценения)) КАК ВариантПроводокПоОбесценениюВНА,
		|
		|	(ЕСТЬNULL(УчетнаяПолитика.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) 
		|		= ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) КАК АмортизацияНачисляетсяПоСтандартамМУ,
		|
		|	(ЕСТЬNULL(УчетнаяПолитика.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) 
		|		= ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) КАК АмортизацияНачисляетсяПоСтандартамМУ_УУ
		|
		|ПОМЕСТИТЬ СписокОбъектовУчета
		|ИЗ
		|	СписокОбъектовУчета_УУ КАК СписокОС
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыАмортизацииОС КАК ПакетыАмортизацииОС
		|		ПО ПакетыАмортизацииОС.Организация = СписокОС.Организация
		|			И ПакетыАмортизацииОС.ОсновноеСредство = СписокОС.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&КонецМесяца) КАК УчетнаяПолитика
		|		ПО УчетнаяПолитика.Организация = СписокОС.ГоловнаяОрганизация
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	Организация";
		
	КонецЕсли; 
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Формирует тексты запросов, которые создают таблицы объектов учета, по которым требуется расчет амортизации.
//
// Параметры:
//  СписокЗапросов	 - Массив	 - Список запросов, в который нужно добавить текст запроса.
//  ПараметрыРасчета - Структура - Содержит параметры расчета амортизации.
//  ПрефиксТаблицы	 - Строка	 - Используется для добавления префикса к созданным таблицам.
//
Процедура ТекстЗапросаСписокВсехОС(СписокЗапросов, ПараметрыРасчета, ПрефиксТаблицы = "") Экспорт

	АмортизацияОСЛокализация.ДополнитьТекстЗапросаСписокВсехОС(СписокЗапросов, ПараметрыРасчета, ПрефиксТаблицы);
	
	#Область СписокОбъектовУчета_Доначисление_УУ
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СтоимостьОС.Организация      КАК Организация,
	|	СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство,
	|	МИНИМУМ(СтоимостьОС.Период)  КАК Период
	|ПОМЕСТИТЬ СписокОбъектовУчета_Доначисление_УУ
	|ИЗ
	|	РегистрНакопления.СтоимостьОС КАК СтоимостьОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&КонецПредыдущегоМесяца) КАК УчетнаяПолитика
	|		ПО УчетнаяПолитика.Организация = СтоимостьОС.Организация.ГоловнаяОрганизация
	|ГДЕ
	|	СтоимостьОС.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
	|	И СтоимостьОС.ОтражатьВУпрУчете
	|	И СтоимостьОС.Активность
	|	И СтоимостьОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|	И (ЕСТЬNULL(УчетнаяПолитика.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) 
	|		= ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ))
	|
	|	И СтоимостьОС.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОС), 
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуПредметовАренды),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.МодернизацияОС))
	|
	|	И (СтоимостьОС.Организация, СтоимостьОС.ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					ПакетыАмортизации.Организация,
	|					ПакетыАмортизации.ОбъектУчета
	|				ИЗ
	|					ПакетыАмортизации КАК ПакетыАмортизации)
	|
	|СГРУППИРОВАТЬ ПО
	|	СтоимостьОС.Организация,
	|	СтоимостьОС.ОсновноеСредство
	|
	|ИМЕЮЩИЕ
	|	СУММА(СтоимостьОС.Стоимость - СтоимостьОС.ПредварительнаяСтоимость) > 0
	|		ИЛИ НЕ &ВедетсяРегламентированныйУчетВНА 
	|			И СУММА(СтоимостьОС.СтоимостьРегл - СтоимостьОС.ПредварительнаяСтоимостьРегл) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация";
	
	Если ЗначениеЗаполнено(ПрефиксТаблицы) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СписокОбъектовУчета_Доначисление_УУ", "СписокОбъектовУчета_Доначисление_УУ" + ПрефиксТаблицы);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СписокОбъектовУчета_УУ", "СписокОбъектовУчета_УУ" + ПрефиксТаблицы);
	КонецЕсли; 

	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область СписокОбъектовУчета_УУ
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&КонецМесяца                    КАК ПериодНачисления,
	|	&НачалоМесяца                   КАК НачалоМесяца,
	|	&КонецМесяца                    КАК КонецМесяца,
	|	&КонецПредыдущегоМесяца         КАК Период,
	|	ЛОЖЬ                            КАК ЭтоДоначисление,
	|	ЛОЖЬ                            КАК ПолученПриРазукомплектацииОбъединении,
	|	ПорядокУчетаОС.Организация      КАК Организация,
	|	ПорядокУчетаОС.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ПорядокУчетаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	ПорядокУчетаОС.НачислятьАмортизациюУУ КАК НачислятьАмортизацию,
	|
	|	ПорядокУчетаОС.СпособОтраженияРасходов КАК СпособОтраженияРасходовУУ,
	|
	|	ПорядокУчетаОС.СтатьяРасходов    КАК СтатьяРасходовУУ,
	|	ПорядокУчетаОС.АналитикаРасходов КАК АналитикаРасходовУУ,
	|
	|	ПорядокУчетаОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюУУ,
	|	ПорядокУчетаОС.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходовУУ,
	|
	|	ЕСТЬNULL(УчетнаяПолитика.ВариантПроводокПоОбесценениюВНА, ЗНАЧЕНИЕ(Перечисление.ВариантыПроводокПоОбесценениюВНА.ДоначислятьКорректировкуОбесценения)) КАК ВариантПроводокПоОбесценениюВНА,
	|
	|	(ЕСТЬNULL(УчетнаяПолитика.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) 
	|		= ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) КАК АмортизацияНачисляетсяПоСтандартамМУ,
	|
	|	(ЕСТЬNULL(УчетнаяПолитика.ПорядокНачисленияАмортизации, ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.СДатыПринятияКУчету)) 
	|		= ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.СДатыПринятияКУчету)) КАК АмортизацияНачисляетсяСДатыПринятияКУчету
	|
	|ПОМЕСТИТЬ СписокОбъектовУчета_УУ
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(
	|			&КонецПредыдущегоМесяца,
	|			(Организация, ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					ПакетыАмортизации.Организация,
	|					ПакетыАмортизации.ОбъектУчета
	|				ИЗ
	|					ПакетыАмортизации КАК ПакетыАмортизации)) КАК ПорядокУчетаОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&КонецМесяца) КАК УчетнаяПолитика
	|		ПО УчетнаяПолитика.Организация = ПорядокУчетаОС.Организация.ГоловнаяОрганизация
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	&КонецМесяца                    КАК ПериодНачисления,
	|	&НачалоПредыдущегоМесяца        КАК НачалоМесяца,
	|	&КонецПредыдущегоМесяца         КАК КонецМесяца,
	|	&НачалоПредыдущегоМесяца        КАК Период,
	|	ИСТИНА                          КАК ЭтоДоначисление,
	|	ЛОЖЬ                            КАК ПолученПриРазукомплектацииОбъединении,
	|	ПорядокУчетаОС.Организация      КАК Организация,
	|	ПорядокУчетаОС.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ПорядокУчетаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	ПорядокУчетаОС.НачислятьАмортизациюУУ КАК НачислятьАмортизацию,
	|
	|	ПорядокУчетаОС.СпособОтраженияРасходов КАК СпособОтраженияРасходовУУ,
	|
	|	ПорядокУчетаОС.СтатьяРасходов    КАК СтатьяРасходовУУ,
	|	ПорядокУчетаОС.АналитикаРасходов КАК АналитикаРасходовУУ,
	|
	|	ПорядокУчетаОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюУУ,
	|	ПорядокУчетаОС.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходовУУ,
	|
	|	ЕСТЬNULL(УчетнаяПолитика.ВариантПроводокПоОбесценениюВНА, ЗНАЧЕНИЕ(Перечисление.ВариантыПроводокПоОбесценениюВНА.ДоначислятьКорректировкуОбесценения)) КАК ВариантПроводокПоОбесценениюВНА,
	|
	|	(ЕСТЬNULL(УчетнаяПолитика.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) 
	|		= ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) КАК АмортизацияНачисляетсяПоСтандартамМУ,
	|
	|	(ЕСТЬNULL(УчетнаяПолитика.ПорядокНачисленияАмортизации, ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.СДатыПринятияКУчету)) 
	|		= ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.СДатыПринятияКУчету)) КАК АмортизацияНачисляетсяСДатыПринятияКУчету
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(
	|			&КонецПозапрошлогоМесяца,
	|			(Организация, ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					СписокОС.Организация,
	|					СписокОС.ОсновноеСредство
	|				ИЗ
	|					СписокОбъектовУчета_Доначисление_УУ КАК СписокОС)) КАК ПорядокУчетаОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&КонецПредыдущегоМесяца) КАК УчетнаяПолитика
	|		ПО УчетнаяПолитика.Организация = ПорядокУчетаОС.Организация.ГоловнаяОрганизация
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	&КонецМесяца                    КАК ПериодНачисления,
	|	&НачалоМесяца                   КАК НачалоМесяца,
	|	&КонецМесяца                    КАК КонецМесяца,
	|	НАЧАЛОПЕРИОДА(ПорядокУчетаОС.Период, ДЕНЬ) КАК Период,
	|	ЛОЖЬ                            КАК ЭтоДоначисление,
	|	ТИПЗНАЧЕНИЯ(ПорядокУчетаОС.Регистратор) В (ТИП(Документ.РазукомплектацияОС), ТИП(Документ.ОбъединениеОС)) КАК ПолученПриРазукомплектацииОбъединении,
	|	ПорядокУчетаОС.Организация      КАК Организация,
	|	ПорядокУчетаОС.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ПорядокУчетаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	ПорядокУчетаОС.НачислятьАмортизациюУУ КАК НачислятьАмортизацию,
	|
	|	ПорядокУчетаОС.СпособОтраженияРасходов КАК СпособОтраженияРасходовУУ,
	|
	|	ПорядокУчетаОС.СтатьяРасходов    КАК СтатьяРасходовУУ,
	|	ПорядокУчетаОС.АналитикаРасходов КАК АналитикаРасходовУУ,
	|
	|	ПорядокУчетаОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюУУ,
	|	ПорядокУчетаОС.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходовУУ,
	|
	|	ЕСТЬNULL(УчетнаяПолитика.ВариантПроводокПоОбесценениюВНА, ЗНАЧЕНИЕ(Перечисление.ВариантыПроводокПоОбесценениюВНА.ДоначислятьКорректировкуОбесценения)) КАК ВариантПроводокПоОбесценениюВНА,
	|
	|	(ЕСТЬNULL(УчетнаяПолитика.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) 
	|		= ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) КАК АмортизацияНачисляетсяПоСтандартамМУ,
	|
	|	(ЕСТЬNULL(УчетнаяПолитика.ПорядокНачисленияАмортизации, ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.СДатыПринятияКУчету)) 
	|		= ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.СДатыПринятияКУчету)) КАК АмортизацияНачисляетсяСДатыПринятияКУчету
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСУУ КАК ПорядокУчетаОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&КонецМесяца) КАК УчетнаяПолитика
	|		ПО УчетнаяПолитика.Организация = ПорядокУчетаОС.Организация.ГоловнаяОрганизация
	|ГДЕ
	|	ПорядокУчетаОС.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
	|	И ПорядокУчетаОС.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|
	|	И (ЕСТЬNULL(УчетнаяПолитика.ПорядокНачисленияАмортизации, ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.СДатыПринятияКУчету)) 
	|		= ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.СДатыПринятияКУчету))
	|
	|	И ТИПЗНАЧЕНИЯ(ПорядокУчетаОС.Регистратор) В (
	|				ТИП(Документ.ПринятиеКУчетуОС2_4),
	|				ТИП(Документ.РазукомплектацияОС),
	|				ТИП(Документ.ОбъединениеОС))
	|
	|	И (ПорядокУчетаОС.Организация, ПорядокУчетаОС.ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					ПакетыАмортизации.Организация,
	|					ПакетыАмортизации.ОбъектУчета
	|				ИЗ
	|					ПакетыАмортизации КАК ПакетыАмортизации)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	&КонецМесяца                    КАК ПериодНачисления,
	|	&НачалоПредыдущегоМесяца        КАК НачалоМесяца,
	|	&КонецПредыдущегоМесяца         КАК КонецМесяца,
	|	НАЧАЛОПЕРИОДА(ПорядокУчетаОС.Период, ДЕНЬ) КАК Период,
	|	ИСТИНА                          КАК ЭтоДоначисление,
	|	ЛОЖЬ                            КАК ПолученПриРазукомплектацииОбъединении,
	|	ПорядокУчетаОС.Организация      КАК Организация,
	|	ПорядокУчетаОС.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ПорядокУчетаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	ПорядокУчетаОС.НачислятьАмортизациюУУ КАК НачислятьАмортизацию,
	|
	|	ПорядокУчетаОС.СпособОтраженияРасходов КАК СпособОтраженияРасходовУУ,
	|
	|	ПорядокУчетаОС.СтатьяРасходов    КАК СтатьяРасходовУУ,
	|	ПорядокУчетаОС.АналитикаРасходов КАК АналитикаРасходовУУ,
	|
	|	ПорядокУчетаОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюУУ,
	|	ПорядокУчетаОС.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходовУУ,
	|
	|	ЕСТЬNULL(УчетнаяПолитика.ВариантПроводокПоОбесценениюВНА, ЗНАЧЕНИЕ(Перечисление.ВариантыПроводокПоОбесценениюВНА.ДоначислятьКорректировкуОбесценения)) КАК ВариантПроводокПоОбесценениюВНА,
	|
	|	(ЕСТЬNULL(УчетнаяПолитика.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) 
	|		= ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) КАК АмортизацияНачисляетсяПоСтандартамМУ,
	|
	|	(ЕСТЬNULL(УчетнаяПолитика.ПорядокНачисленияАмортизации, ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.СДатыПринятияКУчету)) 
	|		= ЗНАЧЕНИЕ(Перечисление.ПорядокНачисленияАмортизации.СДатыПринятияКУчету)) КАК АмортизацияНачисляетсяСДатыПринятияКУчету
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСУУ КАК ПорядокУчетаОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&КонецПредыдущегоМесяца) КАК УчетнаяПолитика
	|		ПО УчетнаяПолитика.Организация = ПорядокУчетаОС.Организация.ГоловнаяОрганизация
	|ГДЕ
	|	ПорядокУчетаОС.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
	|	И ПорядокУчетаОС.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|
	|	И ТИПЗНАЧЕНИЯ(ПорядокУчетаОС.Регистратор) В (
	|				ТИП(Документ.ПринятиеКУчетуОС2_4),
	|				ТИП(Документ.РазукомплектацияОС))
	|
	|	И (ПорядокУчетаОС.Организация, ПорядокУчетаОС.ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					СписокОС.Организация,
	|					СписокОС.ОсновноеСредство
	|				ИЗ
	|					СписокОбъектовУчета_Доначисление_УУ КАК СписокОС)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство, 
	|	Организация";
	
	Если ЗначениеЗаполнено(ПрефиксТаблицы) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СписокОбъектовУчета_Доначисление_УУ", "СписокОбъектовУчета_Доначисление_УУ" + ПрефиксТаблицы);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СписокОбъектовУчета_УУ", "СписокОбъектовУчета_УУ" + ПрефиксТаблицы);
	КонецЕсли; 

	СписокЗапросов.Добавить(ТекстЗапроса);

	#КонецОбласти
	
	#Область СписокОбъектовУчета_Доначисление_Все
	ТекстЗапроса = АмортизацияОСЛокализация.ТекстЗапросаСписокОбъектовУчетаДоначислениеВсе(ПрефиксТаблицы);
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	СписокОбъектовУчета.Организация      КАК Организация,
		|	СписокОбъектовУчета.ОсновноеСредство КАК ОсновноеСредство,
		|	СписокОбъектовУчета.Период           КАК Период
		|
		|ПОМЕСТИТЬ СписокОбъектовУчета_Доначисление_Все
		|ИЗ
		|	СписокОбъектовУчета_Доначисление_УУ КАК СписокОбъектовУчета
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	Организация";
	
		Если ЗначениеЗаполнено(ПрефиксТаблицы) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СписокОбъектовУчета_Доначисление_УУ", "СписокОбъектовУчета_Доначисление_УУ" + ПрефиксТаблицы);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СписокОбъектовУчета_Доначисление_Все", "СписокОбъектовУчета_Доначисление_Все" + ПрефиксТаблицы);
		КонецЕсли;
		
	КонецЕсли;
	 
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
КонецПроцедуры

// Получает все необходимые параметры для расчета.
//
Процедура ТекстЗапросаПараметрыРасчетаАмортизации(СписокЗапросов, ПараметрыРасчета)

	ОбъектУчета = "ОсновноеСредство";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПараметрыРасчетаАмортизации(
		"МестонахождениеОС", "", ОбъектУчета, СписокЗапросов);
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПараметрыРасчетаАмортизации(
		"ПервоначальныеСведенияОС", "", ОбъектУчета, СписокЗапросов);
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПараметрыРасчетаАмортизации(
		"ПорядокУчетаОС", "", ОбъектУчета, СписокЗапросов);
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПараметрыРасчетаАмортизации(
		"ПараметрыАмортизацииОСУУ", "", ОбъектУчета, СписокЗапросов);
	
	#Область ДатыПоследнегоИзменения
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПараметрыАмортизацииОСУУ.НачалоМесяца КАК НачалоМесяца,
	|	ПараметрыАмортизацииОСУУ.Организация КАК Организация,
	|	ПараметрыАмортизацииОСУУ.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(УчетнаяПолитикаФинансовогоУчета.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)
	|			ТОГДА ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ, 1), МЕСЯЦ)
	|	КОНЕЦ КАК Дата
	|
	|ПОМЕСТИТЬ ДатыПоследнегоИзмененияУУ
	|ИЗ
	|	ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&НачалоМесяца) КАК УчетнаяПолитикаФинансовогоУчета
	|		ПО УчетнаяПолитикаФинансовогоУчета.Организация = ПараметрыАмортизацииОСУУ.Организация.ГоловнаяОрганизация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НачалоМесяца,
	|	Организация,
	|	ОсновноеСредство";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область СтоимостьОСНаНачалоМесяца
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&НачалоМесяца КАК НачалоМесяца,
	|	СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СтоимостьОС.СтоимостьОстаток КАК Стоимость,
	|	СтоимостьОС.СтоимостьРеглОстаток КАК СтоимостьРегл,
	|	СтоимостьОС.СтоимостьНУОстаток КАК СтоимостьНУ,
	|	СтоимостьОС.СтоимостьПРОстаток КАК СтоимостьПР,
	|	СтоимостьОС.СтоимостьВРОстаток КАК СтоимостьВР,
	|	СтоимостьОС.СтоимостьЦФОстаток КАК СтоимостьЦФ,
	|	СтоимостьОС.СтоимостьНУЦФОстаток КАК СтоимостьНУЦФ,
	|	СтоимостьОС.СтоимостьПРЦФОстаток КАК СтоимостьПРЦФ,
	|	СтоимостьОС.СтоимостьВРЦФОстаток КАК СтоимостьВРЦФ,
	|	СтоимостьОС.АмортизационнаяПремияОстаток КАК АмортизационнаяПремия
	|ПОМЕСТИТЬ СтоимостьОС_НаНачалоМесяца
	|	ИЗ
	|		РегистрНакопления.СтоимостьОС.Остатки(
	|				&КонецПредыдущегоМесяцаГраница,
	|				(Организация, ОсновноеСредство) В
	|					(ВЫБРАТЬ
	|						СписокОС.Организация,
	|						СписокОС.ОсновноеСредство
	|					ИЗ
	|						СписокОбъектовУчета КАК СписокОС)) КАК СтоимостьОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область СтоимостьИАмортизацияНаНачалоГода
	
	ТекстЗапроса = АмортизацияОСЛокализация.ТекстЗапросаВтСписокОСУменьшаемогоОстатка(ПараметрыРасчета);
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СписокОС.ОсновноеСредство
		|ПОМЕСТИТЬ втСписокОСУменьшаемогоОстатка
		|ИЗ
		|	ПараметрыАмортизацииОСУУ КАК СписокОС
		|ГДЕ
		|	СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство";
		
	КонецЕсли; 
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = АмортизацияОСЛокализация.ТекстЗапросаСтоимостьИАмортизацияНаНачалоГода(ПараметрыРасчета);
	
	Если ТекстЗапроса = Неопределено Тогда
	
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&НачалоМесяца                            КАК НачалоМесяца,
		|	СтоимостьОС.ОсновноеСредство             КАК ОсновноеСредство,
		|	СтоимостьОС.СтоимостьОстаток             КАК Стоимость,
		|	СтоимостьОС.СтоимостьРеглОстаток         КАК СтоимостьРегл,
		|	СтоимостьОС.СтоимостьЦФОстаток           КАК СтоимостьЦФ
		|ПОМЕСТИТЬ СтоимостьОС_НаНачалоГода
		|ИЗ
		|	РегистрНакопления.СтоимостьОС.Остатки(
		|			&НачалоГода,
		|			ОсновноеСредство В
		|				(ВЫБРАТЬ СписокОС.ОсновноеСредство ИЗ втСписокОСУменьшаемогоОстатка КАК СписокОС)) КАК СтоимостьОС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&НачалоМесяца                          КАК НачалоМесяца,
		|	АмортизацияОС.ОсновноеСредство         КАК ОсновноеСредство,
		|	-АмортизацияОС.АмортизацияОстаток      КАК Амортизация,
		|	-АмортизацияОС.АмортизацияРеглОстаток  КАК АмортизацияРегл,
		|	-АмортизацияОС.АмортизацияЦФОстаток    КАК АмортизацияЦФ
		|ПОМЕСТИТЬ АмортизацияОС_НаНачалоГода
		|ИЗ
		|	РегистрНакопления.АмортизацияОС.Остатки(
		|			&НачалоГода,
		|			ОсновноеСредство В
		|				(ВЫБРАТЬ СписокОС.ОсновноеСредство ИЗ втСписокОСУменьшаемогоОстатка КАК СписокОС)) КАК АмортизацияОС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство";
		
	КонецЕсли;
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область ПредварительнаяСтоимостьОС
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&НачалоМесяца                                    КАК НачалоМесяца,
	|	СтоимостьОС.ОсновноеСредство                     КАК ОсновноеСредство,
	|
	|	СУММА(СтоимостьОС.ПредварительнаяСтоимость)      КАК Стоимость,
	|	СУММА(СтоимостьОС.ПредварительнаяСтоимостьРегл)  КАК СтоимостьРегл,
	|
	|	ВЫБОР
	|		КОГДА СУММА(СтоимостьОС.ПредварительнаяСтоимостьРегл) <> 0
	|			ТОГДА ВЫРАЗИТЬ(МАКСИМУМ(ЕСТЬNULL(ТаблицаОС.СуммаЦелевыхСредств, 0)) / СУММА(СтоимостьОС.ПредварительнаяСтоимостьРегл) КАК ЧИСЛО (23, 10))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ДоляЦФ
	|
	|ПОМЕСТИТЬ ПредварительнаяСтоимостьОС
	|ИЗ
	|	РегистрНакопления.СтоимостьОС КАК СтоимостьОС                     
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС2_4.ОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = СтоимостьОС.Регистратор
	|			И ТаблицаОС.ОсновноеСредство = СтоимостьОС.ОсновноеСредство
	|
	|ГДЕ
	|	СтоимостьОС.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
	|
	|	И (СтоимостьОС.ПредварительнаяСтоимость <> 0
	|		ИЛИ СтоимостьОС.ПредварительнаяСтоимостьРегл <> 0)
	|
	|	И СтоимостьОС.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОС), 
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуПредметовАренды), 
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСПоИнвентаризации))
	|
	|	И СтоимостьОС.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				СписокОС.ОсновноеСредство
	|			ИЗ
	|				СписокОбъектовУчета КАК СписокОС)
	|
	|СГРУППИРОВАТЬ ПО
	|	СтоимостьОС.ОсновноеСредство	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&НачалоПредыдущегоМесяца              КАК НачалоМесяца,
	|	СтоимостьОС.ОсновноеСредство          КАК ОсновноеСредство,
	|
	|	ВЫБОР 
	|		КОГДА СУММА(СтоимостьОС.Стоимость - СтоимостьОС.ПредварительнаяСтоимость) > 0 
	|			ТОГДА СУММА(СтоимостьОС.Стоимость - СтоимостьОС.ПредварительнаяСтоимость) 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК Стоимость,
	|
	|	ВЫБОР
	|		КОГДА СУММА(СтоимостьОС.СтоимостьРегл - СтоимостьОС.ПредварительнаяСтоимостьРегл) > 0
	|			ТОГДА СУММА(СтоимостьОС.СтоимостьРегл - СтоимостьОС.ПредварительнаяСтоимостьРегл) 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	|	ВЫБОР
	|		КОГДА СУММА(СтоимостьОС.СтоимостьРегл - СтоимостьОС.ПредварительнаяСтоимостьРегл) > 0
	|			ТОГДА ВЫРАЗИТЬ(МАКСИМУМ(ЕСТЬNULL(ТаблицаОС.СуммаЦелевыхСредств, 0)) / СУММА(СтоимостьОС.СтоимостьРегл) КАК ЧИСЛО (23, 10)) 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ДоляЦФ
	|
	|ИЗ
	|	РегистрНакопления.СтоимостьОС КАК СтоимостьОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС2_4.ОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = СтоимостьОС.Регистратор
	|			И ТаблицаОС.ОсновноеСредство = СтоимостьОС.ОсновноеСредство
	|
	|ГДЕ
	|	СтоимостьОС.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
	|
	|	И СтоимостьОС.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОС), 
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуПредметовАренды), 
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСПоИнвентаризации),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.МодернизацияОС))
	|
	|	И СтоимостьОС.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				СписокОС.ОсновноеСредство
	|			ИЗ
	|				СписокОбъектовУчета_Доначисление_УУ КАК СписокОС)
	|
	|СГРУППИРОВАТЬ ПО
	|	СтоимостьОС.ОсновноеСредство
	|
	|ИМЕЮЩИЕ 
	|	СУММА(СтоимостьОС.Стоимость - СтоимостьОС.ПредварительнаяСтоимость) > 0
	|		ИЛИ СУММА(СтоимостьОС.СтоимостьРегл - СтоимостьОС.ПредварительнаяСтоимостьРегл) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
		
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область АмортизацияОСНаНачалоМесяца
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&НачалоМесяца                          КАК НачалоМесяца,
	|	АмортизацияОС.Организация              КАК Организация,
	|	АмортизацияОС.ОсновноеСредство         КАК ОсновноеСредство,
	|	-АмортизацияОС.АмортизацияОстаток      КАК Амортизация,
	|	-АмортизацияОС.АмортизацияРеглОстаток  КАК АмортизацияРегл,
	|	-АмортизацияОС.АмортизацияНУОстаток    КАК АмортизацияНУ,
	|	-АмортизацияОС.АмортизацияПРОстаток    КАК АмортизацияПР,
	|	-АмортизацияОС.АмортизацияВРОстаток    КАК АмортизацияВР,
	|	-АмортизацияОС.АмортизацияЦФОстаток    КАК АмортизацияЦФ,
	|	-АмортизацияОС.АмортизацияНУЦФОстаток  КАК АмортизацияНУЦФ,
	|	-АмортизацияОС.АмортизацияПРЦФОстаток  КАК АмортизацияПРЦФ,
	|	-АмортизацияОС.АмортизацияВРЦФОстаток  КАК АмортизацияВРЦФ
	|ПОМЕСТИТЬ АмортизацияОС_НаНачалоМесяца
	|ИЗ
	|	РегистрНакопления.АмортизацияОС.Остатки(
	|			&КонецПредыдущегоМесяцаГраница,
	|			(Организация, ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					СписокОС.Организация,
	|					СписокОС.ОсновноеСредство
	|				ИЗ
	|					СписокОбъектовУчета КАК СписокОС)) КАК АмортизацияОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область ОбесценениеВНА
	// В расчете амортизации и корректировки обесценения участвуют только те ВНА,
	// по которым ЕГДС на конец месяца расчета амортизации не изменилась.
	// По объектам ВНА, изменившим ЕГДС, происходит восстановление остатка обесценения,
	// которое производится документом, изменившим ЕГДС, и рассчитывается это после 
	// выполнения операции по расчету амортизации.
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СоставЕГД_НаКонецМесяцаРасчета.ВнеоборотныйАктив КАК ВнеоборотныйАктив,
	|	СоставЕГД_НаКонецМесяцаРасчета.ЕГДС КАК ЕГДСНаКонецМесяца
	|ПОМЕСТИТЬ ЕГДСВНАНаКонецМесяцаРасчета
	|ИЗ
	|	РегистрСведений.СоставЕГДС.СрезПоследних(КОНЕЦПЕРИОДА(&НачалоМесяца, МЕСЯЦ), (Организация, ВнеоборотныйАктив) В
	|		(ВЫБРАТЬ
	|			СписокОС.Организация,
	|			СписокОС.ОсновноеСредство
	|		ИЗ
	|			СписокОбъектовУчета КАК СписокОС)) КАК СоставЕГД_НаКонецМесяцаРасчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВнеоборотныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&НачалоМесяца КАК НачалоМесяца,
	|	ОбесценениеВНА.ВнеоборотныйАктив КАК ОсновноеСредство,
	|	-СУММА(ОбесценениеВНА.ОбесценениеРеглКонечныйОстаток) КАК ОстатокОбесцененияРегл,
	|	-СУММА(ОбесценениеВНА.ОбесценениеУпрКонечныйОстаток) КАК ОстатокОбесцененияУпр,
	|	МАКСИМУМ(ОбесценениеВНА.ЕГДС) КАК ЕГДС,
	|	МАКСИМУМ(ЕСТЬNULL(ЕГДСВНАНаКонецМесяцаРасчета.ЕГДСНаКонецМесяца,
	|		ЗНАЧЕНИЕ(Справочник.ЕдиницыГенерирующиеДенежныеСредства.ПустаяСсылка))) КАК ЕГДСНаКонецМесяца
	|ПОМЕСТИТЬ ОбесценениеВНА_СИзменившимиЕГДС
	|ИЗ
	|	РегистрНакопления.ОбесценениеВНА.ОстаткиИОбороты(&КонецПредыдущегоМесяцаГраница, &КонецПредыдущегоМесяцаГраница,,
	|		ДвиженияИГраницыПериода, (Организация, ВнеоборотныйАктив) В
	|		(ВЫБРАТЬ
	|			СписокОС.Организация,
	|			СписокОС.ОсновноеСредство
	|		ИЗ
	|			СписокОбъектовУчета КАК СписокОС)) КАК ОбесценениеВНА
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЕГДСВНАНаКонецМесяцаРасчета КАК ЕГДСВНАНаКонецМесяцаРасчета
	|		ПО ОбесценениеВНА.ВнеоборотныйАктив = ЕГДСВНАНаКонецМесяцаРасчета.ВнеоборотныйАктив
	|СГРУППИРОВАТЬ ПО
	|	ОбесценениеВНА.ВнеоборотныйАктив
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ЕГДСНаКонецМесяца,
	|	ЕГДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбесценениеВНА_СИзменившимиЕГДС.НачалоМесяца КАК НачалоМесяца,
	|	ОбесценениеВНА_СИзменившимиЕГДС.ОсновноеСредство КАК ОсновноеСредство,
	|	ОбесценениеВНА_СИзменившимиЕГДС.ОстатокОбесцененияРегл КАК ОстатокОбесцененияРегл,
	|	ОбесценениеВНА_СИзменившимиЕГДС.ОстатокОбесцененияУпр КАК ОстатокОбесцененияУпр,
	|	ОбесценениеВНА_СИзменившимиЕГДС.ЕГДС КАК ЕГДС
	|ПОМЕСТИТЬ ОбесценениеВНА
	|ИЗ
	|	ОбесценениеВНА_СИзменившимиЕГДС КАК ОбесценениеВНА_СИзменившимиЕГДС
	|ГДЕ
	|	ОбесценениеВНА_СИзменившимиЕГДС.ЕГДСНаКонецМесяца = ОбесценениеВНА_СИзменившимиЕГДС.ЕГДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ОбесценениеВНА_СИзменившимиЕГДС";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ВыработкаОС
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ПорядокУчетаОС.ПоказательНаработки КАК ПоказательНаработки,
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&НачалоМесяца, МЕСЯЦ, -1), МЕСЯЦ) = НАЧАЛОПЕРИОДА(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ, МЕСЯЦ) 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПринятоКУчетуВПрошломМесяце
	|ПОМЕСТИТЬ СписокОС_Выработка
	|ИЗ
	|	СписокОбъектовУчета КАК СписокОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО (ПервоначальныеСведенияОС.Организация = СписокОС.Организация)
	|			И (ПервоначальныеСведенияОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПервоначальныеСведенияОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО (ПорядокУчетаОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПорядокУчетаОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
	|		ПО (ПараметрыАмортизацииОСУУ.Организация = СписокОС.Организация)
	|			И (ПараметрыАмортизацииОСУУ.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПараметрыАмортизацииОСУУ.НачалоМесяца = СписокОС.НачалоМесяца)
	|ГДЕ
	|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ В (
	|			ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции), 
	|			ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПоЕНАОФНа1000кмПробега))
	|
	|	ИЛИ ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции) 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыработкаОС.ОбъектЭксплуатации КАК ОсновноеСредство,
	|	ВыработкаОС.ПоказательНаработки КАК ПоказательНаработки,
	|	ВыработкаОС.Значение КАК ОбъемВыработки
	|ПОМЕСТИТЬ ВыработкаОС_НаНачалоМесяца
	|ИЗ
	|	РегистрСведений.НаработкиОбъектовЭксплуатации.СрезПоследних(
	|			&КонецПредыдущегоМесяца,
	|			(ОбъектЭксплуатации, ПоказательНаработки) В
	|				(ВЫБРАТЬ
	|					СписокОС.ОсновноеСредство,
	|					СписокОС.ПоказательНаработки
	|				ИЗ
	|					СписокОС_Выработка КАК СписокОС)) КАК ВыработкаОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыработкаОС.ОбъектЭксплуатации КАК ОсновноеСредство,
	|	ВыработкаОС.ПоказательНаработки КАК ПоказательНаработки,
	|	ВыработкаОС.Значение КАК ОбъемВыработки,
	|	СписокОС.ПринятоКУчетуВПрошломМесяце КАК ПринятоКУчетуВПрошломМесяце
	|ПОМЕСТИТЬ ВыработкаОС_НаКонецМесяца
	|ИЗ
	|	РегистрСведений.НаработкиОбъектовЭксплуатации.СрезПоследних(
	|			&КонецМесяца,
	|			(ОбъектЭксплуатации, ПоказательНаработки) В
	|				(ВЫБРАТЬ
	|					СписокОС.ОсновноеСредство,
	|					СписокОС.ПоказательНаработки
	|				ИЗ
	|					СписокОС_Выработка КАК СписокОС)) КАК ВыработкаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ СписокОС_Выработка КАК СписокОС
	|		ПО (СписокОС.ОсновноеСредство = ВыработкаОС.ОбъектЭксплуатации)
	|			И (СписокОС.ПоказательНаработки = ВыработкаОС.ПоказательНаработки)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокОС.ОсновноеСредство,
	|	СписокОС.ПоказательНаработки
	|ПОМЕСТИТЬ СписокОС_ПринятоКУчетуВПрошломМесяце
	|ИЗ
	|	СписокОС_Выработка КАК СписокОС
	|ГДЕ
	|	СписокОС.ПринятоКУчетуВПрошломМесяце
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыработкаОС.ОбъектЭксплуатации КАК ОсновноеСредство,
	|	ВыработкаОС.ПоказательНаработки КАК ПоказательНаработки,
	|	ВыработкаОС.Значение КАК ОбъемВыработки
	|ПОМЕСТИТЬ ВыработкаОС_НаНачалоПредыдущегоМесяца
	|ИЗ
	|	РегистрСведений.НаработкиОбъектовЭксплуатации.СрезПоследних(
	|			&КонецПозапрошлогоМесяца,
	|			(ОбъектЭксплуатации, ПоказательНаработки) В
	|				(ВЫБРАТЬ
	|					СписокОС.ОсновноеСредство,
	|					СписокОС.ПоказательНаработки
	|				ИЗ
	|					СписокОС_ПринятоКУчетуВПрошломМесяце КАК СписокОС)) КАК ВыработкаОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&НачалоМесяца КАК НачалоМесяца,
	|	ВыработкаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ВыработкаОС.ПоказательНаработки КАК ПоказательНаработки,
	|	СУММА(ВыработкаОС.ОбъемВыработки) КАК ОбъемВыработки,
	|	СУММА(ВыработкаОС.ОбъемВыработкиЗаПрошлыйМесяц) КАК ОбъемВыработкиЗаПрошлыйМесяц,
	|	СУММА(ВыработкаОС.ОбъемВыработкиНаНачалоМесяца) КАК ОбъемВыработкиНаНачалоМесяца,
	|	СУММА(ВыработкаОС.ОбъемВыработкиНаНачалоПредыдущегоМесяца) КАК ОбъемВыработкиНаНачалоПредыдущегоМесяца
	|ПОМЕСТИТЬ ВыработкаОС
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВыработкаОС.ОсновноеСредство КАК ОсновноеСредство,
	|		ВыработкаОС.ПоказательНаработки КАК ПоказательНаработки,
	|		-ВыработкаОС.ОбъемВыработки КАК ОбъемВыработки,
	|		ВыработкаОС.ОбъемВыработки КАК ОбъемВыработкиЗаПрошлыйМесяц,
	|		ВыработкаОС.ОбъемВыработки КАК ОбъемВыработкиНаНачалоМесяца,
	|		0 КАК ОбъемВыработкиНаНачалоПредыдущегоМесяца
	|	ИЗ
	|		ВыработкаОС_НаНачалоМесяца КАК ВыработкаОС
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВыработкаОС.ОсновноеСредство,
	|		ВыработкаОС.ПоказательНаработки,
	|		ВыработкаОС.ОбъемВыработки,
	|		0,
	|		0,
	|		0
	|	ИЗ
	|		ВыработкаОС_НаКонецМесяца КАК ВыработкаОС
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВыработкаОС.ОсновноеСредство,
	|		ВыработкаОС.ПоказательНаработки,
	|		0,
	|		0,
	|		-ВыработкаОС.ОбъемВыработки,
	|		ВыработкаОС.ОбъемВыработки
	|	ИЗ
	|		ВыработкаОС_НаНачалоПредыдущегоМесяца КАК ВыработкаОС
	|
	|	) КАК ВыработкаОС
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыработкаОС.ОсновноеСредство,
	|	ВыработкаОС.ПоказательНаработки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область Изменения_УУ
	
	#Область ИзменениеПараметров
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НачалоМесяца КАК Период,
	|	ПакетыАмортизации.Организация КАК Организация
	|ПОМЕСТИТЬ ОрганизацииПоСтандартамМУ
	|ИЗ
	|	ПакетыАмортизации КАК ПакетыАмортизации
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&НачалоМесяца) КАК УчетнаяПолитикаФинансовогоУчета
	|		ПО УчетнаяПолитикаФинансовогоУчета.Организация = ПакетыАмортизации.ГоловнаяОрганизация
	|ГДЕ
	|	ЕСТЬNULL(УчетнаяПолитикаФинансовогоУчета.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НачалоПредыдущегоМесяца КАК Период,
	|	ПакетыАмортизации.Организация КАК Организация
	|ИЗ
	|	ПакетыАмортизации КАК ПакетыАмортизации
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&НачалоПредыдущегоМесяца) КАК УчетнаяПолитикаФинансовогоУчета
	|		ПО УчетнаяПолитикаФинансовогоУчета.Организация = ПакетыАмортизации.ГоловнаяОрганизация
	|ГДЕ
	|	ЕСТЬNULL(УчетнаяПолитикаФинансовогоУчета.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// ПорядокУчетаОС
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НачалоМесяца КАК НачалоМесяца,
	|	ПараметрыАмортизацииОСУУ.Период КАК Период,
	|	ПараметрыАмортизацииОСУУ.МоментВремени,
	|	ПараметрыАмортизацииОСУУ.Регистратор,
	|	ПараметрыАмортизацииОСУУ.ОсновноеСредство,
	|	ПараметрыАмортизацииОСУУ.Организация,
	|	ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения КАК ДатаПоследнегоИзменения,
	|	ПараметрыАмортизацииОСУУ.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользования,
	|	ПараметрыАмортизацииОСУУ.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемНаработки,
	|	ПараметрыАмортизацииОСУУ.КоэффициентУскорения,
	|	ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации,
	|	ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимость,
	|	ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимостьРегл
	|ПОМЕСТИТЬ ПараметрыАмортизацииОСУУ_Изменения
	|ИЗ
	|	РегистрСведений.ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
	|ГДЕ
	|	ПараметрыАмортизацииОСУУ.Активность
	|	И ПараметрыАмортизацииОСУУ.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
	|	И НЕ ПараметрыАмортизацииОСУУ.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
	|	И НЕ ПараметрыАмортизацииОСУУ.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
	|
	|	И ПараметрыАмортизацииОСУУ.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				СписокОС.ОбъектУчета
	|			ИЗ
	|				ПакетыАмортизации КАК СписокОС)
	|
	|	И ПараметрыАмортизацииОСУУ.Организация В
	|			(ВЫБРАТЬ
	|				ОрганизацииПоСтандартамМУ.Организация
	|			ИЗ
	|				ОрганизацииПоСтандартамМУ КАК ОрганизацииПоСтандартамМУ
	|			ГДЕ
	|				ОрганизацииПоСтандартамМУ.Период = &НачалоМесяца)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НачалоПредыдущегоМесяца КАК НачалоМесяца,
	|	ПараметрыАмортизацииОСУУ.Период КАК Период,
	|	ПараметрыАмортизацииОСУУ.МоментВремени,
	|	ПараметрыАмортизацииОСУУ.Регистратор,
	|	ПараметрыАмортизацииОСУУ.ОсновноеСредство,
	|	ПараметрыАмортизацииОСУУ.Организация,
	|	ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения КАК ДатаПоследнегоИзменения,
	|	ПараметрыАмортизацииОСУУ.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользования,
	|	ПараметрыАмортизацииОСУУ.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемНаработки,
	|	ПараметрыАмортизацииОСУУ.КоэффициентУскорения,
	|	ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации,
	|	ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимость,
	|	ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимостьРегл
	|ИЗ
	|	РегистрСведений.ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокОбъектовУчета_Доначисление_УУ КАК Доначисление
	|		ПО ПараметрыАмортизацииОСУУ.ОсновноеСредство = Доначисление.ОсновноеСредство
	|ГДЕ
	|	ПараметрыАмортизацииОСУУ.Активность
	|	И ПараметрыАмортизацииОСУУ.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
	|	И ПараметрыАмортизацииОСУУ.Период >= Доначисление.Период
	|	И НЕ ПараметрыАмортизацииОСУУ.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
	|	И НЕ ПараметрыАмортизацииОСУУ.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
	|	И ПараметрыАмортизацииОСУУ.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				СписокОС.ОсновноеСредство
	|			ИЗ
	|				СписокОбъектовУчета_Доначисление_УУ КАК СписокОС)
	|
	|	И ПараметрыАмортизацииОСУУ.Организация В
	|			(ВЫБРАТЬ
	|				ОрганизацииПоСтандартамМУ.Организация
	|			ИЗ
	|				ОрганизацииПоСтандартамМУ КАК ОрганизацииПоСтандартамМУ
	|			ГДЕ
	|				ОрганизацииПоСтандартамМУ.Период = &НачалоПредыдущегоМесяца)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// ПорядокУчетаОС
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НачалоМесяца КАК НачалоМесяца,
	|	ПорядокУчетаОС.Период КАК Период,
	|	ПорядокУчетаОС.МоментВремени,
	|	ПорядокУчетаОС.Регистратор,
	|	ПорядокУчетаОС.ОсновноеСредство,
	|	ПорядокУчетаОС.Организация,
	|	ПорядокУчетаОС.ГруппаФинансовогоУчета,
	|	ПорядокУчетаОС.НаправлениеДеятельности,
	|	ПорядокУчетаОС.ПоказательНаработки,
	|	ПорядокУчетаОС.ОбъемНаработки
	|ПОМЕСТИТЬ ПорядокУчетаОС_ИзмененияУУ
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОС КАК ПорядокУчетаОС
	|ГДЕ
	|	ПорядокУчетаОС.Активность
	|	И ПорядокУчетаОС.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
	|	И НЕ ПорядокУчетаОС.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
	|	И НЕ ПорядокУчетаОС.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
	|	И ПорядокУчетаОС.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				СписокОС.ОбъектУчета
	|			ИЗ
	|				ПакетыАмортизации КАК СписокОС)
	|
	|	И ПорядокУчетаОС.Организация В
	|			(ВЫБРАТЬ
	|				ОрганизацииПоСтандартамМУ.Организация
	|			ИЗ
	|				ОрганизацииПоСтандартамМУ КАК ОрганизацииПоСтандартамМУ
	|			ГДЕ
	|				ОрганизацииПоСтандартамМУ.Период = &НачалоМесяца)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НачалоПредыдущегоМесяца КАК НачалоМесяца,
	|	ПорядокУчетаОС.Период КАК Период,
	|	ПорядокУчетаОС.МоментВремени,
	|	ПорядокУчетаОС.Регистратор,
	|	ПорядокУчетаОС.ОсновноеСредство,
	|	ПорядокУчетаОС.Организация,
	|	ПорядокУчетаОС.ГруппаФинансовогоУчета,
	|	ПорядокУчетаОС.НаправлениеДеятельности,
	|	ПорядокУчетаОС.ПоказательНаработки,
	|	ПорядокУчетаОС.ОбъемНаработки
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОС КАК ПорядокУчетаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокОбъектовУчета_Доначисление_УУ КАК Доначисление
	|		ПО ПорядокУчетаОС.ОсновноеСредство = Доначисление.ОсновноеСредство
	|ГДЕ
	|	ПорядокУчетаОС.Активность
	|	И ПорядокУчетаОС.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
	|	И ПорядокУчетаОС.Период >= Доначисление.Период
	|	И НЕ ПорядокУчетаОС.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
	|	И НЕ ПорядокУчетаОС.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
	|	И ПорядокУчетаОС.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				СписокОС.ОсновноеСредство
	|			ИЗ
	|				СписокОбъектовУчета_Доначисление_УУ КАК СписокОС)
	|
	|	И ПорядокУчетаОС.Организация В
	|			(ВЫБРАТЬ
	|				ОрганизацииПоСтандартамМУ.Организация
	|			ИЗ
	|				ОрганизацииПоСтандартамМУ КАК ОрганизацииПоСтандартамМУ
	|			ГДЕ
	|				ОрганизацииПоСтандартамМУ.Период = &НачалоПредыдущегоМесяца)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// ПорядокУчетаОСУУ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НачалоМесяца КАК НачалоМесяца,
	|	ПорядокУчетаОСУУ.Период КАК Период,
	|	ПорядокУчетаОСУУ.МоментВремени,
	|	ПорядокУчетаОСУУ.Регистратор,
	|	ПорядокУчетаОСУУ.ОсновноеСредство,
	|	ПорядокУчетаОСУУ.Организация,
	|	ПорядокУчетаОСУУ.Состояние,
	|	ПорядокУчетаОСУУ.НачислятьАмортизациюУУ,
	|	ПорядокУчетаОСУУ.СпособОтраженияРасходов,
	|	ПорядокУчетаОСУУ.СтатьяРасходов,
	|	ПорядокУчетаОСУУ.АналитикаРасходов,
	|	ПорядокУчетаОСУУ.ПередаватьРасходыВДругуюОрганизацию,
	|	ПорядокУчетаОСУУ.ОрганизацияПолучательРасходов
	|ПОМЕСТИТЬ ПорядокУчетаОСУУ_Изменения
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСУУ КАК ПорядокУчетаОСУУ
	|ГДЕ
	|	ПорядокУчетаОСУУ.Активность
	|	И ПорядокУчетаОСУУ.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
	|	И НЕ ПорядокУчетаОСУУ.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
	|	И НЕ ПорядокУчетаОСУУ.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
	|	И (ПорядокУчетаОСУУ.Организация, ПорядокУчетаОСУУ.ОсновноеСредство) В
	|			(ВЫБРАТЬ
	|				СписокОС.Организация,
	|				СписокОС.ОбъектУчета
	|			ИЗ
	|				ПакетыАмортизации КАК СписокОС)
	|
	|	И ПорядокУчетаОСУУ.Организация В
	|			(ВЫБРАТЬ
	|				ОрганизацииПоСтандартамМУ.Организация
	|			ИЗ
	|				ОрганизацииПоСтандартамМУ КАК ОрганизацииПоСтандартамМУ
	|			ГДЕ
	|				ОрганизацииПоСтандартамМУ.Период = &НачалоМесяца)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НачалоПредыдущегоМесяца КАК НачалоМесяца,
	|	ПорядокУчетаОСУУ.Период КАК Период,
	|	ПорядокУчетаОСУУ.МоментВремени,
	|	ПорядокУчетаОСУУ.Регистратор,
	|	ПорядокУчетаОСУУ.ОсновноеСредство,
	|	ПорядокУчетаОСУУ.Организация,
	|	ПорядокУчетаОСУУ.Состояние,
	|	ПорядокУчетаОСУУ.НачислятьАмортизациюУУ,
	|	ПорядокУчетаОСУУ.СпособОтраженияРасходов,
	|	ПорядокУчетаОСУУ.СтатьяРасходов,
	|	ПорядокУчетаОСУУ.АналитикаРасходов,
	|	ПорядокУчетаОСУУ.ПередаватьРасходыВДругуюОрганизацию,
	|	ПорядокУчетаОСУУ.ОрганизацияПолучательРасходов
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСУУ КАК ПорядокУчетаОСУУ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокОбъектовУчета_Доначисление_УУ КАК Доначисление
	|		ПО ПорядокУчетаОСУУ.ОсновноеСредство = Доначисление.ОсновноеСредство
	|ГДЕ
	|	ПорядокУчетаОСУУ.Активность
	|	И ПорядокУчетаОСУУ.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
	|	И ПорядокУчетаОСУУ.Период >= Доначисление.Период
	|	И НЕ ПорядокУчетаОСУУ.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
	|	И НЕ ПорядокУчетаОСУУ.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
	|	И (ПорядокУчетаОСУУ.Организация, ПорядокУчетаОСУУ.ОсновноеСредство) В
	|			(ВЫБРАТЬ
	|				СписокОС.Организация,
	|				СписокОС.ОсновноеСредство
	|			ИЗ
	|				СписокОбъектовУчета_Доначисление_УУ КАК СписокОС)
	|
	|	И ПорядокУчетаОСУУ.Организация В
	|			(ВЫБРАТЬ
	|				ОрганизацииПоСтандартамМУ.Организация
	|			ИЗ
	|				ОрганизацииПоСтандартамМУ КАК ОрганизацииПоСтандартамМУ
	|			ГДЕ
	|				ОрганизацииПоСтандартамМУ.Период = &НачалоПредыдущегоМесяца)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// МестонахождениеОС
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НачалоМесяца КАК НачалоМесяца,
	|	МестонахождениеОС.Период КАК Период,
	|	МестонахождениеОС.МоментВремени,
	|	МестонахождениеОС.Регистратор,
	|	МестонахождениеОС.ОсновноеСредство,
	|	МестонахождениеОС.Организация,
	|	МестонахождениеОС.Местонахождение
	|ПОМЕСТИТЬ МестонахождениеОС_ИзмененияУУ
	|ИЗ
	|	РегистрСведений.МестонахождениеОС КАК МестонахождениеОС
	|ГДЕ
	|	МестонахождениеОС.Активность
	|	И МестонахождениеОС.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
	|	И НЕ МестонахождениеОС.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
	|	И НЕ МестонахождениеОС.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
	|	И МестонахождениеОС.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				СписокОС.ОбъектУчета
	|			ИЗ
	|				ПакетыАмортизации КАК СписокОС)
	|
	|	И МестонахождениеОС.Организация В
	|			(ВЫБРАТЬ
	|				ОрганизацииПоСтандартамМУ.Организация
	|			ИЗ
	|				ОрганизацииПоСтандартамМУ КАК ОрганизацииПоСтандартамМУ
	|			ГДЕ
	|				ОрганизацииПоСтандартамМУ.Период = &НачалоМесяца)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НачалоПредыдущегоМесяца КАК НачалоМесяца,
	|	МестонахождениеОС.Период КАК Период,
	|	МестонахождениеОС.МоментВремени,
	|	МестонахождениеОС.Регистратор,
	|	МестонахождениеОС.ОсновноеСредство,
	|	МестонахождениеОС.Организация,
	|	МестонахождениеОС.Местонахождение
	|ИЗ
	|	РегистрСведений.МестонахождениеОС КАК МестонахождениеОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокОбъектовУчета_Доначисление_УУ КАК Доначисление
	|		ПО МестонахождениеОС.ОсновноеСредство = Доначисление.ОсновноеСредство
	|ГДЕ
	|	МестонахождениеОС.Активность
	|	И МестонахождениеОС.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
	|	И МестонахождениеОС.Период >= Доначисление.Период
	|	И НЕ МестонахождениеОС.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
	|	И НЕ МестонахождениеОС.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
	|	И МестонахождениеОС.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				СписокОС.ОсновноеСредство
	|			ИЗ
	|				СписокОбъектовУчета_Доначисление_УУ КАК СписокОС)
	|
	|	И МестонахождениеОС.Организация В
	|			(ВЫБРАТЬ
	|				ОрганизацииПоСтандартамМУ.Организация
	|			ИЗ
	|				ОрганизацииПоСтандартамМУ КАК ОрганизацииПоСтандартамМУ
	|			ГДЕ
	|				ОрганизацииПоСтандартамМУ.Период = &НачалоПредыдущегоМесяца)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ИзменениеСтоимости
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СтоимостьОС.НачалоМесяца КАК НачалоМесяца,
	|	СтоимостьОС.Период КАК Период,
	|	СтоимостьОС.Регистратор КАК Регистратор,
	|	СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СтоимостьОС.Организация КАК Организация,
	|	СУММА(СтоимостьОС.Стоимость) КАК Стоимость,
	|	СУММА(СтоимостьОС.СтоимостьРегл) КАК СтоимостьРегл
	|ПОМЕСТИТЬ СтоимостьОС_ИзмененияУУ
	|ИЗ
	|	(ВЫБРАТЬ
	|		&НачалоМесяца КАК НачалоМесяца,
	|		СтоимостьОС.Период КАК Период,
	|		СтоимостьОС.Регистратор КАК Регистратор,
	|		СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство,
	|		СтоимостьОС.Организация КАК Организация,
	|		СтоимостьОС.ПредварительнаяСтоимость КАК Стоимость,
	|		СтоимостьОС.ПредварительнаяСтоимостьРегл КАК СтоимостьРегл
	|	ИЗ
	|		РегистрНакопления.СтоимостьОС КАК СтоимостьОС
	|	ГДЕ
	|		СтоимостьОС.Активность
	|		И СтоимостьОС.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
	|		И СтоимостьОС.ОтражатьВУпрУчете
	|		И СтоимостьОС.Регистратор ССЫЛКА Документ.МодернизацияОС2_4
	|		И (СтоимостьОС.Организация, СтоимостьОС.ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					СписокОС.Организация,
	|					СписокОС.ОбъектУчета
	|				ИЗ
	|					ПакетыАмортизации КАК СписокОС)
	|
	|		И СтоимостьОС.Организация В
	|				(ВЫБРАТЬ
	|					ОрганизацииПоСтандартамМУ.Организация
	|				ИЗ
	|					ОрганизацииПоСтандартамМУ КАК ОрганизацииПоСтандартамМУ
	|				ГДЕ
	|					ОрганизацииПоСтандартамМУ.Период = &НачалоМесяца)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		&НачалоМесяца,
	|		СписаниеОС2_4.Дата,
	|		СписаниеОС2_4.Ссылка,
	|		ТаблицаОС.ОсновноеСредство,
	|		СписаниеОС2_4.Организация,
	|		-ТаблицаОС.СуммаСписанияУУ,
	|		-ТаблицаОС.СуммаСписанияБУ
	|	ИЗ
	|		Документ.СписаниеОС2_4 КАК СписаниеОС2_4
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС2_4.ОС КАК ТаблицаОС
	|			ПО (ТаблицаОС.Ссылка = СписаниеОС2_4.Ссылка)
	|	ГДЕ
	|		СписаниеОС2_4.Проведен
	|		И СписаниеОС2_4.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
	|		И СписаниеОС2_4.ОтражатьВУпрУчете
	|		И СписаниеОС2_4.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеОСЧастичное)
	|		И ТаблицаОС.СуммаСписанияУУ <> 0
	|		И (СписаниеОС2_4.Организация, ТаблицаОС.ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					СписокОС.Организация,
	|					СписокОС.ОбъектУчета
	|				ИЗ
	|					ПакетыАмортизации КАК СписокОС)
	|
	|		И СписаниеОС2_4.Организация В
	|				(ВЫБРАТЬ
	|					ОрганизацииПоСтандартамМУ.Организация
	|				ИЗ
	|					ОрганизацииПоСтандартамМУ КАК ОрганизацииПоСтандартамМУ
	|				ГДЕ
	|					ОрганизацииПоСтандартамМУ.Период = &НачалоМесяца)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		&НачалоМесяца,
	|		ДанныеДокумента.Дата,
	|		ДанныеДокумента.Ссылка,
	|		ДанныеДокумента.ОсновноеСредство,
	|		ДанныеДокумента.Организация,
	|		-СУММА(ТаблицаОС.СтоимостьУУ),
	|		-СУММА(ТаблицаОС.СтоимостьБУ)
	|	ИЗ
	|		Документ.РазукомплектацияОС КАК ДанныеДокумента
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|			ПО (ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка)
	|	ГДЕ
	|		ДанныеДокумента.Проведен
	|		И ДанныеДокумента.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
	|		И ДанныеДокумента.ОтражатьВУпрУчете
	|		И ДанныеДокумента.ХозяйственнаяОперация В(ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСЧастичная), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыделениеУзловКомпонентовАмортизации))
	|
	|		И (ДанныеДокумента.Организация, ТаблицаОС.ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					СписокОС.Организация,
	|					СписокОС.ОбъектУчета
	|				ИЗ
	|					ПакетыАмортизации КАК СписокОС)
	|
	|		И ДанныеДокумента.Организация В
	|				(ВЫБРАТЬ
	|					ОрганизацииПоСтандартамМУ.Организация
	|				ИЗ
	|					ОрганизацииПоСтандартамМУ КАК ОрганизацииПоСтандартамМУ
	|				ГДЕ
	|					ОрганизацииПоСтандартамМУ.Период = &НачалоМесяца)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДанныеДокумента.Дата,
	|		ДанныеДокумента.Ссылка,
	|		ДанныеДокумента.ОсновноеСредство,
	|		ДанныеДокумента.Организация
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		&НачалоМесяца,
	|		ПереоценкаОС.Дата,
	|		ПереоценкаОС.Ссылка,
	|		ТаблицаОС.ОсновноеСредство,
	|		ПереоценкаОС.Организация,
	|		ТаблицаОС.СтоимостьУУ,
	|		ТаблицаОС.СтоимостьБУ
	|	ИЗ
	|		Документ.ПереоценкаОС2_4 КАК ПереоценкаОС
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПереоценкаОС2_4.ОС КАК ТаблицаОС
	|			ПО (ТаблицаОС.Ссылка = ПереоценкаОС.Ссылка)
	|	ГДЕ
	|		ПереоценкаОС.Проведен
	|		И ПереоценкаОС.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
	|		И ПереоценкаОС.ОтражатьВУпрУчете
	|		И ТаблицаОС.СтоимостьУУ <> 0
	|		И (ПереоценкаОС.Организация, ТаблицаОС.ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					СписокОС.Организация,
	|					СписокОС.ОбъектУчета
	|				ИЗ
	|					ПакетыАмортизации КАК СписокОС)
	|
	|		И ПереоценкаОС.Организация В
	|				(ВЫБРАТЬ
	|					ОрганизацииПоСтандартамМУ.Организация
	|				ИЗ
	|					ОрганизацииПоСтандартамМУ КАК ОрганизацииПоСтандартамМУ
	|				ГДЕ
	|					ОрганизацииПоСтандартамМУ.Период = &НачалоМесяца)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		&НачалоМесяца,
	|		Корректировка.Дата,
	|		Корректировка.Ссылка,
	|		ТаблицаОС.ОсновноеСредство,
	|		Корректировка.Организация,
	|		ТаблицаОС.СтоимостьУУ,
	|		ТаблицаОС.СтоимостьБУ
	|	ИЗ
	|		Документ.КорректировкаСтоимостиИАмортизацииОС КАК Корректировка
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаСтоимостиИАмортизацииОС.ОС КАК ТаблицаОС
	|			ПО (ТаблицаОС.Ссылка = Корректировка.Ссылка)
	|	ГДЕ
	|		Корректировка.Проведен
	|		И Корректировка.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
	|		И Корректировка.ОтражатьВУпрУчете
	|		И ТаблицаОС.СтоимостьУУ <> 0
	|		И (Корректировка.Организация, ТаблицаОС.ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					СписокОС.Организация,
	|					СписокОС.ОбъектУчета
	|				ИЗ
	|					ПакетыАмортизации КАК СписокОС)
	|
	|		И Корректировка.Организация В
	|				(ВЫБРАТЬ
	|					ОрганизацииПоСтандартамМУ.Организация
	|				ИЗ
	|					ОрганизацииПоСтандартамМУ КАК ОрганизацииПоСтандартамМУ
	|				ГДЕ
	|					ОрганизацииПоСтандартамМУ.Период = &НачалоМесяца)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		&НачалоПредыдущегоМесяца,
	|		СтоимостьОС.Период,
	|		СтоимостьОС.Регистратор,
	|		СтоимостьОС.ОсновноеСредство,
	|		СтоимостьОС.Организация,
	|		СтоимостьОС.Стоимость - СтоимостьОС.ПредварительнаяСтоимость,
	|		СтоимостьОС.СтоимостьРегл - СтоимостьОС.ПредварительнаяСтоимостьРегл
	|	ИЗ
	|		РегистрНакопления.СтоимостьОС КАК СтоимостьОС
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокОбъектовУчета_Доначисление_УУ КАК Доначисление
	|			ПО СтоимостьОС.ОсновноеСредство = Доначисление.ОсновноеСредство
	|	ГДЕ
	|		СтоимостьОС.Активность
	|		И СтоимостьОС.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
	|		И СтоимостьОС.Период >= Доначисление.Период
	|		И СтоимостьОС.Регистратор ССЫЛКА Документ.МодернизацияОС2_4
	|		И (СтоимостьОС.Организация, СтоимостьОС.ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					СписокОС.Организация,
	|					СписокОС.ОсновноеСредство
	|				ИЗ
	|					СписокОбъектовУчета_Доначисление_УУ КАК СписокОС)
	|
	|		И СтоимостьОС.Организация В
	|				(ВЫБРАТЬ
	|					ОрганизацииПоСтандартамМУ.Организация
	|				ИЗ
	|					ОрганизацииПоСтандартамМУ КАК ОрганизацииПоСтандартамМУ
	|				ГДЕ
	|					ОрганизацииПоСтандартамМУ.Период = &НачалоПредыдущегоМесяца)
	|
	|	) КАК СтоимостьОС
	|
	|СГРУППИРОВАТЬ ПО
	|	СтоимостьОС.НачалоМесяца,
	|	СтоимостьОС.Период,
	|	СтоимостьОС.Регистратор,
	|	СтоимостьОС.ОсновноеСредство,
	|	СтоимостьОС.Организация";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ИзменениеАмортизации
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&НачалоМесяца КАК НачалоМесяца,
	|	Корректировка.Дата КАК Период,
	|	Корректировка.Ссылка КАК Регистратор,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	Корректировка.Организация КАК Организация,
	|	ТаблицаОС.АмортизацияУУ КАК Амортизация,
	|	ТаблицаОС.АмортизацияБУ КАК АмортизацияРегл
	|ПОМЕСТИТЬ АмортизацияОС_ИзмененияУУ
	|ИЗ
	|	Документ.КорректировкаСтоимостиИАмортизацииОС КАК Корректировка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаСтоимостиИАмортизацииОС.ОС КАК ТаблицаОС
	|		ПО (ТаблицаОС.Ссылка = Корректировка.Ссылка)
	|ГДЕ
	|	Корректировка.Проведен
	|	И Корректировка.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
	|	И Корректировка.ОтражатьВУпрУчете
	|	И ТаблицаОС.АмортизацияУУ <> 0
	|	И (Корректировка.Организация, ТаблицаОС.ОсновноеСредство) В
	|			(ВЫБРАТЬ
	|				СписокОС.Организация,
	|				СписокОС.ОбъектУчета
	|			ИЗ
	|				ПакетыАмортизации КАК СписокОС)
	|
	|	И Корректировка.Организация В
	|			(ВЫБРАТЬ
	|				ОрганизацииПоСтандартамМУ.Организация
	|			ИЗ
	|				ОрганизацииПоСтандартамМУ КАК ОрганизацииПоСтандартамМУ
	|			ГДЕ
	|				ОрганизацииПоСтандартамМУ.Период = &НачалоМесяца)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ОбъединениеВсехИзменений
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.НачалоМесяца КАК НачалоМесяца,
	|	ТаблицаИзменений.Регистратор,
	|	МАКСИМУМ(ТаблицаИзменений.Период) КАК Период,
	|	МАКСИМУМ(НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ТаблицаИзменений.Период, МЕСЯЦ, 1), МЕСЯЦ)) КАК НачалоСледующегоМесяца,
	|	ТаблицаИзменений.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаИзменений.Организация КАК Организация,
	|	МАКСИМУМ(ПараметрыАмортизацииОСУУПоследнее.Период) КАК ПараметрыАмортизацииОСУУПоследнееПериод,
	|	МАКСИМУМ(ПорядокУчетаОСИзмененияПоследнее.Период) КАК ПорядокУчетаОСИзмененияПоследнееПериод,
	|	МАКСИМУМ(ПорядокУчетаОСУУИзмененияПоследнее.Период) КАК ПорядокУчетаОСУУИзмененияПоследнееПериод,
	|	МАКСИМУМ(МестонахождениеОСИзмененияПоследнее.Период) КАК МестонахождениеОСИзмененияПоследнееПериод
	|ПОМЕСТИТЬ ТаблицаИзмененийУУ
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаИзменений.НачалоМесяца КАК НачалоМесяца,
	|		ТаблицаИзменений.Регистратор КАК Регистратор,
	|		ТаблицаИзменений.Период КАК Период,
	|		ТаблицаИзменений.ОсновноеСредство КАК ОсновноеСредство,
	|		ТаблицаИзменений.Организация КАК Организация
	|	ИЗ
	|		ПараметрыАмортизацииОСУУ_Изменения КАК ТаблицаИзменений
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаИзменений.НачалоМесяца,
	|		ТаблицаИзменений.Регистратор,
	|		ТаблицаИзменений.Период,
	|		ТаблицаИзменений.ОсновноеСредство,
	|		ТаблицаИзменений.Организация
	|	ИЗ
	|		ПорядокУчетаОС_ИзмененияУУ КАК ТаблицаИзменений
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаИзменений.НачалоМесяца,
	|		ТаблицаИзменений.Регистратор,
	|		ТаблицаИзменений.Период,
	|		ТаблицаИзменений.ОсновноеСредство,
	|		ТаблицаИзменений.Организация
	|	ИЗ
	|		ПорядокУчетаОСУУ_Изменения КАК ТаблицаИзменений
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаИзменений.НачалоМесяца,
	|		ТаблицаИзменений.Регистратор,
	|		ТаблицаИзменений.Период,
	|		ТаблицаИзменений.ОсновноеСредство,
	|		ТаблицаИзменений.Организация
	|	ИЗ
	|		МестонахождениеОС_ИзмененияУУ КАК ТаблицаИзменений
	|
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаИзменений.НачалоМесяца,
	|		ТаблицаИзменений.Регистратор,
	|		ТаблицаИзменений.Период,
	|		ТаблицаИзменений.ОсновноеСредство,
	|		ТаблицаИзменений.Организация
	|	ИЗ
	|		СтоимостьОС_ИзмененияУУ КАК ТаблицаИзменений
	|
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаИзменений.НачалоМесяца,
	|		ТаблицаИзменений.Регистратор,
	|		ТаблицаИзменений.Период,
	|		ТаблицаИзменений.ОсновноеСредство,
	|		ТаблицаИзменений.Организация
	|	ИЗ
	|		АмортизацияОС_ИзмененияУУ КАК ТаблицаИзменений
	|
	|	) КАК ТаблицаИзменений
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСУУ_Изменения КАК ПараметрыАмортизацииОСУУПоследнее
	|		ПО (ПараметрыАмортизацииОСУУПоследнее.Организация = ТаблицаИзменений.Организация)
	|			И (ПараметрыАмортизацииОСУУПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
	|			И (ПараметрыАмортизацииОСУУПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
	|			И (ПараметрыАмортизацииОСУУПоследнее.Период <= ТаблицаИзменений.Период)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОС_ИзмененияУУ КАК ПорядокУчетаОСИзмененияПоследнее
	|		ПО (ПорядокУчетаОСИзмененияПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
	|			И (ПорядокУчетаОСИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
	|			И (ПорядокУчетаОСИзмененияПоследнее.Период <= ТаблицаИзменений.Период)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСУУ_Изменения КАК ПорядокУчетаОСУУИзмененияПоследнее
	|		ПО (ПорядокУчетаОСУУИзмененияПоследнее.Организация = ТаблицаИзменений.Организация)
	|			И (ПорядокУчетаОСУУИзмененияПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
	|			И (ПорядокУчетаОСУУИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
	|			И (ПорядокУчетаОСУУИзмененияПоследнее.Период <= ТаблицаИзменений.Период)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОС_ИзмененияУУ КАК МестонахождениеОСИзмененияПоследнее
	|		ПО (МестонахождениеОСИзмененияПоследнее.Организация = ТаблицаИзменений.Организация)
	|			И (МестонахождениеОСИзмененияПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
	|			И (МестонахождениеОСИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
	|			И (МестонахождениеОСИзмененияПоследнее.Период <= ТаблицаИзменений.Период)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.НачалоМесяца,
	|	ТаблицаИзменений.Регистратор,
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.ОсновноеСредство,
	|	ТаблицаИзменений.НачалоМесяца,
	|	ТаблицаИзменений.Регистратор,
	|	МИНИМУМ(ТаблицаИзмененийСледующее.Период) КАК ПериодСледующее
	|ПОМЕСТИТЬ ТаблицаИзмененийСледующее
	|ИЗ
	|	ТаблицаИзмененийУУ КАК ТаблицаИзменений
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаИзмененийУУ КАК ТаблицаИзмененийСледующее
	|		ПО (ТаблицаИзмененийСледующее.Организация = ТаблицаИзменений.Организация)
	|			И (ТаблицаИзмененийСледующее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
	|			И (ТаблицаИзмененийСледующее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
	|			И (ТаблицаИзмененийСледующее.Период > ТаблицаИзменений.Период
	|				ИЛИ ТаблицаИзмененийСледующее.Период = ТаблицаИзменений.Период
	|						И ТаблицаИзмененийСледующее.Регистратор > ТаблицаИзменений.Регистратор)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.НачалоМесяца,
	|	ТаблицаИзменений.Регистратор,
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.ОсновноеСредство,
	|	МИНИМУМ(ТаблицаИзменений.Период) КАК ПериодСледующее
	|ПОМЕСТИТЬ ТаблицаИзмененийВЭтомМесяце
	|ИЗ
	|	ТаблицаИзмененийУУ КАК ТаблицаИзменений
	|ГДЕ
	|	(ТаблицаИзменений.НачалоМесяца = &НачалоМесяца)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.ОсновноеСредство";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область Изменения_УУ
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.НачалоМесяца КАК НачалоМесяца,
	|	КОНЕЦПЕРИОДА(ТаблицаИзменений.НачалоМесяца, МЕСЯЦ) КАК КонецМесяца,
	|	КОНЕЦПЕРИОДА(ТаблицаИзменений.Период, ДЕНЬ) КАК ДатаИзменений,
	|	ТаблицаИзменений.Регистратор КАК Регистратор,
	|
	|	(НЕ ОбъединениеОС.Ссылка ЕСТЬ NULL
	|		ИЛИ НЕ РазукомплектацияОС.Ссылка ЕСТЬ NULL) КАК ПолученПриРазукомплектацииОбъединении,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаИзменений.НачалоМесяца = &НачалоПредыдущегоМесяца
	|				И НАЧАЛОПЕРИОДА(ЕСТЬNULL(ТаблицаИзмененийВЭтомМесяце.ПериодСледующее, &КонецМесяца), ДЕНЬ) = &НачалоМесяца
	|			ТОГДА &НачалоМесяца 
	|		КОГДА ТаблицаИзменений.НачалоМесяца = &НачалоПредыдущегоМесяца
	|			ТОГДА ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ТаблицаИзмененийВЭтомМесяце.ПериодСледующее, ДЕНЬ, -1), &КонецМесяца) 
	|		ИНАЧЕ ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ТаблицаИзмененийСледующее.ПериодСледующее, ДЕНЬ, -1), &КонецМесяца) 
	|	КОНЕЦ КАК Период,
	|
	|	РАЗНОСТЬДАТ(ТаблицаИзменений.Период, ЕСТЬNULL(ТаблицаИзмененийСледующее.ПериодСледующее, ТаблицаИзменений.НачалоСледующегоМесяца), ДЕНЬ) КАК СрокДействия,
	|	ТаблицаИзменений.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаИзменений.Организация КАК Организация,
	|	ЕСТЬNULL(СтоимостьОСИзменения.Стоимость, 0) КАК СтоимостьИзменение, // Изменение стоимости документом
	|	ЕСТЬNULL(СтоимостьОСИзменения.СтоимостьРегл, 0) КАК СтоимостьРеглИзменение, // Изменение стоимости документом
	|	ЕСТЬNULL(АмортизацияОСИзменения.Амортизация, 0) КАК АмортизацияИзменение, // Изменение амортизации документом
	|	ЕСТЬNULL(АмортизацияОСИзменения.АмортизацияРегл, 0) КАК АмортизацияРеглИзменение, // Изменение амортизации документом
	|	ПараметрыАмортизацииОСУУПоследнее.ДатаПоследнегоИзменения,
	|	ПараметрыАмортизацииОСУУПоследнее.СрокИспользования,
	|	ПараметрыАмортизацииОСУУПоследнее.ОбъемНаработки,
	|	ПараметрыАмортизацииОСУУПоследнее.КоэффициентУскорения,
	|	ПараметрыАмортизацииОСУУПоследнее.МетодНачисленияАмортизации,
	|	ПараметрыАмортизацииОСУУПоследнее.ЛиквидационнаяСтоимость,
	|	ПараметрыАмортизацииОСУУПоследнее.ЛиквидационнаяСтоимостьРегл,
	|	ПорядокУчетаОСИзмененияПоследнее.ГруппаФинансовогоУчета,
	|	ПорядокУчетаОСИзмененияПоследнее.НаправлениеДеятельности,
	|	ПорядокУчетаОСИзмененияПоследнее.ПоказательНаработки,
	|	ПорядокУчетаОСУУИзмененияПоследнее.Состояние,
	|	ПорядокУчетаОСУУИзмененияПоследнее.НачислятьАмортизациюУУ,
	|
	|	ВЫБОР
	|		КОГДА ТаблицаИзменений.ОсновноеСредство.ТипОС = ЗНАЧЕНИЕ(Перечисление.ТипыОС.ГрупповоеОС)
	|				И ЕСТЬNULL(ПорядокУчетаОСУУИзмененияПоследнее.СпособОтраженияРасходов, НЕОПРЕДЕЛЕНО) В (&ПустыеСпособыОтраженияРасходов)
	|			ТОГДА ТаблицаИзменений.ОсновноеСредство
	|		ИНАЧЕ ПорядокУчетаОСУУИзмененияПоследнее.СпособОтраженияРасходов
	|	КОНЕЦ КАК СпособОтраженияРасходов,
	|
	|	ПорядокУчетаОСУУИзмененияПоследнее.СтатьяРасходов,
	|	ПорядокУчетаОСУУИзмененияПоследнее.АналитикаРасходов,
	|	ПорядокУчетаОСУУИзмененияПоследнее.ПередаватьРасходыВДругуюОрганизацию,
	|	ПорядокУчетаОСУУИзмененияПоследнее.ОрганизацияПолучательРасходов,
	|	МестонахождениеОСИзмененияПоследнее.Местонахождение
	|
	|ПОМЕСТИТЬ Изменения_УУ
	|
	|ИЗ
	|	ТаблицаИзмененийУУ КАК ТаблицаИзменений
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаИзмененийСледующее КАК ТаблицаИзмененийСледующее
	|		ПО (ТаблицаИзмененийСледующее.Организация = ТаблицаИзменений.Организация)
	|			И (ТаблицаИзмененийСледующее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
	|			И (ТаблицаИзмененийСледующее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
	|			И (ТаблицаИзмененийСледующее.Регистратор = ТаблицаИзменений.Регистратор)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаИзмененийВЭтомМесяце КАК ТаблицаИзмененийВЭтомМесяце
	|		ПО (ТаблицаИзмененийВЭтомМесяце.Организация = ТаблицаИзменений.Организация)
	|			И (ТаблицаИзмененийВЭтомМесяце.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСУУ_Изменения КАК ПараметрыАмортизацииОСУУПоследнее
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСУУ_Изменения КАК ПараметрыАмортизацииОСУУСледующее
	|			ПО (ПараметрыАмортизацииОСУУСледующее.Организация = ПараметрыАмортизацииОСУУПоследнее.Организация)
	|				И (ПараметрыАмортизацииОСУУСледующее.ОсновноеСредство = ПараметрыАмортизацииОСУУПоследнее.ОсновноеСредство)
	|				И (ПараметрыАмортизацииОСУУСледующее.НачалоМесяца = ПараметрыАмортизацииОСУУПоследнее.НачалоМесяца)
	|				И (ПараметрыАмортизацииОСУУСледующее.Период = ПараметрыАмортизацииОСУУПоследнее.Период)
	|				И (ПараметрыАмортизацииОСУУСледующее.МоментВремени > ПараметрыАмортизацииОСУУПоследнее.МоментВремени)
	|		ПО (ПараметрыАмортизацииОСУУПоследнее.Организация = ТаблицаИзменений.Организация)
	|			И (ПараметрыАмортизацииОСУУПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
	|			И (ПараметрыАмортизацииОСУУПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
	|			И (ПараметрыАмортизацииОСУУПоследнее.Период = ТаблицаИзменений.ПараметрыАмортизацииОСУУПоследнееПериод)
	|			И (ПараметрыАмортизацииОСУУСледующее.Регистратор ЕСТЬ NULL)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОС_ИзмененияУУ КАК ПорядокУчетаОСИзмененияПоследнее
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОС_ИзмененияУУ КАК ПорядокУчетаОСИзмененияСледующее
	|			ПО (ПорядокУчетаОСИзмененияСледующее.Организация = ПорядокУчетаОСИзмененияПоследнее.Организация)
	|				И (ПорядокУчетаОСИзмененияСледующее.ОсновноеСредство = ПорядокУчетаОСИзмененияПоследнее.ОсновноеСредство)
	|				И (ПорядокУчетаОСИзмененияСледующее.НачалоМесяца = ПорядокУчетаОСИзмененияПоследнее.НачалоМесяца)
	|				И (ПорядокУчетаОСИзмененияСледующее.Период = ПорядокУчетаОСИзмененияПоследнее.Период)
	|				И (ПорядокУчетаОСИзмененияСледующее.МоментВремени > ПорядокУчетаОСИзмененияПоследнее.МоментВремени)
	|		ПО (ПорядокУчетаОСИзмененияПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
	|			И (ПорядокУчетаОСИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
	|			И (ПорядокУчетаОСИзмененияПоследнее.Период = ТаблицаИзменений.ПорядокУчетаОСИзмененияПоследнееПериод)
	|			И (ПорядокУчетаОСИзмененияСледующее.Регистратор ЕСТЬ NULL)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСУУ_Изменения КАК ПорядокУчетаОСУУИзмененияПоследнее
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСУУ_Изменения КАК ПорядокУчетаОСУУИзмененияСледующее
	|			ПО (ПорядокУчетаОСУУИзмененияСледующее.Организация = ПорядокУчетаОСУУИзмененияПоследнее.Организация)
	|				И (ПорядокУчетаОСУУИзмененияСледующее.ОсновноеСредство = ПорядокУчетаОСУУИзмененияПоследнее.ОсновноеСредство)
	|				И (ПорядокУчетаОСУУИзмененияСледующее.НачалоМесяца = ПорядокУчетаОСУУИзмененияПоследнее.НачалоМесяца)
	|				И (ПорядокУчетаОСУУИзмененияСледующее.Период = ПорядокУчетаОСУУИзмененияПоследнее.Период)
	|				И (ПорядокУчетаОСУУИзмененияСледующее.МоментВремени > ПорядокУчетаОСУУИзмененияПоследнее.МоментВремени)
	|		ПО (ПорядокУчетаОСУУИзмененияПоследнее.Организация = ТаблицаИзменений.Организация)
	|			И (ПорядокУчетаОСУУИзмененияПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
	|			И (ПорядокУчетаОСУУИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
	|			И (ПорядокУчетаОСУУИзмененияПоследнее.Период = ТаблицаИзменений.ПорядокУчетаОСУУИзмененияПоследнееПериод)
	|			И (ПорядокУчетаОСУУИзмененияСледующее.Регистратор ЕСТЬ NULL)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОС_ИзмененияУУ КАК МестонахождениеОСИзмененияПоследнее
	|			ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОС_ИзмененияУУ КАК МестонахождениеОСИзмененияСледующее
	|			ПО (МестонахождениеОСИзмененияСледующее.Организация = МестонахождениеОСИзмененияПоследнее.Организация)
	|				И (МестонахождениеОСИзмененияСледующее.ОсновноеСредство = МестонахождениеОСИзмененияПоследнее.ОсновноеСредство)
	|				И (МестонахождениеОСИзмененияСледующее.НачалоМесяца = МестонахождениеОСИзмененияПоследнее.НачалоМесяца)
	|				И (МестонахождениеОСИзмененияСледующее.Период = МестонахождениеОСИзмененияПоследнее.Период)
	|				И (МестонахождениеОСИзмененияСледующее.МоментВремени > МестонахождениеОСИзмененияПоследнее.МоментВремени)
	|		ПО (МестонахождениеОСИзмененияПоследнее.Организация = ТаблицаИзменений.Организация)
	|			И (МестонахождениеОСИзмененияПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
	|			И (МестонахождениеОСИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
	|			И (МестонахождениеОСИзмененияПоследнее.Период = ТаблицаИзменений.МестонахождениеОСИзмененияПоследнееПериод)
	|			И (МестонахождениеОСИзмененияСледующее.Регистратор ЕСТЬ NULL)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьОС_ИзмененияУУ КАК СтоимостьОСИзменения
	|		ПО (СтоимостьОСИзменения.Организация = ТаблицаИзменений.Организация)
	|			И (СтоимостьОСИзменения.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
	|			И (СтоимостьОСИзменения.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
	|			И (СтоимостьОСИзменения.Регистратор = ТаблицаИзменений.Регистратор)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ АмортизацияОС_ИзмененияУУ КАК АмортизацияОСИзменения
	|		ПО (АмортизацияОСИзменения.Организация = ТаблицаИзменений.Организация)
	|			И (АмортизацияОСИзменения.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
	|			И (АмортизацияОСИзменения.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
	|			И (АмортизацияОСИзменения.Регистратор = ТаблицаИзменений.Регистратор)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОбъединениеОС КАК ОбъединениеОС
	|		ПО (ОбъединениеОС.Ссылка = ТаблицаИзменений.Регистратор)
	|			И (ОбъединениеОС.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РазукомплектацияОС.ОС КАК РазукомплектацияОС
	|		ПО (РазукомплектацияОС.Ссылка = ТаблицаИзменений.Регистратор)
	|			И (РазукомплектацияОС.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#КонецОбласти
	
	#Область ПериодИзменения_УУ
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Изменения_УУ.НачалоМесяца           КАК НачалоМесяца,
	|	Изменения_УУ.ОсновноеСредство       КАК ОсновноеСредство,
	|	Изменения_УУ.Организация            КАК Организация,
	|	МИНИМУМ(Изменения_УУ.ДатаИзменений) КАК Период
	|ПОМЕСТИТЬ ПериодИзменения_УУ
	|ИЗ
	|	Изменения_УУ КАК Изменения_УУ
	|
	|СГРУППИРОВАТЬ ПО
	|	Изменения_УУ.НачалоМесяца,
	|	Изменения_УУ.ОсновноеСредство,
	|	Изменения_УУ.Организация";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область СписокОССПараметрами_УУ
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВЫБОР 
	|		КОГДА ПериодИзмененияВТекущемМесяце.Период ЕСТЬ NULL
	|			ТОГДА СписокОС.ПериодНачисления
	|		КОГДА НАЧАЛОПЕРИОДА(ПериодИзмененияВТекущемМесяце.Период, ДЕНЬ) = НАЧАЛОПЕРИОДА(ПериодИзмененияВТекущемМесяце.Период, МЕСЯЦ)
	|			ТОГДА ПериодИзмененияВТекущемМесяце.Период
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(ПериодИзмененияВТекущемМесяце.Период, ДЕНЬ, -1)
	|	КОНЕЦ КАК Период,
	|
	|	СписокОС.ЭтоДоначисление КАК ЭтоДоначисление,
	|	СписокОС.АмортизацияНачисляетсяПоСтандартамМУ КАК АмортизацияНачисляетсяПоСтандартамМУ,
	|	СписокОС.ВариантПроводокПоОбесценениюВНА КАК ВариантПроводокПоОбесценениюВНА,
	|
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОС.Организация КАК Организация,
	|
	|	ПорядокУчетаОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ПорядокУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ПорядокУчетаОС.ПоказательНаработки КАК ПоказательНаработки,
	|	ПорядокУчетаОС.ОбъемНаработки КАК ОбъемНаработки,
	|
	// РесурсНазначенный
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|				И СписокОС.АмортизацияНачисляетсяПоСтандартамМУ
	|			ТОГДА ЕСТЬNULL(ПорядокУчетаОС.ОбъемНаработки, 0)
	|		КОГДА ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|				И НЕ СписокОС.АмортизацияНачисляетсяПоСтандартамМУ
	|			ТОГДА ЕСТЬNULL(ПараметрыАмортизацииОСУУ.ОбъемПродукцииРаботДляВычисленияАмортизации, 0)
	|		ИНАЧЕ ЕСТЬNULL(ПараметрыАмортизацииОСУУ.СрокИспользованияДляВычисленияАмортизации, 0)
	|	КОНЕЦ КАК РесурсНазначенный,
	|
	// СтоимостьДляВычисленияАмортизации
	|	ВЫБОР 
	|		КОГДА НЕ СписокОС.АмортизацияНачисляетсяПоСтандартамМУ
	|			ТОГДА ПараметрыАмортизацииОСУУ.СтоимостьДляВычисленияАмортизации
	|		КОГДА НАЧАЛОПЕРИОДА(ДатыПоследнегоИзменения.Дата, МЕСЯЦ) = СписокОС.НачалоМесяца
	|				И ТИПЗНАЧЕНИЯ(ПараметрыАмортизацииОСУУ.Регистратор) <> ТИП(Документ.ВводОстатковВнеоборотныхАктивов2_4)
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьОС.Стоимость, 0)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьОС.Стоимость, 0) - ЕСТЬNULL(АмортизацияОС.Амортизация, 0)
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизации,
	|
	// СтоимостьДляВычисленияАмортизацииРегл
	|	ВЫБОР 
	|		КОГДА НЕ СписокОС.АмортизацияНачисляетсяПоСтандартамМУ
	|			ТОГДА ПараметрыАмортизацииОСУУ.СтоимостьДляВычисленияАмортизацииРегл
	|		КОГДА НАЧАЛОПЕРИОДА(ДатыПоследнегоИзменения.Дата, МЕСЯЦ) = СписокОС.НачалоМесяца
	|				И ТИПЗНАЧЕНИЯ(ПараметрыАмортизацииОСУУ.Регистратор) <> ТИП(Документ.ВводОстатковВнеоборотныхАктивов2_4)
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьОС.СтоимостьРегл, 0)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0)
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизацииРегл,
	|
	// СтоимостьНаНачалоГода
	|	ЕСТЬNULL(СтоимостьОСНаНачалоГода.Стоимость, 0)
	|		- ЕСТЬNULL(АмортизацияОСНаНачалоГода.Амортизация, 0) КАК СтоимостьНаНачалоГода,
	|
	// СтоимостьРеглНаНачалоГода
	|	ЕСТЬNULL(СтоимостьОСНаНачалоГода.СтоимостьРегл, 0)
	|		- ЕСТЬNULL(АмортизацияОСНаНачалоГода.АмортизацияРегл, 0) КАК СтоимостьРеглНаНачалоГода,
	|
	// РесурсВыработанныйЗаТекущийПериод
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|			ТОГДА ВЫРАЗИТЬ(ВЫБОР 
	|					КОГДА НЕ ПериодИзменения_УУ.Период ЕСТЬ NULL
	|						ТОГДА ВЫБОР 
	|								КОГДА НАЧАЛОПЕРИОДА(ДатыПоследнегоИзменения.Дата, МЕСЯЦ) = СписокОС.НачалоМесяца
	|									ТОГДА (РАЗНОСТЬДАТ(ДатыПоследнегоИзменения.Дата, ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ПериодИзменения_УУ.Период, ДЕНЬ, -1), СписокОС.КонецМесяца), ДЕНЬ) + 1)
	|									ИНАЧЕ (РАЗНОСТЬДАТ(СписокОС.НачалоМесяца, ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ПериодИзменения_УУ.Период, ДЕНЬ, -1), СписокОС.КонецМесяца), ДЕНЬ) + 1)
	|							КОНЕЦ / ДЕНЬ(СписокОС.КонецМесяца)
	|					ИНАЧЕ 1
	|				КОНЕЦ * ВЫБОР
	|							КОГДА СписокОС.ЭтоДоначисление
	|								ТОГДА ЕСТЬNULL(ВыработкаОС.ОбъемВыработкиЗаПрошлыйМесяц, 0)
	|							КОГДА НЕ СписокОС.АмортизацияНачисляетсяПоСтандартамМУ
	|									И НАЧАЛОПЕРИОДА(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ, МЕСЯЦ) = &НачалоПредыдущегоМесяца
	|								ТОГДА ЕСТЬNULL(ВыработкаОС.ОбъемВыработки, 0) + ЕСТЬNULL(ВыработкаОС.ОбъемВыработкиЗаПрошлыйМесяц, 0)
	|							ИНАЧЕ ЕСТЬNULL(ВыработкаОС.ОбъемВыработки, 0)
	|						КОНЕЦ КАК ЧИСЛО(20,8))
	|
			// Если расчет выполняется в месяце принятия к учету то ДеньПринятия/ВсегоДнейВМесяце
	|		КОГДА НАЧАЛОПЕРИОДА(ДатыПоследнегоИзменения.Дата, МЕСЯЦ) = СписокОС.НачалоМесяца
	|			ТОГДА ВЫРАЗИТЬ((РАЗНОСТЬДАТ(ДатыПоследнегоИзменения.Дата, ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ПериодИзменения_УУ.Период, ДЕНЬ, -1), СписокОС.КонецМесяца), ДЕНЬ) + 1)
	|					/ ДЕНЬ(СписокОС.КонецМесяца) КАК ЧИСЛО(20,8))
	|	
			// В остальных случаях КоличествоДнейДоПервогоИзмененияПараметров/ВсегоДнейВМесяце
	|		ИНАЧЕ ВЫРАЗИТЬ((РАЗНОСТЬДАТ(СписокОС.НачалоМесяца, ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ПериодИзменения_УУ.Период, ДЕНЬ, -1), СписокОС.КонецМесяца), ДЕНЬ) + 1)
	|					/ ДЕНЬ(СписокОС.КонецМесяца) КАК ЧИСЛО(20,8))
	|	КОНЕЦ КАК РесурсВыработанныйЗаТекущийПериод,
	|	  
	// РесурсВыработанныйЗаПрошлыйПериод
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|
	|			ТОГДА ВЫБОР
	|					КОГДА СписокОС.ЭтоДоначисление
	|						ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ВыработкаОС.ОбъемВыработкиНаНачалоПредыдущегоМесяца, 0) КАК ЧИСЛО(20,8))
	|					ИНАЧЕ ВЫРАЗИТЬ(ЕСТЬNULL(ВыработкаОС.ОбъемВыработкиНаНачалоМесяца, 0) КАК ЧИСЛО(20,8))
	|				КОНЕЦ
	|
	|		КОГДА НАЧАЛОПЕРИОДА(ДатыПоследнегоИзменения.Дата, МЕСЯЦ) = СписокОС.НачалоМесяца
	|				ИЛИ ДатыПоследнегоИзменения.Дата ЕСТЬ NULL
	|
	|			ТОГДА ВЫРАЗИТЬ(0 КАК ЧИСЛО(20,8))
	|
	|		КОГДА КОНЕЦПЕРИОДА(ДатыПоследнегоИзменения.Дата, МЕСЯЦ) = КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(СписокОС.НачалоМесяца, МЕСЯЦ, -1), МЕСЯЦ)
	|
	|			ТОГДА ВЫРАЗИТЬ((РАЗНОСТЬДАТ(ДатыПоследнегоИзменения.Дата, КОНЕЦПЕРИОДА(ДатыПоследнегоИзменения.Дата, МЕСЯЦ), ДЕНЬ) + 1) 
	|					/ ДЕНЬ(КОНЕЦПЕРИОДА(ДатыПоследнегоИзменения.Дата, МЕСЯЦ)) КАК ЧИСЛО(20,8))
	
			// Расшифровка формулы: [кол-во отработаннных дней в мес принятия] / [всего дней в мес принятия] + [кол-во месяцев после принятия до начала месяца]
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ((РАЗНОСТЬДАТ(ДатыПоследнегоИзменения.Дата, КОНЕЦПЕРИОДА(ДатыПоследнегоИзменения.Дата, МЕСЯЦ), ДЕНЬ) + 1) 
	|				/ ДЕНЬ(КОНЕЦПЕРИОДА(ДатыПоследнегоИзменения.Дата, МЕСЯЦ))
	|				+ РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ДатыПоследнегоИзменения.Дата, МЕСЯЦ, 1), МЕСЯЦ), СписокОС.КонецМесяца, МЕСЯЦ) КАК ЧИСЛО(20,8))
	|
	|	КОНЕЦ КАК РесурсВыработанныйЗаПрошлыйПериод,
	|
	// ОтработанныхМесяцевПолныхЛет
	|	ВЫБОР
	|		КОГДА ГОД(ДатыПоследнегоИзменения.Дата) <> ГОД(СписокОС.НачалоМесяца) ТОГДА
	|			13 - МЕСЯЦ(ДатыПоследнегоИзменения.Дата)
	|				+ 12 * (ГОД(СписокОС.НачалоМесяца) - Год(ДатыПоследнегоИзменения.Дата) - 1)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОтработанныхМесяцевПолныхЛет,
	|
	|	СписокОС.НачислятьАмортизацию,
	|	СписокОС.СтатьяРасходовУУ,
	|	СписокОС.АналитикаРасходовУУ,
	|
	|	ВЫБОР
	|		КОГДА СписокОС.ОсновноеСредство.ТипОС = ЗНАЧЕНИЕ(Перечисление.ТипыОС.ГрупповоеОС)
	|				И СписокОС.СпособОтраженияРасходовУУ В (&ПустыеСпособыОтраженияРасходов)
	|			ТОГДА СписокОС.ОсновноеСредство
	|		ИНАЧЕ СписокОС.СпособОтраженияРасходовУУ
	|	КОНЕЦ КАК СпособОтраженияРасходовУУ,
	|
	|	СписокОС.ПередаватьРасходыВДругуюОрганизациюУУ КАК ПередаватьРасходыВДругуюОрганизациюУУ,
	|	СписокОС.ОрганизацияПолучательРасходовУУ КАК ОрганизацияПолучательРасходовУУ,
	|
	// ОстаточнаяСтоимость
	|	ВЫБОР 
	|		КОГДА СписокОС.АмортизацияНачисляетсяПоСтандартамМУ
	|				И НАЧАЛОПЕРИОДА(ДатыПоследнегоИзменения.Дата, МЕСЯЦ) = СписокОС.НачалоМесяца
	|				И ТИПЗНАЧЕНИЯ(ПараметрыАмортизацииОСУУ.Регистратор) <> ТИП(Документ.ВводОстатковВнеоборотныхАктивов2_4)
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьОС.Стоимость, 0)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьОС.Стоимость, 0) - ЕСТЬNULL(АмортизацияОС.Амортизация, 0)
	|	КОНЕЦ КАК ОстаточнаяСтоимость,
	|
	// ОстаточнаяСтоимостьРегл
	|	ВЫБОР 
	|		КОГДА СписокОС.АмортизацияНачисляетсяПоСтандартамМУ
	|				И НАЧАЛОПЕРИОДА(ДатыПоследнегоИзменения.Дата, МЕСЯЦ) = СписокОС.НачалоМесяца
	|				И ТИПЗНАЧЕНИЯ(ПараметрыАмортизацииОСУУ.Регистратор) <> ТИП(Документ.ВводОстатковВнеоборотныхАктивов2_4)
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьОС.СтоимостьРегл, 0)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0)
	|	КОНЕЦ КАК ОстаточнаяСтоимостьРегл,
	|
	|	ЕСТЬNULL(ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимость, 0) КАК ЛиквидационнаяСтоимость,
	|	ЕСТЬNULL(ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимостьРегл, 0) КАК ЛиквидационнаяСтоимостьРегл,
	|
	// Стоимость
	|	ВЫБОР 
	|		КОГДА СписокОС.АмортизацияНачисляетсяПоСтандартамМУ
	|				И НАЧАЛОПЕРИОДА(ДатыПоследнегоИзменения.Дата, МЕСЯЦ) = СписокОС.НачалоМесяца
	|				И ТИПЗНАЧЕНИЯ(ПараметрыАмортизацииОСУУ.Регистратор) <> ТИП(Документ.ВводОстатковВнеоборотныхАктивов2_4)
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьОС.Стоимость, 0)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьОС.Стоимость, 0)
	|	КОНЕЦ КАК Стоимость,
	|
	// СтоимостьРегл
	|	ВЫБОР 
	|		КОГДА СписокОС.АмортизацияНачисляетсяПоСтандартамМУ
	|				И НАЧАЛОПЕРИОДА(ДатыПоследнегоИзменения.Дата, МЕСЯЦ) = СписокОС.НачалоМесяца
	|				И ТИПЗНАЧЕНИЯ(ПараметрыАмортизацииОСУУ.Регистратор) <> ТИП(Документ.ВводОстатковВнеоборотныхАктивов2_4)
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьОС.СтоимостьРегл, 0)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0)
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	|	ЕСТЬNULL(АмортизацияОС.Амортизация, 0)               КАК Амортизация,
	|	ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0)           КАК АмортизацияРегл,
	|
	|	ЕСТЬNULL(ОбесценениеНаНачалоМесяца.ОстатокОбесцененияРегл, 0) КАК ОстатокОбесцененияРегл,
	|	ЕСТЬNULL(ОбесценениеНаНачалоМесяца.ОстатокОбесцененияУпр, 0)  КАК ОстатокОбесцененияУпр,
	|
	|	МестонахождениеОС.Местонахождение                    КАК Местонахождение,
	|
	|	ПараметрыАмортизацииОСУУ.СрокИспользования           КАК СрокИспользованияУУ,
	|	ПараметрыАмортизацииОСУУ.КоэффициентУскорения        КАК КоэффициентУскорения,
	|	ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации  КАК МетодНачисленияАмортизации,
	|	ДатыПоследнегоИзменения.Дата                         КАК ДатаПоследнегоИзменения
	|
	|ПОМЕСТИТЬ СписокОССПараметрами_УУ
	|
	|ИЗ
	|	СписокОбъектовУчета_УУ КАК СписокОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПериодИзменения_УУ КАК ПериодИзменения_УУ
	|		ПО (ПериодИзменения_УУ.Организация = СписокОС.Организация)
	|			И (ПериодИзменения_УУ.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПериодИзменения_УУ.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПериодИзменения_УУ КАК ПериодИзмененияВТекущемМесяце
	|		ПО (ПериодИзмененияВТекущемМесяце.Организация = СписокОС.Организация)
	|			И (ПериодИзмененияВТекущемМесяце.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПериодИзмененияВТекущемМесяце.НачалоМесяца = &НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО (ПорядокУчетаОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (НЕ СписокОС.ЭтоДоначисление
	|					И СписокОС.Период = &КонецПредыдущегоМесяца
	|					И НЕ ПорядокУчетаОС.ДляДоначисления
	|					И ПорядокУчетаОС.НачалоМесяца = &НачалоМесяца
	|				ИЛИ НЕ СписокОС.ЭтоДоначисление
	|					И СписокОС.Период > &КонецПредыдущегоМесяца
	|					И ПорядокУчетаОС.Период = СписокОС.Период
	|				ИЛИ СписокОС.ЭтоДоначисление
	|					И СписокОС.Период <> &КонецПредыдущегоМесяца
	|					И ПорядокУчетаОС.Период = СписокОС.Период)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьОС_НаНачалоМесяца КАК СтоимостьОС
	|		ПО (СтоимостьОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (СтоимостьОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьОС_НаНачалоГода КАК СтоимостьОСНаНачалоГода
	|		ПО (СтоимостьОСНаНачалоГода.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ АмортизацияОС_НаНачалоГода КАК АмортизацияОСНаНачалоГода
	|		ПО (АмортизацияОСНаНачалоГода.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПредварительнаяСтоимостьОС КАК ПредварительнаяСтоимостьОС
	|		ПО (ПредварительнаяСтоимостьОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПредварительнаяСтоимостьОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ АмортизацияОС_НаНачалоМесяца КАК АмортизацияОС
	|		ПО (АмортизацияОС.Организация = СписокОС.Организация)
	|			И (АмортизацияОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (АмортизацияОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОбесценениеВНА КАК ОбесценениеНаНачалоМесяца
	|		ПО (ОбесценениеНаНачалоМесяца.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ОбесценениеНаНачалоМесяца.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОС КАК МестонахождениеОС
	|		ПО (МестонахождениеОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (НЕ СписокОС.ЭтоДоначисление
	|					И СписокОС.Период = &КонецПредыдущегоМесяца
	|					И НЕ МестонахождениеОС.ДляДоначисления
	|					И МестонахождениеОС.НачалоМесяца = &НачалоМесяца
	|				ИЛИ НЕ СписокОС.ЭтоДоначисление
	|					И СписокОС.Период > &КонецПредыдущегоМесяца
	|					И МестонахождениеОС.Период = СписокОС.Период
	|				ИЛИ СписокОС.ЭтоДоначисление
	|					И СписокОС.Период <> &КонецПредыдущегоМесяца
	|					И МестонахождениеОС.Период = СписокОС.Период)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО (ПервоначальныеСведенияОС.Организация = СписокОС.Организация)
	|			И (ПервоначальныеСведенияОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (НЕ СписокОС.ЭтоДоначисление
	|					И СписокОС.Период = &КонецПредыдущегоМесяца
	|					И НЕ ПервоначальныеСведенияОС.ДляДоначисления
	|					И ПервоначальныеСведенияОС.НачалоМесяца = &НачалоМесяца
	|				ИЛИ НЕ СписокОС.ЭтоДоначисление
	|					И СписокОС.Период > &КонецПредыдущегоМесяца
	|					И ПервоначальныеСведенияОС.Период = СписокОС.Период
	|				ИЛИ СписокОС.ЭтоДоначисление
	|					И СписокОС.Период <> &КонецПредыдущегоМесяца
	|					И ПервоначальныеСведенияОС.Период = СписокОС.Период)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
	|		ПО (ПараметрыАмортизацииОСУУ.Организация = СписокОС.Организация)
	|			И (ПараметрыАмортизацииОСУУ.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПараметрыАмортизацииОСУУ.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВыработкаОС КАК ВыработкаОС
	|		ПО (ВыработкаОС.ОсновноеСредство = ПорядокУчетаОС.ОсновноеСредство)
	|			И (ВыработкаОС.ПоказательНаработки = ПорядокУчетаОС.ПоказательНаработки)
	|			И (ВыработкаОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыПоследнегоИзмененияУУ КАК ДатыПоследнегоИзменения
	|		ПО (ДатыПоследнегоИзменения.Организация = СписокОС.Организация)
	|			И (ДатыПоследнегоИзменения.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ДатыПоследнегоИзменения.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|ГДЕ
	|	(НЕ СписокОС.ЭтоДоначисление
	|		ИЛИ СписокОС.НачалоМесяца = НАЧАЛОПЕРИОДА(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ, МЕСЯЦ))
	|	И СписокОС.НачислятьАмортизацию
	|	И НЕ СписокОС.ПолученПриРазукомплектацииОбъединении";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область СписокОССИзмененнымиПараметрами_УУ
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Изменения_УУ.Период КАК Период,
	|	Изменения_УУ.Регистратор КАК Регистратор,
	|	Изменения_УУ.ОсновноеСредство КАК ОсновноеСредство,
	|	Изменения_УУ.Организация КАК Организация,
	|	(Изменения_УУ.НачалоМесяца = &НачалоПредыдущегоМесяца) КАК ЭтоДоначисление,
	|
	|	СписокОС.ВариантПроводокПоОбесценениюВНА КАК ВариантПроводокПоОбесценениюВНА,
	|
	|	Изменения_УУ.ПолученПриРазукомплектацииОбъединении КАК ПолученПриРазукомплектацииОбъединении, 
	|
	// Стоимость
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ, МЕСЯЦ) = Изменения_УУ.НачалоМесяца
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьОС.Стоимость, 0)
	|		КОГДА Изменения_УУ.НачалоМесяца = &НачалоПредыдущегоМесяца
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьОС.Стоимость, 0)
	|	КОНЕЦ КАК Стоимость,
	|
	// СтоимостьРегл
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ, МЕСЯЦ) = Изменения_УУ.НачалоМесяца
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьОС.СтоимостьРегл, 0)
	|		КОГДА Изменения_УУ.НачалоМесяца = &НачалоПредыдущегоМесяца
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0)
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	|	ЕСТЬNULL(АмортизацияОС.Амортизация, 0) КАК Амортизация,
	|	ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0) КАК АмортизацияРегл,
	|
	|	Изменения_УУ.СтоимостьИзменение КАК СтоимостьИзменение,
	|	Изменения_УУ.СтоимостьРеглИзменение КАК СтоимостьРеглИзменение,
	|	Изменения_УУ.АмортизацияИзменение КАК АмортизацияИзменение,
	|	Изменения_УУ.АмортизацияРеглИзменение КАК АмортизацияРеглИзменение,
	|
	|	ВЫБОР
	|		КОГДА Изменения_УУ.НачалоМесяца = &НачалоПредыдущегоМесяца
	|			ТОГДА ПорядокУчетаОСНаНачалоМесяца.ГруппаФинансовогоУчета
	|		ИНАЧЕ ЕСТЬNULL(Изменения_УУ.ГруппаФинансовогоУчета, ПорядокУчетаОС.ГруппаФинансовогоУчета)
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|
	|	ЕСТЬNULL(Изменения_УУ.НаправлениеДеятельности, ПорядокУчетаОС.НаправлениеДеятельности) КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(Изменения_УУ.ПоказательНаработки,     ПорядокУчетаОС.ПоказательНаработки)     КАК ПоказательНаработки,
	|
	// РесурсНазначенный
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(Изменения_УУ.МетодНачисленияАмортизации, ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации) = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|			ТОГДА ЕСТЬNULL(Изменения_УУ.ОбъемНаработки, ПорядокУчетаОС.ОбъемНаработки)
	|		ИНАЧЕ ЕСТЬNULL(Изменения_УУ.СрокИспользования, ПараметрыАмортизацииОСУУ.СрокИспользованияДляВычисленияАмортизации)
	|	КОНЕЦ КАК РесурсНазначенный,
	|
	// РесурсВыработанныйЗаТекущийПериод
	|	ВЫРАЗИТЬ(ВЫБОР 
	|		КОГДА ЕСТЬNULL(Изменения_УУ.МетодНачисленияАмортизации, ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации) = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|			ТОГДА ВЫБОР
	|					КОГДА Изменения_УУ.НачалоМесяца = &НачалоПредыдущегоМесяца
	|						ТОГДА ЕСТЬNULL(ВыработкаОС.ОбъемВыработкиЗаПрошлыйМесяц, 0)
	|					КОГДА НЕ СписокОС.АмортизацияНачисляетсяПоСтандартамМУ
	|							И НАЧАЛОПЕРИОДА(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ, МЕСЯЦ) = &НачалоПредыдущегоМесяца
	|						ТОГДА ЕСТЬNULL(ВыработкаОС.ОбъемВыработки, 0) + ЕСТЬNULL(ВыработкаОС.ОбъемВыработкиЗаПрошлыйМесяц, 0)
	|					ИНАЧЕ ЕСТЬNULL(ВыработкаОС.ОбъемВыработки, 0)
	|				КОНЕЦ
	|		ИНАЧЕ 1
	|	КОНЕЦ * Изменения_УУ.СрокДействия / ДЕНЬ(Изменения_УУ.КонецМесяца) КАК ЧИСЛО(20,8)) КАК РесурсВыработанныйЗаТекущийПериод,
	|	  
	// РесурсВыработанныйЗаПрошлыйПериод
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизацииОСУУ.ОсновноеСредство ЕСТЬ NULL
	|			ТОГДА ВЫРАЗИТЬ(0 КАК ЧИСЛО(20,8))
	|
	|		КОГДА ЕСТЬNULL(Изменения_УУ.МетодНачисленияАмортизации, ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации) = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|			ТОГДА ВЫБОР
	|					КОГДА Изменения_УУ.НачалоМесяца = &НачалоПредыдущегоМесяца
	|						ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(ВыработкаОС.ОбъемВыработкиНаНачалоПредыдущегоМесяца, 0) КАК ЧИСЛО(20,8))
	|					ИНАЧЕ ВЫРАЗИТЬ(ЕСТЬNULL(ВыработкаОС.ОбъемВыработкиНаНачалоМесяца, 0) КАК ЧИСЛО(20,8))
	|				КОНЕЦ
	|
	|		КОГДА НАЧАЛОПЕРИОДА(ЕСТЬNULL(Изменения_УУ.ДатаПоследнегоИзменения, ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения), МЕСЯЦ) = Изменения_УУ.НачалоМесяца
	|			ТОГДА ВЫРАЗИТЬ(0 КАК ЧИСЛО(20,8))
	|	
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ((РАЗНОСТЬДАТ(ЕСТЬNULL(Изменения_УУ.ДатаПоследнегоИзменения, ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения), КОНЕЦПЕРИОДА(ЕСТЬNULL(Изменения_УУ.ДатаПоследнегоИзменения, ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения), МЕСЯЦ), ДЕНЬ) + 1) / ДЕНЬ(КОНЕЦПЕРИОДА(ЕСТЬNULL(Изменения_УУ.ДатаПоследнегоИзменения, ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения), МЕСЯЦ))
	|			+ РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ЕСТЬNULL(Изменения_УУ.ДатаПоследнегоИзменения, ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения), МЕСЯЦ, 1), МЕСЯЦ), Изменения_УУ.КонецМесяца, МЕСЯЦ) КАК ЧИСЛО(20,8))
	|	КОНЕЦ КАК РесурсВыработанныйЗаПрошлыйПериод,
	|
	// ОтработанныхМесяцевПолныхЛет
	|	ВЫБОР
	|		КОГДА ГОД(ЕСТЬNULL(Изменения_УУ.ДатаПоследнегоИзменения, ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения)) <> ГОД(Изменения_УУ.НачалоМесяца) ТОГДА
	|			13 - МЕСЯЦ(ЕСТЬNULL(Изменения_УУ.ДатаПоследнегоИзменения, ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения))
	|				+ 12 * (ГОД(Изменения_УУ.НачалоМесяца) - Год(ЕСТЬNULL(Изменения_УУ.ДатаПоследнегоИзменения, ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения)) - 1)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОтработанныхМесяцевПолныхЛет,
	|
	|	ЕСТЬNULL(Изменения_УУ.НачислятьАмортизациюУУ, ЕСТЬNULL(СписокОС.НачислятьАмортизацию, ЛОЖЬ)) КАК НачислятьАмортизацию,
	|	ЕСТЬNULL(Изменения_УУ.СтатьяРасходов, СписокОС.СтатьяРасходовУУ) КАК СтатьяРасходовУУ,
	|	ЕСТЬNULL(Изменения_УУ.АналитикаРасходов, СписокОС.АналитикаРасходовУУ) КАК АналитикаРасходовУУ,
	|	ЕСТЬNULL(Изменения_УУ.СпособОтраженияРасходов, СписокОС.СпособОтраженияРасходовУУ) КАК СпособОтраженияРасходовУУ,
	|	ЕСТЬNULL(Изменения_УУ.ПередаватьРасходыВДругуюОрганизацию, ЕСТЬNULL(СписокОС.ПередаватьРасходыВДругуюОрганизациюУУ, ЛОЖЬ)) КАК ПередаватьРасходыВДругуюОрганизациюУУ,
	|	ЕСТЬNULL(Изменения_УУ.ОрганизацияПолучательРасходов, СписокОС.ОрганизацияПолучательРасходовУУ) КАК ОрганизацияПолучательРасходовУУ,
	|
	|	ЕСТЬNULL(Изменения_УУ.ЛиквидационнаяСтоимость, ЕСТЬNULL(ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимость, 0)) КАК ЛиквидационнаяСтоимость,
	|	ЕСТЬNULL(Изменения_УУ.ЛиквидационнаяСтоимостьРегл, ЕСТЬNULL(ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимостьРегл, 0)) КАК ЛиквидационнаяСтоимостьРегл,
	|
	|	ЕСТЬNULL(ОбесценениеНаНачалоМесяца.ОстатокОбесцененияРегл, 0) КАК ОстатокОбесцененияРегл,
	|	ЕСТЬNULL(ОбесценениеНаНачалоМесяца.ОстатокОбесцененияУпр, 0) КАК ОстатокОбесцененияУпр,
	|
	|	ВЫБОР
	|		КОГДА Изменения_УУ.НачалоМесяца = &НачалоПредыдущегоМесяца
	|			ТОГДА МестонахождениеНаНачалоМесяца.Местонахождение
	|		ИНАЧЕ ЕСТЬNULL(Изменения_УУ.Местонахождение, МестонахождениеОС.Местонахождение) 
	|	КОНЕЦ КАК Местонахождение,
	|
	|	ПараметрыАмортизацииОСУУ.СрокИспользования КАК СрокИспользованияУУ,
	|	ЕСТЬNULL(Изменения_УУ.КоэффициентУскорения, ПараметрыАмортизацииОСУУ.КоэффициентУскорения) КАК КоэффициентУскорения,
	|	ЕСТЬNULL(Изменения_УУ.МетодНачисленияАмортизации, ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации) КАК МетодНачисленияАмортизации
	|
	|ПОМЕСТИТЬ СписокОССИзмененнымиПараметрами_УУ
	|
	|ИЗ
	|	Изменения_УУ КАК Изменения_УУ
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ СписокОбъектовУчета_УУ КАК СписокОС
	|		ПО (СписокОС.Организация = Изменения_УУ.Организация)
	|			И (СписокОС.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (СписокОС.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО (ПорядокУчетаОС.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (ПорядокУчетаОС.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОС КАК ПорядокУчетаОСНаНачалоМесяца
	|		ПО (ПорядокУчетаОСНаНачалоМесяца.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (ПорядокУчетаОСНаНачалоМесяца.НачалоМесяца = &НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьОС_НаНачалоМесяца КАК СтоимостьОС
	|		ПО (СтоимостьОС.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (СтоимостьОС.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОбесценениеВНА КАК ОбесценениеНаНачалоМесяца
	|		ПО (ОбесценениеНаНачалоМесяца.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (ОбесценениеНаНачалоМесяца.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПредварительнаяСтоимостьОС КАК ПредварительнаяСтоимостьОС
	|		ПО (ПредварительнаяСтоимостьОС.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (ПредварительнаяСтоимостьОС.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ АмортизацияОС_НаНачалоМесяца КАК АмортизацияОС
	|		ПО (АмортизацияОС.Организация = Изменения_УУ.Организация)
	|			И (АмортизацияОС.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (АмортизацияОС.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОС КАК МестонахождениеОС
	|		ПО (МестонахождениеОС.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (МестонахождениеОС.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОС КАК МестонахождениеНаНачалоМесяца
	|		ПО (МестонахождениеНаНачалоМесяца.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (МестонахождениеНаНачалоМесяца.НачалоМесяца = &НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО (ПервоначальныеСведенияОС.Организация = Изменения_УУ.Организация)
	|			И (ПервоначальныеСведенияОС.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (ПервоначальныеСведенияОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
	|		ПО (ПараметрыАмортизацииОСУУ.Организация = Изменения_УУ.Организация)
	|			И (ПараметрыАмортизацииОСУУ.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (ПараметрыАмортизацииОСУУ.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВыработкаОС КАК ВыработкаОС
	|		ПО (ВыработкаОС.ОсновноеСредство = ПорядокУчетаОС.ОсновноеСредство)
	|			И (ВыработкаОС.ПоказательНаработки = ПорядокУчетаОС.ПоказательНаработки)
	|			И (ВыработкаОС.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЭтоДоначисление, 
	|	ОсновноеСредство";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область СпособыОтраженияРасходовУУ
	
	ТекстЗапроса = АмортизацияОСЛокализация.ТекстЗапросаСпособыОтраженияРасходовУУ();
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ 
		|	СписокВсехОС.СпособОтраженияРасходов КАК Ссылка
		|ПОМЕСТИТЬ втСпособыОтраженияРасходовУУ
		|ИЗ 
		|	СписокОбъектовУчета КАК СписокВсехОС
		|ГДЕ 
		|	НЕ СписокВсехОС.СпособОтраженияРасходов В (&ПустыеСпособыОтраженияРасходов)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ 
		|	СписокВсехОС.СпособОтраженияРасходов
		|ИЗ 
		|	Изменения_УУ КАК СписокВсехОС
		|ГДЕ 
		|	НЕ СписокВсехОС.СпособОтраженияРасходов В (&ПустыеСпособыОтраженияРасходов)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаАмортизация.Ссылка                               КАК СпособОтраженияРасходов,
		|	ТаблицаАмортизация.Подразделение                        КАК Подразделение,
		|	ТаблицаАмортизация.НаправлениеДеятельности              КАК НаправлениеДеятельности,
		|	ТаблицаАмортизация.СтатьяРасходов                       КАК СтатьяРасходов,
		|	ТаблицаАмортизация.АналитикаРасходов                    КАК АналитикаРасходов,
		|	ТаблицаАмортизация.ПередаватьРасходыВДругуюОрганизацию  КАК ПередаватьРасходыВДругуюОрганизацию,
		|	ТаблицаАмортизация.ОрганизацияПолучательРасходов        КАК ОрганизацияПолучательРасходов,
		|	ТаблицаАмортизация.Коэффициент                          КАК База,
		|	ТаблицаАмортизация.СуммаКоэффициентовПредыдущихСтрок    КАК БазаНакопленная,
		|	ТаблицаАмортизация.Ссылка.СуммаКоэффициентовАмортизации КАК СуммаБазы
		|
		|ПОМЕСТИТЬ СпособыОтраженияРасходовУУ
		|ИЗ
		|	Документ.ИзменениеПараметровОС2_4.Амортизация КАК ТаблицаАмортизация
		|ГДЕ
		|	ТаблицаАмортизация.Ссылка В (ВЫБРАТЬ СпособыОтраженияРасходов.Ссылка ИЗ втСпособыОтраженияРасходовУУ КАК СпособыОтраженияРасходов)
		|
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СпособОтраженияРасходов";
	КонецЕсли;
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	АмортизацияОСЛокализация.ДополнитьТекстЗапросаПараметрыДляРасчетаАмортизации(СписокЗапросов, ПараметрыРасчета);
	
КонецПроцедуры

Процедура ТекстЗапросаПроверкаПараметровРасчета(СписокЗапросов, ПараметрыРасчета)

	#Область ПроверкаПараметровРасчета_УУ
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.ОбъектУчета КАК ОбъектУчета,
	|	СписокОС.НеУказанСрокИспользованияУУ КАК НеУказанСрокИспользованияУУ,
	|	СписокОС.НеУказанОбъемНаработки КАК НеУказанОбъемНаработки,
	|	СписокОС.ОбъемНаработкиВыработан КАК ОбъемНаработкиВыработан
	|
	|ПОМЕСТИТЬ ПроверкаПараметровРасчета_УУ
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		СписокОС.Организация КАК Организация,
	|		СписокОС.ОсновноеСредство КАК ОбъектУчета,
	|
			// НеУказанСрокИспользованияУУ
	|		СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный)
	|			И СписокОС.РесурсНазначенный = 0 КАК НеУказанСрокИспользованияУУ,
	|
			// НеУказанОбъемНаработки
	|		СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|			И СписокОС.ОбъемНаработки = 0 КАК НеУказанОбъемНаработки,
	|
			// ОбъемНаработкиВыработан
	|		СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|			И (СписокОС.РесурсНазначенный - СписокОС.РесурсВыработанныйЗаПрошлыйПериод) <= 0
	|			И СписокОС.ОбъемНаработки <> 0 КАК ОбъемНаработкиВыработан
	|
	|	ИЗ
	|		СписокОССПараметрами_УУ КАК СписокОС
	|	ГДЕ
	|		СписокОС.НачислятьАмортизацию
	|
	|	) КАК СписокОС
	|
	|ГДЕ
	|	(СписокОС.НеУказанСрокИспользованияУУ
	|		ИЛИ СписокОС.НеУказанОбъемНаработки
	|		ИЛИ СписокОС.ОбъемНаработкиВыработан)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ПроверкаПараметровРасчетаИзменения_УУ
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.ОбъектУчета КАК ОбъектУчета,
	|	СписокОС.НеУказанСрокИспользованияУУ КАК НеУказанСрокИспользованияУУ,
	|	СписокОС.НеУказанОбъемНаработки КАК НеУказанОбъемНаработки,
	|	СписокОС.НеУказанРесурсВыработанный КАК НеУказанРесурсВыработанный
	|
	|ПОМЕСТИТЬ ПроверкаПараметровРасчетаИзменения_УУ
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		СписокОС.Организация КАК Организация,
	|		СписокОС.ОсновноеСредство КАК ОбъектУчета,
	|
			// НеУказанСрокИспользованияУУ
	|		СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный)
	|			И ЕСТЬNULL(СписокОС.РесурсНазначенный, 0) = 0 КАК НеУказанСрокИспользованияУУ,
	|
			// НеУказанОбъемНаработки
	|		СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|			И ЕСТЬNULL(СписокОС.РесурсНазначенный, 0) = 0 КАК НеУказанОбъемНаработки,
	|
			// НеУказанРесурсВыработанный
	|		СписокОС.РесурсВыработанныйЗаТекущийПериод ЕСТЬ NULL
	|			ИЛИ СписокОС.РесурсВыработанныйЗаПрошлыйПериод ЕСТЬ NULL КАК НеУказанРесурсВыработанный
	|
	|	ИЗ
	|		СписокОССИзмененнымиПараметрами_УУ КАК СписокОС
	|	ГДЕ
	|		СписокОС.НачислятьАмортизацию
	|	
	|	) КАК СписокОС
	|
	|ГДЕ 
	|	(СписокОС.НеУказанСрокИспользованияУУ
	|		ИЛИ СписокОС.НеУказанОбъемНаработки
	|		ИЛИ СписокОС.НеУказанРесурсВыработанный)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	АмортизацияОСЛокализация.ДополнитьТекстЗапросаПроверкаПараметровРасчета(ПараметрыРасчета, СписокЗапросов);
	
КонецПроцедуры

Процедура ТекстЗапросаДоначислениеАмортизацииБезИзмененийУУ(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокОС.Период КАК Период,
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.Местонахождение КАК Подразделение,
	|	СписокОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	СписокОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОС.СтатьяРасходовУУ КАК СтатьяРасходов,
	|	СписокОС.АналитикаРасходовУУ КАК АналитикаРасходов,
	|	СписокОС.СпособОтраженияРасходовУУ КАК СпособОтраженияРасходов,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизациюУУ КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокОС.ОрганизацияПолучательРасходовУУ КАК ОрганизацияПолучательРасходов,
	|	СписокОС.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|	СписокОС.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	СписокОС.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|	СписокОС.РесурсНазначенный КАК РесурсНазначенный,
	|	СписокОС.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
	|	СписокОС.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	СписокОС.ЛиквидационнаяСтоимостьРегл КАК ЛиквидационнаяСтоимостьРегл,
	|	СписокОС.КоэффициентУскорения КАК КоэффициентУскорения,
	|	СписокОС.ОтработанныхМесяцевПолныхЛет КАК ОтработанныхМесяцевПолныхЛет,
	|	СписокОС.НачислятьАмортизацию КАК НачислятьАмортизацию,
	|
	|	ВЫБОР
	|		КОГДА (СписокОС.ОстаточнаяСтоимость - (&СуммаАмортизацииУУ)) > СписокОС.ЛиквидационнаяСтоимость
	|			ТОГДА &СуммаАмортизацииУУ
	|		КОГДА (СписокОС.ОстаточнаяСтоимость - СписокОС.ЛиквидационнаяСтоимость) > 0
	|			ТОГДА СписокОС.ОстаточнаяСтоимость - СписокОС.ЛиквидационнаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУУ,
	|
	|	ВЫБОР
	|		КОГДА (СписокОС.ОстаточнаяСтоимостьРегл - (&СуммаАмортизацииБУ)) > СписокОС.ЛиквидационнаяСтоимостьРегл
	|			ТОГДА &СуммаАмортизацииБУ
	|		КОГДА (СписокОС.ОстаточнаяСтоимостьРегл - СписокОС.ЛиквидационнаяСтоимостьРегл) > 0
	|			ТОГДА СписокОС.ОстаточнаяСтоимостьРегл - СписокОС.ЛиквидационнаяСтоимостьРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБУ
	|
	|ПОМЕСТИТЬ ДоначислениеАмортизацииБезИзмененийУУ
	|
	|ИЗ
	|	СписокОССПараметрами_УУ КАК СписокОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПроверкаПараметровРасчета_УУ КАК ПроверкаПараметровРасчета
	|		ПО ПроверкаПараметровРасчета.ОбъектУчета = СписокОС.ОсновноеСредство
	|			И ПроверкаПараметровРасчета.Организация = СписокОС.Организация
	|ГДЕ
	|	СписокОС.ЭтоДоначисление
	|	И ПроверкаПараметровРасчета.ОбъектУчета ЕСТЬ NULL";
	
	ПараметрыТекстаЗапроса = ПараметрыТекстаЗапросаСуммаАмортизацииУУ();
	
	ПараметрыТекстаЗапроса.ТекстСтоимостьДляВычисленияАмортизации = "СписокОС.СтоимостьДляВычисленияАмортизации";
	ПараметрыТекстаЗапроса.ТекстОстаточнаяСтоимость = "СписокОС.ОстаточнаяСтоимость";
	ПараметрыТекстаЗапроса.ТекстЛиквидационнаяСтоимость = "0";
	ПараметрыТекстаЗапроса.ТекстСтоимостьНаНачалоГода = "СписокОС.СтоимостьНаНачалоГода";
	ПараметрыТекстаЗапроса.ТекстОбесценение = "0";
	ПараметрыТекстаЗапроса.ТекстСуммаДоначисления = "0";

	ТекстСуммаАмортизацииУУ = ТекстЗапросаСуммаАмортизацииУУ(ПараметрыТекстаЗапроса);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаАмортизацииУУ", ТекстСуммаАмортизацииУУ);
	
	//	
	ПараметрыТекстаЗапроса.ТекстСтоимостьДляВычисленияАмортизации = "СписокОС.СтоимостьДляВычисленияАмортизацииРегл";
	ПараметрыТекстаЗапроса.ТекстОстаточнаяСтоимость = "СписокОС.ОстаточнаяСтоимостьРегл";
	ПараметрыТекстаЗапроса.ТекстЛиквидационнаяСтоимость = "0";
	ПараметрыТекстаЗапроса.ТекстСтоимостьНаНачалоГода = "СписокОС.СтоимостьРеглНаНачалоГода";
	ПараметрыТекстаЗапроса.ТекстОбесценение = "0";
	ПараметрыТекстаЗапроса.ТекстСуммаДоначисления = "0";

	ТекстСуммаАмортизацииБУ = ТекстЗапросаСуммаАмортизацииУУ(ПараметрыТекстаЗапроса);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаАмортизацииБУ", ТекстСуммаАмортизацииБУ);
		
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаДоначислениеАмортизацииВыборкаИзмененийУУ(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	1 КАК Приоритет,
	|	&НачалоМесяца КАК Период,
	|	НЕОПРЕДЕЛЕНО КАК Регистратор,
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.Подразделение КАК Подразделение,
	|	СписокОС.ГруппаФинансовогоУчета,
	|	СписокОС.НаправлениеДеятельности,
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОС.СтатьяРасходов КАК СтатьяРасходов,
	|	СписокОС.АналитикаРасходов КАК АналитикаРасходов,
	|	СписокОС.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	СписокОС.СуммаУУ КАК СуммаУУ,
	|	СписокОС.СуммаБУ КАК СуммаБУ,
	|	0 КАК ОбесценениеУУ,
	|	0 КАК ОбесценениеБУ,
	|	1 КАК Коэффициент,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокОС.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	СписокОС.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод,
	|	СписокОС.РесурсНазначенный,
	|	СписокОС.МетодНачисленияАмортизации,
	|	СписокОС.ЛиквидационнаяСтоимость,
	|	СписокОС.ЛиквидационнаяСтоимостьРегл,
	|	0 КАК ОстатокОбесцененияРегл,
	|	0 КАК ОстатокОбесцененияУпр,
	|	СписокОС.КоэффициентУскорения,
	|	СписокОС.ОтработанныхМесяцевПолныхЛет,
	|	СписокОС.НачислятьАмортизацию,
	|	ЛОЖЬ КАК ПолученПриРазукомплектацииОбъединении, 
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьРегл,
	|	0 КАК СтоимостьИзменение,
	|	0 КАК СтоимостьРеглИзменение,
	|	0 КАК АмортизацияИзменение,
	|	0 КАК АмортизацияРеглИзменение,
	|	0 КАК Амортизация,
	|	0 КАК АмортизацияРегл,
	|	0 КАК СтоимостьДляВычисленияАмортизации,
	|	0 КАК СтоимостьДляВычисленияАмортизацииРегл,
	|	ЛОЖЬ КАК ЭтоИзменения
	|ИЗ
	|	ДоначислениеАмортизацииБезИзмененийУУ КАК СписокОС
	|ГДЕ
	|	СписокОС.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				СписокОС.ОсновноеСредство
	|			ИЗ
	|				СписокОССИзмененнымиПараметрами_УУ КАК СписокОС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1 КАК Приоритет,
	|	СписокОС.Период,
	|	СписокОС.Регистратор,
	|	СписокОС.Организация,
	|	СписокОС.Местонахождение,
	|	СписокОС.ГруппаФинансовогоУчета,
	|	СписокОС.НаправлениеДеятельности,
	|	СписокОС.ОсновноеСредство,
	|	СписокОС.СтатьяРасходовУУ,
	|	СписокОС.АналитикаРасходовУУ,
	|	СписокОС.СпособОтраженияРасходовУУ,
	|	0,
	|	0,
	|	0,
	|	0,
	|	1,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизациюУУ,
	|	СписокОС.ОрганизацияПолучательРасходовУУ,
	|	СписокОС.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод,
	|	СписокОС.РесурсНазначенный,
	|	СписокОС.МетодНачисленияАмортизации,
	|	СписокОС.ЛиквидационнаяСтоимость,
	|	СписокОС.ЛиквидационнаяСтоимостьРегл,
	|	0 КАК ОстатокОбесцененияРегл,
	|	0 КАК ОстатокОбесцененияУпр,
	|	СписокОС.КоэффициентУскорения,
	|	СписокОС.ОтработанныхМесяцевПолныхЛет,
	|	СписокОС.НачислятьАмортизацию,
	|	ЛОЖЬ, 
	|	СписокОС.Стоимость,
	|	СписокОС.СтоимостьРегл,
	|	СписокОС.СтоимостьИзменение,
	|	СписокОС.СтоимостьРеглИзменение,
	|	СписокОС.АмортизацияИзменение,
	|	СписокОС.АмортизацияРеглИзменение,
	|	СписокОС.Амортизация,
	|	СписокОС.АмортизацияРегл,
	|	0,
	|	0,
	|	ИСТИНА
	|ИЗ
	|	СписокОССИзмененнымиПараметрами_УУ КАК СписокОС
	|ГДЕ
	|	СписокОС.ЭтоДоначисление";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаДоначислениеАмортизацииУУ(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокОС.Период КАК Период,
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.Подразделение КАК Подразделение,
	|	СписокОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	СписокОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОС.СтатьяРасходов КАК СтатьяРасходов,
	|	СписокОС.АналитикаРасходов КАК АналитикаРасходов,
	|	СписокОС.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	СписокОС.СуммаУУ КАК СуммаУУ,
	|	СписокОС.СуммаБУ КАК СуммаБУ,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокОС.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	СписокОС.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|	СписокОС.РесурсНазначенный КАК РесурсНазначенный,
	|	СписокОС.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	СписокОС.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|	СписокОС.ОстатокОбесцененияРегл КАК ОстатокОбесцененияРегл,
	|	СписокОС.ОстатокОбесцененияУпр КАК ОстатокОбесцененияУпр
	|
	|ПОМЕСТИТЬ ДоначислениеАмортизацииСУчетомИзмененийУУ
	|
	|ИЗ
	|	&АмортизацияИзменений_УУ КАК СписокОС
	|ГДЕ
	|	СписокОС.ЭтоИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Амортизация.ПериодРасчета,
	|	Амортизация.Организация,
	|	Амортизация.ОсновноеСредство,
	|	Амортизация.ГруппаФинансовогоУчета,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СпособыОтраженияРасходов.Подразделение, Амортизация.Подразделение) = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА Амортизация.Подразделение
	|		ИНАЧЕ ЕСТЬNULL(СпособыОтраженияРасходов.Подразделение, Амортизация.Подразделение) 
	|	КОНЕЦ КАК Подразделение,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.НаправлениеДеятельности, Амортизация.НаправлениеДеятельности) КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.СтатьяРасходов, Амортизация.СтатьяРасходов) КАК СтатьяРасходов,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.АналитикаРасходов, Амортизация.АналитикаРасходов) КАК АналитикаРасходов,
	|	Амортизация.СпособОтраженияРасходов,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.ПередаватьРасходыВДругуюОрганизацию, Амортизация.ПередаватьРасходыВДругуюОрганизацию) КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.ОрганизацияПолучательРасходов, Амортизация.ОрганизацияПолучательРасходов) КАК ОрганизацияПолучательРасходов,
	|	Амортизация.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	Амортизация.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|	Амортизация.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	Амортизация.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|
	|	ВЫБОР
	|		КОГДА НЕ Амортизация.СпособОтраженияРасходов В (&ПустыеСпособыОтраженияРасходов)
	|			ТОГДА &РаспределениеСуммыУУ
	|		ИНАЧЕ Амортизация.СуммаУУ
	|	КОНЕЦ КАК СуммаУУ,
	|
	|	ВЫБОР
	|		КОГДА НЕ Амортизация.СпособОтраженияРасходов В (&ПустыеСпособыОтраженияРасходов)
	|			ТОГДА &РаспределениеСуммыБУ
	|		ИНАЧЕ Амортизация.СуммаБУ
	|	КОНЕЦ КАК СуммаБУ
	|
	|ПОМЕСТИТЬ ДоначислениеАмортизацииУУ
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		СписокОС.Период КАК ПериодРасчета,
	|		СписокОС.Организация КАК Организация,
	|		СписокОС.Подразделение КАК Подразделение,
	|		СписокОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|		СписокОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|		СписокОС.СтатьяРасходов КАК СтатьяРасходов,
	|		СписокОС.АналитикаРасходов КАК АналитикаРасходов,
	|		СписокОС.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|		СписокОС.СуммаУУ КАК СуммаУУ,
	|		СписокОС.СуммаБУ КАК СуммаБУ,
	|		СписокОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|		СписокОС.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|		СписокОС.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|		СписокОС.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|		СписокОС.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|		СписокОС.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл
	|	ИЗ
	|		ДоначислениеАмортизацииБезИзмененийУУ КАК СписокОС
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписокОС.Период,
	|		СписокОС.Организация,
	|		СписокОС.Подразделение,
	|		СписокОС.ГруппаФинансовогоУчета,
	|		СписокОС.НаправлениеДеятельности,
	|		СписокОС.ОсновноеСредство,
	|		СписокОС.СтатьяРасходов,
	|		СписокОС.АналитикаРасходов,
	|		СписокОС.СпособОтраженияРасходов,
	|		СписокОС.СуммаУУ,
	|		СписокОС.СуммаБУ,
	|		СписокОС.ПередаватьРасходыВДругуюОрганизацию,
	|		СписокОС.ОрганизацияПолучательРасходов,
	|		СписокОС.РесурсВыработанныйЗаПрошлыйПериод,
	|		СписокОС.РесурсВыработанныйЗаТекущийПериод,
	|		СписокОС.СтоимостьДляВычисленияАмортизации,
	|		СписокОС.СтоимостьДляВычисленияАмортизацииРегл
	|	ИЗ
	|		ДоначислениеАмортизацииСУчетомИзмененийУУ КАК СписокОС
	|
	|	) КАК Амортизация
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпособыОтраженияРасходовУУ КАК СпособыОтраженияРасходов
	|		ПО (СпособыОтраженияРасходов.СпособОтраженияРасходов = Амортизация.СпособОтраженияРасходов)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РаспределениеСуммыУУ", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаУУ"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РаспределениеСуммыБУ", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаБУ"));
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаДоначислениеАмортизацииУУ_Сгруппированная(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СУММА(СписокОС.СуммаУУ) КАК СуммаУУ,
	|	СУММА(СписокОС.СуммаБУ) КАК СуммаБУ
	|ПОМЕСТИТЬ ДоначислениеАмортизацииУУ_Сгруппированная
	|ИЗ
	|	ДоначислениеАмортизацииУУ КАК СписокОС
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокОС.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаРасчетАмортизацииБезИзмененийУУ(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокОС.Период КАК Период,
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.Местонахождение КАК Подразделение,
	|	СписокОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	СписокОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОС.СтатьяРасходовУУ КАК СтатьяРасходов,
	|	СписокОС.АналитикаРасходовУУ КАК АналитикаРасходов,
	|	СписокОС.СпособОтраженияРасходовУУ КАК СпособОтраженияРасходов,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизациюУУ КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокОС.ОрганизацияПолучательРасходовУУ КАК ОрганизацияПолучательРасходов,
	|	СписокОС.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|	СписокОС.РесурсНазначенный КАК РесурсНазначенный,
	|	СписокОС.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
	|	СписокОС.Стоимость КАК Стоимость,
	|	СписокОС.СтоимостьРегл КАК СтоимостьРегл,
	|	СписокОС.Амортизация КАК Амортизация,
	|	СписокОС.АмортизацияРегл КАК АмортизацияРегл,
	|	СписокОС.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) КАК ОстаточнаяСтоимость,
	|	СписокОС.ОстаточнаяСтоимостьРегл - ЕСТЬNULL(Доначисление.СуммаБУ, 0) КАК ОстаточнаяСтоимостьРегл,
	|	СписокОС.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	СписокОС.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|	СписокОС.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	СписокОС.ЛиквидационнаяСтоимостьРегл КАК ЛиквидационнаяСтоимостьРегл,
	|	СписокОС.ОстатокОбесцененияРегл КАК ОстатокОбесцененияРегл,
	|	СписокОС.ОстатокОбесцененияУпр КАК ОстатокОбесцененияУпр,
	|	СписокОС.КоэффициентУскорения КАК КоэффициентУскорения,
	|	СписокОС.ОтработанныхМесяцевПолныхЛет КАК ОтработанныхМесяцевПолныхЛет,
	|	СписокОС.НачислятьАмортизацию КАК НачислятьАмортизацию,
	|
	|	ВЫБОР
	|		КОГДА &ОбесценениеУУ > СписокОС.ОстатокОбесцененияУпр
	|			ТОГДА СписокОС.ОстатокОбесцененияУпр
	|		ИНАЧЕ &ОбесценениеУУ
	|	КОНЕЦ КАК ОбесценениеУУ,
	|
	|	ВЫБОР
	|		КОГДА &ОбесценениеБУ > СписокОС.ОстатокОбесцененияРегл
	|			ТОГДА СписокОС.ОстатокОбесцененияРегл
	|		ИНАЧЕ &ОбесценениеБУ
	|	КОНЕЦ КАК ОбесценениеБУ,
	|
	|	ВЫБОР 
	|		КОГДА (СписокОС.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) - (&СуммаАмортизацииУУ)) > СписокОС.ЛиквидационнаяСтоимость
	|			ТОГДА 
	|				ВЫБОР
	|					КОГДА СписокОС.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) - (&СуммаАмортизацииУУ) - СписокОС.ЛиквидационнаяСтоимость < &МинимальнаяОстаточнаяСтоимость
	|							И СписокОС.Организация.ВалютаРегламентированногоУчета = &ВалютаУправленческогоУчета
	|							И &ВедетсяРегламентированныйУчетВНА
	|							И НЕ СписокОС.АмортизацияНачисляетсяПоСтандартамМУ
	|
	|						ТОГДА СписокОС.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) - СписокОС.ЛиквидационнаяСтоимость
	|
	|					ИНАЧЕ &СуммаАмортизацииУУ
	|				КОНЕЦ
	|
	|		КОГДА (СписокОС.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) - СписокОС.ЛиквидационнаяСтоимость) > 0
	|			ТОГДА (СписокОС.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) - СписокОС.ЛиквидационнаяСтоимость)
	|
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУУ,
	|
	|	ВЫБОР 
	|		КОГДА (СписокОС.ОстаточнаяСтоимостьРегл - ЕСТЬNULL(Доначисление.СуммаБУ, 0) - (&СуммаАмортизацииБУ)) > СписокОС.ЛиквидационнаяСтоимостьРегл
	|			ТОГДА &СуммаАмортизацииБУ
	|		КОГДА (СписокОС.ОстаточнаяСтоимостьРегл - ЕСТЬNULL(Доначисление.СуммаБУ, 0) - СписокОС.ЛиквидационнаяСтоимостьРегл) > 0
	|			ТОГДА (СписокОС.ОстаточнаяСтоимостьРегл - ЕСТЬNULL(Доначисление.СуммаБУ, 0) - СписокОС.ЛиквидационнаяСтоимостьРегл)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБУ
	|
	|ПОМЕСТИТЬ РасчетАмортизацииБезИзмененийУУ
	|
	|ИЗ
	|	СписокОССПараметрами_УУ КАК СписокОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДоначислениеАмортизацииУУ_Сгруппированная КАК Доначисление
	|		ПО Доначисление.ОсновноеСредство = СписокОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПроверкаПараметровРасчета_УУ КАК ПроверкаПараметровРасчета
	|		ПО ПроверкаПараметровРасчета.ОбъектУчета = СписокОС.ОсновноеСредство
	|			И ПроверкаПараметровРасчета.Организация = СписокОС.Организация
	|ГДЕ
	|	НЕ СписокОС.ЭтоДоначисление
	|	И ПроверкаПараметровРасчета.ОбъектУчета ЕСТЬ NULL";
	
	ПараметрыТекстаЗапроса = ПараметрыТекстаЗапросаСуммаАмортизацииУУ();
	ПараметрыТекстаЗапроса.ТекстСтоимостьДляВычисленияАмортизации = "СписокОС.СтоимостьДляВычисленияАмортизации";
	ПараметрыТекстаЗапроса.ТекстОстаточнаяСтоимость = "СписокОС.ОстаточнаяСтоимость";
	ПараметрыТекстаЗапроса.ТекстЛиквидационнаяСтоимость = "СписокОС.ЛиквидационнаяСтоимость";
	ПараметрыТекстаЗапроса.ТекстСтоимостьНаНачалоГода = "СписокОС.СтоимостьНаНачалоГода";
	ПараметрыТекстаЗапроса.ТекстОбесценение = "СписокОС.ОстатокОбесцененияУпр";
	ПараметрыТекстаЗапроса.ТекстСуммаДоначисления = "ЕСТЬNULL(Доначисление.СуммаУУ, 0)";

	ТекстСуммаАмортизацииУУ = ТекстЗапросаСуммаАмортизацииУУ(ПараметрыТекстаЗапроса);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаАмортизацииУУ", ТекстСуммаАмортизацииУУ);
	
	//
	ПараметрыТекстаЗапроса = ПараметрыТекстаЗапросаСуммаАмортизацииУУ();
	ПараметрыТекстаЗапроса.ТекстСтоимостьДляВычисленияАмортизации = "СписокОС.СтоимостьДляВычисленияАмортизацииРегл";
	ПараметрыТекстаЗапроса.ТекстОстаточнаяСтоимость = "СписокОС.ОстаточнаяСтоимостьРегл";
	ПараметрыТекстаЗапроса.ТекстЛиквидационнаяСтоимость = "СписокОС.ЛиквидационнаяСтоимостьРегл";
	ПараметрыТекстаЗапроса.ТекстСтоимостьНаНачалоГода = "СписокОС.СтоимостьРеглНаНачалоГода";
	ПараметрыТекстаЗапроса.ТекстОбесценение = "СписокОС.ОстатокОбесцененияРегл";
	ПараметрыТекстаЗапроса.ТекстСуммаДоначисления = "ЕСТЬNULL(Доначисление.СуммаБУ, 0)";
	
	ТекстСуммаАмортизацииБУ = ТекстЗапросаСуммаАмортизацииУУ(ПараметрыТекстаЗапроса);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаАмортизацииБУ", ТекстСуммаАмортизацииБУ);
	
	//
	ТекстОбесценениеУУ = ТекстЗапросаСуммаОбесценения("СписокОС.ОстатокОбесцененияУпр");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОбесценениеУУ", ТекстОбесценениеУУ);
	
	//
	ТекстОбесценениеБУ = ТекстЗапросаСуммаОбесценения("СписокОС.ОстатокОбесцененияРегл");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОбесценениеБУ", ТекстОбесценениеБУ);
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаРасчетАмортизацииВыборкаИзмененийУУ(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОбъединениеОС.Ссылка КАК Ссылка,
	|	ОбъединениеОС.ОсновноеСредство КАК ОсновноеСредствоПолучатель,
	|	ТабличнаяЧасть.ОсновноеСредство КАК ОсновноеСредствоИсточник
	|ПОМЕСТИТЬ ОбъединениеОС
	|ИЗ
	|	Документ.ОбъединениеОС КАК ОбъединениеОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОбъединениеОС.ОС КАК ТабличнаяЧасть
	|		ПО (ТабличнаяЧасть.Ссылка = ОбъединениеОС.Ссылка)
	|ГДЕ
	|	ОбъединениеОС.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
	|	И ОбъединениеОС.Проведен
	|	И ОбъединениеОС.ОтражатьВУпрУчете
	|	И (ОбъединениеОС.Организация, ТабличнаяЧасть.ОсновноеСредство) В
	|			(ВЫБРАТЬ
	|				СписокОС.Организация,
	|				СписокОС.ОбъектУчета
	|			ИЗ
	|				ПакетыАмортизации КАК СписокОС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ ОтборОС
	|ИЗ
	|	СписокОССИзмененнымиПараметрами_УУ КАК СписокОС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СписокОС.ОсновноеСредствоИсточник
	|ИЗ
	|	ОбъединениеОС КАК СписокОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РазукомплектацияОС.Ссылка КАК Ссылка,
	|	РазукомплектацияОС.ОсновноеСредство КАК ОсновноеСредствоИсточник,
	|	ТабличнаяЧасть.ОсновноеСредство КАК ОсновноеСредствоПолучатель,
	|	ТабличнаяЧасть.СтоимостьУУ КАК СтоимостьСписанияУпр,
	|	ТабличнаяЧасть.СтоимостьБУ КАК СтоимостьСписанияРегл
	|ИЗ
	|	Документ.РазукомплектацияОС КАК РазукомплектацияОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РазукомплектацияОС.ОС КАК ТабличнаяЧасть
	|		ПО (ТабличнаяЧасть.Ссылка = РазукомплектацияОС.Ссылка)
	|ГДЕ
	|	РазукомплектацияОС.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
	|	И РазукомплектацияОС.Проведен
	|	И РазукомплектацияОС.ОтражатьВУпрУчете
	|	И (РазукомплектацияОС.Организация, ТабличнаяЧасть.ОсновноеСредство) В
	|			(ВЫБРАТЬ
	|				СписокОС.Организация,
	|				СписокОС.ОбъектУчета
	|			ИЗ
	|				ПакетыАмортизации КАК СписокОС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОбъединениеОС.Ссылка,
	|	ОбъединениеОС.ОсновноеСредствоИсточник,
	|	ОбъединениеОС.ОсновноеСредствоПолучатель,
	|	0,
	|	0
	|ИЗ
	|	ОбъединениеОС КАК ОбъединениеОС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 КАК Приоритет,
	|	&НачалоМесяца КАК Период,
	|	НЕОПРЕДЕЛЕНО КАК Регистратор,
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.Подразделение КАК Подразделение,
	|	СписокОС.ГруппаФинансовогоУчета,
	|	СписокОС.НаправлениеДеятельности,
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОС.СтатьяРасходов КАК СтатьяРасходов,
	|	СписокОС.АналитикаРасходов КАК АналитикаРасходов,
	|	СписокОС.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	СписокОС.СуммаУУ КАК СуммаУУ,
	|	СписокОС.СуммаБУ КАК СуммаБУ,
	|	СписокОС.ОбесценениеУУ КАК ОбесценениеУУ,
	|	СписокОС.ОбесценениеБУ КАК ОбесценениеБУ,
	|	1 КАК Коэффициент,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокОС.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	СписокОС.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод,
	|	СписокОС.РесурсНазначенный,
	|	СписокОС.МетодНачисленияАмортизации,
	|	СписокОС.ЛиквидационнаяСтоимость,
	|	СписокОС.ЛиквидационнаяСтоимостьРегл,
	|	СписокОС.КоэффициентУскорения,
	|	СписокОС.ОтработанныхМесяцевПолныхЛет,
	|	СписокОС.НачислятьАмортизацию,
	|	ЛОЖЬ КАК ПолученПриРазукомплектацииОбъединении, 
	|	СписокОС.Стоимость КАК Стоимость,
	|	СписокОС.СтоимостьРегл КАК СтоимостьРегл,
	|	СписокОС.ОстатокОбесцененияРегл КАК ОстатокОбесцененияРегл,
	|	СписокОС.ОстатокОбесцененияУпр КАК ОстатокОбесцененияУпр,
	|	0 КАК СтоимостьИзменение,
	|	0 КАК СтоимостьРеглИзменение,
	|	0 КАК АмортизацияИзменение,
	|	0 КАК АмортизацияРеглИзменение,
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(СписокОС.Период, МЕСЯЦ) = &НачалоМесяца
	|			ТОГДА СписокОС.Амортизация + ЕСТЬNULL(Доначисление.СуммаУУ, 0) 
	|		ИНАЧЕ СписокОС.Амортизация
	|	КОНЕЦ КАК Амортизация,
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(СписокОС.Период, МЕСЯЦ) = &НачалоМесяца
	|			ТОГДА СписокОС.АмортизацияРегл + ЕСТЬNULL(Доначисление.СуммаБУ, 0) 
	|		ИНАЧЕ СписокОС.АмортизацияРегл
	|	КОНЕЦ КАК АмортизацияРегл,
	|	СписокОС.Стоимость - СписокОС.Амортизация КАК СтоимостьДляВычисленияАмортизации,
	|	СписокОС.СтоимостьРегл - СписокОС.АмортизацияРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|	ЛОЖЬ КАК ЭтоИзменения
	|ИЗ
	|	РасчетАмортизацииБезИзмененийУУ КАК СписокОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДоначислениеАмортизацииУУ_Сгруппированная КАК Доначисление
	|		ПО Доначисление.ОсновноеСредство = СписокОС.ОсновноеСредство
	|
	|ГДЕ
	|	СписокОС.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				СписокОС.ОсновноеСредство
	|			ИЗ
	|				ОтборОС КАК СписокОС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(СписокОС.Регистратор) = ТИП(Документ.РазукомплектацияОС)
	|				И НЕ СписокОС.ПолученПриРазукомплектацииОбъединении
	|			ТОГДА 3
	|		КОГДА СписокОС.ПолученПриРазукомплектацииОбъединении
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Приоритет,
	|	СписокОС.Период,
	|	СписокОС.Регистратор,
	|	СписокОС.Организация,
	|	СписокОС.Местонахождение,
	|	СписокОС.ГруппаФинансовогоУчета,
	|	СписокОС.НаправлениеДеятельности,
	|	СписокОС.ОсновноеСредство,
	|	СписокОС.СтатьяРасходовУУ,
	|	СписокОС.АналитикаРасходовУУ,
	|	СписокОС.СпособОтраженияРасходовУУ,
	|	0,
	|	0,
	|	0,
	|	0,
	|	1,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизациюУУ,
	|	СписокОС.ОрганизацияПолучательРасходовУУ,
	|	СписокОС.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод,
	|	СписокОС.РесурсНазначенный,
	|	СписокОС.МетодНачисленияАмортизации,
	|	СписокОС.ЛиквидационнаяСтоимость,
	|	СписокОС.ЛиквидационнаяСтоимостьРегл,
	|	СписокОС.КоэффициентУскорения,
	|	СписокОС.ОтработанныхМесяцевПолныхЛет,
	|	СписокОС.НачислятьАмортизацию,
	|	СписокОС.ПолученПриРазукомплектацииОбъединении, 
	|	СписокОС.Стоимость КАК Стоимость,
	|	СписокОС.СтоимостьРегл КАК СтоимостьРегл,
	|	СписокОС.ОстатокОбесцененияРегл КАК ОстатокОбесцененияРегл,
	|	СписокОС.ОстатокОбесцененияУпр КАК ОстатокОбесцененияУпр,
	|	СписокОС.СтоимостьИзменение КАК СтоимостьИзменение,
	|	СписокОС.СтоимостьРеглИзменение КАК СтоимостьРеглИзменение,
	|	СписокОС.АмортизацияИзменение КАК АмортизацияИзменение,
	|	СписокОС.АмортизацияРеглИзменение КАК АмортизацияРеглИзменение,
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(СписокОС.Период, МЕСЯЦ) = &НачалоМесяца
	|			ТОГДА СписокОС.Амортизация + ЕСТЬNULL(Доначисление.СуммаУУ, 0) 
	|		ИНАЧЕ СписокОС.Амортизация
	|	КОНЕЦ,
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(СписокОС.Период, МЕСЯЦ) = &НачалоМесяца
	|			ТОГДА СписокОС.АмортизацияРегл + ЕСТЬNULL(Доначисление.СуммаБУ, 0) 
	|		ИНАЧЕ СписокОС.АмортизацияРегл
	|	КОНЕЦ,
	|	СписокОС.Стоимость - СписокОС.Амортизация,
	|	СписокОС.СтоимостьРегл - СписокОС.АмортизацияРегл,
	|	ИСТИНА
	|ИЗ
	|	СписокОССИзмененнымиПараметрами_УУ КАК СписокОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДоначислениеАмортизацииУУ_Сгруппированная КАК Доначисление
	|		ПО Доначисление.ОсновноеСредство = СписокОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПроверкаПараметровРасчетаИзменения_УУ КАК ПроверкаПараметровРасчета
	|		ПО ПроверкаПараметровРасчета.ОбъектУчета = СписокОС.ОсновноеСредство
	|			И ПроверкаПараметровРасчета.Организация = СписокОС.Организация
	|ГДЕ
	|	НЕ СписокОС.ЭтоДоначисление
	|	И ПроверкаПараметровРасчета.ОбъектУчета ЕСТЬ NULL";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаРасчетАмортизации_УУ(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Амортизация.ПериодРасчета КАК ПериодРасчета,
	|	Амортизация.Организация КАК Организация,
	|	Амортизация.Подразделение КАК Подразделение,
	|	Амортизация.ОсновноеСредство КАК ОсновноеСредство,
	|	ОбесценениеВНА.ЕГДС КАК ЕГДС,
	|	Амортизация.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	Амортизация.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|
	|	ВЫБОР
	|		КОГДА НЕ СпособыОтраженияРасходов.СтатьяРасходов ЕСТЬ NULL
	|				И СпособыОтраженияРасходов.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|				И СпособыОтраженияРасходов.СтатьяРасходов <> НЕОПРЕДЕЛЕНО
	|			ТОГДА СпособыОтраженияРасходов.СтатьяРасходов
	|		ИНАЧЕ Амортизация.СтатьяРасходов
	|	КОНЕЦ КАК СтатьяРасходов,
	|
	|	ВЫБОР
	|		КОГДА НЕ СпособыОтраженияРасходов.СтатьяРасходов ЕСТЬ NULL
	|				И СпособыОтраженияРасходов.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|				И СпособыОтраженияРасходов.СтатьяРасходов <> НЕОПРЕДЕЛЕНО
	|			ТОГДА СпособыОтраженияРасходов.АналитикаРасходов
	|		ИНАЧЕ Амортизация.АналитикаРасходов
	|	КОНЕЦ КАК АналитикаРасходов,
	|
	|	Амортизация.СпособОтраженияРасходов,
	|
	|	ЕСТЬNULL(СпособыОтраженияРасходов.НаправлениеДеятельности, Амортизация.НаправлениеДеятельности) КАК КорНаправлениеДеятельности,
	|
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СпособыОтраженияРасходов.Подразделение, Амортизация.Подразделение) = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА Амортизация.Подразделение
	|		ИНАЧЕ ЕСТЬNULL(СпособыОтраженияРасходов.Подразделение, Амортизация.Подразделение) 
	|	КОНЕЦ КАК КорПодразделение,
	|
	|	ВЫБОР
	|		КОГДА НЕ СпособыОтраженияРасходов.ОрганизацияПолучательРасходов ЕСТЬ NULL
	|				И СпособыОтраженияРасходов.ПередаватьРасходыВДругуюОрганизацию
	|				И СпособыОтраженияРасходов.ОрганизацияПолучательРасходов <> Амортизация.Организация
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ Амортизация.ПередаватьРасходыВДругуюОрганизацию
	|	КОНЕЦ КАК ПередаватьРасходыВДругуюОрганизацию,
	|
	|	ЕСТЬNULL(СпособыОтраженияРасходов.ОрганизацияПолучательРасходов, Амортизация.ОрганизацияПолучательРасходов) КАК ОрганизацияПолучательРасходов,
	|
	|	Амортизация.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	Амортизация.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|	Амортизация.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	Амортизация.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|	Амортизация.ОстатокОбесцененияРегл КАК ОстатокОбесцененияРегл,
	|	Амортизация.ОстатокОбесцененияУпр КАК ОстатокОбесцененияУпр,
	|
	|	Амортизация.ЭтоДоначисление КАК ЭтоДоначисление,
	|	ЕСТЬNULL(УчетнаяПолитика.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) КАК ПорядокУчетаВНА,
	|	ЕСТЬNULL(УчетнаяПолитика.ВариантПроводокПоОбесценениюВНА, ЗНАЧЕНИЕ(Перечисление.ВариантыПроводокПоОбесценениюВНА.ДоначислятьКорректировкуОбесценения)) КАК ВариантПроводокПоОбесценениюВНА,
	|
	|	ВЫБОР
	|		КОГДА НЕ Амортизация.СпособОтраженияРасходов В (&ПустыеСпособыОтраженияРасходов)
	|				И НЕ СпособыОтраженияРасходов.СпособОтраженияРасходов ЕСТЬ NULL
	|			ТОГДА &РаспределениеСуммыУУ
	|		ИНАЧЕ Амортизация.СуммаУУ
	|	КОНЕЦ КАК СуммаУУ,
	|
	|	ВЫБОР
	|		КОГДА НЕ Амортизация.СпособОтраженияРасходов В (&ПустыеСпособыОтраженияРасходов)
	|				И НЕ СпособыОтраженияРасходов.СпособОтраженияРасходов ЕСТЬ NULL
	|			ТОГДА &РаспределениеСуммыБУ
	|		ИНАЧЕ Амортизация.СуммаБУ
	|	КОНЕЦ КАК СуммаБУ,
	|
	|	ВЫБОР
	|		КОГДА НЕ Амортизация.СпособОтраженияРасходов В (&ПустыеСпособыОтраженияРасходов)
	|				И НЕ СпособыОтраженияРасходов.СпособОтраженияРасходов ЕСТЬ NULL
	|			ТОГДА &РаспределениеОбесцененияУУ
	|		ИНАЧЕ Амортизация.ОбесценениеУУ
	|	КОНЕЦ КАК ОбесценениеУУ,
	|
	|	ВЫБОР
	|		КОГДА НЕ Амортизация.СпособОтраженияРасходов В (&ПустыеСпособыОтраженияРасходов)
	|				И НЕ СпособыОтраженияРасходов.СпособОтраженияРасходов ЕСТЬ NULL
	|			ТОГДА &РаспределениеОбесцененияБУ
	|		ИНАЧЕ Амортизация.ОбесценениеБУ
	|	КОНЕЦ КАК ОбесценениеБУ
	|
	|ПОМЕСТИТЬ РасчетАмортизации_УУ
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		СписокОС.Период КАК ПериодРасчета,
	|		СписокОС.Организация КАК Организация,
	|		ВЫРАЗИТЬ(СписокОС.Организация КАК Справочник.Организации).ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|		СписокОС.Подразделение КАК Подразделение,
	|		СписокОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|		СписокОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|		СписокОС.СтатьяРасходов КАК СтатьяРасходов,
	|		СписокОС.АналитикаРасходов КАК АналитикаРасходов,
	|		СписокОС.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|		СписокОС.СуммаУУ КАК СуммаУУ,
	|		СписокОС.СуммаБУ КАК СуммаБУ,
	|		СписокОС.ОбесценениеУУ КАК ОбесценениеУУ,
	|		СписокОС.ОбесценениеБУ КАК ОбесценениеБУ,
	|		СписокОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|		СписокОС.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|		СписокОС.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|		СписокОС.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|		СписокОС.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|		СписокОС.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|		СписокОС.ОстатокОбесцененияРегл КАК ОстатокОбесцененияРегл,
	|		СписокОС.ОстатокОбесцененияУпр КАК ОстатокОбесцененияУпр,
	|		ЛОЖЬ КАК ЭтоДоначисление
	|	ИЗ
	|		РасчетАмортизацииБезИзмененийУУ КАК СписокОС
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписокОС.Период,
	|		СписокОС.Организация,
	|		ВЫРАЗИТЬ(СписокОС.Организация КАК Справочник.Организации).ГоловнаяОрганизация,
	|		СписокОС.Подразделение,
	|		СписокОС.ГруппаФинансовогоУчета,
	|		СписокОС.НаправлениеДеятельности,
	|		СписокОС.ОсновноеСредство,
	|		СписокОС.СтатьяРасходов,
	|		СписокОС.АналитикаРасходов,
	|		СписокОС.СпособОтраженияРасходов,
	|		СписокОС.СуммаУУ,
	|		СписокОС.СуммаБУ,
	|		СписокОС.ОбесценениеУУ КАК ОбесценениеУУ,
	|		СписокОС.ОбесценениеБУ КАК ОбесценениеБУ,
	|		СписокОС.ПередаватьРасходыВДругуюОрганизацию,
	|		СписокОС.ОрганизацияПолучательРасходов,
	|		СписокОС.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|		СписокОС.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|		СписокОС.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|		СписокОС.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|		СписокОС.ОстатокОбесцененияРегл КАК ОстатокОбесцененияРегл,
	|		СписокОС.ОстатокОбесцененияУпр КАК ОстатокОбесцененияУпр,
	|		ЛОЖЬ КАК ЭтоДоначисление
	|	ИЗ
	|		РасчетАмортизацииСУчетомИзмененийУУ КАК СписокОС
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписокОС.ПериодРасчета КАК ПериодРасчета,
	|		СписокОС.Организация КАК Организация,
	|		ВЫРАЗИТЬ(СписокОС.Организация КАК Справочник.Организации).ГоловнаяОрганизация,
	|		СписокОС.Подразделение КАК Подразделение,
	|		СписокОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|		СписокОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|		СписокОС.СтатьяРасходов КАК СтатьяРасходов,
	|		СписокОС.АналитикаРасходов КАК АналитикаРасходов,
	|		СписокОС.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|		СписокОС.СуммаУУ КАК СуммаУУ,
	|		СписокОС.СуммаБУ КАК СуммаБУ,
	|		0 КАК ОбесценениеУУ,
	|		0 КАК ОбесценениеБУ,
	|		СписокОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|		СписокОС.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|		СписокОС.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|		СписокОС.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|		СписокОС.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|		СписокОС.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|		0 КАК ОстатокОбесцененияРегл,
	|		0 КАК ОстатокОбесцененияУпр,
	|		ИСТИНА КАК ЭтоДоначисление
	|	ИЗ
	|		ДоначислениеАмортизацииУУ КАК СписокОС
	|
	|	) КАК Амортизация
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпособыОтраженияРасходовУУ КАК СпособыОтраженияРасходов
	|		ПО (СпособыОтраженияРасходов.СпособОтраженияРасходов = Амортизация.СпособОтраженияРасходов)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&КонецМесяца) КАК УчетнаяПолитика
	|		ПО УчетнаяПолитика.Организация = Амортизация.ГоловнаяОрганизация
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОбесценениеВНА КАК ОбесценениеВНА
	|		ПО ОбесценениеВНА.ОсновноеСредство = Амортизация.ОсновноеСредство";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РаспределениеСуммыУУ", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаУУ"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РаспределениеСуммыБУ", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаБУ"));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РаспределениеОбесцененияУУ", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.ОбесценениеУУ"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РаспределениеОбесцененияБУ", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.ОбесценениеБУ"));
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаНачисленнаяАмортизация(СписокЗапросов, ПараметрыРасчета)

	СписокПодзапросов = Новый Массив;
	
	ТекстПодзапроса =  
	"	ВЫБРАТЬ
	|		РасчетАмортизации.ПериодРасчета КАК ПериодРасчета,
	|		РасчетАмортизации.Организация КАК Организация,
	|		РасчетАмортизации.Подразделение КАК Подразделение,
	|		РасчетАмортизации.КорПодразделение КАК КорПодразделение,
	|		РасчетАмортизации.ОсновноеСредство КАК ОсновноеСредство,
	|		РасчетАмортизации.ЕГДС КАК ЕГДС,
	|		РасчетАмортизации.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|		РасчетАмортизации.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		РасчетАмортизации.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|		РасчетАмортизации.СтатьяРасходов КАК СтатьяРасходов,
	|		РасчетАмортизации.АналитикаРасходов КАК АналитикаРасходов,
	|		РасчетАмортизации.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|		РасчетАмортизации.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|		ЛОЖЬ КАК НачислятьИзнос,
	|		РасчетАмортизации.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|		РасчетАмортизации.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|		0 КАК РесурсВыработанныйЗаПрошлыйПериодБУ,
	|		0 КАК РесурсВыработанныйЗаТекущийПериодБУ,
	|		РасчетАмортизации.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|		РасчетАмортизации.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|		РасчетАмортизации.ОстатокОбесцененияРегл КАК ОстатокОбесцененияРегл,
	|		РасчетАмортизации.ОстатокОбесцененияУпр КАК ОстатокОбесцененияУпр,
	|		РасчетАмортизации.ЭтоДоначисление КАК ЭтоДоначисление,
	|		ЛОЖЬ КАК ЭтоДоначислениеБУ,
	|		РасчетАмортизации.ПорядокУчетаВНА КАК ПорядокУчетаУУ,
	|		НЕОПРЕДЕЛЕНО КАК ПорядокУчетаБУ,
	|		РасчетАмортизации.ВариантПроводокПоОбесценениюВНА КАК ВариантПроводокПоОбесценениюВНА,
	|
	|		РасчетАмортизации.СуммаУУ КАК СуммаУУ,
	|
	|		ВЫБОР 
	|			КОГДА НЕ &ВедетсяРегламентированныйУчетВНА
	|				ТОГДА РасчетАмортизации.СуммаБУ
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК СуммаБУ,
	|
	|		0 КАК СуммаНУ,
	|		0 КАК СуммаПР,
	|		0 КАК СуммаВР,
	|		0 КАК СуммаЦФ,
	|		0 КАК СуммаНУЦФ,
	|		0 КАК СуммаПРЦФ,
	|		0 КАК СуммаВРЦФ,
	|
	|		РасчетАмортизации.ОбесценениеУУ КАК ОбесценениеУУ,
	|
	|		ВЫБОР 
	|			КОГДА НЕ &ВедетсяРегламентированныйУчетВНА
	|				ТОГДА РасчетАмортизации.ОбесценениеБУ
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ОбесценениеБУ,
	|		
	|		0 КАК ОбесценениеЦФ
	|
	|	ИЗ
	|		РасчетАмортизации_УУ КАК РасчетАмортизации";
	СписокПодзапросов.Добавить(ТекстПодзапроса);
	
	АмортизацияОСЛокализация.ДополнитьТекстПодзапросаНачисленнаяАмортизация(СписокПодзапросов, ПараметрыРасчета);
	
	ТекстПодзапроса = СтрСоединить(СписокПодзапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстЗапросаВтНачисленнаяАмортизация(СписокЗапросов, ТекстПодзапроса);
	
КонецПроцедуры

Функция ТекстЗапросаКоличествоОбработанныхДанных(ПараметрыРасчета)

	ТекстЗапроса = АмортизацияОСЛокализация.ТекстЗапросаКоличествоОбработанныхДанных(ПараметрыРасчета); 
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса =  
		"ВЫБРАТЬ
		|	ЕСТЬNULL(КОЛИЧЕСТВО(НачисленнаяАмортизация.ОсновноеСредство), 0) КАК КоличествоДанных
		|ИЗ
		|	РасчетАмортизации_УУ КАК НачисленнаяАмортизация";
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаСуммаАмортизацииУУ(ПараметрыТекстаЗапроса)
	
	ТекстЗапроса = 
	"ВЫБОР 
	|	КОГДА НЕ СписокОС.НачислятьАмортизацию ИЛИ (&ОстаточнаяСтоимость - &СуммаДоначисления) <= 0 
	|
	|		ТОГДА 0
	|
	|	КОГДА СписокОС.АмортизацияНачисляетсяПоСтандартамМУ
	|			И (СписокОС.РесурсВыработанныйЗаПрошлыйПериод + СписокОС.РесурсВыработанныйЗаТекущийПериод) >= СписокОС.РесурсНазначенный
	|
	|		ТОГДА &ОстаточнаяСтоимость - &СуммаДоначисления - &ЛиквидационнаяСтоимость - &ОстатокОбесценения
	|
		// Линейный, ПропорциональноОбъемуПродукции
	|	КОГДА СписокОС.АмортизацияНачисляетсяПоСтандартамМУ
	|			И СписокОС.МетодНачисленияАмортизации В (
	|						ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный),
	|						ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)) 
	|
	|		ТОГДА ВЫБОР 
	|			КОГДА СписокОС.ЭтоДоначисление
	|				ТОГДА ВЫРАЗИТЬ(ВЫРАЗИТЬ(&СтоимостьДляВычисленияАмортизации - &СуммаДоначисления КАК ЧИСЛО(20,8))
	|								* ВЫРАЗИТЬ(СписокОС.РесурсВыработанныйЗаТекущийПериод КАК ЧИСЛО(20,8)) 
	|								/ ВЫРАЗИТЬ(СписокОС.РесурсНазначенный - СписокОС.РесурсВыработанныйЗаПрошлыйПериод КАК ЧИСЛО(20,8)) КАК ЧИСЛО(31,2))
	|
	|			ИНАЧЕ ВЫРАЗИТЬ(ВЫРАЗИТЬ(&СтоимостьДляВычисленияАмортизации - &СуммаДоначисления - &ЛиквидационнаяСтоимость - &ОстатокОбесценения КАК ЧИСЛО(20,8)) 
	|								* ВЫРАЗИТЬ(СписокОС.РесурсВыработанныйЗаТекущийПериод КАК ЧИСЛО(20,8))
	|								/ ВЫРАЗИТЬ(СписокОС.РесурсНазначенный - СписокОС.РесурсВыработанныйЗаПрошлыйПериод КАК ЧИСЛО(20,8)) КАК ЧИСЛО(31,2))
	|		КОНЕЦ
	|
		// УменьшаемогоОстатка (первый год)
	|	КОГДА СписокОС.АмортизацияНачисляетсяПоСтандартамМУ
	|			И СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка)
	|			И (СписокОС.РесурсНазначенный - СписокОС.РесурсВыработанныйЗаПрошлыйПериод - СписокОС.РесурсВыработанныйЗаТекущийПериод) <= 12
	| 
	|		ТОГДА ВЫБОР 
	|			КОГДА СписокОС.ЭтоДоначисление
	|				ТОГДА ВЫРАЗИТЬ((&СтоимостьДляВычисленияАмортизации - &СуммаДоначисления)
	|								* (ВЫРАЗИТЬ(СписокОС.РесурсВыработанныйЗаТекущийПериод КАК ЧИСЛО(20,8)) 
	|								/ ВЫРАЗИТЬ(СписокОС.РесурсНазначенный - СписокОС.РесурсВыработанныйЗаПрошлыйПериод КАК ЧИСЛО(20,8))) КАК ЧИСЛО(31,2))
	|
	|			ИНАЧЕ ВЫРАЗИТЬ((&СтоимостьДляВычисленияАмортизации - &СуммаДоначисления - &ЛиквидационнаяСтоимость) 
	|								* (ВЫРАЗИТЬ(СписокОС.РесурсВыработанныйЗаТекущийПериод КАК ЧИСЛО(20,8))
	|								/ ВЫРАЗИТЬ(СписокОС.РесурсНазначенный - СписокОС.РесурсВыработанныйЗаПрошлыйПериод КАК ЧИСЛО(20,8))) КАК ЧИСЛО(31,2))
	|		КОНЕЦ
	|
		// УменьшаемогоОстатка (после первого года)
	|	КОГДА СписокОС.АмортизацияНачисляетсяПоСтандартамМУ
	|			И СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка) 
	|   
	|		ТОГДА ВЫРАЗИТЬ((&СтоимостьДляВычисленияАмортизации - &СуммаДоначисления)
	|					* СписокОС.КоэффициентУскорения
	|					* ВЫРАЗИТЬ(СписокОС.РесурсВыработанныйЗаТекущийПериод КАК ЧИСЛО(20,8))
	|					/ ВЫРАЗИТЬ(СписокОС.РесурсНазначенный 
	|						- СписокОС.КоэффициентУскорения * (СписокОС.РесурсВыработанныйЗаПрошлыйПериод - СписокОС.ОтработанныхМесяцевПолныхЛет) КАК ЧИСЛО(20,8)) КАК ЧИСЛО(31,2))
	|
		// Линейный
	|	КОГДА НЕ СписокОС.АмортизацияНачисляетсяПоСтандартамМУ
	|			И СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный)
	| 
	|		ТОГДА ВЫБОР 
	|			КОГДА СписокОС.РесурсНазначенный = 0
	|				ТОГДА 0
	|			ИНАЧЕ
	|				ВЫРАЗИТЬ(&СтоимостьДляВычисленияАмортизации / ВЫРАЗИТЬ(СписокОС.РесурсНазначенный КАК ЧИСЛО(20,8)) КАК ЧИСЛО(31,2))
	|		КОНЕЦ
	|   
		// ПропорциональноОбъемуПродукции
	|	КОГДА НЕ СписокОС.АмортизацияНачисляетсяПоСтандартамМУ
	|			И СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	| 
	|		ТОГДА ВЫБОР 
	|			КОГДА СписокОС.РесурсВыработанныйЗаТекущийПериод <> 0 И СписокОС.РесурсНазначенный <> 0 
	|				ТОГДА ВЫРАЗИТЬ(&СтоимостьДляВычисленияАмортизации 
	|									* ВЫРАЗИТЬ(СписокОС.РесурсВыработанныйЗаТекущийПериод КАК ЧИСЛО(20,8))
	|									/ ВЫРАЗИТЬ(СписокОС.РесурсНазначенный КАК ЧИСЛО(20,8)) КАК ЧИСЛО(31,2))
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	
		// УменьшаемогоОстатка
	|	КОГДА НЕ СписокОС.АмортизацияНачисляетсяПоСтандартамМУ
	|			И СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка)
	| 
	|		ТОГДА ВЫБОР 
	|			КОГДА СписокОС.РесурсНазначенный = 0
	|				ТОГДА 0
	|			ИНАЧЕ
	|				ВЫБОР
	|					КОГДА ГОД(СписокОС.ДатаПоследнегоИзменения) = ГОД(&КонецМесяца) 
	|						ТОГДА ВЫРАЗИТЬ(&СтоимостьДляВычисленияАмортизации 
	|										/ ВЫРАЗИТЬ(СписокОС.РесурсНазначенный КАК ЧИСЛО(20,8)) 
	|										* СписокОС.КоэффициентУскорения КАК ЧИСЛО(31,2))
	|					ИНАЧЕ
	|						ВЫРАЗИТЬ(&СтоимостьНаНачалоГода 
	|									/ ВЫРАЗИТЬ(СписокОС.РесурсНазначенный КАК ЧИСЛО(20,8)) 
	|									* СписокОС.КоэффициентУскорения КАК ЧИСЛО(31,2))
	|				КОНЕЦ
	|		КОНЕЦ 
	|КОНЕЦ"; 
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СтоимостьДляВычисленияАмортизации", ПараметрыТекстаЗапроса.ТекстСтоимостьДляВычисленияАмортизации);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЛиквидационнаяСтоимость", ПараметрыТекстаЗапроса.ТекстЛиквидационнаяСтоимость);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОстаточнаяСтоимость", ПараметрыТекстаЗапроса.ТекстОстаточнаяСтоимость);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СтоимостьНаНачалоГода", ПараметрыТекстаЗапроса.ТекстСтоимостьНаНачалоГода);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаДоначисления", ПараметрыТекстаЗапроса.ТекстСуммаДоначисления);
	
	ТекстОбесценение = 
		СтрЗаменить(
			"(ВЫБОР КОГДА СписокОС.ВариантПроводокПоОбесценениюВНА = &ДоначислятьКорректировкуОбесценения ТОГДА &ОстатокОбесценения ИНАЧЕ 0 КОНЕЦ)", 
			"&ОстатокОбесценения",
			ПараметрыТекстаЗапроса.ТекстОбесценение);			
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОстатокОбесценения", ТекстОбесценение);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаСуммаОбесценения(ТекстОстатокОбесценение)
	
	ТекстЗапроса = 
	"ВЫБОР 
	|	КОГДА НЕ СписокОС.НачислятьАмортизацию 
	|		ИЛИ &ОстатокОбесценения <= 0
	|		ИЛИ НЕ СписокОС.АмортизацияНачисляетсяПоСтандартамМУ
	|
	|		ТОГДА 0
	|
	|	КОГДА (СписокОС.РесурсВыработанныйЗаПрошлыйПериод + СписокОС.РесурсВыработанныйЗаТекущийПериод) >= СписокОС.РесурсНазначенный
	|
	|		ТОГДА &ОстатокОбесценения
	|
		// Линейный, ПропорциональноОбъемуПродукции
	|	КОГДА СписокОС.МетодНачисленияАмортизации В (
	|						ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный),
	|						ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)) 
	|
	|		ТОГДА ВЫРАЗИТЬ(&ОстатокОбесценения 
	|							* (ВЫРАЗИТЬ(СписокОС.РесурсВыработанныйЗаТекущийПериод КАК ЧИСЛО(20,8))
	|							/ ВЫРАЗИТЬ(СписокОС.РесурсНазначенный - СписокОС.РесурсВыработанныйЗаПрошлыйПериод КАК ЧИСЛО(20,8))) КАК ЧИСЛО(31,2))
	|
	|	ИНАЧЕ 0
	|
	|КОНЕЦ";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОстатокОбесценения", ТекстОстатокОбесценение);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// 
// Возвращаемое значение:
//  Структура - Параметры текста запроса сумма амортизации УУ:
// * ТекстСтоимостьДляВычисленияАмортизации - Строка - 
// * ТекстОстаточнаяСтоимость - Строка -
// * ТекстЛиквидационнаяСтоимость - Строка -
// * ТекстСтоимостьНаНачалоГода - Строка -
// * ТекстСуммаДоначисления - Строка -
// * ТекстОбесценение - Строка -
Функция ПараметрыТекстаЗапросаСуммаАмортизацииУУ()
	
	ПараметрыТекстаЗапроса = Новый Структура;
	ПараметрыТекстаЗапроса.Вставить("ТекстСтоимостьДляВычисленияАмортизации");
	ПараметрыТекстаЗапроса.Вставить("ТекстОстаточнаяСтоимость");
	ПараметрыТекстаЗапроса.Вставить("ТекстЛиквидационнаяСтоимость");
	ПараметрыТекстаЗапроса.Вставить("ТекстСтоимостьНаНачалоГода");
	ПараметрыТекстаЗапроса.Вставить("ТекстСуммаДоначисления");
	ПараметрыТекстаЗапроса.Вставить("ТекстОбесценение");
	
	Возврат ПараметрыТекстаЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

#КонецЕсли
