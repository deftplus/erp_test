#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("ОсновныеСредства");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("ПрослеживаемостьИмпортныхТоваров");
	МеханизмыДокумента.Добавить("ОбесценениеВНА");
	
	РазукомплектацияОСЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура:
//     * Ключ - Строка - Имя таблицы.
//     * Значение - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда

		ДопПараметры.ТолькоПомеченные = Регистры <> Неопределено;
		
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДокументыПоОС(Запрос, ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаСтоимостьОС(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаОС(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаОСУУ(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПервоначальныеСведенияОС(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаМестонахождениеОС(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПрослеживаемыеТоварыВСоставеОС(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаУзлыКомпонентыАмортизации(ТекстыЗапроса, Регистры);
		ТекстЗапросаСоставЕГДС(Запрос, ТекстыЗапроса, Регистры);
		
		РазукомплектацияОСЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);

	КонецЕсли;
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

// Формирует таблицы движений при отложенном проведении.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.РазукомплектацияОС - Документ, для которого формируются движения
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Содержит вспомогательные временные таблицы, которые могут использоваться для формирования движений.
//
// Возвращаемое значение:
//  см. ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения
//
Функция ТаблицыОтложенногоФормированияДвижений(ДокументСсылка, МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаСтоимостьОС(Запрос, ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаАмортизацияОС(Запрос, ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаОбесценениеВНА(Запрос, ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(Запрос, ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса);
	
	РазукомплектацияОСЛокализация.ТаблицыОтложенногоФормированияДвижений(Запрос, ТекстыЗапроса);

	ТаблицыДляДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(
							Запрос, ТекстыЗапроса, Неопределено);
	
	Возврат ТаблицыДляДвижений;
	
КонецФункции

#КонецОбласти

#Область Команды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.ИзменениеПараметровОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	Команда = ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаСписка";
	КонецЕсли;
	
	РазукомплектацияОСЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

// Добавляет команду создания документа "Разукомплектация ОС".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
//	СтрокаТаблицыЗначений - добавленная команда
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РазукомплектацияОС) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РазукомплектацияОС.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.РазукомплектацияОС);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьВнеоборотныеАктивы2_4";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	РазукомплектацияОСЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	РазукомплектацияОСЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Вспомогательные реквизиты.
// 
// Параметры:
//  Объект - ДанныеФормыСтруктура -
// 
// Возвращаемое значение:
//  ФиксированнаяСтруктура - 
Функция ВспомогательныеРеквизиты(Объект) Экспорт

	ВспомогательныеРеквизиты = Новый Структура;
	ВспомогательныеРеквизиты.Вставить("ОтражатьВРеглУчете", Объект.ОтражатьВРеглУчете);
	ВспомогательныеРеквизиты.Вставить("ОтражатьВУпрУчете", Объект.ОтражатьВУпрУчете);
	ВспомогательныеРеквизиты.Вставить("Организация", Объект.Организация);
	ВспомогательныеРеквизиты.Вставить("ХозяйственнаяОперация", Объект.ХозяйственнаяОперация);
	ВспомогательныеРеквизиты.Вставить("Дата", Объект.Дата);
	ВспомогательныеРеквизиты.Вставить("ВедетсяРегламентированныйУчетВНА", ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА());
	ВспомогательныеРеквизиты.Вставить("ИспользоватьРеглУчет", РеглУчетСервер.ВедетсяРеглУчет(Объект.Дата));

	ВалютаУпр  = Константы.ВалютаУправленческогоУчета.Получить();
	ВалютаРегл = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Объект.Организация);
	ВспомогательныеРеквизиты.Вставить("ВалютыСовпадают", ВалютаУпр = ВалютаРегл);
	
	РазукомплектацияОСЛокализация.ДополнитьВспомогательныеРеквизиты(Объект, ВспомогательныеРеквизиты);
	
	ВспомогательныеРеквизиты = Новый ФиксированнаяСтруктура(ВспомогательныеРеквизиты);
	
	Возврат ВспомогательныеРеквизиты;
	
КонецФункции

Функция ЗаполнитьРеквизитыВЗависимостиОтСвойств(Объект, ВспомогательныеРеквизиты, ПараметрыРеквизитовОбъекта) Экспорт

	ИзмененныеРеквизиты = Новый Массив;
	
	Для каждого ПараметрыРеквизита Из ПараметрыРеквизитовОбъекта Цикл
		
		Если ПараметрыРеквизита.Свойство = "Видимость" И ПараметрыРеквизита.Значение = Ложь
			ИЛИ ПараметрыРеквизита.Свойство = "ТолькоПросмотр" И ПараметрыРеквизита.Значение = Истина Тогда
			Действие = "Очистить";
		ИначеЕсли ПараметрыРеквизита.Свойство = "Видимость" И ПараметрыРеквизита.Значение = Истина Тогда
			Действие = "Заполнить";
		Иначе
			Продолжить;
		КонецЕсли;
		
		Если ПараметрыРеквизита.ИмяРеквизита = "МетодНачисленияАмортизацииУУ" Тогда
			
			ЗначениеРеквизита = Перечисления.СпособыНачисленияАмортизацииОС.Линейный;
			
			ВнеоборотныеАктивыСлужебный.ЗаполнитьОчиститьРеквизит(
				Объект,
				ПараметрыРеквизита.ИмяРеквизита,
				Действие,
				ЗначениеРеквизита,
				Неопределено,
				ИзмененныеРеквизиты);

		ИначеЕсли ПараметрыРеквизита.ИмяРеквизита = "СрокИспользованияУУ" Тогда
			
			ВнеоборотныеАктивыСлужебный.ЗаполнитьОчиститьРеквизит(
				Объект,
				ПараметрыРеквизита.ИмяРеквизита,
				Действие,
				Объект.СрокИспользованияБУ,
				0,
				ИзмененныеРеквизиты);

		ИначеЕсли ПараметрыРеквизита.ИмяРеквизита = "КоэффициентУскоренияУУ" Тогда
		
			ВнеоборотныеАктивыСлужебный.ЗаполнитьОчиститьРеквизит(
				Объект,
				ПараметрыРеквизита.ИмяРеквизита,
				Действие,
				1,
				0,
				ИзмененныеРеквизиты);
				
		КонецЕсли;
		
		РазукомплектацияОСЛокализация.ЗаполнитьРеквизитыВЗависимостиОтСвойств(
			Объект, ВспомогательныеРеквизиты, ПараметрыРеквизита, Действие, ИзмененныеРеквизиты);
			
	КонецЦикла; 
	
	Возврат СтрСоединить(ИзмененныеРеквизиты, ",");
	
КонецФункции

Процедура ЗаполнитьЗначенияПоУмолчанию(ДанныеСтроки, Объект) Экспорт
	
	Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыделениеУзловКомпонентовАмортизации Тогда
		
		Если НЕ ЗначениеЗаполнено(ДанныеСтроки.ГруппаФинансовогоУчета) Тогда
			ДанныеСтроки.ГруппаФинансовогоУчета = Справочники.ГруппыФинансовогоУчетаВнеоборотныхАктивов.ЗначениеПоУмолчанию(
													Перечисления.ВидыВнеоборотныхАктивов.ОсновноеСредство);
		КонецЕсли;
		
	КонецЕсли; 
	
	РазукомплектацияОСЛокализация.ЗаполнитьЗначенияПоУмолчанию(ДанныеСтроки, Объект);
	
КонецПроцедуры

Функция ПроверитьВыборОсновныхСредств(ОсновноеСредство, СписокОС, Дата) Экспорт

	ПараметрыВыбораОсновныхСредств = Новый Структура;
	ПараметрыВыбораОсновныхСредств.Вставить("ПроблемныеОС", Новый Массив);
	ПараметрыВыбораОсновныхСредств.Вставить("ЗначенияПараметров", Новый Структура);
	
	ТекстЗапроса = РазукомплектацияОСЛокализация.ТекстЗапросаДляПроверкиОС();
	
	Если ТекстЗапроса <> Неопределено Тогда
	
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("СписокОС", СписокОС);
		Запрос.УстановитьПараметр("ОсновноеСредство", ОсновноеСредство);
		Запрос.УстановитьПараметр("Дата", Дата);
		
		Результат = Запрос.ВыполнитьПакет();
		
		ВыборкаОС = Результат[0].Выбрать();
		ВыборкаОС.Выбрать();
		ВыборкаОС.Следующий();
		
		Выборка = Результат[1].Выбрать();
		Пока Выборка.Следующий() Цикл
			ПараметрыВыбораОсновныхСредств.ПроблемныеОС.Добавить(Выборка.Ссылка);
		КонецЦикла;
		
		РазукомплектацияОСЛокализация.ДополнитьРезультатПроверкиОС(Выборка, ВыборкаОС, ПараметрыВыбораОсновныхСредств);
		
	КонецЕсли;
	
	Возврат ПараметрыВыбораОсновныхСредств;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ПолноеИмяОбъекта()

	Возврат "Документ.РазукомплектацияОС";

КонецФункции

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.РазукомплектацияОС";
	ТекстыЗапросаВременныхТаблиц = Новый Соответствие();
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	
	ПереопределениеРасчетаПараметров = Новый Структура;
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		ВЗапросеЕстьИсточник = Ложь;
		
	ИначеЕсли ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоОС(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров,
										ТекстыЗапросаВременныхТаблиц);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка               КАК Ссылка,
	|	ДанныеДокумента.Дата                 КАК Период,
	|	ДанныеДокумента.Номер                КАК Номер,
	|	ДанныеДокумента.Организация          КАК Организация,
	|	ДанныеДокумента.Подразделение        КАК Подразделение,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.ОсновноеСредство     КАК ОсновноеСредство,
	|	ДанныеДокумента.СобытиеОС            КАК СобытиеОС,
	|	ДанныеДокумента.Ответственный        КАК Ответственный,
	|	ДанныеДокумента.Комментарий          КАК Комментарий,
	|	ДанныеДокумента.Проведен             КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления      КАК ПометкаУдаления,
	|	ДанныеДокумента.ОтражатьВРеглУчете   КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете    КАК ОтражатьВУпрУчете,
	|	НастройкиХозяйственныхОпераций.Ссылка  КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	Документ.РазукомплектацияОС КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиХозяйственныхОпераций КАК НастройкиХозяйственныхОпераций
	|		ПО ДанныеДокумента.ХозяйственнаяОперация = НастройкиХозяйственныхОпераций.ХозяйственнаяОперация
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(Реквизиты.Период));
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Реквизиты.Период));
	Запрос.УстановитьПараметр("НомерПакета", 0);
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.РазукомплектацияОС"));
	ЗначенияПараметровПроведения.Вставить("НазваниеДокумента", НСтр("ru = 'Разукомплектация ОС';
																	|en = 'Split fixed assets'"));
	ЗначенияПараметровПроведения.Вставить("СтатьяАП_ОС", ПланыВидовХарактеристик.СтатьиАктивовПассивов.ОсновныеСредства);
	ЗначенияПараметровПроведения.Вставить("ВедетсяРегламентированныйУчетВНА", ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА());

	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&Подразделение                          КАК Подразделение,
	|	&Ответственный                          КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	ЛОЖЬ                                    КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО                            КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО                            КАК ИсправляемыйДокумент,
	|	&Период                                 КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО                            КАК Приоритет";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоОС(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоОС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	0                                       КАК НомерЗаписи,
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК Дата,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Проведен                               КАК Проведен,
	|	&ОтражатьВРеглУчете                     КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                      КАК ОтражатьВУпрУчете,
	|	&СобытиеОС                              КАК СобытиеОС,
	|	&ОсновноеСредство                       КАК ОсновноеСредство
	|ИЗ
	|	Документ.РазукомплектацияОС КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И &ОсновноеСредство <> ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА &ОсновноеСредство <> ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка)
	|			ТОГДА ТаблицаОС.НомерСтроки
	|		ИНАЧЕ ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0)    
	|	КОНЕЦ                                   КАК НомерЗаписи,
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК Дата,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Проведен                               КАК Проведен,
	|	&ОтражатьВРеглУчете                     КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                      КАК ОтражатьВУпрУчете,
	|	&СобытиеОС                              КАК СобытиеОС,
	|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство
	|ИЗ
	|	Документ.РазукомплектацияОС КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И (&ОсновноеСредство = ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка)
	|			ИЛИ НЕ ТаблицаОС.Ссылка ЕСТЬ NULL)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаСтоимостьОС(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "СтоимостьОС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;

	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПустаяТаблицаСтоимостьВНА(ТекстыЗапроса, Запрос);
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтМестонахождениеОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	ВнеоборотныеАктивыЛокализация.ТекстЗапросаТаблицаВтНалогообложениеНДС(ТекстыЗапроса, ПолноеИмяОбъекта());
	ТекстЗапросаТаблицаВтКоэффициенты(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтСуммаКоэффициентов(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтВыделениеСтоимости(ТекстыЗапроса);
	
	ТекстЗапроса =
	// Принятие к учету ОС
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)       КАК ВидДвижения,
	|	&Период                                      КАК Период,
	|	&Организация                                 КАК Организация,
	|	&Подразделение                               КАК Подразделение,
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыделениеУзловКомпонентовАмортизации)
	|			ТОГДА ТаблицаОС.ГруппаФинансовогоУчета
	|		ИНАЧЕ ПорядокУчетаОС.ГруппаФинансовогоУчета
	|	КОНЕЦ                                        КАК ГруппаФинансовогоУчета,
	|	ПорядокУчетаОС.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	МестонахождениеОС.Арендатор                  КАК Арендатор,
	|	&ХозяйственнаяОперация                       КАК ХозяйственнаяОперация,
	|	&ОтражатьВРеглУчете                          КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                           КАК ОтражатьВУпрУчете,
	|	ТаблицаОС.ОсновноеСредство                   КАК ОсновноеСредство,
	|	втНалогообложениеНДС.НалогообложениеНДС      КАК НалогообложениеНДС,
	|
	|	&Подразделение                               КАК КорПодразделение,
	|	ПорядокУчетаОС.НаправлениеДеятельности       КАК КорНаправлениеДеятельности,
	|	НЕ ВТ_СтоимостьВНА.ОбъектУчета ЕСТЬ NULL     КАК РасчетСтоимости,
	|	ТаблицаОС.СтоимостьУУ                        КАК ПредварительнаяСтоимость,
	|	ТаблицаОС.СтоимостьБУ                        КАК ПредварительнаяСтоимостьРегл,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаОС.СтоимостьУУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Стоимость,
	|
	|	ВЫБОР 
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСЧастичная)
	|				И &ОтражатьВРеглУчете
	|			ТОГДА ТаблицаОС.СтоимостьБУ - ВЫРАЗИТЬ(ТаблицаОС.СтоимостьБУ * ЕСТЬNULL(ВТ_СтоимостьВНА.ДоляЦФБУ, 0) КАК ЧИСЛО(31,2))
	|
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСПолная)
	|				И &ОтражатьВРеглУчете
	|			ТОГДА ТаблицаОС.СтоимостьБУ - &СтоимостьЦФ
	|
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыделениеУзловКомпонентовАмортизации)
	|				И &ОтражатьВРеглУчете
	|			ТОГДА ТаблицаОС.СтоимостьБУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.СтоимостьНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьНУ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.СтоимостьПР
	|					- ВЫБОР 
	|						КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСЧастичная)
	|							ТОГДА ВЫРАЗИТЬ(ТаблицаОС.СтоимостьБУ * ЕСТЬNULL(ВТ_СтоимостьВНА.ДоляЦФБУ, 0) КАК ЧИСЛО(31,2))
	|
	|						КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСПолная)
	|							ТОГДА &СтоимостьЦФ
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьПР,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВЫБОР 
	|					КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСЧастичная)
	|						ТОГДА ТаблицаОС.СтоимостьБУ - ВЫРАЗИТЬ(ТаблицаОС.СтоимостьБУ * ЕСТЬNULL(ВТ_СтоимостьВНА.ДоляЦФБУ, 0) КАК ЧИСЛО(31,2))
	|
	|					КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыделениеУзловКомпонентовАмортизации)
	|						ТОГДА ТаблицаОС.СтоимостьБУ
	|
	|					КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСПолная)
	|						ТОГДА ТаблицаОС.СтоимостьБУ - &СтоимостьЦФ
	|					ИНАЧЕ 0
	|				КОНЕЦ 
	|					- ТаблицаОС.СтоимостьНУ 
	|					- (ТаблицаОС.СтоимостьПР
	|						- ВЫБОР 
	|							КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСЧастичная)
	|								ТОГДА ВЫРАЗИТЬ(ТаблицаОС.СтоимостьБУ * ЕСТЬNULL(ВТ_СтоимостьВНА.ДоляЦФБУ, 0) КАК ЧИСЛО(31,2))
	|
	|							КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСПолная)
	|								ТОГДА &СтоимостьЦФ
	|							ИНАЧЕ 0
	|						КОНЕЦ)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьВР,
	|
	|	ВЫБОР 
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСЧастичная)
	|				И &ОтражатьВРеглУчете 
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаОС.СтоимостьБУ * ЕСТЬNULL(ВТ_СтоимостьВНА.ДоляЦФБУ, 0) КАК ЧИСЛО(31,2))
	|
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСПолная)
	|				И &ОтражатьВРеглУчете 
	|			ТОГДА &СтоимостьЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьЦФ,
	|
	|	ВЫБОР 
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСЧастичная)
	|				И &ОтражатьВРеглУчете 
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаОС.СтоимостьБУ * ЕСТЬNULL(ВТ_СтоимостьВНА.ДоляЦФБУ, 0) КАК ЧИСЛО(31,2))
	|
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСПолная)
	|				И &ОтражатьВРеглУчете 
	|			ТОГДА &СтоимостьЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьПРЦФ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ВТ_СтоимостьВНА.РезервПереоценкиСтоимости
	|					* ВЫБОР КОГДА ЕСТЬNULL(ВТ_СтоимостьВНА.Стоимость, 0) = 0
	|						ТОГДА 1
	|						ИНАЧЕ ТаблицаОС.СтоимостьУУ / ЕСТЬNULL(ВТ_СтоимостьВНА.Стоимость, 0)
	|					КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РезервПереоценкиСтоимости,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВТ_СтоимостьВНА.РезервПереоценкиСтоимостиРегл
	|					* ВЫБОР КОГДА (ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьРегл, 0) + ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьЦФ, 0)) = 0
	|						ТОГДА 1
	|						ИНАЧЕ ТаблицаОС.СтоимостьБУ / (ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьРегл, 0) + ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьЦФ, 0))
	|					КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РезервПереоценкиСтоимостиРегл,
	|
	|	0 КАК ПриростСтоимости,
	|	0 КАК ПриростСтоимостиРегл,
	|	
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВыделениеСтоимостиОСПриРазукомплектации) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК ВТ_СтоимостьВНА
	|		ПО ВТ_СтоимостьВНА.ОбъектУчета = &ОсновноеСредство
	|			И ВТ_СтоимостьВНА.Ссылка = &Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втМестонахождениеОС КАК МестонахождениеОС
	|		ПО МестонахождениеОС.ОсновноеСредство = &ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО ПорядокУчетаОС.ОсновноеСредство = &ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втНалогообложениеНДС КАК втНалогообложениеНДС
	|		ПО втНалогообложениеНДС.ОсновноеСредство = &ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтКоэффициенты КАК ВтКоэффициенты
	|		ПО ВтКоэффициенты.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСуммаКоэффициентов КАК ВтСуммаКоэффициентов
	|		ПО ИСТИНА
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Уменьшение стоимости исходного ОС при частичной разукомплектации
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)       КАК ВидДвижения,
	|	&Период                                      КАК Период,
	|	&Организация                                 КАК Организация,
	|	&Подразделение                               КАК Подразделение,
	|	ПорядокУчетаОС.ГруппаФинансовогоУчета        КАК ГруппаФинансовогоУчета,
	|	ПорядокУчетаОС.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	МестонахождениеОС.Арендатор                  КАК Арендатор,
	|	&ХозяйственнаяОперация                       КАК ХозяйственнаяОперация,
	|	&ОтражатьВРеглУчете                          КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                           КАК ОтражатьВУпрУчете,
	|	&ОсновноеСредство                            КАК ОсновноеСредство,
	|	втНалогообложениеНДС.НалогообложениеНДС      КАК НалогообложениеНДС,
	|
	|	&Подразделение                               КАК КорПодразделение,
	|	ПорядокУчетаОС.НаправлениеДеятельности       КАК КорНаправлениеДеятельности,
	|	НЕ ВТ_СтоимостьВНА.ОбъектУчета ЕСТЬ NULL     КАК РасчетСтоимости,
	|	0                                            КАК ПредварительнаяСтоимость,
	|	0                                            КАК ПредварительнаяСтоимостьРегл,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаОС.СтоимостьУУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Стоимость,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.СтоимостьБУ - ВЫРАЗИТЬ(ТаблицаОС.СтоимостьБУ * ЕСТЬNULL(ВТ_СтоимостьВНА.ДоляЦФБУ, 0) КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.СтоимостьНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьНУ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.СтоимостьПР - ВЫРАЗИТЬ(ТаблицаОС.СтоимостьПР * ЕСТЬNULL(ВТ_СтоимостьВНА.ДоляЦФБУ, 0) КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьПР,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.СтоимостьБУ - ВЫРАЗИТЬ(ТаблицаОС.СтоимостьБУ * ЕСТЬNULL(ВТ_СтоимостьВНА.ДоляЦФБУ, 0) КАК ЧИСЛО(31,2))
	|					- ТаблицаОС.СтоимостьНУ
	|					- (ТаблицаОС.СтоимостьПР - ВЫРАЗИТЬ(ТаблицаОС.СтоимостьПР * ЕСТЬNULL(ВТ_СтоимостьВНА.ДоляЦФБУ, 0) КАК ЧИСЛО(31,2)))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьВР,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаОС.СтоимостьБУ * ЕСТЬNULL(ВТ_СтоимостьВНА.ДоляЦФБУ, 0) КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьЦФ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаОС.СтоимостьБУ * ЕСТЬNULL(ВТ_СтоимостьВНА.ДоляЦФБУ, 0) КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьПРЦФ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ВТ_СтоимостьВНА.РезервПереоценкиСтоимости
	|					* ВЫБОР КОГДА ЕСТЬNULL(ВТ_СтоимостьВНА.Стоимость, 0) = 0
	|						ТОГДА 1
	|						ИНАЧЕ ТаблицаОС.СтоимостьУУ / ЕСТЬNULL(ВТ_СтоимостьВНА.Стоимость, 0)
	|					КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РезервПереоценкиСтоимости,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВТ_СтоимостьВНА.РезервПереоценкиСтоимостиРегл
	|					* ВЫБОР КОГДА (ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьРегл, 0) + ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьЦФ, 0)) = 0
	|						ТОГДА 1
	|						ИНАЧЕ ТаблицаОС.СтоимостьБУ / (ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьРегл, 0) + ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьЦФ, 0))
	|					КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РезервПереоценкиСтоимостиРегл,
	|
	|	0 КАК ПриростСтоимости,
	|	0 КАК ПриростСтоимостиРегл,
	|	
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВыделениеСтоимостиОСПриРазукомплектации) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК ВТ_СтоимостьВНА
	|		ПО ВТ_СтоимостьВНА.ОбъектУчета = &ОсновноеСредство
	|			И ВТ_СтоимостьВНА.Ссылка = &Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втМестонахождениеОС КАК МестонахождениеОС
	|		ПО МестонахождениеОС.ОсновноеСредство = &ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО ПорядокУчетаОС.ОсновноеСредство = &ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ втНалогообложениеНДС КАК втНалогообложениеНДС
	|		ПО втНалогообложениеНДС.ОсновноеСредство = &ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСЧастичная), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыделениеУзловКомпонентовАмортизации))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Уменьшение стоимости исходного ОС при полной разукомплектации
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)       КАК ВидДвижения,
	|	&Период                                      КАК Период,
	|	&Организация                                 КАК Организация,
	|	&Подразделение                               КАК Подразделение,
	|	ПорядокУчетаОС.ГруппаФинансовогоУчета        КАК ГруппаФинансовогоУчета,
	|	ПорядокУчетаОС.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	МестонахождениеОС.Арендатор                  КАК Арендатор,
	|	&ХозяйственнаяОперация                       КАК ХозяйственнаяОперация,
	|	&ОтражатьВРеглУчете                          КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                           КАК ОтражатьВУпрУчете,
	|	&ОсновноеСредство                            КАК ОсновноеСредство,
	|	втНалогообложениеНДС.НалогообложениеНДС      КАК НалогообложениеНДС,
	|
	|	&Подразделение                               КАК КорПодразделение,
	|	ПорядокУчетаОС.НаправлениеДеятельности       КАК КорНаправлениеДеятельности,
	|	ИСТИНА                                       КАК РасчетСтоимости,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ВыделениеСтоимости.ПредварительнаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК ПредварительнаяСтоимость,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеСтоимости.ПредварительнаяСтоимостьРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК ПредварительнаяСтоимостьРегл,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ВыделениеСтоимости.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК Стоимость,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеСтоимости.СтоимостьРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК СтоимостьРегл,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеСтоимости.СтоимостьНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК СтоимостьНУ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеСтоимости.СтоимостьПР
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК СтоимостьПР,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеСтоимости.СтоимостьВР
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК СтоимостьВР,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеСтоимости.СтоимостьЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК СтоимостьЦФ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеСтоимости.СтоимостьПРЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК СтоимостьПРЦФ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ВыделениеСтоимости.РезервПереоценкиСтоимости
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК РезервПереоценкиСтоимости,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеСтоимости.РезервПереоценкиСтоимостиРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК РезервПереоценкиСтоимостиРегл,
	|
	|	0                                            КАК ПриростСтоимости,
	|	0                                            КАК ПриростСтоимостиРегл,
	|	
	|	ВыделениеСтоимости.ИдентификаторСтроки       КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВыделениеСтоимостиОСПриРазукомплектации) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ВтВыделениеСтоимости КАК ВыделениеСтоимости
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втМестонахождениеОС КАК МестонахождениеОС
	|		ПО МестонахождениеОС.ОсновноеСредство = &ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО ПорядокУчетаОС.ОсновноеСредство = &ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втНалогообложениеНДС КАК втНалогообложениеНДС
	|		ПО втНалогообложениеНДС.ОсновноеСредство = &ОсновноеСредство
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСПолная)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)       КАК ВидДвижения,
	|	&Период                                      КАК Период,
	|	&Организация                                 КАК Организация,
	|	&Подразделение                               КАК Подразделение,
	|	ПорядокУчетаОС.ГруппаФинансовогоУчета        КАК ГруппаФинансовогоУчета,
	|	ПорядокУчетаОС.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	МестонахождениеОС.Арендатор                  КАК Арендатор,
	|	&ХозяйственнаяОперация                       КАК ХозяйственнаяОперация,
	|	&ОтражатьВРеглУчете                          КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                           КАК ОтражатьВУпрУчете,
	|	&ОсновноеСредство                            КАК ОсновноеСредство,
	|	втНалогообложениеНДС.НалогообложениеНДС      КАК НалогообложениеНДС,
	|
	|	&Подразделение                               КАК КорПодразделение,
	|	ПорядокУчетаОС.НаправлениеДеятельности       КАК КорНаправлениеДеятельности,
	|	ЛОЖЬ                                         КАК РасчетСтоимости,
	|	0                                            КАК ПредварительнаяСтоимость,
	|	0                                            КАК ПредварительнаяСтоимостьРегл,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаОС.СтоимостьУУ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК Стоимость,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.СтоимостьБУ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК СтоимостьРегл,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.СтоимостьНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК СтоимостьНУ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.СтоимостьПР
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК СтоимостьПР,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.СтоимостьВР
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК СтоимостьВР,
	|	0                                            КАК СтоимостьЦФ,
	|	0                                            КАК СтоимостьПРЦФ,
	|	0                                            КАК РезервПереоценкиСтоимости,
	|	0                                            КАК РезервПереоценкиСтоимостиРегл,
	|	0                                            КАК ПриростСтоимости,
	|	0                                            КАК ПриростСтоимостиРегл,
	|	
	|	ТаблицаОС.ИдентификаторСтроки                КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВыделениеСтоимостиОСПриРазукомплектации) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК ВТ_СтоимостьВНА
	|		ПО ВТ_СтоимостьВНА.ОбъектУчета = &ОсновноеСредство
	|			И ВТ_СтоимостьВНА.Ссылка = &Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втМестонахождениеОС КАК МестонахождениеОС
	|		ПО МестонахождениеОС.ОсновноеСредство = &ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО ПорядокУчетаОС.ОсновноеСредство = &ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ втНалогообложениеНДС КАК втНалогообложениеНДС
	|		ПО втНалогообложениеНДС.ОсновноеСредство = &ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСПолная)
	|	И ВТ_СтоимостьВНА.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Прирост стоимости
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)       КАК ВидДвижения,
	|	&Период                                      КАК Период,
	|	&Организация                                 КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                 КАК Подразделение,
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыделениеУзловКомпонентовАмортизации)
	|			ТОГДА ТаблицаОС.ГруппаФинансовогоУчета
	|		ИНАЧЕ ПорядокУчетаОС.ГруппаФинансовогоУчета
	|	КОНЕЦ                                        КАК ГруппаФинансовогоУчета,
	|	ПорядокУчетаОС.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                 КАК Арендатор,
	|	&ХозяйственнаяОперация                       КАК ХозяйственнаяОперация,
	|	&ОтражатьВРеглУчете                          КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                           КАК ОтражатьВУпрУчете,
	|	ТаблицаОС.ОсновноеСредство                   КАК ОсновноеСредство,
	|	НЕОПРЕДЕЛЕНО                                 КАК НалогообложениеНДС,
	|
	|	НЕОПРЕДЕЛЕНО                                 КАК КорПодразделение,
	|	ПорядокУчетаОС.НаправлениеДеятельности       КАК КорНаправлениеДеятельности,
	|	НЕ ВТ_СтоимостьВНА.ОбъектУчета ЕСТЬ NULL     КАК РасчетСтоимости,
	|
	|	0                                            КАК ПредварительнаяСтоимость,
	|	0                                            КАК ПредварительнаяСтоимостьРегл,
	|	0                                            КАК Стоимость,
	|	0                                            КАК СтоимостьРегл,
	|	0                                            КАК СтоимостьНУ,
	|	0                                            КАК СтоимостьПР,
	|	0                                            КАК СтоимостьВР,
	|	0                                            КАК СтоимостьЦФ,
	|	0                                            КАК СтоимостьПРЦФ,
	|	0                                            КАК РезервПереоценкиСтоимости,
	|	0                                            КАК РезервПереоценкиСтоимостиРегл,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ВТ_СтоимостьВНА.ПриростСтоимости
	|					* ВЫБОР КОГДА ЕСТЬNULL(ВТ_СтоимостьВНА.Стоимость, 0) = 0
	|						ТОГДА 1
	|						ИНАЧЕ ТаблицаОС.СтоимостьУУ / ЕСТЬNULL(ВТ_СтоимостьВНА.Стоимость, 0)
	|					КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПриростСтоимости,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВТ_СтоимостьВНА.ПриростСтоимостиРегл
	|					* ВЫБОР КОГДА (ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьРегл, 0) + ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьЦФ, 0)) = 0
	|						ТОГДА 1
	|						ИНАЧЕ ТаблицаОС.СтоимостьБУ / (ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьРегл, 0) + ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьЦФ, 0))
	|					КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПриростСтоимостиРегл,
	|	
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВыделениеСтоимостиОСПриРазукомплектации) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК ВТ_СтоимостьВНА
	|		ПО ВТ_СтоимостьВНА.ОбъектУчета = &ОсновноеСредство
	|			И ВТ_СтоимостьВНА.Ссылка = &Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втМестонахождениеОС КАК МестонахождениеОС
	|		ПО МестонахождениеОС.ОсновноеСредство = &ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО ПорядокУчетаОС.ОсновноеСредство = &ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И (&ОтражатьВУпрУчете И ВТ_СтоимостьВНА.ПриростСтоимости <> 0
	|		ИЛИ &ОтражатьВРеглУчете И ВТ_СтоимостьВНА.ПриростСтоимостиРегл <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)       КАК ВидДвижения,
	|	&Период                                      КАК Период,
	|	&Организация                                 КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                 КАК Подразделение,
	|	ПорядокУчетаОС.ГруппаФинансовогоУчета        КАК ГруппаФинансовогоУчета,
	|	ПорядокУчетаОС.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                 КАК Арендатор,
	|	&ХозяйственнаяОперация                       КАК ХозяйственнаяОперация,
	|	&ОтражатьВРеглУчете                          КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                           КАК ОтражатьВУпрУчете,
	|	&ОсновноеСредство                            КАК ОсновноеСредство,
	|	НЕОПРЕДЕЛЕНО                                 КАК НалогообложениеНДС,
	|
	|	&Подразделение                               КАК КорПодразделение,
	|	ПорядокУчетаОС.НаправлениеДеятельности       КАК КорНаправлениеДеятельности,
	|	НЕ ВТ_СтоимостьВНА.ОбъектУчета ЕСТЬ NULL     КАК РасчетСтоимости,
	|
	|	0                                            КАК ПредварительнаяСтоимость,
	|	0                                            КАК ПредварительнаяСтоимостьРегл,
	|	0                                            КАК Стоимость,
	|	0                                            КАК СтоимостьРегл,
	|	0                                            КАК СтоимостьНУ,
	|	0                                            КАК СтоимостьПР,
	|	0                                            КАК СтоимостьВР,
	|	0                                            КАК СтоимостьЦФ,
	|	0                                            КАК СтоимостьПРЦФ,
	|	0                                            КАК РезервПереоценкиСтоимости,
	|	0                                            КАК РезервПереоценкиСтоимостиРегл,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ВТ_СтоимостьВНА.ПриростСтоимости
	|					* ВЫБОР КОГДА ЕСТЬNULL(ВТ_СтоимостьВНА.Стоимость, 0) = 0
	|						ТОГДА 1
	|						ИНАЧЕ ТаблицаОС.СтоимостьУУ / ЕСТЬNULL(ВТ_СтоимостьВНА.Стоимость, 0)
	|					КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПриростСтоимости,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВТ_СтоимостьВНА.ПриростСтоимостиРегл
	|					* ВЫБОР КОГДА (ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьРегл, 0) + ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьЦФ, 0)) = 0
	|						ТОГДА 1
	|						ИНАЧЕ ТаблицаОС.СтоимостьБУ / (ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьРегл, 0) + ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьЦФ, 0))
	|					КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПриростСтоимостиРегл,
	|	
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВыделениеСтоимостиОСПриРазукомплектации) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК ВТ_СтоимостьВНА
	|		ПО ВТ_СтоимостьВНА.ОбъектУчета = &ОсновноеСредство
	|			И ВТ_СтоимостьВНА.Ссылка = &Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втМестонахождениеОС КАК МестонахождениеОС
	|		ПО МестонахождениеОС.ОсновноеСредство = &ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО ПорядокУчетаОС.ОсновноеСредство = &ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И (&ОтражатьВУпрУчете И ВТ_СтоимостьВНА.ПриростСтоимости <> 0
	|		ИЛИ &ОтражатьВРеглУчете И ВТ_СтоимостьВНА.ПриростСтоимостиРегл <> 0)";
	
	ТекстРаспределенияСуммы = ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы(
		"ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьЦФ, 0)", 
		"ТаблицаОС.СтоимостьБУ", 
		"ВтКоэффициенты.СуммаКоэффициентовПредыдущихСтрокБУ", 
		"ВтСуммаКоэффициентов.СуммаКоэффициентовБУ");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СтоимостьЦФ", ТекстРаспределенияСуммы);
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаАмортизацияОС(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "АмортизацияОС";
	
	ТекстЗапросаТаблицаВтВыделениеАмортизации(ТекстыЗапроса);
	
	ТекстЗапроса =
	// Увеличение амортизации новых ОС
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	&Ссылка                                         КАК Регистратор,
	|	&Период                                         КАК Период,
	|	&Организация                                    КАК Организация,
	|	&Подразделение                                  КАК Подразделение,
	|	&ХозяйственнаяОперация                          КАК ХозяйственнаяОперация,
	|	ТаблицаОС.ОсновноеСредство                      КАК ОсновноеСредство,
	|	ВыделениеАмортизации.ГруппаФинансовогоУчета     КАК ГруппаФинансовогоУчета,
	|	ВыделениеАмортизации.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|	ВыделениеАмортизации.НачислятьИзнос             КАК НачислятьИзнос,
	|	
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ВыделениеАмортизации.Амортизация
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Амортизация,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеАмортизации.АмортизацияРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияРегл,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеАмортизации.АмортизацияНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияНУ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеАмортизации.АмортизацияПР
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияПР,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеАмортизации.АмортизацияВР
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияВР,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеАмортизации.АмортизацияЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеАмортизации.АмортизацияНУЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияНУЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеАмортизации.АмортизацияПРЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияПРЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеАмортизации.АмортизацияВРЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияВРЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ВыделениеАмортизации.РезервПереоценкиАмортизации
	|		ИНАЧЕ 0
	|	КОНЕЦ     КАК РезервПереоценкиАмортизации,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеАмортизации.РезервПереоценкиАмортизацииРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РезервПереоценкиАмортизацииРегл,
	|
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВыделениеАмортизацииОСПриРазукомплектации) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтВыделениеАмортизации КАК ВыделениеАмортизации
	|		ПО ВыделениеАмортизации.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыделениеУзловКомпонентовАмортизации)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Уменьшение амортизации исходного ОС
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	&Ссылка                                         КАК Регистратор,
	|	&Период                                         КАК Период,
	|	&Организация                                    КАК Организация,
	|	&Подразделение                                  КАК Подразделение,
	|	&ХозяйственнаяОперация                          КАК ХозяйственнаяОперация,
	|	&ОсновноеСредство                               КАК ОсновноеСредство,
	|	ВыделениеАмортизации.ГруппаФинансовогоУчета     КАК ГруппаФинансовогоУчета,
	|	ВыделениеАмортизации.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|	ВыделениеАмортизации.НачислятьИзнос             КАК НачислятьИзнос,
	|	
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ВыделениеАмортизации.Амортизация
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Амортизация,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеАмортизации.АмортизацияРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияРегл,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеАмортизации.АмортизацияНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияНУ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеАмортизации.АмортизацияПР
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияПР,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеАмортизации.АмортизацияВР
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияВР,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеАмортизации.АмортизацияЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеАмортизации.АмортизацияНУЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияНУЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеАмортизации.АмортизацияПРЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияПРЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеАмортизации.АмортизацияВРЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияВРЦФ,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ВыделениеАмортизации.РезервПереоценкиАмортизации
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РезервПереоценкиАмортизации,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеАмортизации.РезервПереоценкиАмортизацииРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РезервПереоценкиАмортизацииРегл,
	|
	|	ВыделениеАмортизации.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВыделениеАмортизацииОСПриРазукомплектации) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ВтВыделениеАмортизации КАК ВыделениеАмортизации
	|ГДЕ
	|	&ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыделениеУзловКомпонентовАмортизации)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаОбесценениеВНА(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ОбесценениеВНА";
	
	ТекстЗапросаТаблицаВтВыделениеОбесценения(ТекстыЗапроса);
	
	ТекстЗапроса =
	// Увеличение обесценения новых ОС
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	&Ссылка                                         КАК Регистратор,
	|	&Период                                         КАК Период,
	|	&Организация                                    КАК Организация,
	|	&Подразделение                                  КАК Подразделение,
	|	&ХозяйственнаяОперация                          КАК ХозяйственнаяОперация,
	|	ТаблицаОС.ОсновноеСредство                      КАК ВнеоборотныйАктив,
	|	ВыделениеОбесценения.ГруппаФинансовогоУчета     КАК ГруппаФинансовогоУчета,
	|	ВыделениеОбесценения.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|	ВыделениеОбесценения.ЕГДС                       КАК ЕГДС,
	|	
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ВыделениеОбесценения.ОбесценениеУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОбесценениеУпр,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеОбесценения.ОбесценениеРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОбесценениеРегл,
	|
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.РеклассификацияОС) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтВыделениеОбесценения КАК ВыделениеОбесценения
	|		ПО ВыделениеОбесценения.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыделениеУзловКомпонентовАмортизации)
	|	И (ВыделениеОбесценения.ОбесценениеУпр <> 0 
	|		ИЛИ ВыделениеОбесценения.ОбесценениеРегл <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Уменьшение обесценения исходного ОС
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	&Ссылка                                         КАК Регистратор,
	|	&Период                                         КАК Период,
	|	&Организация                                    КАК Организация,
	|	&Подразделение                                  КАК Подразделение,
	|	&ХозяйственнаяОперация                          КАК ХозяйственнаяОперация,
	|	&ОсновноеСредство                               КАК ВнеоборотныйАктив,
	|	ВыделениеОбесценения.ГруппаФинансовогоУчета     КАК ГруппаФинансовогоУчета,
	|	ВыделениеОбесценения.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|	ВыделениеОбесценения.ЕГДС                       КАК ЕГДС,
	|	
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ВыделениеОбесценения.ОбесценениеУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОбесценениеУпр,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ВыделениеОбесценения.ОбесценениеРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОбесценениеРегл,
	|
	|	ВыделениеОбесценения.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.РеклассификацияОС) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ВтВыделениеОбесценения КАК ВыделениеОбесценения
	|ГДЕ
	|	&ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыделениеУзловКомпонентовАмортизации)
	|	И (ВыделениеОбесценения.ОбесценениеУпр <> 0 
	|		ИЛИ ВыделениеОбесценения.ОбесценениеРегл <> 0)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса)

	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтВыделениеАмортизации(ТекстыЗапроса);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                           КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыделениеАмортизацииОСПриРазукомплектации) КАК ХозяйственнаяОперация,
	|	&Организация                      КАК Организация,
	|
	|	&Подразделение                    КАК Подразделение,
	|	АмортизацияОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК Статья,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК АналитикаАктивовПассивов,
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыделениеУзловКомпонентовАмортизации)
	|			ТОГДА ТаблицаОС.ГруппаФинансовогоУчета
	|		ИНАЧЕ АмортизацияОС.ГруппаФинансовогоУчета
	|	КОНЕЦ                             КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                    КАК КорПодразделение,
	|	АмортизацияОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаРасходов,
	|	&ОсновноеСредство                 КАК КорАналитикаАктивовПассивов,
	|	АмортизацияОС.ГруппаФинансовогоУчета КАК КорГруппаФинансовогоУчета,
	|
	|	&Организация                      КАК КорОрганизация,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА АмортизацияОС.Амортизация
	|		ИНАЧЕ 0
	|	КОНЕЦ                             КАК Сумма,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА АмортизацияОС.Амортизация
	|		ИНАЧЕ 0
	|	КОНЕЦ                             КАК СуммаУпр,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА АмортизацияОС.АмортизацияРегл + АмортизацияОС.АмортизацияЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ                             КАК СуммаРегл
	|ИЗ
	|	Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтВыделениеАмортизации КАК АмортизацияОС
	|		ПО АмортизацияОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                           КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыделениеСтоимостиОСПриРазукомплектации) КАК ХозяйственнаяОперация,
	|	&Организация                      КАК Организация,
	|
	|	&Подразделение                    КАК Подразделение,
	|	ПорядокУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК Статья,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаРасходов,
	|	&ОсновноеСредство                 КАК АналитикаАктивовПассивов,
	|	ПорядокУчетаОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                    КАК КорПодразделение,
	|	ПорядокУчетаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК КорАналитикаАктивовПассивов,
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыделениеУзловКомпонентовАмортизации)
	|			ТОГДА ТаблицаОС.ГруппаФинансовогоУчета
	|		ИНАЧЕ ПорядокУчетаОС.ГруппаФинансовогоУчета
	|	КОНЕЦ                             КАК КорГруппаФинансовогоУчета,
	|
	|	&Организация                      КАК КорОрганизация,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаОС.СтоимостьУУ
	|		ИНАЧЕ 0
	|	КОНЕЦ                             КАК Сумма,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаОС.СтоимостьУУ
	|		ИНАЧЕ 0
	|	КОНЕЦ                             КАК СуммаУпр,
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаОС.СтоимостьБУ
	|		ИНАЧЕ 0
	|	КОНЕЦ                             КАК СуммаРегл
	|ИЗ
	|	Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО ПорядокУчетаОС.ОсновноеСредство = &ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаОС(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПорядокУчетаОС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                 КАК Период,
	|	&Организация                            КАК Организация,
	|	&ОтражатьВРеглУчете                     КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                      КАК ОтражатьВУпрУчете,
	|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство,
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыделениеУзловКомпонентовАмортизации)
	|			ТОГДА ТаблицаОС.ГруппаФинансовогоУчета
	|		ИНАЧЕ ПорядокУчетаОС.ГруппаФинансовогоУчета
	|	КОНЕЦ                                   КАК ГруппаФинансовогоУчета,
	|	ПорядокУчетаОС.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыделениеУзловКомпонентовАмортизации)
	|			ТОГДА ТаблицаОС.ПоказательНаработки
	|		ИНАЧЕ ПорядокУчетаОС.ПоказательНаработки
	|	КОНЕЦ                                   КАК ПоказательНаработки,
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыделениеУзловКомпонентовАмортизации)
	|			ТОГДА ТаблицаОС.ОбъемНаработки
	|		ИНАЧЕ ПорядокУчетаОС.ОбъемНаработки
	|	КОНЕЦ                                   КАК ОбъемНаработки
	|ИЗ
	|	Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО ПорядокУчетаОС.ОсновноеСредство = &ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаОСУУ(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПорядокУчетаОСУУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                                КАК Период,
	|	&Организация                                           КАК Организация,
	|	ТаблицаОС.ОсновноеСредство                             КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)       КАК Состояние,
	|	ПорядокУчета.НачислятьАмортизациюУУ                    КАК НачислятьАмортизациюУУ,
	|	ПорядокУчета.СпособОтраженияРасходов                   КАК СпособОтраженияРасходов,
	|	ПорядокУчета.СтатьяРасходов                            КАК СтатьяРасходов,
	|	ПорядокУчета.АналитикаРасходов                         КАК АналитикаРасходов,
	|	ПорядокУчета.ПередаватьРасходыВДругуюОрганизацию       КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ПорядокУчета.ОрганизацияПолучательРасходов             КАК ОрганизацияПолучательРасходов
	|ИЗ
	|	Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(
	|			&Период,
	|			Регистратор <> &Ссылка
	|				И Организация = &Организация
	|				И ОсновноеСредство = &ОсновноеСредство) КАК ПорядокУчета
	|		ПО ПорядокУчета.ОсновноеСредство = &ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ОтражатьВУпрУчете
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                                КАК Период,
	|	&Организация                                           КАК Организация,
	|	&ОсновноеСредство                                      КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета)         КАК Состояние,
	|	ЛОЖЬ                                                   КАК НачислятьАмортизациюУУ,
	|	НЕОПРЕДЕЛЕНО                                           КАК СпособОтраженияРасходов,
	|	НЕОПРЕДЕЛЕНО                                           КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                                           КАК АналитикаРасходов,
	|	ЛОЖЬ                                                   КАК ПередаватьРасходыВДругуюОрганизацию,
	|	НЕОПРЕДЕЛЕНО                                           КАК ОрганизацияПолучательРасходов
	|ГДЕ
	|	&ОтражатьВУпрУчете
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСПолная)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПараметрыАмортизацииОСУУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПустаяТаблицаСтоимостьВНА(ТекстыЗапроса, Запрос);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                                КАК Период,
	|	&Организация                                           КАК Организация,
	|	ТаблицаОС.ОсновноеСредство                             КАК ОсновноеСредство,
	|	ТаблицаРегистра.ДатаПоследнегоИзменения                КАК ДатаПоследнегоИзменения,
	|	ТаблицаРегистра.СрокИспользования                      КАК СрокИспользования,
	|	ТаблицаРегистра.КоэффициентУскорения                   КАК КоэффициентУскорения,
	|	ТаблицаРегистра.МетодНачисленияАмортизации             КАК МетодНачисленияАмортизации,
	|	ТаблицаОС.ЛиквидационнаяСтоимость                      КАК ЛиквидационнаяСтоимость,
	|	ТаблицаОС.ЛиквидационнаяСтоимостьРегл                  КАК ЛиквидационнаяСтоимостьРегл,
	|	ТаблицаРегистра.СрокИспользованияДляВычисленияАмортизации   КАК СрокИспользованияДляВычисленияАмортизации,
	|	ТаблицаРегистра.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|
	// СтоимостьДляВычисленияАмортизации
	|	ВЫБОР 
	|		КОГДА ТаблицаРегистра.СрокИспользования <> ТаблицаРегистра.СрокИспользованияДляВычисленияАмортизации
	|			ТОГДА 
	|				ВЫБОР
	|					КОГДА ЕСТЬNULL(ВТ_СтоимостьВНА.Стоимость, 0) = 0
	|						ТОГДА 0
	|					ИНАЧЕ ТаблицаРегистра.СтоимостьДляВычисленияАмортизации 
	|							* ТаблицаОС.СтоимостьУУ / ЕСТЬNULL(ВТ_СтоимостьВНА.Стоимость, 0)
	|				КОНЕЦ
	|		ИНАЧЕ ТаблицаОС.СтоимостьУУ
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизации,
	|
	// СтоимостьДляВычисленияАмортизацииРегл
	|	ВЫБОР 
	|		КОГДА &ВедетсяРегламентированныйУчетВНА
	|			ТОГДА 0
	|		КОГДА ТаблицаРегистра.СрокИспользования <> ТаблицаРегистра.СрокИспользованияДляВычисленияАмортизации
	|			ТОГДА 
	|				ВЫБОР
	|					КОГДА ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьРегл, 0) = 0
	|						ТОГДА 0
	|					ИНАЧЕ ТаблицаРегистра.СтоимостьДляВычисленияАмортизацииРегл 
	|							* ТаблицаОС.СтоимостьБУ / ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьРегл, 0)
	|				КОНЕЦ
	|		ИНАЧЕ ТаблицаОС.СтоимостьБУ
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизацииРегл
	|ИЗ
	|	Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК ВТ_СтоимостьВНА
	|		ПО ВТ_СтоимостьВНА.ОбъектУчета = &ОсновноеСредство
	|			И ВТ_СтоимостьВНА.Ссылка = &Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСУУ.СрезПоследних(
	|				&Период,
	|				Регистратор <> &Ссылка
	|					И Организация = &Организация
	|					И ОсновноеСредство = &ОсновноеСредство) КАК ТаблицаРегистра
	|		ПО (ТаблицаРегистра.ОсновноеСредство = &ОсновноеСредство)
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ОтражатьВУпрУчете
	|	И &ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыделениеУзловКомпонентовАмортизации)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                                КАК Период,
	|	&Организация                                           КАК Организация,
	|	ТаблицаОС.ОсновноеСредство                             КАК ОсновноеСредство,
	|	ТаблицаРегистра.ДатаПоследнегоИзменения                КАК ДатаПоследнегоИзменения,
	|	ТаблицаОС.СрокИспользованияУУ                          КАК СрокИспользования,
	|	ТаблицаОС.КоэффициентУскоренияУУ                       КАК КоэффициентУскорения,
	|	ТаблицаОС.МетодНачисленияАмортизацииУУ                 КАК МетодНачисленияАмортизации,
	|	ТаблицаОС.ЛиквидационнаяСтоимость                      КАК ЛиквидационнаяСтоимость,
	|	ТаблицаОС.ЛиквидационнаяСтоимостьРегл                  КАК ЛиквидационнаяСтоимостьРегл,
	|	ТаблицаОС.СрокИспользованияУУ                          КАК СрокИспользованияДляВычисленияАмортизации,
	|	ТаблицаОС.ОбъемНаработки                               КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|
	// СтоимостьДляВычисленияАмортизации
	|	ТаблицаОС.СтоимостьУУ                                  КАК СтоимостьДляВычисленияАмортизации,
	|
	// СтоимостьДляВычисленияАмортизацииРегл
	|	ВЫБОР 
	|		КОГДА &ВедетсяРегламентированныйУчетВНА
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаОС.СтоимостьБУ
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизацииРегл
	|ИЗ
	|	Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСУУ.СрезПоследних(
	|				&Период,
	|				Регистратор <> &Ссылка
	|					И Организация = &Организация
	|					И ОсновноеСредство = &ОсновноеСредство) КАК ТаблицаРегистра
	|		ПО (ТаблицаРегистра.ОсновноеСредство = &ОсновноеСредство)
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ОтражатьВУпрУчете
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыделениеУзловКомпонентовАмортизации)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                                КАК Период,
	|	&Организация                                           КАК Организация,
	|	&ОсновноеСредство                                      КАК ОсновноеСредство,
	|	ТаблицаРегистра.ДатаПоследнегоИзменения                КАК ДатаПоследнегоИзменения,
	|	ТаблицаРегистра.СрокИспользования                      КАК СрокИспользования,
	|	ТаблицаРегистра.КоэффициентУскорения                   КАК КоэффициентУскорения,
	|	ТаблицаРегистра.МетодНачисленияАмортизации             КАК МетодНачисленияАмортизации,
	|	ТаблицаРегистра.ЛиквидационнаяСтоимость                КАК ЛиквидационнаяСтоимость,
	|	ТаблицаРегистра.ЛиквидационнаяСтоимостьРегл            КАК ЛиквидационнаяСтоимостьРегл,
	|
	|	ТаблицаРегистра.СрокИспользованияДляВычисленияАмортизации   КАК СрокИспользованияДляВычисленияАмортизации,
	|	ТаблицаРегистра.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|
	// СтоимостьДляВычисленияАмортизации
	|	ТаблицаРегистра.СтоимостьДляВычисленияАмортизации
	|		- СУММА(ВЫБОР 
	|					КОГДА ТаблицаРегистра.СрокИспользования <> ТаблицаРегистра.СрокИспользованияДляВычисленияАмортизации
	|						ТОГДА ВЫБОР
	|								КОГДА ЕСТЬNULL(ВТ_СтоимостьВНА.Стоимость, 0) = 0
	|									ТОГДА 0
	|								ИНАЧЕ ВЫРАЗИТЬ(ТаблицаРегистра.СтоимостьДляВычисленияАмортизации 
	|												* ТаблицаОС.СтоимостьУУ / ЕСТЬNULL(ВТ_СтоимостьВНА.Стоимость, 0) КАК ЧИСЛО(31, 2))
	|							КОНЕЦ
	|					ИНАЧЕ  
	|						ТаблицаОС.СтоимостьУУ
	|				КОНЕЦ) КАК СтоимостьДляВычисленияАмортизации,
	|
	// СтоимостьДляВычисленияАмортизацииРегл
	|	ТаблицаРегистра.СтоимостьДляВычисленияАмортизацииРегл
	|		- СУММА(ВЫБОР
	|					КОГДА &ВедетсяРегламентированныйУчетВНА
	|						ТОГДА 0
	|					КОГДА ТаблицаРегистра.СрокИспользования <> ТаблицаРегистра.СрокИспользованияДляВычисленияАмортизации
	|						ТОГДА ВЫБОР
	|								КОГДА ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьРегл, 0) = 0
	|									ТОГДА 0
	|								ИНАЧЕ ВЫРАЗИТЬ(ТаблицаРегистра.СтоимостьДляВычисленияАмортизацииРегл 
	|												* ТаблицаОС.СтоимостьБУ / ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьРегл, 0) КАК ЧИСЛО(31, 2))
	|							КОНЕЦ
	|					ИНАЧЕ  
	|						ТаблицаОС.СтоимостьБУ
	|				КОНЕЦ) КАК СтоимостьДляВычисленияАмортизацииРегл
	|ИЗ
	|	РегистрСведений.ПараметрыАмортизацииОСУУ.СрезПоследних(
	|				&Период,
	|				Регистратор <> &Ссылка
	|					И Организация = &Организация
	|					И ОсновноеСредство = &ОсновноеСредство) КАК ТаблицаРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК ВТ_СтоимостьВНА
	|		ПО ВТ_СтоимостьВНА.ОбъектУчета = &ОсновноеСредство
	|			И ВТ_СтоимостьВНА.Ссылка = &Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = &Ссылка
	|ГДЕ
	|	&ОтражатьВУпрУчете
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСЧастичная)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРегистра.ДатаПоследнегоИзменения,
	|	ТаблицаРегистра.СрокИспользования,
	|	ТаблицаРегистра.КоэффициентУскорения,
	|	ТаблицаРегистра.МетодНачисленияАмортизации,
	|	ТаблицаРегистра.ЛиквидационнаяСтоимость,
	|	ТаблицаРегистра.ЛиквидационнаяСтоимостьРегл,
	|	ТаблицаРегистра.СрокИспользованияДляВычисленияАмортизации,
	|	ТаблицаРегистра.ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ТаблицаРегистра.СтоимостьДляВычисленияАмортизации,
	|	ТаблицаРегистра.СтоимостьДляВычисленияАмортизацииРегл
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                                КАК Период,
	|	&Организация                                           КАК Организация,
	|	&ОсновноеСредство                                      КАК ОсновноеСредство,
	|	ТаблицаРегистра.ДатаПоследнегоИзменения                КАК ДатаПоследнегоИзменения,
	|	ТаблицаРегистра.СрокИспользования                      КАК СрокИспользования,
	|	ТаблицаРегистра.КоэффициентУскорения                   КАК КоэффициентУскорения,
	|	ТаблицаРегистра.МетодНачисленияАмортизации             КАК МетодНачисленияАмортизации,
	|	ТаблицаРегистра.ЛиквидационнаяСтоимость - СУММА(ТаблицаОС.ЛиквидационнаяСтоимость)         КАК ЛиквидационнаяСтоимость,
	|	ТаблицаРегистра.ЛиквидационнаяСтоимостьРегл - СУММА(ТаблицаОС.ЛиквидационнаяСтоимостьРегл) КАК ЛиквидационнаяСтоимостьРегл,
	|
	|	ТаблицаРегистра.СрокИспользованияДляВычисленияАмортизации   КАК СрокИспользованияДляВычисленияАмортизации,
	|	ТаблицаРегистра.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|
	// СтоимостьДляВычисленияАмортизации
	|	ТаблицаРегистра.СтоимостьДляВычисленияАмортизации
	|		- СУММА(ВЫБОР 
	|					КОГДА ТаблицаРегистра.СрокИспользования <> ТаблицаРегистра.СрокИспользованияДляВычисленияАмортизации
	|						ТОГДА ВЫБОР
	|								КОГДА ЕСТЬNULL(ВТ_СтоимостьВНА.Стоимость, 0) = 0
	|									ТОГДА 0
	|								ИНАЧЕ ВЫРАЗИТЬ(ТаблицаРегистра.СтоимостьДляВычисленияАмортизации 
	|												* ТаблицаОС.СтоимостьУУ / ЕСТЬNULL(ВТ_СтоимостьВНА.Стоимость, 0) КАК ЧИСЛО(31, 2))
	|							КОНЕЦ
	|					ИНАЧЕ  
	|						ТаблицаОС.СтоимостьУУ
	|				КОНЕЦ) КАК СтоимостьДляВычисленияАмортизации,
	|
	// СтоимостьДляВычисленияАмортизацииРегл
	|	ТаблицаРегистра.СтоимостьДляВычисленияАмортизацииРегл
	|		- СУММА(ВЫБОР
	|					КОГДА &ВедетсяРегламентированныйУчетВНА
	|						ТОГДА 0
	|					КОГДА ТаблицаРегистра.СрокИспользования <> ТаблицаРегистра.СрокИспользованияДляВычисленияАмортизации
	|						ТОГДА ВЫБОР
	|								КОГДА ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьРегл, 0) = 0
	|									ТОГДА 0
	|								ИНАЧЕ ВЫРАЗИТЬ(ТаблицаРегистра.СтоимостьДляВычисленияАмортизацииРегл 
	|												* ТаблицаОС.СтоимостьБУ / ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьРегл, 0) КАК ЧИСЛО(31, 2))
	|							КОНЕЦ
	|					ИНАЧЕ  
	|						ТаблицаОС.СтоимостьБУ
	|				КОНЕЦ) КАК СтоимостьДляВычисленияАмортизацииРегл
	|ИЗ
	|	РегистрСведений.ПараметрыАмортизацииОСУУ.СрезПоследних(
	|				&Период,
	|				Регистратор <> &Ссылка
	|					И Организация = &Организация
	|					И ОсновноеСредство = &ОсновноеСредство) КАК ТаблицаРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК ВТ_СтоимостьВНА
	|		ПО ВТ_СтоимостьВНА.ОбъектУчета = &ОсновноеСредство
	|			И ВТ_СтоимостьВНА.Ссылка = &Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = &Ссылка
	|ГДЕ
	|	&ОтражатьВУпрУчете
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыделениеУзловКомпонентовАмортизации)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРегистра.ДатаПоследнегоИзменения,
	|	ТаблицаРегистра.СрокИспользования,
	|	ТаблицаРегистра.КоэффициентУскорения,
	|	ТаблицаРегистра.МетодНачисленияАмортизации,
	|	ТаблицаРегистра.ЛиквидационнаяСтоимость,
	|	ТаблицаРегистра.ЛиквидационнаяСтоимостьРегл,
	|	ТаблицаРегистра.СрокИспользованияДляВычисленияАмортизации,
	|	ТаблицаРегистра.ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ТаблицаРегистра.СтоимостьДляВычисленияАмортизации,
	|	ТаблицаРегистра.СтоимостьДляВычисленияАмортизацииРегл";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПервоначальныеСведенияОС(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПервоначальныеСведенияОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтСписокНовыхОС(ТекстыЗапроса);
	
	ТекстЗапроса = РазукомплектацияОСЛокализация.ТекстЗапросаТаблицаПервоначальныеСведенияОС();
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	&Период                                                 КАК Период,
		|	&Организация                                            КАК Организация,
		|	ТаблицаОС.ОсновноеСредство                              КАК ОсновноеСредство,
		|	ТаблицаРегистра.СпособПоступления                       КАК СпособПоступления,
		|	ТаблицаРегистра.Контрагент                              КАК Контрагент,
		|		
		|	ТаблицаРегистра.ПорядокУчетаУУ                          КАК ПорядокУчетаУУ,
		|	ТаблицаОС.СтоимостьБУ                                   КАК ПервоначальнаяСтоимостьБУ,
		|	ТаблицаОС.СтоимостьУУ                                   КАК ПервоначальнаяСтоимостьУУ,
		|	ТаблицаРегистра.ДатаВводаВЭксплуатациюБУ                КАК ДатаВводаВЭксплуатациюБУ,
		|	ТаблицаРегистра.ДатаВводаВЭксплуатациюУУ                КАК ДатаВводаВЭксплуатациюУУ,
		|	&Ссылка                                                 КАК ДокументВводаВЭксплуатациюБУ,
		|	&Ссылка                                                 КАК ДокументВводаВЭксплуатациюУУ,
		|	НЕОПРЕДЕЛЕНО                                            КАК ДокументСнятияСУчетаУУ
		|
		|ИЗ
		|	Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
		|				&Период,
		|				Регистратор <> &Ссылка
		|					И Организация = &Организация
		|					И ОсновноеСредство = &ОсновноеСредство) КАК ТаблицаРегистра
		|		ПО (ТаблицаРегистра.ОсновноеСредство = &ОсновноеСредство)
		|ГДЕ
		|	ТаблицаОС.Ссылка = &Ссылка
		|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСЧастичная)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период                                                 КАК Период,
		|	&Организация                                            КАК Организация,
		|	ТаблицаОС.ОсновноеСредство                              КАК ОсновноеСредство,
		|	ТаблицаРегистра.СпособПоступления                       КАК СпособПоступления,
		|	ТаблицаРегистра.Контрагент                              КАК Контрагент,
		|		
		|	ТаблицаРегистра.ПорядокУчетаУУ                          КАК ПорядокУчетаУУ,
		|	ТаблицаОС.СтоимостьБУ                                   КАК ПервоначальнаяСтоимостьБУ,
		|	ТаблицаОС.СтоимостьУУ                                   КАК ПервоначальнаяСтоимостьУУ,
		|	&Период                                                 КАК ДатаВводаВЭксплуатациюБУ,
		|	&Период                                                 КАК ДатаВводаВЭксплуатациюУУ,
		|	&Ссылка                                                 КАК ДокументВводаВЭксплуатациюБУ,
		|	&Ссылка                                                 КАК ДокументВводаВЭксплуатациюУУ,
		|	НЕОПРЕДЕЛЕНО                                            КАК ДокументСнятияСУчетаУУ
		|
		|ИЗ
		|	Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
		|				&Период,
		|				Регистратор <> &Ссылка
		|					И Организация = &Организация
		|					И ОсновноеСредство = &ОсновноеСредство) КАК ТаблицаРегистра
		|		ПО (ТаблицаРегистра.ОсновноеСредство = &ОсновноеСредство)
		|ГДЕ
		|	ТаблицаОС.Ссылка = &Ссылка
		|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыделениеУзловКомпонентовАмортизации)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Период                                                КАК Период,
		|	&Организация                                           КАК Организация,
		|	&ОсновноеСредство                                      КАК ОсновноеСредство,
		|	ТаблицаРегистра.СпособПоступления                      КАК СпособПоступления,
		|	ТаблицаРегистра.Контрагент                             КАК Контрагент,
		|
		|	ТаблицаРегистра.ПорядокУчетаУУ                         КАК ПорядокУчетаУУ,
		|	ТаблицаРегистра.ПервоначальнаяСтоимостьБУ              КАК ПервоначальнаяСтоимостьБУ,
		|	ТаблицаРегистра.ПервоначальнаяСтоимостьУУ              КАК ПервоначальнаяСтоимостьУУ,
		|	ТаблицаРегистра.ДатаВводаВЭксплуатациюБУ               КАК ДатаВводаВЭксплуатациюБУ,
		|	ТаблицаРегистра.ДатаВводаВЭксплуатациюУУ               КАК ДатаВводаВЭксплуатациюУУ,
		|	ТаблицаРегистра.ДокументВводаВЭксплуатациюБУ           КАК ДокументВводаВЭксплуатациюБУ,
		|	ТаблицаРегистра.ДокументВводаВЭксплуатациюУУ           КАК ДокументВводаВЭксплуатациюУУ,
		|	&Ссылка                                                КАК ДокументСнятияСУчетаУУ
		|
		|ИЗ
		|	РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
		|				&Период,
		|				Регистратор <> &Ссылка
		|					И Организация = &Организация
		|					И ОсновноеСредство = &ОсновноеСредство) КАК ТаблицаРегистра
		|ГДЕ
		|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСПолная)";
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаМестонахождениеОС(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "МестонахождениеОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                                КАК Период,
	|	&Организация                                           КАК Организация,
	|	ТаблицаОС.ОсновноеСредство                             КАК ОсновноеСредство,
	|	ТаблицаРегистра.МОЛ                                    КАК МОЛ,
	|	ТаблицаРегистра.Местонахождение                        КАК Местонахождение,
	|	ТаблицаРегистра.АдресМестонахождения                   КАК АдресМестонахождения,
	|	ТаблицаРегистра.АдресМестонахожденияЗначение           КАК АдресМестонахожденияЗначение,
	|	ТаблицаРегистра.Арендатор                              КАК Арендатор,
	|	ТаблицаРегистра.ПодразделениеАрендатора                КАК ПодразделениеАрендатора,
	|	ТаблицаРегистра.МОЛАрендатора                          КАК МОЛАрендатора,
	|	&ОтражатьВРеглУчете                                    КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                                     КАК ОтражатьВУпрУчете
	|ИЗ
	|	Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС.СрезПоследних(
	|				&Период,
	|				Регистратор <> &Ссылка
	|					И ОсновноеСредство = &ОсновноеСредство) КАК ТаблицаРегистра
	|		ПО (ТаблицаРегистра.ОсновноеСредство = &ОсновноеСредство)
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаУзлыКомпонентыАмортизации(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "УзлыКомпонентыАмортизации";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Ссылка                    КАК Регистратор,
	|	&Период                    КАК Период,
	|	ТаблицаОС.ОсновноеСредство КАК УзелКомпонент,
	|	&ОсновноеСредство          КАК ОсновноеСредство
	|ИЗ
	|	Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыделениеУзловКомпонентовАмортизации)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтСписокНовыхОС(ТекстыЗапроса)
	
	ИмяТаблицы = "втСписокНовыхОС";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ втСписокНовыхОС
	|ИЗ
	|	Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтВыделениеСтоимости(ТекстыЗапроса)

	ИмяТаблицы = "ВтВыделениеСтоимости";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтКоэффициенты(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтСуммаКоэффициентов(ТекстыЗапроса);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаОС.ОсновноеСредство                   КАК ОсновноеСредство,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовУУ <> 0
	|			ТОГДА &ПредварительнаяСтоимостьУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК ПредварительнаяСтоимость,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовБУ <> 0
	|			ТОГДА &ПредварительнаяСтоимостьРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК ПредварительнаяСтоимостьРегл,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовУУ <> 0
	|			ТОГДА &СтоимостьУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК Стоимость,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовБУ <> 0
	|			ТОГДА &СтоимостьРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК СтоимостьРегл,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовНУ <> 0
	|			ТОГДА &СтоимостьНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК СтоимостьНУ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовПР <> 0
	|			ТОГДА &СтоимостьПР
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК СтоимостьПР,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовВР <> 0
	|			ТОГДА &СтоимостьВР
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК СтоимостьВР,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовБУ <> 0
	|			ТОГДА &СтоимостьЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК СтоимостьЦФ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовБУ <> 0
	|			ТОГДА &Стоимость_ПРЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК СтоимостьПРЦФ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовУУ <> 0
	|			ТОГДА &РезервПереоценкиСтоимостиУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК РезервПереоценкиСтоимости,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВРеглУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовБУ <> 0
	|			ТОГДА &РезервПереоценкиСтоимостиРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ                                        КАК РезервПереоценкиСтоимостиРегл,
	|	
	|	ТаблицаОС.ИдентификаторСтроки                КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВтВыделениеСтоимости
	|ИЗ
	|	Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК ВТ_СтоимостьВНА
	|		ПО ВТ_СтоимостьВНА.ОбъектУчета = &ОсновноеСредство
	|			И ВТ_СтоимостьВНА.Ссылка = &Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтКоэффициенты КАК ВтКоэффициенты
	|		ПО ВтКоэффициенты.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСуммаКоэффициентов КАК ВтСуммаКоэффициентов
	|		ПО ИСТИНА
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	// ПредварительнаяСтоимость
	ТекстРаспределенияСуммы = ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы(
		"ЕСТЬNULL(ВТ_СтоимостьВНА.ПредварительнаяСтоимость, 0)", 
		"ТаблицаОС.СтоимостьУУ", 
		"ВтКоэффициенты.СуммаКоэффициентовПредыдущихСтрокУУ", 
		"ВтСуммаКоэффициентов.СуммаКоэффициентовУУ");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПредварительнаяСтоимостьУпр", ТекстРаспределенияСуммы);
	
	// ПредварительнаяСтоимостьРегл
	ТекстРаспределенияСуммы = ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы(
		"ЕСТЬNULL(ВТ_СтоимостьВНА.ПредварительнаяСтоимостьРегл, 0)", 
		"ТаблицаОС.СтоимостьБУ", 
		"ВтКоэффициенты.СуммаКоэффициентовПредыдущихСтрокБУ", 
		"ВтСуммаКоэффициентов.СуммаКоэффициентовБУ");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПредварительнаяСтоимостьРегл", ТекстРаспределенияСуммы);
	
	// Стоимость
	ТекстРаспределенияСуммы = ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы(
		"ЕСТЬNULL(ВТ_СтоимостьВНА.Стоимость, 0)", 
		"ТаблицаОС.СтоимостьУУ", 
		"ВтКоэффициенты.СуммаКоэффициентовПредыдущихСтрокУУ", 
		"ВтСуммаКоэффициентов.СуммаКоэффициентовУУ");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СтоимостьУпр", ТекстРаспределенияСуммы);
	
	// СтоимостьРегл
	ТекстРаспределенияСуммы = ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы(
		"ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьРегл, 0)", 
		"ТаблицаОС.СтоимостьБУ", 
		"ВтКоэффициенты.СуммаКоэффициентовПредыдущихСтрокБУ", 
		"ВтСуммаКоэффициентов.СуммаКоэффициентовБУ");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СтоимостьРегл", ТекстРаспределенияСуммы);
	
	// СтоимостьНУ
	ТекстРаспределенияСуммы = ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы(
		"ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьНУ, 0)", 
		"ТаблицаОС.СтоимостьНУ", 
		"ВтКоэффициенты.СуммаКоэффициентовПредыдущихСтрокНУ", 
		"ВтСуммаКоэффициентов.СуммаКоэффициентовНУ");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СтоимостьНУ", ТекстРаспределенияСуммы);
	
	// СтоимостьПР
	ТекстРаспределенияСуммы = ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы(
		"ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьПР, 0)", 
		"ТаблицаОС.СтоимостьПР", 
		"ВтКоэффициенты.СуммаКоэффициентовПредыдущихСтрокПР", 
		"ВтСуммаКоэффициентов.СуммаКоэффициентовПР");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СтоимостьПР", ТекстРаспределенияСуммы);
	
	// СтоимостьВР
	ТекстРаспределенияСуммы = ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы(
		"ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьВР, 0)", 
		"ТаблицаОС.СтоимостьВР", 
		"ВтКоэффициенты.СуммаКоэффициентовПредыдущихСтрокВР", 
		"ВтСуммаКоэффициентов.СуммаКоэффициентовВР");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СтоимостьВР", ТекстРаспределенияСуммы);
	
	// СтоимостьЦФ
	ТекстРаспределенияСуммы = ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы(
		"ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьЦФ, 0)", 
		"ТаблицаОС.СтоимостьБУ", 
		"ВтКоэффициенты.СуммаКоэффициентовПредыдущихСтрокБУ", 
		"ВтСуммаКоэффициентов.СуммаКоэффициентовБУ");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СтоимостьЦФ", ТекстРаспределенияСуммы);
	
	// СтоимостьПРЦФ
	ТекстРаспределенияСуммы = ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы(
		"ЕСТЬNULL(ВТ_СтоимостьВНА.СтоимостьПРЦФ, 0)", 
		"ТаблицаОС.СтоимостьБУ", 
		"ВтКоэффициенты.СуммаКоэффициентовПредыдущихСтрокБУ", 
		"ВтСуммаКоэффициентов.СуммаКоэффициентовБУ");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Стоимость_ПРЦФ", ТекстРаспределенияСуммы);
	
	// РезервПереоценкиСтоимости
	ТекстРаспределенияСуммы = ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы(
		"ЕСТЬNULL(ВТ_СтоимостьВНА.РезервПереоценкиСтоимости, 0)", 
		"ТаблицаОС.СтоимостьУУ", 
		"ВтКоэффициенты.СуммаКоэффициентовПредыдущихСтрокУУ", 
		"ВтСуммаКоэффициентов.СуммаКоэффициентовУУ");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РезервПереоценкиСтоимостиУпр", ТекстРаспределенияСуммы);
	
	// РезервПереоценкиСтоимостиРегл
	ТекстРаспределенияСуммы = ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы(
		"ЕСТЬNULL(ВТ_СтоимостьВНА.РезервПереоценкиСтоимостиРегл, 0)", 
		"ТаблицаОС.СтоимостьБУ", 
		"ВтКоэффициенты.СуммаКоэффициентовПредыдущихСтрокБУ", 
		"ВтСуммаКоэффициентов.СуммаКоэффициентовБУ");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РезервПереоценкиСтоимостиРегл", ТекстРаспределенияСуммы);
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтВыделениеАмортизации(ТекстыЗапроса)

	ИмяТаблицы = "ВтВыделениеАмортизации";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	ТекстЗапросаТаблицаВтКоэффициенты(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтСуммаКоэффициентов(ТекстыЗапроса);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаОС.ОсновноеСредство                      КАК ОсновноеСредство,
	|	ПорядокУчетаОС.ГруппаФинансовогоУчета           КАК ГруппаФинансовогоУчета,
	|	ПорядокУчетаОС.НаправлениеДеятельности          КАК НаправлениеДеятельности,
	|	АмортизацияОС.НачислятьИзнос                    КАК НачислятьИзнос,
	
	// Амортизация
	|	ВЫБОР
	|		КОГДА &ОтражатьВУпрУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовУУ <> 0
	|			ТОГДА &АмортизацияУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Амортизация,
		
	// АмортизацияРегл
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовБУ <> 0
	|			ТОГДА &АмортизацияРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияРегл,
		
	// АмортизацияНУ
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовНУ <> 0
	|			ТОГДА &АмортизацияНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияНУ,
		
	// АмортизацияПР
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовПР <> 0
	|			ТОГДА &АмортизацияПР
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияПР,
		
	// АмортизацияВР
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовБУ <> 0
	|			ТОГДА &АмортизацияРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ 
	|		- ВЫБОР
	|			КОГДА &ОтражатьВРеглУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовНУ <> 0
	|				ТОГДА &АмортизацияНУ
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|		- ВЫБОР
	|			КОГДА &ОтражатьВРеглУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовПР <> 0
	|				ТОГДА &АмортизацияПР
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК АмортизацияВР,
		
	// АмортизацияЦФ
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовБУ <> 0
	|			ТОГДА &Амортизация_ЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияЦФ,
		
	// АмортизацияНУЦФ
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовНУ <> 0
	|			ТОГДА &Амортизация_НУЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияНУЦФ,
		
	// АмортизацияПРЦФ
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовПР <> 0
	|			ТОГДА &Амортизация_ПРЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияПРЦФ,
		
	// АмортизацияВРЦФ
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовБУ <> 0
	|			ТОГДА &Амортизация_ЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ 
	|		- ВЫБОР
	|			КОГДА &ОтражатьВРеглУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовНУ <> 0
	|				ТОГДА &Амортизация_НУЦФ
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|		- ВЫБОР
	|			КОГДА &ОтражатьВРеглУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовПР <> 0
	|				ТОГДА &Амортизация_ПРЦФ
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК АмортизацияВРЦФ,
		
	// РезервПереоценкиАмортизации
	|	ВЫБОР
	|		КОГДА &ОтражатьВУпрУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовУУ <> 0
	|			ТОГДА &РезервПереоценкиАмортизацииУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РезервПереоценкиАмортизации,
		
	// РезервПереоценкиАмортизацииРегл
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовБУ <> 0
	|			ТОГДА &РезервПереоценкиАмортизацииРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РезервПереоценкиАмортизацииРегл,
	|	
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторСтроки
	|	
	|ПОМЕСТИТЬ ВтВыделениеАмортизации
	|ИЗ
	|	Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО АмортизацияОС.ОбъектУчета = &ОсновноеСредство
	|			И АмортизацияОС.Ссылка = &Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.ОбъектУчета = &ОсновноеСредство
	|			И СтоимостьОС.Ссылка = &Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО ПорядокУчетаОС.ОсновноеСредство = &ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтКоэффициенты КАК ВтКоэффициенты
	|		ПО ВтКоэффициенты.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСуммаКоэффициентов КАК ВтСуммаКоэффициентов
	|		ПО ИСТИНА
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	// АмортизацияУпр
	ТекстРаспределенияСуммы = ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы(
		"ВЫБОР 
		|	КОГДА &ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСПолная) 
		|		ТОГДА ВЫБОР 
		|				КОГДА ЕСТЬNULL(СтоимостьОС.Стоимость, 0) <> 0
		|					ТОГДА ЕСТЬNULL(АмортизацияОС.Амортизация, 0) * ВтСуммаКоэффициентов.СуммаКоэффициентовУУ / ЕСТЬNULL(СтоимостьОС.Стоимость, 0)
		|				ИНАЧЕ 0 
		|		КОНЕЦ
		|	ИНАЧЕ ЕСТЬNULL(АмортизацияОС.Амортизация, 0) 
		|КОНЕЦ", 
		"ТаблицаОС.СтоимостьУУ", 
		"ВтКоэффициенты.СуммаКоэффициентовПредыдущихСтрокУУ",
		"ВтСуммаКоэффициентов.СуммаКоэффициентовУУ");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&АмортизацияУпр", ТекстРаспределенияСуммы);
	
	// АмортизацияРегл
	ТекстРаспределенияСуммы = ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы(
		"ВЫБОР 
		|	КОГДА &ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСПолная) 
		|		ТОГДА ВЫБОР 
		|				КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0) <> 0
		|					ТОГДА ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0) * ВтСуммаКоэффициентов.СуммаКоэффициентовБУ / (ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0))
		|				ИНАЧЕ 0 
		|		КОНЕЦ
		|	ИНАЧЕ ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0) 
		|КОНЕЦ", 
		"ТаблицаОС.СтоимостьБУ", 
		"ВтКоэффициенты.СуммаКоэффициентовПредыдущихСтрокБУ", 
		"ВтСуммаКоэффициентов.СуммаКоэффициентовБУ");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&АмортизацияРегл", ТекстРаспределенияСуммы);
	
	// АмортизацияНУ
	ТекстРаспределенияСуммы = ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы(
		"ВЫБОР 
		|	КОГДА &ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСПолная) 
		|		ТОГДА ВЫБОР 
		|				КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьНУЦФ, 0) <> 0
		|					ТОГДА ЕСТЬNULL(АмортизацияОС.АмортизацияНУ, 0) * ВтСуммаКоэффициентов.СуммаКоэффициентовНУ / (ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьНУЦФ, 0))
		|				ИНАЧЕ 0 
		|		КОНЕЦ
		|	ИНАЧЕ ЕСТЬNULL(АмортизацияОС.АмортизацияНУ, 0) 
		|КОНЕЦ", 
		"ТаблицаОС.СтоимостьНУ", 
		"ВтКоэффициенты.СуммаКоэффициентовПредыдущихСтрокНУ", 
		"ВтСуммаКоэффициентов.СуммаКоэффициентовНУ");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&АмортизацияНУ", ТекстРаспределенияСуммы);
	
	// АмортизацияПР
	ТекстРаспределенияСуммы = ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы(
		"ВЫБОР 
		|	КОГДА &ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСПолная) 
		|		ТОГДА ВЫБОР 
		|				КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьПР, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьПРЦФ, 0) <> 0
		|					ТОГДА ЕСТЬNULL(АмортизацияОС.АмортизацияПР, 0) * ВтСуммаКоэффициентов.СуммаКоэффициентовНУ / (ЕСТЬNULL(СтоимостьОС.СтоимостьПР, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьПРЦФ, 0))
		|				ИНАЧЕ 0 
		|		КОНЕЦ
		|	ИНАЧЕ ЕСТЬNULL(АмортизацияОС.АмортизацияПР, 0) 
		|КОНЕЦ", 
		"ТаблицаОС.СтоимостьПР", 
		"ВтКоэффициенты.СуммаКоэффициентовПредыдущихСтрокПР", 
		"ВтСуммаКоэффициентов.СуммаКоэффициентовПР");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&АмортизацияПР", ТекстРаспределенияСуммы);
	
	// Амортизация_ЦФ
	ТекстРаспределенияСуммы = ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы(
		"ВЫБОР 
		|	КОГДА &ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСПолная) 
		|		ТОГДА ВЫБОР 
		|				КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьПР, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0) <> 0
		|					ТОГДА ЕСТЬNULL(АмортизацияОС.АмортизацияЦФ, 0) * ВтСуммаКоэффициентов.СуммаКоэффициентовБУ / (ЕСТЬNULL(СтоимостьОС.СтоимостьПР, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0))
		|				ИНАЧЕ 0 
		|		КОНЕЦ
		|	ИНАЧЕ ЕСТЬNULL(АмортизацияОС.АмортизацияЦФ, 0) 
		|КОНЕЦ", 
		"ТаблицаОС.СтоимостьБУ", 
		"ВтКоэффициенты.СуммаКоэффициентовПредыдущихСтрокБУ", 
		"ВтСуммаКоэффициентов.СуммаКоэффициентовБУ");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Амортизация_ЦФ", ТекстРаспределенияСуммы);
	
	// Амортизация_НУЦФ
	ТекстРаспределенияСуммы = ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы(
		"ВЫБОР 
		|	КОГДА &ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСПолная) 
		|		ТОГДА ВЫБОР 
		|				КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьНУЦФ, 0) <> 0
		|					ТОГДА ЕСТЬNULL(АмортизацияОС.АмортизацияНУЦФ, 0) * ВтСуммаКоэффициентов.СуммаКоэффициентовНУ / (ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьНУЦФ, 0))
		|				ИНАЧЕ 0 
		|		КОНЕЦ
		|	ИНАЧЕ ЕСТЬNULL(АмортизацияОС.АмортизацияНУЦФ, 0) 
		|КОНЕЦ", 
		"ТаблицаОС.СтоимостьНУ", 
		"ВтКоэффициенты.СуммаКоэффициентовПредыдущихСтрокНУ", 
		"ВтСуммаКоэффициентов.СуммаКоэффициентовНУ");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Амортизация_НУЦФ", ТекстРаспределенияСуммы);
	
	// Амортизация_ПРЦФ
	ТекстРаспределенияСуммы = ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы(
		"ВЫБОР 
		|	КОГДА &ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСПолная) 
		|		ТОГДА ВЫБОР 
		|				КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьПР, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьПРЦФ, 0) <> 0
		|					ТОГДА ЕСТЬNULL(АмортизацияОС.АмортизацияПРЦФ, 0) * ВтСуммаКоэффициентов.СуммаКоэффициентовНУ / (ЕСТЬNULL(СтоимостьОС.СтоимостьПР, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьПРЦФ, 0))
		|				ИНАЧЕ 0 
		|		КОНЕЦ
		|	ИНАЧЕ ЕСТЬNULL(АмортизацияОС.АмортизацияПРЦФ, 0) 
		|КОНЕЦ", 
		"ТаблицаОС.СтоимостьПР", 
		"ВтКоэффициенты.СуммаКоэффициентовПредыдущихСтрокПР", 
		"ВтСуммаКоэффициентов.СуммаКоэффициентовПР");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Амортизация_ПРЦФ", ТекстРаспределенияСуммы);
	
	// РезервПереоценкиАмортизацииУпр
	ТекстРаспределенияСуммы = ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы(
		"ВЫБОР 
		|	КОГДА &ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСПолная) 
		|		ТОГДА ВЫБОР 
		|				КОГДА ЕСТЬNULL(СтоимостьОС.Стоимость, 0) <> 0
		|					ТОГДА ЕСТЬNULL(АмортизацияОС.РезервПереоценкиАмортизации, 0) * ВтСуммаКоэффициентов.СуммаКоэффициентовУУ / ЕСТЬNULL(СтоимостьОС.Стоимость, 0)
		|				ИНАЧЕ 0 
		|		КОНЕЦ
		|	ИНАЧЕ ЕСТЬNULL(АмортизацияОС.РезервПереоценкиАмортизации, 0) 
		|КОНЕЦ", 
		"ТаблицаОС.СтоимостьУУ", 
		"ВтКоэффициенты.СуммаКоэффициентовПредыдущихСтрокУУ", 
		"ВтСуммаКоэффициентов.СуммаКоэффициентовУУ");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РезервПереоценкиАмортизацииУпр", ТекстРаспределенияСуммы);
	
	// РезервПереоценкиАмортизацииРегл
	ТекстРаспределенияСуммы = ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы(
		"ВЫБОР 
		|	КОГДА &ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСПолная) 
		|		ТОГДА ВЫБОР 
		|				КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0) <> 0
		|					ТОГДА ЕСТЬNULL(АмортизацияОС.РезервПереоценкиАмортизацииРегл, 0) * ВтСуммаКоэффициентов.СуммаКоэффициентовБУ / (ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0))
		|				ИНАЧЕ 0 
		|		КОНЕЦ
		|	ИНАЧЕ ЕСТЬNULL(АмортизацияОС.РезервПереоценкиАмортизацииРегл, 0) 
		|КОНЕЦ", 
		"ТаблицаОС.СтоимостьБУ", 
		"ВтКоэффициенты.СуммаКоэффициентовПредыдущихСтрокБУ",
		"ВтСуммаКоэффициентов.СуммаКоэффициентовБУ");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РезервПереоценкиАмортизацииРегл", ТекстРаспределенияСуммы);

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтВыделениеОбесценения(ТекстыЗапроса)

	ИмяТаблицы = "ВтВыделениеОбесценения";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	ТекстЗапросаТаблицаВтКоэффициенты(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтСуммаКоэффициентов(ТекстыЗапроса);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаОС.ОсновноеСредство                      КАК ОсновноеСредство,
	|	ПорядокУчетаОС.ГруппаФинансовогоУчета           КАК ГруппаФинансовогоУчета,
	|	ПорядокУчетаОС.НаправлениеДеятельности          КАК НаправлениеДеятельности,
	|	ОбесценениеВНА.ЕГДС                             КАК ЕГДС,
	
	// ОбесценениеУпр
	|	ВЫБОР
	|		КОГДА &ОтражатьВУпрУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовУУ <> 0
	|			ТОГДА &ОбесценениеУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОбесценениеУпр,
		
	// ОбесценениеРегл
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете И ВтСуммаКоэффициентов.СуммаКоэффициентовБУ <> 0
	|			ТОГДА &ОбесценениеРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОбесценениеРегл,
	|
	|	
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторСтроки
	|	
	|ПОМЕСТИТЬ ВтВыделениеОбесценения
	|ИЗ
	|	Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОбесценениеВНА КАК ОбесценениеВНА
	|		ПО ОбесценениеВНА.ОбъектУчета = &ОсновноеСредство
	|			И ОбесценениеВНА.Ссылка = &Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.ОбъектУчета = &ОсновноеСредство
	|			И СтоимостьОС.Ссылка = &Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО ПорядокУчетаОС.ОсновноеСредство = &ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтКоэффициенты КАК ВтКоэффициенты
	|		ПО ВтКоэффициенты.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСуммаКоэффициентов КАК ВтСуммаКоэффициентов
	|		ПО ИСТИНА
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыделениеУзловКомпонентовАмортизации)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	// ОбесценениеУпр
	ТекстРаспределенияСуммы = ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы(
		"ВЫБОР 
		|	КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСЧастичная) 
		|		ТОГДА ВЫБОР 
		|				КОГДА ЕСТЬNULL(СтоимостьОС.Стоимость, 0) <> 0
		|					ТОГДА ЕСТЬNULL(ОбесценениеВНА.ОбесценениеУпр, 0) * ВтСуммаКоэффициентов.СуммаКоэффициентовУУ / ЕСТЬNULL(СтоимостьОС.Стоимость, 0)
		|				ИНАЧЕ 0 
		|		КОНЕЦ
		|	ИНАЧЕ ЕСТЬNULL(ОбесценениеВНА.ОбесценениеУпр, 0) 
		|КОНЕЦ", 
		"ТаблицаОС.СтоимостьУУ", 
		"ВтКоэффициенты.СуммаКоэффициентовПредыдущихСтрокУУ",
		"ВтСуммаКоэффициентов.СуммаКоэффициентовУУ");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОбесценениеУпр", ТекстРаспределенияСуммы);
	
	// ОбесценениеРегл
	ТекстРаспределенияСуммы = ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы(
		"ВЫБОР 
		|	КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСЧастичная) 
		|		ТОГДА ВЫБОР 
		|				КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0) <> 0
		|					ТОГДА ЕСТЬNULL(ОбесценениеВНА.ОбесценениеРегл, 0) * ВтСуммаКоэффициентов.СуммаКоэффициентовБУ / (ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0))
		|				ИНАЧЕ 0 
		|		КОНЕЦ
		|	ИНАЧЕ ЕСТЬNULL(ОбесценениеВНА.ОбесценениеРегл, 0) 
		|КОНЕЦ", 
		"ТаблицаОС.СтоимостьБУ", 
		"ВтКоэффициенты.СуммаКоэффициентовПредыдущихСтрокБУ", 
		"ВтСуммаКоэффициентов.СуммаКоэффициентовБУ");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОбесценениеРегл", ТекстРаспределенияСуммы);

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтКоэффициенты(ТекстыЗапроса)
	
	ИмяТаблицы = "ВтКоэффициенты";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СУММА(ЕСТЬNULL(ТаблицаОСПредыдущие.СтоимостьБУ, 0)) КАК СуммаКоэффициентовПредыдущихСтрокБУ,
	|	СУММА(ЕСТЬNULL(ТаблицаОСПредыдущие.СтоимостьНУ, 0)) КАК СуммаКоэффициентовПредыдущихСтрокНУ,
	|	СУММА(ЕСТЬNULL(ТаблицаОСПредыдущие.СтоимостьПР, 0)) КАК СуммаКоэффициентовПредыдущихСтрокПР,
	|	СУММА(ЕСТЬNULL(ТаблицаОСПредыдущие.СтоимостьВР, 0)) КАК СуммаКоэффициентовПредыдущихСтрокВР,
	|	СУММА(ЕСТЬNULL(ТаблицаОСПредыдущие.СтоимостьУУ, 0)) КАК СуммаКоэффициентовПредыдущихСтрокУУ
	|ПОМЕСТИТЬ ВтКоэффициенты
	|ИЗ
	|	Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РазукомплектацияОС.ОС КАК ТаблицаОСПредыдущие
	|		ПО ТаблицаОСПредыдущие.Ссылка = ТаблицаОС.Ссылка
	|			И ТаблицаОСПредыдущие.НомерСтроки < ТаблицаОС.НомерСтроки
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОС.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтСуммаКоэффициентов(ТекстыЗапроса)
	
	ИмяТаблицы = "ВтСуммаКоэффициентов";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СУММА(ТаблицаОС.СтоимостьБУ) КАК СуммаКоэффициентовБУ,
	|	СУММА(ТаблицаОС.СтоимостьНУ) КАК СуммаКоэффициентовНУ,
	|	СУММА(ТаблицаОС.СтоимостьПР) КАК СуммаКоэффициентовПР,
	|	СУММА(ТаблицаОС.СтоимостьВР) КАК СуммаКоэффициентовВР,
	|	СУММА(ТаблицаОС.СтоимостьУУ) КАК СуммаКоэффициентовУУ
	|ПОМЕСТИТЬ ВтСуммаКоэффициентов
	|ИЗ
	|	Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаСоставЕГДС(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "СоставЕГДС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;

	ТекстЗапроса =
	// У исходного ОС 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ВнеоборотныйАктив,
	|	СоставЕГДССрезПоследних.ЕГДС КАК ЕГДС
	|ИЗ
	|	РегистрСведений.СоставЕГДС.СрезПоследних(&Период, Организация = &Организация
	|	И ВнеоборотныйАктив = &ОсновноеСредство) КАК СоставЕГДССрезПоследних,
	|	Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И СоставЕГДССрезПоследних.ЕГДС <> ЗНАЧЕНИЕ(Справочник.ЕдиницыГенерирующиеДенежныеСредства.ПустаяСсылка)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаПрослеживаемыеТоварыВСоставеОС(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрослеживаемыеТоварыВСоставеОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	ПрослеживаемыеТовары.НомерСтроки						КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)					КАК ВидДвижения,
	|	&Период													КАК Период,
	|	&Организация											КАК Организация,
	|	ПрослеживаемыеТовары.НомерГТД							КАК РНПТ,
	|	ПрослеживаемыеТовары.ОсновноеСредство					КАК ОсновноеСредство,
	|	ПрослеживаемыеТовары.КодТНВЭД							КАК КодТНВЭД,
	|	ПрослеживаемыеТовары.КоличествоПоРНПТ					КАК КоличествоПоРНПТ
	|ИЗ
	|	Документ.РазукомплектацияОС.ПрослеживаемыеТовары КАК ПрослеживаемыеТовары
	|ГДЕ
	|	ПрослеживаемыеТовары.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ 
	|
	|ВЫБРАТЬ
	|	ПрослеживаемыеТовары.НомерСтроки				КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)			КАК ВидДвижения,
	|	&Период											КАК Период,
	|	&Организация									КАК Организация,
	|	ПрослеживаемыеТовары.НомерГТД					КАК РНПТ,
	|	&ОсновноеСредство								КАК ОсновноеСредство,
	|	ПрослеживаемыеТовары.КодТНВЭД					КАК КодТНВЭД,
	|	ПрослеживаемыеТовары.КоличествоПоРНПТ			КАК КоличествоПоРНПТ
	|ИЗ
	|	Документ.РазукомплектацияОС.ПрослеживаемыеТовары КАК ПрослеживаемыеТовары
	|ГДЕ
	|	ПрослеживаемыеТовары.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат РазукомплектацияОСЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат РазукомплектацияОСЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

#КонецОбласти

#Область Печать

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	

КонецПроцедуры

Функция ПолучитьДанныеДляПечатнойФормыОС4(МассивОбъектов) Экспорт

	Возврат РазукомплектацияОСЛокализация.ПолучитьДанныеДляПечатнойФормыОС4(МассивОбъектов);

КонецФункции
 
#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Документы.РазукомплектацияОС.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.5.1";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("90c93fba-9b0e-4a65-8c06-247ff3d551ed");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.РазукомплектацияОС.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Обновляет документ ""Разукомплектация ОС"":
								  |- заполняет новый реквизит ""Хозяйственная операция"".
								  |- заполняет реквизит ""Идентификатор строки"" в табличных частях.';
								  |en = 'Updates the ""Split fixed assets"" document:
								  |- populates the new ""Business transaction"" attribute.
								  |- populates the ""Line ID"" attribute in tables.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.РазукомплектацияОС.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Документы.РазукомплектацияОС.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Документы.РазукомплектацияОС.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");

КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт

	ПолноеИмяОбъекта = "Документ.РазукомплектацияОС";
	
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
 
 	Пока Выборка.Следующий() Цикл
 		
 		НачатьТранзакцию();
		
 		Попытка
			
 			Блокировка = Новый БлокировкаДанных;
 			ЭлементБлокировки = Блокировка.Добавить("Документ.РазукомплектацияОС");
 			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
 			Блокировка.Заблокировать();
 			
			ДанныеОбъекта = Выборка.Ссылка.ПолучитьОбъект();
			Если ДанныеОбъекта = Неопределено Тогда
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
				ЗафиксироватьТранзакцию();
 				Продолжить;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ДанныеОбъекта.ХозяйственнаяОперация) Тогда
				ДанныеОбъекта.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РазукомплектацияОСЧастичная;
			КонецЕсли;
			
			ОбщегоНазначенияУТ.ЗаполнитьИдентификаторыДокумента(ДанныеОбъекта, "ОС");
			
			Если ДанныеОбъекта.Модифицированность() Тогда
	 			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДанныеОбъекта);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
			КонецЕсли; 
	
			ЗафиксироватьТранзакцию();
			
 		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru = 'Не удалось обработать объект: %Ссылка% по причине: %Причина%';
									|en = 'Cannot process the object: %Ссылка%. Reason: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Выборка.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
									УровеньЖурналаРегистрации.Предупреждение,
									Выборка.Ссылка.Метаданные(),
									Выборка.Ссылка,
									ТекстСообщения);
									
 		КонецПопытки;
 
 	КонецЦикла;
 		
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РазукомплектацияОС.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РазукомплектацияОС КАК РазукомплектацияОС
	|ГДЕ
	|	РазукомплектацияОС.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|	ИЛИ ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			Документ.РазукомплектацияОС.ОС КАК ТабЧасть
	|		ГДЕ
	|			ТабЧасть.Ссылка = РазукомплектацияОС.Ссылка
	|			И ТабЧасть.ИдентификаторСтроки = """")";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	СписокСсылок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, СписокСсылок);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
