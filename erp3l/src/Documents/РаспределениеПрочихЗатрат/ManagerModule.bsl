#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область РаспределениеНаФинансовыйРезультат
// Распределяет расходы на финансовый результат.
// Параметры:
// 	Период - Дата - период распределения расходов.
//	Организации - Массив - список организаций, по которым необходимо выполнить распределение.
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер временных таблиц.
//	ДокументРаспределения - ДокументСсылка.РаспределениеПрочихЗатрат - отбор по документу, для формирования отчета.
// Возвращаемое значение:
//	Массив, Неопределено - организации, по которым выполнилось распределение.
Функция РаспределитьРасходыНаФинансовыйРезультат(Период, Организации, МенеджерВременныхТаблиц = Неопределено, ДокументРаспределения = Неопределено) Экспорт
	
	Если ДокументРаспределения = Неопределено Тогда
		Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
			"Документ.РаспределениеПрочихЗатрат.МодульМенеджера.РаспределитьРасходыНаФинансовыйРезультат");
	КонецЕсли;
	
	ПараметрыРасчета = Неопределено;
	//++ НЕ УТ
	ПараметрыРасчета = РасчетСебестоимостиПостатейныеЗатраты.ПодготовитьБазуПоПрямымЗатрат(Период, Организации, ДокументРаспределения);
	//-- НЕ УТ
	
	Запрос = Новый Запрос;
	Если ПараметрыРасчета = Неопределено Тогда
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Иначе
		Запрос.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
	КонецЕсли;
	
	ФормироватьФинансовыйРезультат = ПолучитьФункциональнуюОпцию("ФормироватьФинансовыйРезультат");
	Запрос.УстановитьПараметр("НачалоПериода", 		НачалоМесяца(Период));
	Запрос.УстановитьПараметр("КонецПериода", 		КонецМесяца(Период));
	Запрос.УстановитьПараметр("ГраницаПериода", 	Новый Граница(КонецМесяца(Период), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("МассивОрганизаций", 	Организации);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", Организации.Количество() = 0);
	Запрос.УстановитьПараметр("ФормироватьФинансовыйРезультат", ФормироватьФинансовыйРезультат);
	Запрос.УстановитьПараметр("ДокументРаспределения", ДокументРаспределения);
	Запрос.УстановитьПараметр("ПредварительныйРасчет", Не ДокументРаспределения = Неопределено);
	//++ НЕ УТ
	Запрос.УстановитьПараметр("ВидыРасходовНормируемые", Перечисления.ВидыРасходовНУ.НормируемыеРасходы());
	//-- НЕ УТ
	
	ПодготовитьСлужебныеТаблицы(Период, Организации, Запрос);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Расходы.Организация КАК Организация,
		|	Расходы.Подразделение КАК Подразделение,
		|	Расходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Расходы.СтатьяРасходов КАК СтатьяРасходов,
		|	Расходы.АналитикаРасходов КАК АналитикаРасходов,
		|	Расходы.СуммаОстаток КАК Сумма,
		|	Расходы.СуммаБезНДСОстаток КАК СуммаБезНДС,
		|	Расходы.СуммаРеглОстаток КАК СуммаРегл,
		|	Расходы.ПостояннаяРазницаОстаток КАК ПостояннаяРазница,
		|	Расходы.ВременнаяРазницаОстаток КАК ВременнаяРазница,
		|	Расходы.СуммаУпрОстаток КАК СуммаУпр
		|ПОМЕСТИТЬ ПредварительныеОстаткиРасходов
		|ИЗ
		|	РегистрНакопления.ПрочиеРасходы.Остатки(
		|			&ГраницаПериода,
		|			(Организация, Подразделение, НаправлениеДеятельности, СтатьяРасходов, АналитикаРасходов) В
		|				(ВЫБРАТЬ
		|					НастроенныеРасходы.Организация,
		|					НастроенныеРасходы.Подразделение,
		|					НастроенныеРасходы.НаправлениеДеятельности,
		|					НастроенныеРасходы.СтатьяРасходов,
		|					НастроенныеРасходы.АналитикаРасходов
		|				ИЗ
		|					Регистраторы КАК НастроенныеРасходы
		//++ НЕ УТ
		|				ГДЕ НЕ НастроенныеРасходы.ЭтоОВЗ
		|				)
		|			ИЛИ
		|			(Организация, Подразделение, АналитикаРасходов) В
		|				(ВЫБРАТЬ
		|					НастроенныеРасходы.Организация,
		|					НастроенныеРасходы.Подразделение,
		|					НастроенныеРасходы.АналитикаРасходов
		|				ИЗ
		|					Регистраторы КАК НастроенныеРасходы
		|				ГДЕ НастроенныеРасходы.ЭтоОВЗ
		//-- НЕ УТ
		|				)
		|) КАК Расходы
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СтатьяРасходов,
		|	АналитикаРасходов,
		|	НаправлениеДеятельности,
		|	Подразделение,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Расходы.Организация КАК Организация,
		|	Расходы.Подразделение КАК Подразделение,
		|	Расходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Расходы.СтатьяРасходов КАК СтатьяРасходов,
		|	Расходы.АналитикаРасходов КАК АналитикаРасходов,
		|	СУММА(Расходы.Сумма) КАК Сумма,
		|	СУММА(Расходы.СуммаБезНДС) КАК СуммаБезНДС,
		|	СУММА(Расходы.СуммаРегл) КАК СуммаРегл,
		|	СУММА(Расходы.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(Расходы.ВременнаяРазница) КАК ВременнаяРазница,
		|	СУММА(Расходы.СуммаУпр) КАК СуммаУпр
		|ПОМЕСТИТЬ ОстаткиРасходов
		|ИЗ
		|	(ВЫБРАТЬ
		|		Расходы.Организация КАК Организация,
		|		Расходы.Подразделение КАК Подразделение,
		|		Расходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		Расходы.СтатьяРасходов КАК СтатьяРасходов,
		|		Расходы.АналитикаРасходов КАК АналитикаРасходов,
		|		Расходы.Сумма КАК Сумма,
		|		Расходы.СуммаБезНДС КАК СуммаБезНДС,
		|		Расходы.СуммаРегл КАК СуммаРегл,
		|		Расходы.ПостояннаяРазница КАК ПостояннаяРазница,
		|		Расходы.ВременнаяРазница КАК ВременнаяРазница,
		|		Расходы.СуммаУпр КАК СуммаУпр
		|	ИЗ
		|		ПредварительныеОстаткиРасходов КАК Расходы
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТекущиеДвижения.Организация,
		|		ТекущиеДвижения.Подразделение,
		|		ТекущиеДвижения.НаправлениеДеятельности,
		|		ТекущиеДвижения.СтатьяРасходов,
		|		ТекущиеДвижения.АналитикаРасходов,
		|		ВЫБОР
		|			КОГДА ЕСТЬNULL(РеквизитыДокументаРаспределения.УправленческийУчет, ЛОЖЬ)
		|					ИЛИ РеквизитыДокументаРаспределения.Ссылка ЕСТЬ NULL
		|				ТОГДА ТекущиеДвижения.Сумма
		|			КОГДА Остатки.Сумма = 0
		|				И Остатки.СуммаУпр = 0
		|				ТОГДА 0
		|			ИНАЧЕ ТекущиеДвижения.Сумма
		|		КОНЕЦ КАК Сумма,
		|		ВЫБОР
		|			КОГДА ЕСТЬNULL(РеквизитыДокументаРаспределения.УправленческийУчет, ЛОЖЬ)
		|					ИЛИ РеквизитыДокументаРаспределения.Ссылка ЕСТЬ NULL
		|				ТОГДА ТекущиеДвижения.СуммаБезНДС
		|			КОГДА Остатки.СуммаБезНДС = 0
		|				И Остатки.СуммаУпр = 0
		|				ТОГДА 0
		|			ИНАЧЕ ТекущиеДвижения.СуммаБезНДС
		|		КОНЕЦ КАК СуммаБезНДС,
		|		ВЫБОР 
		|			КОГДА ЕСТЬNULL(РеквизитыДокументаРаспределения.РегламентированныйУчет, ЛОЖЬ)
		|					ИЛИ РеквизитыДокументаРаспределения.Ссылка ЕСТЬ NULL
		|				ТОГДА ТекущиеДвижения.СуммаРегл
		|			КОГДА Остатки.СуммаРегл = 0
		|				И Остатки.ПостояннаяРазница = 0
		|				И Остатки.ВременнаяРазница = 0
		|				ТОГДА 0
		|			ИНАЧЕ ТекущиеДвижения.СуммаРегл
		|		КОНЕЦ КАК СуммаРегл,
		|		ВЫБОР 
		|			КОГДА ЕСТЬNULL(РеквизитыДокументаРаспределения.РегламентированныйУчет, ЛОЖЬ)
		|				И Остатки.СуммаРегл = 0
		|				ТОГДА 0
		|			КОГДА ЕСТЬNULL(РеквизитыДокументаРаспределения.НалоговыйУчет, ЛОЖЬ)
		|				И Остатки.СуммаРегл <> 0
		|				ТОГДА 0
		|			КОГДА Остатки.СуммаРегл = 0
		|				И Остатки.ПостояннаяРазница = 0
		|				И Остатки.ВременнаяРазница = 0
		|				ТОГДА 0
		|			ИНАЧЕ ТекущиеДвижения.ПостояннаяРазница
		|		КОНЕЦ КАК ПостояннаяРазница,
		|		ВЫБОР 
		|			КОГДА ЕСТЬNULL(РеквизитыДокументаРаспределения.РегламентированныйУчет, ЛОЖЬ)
		|				И Остатки.СуммаРегл = 0
		|				ТОГДА 0
		|			КОГДА ЕСТЬNULL(РеквизитыДокументаРаспределения.НалоговыйУчет, ЛОЖЬ)
		|				И Остатки.СуммаРегл <> 0
		|				ТОГДА 0
		|			КОГДА Остатки.СуммаРегл = 0
		|				И Остатки.ПостояннаяРазница = 0
		|				И Остатки.ВременнаяРазница = 0
		|				ТОГДА 0
		|			ИНАЧЕ ТекущиеДвижения.ВременнаяРазница
		|		КОНЕЦ КАК ВременнаяРазница,
		|		ВЫБОР 
		|			КОГДА ЕСТЬNULL(РеквизитыДокументаРаспределения.УправленческийУчет, ЛОЖЬ)
		|					ИЛИ РеквизитыДокументаРаспределения.Ссылка ЕСТЬ NULL
		|				ТОГДА ТекущиеДвижения.СуммаУпр
		|			КОГДА Остатки.Сумма = 0
		|				И Остатки.СуммаУпр = 0
		|				ТОГДА 0
		|			ИНАЧЕ ТекущиеДвижения.СуммаУпр
		|		КОНЕЦ КАК СуммаУпр
		|	ИЗ
		|		ПредварительныеОстаткиРасходов КАК Остатки
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы КАК ТекущиеДвижения
		|			ПО (ТекущиеДвижения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
		|				И (ТекущиеДвижения.Организация = Остатки.Организация)
		|				И (ТекущиеДвижения.Подразделение = Остатки.Подразделение)
		|				И (ТекущиеДвижения.НаправлениеДеятельности = Остатки.НаправлениеДеятельности)
		|				И (ТекущиеДвижения.СтатьяРасходов = Остатки.СтатьяРасходов)
		|				И (ТекущиеДвижения.АналитикаРасходов = Остатки.АналитикаРасходов)
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК РеквизитыДокументаРаспределения
		|			ПО РеквизитыДокументаРаспределения.Ссылка = ТекущиеДвижения.Регистратор
		|	ГДЕ
		|		ТекущиеДвижения.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТекущиеДвижения.Активность
		|		И (ТИПЗНАЧЕНИЯ(ТекущиеДвижения.Регистратор) В (ТИП(Документ.РаспределениеПрочихЗатрат),
		|				ТИП(Документ.РаспределениеДоходовПоНаправлениямДеятельности))
		|				И ТекущиеДвижения.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности)
		//++ НЕ УТ
		//++ Локализация
		|			ИЛИ ТИПЗНАЧЕНИЯ(ТекущиеДвижения.Регистратор) = ТИП(Документ.РегламентнаяОперация)
		|				И НЕ ТекущиеДвижения.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеКосвенныхРасходов)
		//-- Локализация
		//-- НЕ УТ
		|			)
		|	
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТекущиеДвижения.Организация,
		|		ТекущиеДвижения.Подразделение,
		|		ТекущиеДвижения.НаправлениеДеятельности,
		|		ТекущиеДвижения.СтатьяРасходов,
		|		ТекущиеДвижения.АналитикаРасходов,
		|		ТекущиеДвижения.Сумма,
		|		ТекущиеДвижения.СуммаБезНДС,
		|		ТекущиеДвижения.СуммаРегл,
		|		ТекущиеДвижения.ПостояннаяРазница,
		|		ТекущиеДвижения.ВременнаяРазница,
		|		ТекущиеДвижения.СуммаУпр
		|	ИЗ
		|		РегистрНакопления.ПрочиеРасходы КАК ТекущиеДвижения
		|	ГДЕ
		|		ТекущиеДвижения.Регистратор = &ДокументРаспределения) КАК Расходы
		|
		|СГРУППИРОВАТЬ ПО
		|	Расходы.СтатьяРасходов,
		|	Расходы.АналитикаРасходов,
		|	Расходы.НаправлениеДеятельности,
		|	Расходы.Подразделение,
		|	Расходы.Организация
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СтатьяРасходов,
		|	АналитикаРасходов,
		|	НаправлениеДеятельности,
		|	Подразделение,
		|	Организация
		|
		//++ НЕ УТ
		|;
		|
		// Списание в НУ нормируемых расходов, с отличным от НаНаправленияДеятельности вариантом распределения
		|ВЫБРАТЬ
		|	Регистраторы.Регистратор,
		|	ОстаткиРасходов.Организация,
		|	ОстаткиРасходов.Подразделение,
		|	ОстаткиРасходов.НаправлениеДеятельности,
		|	ОстаткиРасходов.СтатьяРасходов,
		|	ОстаткиРасходов.АналитикаРасходов,
		|	ОстаткиРасходов.ПостояннаяРазница КАК ПостояннаяРазница,
		|	-1 * (ОстаткиРасходов.СуммаРегл - ОстаткиРасходов.ВременнаяРазница)
		|		КАК ВременнаяРазница,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеНормируемыхРасходовПоНУ) КАК ХозяйственнаяОперация,
		|	Регистраторы.БазаРаспределенияПоПартиям КАК БазаРаспределенияПоПартиям,
		|	ИСТИНА КАК ТребуетсяРаспределение
		|ПОМЕСТИТЬ СписаниеНУ
		|ИЗ
		|	ОстаткиРасходов КАК ОстаткиРасходов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Регистраторы КАК Регистраторы
		|		ПО ОстаткиРасходов.Организация = Регистраторы.Организация
		|			И ОстаткиРасходов.Подразделение = Регистраторы.Подразделение
		|			И ОстаткиРасходов.НаправлениеДеятельности = Регистраторы.НаправлениеДеятельности
		|			И ОстаткиРасходов.СтатьяРасходов = Регистраторы.СтатьяРасходов
		|			И ОстаткиРасходов.АналитикаРасходов = Регистраторы.АналитикаРасходов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДолиСписанияКосвенныхРасходов КАК ДолиСписания
		|		ПО ОстаткиРасходов.СтатьяРасходов = ДолиСписания.СтатьяРасходов
		|			И ОстаткиРасходов.Организация = ДолиСписания.Организация
		|ГДЕ
		|	Регистраторы.НалоговыйУчет
		|	И ЕСТЬNULL(ДолиСписания.Нормируемые, ЛОЖЬ)
		|	И ОстаткиРасходов.СтатьяРасходов.ПринятиеКНалоговомуУчету
		|	И НЕ ОстаткиРасходов.СтатьяРасходов.ВариантРаспределенияРасходовНУ = 
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// Списание в НУ расходов, с вариантом распределения НаНаправленияДеятельности 
		|ВЫБРАТЬ
		|	Регистраторы.Регистратор,
		|	ОстаткиРасходов.Организация,
		|	ОстаткиРасходов.Подразделение,
		|	ОстаткиРасходов.НаправлениеДеятельности,
		|	ОстаткиРасходов.СтатьяРасходов,
		|	ОстаткиРасходов.АналитикаРасходов,
		|	ОстаткиРасходов.ПостояннаяРазница,
		|	(ВЫБОР
		|		КОГДА ЕСТЬNULL(ДолиСписания.Транспортные, ЛОЖЬ)
		|			ТОГДА ВЫРАЗИТЬ(-1 * (ОстаткиРасходов.СуммаРегл - ОстаткиРасходов.ВременнаяРазница) * ДолиСписания.ДоляСписания КАК ЧИСЛО(31, 2))
		|		ИНАЧЕ -1 * (ОстаткиРасходов.СуммаРегл - ОстаткиРасходов.ВременнаяРазница)
		|	КОНЕЦ),
		|	(ВЫБОР
		|		КОГДА ЕСТЬNULL(ДолиСписания.Нормируемые, ЛОЖЬ)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеНормируемыхРасходовПоНУ)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности)
		|	КОНЕЦ) КАК ХозяйственнаяОперация,
		|	Регистраторы.БазаРаспределенияПоПартиям КАК БазаРаспределенияПоПартиям,
		|	ИСТИНА КАК ТребуетсяРаспределение
		|ИЗ
		|	ОстаткиРасходов КАК ОстаткиРасходов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Регистраторы КАК Регистраторы
		|		ПО ОстаткиРасходов.Организация = Регистраторы.Организация
		|			И ОстаткиРасходов.Подразделение = Регистраторы.Подразделение
		|			И ОстаткиРасходов.НаправлениеДеятельности = Регистраторы.НаправлениеДеятельности
		|			И ОстаткиРасходов.СтатьяРасходов = Регистраторы.СтатьяРасходов
		|			И ОстаткиРасходов.АналитикаРасходов = Регистраторы.АналитикаРасходов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДолиСписанияКосвенныхРасходов КАК ДолиСписания
		|		ПО ОстаткиРасходов.СтатьяРасходов = ДолиСписания.СтатьяРасходов
		|			И ОстаткиРасходов.Организация = ДолиСписания.Организация
		|ГДЕ
		|	Регистраторы.НалоговыйУчет
		|	И ОстаткиРасходов.СтатьяРасходов.ПринятиеКНалоговомуУчету
		|	И ОстаткиРасходов.СтатьяРасходов.ВариантРаспределенияРасходовНУ = 
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// Списание в НУ расходов ОВЗ, с вариантом распределения НаНаправленияДеятельности 
		|ВЫБРАТЬ
		|	Регистраторы.Регистратор,
		|	ОстаткиРасходов.Организация,
		|	ОстаткиРасходов.Подразделение,
		|	ОстаткиРасходов.НаправлениеДеятельности,
		|	ОстаткиРасходов.СтатьяРасходов,
		|	ОстаткиРасходов.АналитикаРасходов,
		|	0 КАК ПостояннаяРазница,
		|	-1 * (ОстаткиРасходов.СуммаРегл - ОстаткиРасходов.ВременнаяРазница),
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности),
		|	Регистраторы.БазаРаспределенияПоПартиям КАК БазаРаспределенияПоПартиям,
		|	ИСТИНА КАК ТребуетсяРаспределение
		|ИЗ
		|	ОстаткиРасходов КАК ОстаткиРасходов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОВЗ КАК ВтОВЗ
		|		ПО ОстаткиРасходов.АналитикаРасходов = ВтОВЗ.ОВЗ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Регистраторы КАК Регистраторы
		|		ПО ОстаткиРасходов.Организация = Регистраторы.Организация
		|			И ОстаткиРасходов.Подразделение = Регистраторы.Подразделение
		|			И ОстаткиРасходов.АналитикаРасходов = Регистраторы.АналитикаРасходов
		|ГДЕ
		|	Регистраторы.НалоговыйУчет
		|	И Регистраторы.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
		|	И ОстаткиРасходов.СтатьяРасходов.ПринятиеКНалоговомуУчету
		|	И (
		|		ОстаткиРасходов.СтатьяРасходов.ВариантРаспределенияРасходовНУ = 
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|		ИЛИ ВтОВЗ.ВариантРаспределенияРасходовНУ = 
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|	)
		//-- НЕ УТ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Организация КАК Организация,
		|	ВложенныйЗапрос.Подразделение КАК Подразделение,
		|	ВложенныйЗапрос.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ВложенныйЗапрос.СтатьяРасходов КАК СтатьяРасходов,
		|	ВложенныйЗапрос.АналитикаРасходов КАК АналитикаРасходов,
		|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
		|	СУММА(ВложенныйЗапрос.СуммаБезНДС) КАК СуммаБезНДС,
		|	СУММА(ВложенныйЗапрос.СуммаУпр) КАК СуммаУпр,
		|	СУММА(ВложенныйЗапрос.СуммаРегл) КАК СуммаРегл,
		|	СУММА(ВложенныйЗапрос.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ВложенныйЗапрос.ВременнаяРазница) КАК ВременнаяРазница,
		|	ВложенныйЗапрос.Регистратор КАК Регистратор,
		|	ВложенныйЗапрос.ТипБазыРаспределения КАК ТипБазыРаспределения,
		|	ВложенныйЗапрос.ТребуетсяРаспределение КАК ТребуетсяРаспределение
		|ПОМЕСТИТЬ РасходыКРаспределению
		|ИЗ
		|	(ВЫБРАТЬ
		|	ОстаткиРасходов.Организация КАК Организация,
		|	ОстаткиРасходов.Подразделение КАК Подразделение,
		|	ОстаткиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ОстаткиРасходов.СтатьяРасходов КАК СтатьяРасходов,
		|	ОстаткиРасходов.АналитикаРасходов КАК АналитикаРасходов,
		|	ВЫБОР
		|		КОГДА НЕ Регистраторы.УправленческийУчет
		|			ИЛИ НЕ ОстаткиРасходов.СтатьяРасходов.ВариантРаспределенияРасходовУпр = 
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			ТОГДА 0
		|		ИНАЧЕ ОстаткиРасходов.Сумма
		|	КОНЕЦ КАК Сумма,
		|	ВЫБОР
		|		КОГДА НЕ Регистраторы.УправленческийУчет
		|			ИЛИ НЕ ОстаткиРасходов.СтатьяРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			ТОГДА 0
		|		ИНАЧЕ ОстаткиРасходов.СуммаБезНДС
		|	КОНЕЦ КАК СуммаБезНДС,
		|	ВЫБОР
		|		КОГДА НЕ Регистраторы.УправленческийУчет
		|			ИЛИ НЕ ОстаткиРасходов.СтатьяРасходов.ВариантРаспределенияРасходовУпр = 
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			ТОГДА 0
		|		ИНАЧЕ ОстаткиРасходов.СуммаУпр
		|	КОНЕЦ КАК СуммаУпр,
		|	ВЫБОР
		|		КОГДА НЕ Регистраторы.РегламентированныйУчет
		|			ИЛИ НЕ ОстаткиРасходов.СтатьяРасходов.ВариантРаспределенияРасходовРегл = 
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			ТОГДА 0
		|		КОГДА ЕСТЬNULL(ДолиСписания.Транспортные, ЛОЖЬ)
		|			ТОГДА ВЫРАЗИТЬ(ОстаткиРасходов.СуммаРегл * ДолиСписания.ДоляСписания КАК ЧИСЛО(31, 2))
		|		ИНАЧЕ ОстаткиРасходов.СуммаРегл
		|	КОНЕЦ КАК СуммаРегл,
		|	ВЫБОР
		|		КОГДА НЕ Регистраторы.РегламентированныйУчет
		|			ИЛИ НЕ ОстаткиРасходов.СтатьяРасходов.ВариантРаспределенияРасходовРегл = 
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			ТОГДА 0
		//++ НЕ УТ
		|		КОГДА Регистраторы.РегламентированныйУчет И НЕ ОстаткиРасходов.СтатьяРасходов.ПринятиеКНалоговомуУчету
		|			ТОГДА ОстаткиРасходов.ПостояннаяРазница
		//-- НЕ УТ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПостояннаяРазница,
		|	ВЫБОР
		|		КОГДА НЕ Регистраторы.РегламентированныйУчет
		|			ИЛИ НЕ ОстаткиРасходов.СтатьяРасходов.ВариантРаспределенияРасходовРегл = 
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			ТОГДА 0
		//++ НЕ УТ
		|		КОГДА ЕСТЬNULL(ДолиСписания.Транспортные, ЛОЖЬ)
		|			ТОГДА ВЫРАЗИТЬ(ОстаткиРасходов.СуммаРегл * ДолиСписания.ДоляСписания КАК ЧИСЛО(31, 2))
		|		КОГДА Регистраторы.РегламентированныйУчет И НЕ ОстаткиРасходов.СтатьяРасходов.ПринятиеКНалоговомуУчету
		|			ТОГДА ОстаткиРасходов.ВременнаяРазница
		|		КОГДА Регистраторы.РегламентированныйУчет И ОстаткиРасходов.СтатьяРасходов.ПринятиеКНалоговомуУчету
		|			ТОГДА ОстаткиРасходов.СуммаРегл
		//-- НЕ УТ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ВременнаяРазница,
		|	Регистраторы.Регистратор КАК Регистратор,
		|	Регистраторы.БазаРаспределенияПоПартиям КАК ТипБазыРаспределения,
		|	Регистраторы.ТребуетсяРаспределение
		|ИЗ
		|	ОстаткиРасходов КАК ОстаткиРасходов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Регистраторы КАК Регистраторы
		|		ПО ОстаткиРасходов.СтатьяРасходов = Регистраторы.СтатьяРасходов
		|			И ОстаткиРасходов.АналитикаРасходов = Регистраторы.АналитикаРасходов
		|			И ОстаткиРасходов.Организация = Регистраторы.Организация
		|			И ОстаткиРасходов.Подразделение = Регистраторы.Подразделение
		|			И ОстаткиРасходов.НаправлениеДеятельности = Регистраторы.НаправлениеДеятельности
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДолиСписанияКосвенныхРасходов КАК ДолиСписания
		|		ПО ОстаткиРасходов.СтатьяРасходов = ДолиСписания.СтатьяРасходов
		|			И ОстаткиРасходов.Организация = ДолиСписания.Организация
		|ГДЕ
		|	(НЕ(ОстаткиРасходов.Сумма = 0 И ОстаткиРасходов.СуммаБезНДС = 0
		|		И ОстаткиРасходов.СуммаУпр = 0) И Регистраторы.УправленческийУчет)
		|	ИЛИ
		|		(НЕ ОстаткиРасходов.СуммаРегл = 0 И Регистраторы.РегламентированныйУчет)
		|
		//++ НЕ УТ
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ОстаткиРасходов.Организация КАК Организация,
		|	ОстаткиРасходов.Подразделение КАК Подразделение,
		|	ОстаткиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ОстаткиРасходов.СтатьяРасходов КАК СтатьяРасходов,
		|	ОстаткиРасходов.АналитикаРасходов КАК АналитикаРасходов,
		|	ВЫБОР
		|		КОГДА НЕ Регистраторы.УправленческийУчет
		|			ИЛИ НЕ ВтОВЗ.ВариантРаспределенияРасходовУпр = 
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			ТОГДА 0
		|		ИНАЧЕ ОстаткиРасходов.Сумма
		|	КОНЕЦ КАК Сумма,
		|	ВЫБОР
		|		КОГДА НЕ Регистраторы.УправленческийУчет
		|			ИЛИ НЕ ВтОВЗ.ВариантРаспределенияРасходовУпр = 
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			ТОГДА 0
		|		ИНАЧЕ ОстаткиРасходов.СуммаБезНДС
		|	КОНЕЦ КАК СуммаБезНДС,
		|	ВЫБОР
		|		КОГДА НЕ Регистраторы.УправленческийУчет
		|			ИЛИ НЕ ВтОВЗ.ВариантРаспределенияРасходовУпр = 
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			ТОГДА 0
		|		ИНАЧЕ ОстаткиРасходов.СуммаУпр
		|	КОНЕЦ КАК СуммаУпр,
		|	ВЫБОР
		|		КОГДА НЕ Регистраторы.РегламентированныйУчет
		|			ИЛИ НЕ ВтОВЗ.ВариантРаспределенияРасходовРегл = 
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			ТОГДА 0
		|		КОГДА ЕСТЬNULL(ДолиСписания.Транспортные, ЛОЖЬ)
		|			ТОГДА ВЫРАЗИТЬ(ОстаткиРасходов.СуммаРегл * ДолиСписания.ДоляСписания КАК ЧИСЛО(31, 2))
		|		ИНАЧЕ ОстаткиРасходов.СуммаРегл
		|	КОНЕЦ КАК СуммаРегл,
		|	0 КАК ПостояннаяРазница,
		|	ВЫБОР
		|		КОГДА НЕ Регистраторы.РегламентированныйУчет
		|			ИЛИ НЕ ВтОВЗ.ВариантРаспределенияРасходовРегл = 
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			ТОГДА 0
		|		КОГДА ЕСТЬNULL(ДолиСписания.Транспортные, ЛОЖЬ)
		|			ТОГДА ВЫРАЗИТЬ(ОстаткиРасходов.СуммаРегл * ДолиСписания.ДоляСписания КАК ЧИСЛО(31, 2))
		|		ИНАЧЕ ОстаткиРасходов.СуммаРегл
		|	КОНЕЦ КАК ВременнаяРазница,
		|	Регистраторы.Регистратор КАК Регистратор,
		|	Регистраторы.БазаРаспределенияПоПартиям КАК ТипБазыРаспределения,
		|	Регистраторы.ТребуетсяРаспределение
		|ИЗ
		|	ОстаткиРасходов КАК ОстаткиРасходов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОВЗ КАК ВтОВЗ
		|			ПО ОстаткиРасходов.АналитикаРасходов = ВтОВЗ.ОВЗ
		|			И ОстаткиРасходов.Организация = ВтОВЗ.Организация
		|			И ОстаткиРасходов.Подразделение = ВтОВЗ.Подразделение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Регистраторы КАК Регистраторы
		|		ПО ОстаткиРасходов.АналитикаРасходов = Регистраторы.АналитикаРасходов
		|			И ОстаткиРасходов.Организация = Регистраторы.Организация
		|			И ОстаткиРасходов.Подразделение = Регистраторы.Подразделение
		|			И Регистраторы.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
		|			И Регистраторы.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДолиСписанияКосвенныхРасходов КАК ДолиСписания
		|		ПО ОстаткиРасходов.СтатьяРасходов = ДолиСписания.СтатьяРасходов
		|			И ОстаткиРасходов.Организация = ДолиСписания.Организация
		|ГДЕ
		|	(НЕ(ОстаткиРасходов.Сумма = 0 И ОстаткиРасходов.СуммаБезНДС = 0
		|		И ОстаткиРасходов.СуммаУпр = 0) И Регистраторы.УправленческийУчет)
		|	ИЛИ
		|		(НЕ ОстаткиРасходов.СуммаРегл = 0 И Регистраторы.РегламентированныйУчет)
		//-- НЕ УТ
		|	) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|		ВложенныйЗапрос.Организация,
		|		ВложенныйЗапрос.Подразделение,
		|		ВложенныйЗапрос.НаправлениеДеятельности,
		|		ВложенныйЗапрос.СтатьяРасходов,
		|		ВложенныйЗапрос.АналитикаРасходов,
		|		ВложенныйЗапрос.Регистратор,
		|		ВложенныйЗапрос.ТипБазыРаспределения,
		|		ВложенныйЗапрос.ТребуетсяРаспределение
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ВложенныйЗапрос.СтатьяРасходов,
		|	ВложенныйЗапрос.АналитикаРасходов,
		|	ВложенныйЗапрос.НаправлениеДеятельности,
		|	ВложенныйЗапрос.Подразделение,
		|	ВложенныйЗапрос.Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФинансовыеРезультаты.Организация,
		|	ФинансовыеРезультаты.НаправлениеДеятельности,
		|	СУММА(ФинансовыеРезультаты.Доходы) КАК Доходы,
		|	СУММА(ФинансовыеРезультаты.Расходы) КАК Расходы
		|ПОМЕСТИТЬ ФинансовыеРезультаты
		|ИЗ
		|	РегистрНакопления.ФинансовыеРезультаты КАК ФинансовыеРезультаты
		|ГДЕ
		|	ФинансовыеРезультаты.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ФинансовыеРезультаты.Организация В(&МассивОрганизаций)
		|	И ФинансовыеРезультаты.Активность
		|	И (ТИПЗНАЧЕНИЯ(ФинансовыеРезультаты.Регистратор) = ТИП(Документ.РасчетСебестоимостиТоваров)
		|			ИЛИ ФинансовыеРезультаты.РасчетСебестоимости)
		|
		|СГРУППИРОВАТЬ ПО
		|	ФинансовыеРезультаты.Организация,
		|	ФинансовыеРезультаты.НаправлениеДеятельности
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	""БазаРаспределения_1"" КАК ЗапросИсточник,
		|	Регистраторы.Регистратор КАК Регистратор,
		|	Регистраторы.БазаРаспределенияПоПартиям КАК ТипБазыРаспределения,
		|	ЕСТЬNULL(ФинансовыеРезультаты.НаправлениеДеятельности, 
		|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
		//++ НЕ УТ
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПроизводство,
		|	НЕОПРЕДЕЛЕНО КАК КодСтрокиПродукция,
		|	НЕОПРЕДЕЛЕНО КАК ПартияПроизводства,
		//-- НЕ УТ
		|	ЕСТЬNULL(ФинансовыеРезультаты.Доходы, 0) КАК База
		|ПОМЕСТИТЬ БазаРаспределения
		|ИЗ
		|	Регистраторы КАК Регистраторы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.ОтборПоНаправлениямДеятельности КАК НаправленияДеятельности
		|		ПО Регистраторы.Регистратор = НаправленияДеятельности.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ФинансовыеРезультаты КАК ФинансовыеРезультаты
		|		ПО Регистраторы.Организация = ФинансовыеРезультаты.Организация
		|			И (НаправленияДеятельности.НаправлениеДеятельности = ФинансовыеРезультаты.НаправлениеДеятельности
		|				ИЛИ НаправленияДеятельности.НаправлениеДеятельности ЕСТЬ NULL)
		|			И НЕ ФинансовыеРезультаты.Доходы = 0
		|ГДЕ
		|	Регистраторы.ТребуетсяРаспределение
		|	И Регистраторы.БазаРаспределенияПоПартиям = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВыручкаОтПродаж)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	""БазаРаспределения_2"" КАК ЗапросИсточник,
		|	Регистраторы.Регистратор,
		|	Регистраторы.БазаРаспределенияПоПартиям,
		|	ЕСТЬNULL(ФинансовыеРезультаты.НаправлениеДеятельности, 
		|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
		//++ НЕ УТ
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		//-- НЕ УТ
		|	ЕСТЬNULL(ФинансовыеРезультаты.Расходы, 0)
		|ИЗ
		|	Регистраторы КАК Регистраторы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.ОтборПоНаправлениямДеятельности КАК НаправленияДеятельности
		|		ПО Регистраторы.Регистратор = НаправленияДеятельности.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ФинансовыеРезультаты КАК ФинансовыеРезультаты
		|		ПО Регистраторы.Организация = ФинансовыеРезультаты.Организация
		|			И (НаправленияДеятельности.НаправлениеДеятельности = ФинансовыеРезультаты.НаправлениеДеятельности
		|				ИЛИ НаправленияДеятельности.НаправлениеДеятельности ЕСТЬ NULL)
		|			И НЕ ФинансовыеРезультаты.Расходы = 0
		|ГДЕ
		|	Регистраторы.ТребуетсяРаспределение
		|	И Регистраторы.БазаРаспределенияПоПартиям = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СебестоимостьПродаж)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	""БазаРаспределения_3"" КАК ЗапросИсточник,
		|	Регистраторы.Регистратор,
		|	Регистраторы.БазаРаспределенияПоПартиям,
		|	ЕСТЬNULL(ФинансовыеРезультаты.НаправлениеДеятельности, 
		|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
		//++ НЕ УТ
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		//-- НЕ УТ
		|	ЕСТЬNULL(ФинансовыеРезультаты.Доходы, 0) - ЕСТЬNULL(ФинансовыеРезультаты.Расходы, 0)
		|ИЗ
		|	Регистраторы КАК Регистраторы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.ОтборПоНаправлениямДеятельности КАК НаправленияДеятельности
		|		ПО Регистраторы.Регистратор = НаправленияДеятельности.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ФинансовыеРезультаты КАК ФинансовыеРезультаты
		|		ПО Регистраторы.Организация = ФинансовыеРезультаты.Организация
		|			И (НаправленияДеятельности.НаправлениеДеятельности = ФинансовыеРезультаты.НаправлениеДеятельности
		|				ИЛИ НаправленияДеятельности.НаправлениеДеятельности ЕСТЬ NULL)
		|			И (НЕ ФинансовыеРезультаты.Доходы - ФинансовыеРезультаты.Расходы = 0)
		|ГДЕ
		|	Регистраторы.ТребуетсяРаспределение
		|	И Регистраторы.БазаРаспределенияПоПартиям = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВаловаяПрибыль)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	""БазаРаспределения_4"" КАК ЗапросИсточник,
		|	НаправленияДеятельности.Ссылка,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПустаяСсылка),
		|	НаправленияДеятельности.НаправлениеДеятельности,
		//++ НЕ УТ
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		//-- НЕ УТ
		|	НаправленияДеятельности.ДоляСтоимости
		|ИЗ
		|	Регистраторы КАК Регистраторы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.НаправленияДеятельности КАК НаправленияДеятельности
		|		ПО Регистраторы.Регистратор = НаправленияДеятельности.Ссылка
		|ГДЕ
		|	Регистраторы.ТребуетсяРаспределение
		//++ НЕ УТ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	""БазаРаспределения_5"" КАК ЗапросИсточник,
		|	БазаПоЭтапам.Регистратор,
		|	Регистраторы.БазаРаспределенияПоПартиям,
		|	БазаПоЭтапам.НаправлениеДеятельности,
		|	ВЫБОР
		|		КОГДА &ПредварительныйРасчет
		|			ТОГДА БазаПоЭтапам.ЗаказНаПроизводство
		|		ИНАЧЕ
		|			НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА &ПредварительныйРасчет
		|			ТОГДА БазаПоЭтапам.КодСтрокиПродукция
		|		ИНАЧЕ
		|			НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА &ПредварительныйРасчет
		|			ТОГДА БазаПоЭтапам.ПартияПроизводства
		|		ИНАЧЕ
		|			НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	СУММА(БазаПоЭтапам.Стоимость)
		|ИЗ
		|	БазаПоЭтапам КАК БазаПоЭтапам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Регистраторы КАК Регистраторы
		|		ПО БазаПоЭтапам.Регистратор = Регистраторы.Регистратор
		|			И Регистраторы.НазначениеНастройкиРаспределения = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаФинансовыйРезультат)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.ОтборПоНаправлениямДеятельности КАК НаправленияДеятельности
		|		ПО Регистраторы.Регистратор = НаправленияДеятельности.Ссылка
		|ГДЕ
		|	НаправленияДеятельности.НаправлениеДеятельности = БазаПоЭтапам.НаправлениеДеятельности
		|	ИЛИ НаправленияДеятельности.НаправлениеДеятельности ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	БазаПоЭтапам.НаправлениеДеятельности,
		|	БазаПоЭтапам.Регистратор,
		|	Регистраторы.БазаРаспределенияПоПартиям,
		|	ВЫБОР
		|		КОГДА &ПредварительныйРасчет
		|			ТОГДА БазаПоЭтапам.ЗаказНаПроизводство
		|		ИНАЧЕ
		|			НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА &ПредварительныйРасчет
		|			ТОГДА БазаПоЭтапам.КодСтрокиПродукция
		|		ИНАЧЕ
		|			НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА &ПредварительныйРасчет
		|			ТОГДА БазаПоЭтапам.ПартияПроизводства
		|		ИНАЧЕ
		|			НЕОПРЕДЕЛЕНО
		|	КОНЕЦ
		//-- НЕ УТ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	БазаРаспределения.Регистратор КАК Регистратор,
		|	БазаРаспределения.ТипБазыРаспределения КАК ТипБазыРаспределения,
		|	СУММА(БазаРаспределения.База) КАК База
		|ПОМЕСТИТЬ БазыРаспределенияСводно
		|ИЗ
		|	БазаРаспределения КАК БазаРаспределения
		|
		|СГРУППИРОВАТЬ ПО
		|	БазаРаспределения.Регистратор,
		|	БазаРаспределения.ТипБазыРаспределения
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	ТипБазыРаспределения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА НЕ РасходыКРаспределению.ТребуетсяРаспределение
		|				ИЛИ НЕ &ФормироватьФинансовыйРезультат
		|			ТОГДА РасходыКРаспределению.Сумма
		|		КОГДА ЕСТЬNULL(БазыРаспределенияСводно.База, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫРАЗИТЬ(РасходыКРаспределению.Сумма * (БазаРаспределения.База / БазыРаспределенияСводно.База) КАК ЧИСЛО(31, 2))
		|	КОНЕЦ КАК Сумма,
		|	ВЫБОР
		|		КОГДА НЕ РасходыКРаспределению.ТребуетсяРаспределение
		|				ИЛИ НЕ &ФормироватьФинансовыйРезультат
		|			ТОГДА РасходыКРаспределению.СуммаБезНДС
		|		КОГДА ЕСТЬNULL(БазыРаспределенияСводно.База, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫРАЗИТЬ(РасходыКРаспределению.СуммаБезНДС * (БазаРаспределения.База / БазыРаспределенияСводно.База) КАК ЧИСЛО(31, 2))
		|	КОНЕЦ КАК СуммаБезНДС,
		|	ВЫБОР
		|		КОГДА НЕ РасходыКРаспределению.ТребуетсяРаспределение
		|				ИЛИ НЕ &ФормироватьФинансовыйРезультат
		|			ТОГДА РасходыКРаспределению.СуммаУпр
		|		КОГДА ЕСТЬNULL(БазыРаспределенияСводно.База, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫРАЗИТЬ(РасходыКРаспределению.СуммаУпр * (БазаРаспределения.База / БазыРаспределенияСводно.База) КАК ЧИСЛО(31, 2))
		|	КОНЕЦ КАК СуммаУпр,
		|	ВЫБОР
		|		КОГДА НЕ РасходыКРаспределению.ТребуетсяРаспределение
		|				ИЛИ НЕ &ФормироватьФинансовыйРезультат
		|			ТОГДА РасходыКРаспределению.СуммаРегл
		|		КОГДА ЕСТЬNULL(БазыРаспределенияСводно.База, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫРАЗИТЬ(РасходыКРаспределению.СуммаРегл * (БазаРаспределения.База / БазыРаспределенияСводно.База) КАК ЧИСЛО(31, 2))
		|	КОНЕЦ КАК СуммаРегл,
		|	ВЫБОР
		|		КОГДА НЕ РасходыКРаспределению.ТребуетсяРаспределение
		|				ИЛИ НЕ &ФормироватьФинансовыйРезультат
		|			ТОГДА РасходыКРаспределению.ПостояннаяРазница
		|		КОГДА ЕСТЬNULL(БазыРаспределенияСводно.База, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫРАЗИТЬ(РасходыКРаспределению.ПостояннаяРазница * (БазаРаспределения.База / БазыРаспределенияСводно.База) КАК ЧИСЛО(31, 2))
		|	КОНЕЦ КАК ПостояннаяРазница,
		|	ВЫБОР
		|		КОГДА НЕ РасходыКРаспределению.ТребуетсяРаспределение
		|				ИЛИ НЕ &ФормироватьФинансовыйРезультат
		|			ТОГДА РасходыКРаспределению.ВременнаяРазница
		|		КОГДА ЕСТЬNULL(БазыРаспределенияСводно.База, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВЫРАЗИТЬ(РасходыКРаспределению.ВременнаяРазница * (БазаРаспределения.База / БазыРаспределенияСводно.База) КАК ЧИСЛО(31, 2))
		|	КОНЕЦ КАК ВременнаяРазница,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(БазыРаспределенияСводно.База, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ БазаРаспределения.База / БазыРаспределенияСводно.База
		|	КОНЕЦ КАК КоэффициентРаспределения,
		|	ЕСТЬNULL(БазаРаспределения.База, 0) КАК База,
		|	РасходыКРаспределению.Регистратор КАК Регистратор,
		|	РасходыКРаспределению.Организация КАК Организация,
		|	РасходыКРаспределению.Подразделение КАК Подразделение,
		|	РасходыКРаспределению.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	РасходыКРаспределению.СтатьяРасходов КАК СтатьяРасходов,
		|	РасходыКРаспределению.АналитикаРасходов КАК АналитикаРасходов,
		//++ НЕ УТ
		|	ЕСТЬNULL(БазаРаспределения.ЗаказНаПроизводство, НЕОПРЕДЕЛЕНО) КАК ЗаказНаПроизводство,
		|	ЕСТЬNULL(БазаРаспределения.КодСтрокиПродукция, НЕОПРЕДЕЛЕНО) КАК КодСтрокиПродукция,
		|	ЕСТЬNULL(БазаРаспределения.ПартияПроизводства, НЕОПРЕДЕЛЕНО) КАК ПартияПроизводства,
		//-- НЕ УТ
		|	ВЫБОР
		|		КОГДА НЕ РасходыКРаспределению.ТребуетсяРаспределение
		|			ТОГДА 
		|				ВЫБОР 
		|					КОГДА НЕ РасходыКРаспределению.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
		|						ТОГДА РасходыКРаспределению.НаправлениеДеятельности
		|					КОГДА ТИПЗНАЧЕНИЯ(РасходыКРаспределению.АналитикаРасходов) = ТИП(Справочник.НаправленияДеятельности)
		|						И НЕ РасходыКРаспределению.АналитикаРасходов = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
		|						ТОГДА РасходыКРаспределению.АналитикаРасходов
		|					ИНАЧЕ
		|						ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
		|				КОНЕЦ
		|		ИНАЧЕ
		|			БазаРаспределения.НаправлениеДеятельности
		|	КОНЕЦ КАК НаправлениеДеятельностиПриемник,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности) КАК ХозяйственнаяОперация
		|ПОМЕСТИТЬ РаспределенныеРасходы
		|ИЗ
		|	РасходыКРаспределению КАК РасходыКРаспределению
		|		ЛЕВОЕ СОЕДИНЕНИЕ БазаРаспределения КАК БазаРаспределения
		|		ПО РасходыКРаспределению.Регистратор = БазаРаспределения.Регистратор
		|			И РасходыКРаспределению.ТипБазыРаспределения = БазаРаспределения.ТипБазыРаспределения
		|		ЛЕВОЕ СОЕДИНЕНИЕ БазыРаспределенияСводно КАК БазыРаспределенияСводно
		|		ПО РасходыКРаспределению.Регистратор = БазыРаспределенияСводно.Регистратор
		|			И РасходыКРаспределению.ТипБазыРаспределения = БазыРаспределенияСводно.ТипБазыРаспределения
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	КоэффициентРаспределения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РаспределенныеРасходы.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ДокументыРаспределения
		|ИЗ
		|	РаспределенныеРасходы КАК РаспределенныеРасходы
		|		ЛЕВОЕ СОЕДИНЕНИЕ БазыРаспределенияСводно КАК БазаРаспределения
		|		ПО РаспределенныеРасходы.Регистратор = БазаРаспределения.Регистратор
		|ГДЕ
		|	НЕ (РаспределенныеРасходы.Сумма = 0
		|	И РаспределенныеРасходы.СуммаБезНДС = 0
		|	И РаспределенныеРасходы.СуммаУпр = 0
		|	И РаспределенныеРасходы.СуммаРегл = 0
		|	И РаспределенныеРасходы.ПостояннаяРазница = 0
		|	И РаспределенныеРасходы.ВременнаяРазница = 0)
		|		ИЛИ НЕ БазаРаспределения.Регистратор ЕСТЬ NULL
		|	
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РаспределенныеРасходы.Регистратор КАК Регистратор,
		|	МАКСИМУМ(РаспределенныеРасходы.КоэффициентРаспределения) КАК КоэффициентРаспределения
		|ПОМЕСТИТЬ МаксимальныеКоэффициентыПоРегистраторам
		|ИЗ
		|	РаспределенныеРасходы КАК РаспределенныеРасходы
		|
		|СГРУППИРОВАТЬ ПО
		|	РаспределенныеРасходы.Регистратор
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МаксимальныеКоэффициентыПоРегистраторам.Регистратор КАК Регистратор,
		|	МАКСИМУМ(РаспределенныеРасходы.НаправлениеДеятельностиПриемник) КАК НаправлениеДеятельностиПриемник
		|ПОМЕСТИТЬ НаправленияДеятельностиДляПогрешности
		|ИЗ
		|	МаксимальныеКоэффициентыПоРегистраторам КАК МаксимальныеКоэффициентыПоРегистраторам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РаспределенныеРасходы КАК РаспределенныеРасходы
		|		ПО МаксимальныеКоэффициентыПоРегистраторам.Регистратор = РаспределенныеРасходы.Регистратор
		|			И МаксимальныеКоэффициентыПоРегистраторам.КоэффициентРаспределения = РаспределенныеРасходы.КоэффициентРаспределения
		|
		|СГРУППИРОВАТЬ ПО
		|	МаксимальныеКоэффициентыПоРегистраторам.Регистратор
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
		|	СУММА(ВложенныйЗапрос.СуммаБезНДС) КАК СуммаБезНДС,
		|	СУММА(ВложенныйЗапрос.СуммаУпр) КАК СуммаУпр,
		|	СУММА(ВложенныйЗапрос.СуммаРегл) КАК СуммаРегл,
		|	СУММА(ВложенныйЗапрос.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ВложенныйЗапрос.ВременнаяРазница) КАК ВременнаяРазница,
		|	ВложенныйЗапрос.СтатьяРасходов КАК СтатьяРасходов,
		|	ВложенныйЗапрос.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ПогрешностиОкругления
		|ИЗ
		|	(ВЫБРАТЬ
		|		РасходыКРаспределению.Сумма,
		|		РасходыКРаспределению.СуммаБезНДС,
		|		РасходыКРаспределению.СуммаУпр,
		|		РасходыКРаспределению.СуммаРегл,
		|		РасходыКРаспределению.ПостояннаяРазница,
		|		РасходыКРаспределению.ВременнаяРазница,
		|		РасходыКРаспределению.СтатьяРасходов КАК СтатьяРасходов,
		|		РасходыКРаспределению.Регистратор КАК Регистратор
		|	ИЗ
		|		РасходыКРаспределению КАК РасходыКРаспределению
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыРаспределения КАК ДокументыРаспределения
		|			ПО РасходыКРаспределению.Регистратор = ДокументыРаспределения.Регистратор
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		-РаспределенныеРасходы.Сумма,
		|		-РаспределенныеРасходы.СуммаБезНДС,
		|		-РаспределенныеРасходы.СуммаУпр,
		|		-РаспределенныеРасходы.СуммаРегл,
		|		-РаспределенныеРасходы.ПостояннаяРазница,
		|		-РаспределенныеРасходы.ВременнаяРазница,
		|		РаспределенныеРасходы.СтатьяРасходов,
		|		РаспределенныеРасходы.Регистратор
		|	ИЗ
		|		РаспределенныеРасходы КАК РаспределенныеРасходы) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Регистратор,
		|	ВложенныйЗапрос.СтатьяРасходов
		|
		|ИМЕЮЩИЕ
		|	НЕ (СУММА(ВложенныйЗапрос.Сумма) = 0
		|		И СУММА(ВложенныйЗапрос.СуммаБезНДС) = 0
		|		И СУММА(ВложенныйЗапрос.СуммаУпр) = 0
		|		И СУММА(ВложенныйЗапрос.СуммаРегл) = 0
		|		И СУММА(ВложенныйЗапрос.ПостояннаяРазница) = 0
		|		И СУММА(ВложенныйЗапрос.ВременнаяРазница) = 0)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	СтатьяРасходов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасчетныеОстатки.Регистратор 				КАК Регистратор,
		|	РасчетныеОстатки.Организация 				КАК Организация,
		|	РасчетныеОстатки.Подразделение 				КАК Подразделение,
		|	РасчетныеОстатки.НаправлениеДеятельности	КАК НаправлениеДеятельности,
		|	РасчетныеОстатки.СтатьяРасходов				КАК СтатьяРасходов,
		|	РасчетныеОстатки.АналитикаРасходов			КАК АналитикаРасходов
		|ПОМЕСТИТЬ РегистраторыКОчисткеДвижений
		|ИЗ
		|	(ВЫБРАТЬ
		|		Регистраторы.Регистратор 				КАК Регистратор,
		|		ОстаткиРасходов.Организация 			КАК Организация,
		|		ОстаткиРасходов.Подразделение 			КАК Подразделение,
		|		ОстаткиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		ОстаткиРасходов.СтатьяРасходов			КАК СтатьяРасходов,
		|		ОстаткиРасходов.АналитикаРасходов		КАК АналитикаРасходов,
		|		ВЫБОР
		|			КОГДА Реквизиты.РегламентированныйУчет
		|				ТОГДА ОстаткиРасходов.СуммаРегл
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК СуммаРегл,
		|		ВЫБОР
		|			КОГДА Реквизиты.РегламентированныйУчет
		|				ТОГДА ОстаткиРасходов.ПостояннаяРазница
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК ПостояннаяРазница,
		|		ВЫБОР
		|			КОГДА Реквизиты.РегламентированныйУчет ИЛИ Реквизиты.НалоговыйУчет
		|				ТОГДА ОстаткиРасходов.ВременнаяРазница
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК ВременнаяРазница,
		|		ВЫБОР
		|			КОГДА Реквизиты.УправленческийУчет
		|				ТОГДА ОстаткиРасходов.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК Сумма,
		|		ВЫБОР
		|			КОГДА Реквизиты.УправленческийУчет
		|				ТОГДА ОстаткиРасходов.СуммаБезНДС
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК СуммаБезНДС,
		|		ВЫБОР
		|			КОГДА Реквизиты.УправленческийУчет
		|				ТОГДА ОстаткиРасходов.СуммаУпр
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК СуммаУпр
		|	ИЗ
		|		ПредварительныеОстаткиРасходов КАК ОстаткиРасходов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Регистраторы КАК Регистраторы
		|			ПО ОстаткиРасходов.Организация = Регистраторы.Организация
		|				И ОстаткиРасходов.Подразделение = Регистраторы.Подразделение
		|				И ОстаткиРасходов.НаправлениеДеятельности = Регистраторы.НаправлениеДеятельности
		|				И ОстаткиРасходов.СтатьяРасходов = Регистраторы.СтатьяРасходов
		|				И ОстаткиРасходов.АналитикаРасходов = Регистраторы.АналитикаРасходов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК Реквизиты
		|			ПО Регистраторы.Регистратор = Реквизиты.Ссылка
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Регистраторы.Регистратор 				КАК Регистратор,
		|		ОстаткиРасходов.Организация 			КАК Организация,
		|		ОстаткиРасходов.Подразделение 			КАК Подразделение,
		|		ОстаткиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		ОстаткиРасходов.СтатьяРасходов			КАК СтатьяРасходов,
		|		ОстаткиРасходов.АналитикаРасходов		КАК АналитикаРасходов,
		|		ВЫБОР
		|			КОГДА Реквизиты.РегламентированныйУчет
		|				ТОГДА ТекущиеДвижения.СуммаРегл
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК СуммаРегл,
		|		ВЫБОР
		|			КОГДА Реквизиты.РегламентированныйУчет
		|				ТОГДА ТекущиеДвижения.ПостояннаяРазница
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК ПостояннаяРазница,
		|		ВЫБОР
		|			КОГДА Реквизиты.РегламентированныйУчет ИЛИ Реквизиты.НалоговыйУчет
		|				ТОГДА ТекущиеДвижения.ВременнаяРазница
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК ВременнаяРазница,
		|		ВЫБОР
		|			КОГДА Реквизиты.УправленческийУчет
		|				ТОГДА ТекущиеДвижения.Сумма
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК Сумма,
		|		ВЫБОР
		|			КОГДА Реквизиты.УправленческийУчет
		|				ТОГДА ТекущиеДвижения.СуммаБезНДС
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК СуммаБезНДС,
		|		ВЫБОР
		|			КОГДА Реквизиты.УправленческийУчет
		|				ТОГДА ТекущиеДвижения.СуммаУпр
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК СуммаУпр
		|	ИЗ
		|		ПредварительныеОстаткиРасходов КАК ОстаткиРасходов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Регистраторы КАК Регистраторы
		|			ПО ОстаткиРасходов.Организация = Регистраторы.Организация
		|				И ОстаткиРасходов.Подразделение = Регистраторы.Подразделение
		|				И ОстаткиРасходов.НаправлениеДеятельности = Регистраторы.НаправлениеДеятельности
		|				И ОстаткиРасходов.СтатьяРасходов = Регистраторы.СтатьяРасходов
		|				И ОстаткиРасходов.АналитикаРасходов = Регистраторы.АналитикаРасходов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы КАК ТекущиеДвижения
		|			ПО Регистраторы.Регистратор = ТекущиеДвижения.Регистратор
		|				И (ТекущиеДвижения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
		|				И (ТекущиеДвижения.Организация = ОстаткиРасходов.Организация)
		|				И (ТекущиеДвижения.Подразделение = ОстаткиРасходов.Подразделение)
		|				И (ТекущиеДвижения.НаправлениеДеятельности = ОстаткиРасходов.НаправлениеДеятельности)
		|				И (ТекущиеДвижения.СтатьяРасходов = ОстаткиРасходов.СтатьяРасходов)
		|				И (ТекущиеДвижения.АналитикаРасходов = ОстаткиРасходов.АналитикаРасходов)
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК Реквизиты
		|			ПО Реквизиты.Ссылка = ТекущиеДвижения.Регистратор
		|	ГДЕ
		|		ТекущиеДвижения.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТекущиеДвижения.Активность) КАК РасчетныеОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетныеОстатки.Регистратор,
		|	РасчетныеОстатки.Организация,
		|	РасчетныеОстатки.Подразделение,
		|	РасчетныеОстатки.НаправлениеДеятельности,
		|	РасчетныеОстатки.СтатьяРасходов,
		|	РасчетныеОстатки.АналитикаРасходов
		|
		|ИМЕЮЩИЕ
		|	СУММА(РасчетныеОстатки.Сумма) = 0
		|	И СУММА(РасчетныеОстатки.СуммаБезНДС) = 0
		|	И СУММА(РасчетныеОстатки.СуммаУпр) = 0
		|	И СУММА(РасчетныеОстатки.СуммаРегл) = 0
		|	И СУММА(РасчетныеОстатки.ПостояннаяРазница) = 0
		|	И СУММА(РасчетныеОстатки.ВременнаяРазница) = 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	АналитикиРасходов.Организация КАК Организация,
		|	АналитикиРасходов.Подразделение КАК Подразделение,
		|	АналитикиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	АналитикиРасходов.СтатьяРасходов КАК СтатьяРасходов,
		|	АналитикиРасходов.АналитикаРасходов КАК АналитикаРасходов
		|ПОМЕСТИТЬ РаспределенныеАналитикиРасходов
		|ИЗ
		|	(ВЫБРАТЬ
		|		РаспределенныеРасходы.Организация КАК Организация,
		|		РаспределенныеРасходы.Подразделение КАК Подразделение,
		|		РаспределенныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		РаспределенныеРасходы.СтатьяРасходов КАК СтатьяРасходов,
		|		РаспределенныеРасходы.АналитикаРасходов КАК АналитикаРасходов
		|	ИЗ
		|		РаспределенныеРасходы КАК РаспределенныеРасходы
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыРаспределения КАК Документы
		|			ПО РаспределенныеРасходы.Регистратор = Документы.Регистратор
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		АналитикиРасходов.Организация КАК Организация,
		|		АналитикиРасходов.Подразделение КАК Подразделение,
		|		АналитикиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		АналитикиРасходов.СтатьяРасходов КАК СтатьяРасходов,
		|		АналитикиРасходов.АналитикаРасходов КАК АналитикаРасходов
		|	ИЗ
		|		РегистраторыКОчисткеДвижений КАК АналитикиРасходов
		//++ НЕ УТ
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		СписаниеНУ.Организация КАК Организация,
		|		СписаниеНУ.Подразделение КАК Подразделение,
		|		СписаниеНУ.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		СписаниеНУ.СтатьяРасходов КАК СтатьяРасходов,
		|		СписаниеНУ.АналитикаРасходов КАК АналитикаРасходов
		|	ИЗ
		|		СписаниеНУ КАК СписаниеНУ
		//-- НЕ УТ
		|	) КАК АналитикиРасходов
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	Подразделение,
		|	НаправлениеДеятельности,
		|	СтатьяРасходов,
		|	АналитикаРасходов
		|;
		//++ НЕ УТ
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Регистратор
		|ПОМЕСТИТЬ ДокументыСНовымиДвижениями
		|ИЗ
		|	(ВЫБРАТЬ
		|		Т.Регистратор
		|	ИЗ
		|		ДокументыРаспределения КАК Т
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Т.Регистратор
		|	ИЗ
		|		СписаниеНУ КАК Т) КАК Т
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ 
		|	Т.Регистратор КАК Документ,
		|	Т.Регистратор.Организация КАК Организация,
		|	Т.Регистратор.Дата КАК ДатаОтражения
		|ПОМЕСТИТЬ ДокументыКОтражению
		|ИЗ ДокументыСНовымиДвижениями КАК Т
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПрочиеРасходы.*
		|ИЗ
		|	ДокументыСНовымиДвижениями КАК ДокументыСНовымиДвижениями
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы КАК ПрочиеРасходы
		|		ПО ДокументыСНовымиДвижениями.Регистратор = ПрочиеРасходы.Регистратор
		|ГДЕ
		|	НЕ ПрочиеРасходы.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности),
		|												ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеНормируемыхРасходовПоНУ))
		|
		|ИТОГИ ПО
		|	ПрочиеРасходы.Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПрочиеРасходыНЗП.*
		|ИЗ
		|	ДокументыСНовымиДвижениями КАК ДокументыСНовымиДвижениями
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходыНезавершенногоПроизводства КАК ПрочиеРасходыНЗП
		|		ПО ДокументыСНовымиДвижениями.Регистратор = ПрочиеРасходыНЗП.Регистратор
		|
		|ИТОГИ ПО
		|	ПрочиеРасходыНЗП.Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПрочиеАктивыПассивы.*
		|ИЗ
		|	ДокументыСНовымиДвижениями КАК ДокументыСНовымиДвижениями
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеАктивыПассивы КАК ПрочиеАктивыПассивы
		|		ПО ДокументыСНовымиДвижениями.Регистратор = ПрочиеАктивыПассивы.Регистратор
		|ГДЕ
		|	ПрочиеАктивыПассивы.РасчетСебестоимости
		|	И НЕ ПрочиеАктивыПассивы.Источник = ЗНАЧЕНИЕ(Перечисление.ИсточникиУправленческогоБаланса.ПустаяСсылка)
		|;
		//-- НЕ УТ
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Период КАК Период,
		|	ВложенныйЗапрос.ВидДвижения КАК ВидДвижения,
		|	ВложенныйЗапрос.Регистратор КАК Регистратор,
		|	ВложенныйЗапрос.Организация КАК Организация,
		|	ВложенныйЗапрос.Подразделение КАК Подразделение,
		|	ВложенныйЗапрос.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ВложенныйЗапрос.НаправлениеДеятельностиПриемник КАК НаправлениеДеятельностиПриемник,
		|	ВложенныйЗапрос.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		//++ НЕ УТ
		|	ВложенныйЗапрос.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	ВложенныйЗапрос.КодСтрокиПродукция КАК КодСтрокиПродукция,
		|	ВложенныйЗапрос.ПартияПроизводства КАК ПартияПроизводства,
		//-- НЕ УТ
		|	ВложенныйЗапрос.СтатьяРасходов КАК СтатьяРасходов,
		|	ВложенныйЗапрос.АналитикаРасходов КАК АналитикаРасходов,
		|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
		|	СУММА(ВложенныйЗапрос.СуммаБезНДС) КАК СуммаБезНДС,
		|	СУММА(ВложенныйЗапрос.СуммаУпр) КАК СуммаУпр,
		|	СУММА(ВложенныйЗапрос.СуммаРегл) КАК СуммаРегл,
		|	СУММА(ВложенныйЗапрос.ПостояннаяРазница) КАК ПостояннаяРазница,
		|	СУММА(ВложенныйЗапрос.ВременнаяРазница) КАК ВременнаяРазница,
		|	СУММА(ВложенныйЗапрос.Расходы) КАК Расходы
		|ПОМЕСТИТЬ РезультатРаспределения
		|ИЗ
		|	(ВЫБРАТЬ
		|		&КонецПериода КАК Период,
		|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|		РаспределенныеРасходы.Регистратор КАК Регистратор,
		|		РаспределенныеРасходы.Организация КАК Организация,
		|		РаспределенныеРасходы.Подразделение КАК Подразделение,
		|		РаспределенныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		РаспределенныеРасходы.НаправлениеДеятельностиПриемник КАК НаправлениеДеятельностиПриемник,
		|		РаспределенныеРасходы.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		//++ НЕ УТ
		|		РаспределенныеРасходы.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|		РаспределенныеРасходы.КодСтрокиПродукция КАК КодСтрокиПродукция,
		|		РаспределенныеРасходы.ПартияПроизводства КАК ПартияПроизводства,
		//-- НЕ УТ
		|		РаспределенныеРасходы.СтатьяРасходов КАК СтатьяРасходов,
		|		РаспределенныеРасходы.АналитикаРасходов КАК АналитикаРасходов,
		|		РаспределенныеРасходы.Сумма КАК Сумма,
		|		РаспределенныеРасходы.СуммаБезНДС КАК СуммаБезНДС,
		|		РаспределенныеРасходы.СуммаУпр КАК СуммаУпр,
		|		РаспределенныеРасходы.СуммаРегл КАК СуммаРегл,
		|		РаспределенныеРасходы.ПостояннаяРазница КАК ПостояннаяРазница,
		|		РаспределенныеРасходы.ВременнаяРазница КАК ВременнаяРазница,
		|		РаспределенныеРасходы.Сумма КАК Расходы
		|	ИЗ
		|		РаспределенныеРасходы КАК РаспределенныеРасходы
		//++ НЕ УТ
		//++ Локализация
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		&КонецПериода,
		|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
		|		СписаниеНУ.Регистратор,
		|		СписаниеНУ.Организация,
		|		СписаниеНУ.Подразделение,
		|		СписаниеНУ.НаправлениеДеятельности,
		|		СписаниеНУ.НаправлениеДеятельности,
		|		СписаниеНУ.ХозяйственнаяОперация,
		|		НЕОПРЕДЕЛЕНО,
		|		НЕОПРЕДЕЛЕНО,
		|		НЕОПРЕДЕЛЕНО,
		|		СписаниеНУ.СтатьяРасходов,
		|		СписаниеНУ.АналитикаРасходов,
		|		0,
		|		0,
		|		0,
		|		0,
		|		СписаниеНУ.ПостояннаяРазница,
		|		СписаниеНУ.ВременнаяРазница,
		|		0
		|	ИЗ
		|		СписаниеНУ КАК СписаниеНУ
		//-- Локализация
		//-- НЕ УТ
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		&КонецПериода,
		|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
		|		РасходыКРаспределению.Регистратор,
		|		РасходыКРаспределению.Организация,
		|		РасходыКРаспределению.Подразделение,
		|		РасходыКРаспределению.НаправлениеДеятельности,
		|		НаправленияДеятельностиДляПогрешности.НаправлениеДеятельностиПриемник,
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности),
		//++ НЕ УТ
		|		НЕОПРЕДЕЛЕНО,
		|		НЕОПРЕДЕЛЕНО,
		|		НЕОПРЕДЕЛЕНО,
		//-- НЕ УТ
		|		РасходыКРаспределению.СтатьяРасходов,
		|		РасходыКРаспределению.АналитикаРасходов,
		|		ПогрешностиОкругления.Сумма,
		|		ПогрешностиОкругления.СуммаБезНДС,
		|		ПогрешностиОкругления.СуммаУпр,
		|		ПогрешностиОкругления.СуммаРегл,
		|		ПогрешностиОкругления.ПостояннаяРазница,
		|		ПогрешностиОкругления.ВременнаяРазница,
		|		ПогрешностиОкругления.Сумма КАК Расходы
		|	ИЗ
		|		РасходыКРаспределению КАК РасходыКРаспределению
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПогрешностиОкругления КАК ПогрешностиОкругления
		|			ПО РасходыКРаспределению.Регистратор = ПогрешностиОкругления.Регистратор
		|				И РасходыКРаспределению.СтатьяРасходов = ПогрешностиОкругления.СтатьяРасходов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ НаправленияДеятельностиДляПогрешности КАК НаправленияДеятельностиДляПогрешности
		|			ПО РасходыКРаспределению.Регистратор = НаправленияДеятельностиДляПогрешности.Регистратор
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		&КонецПериода,
		|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
		|		РегистраторыИАналитики.Регистратор,
		|		РегистраторыИАналитики.Организация,
		|		РегистраторыИАналитики.Подразделение,
		|		РегистраторыИАналитики.НаправлениеДеятельности,
		|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка),
		//++ НЕ УТ
		|		НЕОПРЕДЕЛЕНО,
		|		НЕОПРЕДЕЛЕНО,
		|		НЕОПРЕДЕЛЕНО,
		//-- НЕ УТ
		|		РегистраторыИАналитики.СтатьяРасходов,
		|		РегистраторыИАналитики.АналитикаРасходов,
		|		0 КАК Сумма,
		|		0 КАК СуммаБезНДС,
		|		0 КАК СуммаУпр,
		|		0 КАК СуммаРегл,
		|		0 КАК ПостояннаяРазница,
		|		0 КАК ВременнаяРазница,
		|		0 КАК Расходы
		|	ИЗ
		|		РегистраторыКОчисткеДвижений КАК РегистраторыИАналитики) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Период,
		|	ВложенныйЗапрос.Регистратор,
		|	ВложенныйЗапрос.Организация,
		|	ВложенныйЗапрос.ВидДвижения,
		|	ВложенныйЗапрос.Подразделение,
		|	ВложенныйЗапрос.НаправлениеДеятельности,
		|	ВложенныйЗапрос.НаправлениеДеятельностиПриемник,
		|	ВложенныйЗапрос.ХозяйственнаяОперация,
		//++ НЕ УТ
		|	ВложенныйЗапрос.ЗаказНаПроизводство,
		|	ВложенныйЗапрос.КодСтрокиПродукция,
		|	ВложенныйЗапрос.ПартияПроизводства,
		//-- НЕ УТ
		|	ВложенныйЗапрос.СтатьяРасходов,
		|	ВложенныйЗапрос.АналитикаРасходов";
			
	Если ДокументРаспределения = Неопределено Тогда
		
		Запрос.Текст = Запрос.Текст + "
		|
		|ИТОГИ ПО
		|	Организация,
		|	Регистратор";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПОМЕСТИТЬ РезультатРаспределения", "");
		
	Иначе
		
		Запрос.Выполнить();
		МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	Результат = Запрос.ВыполнитьПакет();
	ВыборкаПоОрганизациям = Результат[Результат.Количество() - 1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	//++ НЕ УТ
	ВыборкаДвиженийПрочиеАктивыПассивы = Результат[Результат.Количество() - 2].Выбрать();
	ВыборкаДвиженийПрочиеРасходыНЗП = Результат[Результат.Количество() - 3].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаДвиженийПрочиеРасходы = Результат[Результат.Количество() - 4].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	//-- НЕ УТ
	РассчитанныеОрганизации = Новый Массив;
	ВсегоОбработано = 0;
	
	ИдентификаторНеиспользуемойФинЗаписи = ПроведениеДокументов.ИдентификаторНеиспользуемойФинЗаписи();
	ФормироватьУправленческийБаланс = ПолучитьФункциональнуюОпцию("ФормироватьУправленческийБаланс");
	
	//++ НЕ УТ
	ДокументыКОтражению = Новый ТаблицаЗначений();
	ДокументыКОтражению.Колонки.Добавить("Документ", Новый ОписаниеТипов("ДокументСсылка.РаспределениеПрочихЗатрат"));
	ДокументыКОтражению.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ДокументыКОтражению.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	ДокументыКОтражению.Колонки.Добавить("ДатаОтражения", Новый ОписаниеТипов("Дата"));
	//-- НЕ УТ
	
	Пока ВыборкаПоОрганизациям.Следующий() Цикл
		
		БылиОшибки = Ложь;
		
		ВыборкаРегистраторов = ВыборкаПоОрганизациям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		ВсегоОбработано = ВыборкаРегистраторов.Количество();
		
		Пока ВыборкаРегистраторов.Следующий() Цикл 
			
			ФинансовыеРезультаты = РегистрыНакопления.ФинансовыеРезультаты.СоздатьНаборЗаписей();
			ФинансовыеРезультаты.Отбор.Регистратор.Установить(ВыборкаРегистраторов.Регистратор);
			
			ПрочиеРасходы = РегистрыНакопления.ПрочиеРасходы.СоздатьНаборЗаписей();
			ПрочиеРасходы.Отбор.Регистратор.Установить(ВыборкаРегистраторов.Регистратор);
			//++ НЕ УТ
			ПрочиеРасходыНЗП = РегистрыНакопления.ПрочиеРасходыНезавершенногоПроизводства.СоздатьНаборЗаписей();
			ПрочиеРасходыНЗП.Отбор.Регистратор.Установить(ВыборкаРегистраторов.Регистратор);
			//-- НЕ УТ
			
			ПрочиеАктивыПассив = РегистрыНакопления.ПрочиеАктивыПассивы.СоздатьНаборЗаписей();
			ПрочиеАктивыПассив.Отбор.Регистратор.Установить(ВыборкаРегистраторов.Регистратор);
			
			ДвиженияДоходыРасходыПрочиеАктивыПассивы = РегистрыНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы.СоздатьНаборЗаписей();
			ДвиженияДоходыРасходыПрочиеАктивыПассивы.Отбор.Регистратор.Установить(ВыборкаРегистраторов.Регистратор);
			
			ДвиженияПоПрочимАктивамПассивам = РегистрыНакопления.ДвиженияПоПрочимАктивамПассивам.СоздатьНаборЗаписей();
			ДвиженияПоПрочимАктивамПассивам.Отбор.Регистратор.Установить(ВыборкаРегистраторов.Регистратор);
			
			ВыборкаДетальная = ВыборкаРегистраторов.Выбрать();
			Пока ВыборкаДетальная.Следующий() Цикл
				
				ИдентификаторФинЗаписи = Строка(Новый УникальныйИдентификатор());
				
				Если Не (ВыборкаДетальная.Сумма = 0
					И ВыборкаДетальная.СуммаБезНДС = 0
					И ВыборкаДетальная.СуммаУпр = 0
					И ВыборкаДетальная.СуммаРегл = 0
					И ВыборкаДетальная.ПостояннаяРазница = 0
					И ВыборкаДетальная.ВременнаяРазница = 0) Тогда
					
					НоваяЗапись = ПрочиеРасходы.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаДетальная);
					НоваяЗапись.КорНаправлениеДеятельности = ВыборкаДетальная.НаправлениеДеятельностиПриемник;
					НоваяЗапись.НастройкаХозяйственнойОперации = Справочники.НастройкиХозяйственныхОпераций.РаспределениеРасходовПоНаправлениямДеятельности;
					НоваяЗапись.ИдентификаторФинЗаписи = ИдентификаторФинЗаписи;
					НоваяЗапись.РасчетСебестоимости = Истина;
					
				КонецЕсли;
				
				Если Не ВыборкаДетальная.Сумма = 0 Тогда
					
					НоваяЗапись = ПрочиеАктивыПассив.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаДетальная, "Период, Регистратор, Организация, Подразделение, НаправлениеДеятельности, Сумма");
					НоваяЗапись.Статья = ПланыВидовХарактеристик.СтатьиАктивовПассивов.ПрибылиИУбытки;
					НоваяЗапись.Аналитика = Неопределено;
					НоваяЗапись.РасчетСебестоимости = Истина;
					
				КонецЕсли;
				
				Если Не (ВыборкаДетальная.Сумма = 0
					И ВыборкаДетальная.СуммаБезНДС = 0
					И ВыборкаДетальная.СуммаУпр = 0
					И ВыборкаДетальная.СуммаРегл = 0) Тогда
					
					НоваяЗапись = ДвиженияДоходыРасходыПрочиеАктивыПассивы.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаДетальная, 
						"Период, Регистратор, Организация, Подразделение, НаправлениеДеятельности, 
						|АналитикаРасходов, ХозяйственнаяОперация, Сумма, СуммаУпр, СуммаРегл");
					НоваяЗапись.Статья = ВыборкаДетальная.СтатьяРасходов;
					НоваяЗапись.КорСтатья = ПланыВидовХарактеристик.СтатьиАктивовПассивов.ПрибылиИУбытки;
					НоваяЗапись.КорНаправлениеДеятельности = ВыборкаДетальная.НаправлениеДеятельностиПриемник;
					
				КонецЕсли;
				
				Если Не ВыборкаДетальная.Расходы = 0 Тогда
					
					НоваяЗапись = ФинансовыеРезультаты.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаДетальная);
					НоваяЗапись.НаправлениеДеятельности = ВыборкаДетальная.НаправлениеДеятельностиПриемник;
					НоваяЗапись.РасчетСебестоимости = Истина;
					
				КонецЕсли;
				
				Если Не (ВыборкаДетальная.Сумма = 0
					И ВыборкаДетальная.СуммаУпр = 0
					И ВыборкаДетальная.СуммаРегл = 0
					И ВыборкаДетальная.ПостояннаяРазница = 0
					И ВыборкаДетальная.ВременнаяРазница = 0) Тогда
					
					НоваяЗапись = ДвиженияПоПрочимАктивамПассивам.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаДетальная);
					Если ВыборкаДетальная.ВидДвижения = ВидДвиженияНакопления.Расход Тогда
						НоваяЗапись.ДебетКредит = Перечисления.ВидыДвиженийПрочихАктивовПассивов.Дебет;
					Иначе
						НоваяЗапись.ДебетКредит = Перечисления.ВидыДвиженийПрочихАктивовПассивов.Кредит;
					КонецЕсли;
					НоваяЗапись.НаправлениеДеятельности = ВыборкаДетальная.НаправлениеДеятельностиПриемник;
					НоваяЗапись.Статья = ПланыВидовХарактеристик.СтатьиАктивовПассивов.ПрибылиИУбытки;
					НоваяЗапись.Аналитика = Неопределено;
					НоваяЗапись.СуммаСНДС = ВыборкаДетальная.Сумма;
					НоваяЗапись.СуммаБезНДС = ВыборкаДетальная.Сумма;
					НоваяЗапись.НастройкаХозяйственнойОперации = Справочники.НастройкиХозяйственныхОпераций.РаспределениеРасходовПоНаправлениямДеятельности;
					НоваяЗапись.ИдентификаторФинЗаписи = ИдентификаторФинЗаписи;
					НоваяЗапись.РасчетСебестоимости = Истина;
					
				КонецЕсли;
				
			КонецЦикла;
			
			//++ НЕ УТ
			ВыборкаДвиженийПрочиеРасходы.Сбросить();
			КлючПоискаДвижений = Новый Структура("Регистратор", ВыборкаРегистраторов.Регистратор);
			Пока ВыборкаДвиженийПрочиеРасходы.НайтиСледующий(КлючПоискаДвижений) Цикл
				
				ВыборкаДетальныеЗаписи = ВыборкаДвиженийПрочиеРасходы.Выбрать();
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					ЗаполнитьЗначенияСвойств(ПрочиеРасходы.Добавить(), ВыборкаДетальныеЗаписи);
				КонецЦикла;
				
			КонецЦикла;
			
			ВыборкаДвиженийПрочиеРасходыНЗП.Сбросить();
			КлючПоискаДвижений = Новый Структура("Регистратор", ВыборкаРегистраторов.Регистратор);
			Пока ВыборкаДвиженийПрочиеРасходыНЗП.НайтиСледующий(КлючПоискаДвижений) Цикл
				
				ВыборкаДетальныеЗаписи = ВыборкаДвиженийПрочиеРасходыНЗП.Выбрать();
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					ЗаполнитьЗначенияСвойств(ПрочиеРасходыНЗП.Добавить(), ВыборкаДетальныеЗаписи);
				КонецЦикла;
				
			КонецЦикла;
	
			ВыборкаДвиженийПрочиеАктивыПассивы.Сбросить();
			КлючПоискаДвижений = Новый Структура("Регистратор", ВыборкаРегистраторов.Регистратор);
			Пока ВыборкаДвиженийПрочиеАктивыПассивы.НайтиСледующий(КлючПоискаДвижений) Цикл
				ЗаполнитьЗначенияСвойств(ПрочиеАктивыПассив.Добавить(), ВыборкаДвиженийПрочиеАктивыПассивы);
			КонецЦикла;
			//-- НЕ УТ
			
			НачатьТранзакцию();
			Попытка
			
				Если ФормироватьУправленческийБаланс Тогда
					ДвиженияУпрБаланса = Новый Структура;
					ДвиженияУпрБаланса.Вставить("ПрочиеРасходы", ПрочиеРасходы.Выгрузить());
					//++ НЕ УТ
					ДвиженияУпрБаланса.Вставить("ПрочиеРасходыНезавершенногоПроизводства", ПрочиеРасходыНЗП.Выгрузить());
					//-- НЕ УТ
					ДвиженияУпрБаланса.Вставить("ПрочиеАктивыПассивы", ПрочиеАктивыПассив.Выгрузить());
					
					УправленческийУчетПроведениеСервер.ОбновитьДвиженияАктивовПассивов(ВыборкаРегистраторов.Регистратор, ДвиженияУпрБаланса);
					
					ПрочиеАктивыПассив.Загрузить(ДвиженияУпрБаланса.ПрочиеАктивыПассивы);
				КонецЕсли;
				
				ФинансовыеРезультаты.Записать();
				ПрочиеРасходы.Записать();
				ПрочиеАктивыПассив.Записать();
				ДвиженияДоходыРасходыПрочиеАктивыПассивы.Записать();
				ДвиженияПоПрочимАктивамПассивам.Записать();
				//++ НЕ УТ
				РаспределениеПрочихЗатратЛокализация.ЗарегистрироватьОтражениеДокумента(
					ВыборкаРегистраторов.Регистратор);
				КОтражению = ДокументыКОтражению.Добавить();
				КОтражению.Документ = ВыборкаРегистраторов.Регистратор;
				КОтражению.Организация = ВыборкаРегистраторов.Организация;
				КОтражению.Период = Период;
				КОтражению.ДатаОтражения = КонецМесяца(Период);
				//-- НЕ УТ
				
				ЗафиксироватьТранзакцию();
				
			Исключение
				
				ОтменитьТранзакцию();
				БылиОшибки = Истина;
				ЗаписьЖурналаРегистрации("РаспределениеРасходовНаФинансовыйРезультат",
										УровеньЖурналаРегистрации.Ошибка,
										ВыборкаРегистраторов.Регистратор.Метаданные(),
										ВыборкаРегистраторов.Регистратор,
										ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
										
			КонецПопытки;
			
		КонецЦикла;
		
		Если Не БылиОшибки Тогда
			РассчитанныеОрганизации.Добавить(ВыборкаПоОрганизациям.Организация);
		КонецЕсли;
		
	КонецЦикла;

	//++ НЕ УТКА
	МеждународныйУчетПроведениеСервер.ВернутьДокументыКОтражению(ДокументыКОтражению);
	//-- НЕ УТКА
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, ВсегоОбработано);
	
	Если Не ЕстьУстаревшиеДокументыСРасходами(Запрос) Тогда
		Возврат РассчитанныеОрганизации;
	КонецЕсли;
	
	// Удаление движений распределенных расходов в старом документе "Распределение доходов по направлениям деятельности".
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Движения.Регистратор
		|ПОМЕСТИТЬ РегистраторыКПерезаписиДвижений
		|ИЗ
		|	РегистрНакопления.ПрочиеРасходы КАК Движения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РаспределенныеАналитикиРасходов КАК Аналитики
		|		ПО Движения.Организация = Аналитики.Организация
		|			И Движения.Подразделение = Аналитики.Подразделение
		|			И Движения.НаправлениеДеятельности = Аналитики.НаправлениеДеятельности
		|			И Движения.СтатьяРасходов = Аналитики.СтатьяРасходов
		|			И Движения.АналитикаРасходов = Аналитики.АналитикаРасходов
		|ГДЕ
		|	Движения.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Движения.Активность
		|	И ТИПЗНАЧЕНИЯ(Движения.Регистратор) = ТИП(Документ.РаспределениеДоходовПоНаправлениямДеятельности)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Движения.*,
		|	(НЕ Аналитики.Организация ЕСТЬ NULL) КАК ПропуститьЗапись
		|ИЗ
		|	РегистрНакопления.ПрочиеРасходы КАК Движения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистраторыКПерезаписиДвижений КАК Регистраторы
		|		ПО Движения.Регистратор = Регистраторы.Регистратор
		|		ЛЕВОЕ СОЕДИНЕНИЕ РаспределенныеАналитикиРасходов КАК Аналитики
		|		ПО Движения.Организация = Аналитики.Организация
		|			И Движения.Подразделение = Аналитики.Подразделение
		|			И Движения.НаправлениеДеятельности = Аналитики.НаправлениеДеятельности
		|			И Движения.СтатьяРасходов = Аналитики.СтатьяРасходов
		|			И Движения.АналитикаРасходов = Аналитики.АналитикаРасходов
		|
		|ИТОГИ ПО
		|	Движения.Регистратор";
		
	Результат = Запрос.Выполнить();
	
	ОрганизацииКПересчету = Новый СписокЗначений;
	ВыборкаРегистраторовНовыхДвижений = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаРегистраторовНовыхДвижений.Следующий() Цикл
		
		ОрганизацииКПересчету.Очистить();
		
		ПрочиеРасходы = РегистрыНакопления.ПрочиеРасходы.СоздатьНаборЗаписей();
		ПрочиеРасходы.Отбор.Регистратор.Установить(ВыборкаРегистраторовНовыхДвижений.Регистратор);
		
		ВыборкаДетальная = ВыборкаРегистраторовНовыхДвижений.Выбрать();
		Пока ВыборкаДетальная.Следующий() Цикл
			
			Если ОрганизацииКПересчету.НайтиПоЗначению(ВыборкаДетальная.Организация) = Неопределено Тогда
				ОрганизацииКПересчету.Добавить(ВыборкаДетальная.Организация);
			КонецЕсли;
			
			Если ВыборкаДетальная.ПропуститьЗапись Тогда
				Продолжить;
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(ПрочиеРасходы.Добавить(), ВыборкаДетальная);
			
		КонецЦикла;
		
		ПрочиеРасходы.Записать();
		
		Для Каждого ПересчитываемаяОрганизация Из ОрганизацииКПересчету Цикл
			РегистрыСведений.ЗаданияКЗакрытиюМесяца.СоздатьЗаписьРегистра(Период, 
				ВыборкаРегистраторовНовыхДвижений.Регистратор,
				ПересчитываемаяОрганизация.Значение, 
				Перечисления.ОперацииЗакрытияМесяца.РаспределениеДоходовПоНаправлениямДеятельности);
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат РассчитанныеОрганизации;
	
КонецФункции

// Добавляет служебные таблицы в менеджер временных таблиц, необходимые для расчета
// базы прямых производственных затрат.
// Параметры:
//	ПараметрыРасчета - Структура - см. РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьОбщиеПараметрыРасчета()
//	Запрос - Запрос - инициализированный ранее запрос.
Процедура ПодготовитьТаблицыКРасчету(ПараметрыРасчета, Запрос = Неопределено) Экспорт
	
	Если Запрос = Неопределено Тогда
		
		Запрос = Новый Запрос;
		РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(
			Запрос,
			ПараметрыРасчета);
			
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыРасчета) = Тип("Структура") И ПараметрыРасчета.Свойство("Регистратор") Тогда
		
		Запрос.УстановитьПараметр("Регистратор", ПараметрыРасчета.Регистратор);
		Запрос.УстановитьПараметр("ПоВсемДокументам", ПараметрыРасчета.Регистратор = Неопределено);
		
	Иначе
		
		Запрос.УстановитьПараметр("Регистратор", Неопределено);
		Запрос.УстановитьПараметр("ПоВсемДокументам", Истина);
		
	КонецЕсли;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Регистратор,
		|	Реквизиты.Дата КАК Дата,
		|	Реквизиты.НазначениеНастройкиРаспределения,
		|	ВЫБОР
		|		КОГДА Реквизиты.НаправлениеРаспределения = ЗНАЧЕНИЕ(Перечисление.НаправлениеРаспределенияПоПодразделениям.ПоКоэффициентам)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПустаяСсылка)
		|		ИНАЧЕ	
		|			Реквизиты.БазаРаспределенияПоПартиям
		|	КОНЕЦ КАК БазаРаспределенияПоПартиям,
		//++ НЕ УТ
		|	Реквизиты.НаЛюбыеНаправлениеДеятельности,
		|	Реквизиты.БазаРаспределенияПоПодразделениям,
		|	Реквизиты.Показатель,
		|	Реквизиты.СтатьяКалькуляции,
		|	Реквизиты.РаспределятьПоПартиям,
		|	Реквизиты.РаспределятьПоПодразделениям,
		|	Реквизиты.РаспределятьНаСтатьи,
		|	Реквизиты.ОставитьВНЗП,
		|	Реквизиты.НаВыпускТекущегоМесяца,
		|	Реквизиты.ПартииУказаныВручную,
		|	Реквизиты.ПодразделенияУказаныВручную,
		|	ЗНАЧЕНИЕ(Перечисление.ПравилаОтнесенияНаВыпуск.ПустаяСсылка) КАК ПравилоОтнесенияНаВыпуск,
		|	ЕСТЬNULL(ВтОВЗ.ЭтоОВЗ,ЛОЖЬ) КАК ЭтоОВЗ,
		//-- НЕ УТ
		|	Реквизиты.Организация КАК Организация,
		|	Реквизиты.Подразделение КАК Подразделение,
		|	Реквизиты.СтатьяРасходов КАК СтатьяРасходов,
		|	Реквизиты.АналитикаРасходов КАК АналитикаРасходов,
		|	Реквизиты.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	(НЕ Реквизиты.НаправлениеРаспределения = ЗНАЧЕНИЕ(Перечисление.НаправлениеРаспределенияПоПодразделениям.Текущее)) КАК ТребуетсяРаспределение,
		|	Реквизиты.УправленческийУчет КАК УправленческийУчет,
		|	Реквизиты.РегламентированныйУчет КАК РегламентированныйУчет,
		|	Реквизиты.НалоговыйУчет КАК НалоговыйУчет
		|ПОМЕСТИТЬ Регистраторы
		|ИЗ
		|	Документ.РаспределениеПрочихЗатрат КАК Реквизиты
		//++ НЕ УТ
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВтОВЗ КАК ВтОВЗ
		|		ПО Реквизиты.АналитикаРасходов = ВтОВЗ.ОВЗ
		|			И Реквизиты.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
		//-- НЕ УТ
		|ГДЕ
		|	Реквизиты.Проведен
		|	И (
		|		Реквизиты.НазначениеНастройкиРаспределения = 
		|			ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаФинансовыйРезультат)
		|		ИЛИ (
		|			Реквизиты.НазначениеНастройкиРаспределения В (
		|				ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаСебестоимостьПроизводства), 
		|				ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеСтатейРасходовПоЭтапамПроизводства)
		|				) 
		|			И Реквизиты.НалоговыйУчет
		|			И Реквизиты.СтатьяРасходов.ВариантРаспределенияРасходовНУ = 
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|		)
		|		ИЛИ (
		|			Реквизиты.НазначениеНастройкиРаспределения В (
		//++ НЕ УТ
		|				ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаОВЗ),
		//-- НЕ УТ
		|				ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеСтатейРасходовПоЭтапамПроизводства) 
		|				) 
		|			И (Реквизиты.РегламентированныйУчет ИЛИ Реквизиты.НалоговыйУчет)
		|			И Реквизиты.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
		|		)
		|	)
		|	И Реквизиты.Организация В (&МассивОрганизаций)
		|	И Реквизиты.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И (Реквизиты.Ссылка = &Регистратор
		|		ИЛИ &ПоВсемДокументам)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СтатьяРасходов,
		|	АналитикаРасходов,
		|	НаправлениеДеятельности,
		|	Подразделение,
		|	Организация
		//++ НЕ УТ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Документ.РаспределениеПрочихЗатрат.ПустаяСсылка) КАК Регистратор,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПустаяСсылка) КАК БазаРаспределенияПоПодразделениям,
		|	ЗНАЧЕНИЕ(Справочник.ПравилаРаспределенияРасходов.ПустаяСсылка) КАК Показатель,
		|	ЛОЖЬ КАК ПодразделенияУказаныВручную
		|ПОМЕСТИТЬ РаспределитьПоПодразделениям
		|;
		|
		|ВЫБРАТЬ
		|	Т.Регистратор
		|ПОМЕСТИТЬ РаспределитьПоЭтапам
		|ИЗ
		|	Регистраторы КАК Т
		|ГДЕ
		|	Т.ТребуетсяРаспределение
		|	И НЕ Т.БазаРаспределенияПоПартиям В (
		|		ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПустаяСсылка),
		|		ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоТоваров),
		|		ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВыручкаОтПродаж),
		|		ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВаловаяПрибыль),
		|		ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СебестоимостьПродаж))
		//-- НЕ УТ
		|";
	
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	
	РаспределениеПрочихЗатратЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * ТаблицаИмяРегистра - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
		
		РаспределениеПрочихЗатратЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// Возвращает список статей, по которым необходимо выполнить распределение.
//
// Параметры;
//		Период - Дата - Период выборки.
//		Организации - Массив - Список организаций по которым анализируются статьи.
//		Подразделения - Массив - Список подразделений по которым анализируются статьи.
//		Состояние - ПеречислениеСсылка.СостоянияРаспределенияРасходов - Фильтр по состоянию.
//
//	Возвращаемое значение:
//		ТаблицаЗначений - Таблица статей к распределению.
//
Функция СтатьиКРаспределению(Период, Организации, Подразделения, Состояние, ВариантРаспределения = Неопределено) Экспорт
	
	ПараметрыЗапроса = ОписаниеПараметровЗапросаПолученияДанныхДляРаспределения();
	ПараметрыЗапроса.Период = Период;
	ПараметрыЗапроса.Организации = Организации;
	ПараметрыЗапроса.Подразделения = Подразделения;
	ПараметрыЗапроса.Состояние = Состояние;
	ПараметрыЗапроса.ВариантРаспределения = ВариантРаспределения;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ИнициализироватьЗапросПолученияДанныхДляРаспределения(Запрос, ПараметрыЗапроса);
	
	Запрос.Текст = ТекстЗапросаДанныеДляРаспределения();
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Выгрузить();
	
КонецФункции

// Получает и помещает таблицу статей расходов для настройки распределения во временное хранилище.
// Параметры:
//	Параметры - Структура - параметры получения статей расходов.
//		* Период - Дата - месяц распределения.
//		* Организации - СправочникСсылка.Организация - организация расходов.
//		* Подразделения - Массив - подразделения расходов.
//		* Состояние - ПеречислениеСсылка.СостоянияРаспределенияРасходов - состояние распределения расхода.
//	АдресХранилища - Строка - адрес во временном хранилище, куда следует поместить результат работы.
//
Процедура СтатьиКРаспределениюВФоне(Параметры, АдресХранилища) Экспорт
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"Документ.РаспределениеПрочихЗатрат.МодульМенеджера.СтатьиКРаспределениюВФоне");
	
	Подразделения = Новый Массив;
	Если ЗначениеЗаполнено(Параметры.Подразделение) Тогда
		Подразделения.Добавить(Параметры.Подразделение);
	КонецЕсли;
	
	Организации = Новый Массив;
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		Организации.Добавить(Параметры.Организация);
	КонецЕсли;
	
	Статьи = СтатьиКРаспределению(Параметры.Период, Организации, 
		Подразделения, Параметры.Состояние);
		
	ПоместитьВоВременноеХранилище(Статьи, АдресХранилища);
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, Статьи.Количество() / 100);
	
КонецПроцедуры

// Формирует документ "Распределение прочих затрат" по заданным условиям.
// Параметры:
//	ПараметрыЗаполнения - Структура - Параметры заполнения документа.
// Возвращаемое значение:
//	Структура - Содержит сведения о документе и состоянии распределения.
//		* СостояниеУпр - ПеречислениеСсылка.СостоянияРаспределенияРасходов - состояние распределения расхода в управленческом учете.
//		* СостояниеРегл - ПеречислениеСсылка.СостоянияРаспределенияРасходов - состояние распределения расхода в регламентированном учете.
//		* ДокументУпр - ДокументСсылка.РаспределениеПрочихЗатрат - созданный документ распределения для управленческого учета.
//		* ДокументРегл - ДокументСсылка.РаспределениеПрочихЗатрат - созданный документ распределения для регламентированном учета.
//		* ОписаниеОшибкиУпр - Строка - текст ошибки возникший в результате создания документа для управленческого учета.
//		* ОписаниеОшибкиРегл - Строка - текст ошибки возникший в результате создания документа для регламентированного учета.
//
Функция СформироватьДокумент(ПараметрыЗаполнения) Экспорт
	
	ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(НСтр("ru = 'Распределение прочих затрат';
															|en = 'Allocation of other costs'"));
	
	ПараметрыВидовУчета = Новый Структура;
	ПараметрыВидовУчета.Вставить("УправленческийУчет", 
		Новый Структура("Состояние, Документ, ОписаниеОшибки", "СостояниеУпр", "ДокументУпр", "ОписаниеОшибкиУпр"));
	ПараметрыВидовУчета.Вставить("РегламентированныйУчет", 
		Новый Структура("Состояние, Документ, ОписаниеОшибки", "СостояниеРегл", "ДокументРегл", "ОписаниеОшибкиРегл"));
	//++ НЕ УТ
	//++ Локализация
	ПараметрыВидовУчета.Вставить("НалоговыйУчет", 
		Новый Структура("Состояние, Документ, ОписаниеОшибки", "СостояниеНал", "ДокументНал", "ОписаниеОшибкиНал"));
	//-- Локализация
	//-- НЕ УТ
		
	ДокументыКПроведению = Новый Массив; // Массив из ДокументОбъект.РаспределениеПрочихЗатрат
	ПараметрыПередачи = Новый Структура;
	
	Для Каждого ВидУчета Из ПараметрыВидовУчета Цикл
		
		РеквизитыВидаУчета = ВидУчета.Значение;
		
		Если ЗначениеЗаполнено(ПараметрыЗаполнения[РеквизитыВидаУчета.Документ])
			Или ПараметрыЗаполнения[ПараметрыВидовУчета[ВидУчета.Ключ].Состояние] = Перечисления.СостоянияРаспределенияРасходов.ТребуетсяРучнойВводДокумента
			Или ПараметрыЗаполнения[ПараметрыВидовУчета[ВидУчета.Ключ].Состояние] = Перечисления.СостоянияРаспределенияРасходов.НеТребуется Тогда
			Продолжить;
		КонецЕсли;
		
		Док = ПодобратьПодходящийДокумент(ДокументыКПроведению, ВидУчета.Ключ, ПараметрыЗаполнения);
		
		Если Не Док = Неопределено Тогда
			
			Если ЗначениеЗаполнено(ПараметрыЗаполнения[РеквизитыВидаУчета.Документ]) 
				И Не ПараметрыЗаполнения[РеквизитыВидаУчета.Документ] = Док.Ссылка Тогда
						
				Попытка
					
					ПредыдущийДокумент = ПараметрыЗаполнения[РеквизитыВидаУчета.Документ].ПолучитьОбъект(); // ДокументОбъект
					ПредыдущийДокумент.Записать(РежимЗаписиДокумента.ОтменаПроведения);
					
				Исключение
					
					ЗаписьЖурналаРегистрации(Док.Метаданные().Имя, 
						УровеньЖурналаРегистрации.Ошибка,,,
						ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					
					ШаблонОшибки = НСтр("ru = 'Не удалось обновить настройки распределения по виду учета ""%1"" по причине: %2.';
										|en = 'Cannot update allocation settings by accounting kind ""%1"". Reason: %2.'", ОбщегоНазначения.КодОсновногоЯзыка());
					
					ПараметрыПередачи.Вставить(РеквизитыВидаУчета["Состояние"], 
						Перечисления.СостоянияРаспределенияРасходов.ГотовоКРаспределениюПоБазе);
					ПараметрыПередачи.Вставить(РеквизитыВидаУчета["Документ"], 
						ПараметрыЗаполнения[РеквизитыВидаУчета.Документ]);
					ПараметрыПередачи.Вставить(РеквизитыВидаУчета["ОписаниеОшибки"], 
						СтрШаблон(ШаблонОшибки, ВидУчета.Ключ, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
					
					Продолжить;
					
				КонецПопытки;
				
			КонецЕсли;
			
			Док[ВидУчета.Ключ] = Истина;
			
			Продолжить;
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ПараметрыЗаполнения[РеквизитыВидаУчета.Документ])
			Или (Не Док = Неопределено 
				И Док.Ссылка = ПараметрыЗаполнения[РеквизитыВидаУчета.Документ]) Тогда
			Док = Документы.РаспределениеПрочихЗатрат.СоздатьДокумент();
		Иначе
			
			Док = ПараметрыЗаполнения[РеквизитыВидаУчета.Документ].ПолучитьОбъект();
			Док.УправленческийУчет = Ложь;
			Док.РегламентированныйУчет = Ложь;
			//++ НЕ УТ
			//++ Локализация
			Док.НалоговыйУчет = Ложь;
			//-- Локализация
			//-- НЕ УТ
			
		КонецЕсли;
			
		Док[ВидУчета.Ключ] = Истина;
		
		Док.Заполнить(ПараметрыЗаполнения);
		
		ДокументыКПроведению.Вставить(0, Док);
				
	КонецЦикла;
	
	Для Каждого ДокументКПроведению Из ДокументыКПроведению Цикл
		
		Если ДокументКПроведению.ПроверитьЗаполнение() Тогда
			Попытка
				ДокументКПроведению.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				ЗаписьЖурналаРегистрации(ДокументКПроведению.Метаданные().Имя, 
					УровеньЖурналаРегистрации.Ошибка,,,
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
		КонецЕсли;
		
		ТекстОшибки = "";
		ТекстыОшибок = Новый Массив;
		Ошибки = ПолучитьСообщенияПользователю(Истина);
		Для Индекс = 0 По Ошибки.Количество() - 1 Цикл
			ТекстыОшибок.Добавить(Ошибки[Индекс].Текст);
		КонецЦикла;
		
		ТекстОшибки = Символы.Таб + СтрСоединить(ТекстыОшибок, Символы.ПС + Символы.Таб);
			
		Для Каждого ВидУчета Из ПараметрыВидовУчета Цикл
			
			Если Не ДокументКПроведению[ВидУчета.Ключ] Тогда
				Продолжить;
			КонецЕсли;
			
			РеквизитыВидаУчета = ВидУчета.Значение;
			
			Если Не ПустаяСтрока(ТекстОшибки) Тогда
				
				ПараметрыПередачи.Вставить(РеквизитыВидаУчета["Состояние"], 
					Перечисления.СостоянияРаспределенияРасходов.ТребуетсяСформироватьДокумент);
				ПараметрыПередачи.Вставить(РеквизитыВидаУчета["ОписаниеОшибки"], ТекстОшибки);
				
			Иначе
				
				ПараметрыПередачи.Вставить(РеквизитыВидаУчета["Состояние"], Перечисления.СостоянияРаспределенияРасходов.ГотовоКРаспределениюПоБазе);
				ПараметрыПередачи.Вставить(РеквизитыВидаУчета["Документ"], ДокументКПроведению.Ссылка);
				
			КонецЕсли;
			
		КонецЦикла;
		
		
	КонецЦикла;
	
	Возврат ПараметрыПередачи;
	
КонецФункции

// Возвращает текст запроса для получения данных о прочих расходах.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаПоступилоПрочихРасходов(ВариантРасчета = "ПредварительныйРасчет", ИнициализацияОВЗ = Истина) Экспорт
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Ссылка,
		|	Реквизиты.Предопределенный КАК Предопределенный,
		|	Реквизиты.ВариантРаспределенияРасходовУпр КАК ВариантРаспределенияРасходов
		|ПОМЕСТИТЬ СтатьиРасходовКРаспределениюУпр
		|ИЗ
		|	ПланВидовХарактеристик.СтатьиРасходов КАК Реквизиты
		|ГДЕ
		|	Реквизиты.ВариантРаспределенияРасходовУпр В (&ВариантыРаспределенияРасходов)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Ссылка,
		|	Реквизиты.Предопределенный КАК Предопределенный,
		|	Реквизиты.ВариантРаспределенияРасходовРегл КАК ВариантРаспределенияРасходов
		|ПОМЕСТИТЬ СтатьиРасходовКРаспределениюРегл
		|ИЗ
		|	ПланВидовХарактеристик.СтатьиРасходов КАК Реквизиты
		|ГДЕ
		|	Реквизиты.ВариантРаспределенияРасходовРегл В (&ВариантыРаспределенияРасходов)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		//++ НЕ УТ
		|ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Ссылка,
		|	Реквизиты.Предопределенный КАК Предопределенный,
		|	Реквизиты.ВариантРаспределенияРасходовНУ КАК ВариантРаспределенияРасходов
		|ПОМЕСТИТЬ СтатьиРасходовКРаспределениюНУ
		|ИЗ
		|	ПланВидовХарактеристик.СтатьиРасходов КАК Реквизиты
		|ГДЕ
		|	Реквизиты.ВариантРаспределенияРасходовНУ В (&ВариантыРаспределенияРасходов)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		//-- НЕ УТ
		|ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Ссылка,
		|	Реквизиты.Предопределенный КАК Предопределенный,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка) КАК ВариантРаспределенияРасходов
		|ПОМЕСТИТЬ СтатьиРасходовКРаспределению
		|ИЗ
		|	СтатьиРасходовКРаспределениюУпр КАК Реквизиты
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Ссылка,
		|	Реквизиты.Предопределенный КАК Предопределенный,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка)
		|ИЗ
		|	СтатьиРасходовКРаспределениюРегл КАК Реквизиты
		|	
		//++ НЕ УТ
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Ссылка,
		|	Реквизиты.Предопределенный КАК Предопределенный,
		|	ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка)
		|ИЗ
		|	СтатьиРасходовКРаспределениюНУ КАК Реквизиты
		|
		//-- НЕ УТ
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		//++ НЕ УТ
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Реквизиты.ОВЗ КАК ОВЗ
		|ПОМЕСТИТЬ ОВЗКРаспределениюУпр
		|ИЗ
		|	ВтОВЗ КАК Реквизиты
		|ГДЕ
		|	Реквизиты.ВариантРаспределенияРасходовУпр В (&ВариантыРаспределенияРасходов)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОВЗ
		|;
		|
		|ВЫБРАТЬ
		|	Реквизиты.ОВЗ КАК ОВЗ
		|ПОМЕСТИТЬ ОВЗКРаспределениюРегл
		|ИЗ
		|	ВтОВЗ КАК Реквизиты
		|ГДЕ
		|	Реквизиты.ВариантРаспределенияРасходовРегл В (&ВариантыРаспределенияРасходов)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОВЗ
		|;
		|
		|ВЫБРАТЬ
		|	Реквизиты.ОВЗ КАК ОВЗ
		|ПОМЕСТИТЬ ОВЗКРаспределениюНУ
		|ИЗ
		|	ВтОВЗ КАК Реквизиты
		|ГДЕ
		|	Реквизиты.ВариантРаспределенияРасходовНУ В (&ВариантыРаспределенияРасходов)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОВЗ
		|;
		|
		|ВЫБРАТЬ
		|	Реквизиты.ОВЗ КАК ОВЗ
		|ПОМЕСТИТЬ ОВЗКРаспределению
		|ИЗ
		|	ОВЗКРаспределениюУпр КАК Реквизиты
		|ОБЪЕДИНИТЬ
		|ВЫБРАТЬ
		|	Реквизиты.ОВЗ
		|ИЗ
		|	ОВЗКРаспределениюРегл КАК Реквизиты
		|ОБЪЕДИНИТЬ
		|ВЫБРАТЬ
		|	Реквизиты.ОВЗ
		|ИЗ
		|	ОВЗКРаспределениюНУ КАК Реквизиты
		|	
		|ИНДЕКСИРОВАТЬ ПО
		|	ОВЗ
		|;
		|
		//-- НЕ УТ
		|////////////////////////////////////////////////////////////////////////////////
		|
		|// Для целей отладки предусмотрен вывод источников расходов в исходном виде
		|// ВтИсточникиРасходов
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Расходы.Организация 					КАК Организация,
		|	Расходы.Подразделение 					КАК Подразделение,
		|	Расходы.НаправлениеДеятельности 		КАК НаправлениеДеятельности,
		|	Расходы.СтатьяРасходов 					КАК СтатьяРасходов,
		|	Расходы.АналитикаРасходов 				КАК АналитикаРасходов,
		|	Расходы.НаправлениеДеятельностиДляСвязи	КАК НаправлениеДеятельностиДляСвязи,
		|	Расходы.СтатьяРасходовДляСвязи			КАК СтатьяРасходовДляСвязи,
		|	НЕ Расходы.СтатьяРасходовДляСвязи = Расходы.СтатьяРасходов КАК ЭтоРасходОВЗ,
		|	СУММА(Расходы.Сумма) 					КАК Сумма,
		|	СУММА(Расходы.СуммаБезНДС) 				КАК СуммаБезНДС,
		|	СУММА(Расходы.СуммаРегл) 				КАК СуммаРегл,
		|	СУММА(Расходы.ПостояннаяРазница) 		КАК ПостояннаяРазница,
		|	СУММА(Расходы.ВременнаяРазница) 		КАК ВременнаяРазница,
		|	СУММА(Расходы.СуммаУпр) 				КАК СуммаУпр,
		|	СУММА(Расходы.СуммаРеглРасход) 			КАК СуммаРеглРасход,
		|	СУММА(Расходы.ПостояннаяРазницаРасход) 	КАК ПостояннаяРазницаРасход,
		|	СУММА(Расходы.ВременнаяРазницаРасход) 	КАК ВременнаяРазницаРасход		
		|ПОМЕСТИТЬ РасходыКРаспределению
		|ИЗ
		|	&ИсточникиРасходов КАК Расходы
		|
		|СГРУППИРОВАТЬ ПО
		|	Расходы.Организация,
		|	Расходы.Подразделение,
		|	Расходы.НаправлениеДеятельности,
		|	Расходы.СтатьяРасходов,
		|	Расходы.АналитикаРасходов,
		|	Расходы.НаправлениеДеятельностиДляСвязи,
		|	Расходы.СтатьяРасходовДляСвязи,
		|	НЕ Расходы.СтатьяРасходовДляСвязи = Расходы.СтатьяРасходов
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Расходы.СтатьяРасходов,
		|	Расходы.АналитикаРасходов,
		|	Расходы.Подразделение
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Расходы.Организация 					КАК Организация,
		|	Расходы.Подразделение 					КАК Подразделение,
		|	Расходы.НаправлениеДеятельностиДляСвязи	КАК НаправлениеДеятельности,
		|	Расходы.СтатьяРасходовДляСвязи			КАК СтатьяРасходов,
		|	Расходы.АналитикаРасходов 				КАК АналитикаРасходов,
		|	&ВариантРаспределенияРасходовУпр КАК ВариантРаспределенияРасходовУпр,
		|	&ВариантРаспределенияРасходовРегл КАК ВариантРаспределенияРасходовРегл,
		|	&ВариантРаспределенияРасходовНУ КАК ВариантРаспределенияРасходовНУ,
		|	СУММА(Расходы.Сумма) 					КАК Сумма,
		|	СУММА(Расходы.СуммаБезНДС) 				КАК СуммаБезНДС,
		|	СУММА(Расходы.СуммаРегл) 				КАК СуммаРегл,
		|	СУММА(Расходы.ПостояннаяРазница) 		КАК ПостояннаяРазница,
		|	СУММА(Расходы.ВременнаяРазница) 		КАК ВременнаяРазница,
		|	СУММА(Расходы.СуммаУпр) 				КАК СуммаУпр,
		|	СУММА(Расходы.СуммаРеглРасход) 			КАК СуммаРеглРасход,
		|	СУММА(Расходы.ПостояннаяРазницаРасход) 	КАК ПостояннаяРазницаРасход,
		|	СУММА(Расходы.ВременнаяРазницаРасход) 	КАК ВременнаяРазницаРасход		
		|ПОМЕСТИТЬ РасходыКРаспределениюСводно
		|ИЗ
		|	РасходыКРаспределению КАК Расходы
		|
		|СГРУППИРОВАТЬ ПО
		|	Расходы.Организация,
		|	Расходы.Подразделение,
		|	Расходы.АналитикаРасходов,
		|	Расходы.НаправлениеДеятельностиДляСвязи,
		|	Расходы.СтатьяРасходовДляСвязи,
		|	&ВариантРаспределенияРасходовУпр,
		|	&ВариантРаспределенияРасходовРегл,
		|	&ВариантРаспределенияРасходовНУ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Расходы.СтатьяРасходовДляСвязи,
		|	Расходы.АналитикаРасходов,
		|	Расходы.Подразделение
		|;
		|";

		ВариантУпр = "Расходы.СтатьяРасходов.ВариантРаспределенияРасходовУпр";
		ВариантРегл = "Расходы.СтатьяРасходов.ВариантРаспределенияРасходовРегл";
		ВариантНУ = "Расходы.СтатьяРасходов.ВариантРаспределенияРасходовНУ";
		
		//++ НЕ УТ
		Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОбъектамВозникновенияЗатрат") Тогда
			ВариантУпр = "
			|ВЫБОР 
			|		КОГДА 
			|			Расходы.СтатьяРасходовДляСвязи = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
			|		ТОГДА Расходы.АналитикаРасходов.ВариантРаспределенияРасходовУпр
			|		ИНАЧЕ Расходы.СтатьяРасходов.ВариантРаспределенияРасходовУпр
			|	КОНЕЦ";
			
			ВариантРегл = "
			|ВЫБОР 
			|		КОГДА 
			|			Расходы.СтатьяРасходовДляСвязи = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
			|		ТОГДА Расходы.АналитикаРасходов.ВариантРаспределенияРасходовРегл
			|		ИНАЧЕ Расходы.СтатьяРасходов.ВариантРаспределенияРасходовРегл
			|	КОНЕЦ";
			
			ВариантНУ = "
			|ВЫБОР 
			|		КОГДА 
			|			Расходы.СтатьяРасходовДляСвязи = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
			|		ТОГДА Расходы.АналитикаРасходов.ВариантРаспределенияРасходовНУ
			|		ИНАЧЕ Расходы.СтатьяРасходов.ВариантРаспределенияРасходовНУ
			|	КОНЕЦ";
		КонецЕсли;
		//-- НЕ УТ
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВариантРаспределенияРасходовУпр", ВариантУпр);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВариантРаспределенияРасходовРегл", ВариантРегл);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВариантРаспределенияРасходовНУ", ВариантНУ);
		
		Если РасчетСебестоимостиПовтИсп.ЗначенияТехнологическихПараметров().ФормироватьВтИсточникиРасходов Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "// ВтИсточникиРасходов", ТекстЗапросаПоступилоПрочихРасходовИсточники(ВариантРасчета, Истина)+";");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИсточникиРасходов", "ВтИсточникиРасходов");
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИсточникиРасходов", "("+ТекстЗапросаПоступилоПрочихРасходовИсточники(ВариантРасчета)+")");
		КонецЕсли;
		
	Возврат 
		//++ НЕ УТ
		?(ИнициализацияОВЗ, РасчетСебестоимостиПостатейныеЗатраты.ТекстВТпоОВЗ() 
			+ РасчетСебестоимостиПостатейныеЗатраты.ТекстЗначенияПоказателейРаспределенияНаОВЗ(),"") +
		//-- НЕ УТ
		ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса для получения данных для рабочего места по распределению расходов.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаДанныеДляРаспределения(ВыбиратьПервые = 0, ПоместитьСостояниеРасходовВВТ = Ложь, ВызовДляСКД = Ложь) Экспорт
	
	// Распределяемые документы.
	ТекстЗапроса = ТекстЗапросаПоступилоПрочихРасходов("РабочееМесто", ВызовДляСКД) +
		"
		//++ НЕ УТ
		|ВЫБРАТЬ 
		|	Правила.Ссылка КАК Правило
		|ПОМЕСТИТЬ ПравилаРаспределенияСРучнымВводом
		|ИЗ Справочник.ПравилаРаспределенияРасходов КАК Правила
		|ГДЕ Правила.УточнятьПартииВДокументе
		|;
		|
		//-- НЕ УТ
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ЕСТЬNULL(РаспределениеРасходов.НаправлениеДеятельности, ДД.НаправлениеДеятельности) КАК НаправлениеДеятельности,
		|	ЕСТЬNULL(РаспределениеРасходов.СтатьяРасходов, ДД.СтатьяРасходов) КАК СтатьяРасходов,
		|	ДД.АналитикаРасходов,
		//++ НЕ УТ
		|	МАКСИМУМ((ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ТранзитРасходовМеждуОВЗ)
		|				И (ДД.Сумма <> 0 ИЛИ ДД.СуммаБезНДС <> 0 ИЛИ ДД.СуммаУпр <> 0))) КАК ЕстьДвиженияМеждуОВЗУпр,
		|	МАКСИМУМ((ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ТранзитРасходовМеждуОВЗ)
		|				И ДД.СуммаРегл <> 0)) КАК ЕстьДвиженияМеждуОВЗРегл,
		|	МАКСИМУМ((ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ТранзитРасходовМеждуОВЗ)
		|				И (ДД.СуммаРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница <> 0))) КАК ЕстьДвиженияМеждуОВЗНал,
		//-- НЕ УТ
		|	МАКСИМУМ((ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности)
		|				И (ДД.Сумма <> 0 ИЛИ ДД.СуммаБезНДС <> 0 ИЛИ ДД.СуммаУпр <> 0))) КАК ЕстьДвиженияНаФинансовыйРезультатУпр,
		|	МАКСИМУМ(НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности)
		|				И НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов)
		|				И НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеНормируемыхРасходовПоНУ)
		//++ НЕ УТ
		|				И НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ТранзитРасходовМеждуОВЗ)
		//-- НЕ УТ
		|				И (ДД.Сумма <> 0 ИЛИ ДД.СуммаБезНДС <> 0 ИЛИ ДД.СуммаУпр <> 0)) КАК ЕстьДвиженияНаПроизводствоУпр,
		|	МАКСИМУМ((ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности)
		|				И ДД.СуммаРегл <> 0)) КАК ЕстьДвиженияНаФинансовыйРезультатРегл,
		|	МАКСИМУМ((ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности)
		|				И (ДД.СуммаРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница <> 0))) КАК ЕстьДвиженияНаФинансовыйРезультатНал,
		|	МАКСИМУМ(НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности)
		|				И НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов)
		|				И НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеНормируемыхРасходовПоНУ)
		//++ НЕ УТ
		|				И НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ТранзитРасходовМеждуОВЗ)
		//-- НЕ УТ
		|				И ДД.СуммаРегл <> 0) ЕстьДвиженияНаПроизводствоРегл,
		|	МАКСИМУМ(НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовПоНаправлениямДеятельности)
		|				И НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов)
		|				И НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеНормируемыхРасходовПоНУ)
		//++ НЕ УТ
		|				И НЕ ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ТранзитРасходовМеждуОВЗ)
		//-- НЕ УТ
		|				И (ДД.СуммаРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница <> 0)) ЕстьДвиженияНаПроизводствоНал,
		|	МАКСИМУМ(ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов)
		|				И (ДД.Сумма <> 0 ИЛИ ДД.СуммаБезНДС <> 0 ИЛИ ДД.СуммаУпр <> 0)) ЕстьДвиженияНаДругиеСтатьиУпр,
		|	МАКСИМУМ(ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов)
		|				И ДД.СуммаРегл <> 0) ЕстьДвиженияНаДругиеСтатьиРегл,
		|	МАКСИМУМ(ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов)
		|				И (ДД.СуммаРегл - ДД.ПостояннаяРазница - ДД.ВременнаяРазница <> 0)) ЕстьДвиженияНаДругиеСтатьиНал,
		|	МАКСИМУМ(ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеНормируемыхРасходовПоНУ)
		|				И (ДД.ПостояннаяРазница <> 0 ИЛИ ДД.ВременнаяРазница <> 0)) КАК ЕстьСписаниеНУ
		|ПОМЕСТИТЬ ДвиженияСтатейРасходов
		|ИЗ
		|	РегистрНакопления.ПрочиеРасходы КАК ДД
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК РаспределениеРасходов
		|		ПО ДД.Регистратор = РаспределениеРасходов.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеДоходовПоНаправлениямДеятельности КАК РаспределениеДоходов
		|		ПО ДД.Регистратор = РаспределениеДоходов.Ссылка
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И (&ПоВсемОрганизациям ИЛИ ДД.Организация В (&МассивОрганизаций))
		|	И (&ПоВсемПодразделениям ИЛИ ДД.Подразделение В (&СписокПодразделений))
		|	И ДД.Активность
		|	И НЕ (РаспределениеРасходов.Ссылка ЕСТЬ NULL
		|		И РаспределениеДоходов.Ссылка ЕСТЬ NULL)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.Организация,
		|	ДД.Подразделение,
		|	ЕСТЬNULL(РаспределениеРасходов.НаправлениеДеятельности, ДД.НаправлениеДеятельности),
		|	ЕСТЬNULL(РаспределениеРасходов.СтатьяРасходов, ДД.СтатьяРасходов),
		|	ДД.АналитикаРасходов
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СтатьяРасходов,
		|	ДД.АналитикаРасходов,
		|	ДД.Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Реквизиты.Ссылка КАК Документ,
		|	Реквизиты.УправленческийУчет,
		|	Реквизиты.РегламентированныйУчет,
		|	Реквизиты.НалоговыйУчет,
		//++ НЕ УТ
		|	Реквизиты.ОставитьВНЗП,
		|	Реквизиты.ДоляСтоимостиНаНЗП,
		|	Реквизиты.ВсегоДолейСтоимости,
		//-- НЕ УТ
		|	Реквизиты.НазначениеНастройкиРаспределения КАК НазначениеНастройкиРаспределения,
		|	Реквизиты.Организация КАК Организация,
		|	Реквизиты.Подразделение КАК Подразделение,
		|	Реквизиты.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Реквизиты.СтатьяРасходов КАК СтатьяРасходов,
		|	Реквизиты.АналитикаРасходов КАК АналитикаРасходов
		|ПОМЕСТИТЬ ДокументыРаспределения
		|ИЗ
		|	Документ.РаспределениеПрочихЗатрат КАК Реквизиты
		|ГДЕ
		|	Реквизиты.Дата МЕЖДУ &НачалоПериода И КОНЕЦПЕРИОДА(&КонецПериода, МЕСЯЦ)
		|	И (&ПоВсемОрганизациям ИЛИ Реквизиты.Организация В (&МассивОрганизаций))
		|	И (&ПоВсемПодразделениям ИЛИ Реквизиты.Подразделение В (&СписокПодразделений))
		|	И Реквизиты.Проведен
		|
		|ИНДЕКСИРОВАТЬ ПО
		|
		|СтатьяРасходов,
		|АналитикаРасходов,
		|Подразделение,
		|Организация,
		|НаправлениеДеятельности
		//++ НЕ УТ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Организация,
		|	Т.Подразделение,
		|	Т.НаправлениеДеятельности,
		|	Т.СтатьяРасходов,
		|	Т.АналитикаРасходов
		|ПОМЕСТИТЬ НормированиеНаСчетахУчетах
		|ИЗ
		|	РегистрНакопления.ПрочиеРасходы КАК Т
		|ГДЕ
		|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Т.Активность
		|	И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеНормируемыхРасходовПоНУ)
		//-- НЕ УТ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НеРаспределенные.Организация КАК Организация,
		|	НеРаспределенные.Подразделение КАК Подразделение,
		|	НеРаспределенные.СтатьяРасходов КАК СтатьяРасходов,
		|	НеРаспределенные.АналитикаРасходов КАК АналитикаРасходов,
		|	НеРаспределенные.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ВЫБОР
		|		КОГДА НЕ НеРаспределенные.ВариантРаспределенияРасходовУпр В (
		//++ НЕ УТ
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты),
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства),
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат),
		//-- НЕ УТ
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров),
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности))
		|			ТОГДА ИСТИНА
		|		КОГДА НЕ МАКСИМУМ(НеРаспределенные.ЕстьПогрешностьРасчетаУУ)
		|			ТОГДА (СУММА(НеРаспределенные.СуммаОстаток) = 0
		|					И СУММА(НеРаспределенные.СуммаБезНДСОстаток) = 0
		|					И СУММА(НеРаспределенные.СуммаУпрОстаток) = 0)
		|		ИНАЧЕ (СУММА(НеРаспределенные.СуммаОстаток) МЕЖДУ -&ЗначениеПогрешностиУпр И &ЗначениеПогрешностиУпр
		|			И СУММА(НеРаспределенные.СуммаБезНДСОстаток) МЕЖДУ -&ЗначениеПогрешностиУпр И &ЗначениеПогрешностиУпр
		|			И СУММА(НеРаспределенные.СуммаУпрОстаток) МЕЖДУ -&ЗначениеПогрешностиУпр И &ЗначениеПогрешностиУпр)
		|	КОНЕЦ КАК РаспределеноВУУ,
		|	ВЫБОР
		|		КОГДА НЕ НеРаспределенные.ВариантРаспределенияРасходовРегл В (
		//++ НЕ УТ
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты),
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства),
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат),
		//-- НЕ УТ
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров),
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности))
		|			ТОГДА ИСТИНА
		|		КОГДА НЕ МАКСИМУМ(НеРаспределенные.ЕстьПогрешностьРасчетаБУ)
		|			ТОГДА СУММА(НеРаспределенные.СуммаРеглОстаток) = 0
		|		ИНАЧЕ СУММА(НеРаспределенные.СуммаРеглОстаток) МЕЖДУ -&ЗначениеПогрешностиРегл И &ЗначениеПогрешностиРегл
		|	КОНЕЦ КАК РаспределеноВБУ,
		|	ВЫБОР
		|		КОГДА НЕ НеРаспределенные.ВариантРаспределенияРасходовНУ В (
		//++ НЕ УТ
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты),
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства),
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат),
		//-- НЕ УТ
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров),
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности))
		|			ТОГДА ИСТИНА
		|		КОГДА НЕ МАКСИМУМ(НеРаспределенные.ЕстьПогрешностьРасчетаНУ)
		|			ТОГДА (СУММА(НеРаспределенные.СуммаНУОстаток) = 0)
		|		ИНАЧЕ (СУММА(НеРаспределенные.СуммаНУОстаток) МЕЖДУ -&ЗначениеПогрешностиРегл И &ЗначениеПогрешностиРегл)
		|	КОНЕЦ КАК  РаспределеноВНУ
		|ПОМЕСТИТЬ РасходыПослеРаспределения
		|ИЗ
		|	(ВЫБРАТЬ
		|		НеРаспределенные.Организация КАК Организация,
		|		НеРаспределенные.Подразделение КАК Подразделение,
		|		НеРаспределенные.СтатьяРасходов КАК СтатьяРасходов,
		|		НеРаспределенные.АналитикаРасходов КАК АналитикаРасходов,
		|		НеРаспределенные.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		Статьи.ВариантРаспределенияРасходовУпр,
		|		Статьи.ВариантРаспределенияРасходовРегл,
		|		Статьи.ВариантРаспределенияРасходовНУ,
		|		ВЫБОР
		|			КОГДА НЕ Статьи.ВариантРаспределенияРасходовУпр В (&ВариантыРаспределенияРасходов)
		//++ НЕ УТ
		|					ИЛИ Статьи.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат)
		//-- НЕ УТ
		|				ТОГДА 0
		|			ИНАЧЕ
		|				НеРаспределенные.СуммаОстаток
		|		КОНЕЦ КАК СуммаОстаток,
		|		ВЫБОР
		|			КОГДА НЕ Статьи.ВариантРаспределенияРасходовУпр В (&ВариантыРаспределенияРасходов)
		//++ НЕ УТ
		|					ИЛИ Статьи.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат)
		//-- НЕ УТ
		|				ТОГДА 0
		|			ИНАЧЕ
		|				НеРаспределенные.СуммаБезНДСОстаток
		|		КОНЕЦ КАК СуммаБезНДСОстаток,
		|		ВЫБОР
		|			КОГДА НЕ Статьи.ВариантРаспределенияРасходовУпр В (&ВариантыРаспределенияРасходов)
		//++ НЕ УТ
		|					ИЛИ Статьи.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат)
		//-- НЕ УТ
		|				ТОГДА 0
		|			ИНАЧЕ
		|				НеРаспределенные.СуммаУпрОстаток
		|		КОНЕЦ КАК СуммаУпрОстаток,
		|		ВЫБОР
		|			КОГДА &ИсключитьИзКонтроляРазницы
		|				ТОГДА 0
		//++ НЕ УТ
		|			КОГДА ЕСТЬNULL(ДолиСписания.Нормируемые, ЛОЖЬ)
		|				И (ЕСТЬNULL(ДолиСписания.НовыйАлгоритмСписания, ЛОЖЬ) ИЛИ НЕ НормированиеНаСчетахУчетах.Организация ЕСТЬ NULL)
		|				ИЛИ Статьи.ВариантРаспределенияРасходовРегл В (
		|						ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты),
		|						ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства),
		|						ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат))
		|					И Статьи.ВариантРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|				ИЛИ НЕ ЕСТЬNULL(ДолиСписания.Нормируемые, ЛОЖЬ)
		|					И НЕ ЕСТЬNULL(ДолиСписания.Транспортные, ЛОЖЬ)
		|					И Статьи.ВариантРаспределенияРасходовРегл В (
		|						ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты),
		|						ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности))
		|				ТОГДА НеРаспределенные.СуммаРеглОстаток - НеРаспределенные.ПостояннаяРазницаОстаток - НеРаспределенные.ВременнаяРазницаОстаток
		//-- НЕ УТ
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК СуммаНУОстаток,
		|		ВЫБОР
		|			КОГДА НЕ Статьи.ВариантРаспределенияРасходовРегл В (&ВариантыРаспределенияРасходов)
		//++ НЕ УТ
		|					ИЛИ Статьи.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат)
		//-- НЕ УТ
		|				ТОГДА 0
		|			ИНАЧЕ
		|				НеРаспределенные.СуммаРеглОстаток
		|		КОНЕЦ КАК СуммаРеглОстаток,
		|		ВЫБОР
		|			КОГДА &ИсключитьИзКонтроляРазницы
		|				ТОГДА 0
		|			ИНАЧЕ
		|				НеРаспределенные.ВременнаяРазницаОстаток
		|		КОНЕЦ КАК ВременнаяРазницаОстаток,
		|		ВЫБОР
		|			КОГДА &ИсключитьИзКонтроляРазницы
		|				ТОГДА 0
		|			ИНАЧЕ
		|				НеРаспределенные.ПостояннаяРазницаОстаток
		|		КОНЕЦ КАК ПостояннаяРазницаОстаток,
		|		ЛОЖЬ КАК ЕстьПогрешностьРасчетаУУ,
		|		ЛОЖЬ КАК ЕстьПогрешностьРасчетаБУ,
		|		ЛОЖЬ КАК ЕстьПогрешностьРасчетаНУ
		|	ИЗ
		|		РегистрНакопления.ПрочиеРасходы.Остатки(
		|				&ГраницаКонецПериода,
		|				(Организация, Подразделение, НаправлениеДеятельности, СтатьяРасходов, АналитикаРасходов) В 
		|					(ВЫБРАТЬ 
		|						Т.Организация,
		|						Т.Подразделение,
		|						Т.НаправлениеДеятельности,
		|						Т.СтатьяРасходов,
		|						Т.АналитикаРасходов 
		|					ИЗ РасходыКРаспределениюСводно КАК Т
		|					ГДЕ НЕ Т.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка))) КАК НеРаспределенные
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
		|			ПО НеРаспределенные.СтатьяРасходов = Статьи.Ссылка
		//++ НЕ УТ
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВтДолиСписанияКосвенныхРасходов КАК ДолиСписания
		|			ПО НеРаспределенные.СтатьяРасходов = ДолиСписания.СтатьяРасходов
		|				И НеРаспределенные.Организация = ДолиСписания.Организация
		|			ЛЕВОЕ СОЕДИНЕНИЕ НормированиеНаСчетахУчетах КАК НормированиеНаСчетахУчетах
		|			ПО НеРаспределенные.Организация = НормированиеНаСчетахУчетах.Организация
		|				И НеРаспределенные.Подразделение = НормированиеНаСчетахУчетах.Подразделение
		|				И НеРаспределенные.НаправлениеДеятельности = НормированиеНаСчетахУчетах.НаправлениеДеятельности
		|				И НеРаспределенные.СтатьяРасходов = НормированиеНаСчетахУчетах.СтатьяРасходов
		|				И НеРаспределенные.АналитикаРасходов = НормированиеНаСчетахУчетах.АналитикаРасходов
		//++ Локализация
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		РегламентныеДвижения.Организация КАК Организация,
		|		РегламентныеДвижения.Подразделение КАК Подразделение,
		|		РегламентныеДвижения.СтатьяРасходов КАК СтатьяРасходов,
		|		РегламентныеДвижения.АналитикаРасходов КАК АналитикаРасходов,
		|		РегламентныеДвижения.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		Статьи.ВариантРаспределенияРасходовУпр,
		|		Статьи.ВариантРаспределенияРасходовРегл,
		|		Статьи.ВариантРаспределенияРасходовНУ,
		|		0 КАК СуммаОстаток,
		|		0 КАК СуммаБезНДСОстаток,
		|		0 КАК СуммаУпрОстаток,
		|		0 КАК СуммаНУОстаток,
		|		0 КАК СуммаРеглОстаток,
		|		0 КАК ВременнаяРазницаОстаток,
		|		РегламентныеДвижения.ПостояннаяРазница КАК ПостояннаяРазницаОстаток,
		|		ЛОЖЬ КАК ЕстьПогрешностьРасчетаУУ,
		|		ЛОЖЬ КАК ЕстьПогрешностьРасчетаБУ,
		|		ЛОЖЬ КАК ЕстьПогрешностьРасчетаНУ
		|	ИЗ
		|		РегистрНакопления.ПрочиеРасходы КАК РегламентныеДвижения
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РасходыКРаспределению КАК Расходы
		|			ПО РегламентныеДвижения.Организация = Расходы.Организация
		|				И РегламентныеДвижения.Подразделение = Расходы.Подразделение
		|				И РегламентныеДвижения.НаправлениеДеятельности = Расходы.НаправлениеДеятельности
		|				И РегламентныеДвижения.СтатьяРасходов = Расходы.СтатьяРасходов
		|				И РегламентныеДвижения.АналитикаРасходов = Расходы.АналитикаРасходов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
		|			ПО РегламентныеДвижения.СтатьяРасходов = Статьи.Ссылка
		|	ГДЕ
		|		РегламентныеДвижения.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И РегламентныеДвижения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		И ТИПЗНАЧЕНИЯ(РегламентныеДвижения.Регистратор) = ТИП(Документ.РегламентнаяОперация)
		|		И НЕ РегламентныеДвижения.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеКосвенныхРасходов)
		//-- Локализация
		|		
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Расходы.Организация,
		|		Расходы.Подразделение,
		|		Расходы.СтатьяРасходов,
		|		Расходы.АналитикаРасходов,
		|		Расходы.НаправлениеДеятельности,
		|		Статьи.ВариантРаспределенияРасходовУпр,
		|		Статьи.ВариантРаспределенияРасходовРегл,
		|		Статьи.ВариантРаспределенияРасходовНУ,
		|		ВЫБОР
		|			КОГДА НЕ ДокументыРаспределенияУУ.Документ ЕСТЬ NULL
		|				ТОГДА -Расходы.Сумма * ДокументыРаспределенияУУ.ДоляСтоимостиНаНЗП / ДокументыРаспределенияУУ.ВсегоДолейСтоимости
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА НЕ ДокументыРаспределенияУУ.Документ ЕСТЬ NULL
		|				ТОГДА -Расходы.СуммаБезНДС * ДокументыРаспределенияУУ.ДоляСтоимостиНаНЗП / ДокументыРаспределенияУУ.ВсегоДолейСтоимости
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА НЕ ДокументыРаспределенияУУ.Документ ЕСТЬ NULL
		|				ТОГДА -Расходы.СуммаУпр * ДокументыРаспределенияУУ.ДоляСтоимостиНаНЗП / ДокументыРаспределенияУУ.ВсегоДолейСтоимости
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА &ИсключитьИзКонтроляРазницы
		|				ТОГДА 0
		|			КОГДА НЕ ДокументыРаспределенияНУ.Документ ЕСТЬ NULL
		|				ТОГДА (
		|					ВЫБОР
		|						КОГДА ДокументыРаспределенияНУ.НазначениеНастройкиРаспределения = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеСтатейРасходовПоЭтапамПроизводства)
		|							И Статьи.ВариантРаспределенияРасходовНУ = Статьи.ВариантРаспределенияРасходовРегл
		|						ТОГДА 
		|							Расходы.СуммаРегл - Расходы.ПостояннаяРазница - Расходы.ВременнаяРазница
		|						ИНАЧЕ
		|							0
		|					КОНЕЦ) * ДокументыРаспределенияНУ.ДоляСтоимостиНаНЗП / ДокументыРаспределенияНУ.ВсегоДолейСтоимости
		|			ИНАЧЕ 0
		|		КОНЕЦ * -1,
		|		ВЫБОР
		|			КОГДА НЕ ДокументыРаспределенияБУ.Документ ЕСТЬ NULL
		|				ТОГДА -Расходы.СуммаРегл * ДокументыРаспределенияБУ.ДоляСтоимостиНаНЗП / ДокументыРаспределенияБУ.ВсегоДолейСтоимости
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА &ИсключитьИзКонтроляРазницы
		|				ТОГДА 0
		|			КОГДА НЕ ДокументыРаспределенияНУ.Документ ЕСТЬ NULL
		|				ТОГДА (Расходы.ВременнаяРазница
		|					+ ВЫБОР
		|						КОГДА ДокументыРаспределенияНУ.НазначениеНастройкиРаспределения = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеСтатейРасходовПоЭтапамПроизводства)
		|							И Статьи.ВариантРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|							ТОГДА Расходы.СуммаРегл - Расходы.ПостояннаяРазница - Расходы.ВременнаяРазница
		|						ИНАЧЕ
		|							0
		|					КОНЕЦ) * ДокументыРаспределенияНУ.ДоляСтоимостиНаНЗП / ДокументыРаспределенияНУ.ВсегоДолейСтоимости
		|			ИНАЧЕ 0
		|		КОНЕЦ * -1,
		|		ВЫБОР
		|			КОГДА &ИсключитьИзКонтроляРазницы
		|				ТОГДА 0
		|			КОГДА НЕ ДокументыРаспределенияНУ.Документ ЕСТЬ NULL
		|				ТОГДА -Расходы.ПостояннаяРазница * ДокументыРаспределенияНУ.ДоляСтоимостиНаНЗП / ДокументыРаспределенияНУ.ВсегоДолейСтоимости
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		(НЕ ДокументыРаспределенияУУ.Документ ЕСТЬ NULL),
		|		(НЕ ДокументыРаспределенияБУ.Документ ЕСТЬ NULL),
		|		(НЕ ДокументыРаспределенияНУ.Документ ЕСТЬ NULL)
		|	ИЗ
		|		РасходыКРаспределениюСводно КАК Расходы
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
		|			ПО Расходы.СтатьяРасходов = Статьи.Ссылка
		|			ЛЕВОЕ СОЕДИНЕНИЕ ДокументыРаспределения КАК ДокументыРаспределенияУУ
		|			ПО Расходы.Организация = ДокументыРаспределенияУУ.Организация
		|				И Расходы.Подразделение = ДокументыРаспределенияУУ.Подразделение
		|				И Расходы.НаправлениеДеятельности = ДокументыРаспределенияУУ.НаправлениеДеятельности
		|				И Расходы.СтатьяРасходов = ДокументыРаспределенияУУ.СтатьяРасходов
		|				И Расходы.АналитикаРасходов = ДокументыРаспределенияУУ.АналитикаРасходов
		|				И ДокументыРаспределенияУУ.УправленческийУчет
		|				И (ДокументыРаспределенияУУ.ОставитьВНЗП)
		|			ЛЕВОЕ СОЕДИНЕНИЕ ДокументыРаспределения КАК ДокументыРаспределенияБУ
		|			ПО Расходы.Организация = ДокументыРаспределенияБУ.Организация
		|				И Расходы.Подразделение = ДокументыРаспределенияБУ.Подразделение
		|				И Расходы.НаправлениеДеятельности = ДокументыРаспределенияБУ.НаправлениеДеятельности
		|				И Расходы.СтатьяРасходов = ДокументыРаспределенияБУ.СтатьяРасходов
		|				И Расходы.АналитикаРасходов = ДокументыРаспределенияБУ.АналитикаРасходов
		|				И ДокументыРаспределенияБУ.РегламентированныйУчет
		|				И (ДокументыРаспределенияБУ.ОставитьВНЗП)
		|			ЛЕВОЕ СОЕДИНЕНИЕ ДокументыРаспределения КАК ДокументыРаспределенияНУ
		|			ПО Расходы.Организация = ДокументыРаспределенияНУ.Организация
		|				И Расходы.Подразделение = ДокументыРаспределенияНУ.Подразделение
		|				И Расходы.НаправлениеДеятельности = ДокументыРаспределенияНУ.НаправлениеДеятельности
		|				И Расходы.СтатьяРасходов = ДокументыРаспределенияНУ.СтатьяРасходов
		|				И Расходы.АналитикаРасходов = ДокументыРаспределенияНУ.АналитикаРасходов
		|				И ДокументыРаспределенияНУ.НалоговыйУчет
		|				И (ДокументыРаспределенияНУ.ОставитьВНЗП)
		|	ГДЕ
		|		НЕ ДокументыРаспределенияУУ.Документ ЕСТЬ NULL
		|		ИЛИ НЕ ДокументыРаспределенияБУ.Документ ЕСТЬ NULL
		|		ИЛИ НЕ ДокументыРаспределенияНУ.Документ ЕСТЬ NULL
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Расходы.Организация,
		|		Расходы.Подразделение,
		|		Расходы.СтатьяРасходов,
		|		Расходы.АналитикаРасходов,
		|		Расходы.НаправлениеДеятельности,
		|		Статьи.ВариантРаспределенияРасходовУпр,
		|		Статьи.ВариантРаспределенияРасходовРегл,
		|		Статьи.ВариантРаспределенияРасходовНУ,
		|		0,
		|		0,
		|		0,
		|		0,
		|		ВЫБОР
		|			КОГДА НЕ ДолиСписания.Транспортные
		|				ТОГДА 0
		|			КОГДА ДолиСписания.ДоляСписания = 0
		|				ТОГДА Расходы.СуммаРегл
		|			КОГДА Расходы.СуммаРеглРасход = 0
		|				ТОГДА 0
		|			ИНАЧЕ
		|				Расходы.СуммаРеглРасход / ДолиСписания.ДоляСписания
		|					+ (Расходы.СуммаРегл * ДолиСписания.ДоляСписания - Расходы.СуммаРеглРасход) / ДолиСписания.ДоляСписания - Расходы.СуммаРеглРасход
		|		КОНЕЦ * (-1) КАК СуммаРегл,
		|		ВЫБОР
		|			КОГДА ДолиСписания.Нормируемые
		|				ТОГДА ВЫБОР
		|					КОГДА НЕ ДолиСписания.НовыйАлгоритмСписания
		|						ТОГДА Расходы.ВременнаяРазница - Расходы.ВременнаяРазницаРасход
		|					ИНАЧЕ 0
		|				КОНЕЦ
		|			КОГДА НЕ ДолиСписания.Транспортные
		|				ТОГДА 0
		|			КОГДА ДолиСписания.ДоляСписания = 0
		|				ТОГДА Расходы.ВременнаяРазница
		|			КОГДА Расходы.ВременнаяРазницаРасход = 0
		|				ТОГДА 0
		|			ИНАЧЕ Расходы.ВременнаяРазницаРасход / ДолиСписания.ДоляСписания
		|					+ (Расходы.ВременнаяРазница * ДолиСписания.ДоляСписания - Расходы.ВременнаяРазницаРасход) / ДолиСписания.ДоляСписания - Расходы.ВременнаяРазницаРасход
		|		КОНЕЦ * (-1) КАК ВременнаяРазница,
		|		ВЫБОР
		|			КОГДА НЕ (ДолиСписания.Транспортные
		|					ИЛИ ДолиСписания.Нормируемые)
		|				ИЛИ Расходы.Организация = ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
		|				ТОГДА 0
		|			КОГДА ДолиСписания.Нормируемые
		|				ТОГДА ВЫБОР
		|					КОГДА НЕ ДолиСписания.НовыйАлгоритмСписания
		|						ТОГДА Расходы.ПостояннаяРазница - (Расходы.СуммаРегл - Расходы.ВременнаяРазница 
		|						- ВЫРАЗИТЬ((Расходы.СуммаРегл - Расходы.ПостояннаяРазница - Расходы.ВременнаяРазница) * ДолиСписания.ДоляСписания КАК ЧИСЛО(31, 2)))
		|					ИНАЧЕ 0
		|				КОНЕЦ
		|			КОГДА ДолиСписания.ДоляСписания = 0
		|				ТОГДА Расходы.ПостояннаяРазница
		|			КОГДА ДолиСписания.Транспортные
		|					И НЕ Расходы.ПостояннаяРазницаРасход = 0
		|				ТОГДА Расходы.ПостояннаяРазницаРасход / ДолиСписания.ДоляСписания
		|					+ (Расходы.ПостояннаяРазница * ДолиСписания.ДоляСписания - Расходы.ПостояннаяРазницаРасход) / ДолиСписания.ДоляСписания - Расходы.ПостояннаяРазницаРасход
		|			ИНАЧЕ 0
		|		КОНЕЦ * -1 КАК ПостояннаяРазница,
		|		ЛОЖЬ,
		|		ВЫБОР
		|			КОГДА ВЫРАЗИТЬ(Расходы.СуммаРегл * ДолиСписания.ДоляСписания КАК ЧИСЛО(31, 2)) = 0
		|				И ВЫРАЗИТЬ(Расходы.ВременнаяРазница * ДолиСписания.ДоляСписания КАК ЧИСЛО(31, 2)) = 0
		|				И ВЫРАЗИТЬ(Расходы.ПостояннаяРазница * ДолиСписания.ДоляСписания КАК ЧИСЛО(31, 2)) = 0
		|			ТОГДА ИСТИНА
		|			ИНАЧЕ 
		|				ЕСТЬNULL(ВозможныеДвижения.ЕстьДвиженияНаФинансовыйРезультатРегл, ЛОЖЬ)
		|		КОНЕЦ,
		|		ЛОЖЬ
		|	ИЗ
		|		РасходыКРаспределению КАК Расходы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
		|		ПО Расходы.СтатьяРасходов = Статьи.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДолиСписанияКосвенныхРасходов КАК ДолиСписания
		|		ПО Расходы.СтатьяРасходов = ДолиСписания.СтатьяРасходов
		|			И Расходы.Организация = ДолиСписания.Организация
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДвиженияСтатейРасходов КАК ВозможныеДвижения
		|		ПО Расходы.Организация = ВозможныеДвижения.Организация
		|			И Расходы.Подразделение = ВозможныеДвижения.Подразделение
		|			И Расходы.НаправлениеДеятельности = ВозможныеДвижения.НаправлениеДеятельности
		|			И Расходы.СтатьяРасходов = ВозможныеДвижения.СтатьяРасходов
		|			И Расходы.АналитикаРасходов = ВозможныеДвижения.АналитикаРасходов
		|	ГДЕ
		|		НЕ &ИсключитьИзКонтроляРазницы
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		НеРаспределенные.Организация КАК Организация,
		|		НеРаспределенные.Подразделение КАК Подразделение,
		|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
		|		НеРаспределенные.АналитикаРасходов КАК АналитикаРасходов,
		|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
		|		ОВЗ.ВариантРаспределенияРасходовУпр,
		|		ОВЗ.ВариантРаспределенияРасходовРегл,
		|		ОВЗ.ВариантРаспределенияРасходовНУ,
		|		ВЫБОР
		|			КОГДА НЕ Статьи.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат)
		|					ИЛИ НЕ ОВЗ.ВариантРаспределенияРасходовУпр В (&ВариантыРаспределенияРасходов)
		|				ТОГДА 0
		|			ИНАЧЕ
		|				НеРаспределенные.СуммаОстаток
		|		КОНЕЦ КАК СуммаОстаток,
		|		ВЫБОР
		|			КОГДА НЕ Статьи.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат)
		|					ИЛИ НЕ ОВЗ.ВариантРаспределенияРасходовУпр В (&ВариантыРаспределенияРасходов)
		|				ТОГДА 0
		|			ИНАЧЕ
		|				НеРаспределенные.СуммаБезНДСОстаток
		|		КОНЕЦ КАК СуммаБезНДСОстаток,
		|		ВЫБОР
		|			КОГДА НЕ Статьи.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат)
		|					ИЛИ НЕ ОВЗ.ВариантРаспределенияРасходовУпр В (&ВариантыРаспределенияРасходов)
		|				ТОГДА 0
		|			ИНАЧЕ
		|				НеРаспределенные.СуммаУпрОстаток
		|		КОНЕЦ КАК СуммаУпрОстаток,
		|		ВЫБОР
		|			КОГДА &ИсключитьИзКонтроляРазницы
		|				ТОГДА 0
		|			КОГДА Статьи.ВариантРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат)
		|					И ОВЗ.ВариантРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|				ТОГДА НеРаспределенные.СуммаРеглОстаток - НеРаспределенные.ПостояннаяРазницаОстаток - НеРаспределенные.ВременнаяРазницаОстаток
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК СуммаНУОстаток,
		|		ВЫБОР
		|			КОГДА НЕ Статьи.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат)
		|					ИЛИ НЕ ОВЗ.ВариантРаспределенияРасходовРегл В (&ВариантыРаспределенияРасходов)
		|				ТОГДА 0
		|			ИНАЧЕ
		|				НеРаспределенные.СуммаРеглОстаток
		|		КОНЕЦ КАК СуммаРеглОстаток,
		|		ВЫБОР
		|			КОГДА &ИсключитьИзКонтроляРазницы
		|				ТОГДА 0
		|			КОГДА Статьи.ВариантРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат)
		|					И ОВЗ.ВариантРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|				ТОГДА НеРаспределенные.ВременнаяРазницаОстаток
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА &ИсключитьИзКонтроляРазницы
		|				ТОГДА 0
		|			КОГДА Статьи.ВариантРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат)
		|					И ОВЗ.ВариантРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|				ТОГДА НеРаспределенные.ПостояннаяРазницаОстаток
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ,
		|		ЛОЖЬ КАК ЕстьПогрешностьРасчетаУУ,
		|		ЛОЖЬ КАК ЕстьПогрешностьРасчетаБУ,
		|		ЛОЖЬ КАК ЕстьПогрешностьРасчетаНУ
		|	ИЗ
		|		РегистрНакопления.ПрочиеРасходы.Остатки(
		|				&ГраницаКонецПериода,
		|				(Организация, Подразделение, АналитикаРасходов) В 
		|					(ВЫБРАТЬ 
		|						Т.Организация,
		|						Т.Подразделение,
		|						Т.АналитикаРасходов 
		|					ИЗ РасходыКРаспределениюСводно КАК Т
		|					ГДЕ Т.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка))) КАК НеРаспределенные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
		|		ПО НеРаспределенные.СтатьяРасходов = Статьи.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыВозникновенияЗатрат КАК ОВЗ
		|		ПО НеРаспределенные.АналитикаРасходов = ОВЗ.Ссылка
		//-- НЕ УТ
		|) КАК НеРаспределенные
		|
		|СГРУППИРОВАТЬ ПО
		|	НеРаспределенные.Организация,
		|	НеРаспределенные.Подразделение,
		|	НеРаспределенные.НаправлениеДеятельности,
		|	НеРаспределенные.СтатьяРасходов,
		|	НеРаспределенные.АналитикаРасходов,
		|	НеРаспределенные.ВариантРаспределенияРасходовУпр,
		|	НеРаспределенные.ВариантРаспределенияРасходовРегл,
		|	НеРаспределенные.ВариантРаспределенияРасходовНУ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НеРаспределенные.СтатьяРасходов,
		|	НеРаспределенные.АналитикаРасходов,
		|	НеРаспределенные.Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Т.Организация КАК Организация,
		|	МИНИМУМ(Т.Месяц) КАК Месяц
		|ПОМЕСТИТЬ Задания
		|ИЗ
		|	(ВЫБРАТЬ
		|		Задания.Организация КАК Организация,
		|		НАЧАЛОПЕРИОДА(Задания.Месяц, МЕСЯЦ) КАК Месяц
		|	ИЗ
		|		РегистрСведений.ЗаданияКРасчетуСебестоимости КАК Задания
		|	ГДЕ
		|		(&ПоВсемОрганизациям
		|				ИЛИ Задания.Организация В (&МассивОрганизаций))
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Задания.Организация,
		|		НАЧАЛОПЕРИОДА(Задания.Месяц, МЕСЯЦ)
		|	ИЗ
		|		РегистрСведений.ЗаданияКЗакрытиюМесяца КАК Задания
		|	ГДЕ
		|		(&ПоВсемОрганизациям
		|				ИЛИ Задания.Организация В (&МассивОрганизаций)
		|		И Задания.Операция = ЗНАЧЕНИЕ(Перечисление.ОперацииЗакрытияМесяца.РаспределениеРасходовПоНаправлениямДеятельности))) КАК Т
		|
		|СГРУППИРОВАТЬ ПО
		|	Т.Организация
		|;
		|
		|ВЫБРАТЬ
		|	Т.Организация КАК Организация,
		|	Т.Подразделение КАК Подразделение,
		|	Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Т.СтатьяРасходов КАК СтатьяРасходов,
		|	Т.АналитикаРасходов КАК АналитикаРасходов,
		|	СтатьяРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров) КАК НеРаспределяетсяВУпр,
		|	СтатьяРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров) КАК НеРаспределяетсяВРегл,
		|	СтатьяРасходов.ВариантРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров) КАК НеРаспределяетсяВНУ
		|ПОМЕСТИТЬ НераспределяемыеРасходыНаСебестоимостьТоваров
		|ИЗ
		|	РасходыКРаспределениюСводно КАК Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
		|		ПО Т.СтатьяРасходов = СтатьиРасходов.Ссылка
		|ГДЕ
		|	(СтатьяРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|	ИЛИ СтатьяРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|	ИЛИ СтатьяРасходов.ВариантРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров))
		|	И ИСТИНА В (ВЫБРАТЬ ПЕРВЫЕ 1 ИСТИНА
		|		ИЗ РегистрСведений.РеестрДокументов КАК ДД
		|		ГДЕ ДД.Ссылка = Т.АналитикаРасходов
		|		И (ДД.ДатаДокументаИБ > &Конецпериода 
		|			ИЛИ НЕ ДД.Проведен)
		|	)
		|ИНДЕКСИРОВАТЬ ПО
		|	СтатьяРасходов,
		|	АналитикаРасходов,
		|	Подразделение,
		|	Организация,
		|	НаправлениеДеятельности
		|;
		|
		|ВЫБРАТЬ
		|	Источник.Организация КАК Организация,
		|	Источник.Подразделение КАК Подразделение,
		|	Источник.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Источник.СтатьяРасходов КАК СтатьяРасходов,
		|	Источник.АналитикаРасходов КАК АналитикаРасходов,
		|	ЕСТЬNULL(ДокументыУпр.Документ, ЗНАЧЕНИЕ(Документ.РаспределениеПрочихЗатрат.ПустаяСсылка)) КАК ДокументУпр,
		|	ЕСТЬNULL(ДокументыРегл.Документ, ЗНАЧЕНИЕ(Документ.РаспределениеПрочихЗатрат.ПустаяСсылка)) КАК ДокументРегл,
		//++ НЕ УТ
		//++ Локализация
		|	ЕСТЬNULL(ДокументыНал.Документ, ЗНАЧЕНИЕ(Документ.РаспределениеПрочихЗатрат.ПустаяСсылка)) КАК ДокументНал,
		//-- Локализация
		//-- НЕ УТ
		|	Источник.ВариантРаспределенияРасходовУпр КАК ВариантРаспределенияРасходовУпр,
		|	Источник.ВариантРаспределенияРасходовРегл КАК ВариантРаспределенияРасходовРегл,
		|	Источник.ВариантРаспределенияРасходовНУ КАК ВариантРаспределенияРасходовНУ,
		|	Источник.Сумма КАК Сумма,
		|	Источник.СуммаБезНДС КАК СуммаБезНДС,
		|	Источник.СуммаРегл КАК СуммаРегл,
		|	Источник.СуммаУпр КАК СуммаУпр,
		|	Источник.ПостояннаяРазница КАК ПостояннаяРазница,
		|	Источник.ВременнаяРазница КАК ВременнаяРазница,
		|	ЕСТЬNULL(Остатки.РаспределеноВУУ, ИСТИНА) КАК РаспределеноВУУ,
		|	ЕСТЬNULL(Остатки.РаспределеноВБУ, ИСТИНА) КАК РаспределеноВБУ,
		|	ЕСТЬNULL(Остатки.РаспределеноВНУ, ИСТИНА) КАК РаспределеноВНУ,
		|	ВЫБОР
		// отличные варианты распределения от перечисленных, рабочее место не обслуживает.
		|		КОГДА НЕ Источник.ВариантРаспределенияРасходовУпр В (&ВариантыРаспределенияРасходов)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		// аналитика распределения некорректна либо относится к будущим периодам закрытия
		|		КОГДА ЕСТЬNULL(НераспределяемыеРасходы.НеРаспределяетсяВУпр, ЛОЖЬ)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		//++ НЕ УТ
		// статьи с вариантом распределения ""На ОВЗ"" обрабатываются сводно, документ не требуется
		|		КОГДА НЕ Источник.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
		|				И Источник.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		//-- НЕ УТ
		// если месяц закрыт, расходов и движений нет
		|		КОГДА (Задания.Месяц > &КонецПериода ИЛИ Задания.Месяц ЕСТЬ NULL)
		|			И ЕСТЬNULL(Остатки.РаспределеноВУУ, ИСТИНА)
		|			И НЕ (
		|				ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатУпр, ЛОЖЬ) 
		|				ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаПроизводствоУпр, ЛОЖЬ)
		|				ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаДругиеСтатьиУпр, ЛОЖЬ)
		//++ НЕ УТ
		|				ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияМеждуОВЗУпр, ЛОЖЬ)
		//-- НЕ УТ
		|			)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		// если месяц закрыт и расходов нет, то состояние Распределено
		|		КОГДА (Задания.Месяц > &КонецПериода ИЛИ Задания.Месяц ЕСТЬ NULL) 
		|			И ЕСТЬNULL(Остатки.РаспределеноВУУ, ИСТИНА)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.Распределено)
		// Если не настроено распределение долей, и есть остаток к распределению, и правило с ручным заполнением, то требуется настройка вручную.
		//++ НЕ УТ
		|		КОГДА ДокументыУпр.Документ ЕСТЬ NULL
		|				И НЕ (
		|					ПравилаУпр.Правило ЕСТЬ NULL 
		|					И ПравилаУпрОВЗ.Правило ЕСТЬ NULL
		|				)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ТребуетсяРучнойВводДокумента)
		//-- НЕ УТ
		// Если не настроено распределение долей, и есть остаток к распределению, то требуется настройка.
		|		КОГДА ДокументыУпр.Документ ЕСТЬ NULL
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ТребуетсяСформироватьДокумент)
		// Если распределение долей есть, документ распределения сделал движения на производство, но есть остаток к распределеню, то есть ошибка.
		|		КОГДА Задания.Месяц > &КонецПериода И НЕ ЕСТЬNULL(Остатки.РаспределеноВУУ, ИСТИНА)
		|			И Источник.ВариантРаспределенияРасходовУпр 
		|				В (ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты),
		|					ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства))
		|			И (ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаПроизводствоУпр, ЛОЖЬ)
		|				ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаДругиеСтатьиУпр, ЛОЖЬ))
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ОшибкаРаспределения)
		//++ НЕ УТ
		// Если распределение между ОВЗ есть, документ распределения сделал движения, но есть остаток к распределеню, то есть ошибка.
		|		КОГДА Задания.Месяц > &КонецПериода И НЕ ЕСТЬNULL(Остатки.РаспределеноВУУ, ИСТИНА)
		|			И Источник.ВариантРаспределенияРасходовУпр
		|				= ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат)
		|			И ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияМеждуОВЗУпр, ЛОЖЬ)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ОшибкаРаспределения)
		//-- НЕ УТ
		// Если распределение долей есть, документ распределения сделал движения на фин. рез., но есть остаток к распределеню, то есть ошибка.
		|		КОГДА Задания.Месяц > &КонецПериода И НЕ ЕСТЬNULL(Остатки.РаспределеноВУУ, ИСТИНА)
		|			И Источник.ВариантРаспределенияРасходовУпр
		|				= ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			И ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатУпр, ЛОЖЬ)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ОшибкаРаспределения)
		// Если распределение долей есть, документ распределения сделал движения и статья полностью распределена то расходы распределены.
		|		КОГДА ЕСТЬNULL(Остатки.РаспределеноВУУ, ИСТИНА)
		|			И (ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатУпр, ЛОЖЬ) 
		|				ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаПроизводствоУпр, ЛОЖЬ) 
		|				ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаДругиеСтатьиУпр, ЛОЖЬ)
		//++ НЕ УТ
		|				ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияМеждуОВЗУпр, ЛОЖЬ)
		//-- НЕ УТ
		|			)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.Распределено)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ГотовоКРаспределениюПоБазе)
		|	КОНЕЦ КАК СостояниеУУ,
		|	ВЫБОР
		//++ НЕ УТ
		|// Если Источник.СтатьяРасходов заполнена при варианте ""На ОВЗ"", значит настройка будет в разрезе ОВЗ, а не статьи
		|		КОГДА Источник.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат)
		|				И НЕ Источник.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		//-- НЕ УТ
		|		КОГДА Не Источник.ВариантРаспределенияРасходовРегл В (&ВариантыРаспределенияРасходов)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		|		КОГДА ЕСТЬNULL(НераспределяемыеРасходы.НеРаспределяетсяВРегл, ЛОЖЬ)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		|		КОГДА (Задания.Месяц > &КонецПериода ИЛИ Задания.Месяц ЕСТЬ NULL) 
		|			И ЕСТЬNULL(Остатки.РаспределеноВБУ, ИСТИНА)
		|			И НЕ (ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатРегл, ЛОЖЬ) ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаПроизводствоРегл, ЛОЖЬ)
		|					ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаДругиеСтатьиРегл, ЛОЖЬ)
		//++ НЕ УТ
		|					ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияМеждуОВЗРегл, ЛОЖЬ)
		//-- НЕ УТ
		|				)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		|		КОГДА (Задания.Месяц > &КонецПериода ИЛИ Задания.Месяц ЕСТЬ NULL) 
		|			И ЕСТЬNULL(Остатки.РаспределеноВБУ, ИСТИНА)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.Распределено)
		//++ НЕ УТ
		|		КОГДА ДокументыРегл.Документ ЕСТЬ NULL
		|				И НЕ (
		|					ПравилаРегл.Правило ЕСТЬ NULL
		|					И ПравилаРеглОВЗ.Правило ЕСТЬ NULL
		|				)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ТребуетсяРучнойВводДокумента)
		//-- НЕ УТ
		|		КОГДА ДокументыРегл.Документ ЕСТЬ NULL
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ТребуетсяСформироватьДокумент)
		|		КОГДА Задания.Месяц > &КонецПериода И НЕ ЕСТЬNULL(Остатки.РаспределеноВБУ, ИСТИНА)
		|			И Источник.ВариантРаспределенияРасходовРегл 
		|				В (ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты),
		|					ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства))
		|			И (ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаПроизводствоРегл, ЛОЖЬ)
		|				ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаДругиеСтатьиРегл, ЛОЖЬ))
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ОшибкаРаспределения)
		//++ НЕ УТ
		|		КОГДА Задания.Месяц > &КонецПериода И НЕ ЕСТЬNULL(Остатки.РаспределеноВБУ, ИСТИНА)
		|			И Источник.ВариантРаспределенияРасходовРегл 
		|				= ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат)
		|			И ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияМеждуОВЗРегл, ЛОЖЬ)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ОшибкаРаспределения)
		//-- НЕ УТ
		|		КОГДА Задания.Месяц > &КонецПериода И НЕ ЕСТЬNULL(Остатки.РаспределеноВБУ, ИСТИНА)
		|			И Источник.ВариантРаспределенияРасходовРегл 
		|				= ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			И ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатРегл, ЛОЖЬ)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ОшибкаРаспределения)
		|		КОГДА ЕСТЬNULL(Остатки.РаспределеноВБУ, ИСТИНА)
		|			И (ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатРегл, ЛОЖЬ) 
		|				ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаПроизводствоРегл, ЛОЖЬ) 
		//++ НЕ УТ
		|				ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияМеждуОВЗРегл, ЛОЖЬ)
		//-- НЕ УТ
		|			)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.Распределено)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ГотовоКРаспределениюПоБазе)
		|	КОНЕЦ КАК СостояниеБУ
		//++ НЕ УТ
		//++ Локализация	
		|	,
		|	ВЫБОР
		|// Если Источник.СтатьяРасходов заполнена при варианте ""На ОВЗ"", значит настройка будет в разрезе ОВЗ, а не статьи
		|		КОГДА Источник.ВариантРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат)
		|				И НЕ Источник.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		|		КОГДА НЕ Источник.ВариантРаспределенияРасходовНУ В (&ВариантыРаспределенияРасходов)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		|		КОГДА ЕСТЬNULL(НераспределяемыеРасходы.НеРаспределяетсяВНУ, ЛОЖЬ)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		|		КОГДА (Задания.Месяц > &КонецПериода ИЛИ Задания.Месяц ЕСТЬ NULL) 
		|			И ЕСТЬNULL(Остатки.РаспределеноВНУ, ИСТИНА)
		|			И НЕ (ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатНал, ЛОЖЬ) ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаПроизводствоНал, ЛОЖЬ)
		|					ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьСписаниеНУ, ЛОЖЬ)
		|					ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаДругиеСтатьиНал, ЛОЖЬ)
		|					ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияМеждуОВЗНал, ЛОЖЬ)
		|				)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		|		КОГДА (Задания.Месяц > &КонецПериода ИЛИ Задания.Месяц ЕСТЬ NULL) 
		|			И (ЕСТЬNULL(Остатки.РаспределеноВНУ, ИСТИНА)
		|				ИЛИ Источник.СтатьяРасходов.ВидРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ТранспортныеРасходы)
		|						И (ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатНал, ЛОЖЬ)
		|							ИЛИ ДолиСписания.ДоляСписания = 0)
		|				)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.Распределено)
		|		КОГДА ДокументыНал.Документ ЕСТЬ NULL
		|				И НЕ (
		|					ПравилаНал.Правило ЕСТЬ NULL
		|					И ПравилаНалОВЗ.Правило ЕСТЬ NULL
		|				)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ТребуетсяРучнойВводДокумента)
		|		КОГДА ДокументыНал.Документ ЕСТЬ NULL
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ТребуетсяСформироватьДокумент)
		|		КОГДА Задания.Месяц > &КонецПериода И НЕ ЕСТЬNULL(Остатки.РаспределеноВНУ, ИСТИНА)
		|			И (ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатНал, ЛОЖЬ)
		|				ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьСписаниеНУ, ЛОЖЬ))
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ОшибкаРаспределения)
		|		КОГДА Задания.Месяц > &КонецПериода И НЕ ЕСТЬNULL(Остатки.РаспределеноВНУ, ИСТИНА)
		|			И Источник.ВариантРаспределенияРасходовНУ 
		|				= ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
		|			И ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаПроизводствоНал, ЛОЖЬ)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ОшибкаРаспределения)
		|		КОГДА Задания.Месяц > &КонецПериода И НЕ ЕСТЬNULL(Остатки.РаспределеноВНУ, ИСТИНА)			
		|			И Источник.ВариантРаспределенияРасходовНУ 
		|				= ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат)
		|			И ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияМеждуОВЗНал, ЛОЖЬ)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ОшибкаРаспределения)
		|		КОГДА Задания.Месяц > &КонецПериода И НЕ ЕСТЬNULL(Остатки.РаспределеноВНУ, ИСТИНА)
		|			И Источник.ВариантРаспределенияРасходовНУ 
		|				= ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			И ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатНал, ЛОЖЬ)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ОшибкаРаспределения)
		|		КОГДА ЕСТЬNULL(Остатки.РаспределеноВНУ, ИСТИНА)
		|			И (ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаФинансовыйРезультатНал, ЛОЖЬ) 
		|				ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияНаПроизводствоНал, ЛОЖЬ) 
		|				ИЛИ ЕСТЬNULL(ДвиженияСтатейРасходов.ЕстьДвиженияМеждуОВЗНал, ЛОЖЬ)
		|			)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.Распределено)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ГотовоКРаспределениюПоБазе)
		|	КОНЕЦ КАК СостояниеНУ
		//-- Локализация
		//-- НЕ УТ
		|ПОМЕСТИТЬ ВТДанныеДляРаспределения
		|ИЗ
		|	РасходыКРаспределениюСводно КАК Источник
		|		ЛЕВОЕ СОЕДИНЕНИЕ Задания КАК Задания
		|		ПО Источник.Организация = Задания.Организация
		|
		//++ НЕ УТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтОВЗ КАК ВтОВЗ
		|		ПО Источник.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
		|			И Источник.АналитикаРасходов = ВтОВЗ.ОВЗ
		|
		//-- НЕ УТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РасходыПослеРаспределения КАК Остатки
		|		ПО	Источник.СтатьяРасходов 			= Остатки.СтатьяРасходов
		|			И Источник.АналитикаРасходов 		= Остатки.АналитикаРасходов
		|			И Источник.Подразделение			= Остатки.Подразделение
		|			И Источник.Организация				= Остатки.Организация
		|			И Источник.НаправлениеДеятельности	= Остатки.НаправлениеДеятельности
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДокументыРаспределения КАК ДокументыУпр
		|		ПО	Источник.СтатьяРасходов				= ДокументыУпр.СтатьяРасходов
		|			И Источник.АналитикаРасходов 		= ДокументыУпр.АналитикаРасходов
		|			И Источник.Подразделение			= ДокументыУпр.Подразделение
		|			И Источник.НаправлениеДеятельности	= ДокументыУпр.НаправлениеДеятельности
		|			И Источник.Организация				= ДокументыУпр.Организация
		|			И ДокументыУпр.УправленческийУчет
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДокументыРаспределения КАК ДокументыРегл
		|		ПО	Источник.СтатьяРасходов				= ДокументыРегл.СтатьяРасходов
		|			И Источник.АналитикаРасходов 		= ДокументыРегл.АналитикаРасходов
		|			И Источник.Подразделение			= ДокументыРегл.Подразделение
		|			И Источник.НаправлениеДеятельности	= ДокументыРегл.НаправлениеДеятельности
		|			И Источник.Организация				= ДокументыРегл.Организация
		|			И ДокументыРегл.РегламентированныйУчет
		|
		//++ НЕ УТ
		//++ Локализация
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДокументыРаспределения КАК ДокументыНал
		|		ПО	Источник.СтатьяРасходов				= ДокументыНал.СтатьяРасходов
		|			И Источник.АналитикаРасходов 		= ДокументыНал.АналитикаРасходов
		|			И Источник.Подразделение			= ДокументыНал.Подразделение
		|			И Источник.НаправлениеДеятельности	= ДокументыНал.НаправлениеДеятельности
		|			И Источник.Организация				= ДокументыНал.Организация
		|			И ДокументыНал.НалоговыйУчет
		//-- Локализация
		//-- НЕ УТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДвиженияСтатейРасходов КАК ДвиженияСтатейРасходов
		|		ПО	Источник.СтатьяРасходов				= ДвиженияСтатейРасходов.СтатьяРасходов
		|			И Источник.АналитикаРасходов 		= ДвиженияСтатейРасходов.АналитикаРасходов
		|			И Источник.Подразделение			= ДвиженияСтатейРасходов.Подразделение
		|			И Источник.НаправлениеДеятельности	= ДвиженияСтатейРасходов.НаправлениеДеятельности
		|			И Источник.Организация				= ДвиженияСтатейРасходов.Организация
		|		ЛЕВОЕ СОЕДИНЕНИЕ НераспределяемыеРасходыНаСебестоимостьТоваров КАК НераспределяемыеРасходы
		|		ПО	Источник.СтатьяРасходов				= НераспределяемыеРасходы.СтатьяРасходов
		|			И Источник.АналитикаРасходов 		= НераспределяемыеРасходы.АналитикаРасходов
		|			И Источник.Подразделение			= НераспределяемыеРасходы.Подразделение
		|			И Источник.НаправлениеДеятельности	= НераспределяемыеРасходы.НаправлениеДеятельности
		|			И Источник.Организация				= НераспределяемыеРасходы.Организация
		//++ НЕ УТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДолиСписанияКосвенныхРасходов КАК ДолиСписания
		|		ПО Источник.СтатьяРасходов = ДолиСписания.СтатьяРасходов
		|			И Источник.Организация = ДолиСписания.Организация
		|			И ДолиСписания.Транспортные
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПравилаРаспределенияСРучнымВводом КАК ПравилаУпр
		|		ПО	Источник.СтатьяРасходов.ПравилоРаспределенияРасходовУпр = ПравилаУпр.Правило
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПравилаРаспределенияСРучнымВводом КАК ПравилаРегл
		|		ПО	Источник.СтатьяРасходов.ПравилоРаспределенияРасходовРегл = ПравилаРегл.Правило
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПравилаРаспределенияСРучнымВводом КАК ПравилаНал
		|		ПО ВЫБОР 
		|			КОГДА Источник.СтатьяРасходов.ПравилоРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Справочник.ПравилаРаспределенияРасходов.ПустаяСсылка)
		|				ТОГДА Источник.СтатьяРасходов.ПравилоРаспределенияРасходовРегл = ПравилаНал.Правило
		|			ИНАЧЕ Источник.СтатьяРасходов.ПравилоРаспределенияРасходовНУ = ПравилаНал.Правило
		|		КОНЕЦ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПравилаРаспределенияСРучнымВводом КАК ПравилаУпрОВЗ
		|		ПО	ВтОВЗ.ПравилоРаспределенияРасходовУпр = ПравилаУпр.Правило
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПравилаРаспределенияСРучнымВводом КАК ПравилаРеглОВЗ
		|		ПО	ВтОВЗ.ПравилоРаспределенияРасходовРегл = ПравилаРегл.Правило
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПравилаРаспределенияСРучнымВводом КАК ПравилаНалОВЗ
		|		ПО ВЫБОР 
		|			КОГДА ВтОВЗ.ПравилоРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Справочник.ПравилаРаспределенияРасходов.ПустаяСсылка)
		|				ТОГДА ВтОВЗ.ПравилоРаспределенияРасходовРегл = ПравилаНал.Правило
		|			ИНАЧЕ ВтОВЗ.ПравилоРаспределенияРасходовНУ = ПравилаНал.Правило
		|		КОНЕЦ
		|ОБЪЕДИНИТЬ
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОбъектыВозникновенияЗатрат.Организация,
		|	ОбъектыВозникновенияЗатрат.Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка),
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка),
		|	ОбъектыВозникновенияЗатрат.Ссылка,
		|	ДокументыУпр.Ссылка,
		|	ДокументыРегл.Ссылка,
		//++ Локализация
		|	ДокументыНал.Ссылка,
		//-- Локализация
		|	ОбъектыВозникновенияЗатрат.ВариантРаспределенияРасходовУпр,
		|	ОбъектыВозникновенияЗатрат.ВариантРаспределенияРасходовРегл,
		|	ОбъектыВозникновенияЗатрат.ВариантРаспределенияРасходовНУ,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	Задания.Месяц > &КонецПериода,
		|	Задания.Месяц > &КонецПериода,
		|	Задания.Месяц > &КонецПериода,
		|	ВЫБОР
		|		КОГДА НЕ ОбъектыВозникновенияЗатрат.ВариантРаспределенияРасходовУпр В (&ВариантыРаспределенияРасходов)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		|		КОГДА Задания.Месяц > &КонецПериода
		|			И ОбъектыВозникновенияЗатрат.ВариантРаспределенияРасходовУпр 
		|				В (ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты),
		|					ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства),
		|					ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности),
		|					ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат))
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеПоступилиРасходы)
		|		КОГДА ДокументыУпр.Ссылка ЕСТЬ NULL
		|				И ОбъектыВозникновенияЗатрат.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
		|				И НЕ ПравилаУпр.Правило ЕСТЬ NULL
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ТребуетсяРучнойВводДокумента)
		|		КОГДА ДокументыУпр.Ссылка ЕСТЬ NULL
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ТребуетсяСформироватьДокумент)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ГотовоКРаспределениюПоБазе)
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА НЕ ОбъектыВозникновенияЗатрат.ВариантРаспределенияРасходовРегл В (&ВариантыРаспределенияРасходов)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		|		КОГДА Задания.Месяц > &КонецПериода
		|			И ОбъектыВозникновенияЗатрат.ВариантРаспределенияРасходовРегл 
		|				В (ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты),
		|					ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства),
		|					ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности),
		|					ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат))
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеПоступилиРасходы)
		|		КОГДА ДокументыРегл.Ссылка ЕСТЬ NULL
		|				И ОбъектыВозникновенияЗатрат.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
		|				И НЕ ПравилаРегл.Правило ЕСТЬ NULL
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ТребуетсяРучнойВводДокумента)
		|		КОГДА ДокументыРегл.Ссылка ЕСТЬ NULL
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ТребуетсяСформироватьДокумент)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ГотовоКРаспределениюПоБазе)
		|	КОНЕЦ
		//++ Локализация
		|	,
		|	ВЫБОР
		|		КОГДА НЕ ОбъектыВозникновенияЗатрат.ВариантРаспределенияРасходовНУ В (&ВариантыРаспределенияРасходов)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		|		КОГДА Задания.Месяц > &КонецПериода
		|			И ОбъектыВозникновенияЗатрат.ВариантРаспределенияРасходовНУ 
		|				В (ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты),
		|					ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства),
		|					ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности),
		|					ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат))
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеПоступилиРасходы)
		|		КОГДА ДокументыНал.Ссылка ЕСТЬ NULL
		|				И ОбъектыВозникновенияЗатрат.ВариантРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
		|				И НЕ ПравилаНал.Правило ЕСТЬ NULL
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ТребуетсяРучнойВводДокумента)
		|		КОГДА ДокументыНал.Ссылка ЕСТЬ NULL
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ТребуетсяСформироватьДокумент)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ГотовоКРаспределениюПоБазе)
		|	КОНЕЦ
		//-- Локализация
		|ИЗ
		|	РегистрСведений.ЗначенияПоказателейДляРаспределенияРасходовНаОВЗ.СрезПоследних(&КонецПериода) КАК ЗначенияНаОВЗ
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыВозникновенияЗатрат КАК ОбъектыВозникновенияЗатрат
		|		ПО	ОбъектыВозникновенияЗатрат.Ссылка = ЗначенияНаОВЗ.ОВЗ
		|	ЛЕВОЕ СОЕДИНЕНИЕ Задания КАК Задания
		|		ПО ОбъектыВозникновенияЗатрат.Организация = Задания.Организация
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК ДокументыУпр
		|		ПО	ДокументыУпр.АналитикаРасходов = ОбъектыВозникновенияЗатрат.Ссылка
		|			И ДокументыУпр.УправленческийУчет
		|			И ДокументыУпр.Проведен
		|			И НАЧАЛОПЕРИОДА(ДокументыУпр.Дата, МЕСЯЦ) = &НачалоПериода
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК ДокументыРегл
		|		ПО	ДокументыРегл.АналитикаРасходов = ОбъектыВозникновенияЗатрат.Ссылка 
		|			И ДокументыРегл.РегламентированныйУчет
		|			И ДокументыРегл.Проведен
		|			И НАЧАЛОПЕРИОДА(ДокументыРегл.Дата, МЕСЯЦ) = &НачалоПериода
		//++ Локализация
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК ДокументыНал
		|		ПО	ДокументыНал.АналитикаРасходов = ОбъектыВозникновенияЗатрат.Ссылка 
		|			И ДокументыНал.НалоговыйУчет
		|			И ДокументыНал.Проведен
		|			И НАЧАЛОПЕРИОДА(ДокументыНал.Дата, МЕСЯЦ) = &НачалоПериода
		//-- Локализация
		|	ЛЕВОЕ СОЕДИНЕНИЕ РасходыКРаспределению КАК РасходыДругогоСегмента
		|		ПО	ОбъектыВозникновенияЗатрат.Ссылка 		= РасходыДругогоСегмента.АналитикаРасходов
		|			И ОбъектыВозникновенияЗатрат.Подразделение			= РасходыДругогоСегмента.Подразделение
		|			И ОбъектыВозникновенияЗатрат.Организация				= РасходыДругогоСегмента.Организация
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПравилаРаспределенияСРучнымВводом КАК ПравилаУпр
		|		ПО	ОбъектыВозникновенияЗатрат.ПравилоРаспределенияРасходовУпр = ПравилаУпр.Правило
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПравилаРаспределенияСРучнымВводом КАК ПравилаРегл
		|		ПО	ОбъектыВозникновенияЗатрат.ПравилоРаспределенияРасходовРегл = ПравилаРегл.Правило
		//++ Локализация
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПравилаРаспределенияСРучнымВводом КАК ПравилаНал
		|		ПО ВЫБОР 
		|			КОГДА ОбъектыВозникновенияЗатрат.ПравилоРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Справочник.ПравилаРаспределенияРасходов.ПустаяСсылка)
		|				ТОГДА ОбъектыВозникновенияЗатрат.ПравилоРаспределенияРасходовРегл = ПравилаНал.Правило
		|			ИНАЧЕ ОбъектыВозникновенияЗатрат.ПравилоРаспределенияРасходовНУ = ПравилаНал.Правило
		|		КОНЕЦ
		//-- Локализация
		|ГДЕ
		|	(ЗначенияНаОВЗ.Показатель.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВводитсяЕжемесячно) 
		|	И ЗначенияНаОВЗ.Период = &НачалоПериода
		|	ИЛИ ЗначенияНаОВЗ.Показатель.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВводитсяПриИзменении))
		|	И НЕ ЗначенияНаОВЗ.ЗначениеПоказателя = 0
		|	И (&ПоВсемОрганизациям
		|		ИЛИ ОбъектыВозникновенияЗатрат.Организация В (&МассивОрганизаций))
		|	И (&ПоВсемПодразделениям
		|		ИЛИ ОбъектыВозникновенияЗатрат.Подразделение В (&СписокПодразделений))
		|	И РасходыДругогоСегмента.Организация ЕСТЬ NULL 
		|	И (ОбъектыВозникновенияЗатрат.ВариантРаспределенияРасходовУпр В (&ВариантыРаспределенияРасходов)
		|	ИЛИ ОбъектыВозникновенияЗатрат.ВариантРаспределенияРасходовРегл В (&ВариантыРаспределенияРасходов)
		//++ Локализация
		|	ИЛИ ОбъектыВозникновенияЗатрат.ВариантРаспределенияРасходовНУ В (&ВариантыРаспределенияРасходов)
		//-- Локализация
		|	)
		//-- НЕ УТ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 100
		|	ВТДанныеДляЗагрузки.Организация КАК Организация,
		|	ВТДанныеДляЗагрузки.Подразделение КАК Подразделение,
		|	ВТДанныеДляЗагрузки.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ВТДанныеДляЗагрузки.СтатьяРасходов КАК СтатьяРасходов,
		|	ЕСТЬNULL(СтатьиРасходов.Родитель, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)) КАК ГруппаСтатей,
		|	ВТДанныеДляЗагрузки.АналитикаРасходов КАК АналитикаРасходов,
		|	ВТДанныеДляЗагрузки.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК ЭтоОВЗ,
		|	ВТДанныеДляЗагрузки.ДокументУпр КАК ДокументУпр,
		|	ВТДанныеДляЗагрузки.ДокументРегл КАК ДокументРегл,
		//++ НЕ УТ
		//++ Локализация
		|	ВТДанныеДляЗагрузки.ДокументНал КАК ДокументНал,
		//-- Локализация
		//-- НЕ УТ
		|	ВТДанныеДляЗагрузки.СостояниеУУ КАК СостояниеУпр,
		|	ВТДанныеДляЗагрузки.СостояниеБУ КАК СостояниеРегл
		//++ НЕ УТ
		//++ Локализация
		|	,
		|	ВТДанныеДляЗагрузки.СостояниеНУ КАК СостояниеНал
		//-- Локализация
		//-- НЕ УТ
		|ПОМЕСТИТЬ СостояниеРаспределенияРасходов
		|ИЗ
		|	ВТДанныеДляРаспределения КАК ВТДанныеДляЗагрузки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
		|		ПО ВТДанныеДляЗагрузки.СтатьяРасходов = СтатьиРасходов.Ссылка
		|ГДЕ
		|	ВЫБОР
		|		КОГДА &ФильтрПоСостоянию = ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ 
		|			(ВТДанныеДляЗагрузки.ВариантРаспределенияРасходовУпр В (&ВариантыРаспределенияРасходов)
		|				 И ВТДанныеДляЗагрузки.СостояниеУУ = &ФильтрПоСостоянию)
		|			ИЛИ
		|			(ВТДанныеДляЗагрузки.ВариантРаспределенияРасходовРегл В (&ВариантыРаспределенияРасходов)
		|				 И ВТДанныеДляЗагрузки.СостояниеБУ = &ФильтрПоСостоянию)
		//++ НЕ УТ
		//++ Локализация
		|			ИЛИ
		|			(ВТДанныеДляЗагрузки.ВариантРаспределенияРасходовНУ В (&ВариантыРаспределенияРасходов)
		|				 И ВТДанныеДляЗагрузки.СостояниеНУ = &ФильтрПоСостоянию)
		//-- Локализация
		//-- НЕ УТ
		|	КОНЕЦ
		|	И НЕ (ВТДанныеДляЗагрузки.СостояниеУУ = ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		|		И ВТДанныеДляЗагрузки.СостояниеБУ = ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		//++ НЕ УТ
		//++ Локализация
		|		И ВТДанныеДляЗагрузки.СостояниеНУ = ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.НеТребуется)
		//-- Локализация
		//-- НЕ УТ
		|		)
		|;
		|";
	
	ЗаменяемыйТекст = 
		"ПЕРВЫЕ 100"; //@Query-part
	Если ВыбиратьПервые > 0 Тогда
		ЗаменяющийТекст = 
			"ПЕРВЫЕ " //@Query-part
			+ Строка(ВыбиратьПервые); 
	Иначе
		ЗаменяющийТекст = ""; 
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЗаменяемыйТекст, ЗаменяющийТекст);
	
	Если Не ПоместитьСостояниеРасходовВВТ Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
			"ПОМЕСТИТЬ СостояниеРаспределенияРасходов", //@Query-part
			"");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
			"ВТДанныеДляЗагрузки.СтатьяРасходов КАК СтатьяРасходов", //@Query-part
			"СтатьиРасходов.Ссылка КАК СтатьяРасходов" //@Query-part
			);
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Устанавливает необходимые параметры запроса ТекстЗапросаДанныеДляРаспределения().
// Параметры:
//	Запрос - Запрос - запрос с текстом см. ТекстЗапросаДанныеДляРаспределения().
//	ПараметрыЗапроса - Структура - см. ОписаниеПараметровЗапросаПолученияДанныхДляРаспределения().
//
Процедура ИнициализироватьЗапросПолученияДанныхДляРаспределения(Запрос, ПараметрыЗапроса) Экспорт
	
	ПараметрыЗакрытия = РасчетСебестоимостиПовтИсп.ЗначенияТехнологическихПараметров();
	
	ПоддерживаемыеВариантыРаспределения = Новый Массив;
	Если ПараметрыЗапроса.ВариантРаспределения = Неопределено Тогда
		
		//++ НЕ УТ
		ПоддерживаемыеВариантыРаспределения.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты);
		ПоддерживаемыеВариантыРаспределения.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства);
		Если (ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОбъектамВозникновенияЗатрат")) Тогда
			ПоддерживаемыеВариантыРаспределения.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат);
		КонецЕсли;
		//-- НЕ УТ
		ПоддерживаемыеВариантыРаспределения.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности);
		Если РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии22(НачалоМесяца(ПараметрыЗапроса.Период)) Тогда
			ПоддерживаемыеВариантыРаспределения.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ПараметрыЗапроса.ВариантРаспределения) = Тип("Массив") Тогда
		
		Для каждого ЗначениеМассива Из ПараметрыЗапроса.ВариантРаспределения Цикл
			ПоддерживаемыеВариантыРаспределения.Добавить(ЗначениеМассива);
		КонецЦикла;
		
	Иначе
		ПоддерживаемыеВариантыРаспределения.Добавить(ПараметрыЗапроса.ВариантРаспределения);
	КонецЕсли;
	
	Если Не Запрос.Параметры.Свойство("НачалоПериода")
		Или Не ЗначениеЗаполнено(Запрос.Параметры.НачалоПериода) Тогда
		
		Если ЗначениеЗаполнено(ПараметрыЗапроса.НачалоПериода) Тогда
			Запрос.УстановитьПараметр("НачалоПериода", ПараметрыЗапроса.НачалоПериода);
		Иначе
			Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(ПараметрыЗапроса.Период));
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не Запрос.Параметры.Свойство("КонецПериода") 
		Или Не ЗначениеЗаполнено(Запрос.Параметры.КонецПериода) Тогда
		
		Если ЗначениеЗаполнено(ПараметрыЗапроса.КонецПериода) Тогда
			Запрос.УстановитьПараметр("КонецПериода", ПараметрыЗапроса.КонецПериода);
		Иначе
			Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(ПараметрыЗапроса.Период));
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не Запрос.Параметры.Свойство("МассивОрганизаций") 
		Или Запрос.Параметры.МассивОрганизаций.Количество() = 0	Тогда
		
		Если ПараметрыЗапроса.Организации.Количество() = 0 Тогда
			ЗапросОрганизаций = Новый Запрос;
			ЗапросОрганизаций.Текст = 
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	Организации.Ссылка КАК Ссылка
				|ИЗ
				|	Справочник.Организации КАК Организации
				|ГДЕ
				|	Организации.Ссылка <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)";
			
			Запрос.УстановитьПараметр("МассивОрганизаций", 
				ЗапросОрганизаций.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
				
		Иначе
			Запрос.УстановитьПараметр("МассивОрганизаций", ПараметрыЗапроса.Организации);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ГраницаКонецПериода",  
		Новый Граница(Запрос.Параметры.КонецПериода, ВидГраницы.Включая));		
	Запрос.УстановитьПараметр("СписокПодразделений",  ПараметрыЗапроса.Подразделения);
	Запрос.УстановитьПараметр("ПоВсемПодразделениям", Запрос.Параметры.СписокПодразделений.Количество() = 0);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям",   Запрос.Параметры.МассивОрганизаций.Количество() = 0);
	Запрос.УстановитьПараметр("ФильтрПоСостоянию",    ПараметрыЗапроса.Состояние);
	Запрос.УстановитьПараметр("ИсключитьИзКонтроляРазницы", ПараметрыЗапроса.ИсключитьИзКонтроляРазницы);
	Запрос.УстановитьПараметр("ЗначениеПогрешностиУпр",  ПараметрыЗакрытия.ЗначениеПогрешностиРасходыУпр);
	Запрос.УстановитьПараметр("ЗначениеПогрешностиРегл",  ПараметрыЗакрытия.ЗначениеПогрешностиРасходыРегл);
	Запрос.УстановитьПараметр("ВариантыРаспределенияРасходов", ПоддерживаемыеВариантыРаспределения);
	Запрос.УстановитьПараметр("УчетПоНаправлениямДеятельности", ПолучитьФункциональнуюОпцию("ФормироватьФинансовыйРезультат"));

	//++ НЕ УТ
	Запрос.УстановитьПараметр("ВидыРасходовНормируемые", Перечисления.ВидыРасходовНУ.НормируемыеРасходы());
	ПодготовитьСлужебныеТаблицы(Запрос.Параметры.КонецПериода, 
		Запрос.Параметры.МассивОрганизаций, Запрос, ПараметрыЗапроса.ИсключитьТранспортныеРасходы);
	//-- НЕ УТ	
	
КонецПроцедуры

// Возвращает структуру параметров запроса ТекстЗапросаДанныеДляРаспределения() необходимых для выполнения.
// Возвращаемое значение:
//	Структура - параметры выполнения запроса получения данных распределения.
//		* Период - Дата - дата на которую требуются остатки расходов.
//		* НачалоПериода - Дата - начало периода анализа расходов.
//		* КонецПериода - Дата - конец периода анализа расходов.
//		* Организации - Массив из СправочникСсылка.Организации - организации расходов.
//		* Подразделения - Массив из СправочникСсылка.СтруктураПредприятия - подразделения расходов.
//		* Состояние - ПеречисленияСсылка.СостоянияРаспределенияРасходов - отбор по состоянию распределения.
//		* ИсключитьИзКонтроляРазницы - Булево - исключать в остатках постоянные и переменные разницы.
//		* ИсключитьТранспортныеРасходы - Булево - исключать транспортные расходы.
//		* ВариантРаспределения - Массив из ПеречисленияСсылка.ВариантыРаспределенияРасходов - отбор по варианту распределения.
Функция ОписаниеПараметровЗапросаПолученияДанныхДляРаспределения() Экспорт
	
	ОписаниеПараметров = Новый Структура;
	ОписаниеПараметров.Вставить("Период", Дата(1, 1, 1));
	ОписаниеПараметров.Вставить("НачалоПериода", Дата(1, 1, 1));
	ОписаниеПараметров.Вставить("КонецПериода", Дата(1, 1, 1));
	ОписаниеПараметров.Вставить("Организации", Новый Массив); // Тип элементов СправочникСсылка.Организации.
	ОписаниеПараметров.Вставить("Подразделения", Новый Массив); // Тип элементов СправочникСсылка.СтруктураПредприятия.
	ОписаниеПараметров.Вставить("Состояние", Перечисления.СостоянияРаспределенияРасходов.ПустаяСсылка());
	ОписаниеПараметров.Вставить("ИсключитьИзКонтроляРазницы", Ложь);
	ОписаниеПараметров.Вставить("ИсключитьТранспортныеРасходы", Ложь);
	// Можно переопределить на Массив с элементами Перечисления.ВариантыРаспределенияРасходов.
	ОписаниеПараметров.Вставить("ВариантРаспределения", Неопределено);
	
	Возврат ОписаниеПараметров;
	
КонецФункции

// Возвращает настройки распределения для набора параметров.
// Параметры:
//	ПараметрыРасходов - Массив, ТаблицаЗначений - содержит структуру со свойствами или таблицу с колонками:
//		* Дата - Дата - дата документа
//		* ИДСтроки - Число - идентификатор строки в ТЧ
//		* Документ - ДокументСсылка.РаспределениеПрочихЗатрат - созданный документ
//		* СтатьяРасходов - ПланВидовХарактеристикСсылка.СтатьиРасходов - статья расходов
//		* АналитикаРасходов - Характеристика.СтатьиРасходов - аналитика расходов статьи
//		* Организация - СправочникСсылка.Организации - организация, в которой возник расход
//		* Подразделение - СправочникСсылка.СтруктураПредприятия - подразделение, в котором возник расход
//		* НаправлениеДеятельности - СправочникСсылка.НаправленияДеятельности - направление деятельности.
//
// Возвращаемое значение:
//	Массив - Массив параметров распределения.
//
Функция ПолучитьНастройкиРаспределенияСтатейРасходов(ПараметрыРасходов) Экспорт

	Если ТипЗнч(ПараметрыРасходов) = Тип("Массив") Тогда
		ТаблицаДанных = КонвертироватьДанные(ПараметрыРасходов);
	Иначе
		ТаблицаДанных = ПараметрыРасходов;
	КонецЕсли;
		
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДанныеПоРасходам.Дата КАК Дата,
		|	ДанныеПоРасходам.ИДСтроки КАК ИДСтроки,
		|	ДанныеПоРасходам.ДокументУпр КАК ДокументУпр,
		//++ НЕ УТ
		//++ Локализация
		|	ДанныеПоРасходам.ДокументНал КАК ДокументНал,
		//-- Локализация
		//-- НЕ УТ
		|	ДанныеПоРасходам.ДокументРегл КАК ДокументРегл,
		|	ДанныеПоРасходам.СтатьяРасходов КАК СтатьяРасходов,
		|	ДанныеПоРасходам.АналитикаРасходов КАК АналитикаРасходов,
		|	ДанныеПоРасходам.Организация КАК Организация,
		|	ДанныеПоРасходам.Подразделение КАК Подразделение,
		|	ДанныеПоРасходам.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ДанныеПоРасходам.СостояниеУпр КАК СостояниеУпр,
		//++ НЕ УТ
		//++ Локализация
		|	ДанныеПоРасходам.СостояниеНал КАК СостояниеНал,
		//-- Локализация
		//-- НЕ УТ
		|	ДанныеПоРасходам.СостояниеРегл КАК СостояниеРегл
		|ПОМЕСТИТЬ ДанныеПоРасходам
		|ИЗ
		|	&ДанныеПоРасходам КАК ДанныеПоРасходам
		|;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеПоРасходам.Дата КАК Дата,
		|	ДанныеПоРасходам.ИДСтроки КАК ИДСтроки,
		|	ДанныеПоРасходам.ДокументУпр КАК ДокументУпр,
		//++ НЕ УТ
		//++ Локализация
		|	ДанныеПоРасходам.ДокументНал КАК ДокументНал,
		//-- Локализация
		//-- НЕ УТ
		|	ДанныеПоРасходам.ДокументРегл КАК ДокументРегл,
		|	ДанныеПоРасходам.СтатьяРасходов КАК СтатьяРасходов,
		|	ДанныеПоРасходам.АналитикаРасходов КАК АналитикаРасходов,
		|	ДанныеПоРасходам.СостояниеУпр КАК СостояниеУпр,
		//++ НЕ УТ
		//++ Локализация
		|	ДанныеПоРасходам.СостояниеНал КАК СостояниеНал,
		//-- Локализация
		//-- НЕ УТ
		|	ДанныеПоРасходам.СостояниеРегл КАК СостояниеРегл,
		|	РеквизитыСтатьи.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	РеквизитыСтатьи.ВариантРаспределенияРасходовРегл КАК ВариантРаспределенияРегл,
		//++ НЕ УТ
		//++ Локализация
		|	РеквизитыСтатьи.ВариантРаспределенияРасходовНУ КАК ВариантРаспределенияНал,
		//-- Локализация
		//-- НЕ УТ
		|	РеквизитыСтатьи.ВариантРаспределенияРасходовУпр КАК ВариантРаспределенияУпр,
		|	ДанныеПоРасходам.Организация КАК Организация,
		|	ДанныеПоРасходам.Подразделение КАК Подразделение,
		|	ДанныеПоРасходам.НаправлениеДеятельности КАК НаправлениеДеятельности,
		//++ НЕ УТ
		|	ЕСТЬNULL(НастройкиПоОрганизацииИПодразделению.НастройкаРегл, 
		|		ЕСТЬNULL(НастройкиПоОрганизации.НастройкаРегл, 
		//-- НЕ УТ
		|					РеквизитыСтатьи.ПравилоРаспределенияРасходовРегл
		//++ НЕ УТ
		|	))
		//-- НЕ УТ
		|		КАК ПравилоРаспределенияРегл,
		//++ НЕ УТ
		//++ Локализация
		|	ЕСТЬNULL(НастройкиПоОрганизацииИПодразделению.НастройкаНУ, 
		|		ЕСТЬNULL(НастройкиПоОрганизации.НастройкаНУ, 
		|					РеквизитыСтатьи.ПравилоРаспределенияРасходовНУ
		|	))
		|		КАК ПравилоРаспределенияНал,
		//-- Локализация
		//-- НЕ УТ
		//++ НЕ УТ
		|	ЕСТЬNULL(НастройкиПоОрганизацииИПодразделению.НастройкаУпр, 
		|		ЕСТЬNULL(НастройкиПоОрганизации.НастройкаУпр,
		//-- НЕ УТ
		|					РеквизитыСтатьи.ПравилоРаспределенияРасходовУпр
		//++ НЕ УТ
		|	))
		//-- НЕ УТ
		|		КАК ПравилоРаспределенияУпр		
		|ИЗ
		|	ДанныеПоРасходам КАК ДанныеПоРасходам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК РеквизитыСтатьи
		|		ПО ДанныеПоРасходам.СтатьяРасходов = РеквизитыСтатьи.Ссылка
		//++ НЕ УТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиРаспределенияПостатейныхРасходов КАК НастройкиПоОрганизации
		|		ПО ДанныеПоРасходам.СтатьяРасходов = НастройкиПоОрганизации.СтатьяРасходов
		|			И ДанныеПоРасходам.Организация = НастройкиПоОрганизации.Организация
		|			И (НастройкиПоОрганизации.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиРаспределенияПостатейныхРасходов КАК НастройкиПоОрганизацииИПодразделению
		|		ПО ДанныеПоРасходам.СтатьяРасходов = НастройкиПоОрганизацииИПодразделению.СтатьяРасходов
		|			И ДанныеПоРасходам.Организация = НастройкиПоОрганизацииИПодразделению.Организация
		|			И ДанныеПоРасходам.Подразделение = НастройкиПоОрганизацииИПодразделению.Подразделение
		|ОБЪЕДИНИТЬ
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеПоРасходам.Дата КАК Дата,
		|	ДанныеПоРасходам.ИДСтроки КАК ИДСтроки,
		|	ДанныеПоРасходам.ДокументУпр КАК ДокументУпр,
		//++ Локализация
		|	ДанныеПоРасходам.ДокументНал КАК ДокументНал,
		//-- Локализация
		|	ДанныеПоРасходам.ДокументРегл КАК ДокументРегл,
		|	ДанныеПоРасходам.СтатьяРасходов КАК СтатьяРасходов,
		|	ДанныеПоРасходам.АналитикаРасходов КАК АналитикаРасходов,
		|	ДанныеПоРасходам.СостояниеУпр КАК СостояниеУпр,
		//++ Локализация
		|	ДанныеПоРасходам.СостояниеНал КАК СостояниеНал,
		//-- Локализация
		|	ДанныеПоРасходам.СостояниеРегл КАК СостояниеРегл,
		|	РеквизитыСтатьи.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	РеквизитыСтатьи.ВариантРаспределенияРасходовРегл КАК ВариантРаспределенияРегл,
		//++ Локализация
		|	РеквизитыСтатьи.ВариантРаспределенияРасходовНУ КАК ВариантРаспределенияНал,
		//-- Локализация
		|	РеквизитыСтатьи.ВариантРаспределенияРасходовУпр КАК ВариантРаспределенияУпр,
		|	ДанныеПоРасходам.Организация КАК Организация,
		|	ДанныеПоРасходам.Подразделение КАК Подразделение,
		|	ДанныеПоРасходам.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	РеквизитыСтатьи.ПравилоРаспределенияРасходовРегл КАК ПравилоРаспределенияРегл,
		//++ Локализация
		|	РеквизитыСтатьи.ПравилоРаспределенияРасходовНУ КАК ПравилоРаспределенияНал,
		//-- Локализация
		|	РеквизитыСтатьи.ПравилоРаспределенияРасходовУпр КАК ПравилоРаспределенияУпр
		|ИЗ
		|	ДанныеПоРасходам КАК ДанныеПоРасходам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыВозникновенияЗатрат КАК РеквизитыСтатьи
		|		ПО ДанныеПоРасходам.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
		|		И ДанныеПоРасходам.АналитикаРасходов = РеквизитыСтатьи.Ссылка
		//-- НЕ УТ
		|";
	
	Запрос.УстановитьПараметр("ДанныеПоРасходам", ТаблицаДанных);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	МассивНастроек = Новый Массив;
	Пока Выборка.Следующий() Цикл
		
		ФорматДанныхРаспределенияСтатьиРасходов = ФорматДанныхРаспределенияСтатьиРасходов();
		ЗаполнитьЗначенияСвойств(ФорматДанныхРаспределенияСтатьиРасходов, Выборка);
		
		МассивНастроек.Добавить(ФорматДанныхРаспределенияСтатьиРасходов);
		
	КонецЦикла;
		
	Возврат МассивНастроек;
	
КонецФункции

// Формирует документы распределения расходов по переданным параметрам
// и помещает результат создания документов во временное хранилище.
// Параметры:
//	Параметры - Структура - см. Документ.РаспределениеПрочихЗатрат.ФормаРабочееМесто.ПолучитьПараметрыРасходов().
//	АдресХранилища - Строка - адрес во временном хранилище, куда следует поместить результат создания документов.
// Возвращаемое значение:
//	Соответствие - в качестве ключа документа, а в качестве значения структура.
Функция СформироватьДокументы(Параметры, АдресХранилища) Экспорт
	
	Перем ПараметрыРасходов;
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"Документ.РаспределениеПрочихЗатрат.МодульМенеджера.СформироватьДокументы");
	
	Параметры.Свойство("ПараметрыРасходов", ПараметрыРасходов);
	
	РезультатыФормированияДокументов = Новый Соответствие();
	
	ШаблонСообщения = НСтр("ru = 'Не удалось создать документ распределения по %1 учету для статьи %2 с аналитикой %3 по причине: %4 %5.';
							|en = 'Cannot generate the allocation document by %1 accounting for the item %2 with dimension %3. Reason: %4 %5.'", ОбщегоНазначения.КодОсновногоЯзыка());
	
	НастройкиРаспределенияСтатьиРасходов = 
		ПолучитьНастройкиРаспределенияСтатейРасходов(ПараметрыРасходов);

	Для Каждого НастройкаРаспределения Из НастройкиРаспределенияСтатьиРасходов Цикл
		
		ДетализацияРезультатовФормирования = РезультатыФормированияДокументов.Получить(НастройкаРаспределения.Организация);
		Если ДетализацияРезультатовФормирования = Неопределено Тогда
			
			ДетализацияРезультатов = Новый Структура;
			ДетализацияРезультатов.Вставить("СформированоДокументов", 0);
			ДетализацияРезультатов.Вставить("ОбновленныеДанные", Новый Массив);
			ДетализацияРезультатов.Вставить("ОписаниеОшибок", Новый Массив);
			
			РезультатыФормированияДокументов.Вставить(НастройкаРаспределения.Организация,
				ДетализацияРезультатов);
				
		КонецЕсли;
		
		Результат = СформироватьДокумент(НастройкаРаспределения);
		Если Результат.Свойство("ОписаниеОшибкиРегл") Тогда
			
			ТекстСообщения = СтрШаблон(
				ШаблонСообщения,
				НСтр("ru = 'регламентированному';
					|en = 'local'", ОбщегоНазначения.КодОсновногоЯзыка()),
				НастройкаРаспределения.СтатьяРасходов,
				НастройкаРаспределения.АналитикаРасходов,
				Символы.ПС,
				Результат.ОписаниеОшибкиРегл);

			ОписаниеОшибки = Новый Структура;
			ОписаниеОшибки.Вставить("СтатьяРасходов", НастройкаРаспределения.СтатьяРасходов);
			ОписаниеОшибки.Вставить("ТекстОшибки", ТекстСообщения);
			
			ДетализацияРезультатов.ОписаниеОшибок.Добавить(ОписаниеОшибки);
			
		Иначе
			ДетализацияРезультатов.СформированоДокументов = ДетализацияРезультатов.СформированоДокументов + 1;
		КонецЕсли;
		
		Если Результат.Свойство("ОписаниеОшибкиУпр") Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСообщения,
				НСтр("ru = 'управленческому';
					|en = 'management'", ОбщегоНазначения.КодОсновногоЯзыка()),
				НастройкаРаспределения.СтатьяРасходов,
				НастройкаРаспределения.АналитикаРасходов,
				Символы.ПС,
				Результат.ОписаниеОшибкиУпр);

			ОписаниеОшибки = Новый Структура;
			ОписаниеОшибки.Вставить("СтатьяРасходов", НастройкаРаспределения.СтатьяРасходов);
			ОписаниеОшибки.Вставить("ТекстОшибки", ТекстСообщения);
			
			ДетализацияРезультатов.ОписаниеОшибок.Добавить(ОписаниеОшибки);
			
		Иначе
			ДетализацияРезультатов.СформированоДокументов = ДетализацияРезультатов.СформированоДокументов + 1;
		КонецЕсли;
		
		НовыеЗначения = Новый Структура("СтатьяРасходов, АналитикаРасходов, Организация, Подразделение, НаправлениеДеятельности, ИдСтроки");
		ЗаполнитьЗначенияСвойств(НовыеЗначения, НастройкаРаспределения);
		
		Для Каждого КлючИЗначение Из Результат Цикл
			НовыеЗначения.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЦикла;
		
		ДетализацияРезультатов.ОбновленныеДанные.Добавить(НовыеЗначения);
		
	КонецЦикла;
		
	Если Не АдресХранилища = Неопределено Тогда
		ПоместитьВоВременноеХранилище(РезультатыФормированияДокументов, АдресХранилища);
	КонецЕсли;
	
	СформированоДокументов = 0;
	Для Каждого РезультатПоОрганизации Из РезультатыФормированияДокументов Цикл
		СформированоДокументов = СформированоДокументов + РезультатПоОрганизации.Значение.СформированоДокументов;
	КонецЦикла;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, СформированоДокументов);
	
	Возврат РезультатыФормированияДокументов;
	
КонецФункции

//++ НЕ УТ

// Возвращает список подразделений по направлению распределения.
// Параметры:
//	Подразделение - СправочникСсылка.СтруктураПредприятия - подразделение от которого требуется получить подразделения по направлению распределения.
//	НаправлениеРаспределения - ПеречислениеСсылка.НаправлениеРаспределенияПоПодразделениям - направление распределения.
// Возвращаемое значение:
//	Массив - подразделения по направлению.
//
Функция ПодразделенияПоНаправлению(Подразделение, НаправлениеРаспределения) Экспорт
	
	Если Не ЗначениеЗаполнено(НаправлениеРаспределения) Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	Если НаправлениеРаспределения = Перечисления.НаправлениеРаспределенияПоПодразделениям.Текущее Тогда
		
		МассивПодразделений = Новый Массив;
		МассивПодразделений.Добавить(Подразделение);
		
		Возврат МассивПодразделений;
		
	КонецЕсли;
		
	Если НаправлениеРаспределения = Перечисления.НаправлениеРаспределенияПоПодразделениям.Вышестоящее Тогда
		
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	СтруктураПредприятия.Родитель КАК Подразделение
			|ИЗ
			|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
			|ГДЕ
			|	СтруктураПредприятия.Ссылка = &Подразделение";
		
		
	ИначеЕсли НаправлениеРаспределения = Перечисления.НаправлениеРаспределенияПоПодразделениям.Нижестоящие Тогда
	
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	СтруктураПредприятия.Ссылка КАК Подразделение
			|ИЗ
			|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
			|ГДЕ
			|	СтруктураПредприятия.Ссылка В ИЕРАРХИИ(&Подразделение)
			|	И СтруктураПредприятия.Ссылка <> &Подразделение";
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Подразделение");
	
КонецФункции

// Возвращает параметры настройки счетов учета в документе.
//  
// Возвращаемое значение:
//	См. НастройкаСчетовУчетаСервер.ПараметрыНастройки
//
Функция ПараметрыНастройкиСчетовУчета() Экспорт
	
	ПараметрыНастройки = НастройкаСчетовУчетаСервер.ПараметрыНастройки();
	
	ПараметрыНастройки.ДоступностьПоОперации = Истина;
	ПараметрыНастройки.ПутьКДанным           = "Объект.Списание";
	ПараметрыНастройки.ТипСтатьи             = "ТипСтатьи";
	
	ПараметрыНастройки.ЭлементыФормы.Добавить("СписаниеПредставлениеОтраженияОперации");
	
	Возврат ПараметрыНастройки;
	
КонецФункции
//-- НЕ УТ

// Возвращает параметры выбора статей и аналитик.
// 
// Возвращаемое значение:
//	См. ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики
//
Функция ПараметрыВыбораСтатейИАналитик() Экспорт 
	
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект.Списание";
	ПараметрыВыбора.Статья      = "СтатьяРасходов";
	ПараметрыВыбора.ТипСтатьи   = "ТипСтатьи";
	
	ПараметрыВыбора.АналитикаРасходов 		 = "АналитикаРасходов";
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивов";
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	
	МассивВариантов = Новый Массив;
	МассивВариантов.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности);
	МассивВариантов.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаРасходыБудущихПериодов);
	МассивВариантов.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров);
	МассивВариантов.Добавить(Перечисления.ВариантыРаспределенияРасходов.НаПрочиеАктивы);
	МассивВариантов.Добавить(Перечисления.ВариантыРаспределенияРасходов.НеРаспределять);
	
	ПараметрыВыбора.ОтборСтатейРасходов.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеТоваровПоТребованию;
	ПараметрыВыбора.ОтборСтатейРасходов.ВариантРаспределенияРасходов = МассивВариантов;
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СписаниеСтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("СписаниеАналитикаРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("СписаниеАналитикаАктивовПассивов");
	
	Возврат ПараметрыВыбора;
	
КонецФункции

// Устанавливает доступность флагов использования документа для видов учета в зависимости от настроек статьи расходов
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта документа "Распределение расходов"
//
Процедура УстановитьДоступностьВидовУчета(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	Элементы.УправленческийУчет.Доступность = Истина;
	Элементы.РегламентированныйУчет.Доступность = Истина;
	//++ НЕ УТ
	//++ Локализация
	Элементы.НалоговыйУчет.Доступность = Истина;	
	//-- Локализация
	//-- НЕ УТ
	
	НастройкиРаспределения = ПолучитьНастройкиРаспределенияПоДокументу(Объект);
	
	Если Объект.УправленческийУчет Тогда
		Элементы.РегламентированныйУчет.Доступность = Объект.РегламентированныйУчет
			ИЛИ НастройкиРаспределения.ВариантРаспределенияРасходовУпр = НастройкиРаспределения.ВариантРаспределенияРасходовРегл;
		//++ НЕ УТ
		//++ Локализация
		Элементы.НалоговыйУчет.Доступность = Объект.НалоговыйУчет
			ИЛИ НастройкиРаспределения.ВариантРаспределенияРасходовУпр = НастройкиРаспределения.ВариантРаспределенияРасходовНУ;	
		//-- Локализация
		//-- НЕ УТ
	ИначеЕсли Объект.РегламентированныйУчет Тогда
		Элементы.УправленческийУчет.Доступность =
			НастройкиРаспределения.ВариантРаспределенияРасходовУпр = НастройкиРаспределения.ВариантРаспределенияРасходовРегл;
		//++ НЕ УТ
		//++ Локализация
		Элементы.НалоговыйУчет.Доступность = Объект.НалоговыйУчет
			ИЛИ НастройкиРаспределения.ВариантРаспределенияРасходовРегл = НастройкиРаспределения.ВариантРаспределенияРасходовНУ;	
	ИначеЕсли Объект.НалоговыйУчет Тогда
		Элементы.УправленческийУчет.Доступность =
			НастройкиРаспределения.ВариантРаспределенияРасходовУпр = НастройкиРаспределения.ВариантРаспределенияРасходовНУ;
		Элементы.РегламентированныйУчет.Доступность =
			НастройкиРаспределения.ВариантРаспределенияРасходовРегл = НастройкиРаспределения.ВариантРаспределенияРасходовНУ;	
		//-- Локализация
		//-- НЕ УТ
	КонецЕсли;
		
КонецПроцедуры

// Устанавливает или снимает признак отражения в БУ в зависимости от признака НУ и настроек статьи расходов и наоборот.// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта документа "Распределение расходов"
//  ВидУчета - Строка - имя вида учета, для которого пользователь установил или снял признак отражения в документе.
//
Процедура СинхронизироватьВидыУчетаБУиНУ(Форма, ВидУчета) Экспорт
	
	Если ВидУчета = "УправленческийУчет" Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	УстановитьПризнакиБУиНУ(Объект, ВидУчета);
	
КонецПроцедуры

Процедура УстановитьПризнакиБУиНУ(Объект, ВидУчета)
	
	//++ НЕ УТ
	//++ Локализация

	НастройкиРаспределения = ПолучитьНастройкиРаспределенияПоДокументу(Объект);
	
	НастройкиБУиНУСоответствуют = 
		Не РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии22(?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, Неопределено))
		И НастройкиРаспределения.ВариантРаспределенияРасходовНУ = НастройкиРаспределения.ВариантРаспределенияРасходовРегл
		И НастройкиРаспределения.ВариантРаспределенияРасходовНУ = 
			ПредопределенноеЗначение("Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты")
		ИЛИ	НастройкиРаспределения.ВариантРаспределенияРасходовНУ = НастройкиРаспределения.ВариантРаспределенияРасходовРегл
		И (НастройкиРаспределения.ПравилоРаспределенияРасходовНУ = НастройкиРаспределения.ПравилоРаспределенияРасходовРегл
			ИЛИ НЕ Перечисления.ВариантыРаспределенияРасходов.ВариантИспользуетПравилоРаспределенияНУ(НастройкиРаспределения.ВариантРаспределенияРасходовНУ)
			ИЛИ НастройкиРаспределения.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров
			ИЛИ НастройкиРаспределения.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства);
	
	Если НастройкиБУиНУСоответствуют Тогда
		Если ВидУчета = "РегламентированныйУчет" Тогда
			Объект.НалоговыйУчет = Объект.РегламентированныйУчет;
		ИначеЕсли ВидУчета = "НалоговыйУчет" Тогда
			Объект.РегламентированныйУчет = Объект.НалоговыйУчет;
		КонецЕсли;
	КонецЕсли;
	
	Возврат;

	//-- Локализация
	//-- НЕ УТ
	
	Если ВидУчета = "РегламентированныйУчет" Тогда
		Объект.НалоговыйУчет = Объект.РегламентированныйУчет;
	КонецЕсли;
	
КонецПроцедуры

#Область СтандартныеПодсистемы

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	РаспределениеПрочихЗатратЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

// Добавляет команду создания документа "Распределение расходов на себестоимость продукции".
//
// Параметры:
//  КомандыСозданияНаОсновании - ТаблицаЗначений - описание в СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
// Возвращаемое значение:
// 	СтрокаТаблицыЗначений - описание в СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании 
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РаспределениеПрочихЗатрат) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РаспределениеПрочихЗатрат.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.РаспределениеПрочихЗатрат);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьПроизводство,ИспользоватьУчетПрочихДоходовРасходов";
	
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	//++ НЕ УТ
	Отчеты.БазаРаспределенияПроизводственныхРасходов.ДобавитьКомандуОтчета(КомандыОтчетов);
	//-- НЕ УТ
	РаспределениеПрочихЗатратЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	РаспределениеПрочихЗатратЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

#КонецОбласти

//++ НЕ УТ
#Область РегламентированныйУчет

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//   Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат РаспределениеПрочихЗатратЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//   Строка - Текст запроса временной таблицы.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат РаспределениеПрочихЗатратЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

#КонецОбласти
//-- НЕ УТ
#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если Не ВидФормы = "ФормаОбъекта" Тогда
		Возврат;
	КонецЕсли;
	
	НаФР = ПредопределенноеЗначение("Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаФинансовыйРезультат");
	//++ НЕ УТ
	НаПартии = ПредопределенноеЗначение("Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеСтатейРасходовПоЭтапамПроизводства");
	НаОВЗ = ПредопределенноеЗначение("Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаОВЗ");
	НаПроизводство = ПредопределенноеЗначение("Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаСебестоимостьПроизводства");
	//-- НЕ УТ
	НаСебестоимость = ПредопределенноеЗначение("Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаСебестоимостьТоваров");
	
	ФормыПоНазначению = Новый Соответствие;
	ФормыПоНазначению.Вставить(НаФР, "ФормаНастроекНаФР");
	//++ НЕ УТ
	ФормыПоНазначению.Вставить(НаПартии, "ФормаНастроекНаПартии");
	ФормыПоНазначению.Вставить(НаОВЗ, "ФормаНастроекНаОВЗ");
	ФормыПоНазначению.Вставить(НаПроизводство, "ФормаНастроекНаПроизводствоПрямые");
	//-- НЕ УТ
	ФормыПоНазначению.Вставить(НаСебестоимость, "ФормаНастроекНаСебестоимостьТоваров");
	
	Если Параметры.Свойство("Ключ") Тогда
		
		ВыбраннаяФорма = ФормыПоНазначению.Получить(
			ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(
				Параметры.Ключ, 
				"НазначениеНастройкиРаспределения"));
				
	ИначеЕсли Параметры.Свойство("Основание") Тогда
		
		НазначенияПоВарианту = Новый Соответствие;
		НазначенияПоВарианту.Вставить(
			ПредопределенноеЗначение("Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности"),
			НаФР);
		//++ НЕ УТ
		НазначенияПоВарианту.Вставить(
			ПредопределенноеЗначение("Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты"),
			НаПартии);
		НазначенияПоВарианту.Вставить(
			ПредопределенноеЗначение("Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат"),
			НаОВЗ);
		НазначенияПоВарианту.Вставить(
			ПредопределенноеЗначение("Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства"),
			НаПроизводство);
		//-- НЕ УТ
		НазначенияПоВарианту.Вставить(
			ПредопределенноеЗначение("Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров"),
			НаСебестоимость);
			
		//++ НЕ УТ
		//++ Локализация
		Если Параметры.Основание.НалоговыйУчет Тогда
			ВыбраннаяФорма = ФормыПоНазначению.Получить(НазначенияПоВарианту.Получить(Параметры.Основание.ВариантРаспределенияНал));
		КонецЕсли;
		//-- Локализация
		//-- НЕ УТ
		
		Если Параметры.Основание.УправленческийУчет Тогда
			ВыбраннаяФорма = ФормыПоНазначению.Получить(НазначенияПоВарианту.Получить(Параметры.Основание.ВариантРаспределенияУпр));
		КонецЕсли;
		
		Если Параметры.Основание.РегламентированныйУчет Тогда
			ВыбраннаяФорма = ФормыПоНазначению.Получить(НазначенияПоВарианту.Получить(Параметры.Основание.ВариантРаспределенияРегл));
		КонецЕсли;
		
	ИначеЕсли Параметры.Свойство("ЗначенияЗаполнения")
		И Параметры.ЗначенияЗаполнения.Свойство("НазначениеНастройкиРаспределения") Тогда
		ВыбраннаяФорма = ФормыПоНазначению.Получить(Параметры.ЗначенияЗаполнения.НазначениеНастройкиРаспределения);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыбраннаяФорма) Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиЭтаповЗакрытияМесяца

#Область ОформлениеДокументовРаспределенияРасходов

// Добавляет этап в таблицу этапов закрытия месяца.
// Элементы данной таблицы являются элементами второго уровня в дереве этапов в форме закрытия месяца.
//
// Параметры:
//	ТаблицаЭтапов - см. Обработки.ОперацииЗакрытияМесяца.ЗаполнитьОписаниеЭтаповЗакрытияМесяца
//	ТекущийРодитель - Строка - идентификатор группы.
Процедура ДобавитьЭтап_ОформлениеДокументовРаспределенияРасходовПоНаправлениямДеятельности(ТаблицаЭтапов, ТекущийРодитель) Экспорт
	
	НоваяСтрока = ЗакрытиеМесяцаСервер.ДобавитьЭтапВТаблицу(ТаблицаЭтапов, ТекущийРодитель,
		Перечисления.ОперацииЗакрытияМесяца.ОформлениеДокументовРаспределенияРасходовПоНаправлениямДеятельности);
	НоваяСтрока.ТекстВыполнить = НСтр("ru = 'Оформить';
										|en = 'Register'");
	НоваяСтрока.ДействиеИспользование = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"Документы.РаспределениеПрочихЗатрат.Использование_ОформлениеДокументовРаспределенияРасходовПоНаправлениямДеятельности");
	НоваяСтрока.ДействиеВыполнить  = ЗакрытиеМесяцаСервер.ОписаниеДействия_ВыполнитьРасчет(
		"Документы.РаспределениеПрочихЗатрат.Выполнить_ОформлениеДокументовРаспределенияРасходовПоНаправлениямДеятельности");
	НоваяСтрока.ДействиеПодробнее = ЗакрытиеМесяцаСервер.ОписаниеДействия_ОткрытьФорму(
		Метаданные.Документы.РаспределениеПрочихЗатрат.Формы.ФормаРабочееМесто.ПолноеИмя());
	НоваяСтрока.ДействиеПодробнее.ПараметрыФормы.Вставить("Подразделение", Справочники.СтруктураПредприятия.ПустаяСсылка());
	НоваяСтрока.ДействиеПодробнее.ПараметрыФормы.Вставить("Состояние", Перечисления.СостоянияРаспределенияРасходов.ТребуетсяСформироватьДокумент);
		
КонецПроцедуры

// Обработчики этапа.

Процедура Использование_ОформлениеДокументовРаспределенияРасходовПоНаправлениямДеятельности(ПараметрыОбработчика) Экспорт
	
	Запрос = Новый Запрос;
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(Запрос, ПараметрыОбработчика);
	
	ПараметрыЗапроса = ОписаниеПараметровЗапросаПолученияДанныхДляРаспределения();
	ПараметрыЗапроса.Состояние = Перечисления.СостоянияРаспределенияРасходов.ТребуетсяСформироватьДокумент;
	ПараметрыЗапроса.ВариантРаспределения =
		ОбщегоНазначенияУТКлиентСервер.Массив(Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности);
	Документы.РаспределениеПрочихЗатрат.ИнициализироватьЗапросПолученияДанныхДляРаспределения(Запрос, ПараметрыЗапроса);
	
	Запрос.Текст = ТекстЗапросаДанныеДляРаспределения(, Истина);
	Запрос.Выполнить();	
	
	РазмерыВременныхТаблиц = ЗакрытиеМесяцаСервер.РазмерыВременныхТаблиц(Запрос, ПараметрыОбработчика);
	
	// Для этапа уже установлено состояние "Не требуется", поэтому проверку состояния не выполняем.
	Если ПараметрыОбработчика.ДанныеЭтапа.Состояние = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется Тогда
		Возврат;
	КонецЕсли;
	
	Если РазмерыВременныхТаблиц.ВТДанныеДляРаспределения = 0 Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеТребуется(
			ПараметрыОбработчика,
			НСтр("ru = 'Нет данных для распределения расходов.';
				|en = 'No data for cost allocation.'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
		Возврат;
		
	КонецЕсли;
	
	Если РазмерыВременныхТаблиц.СостояниеРаспределенияРасходов = 0 Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеТребуется(
			ПараметрыОбработчика,
			НСтр("ru = 'По всем расходам заданы настройки распределения.';
				|en = 'Allocation settings are set up for all expenses.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	Иначе
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеВыполнен(
			ПараметрыОбработчика,
			НСтр("ru = 'Требуется оформление документов распределения расходов.';
				|en = 'Generation of expense allocation documents is required.'"));	
	КонецЕсли;
	
КонецПроцедуры

Процедура Выполнить_ОформлениеДокументовРаспределенияРасходовПоНаправлениямДеятельности(ПараметрыОбработчика) Экспорт
	
	ПараметрыРасчета = ПараметрыОбработчика.ПараметрыРасчета;
	
	СтатьиКРаспределению = СтатьиКРаспределению(
		ПараметрыРасчета.ПериодРегистрации,
		ПараметрыРасчета.МассивОрганизаций,
		Новый Массив,
		Перечисления.СостоянияРаспределенияРасходов.ТребуетсяСформироватьДокумент,
		Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности);
		
	Если СтатьиКРаспределению.Количество() = 0 Тогда // нет данных для распределения
		Возврат;
	КонецЕсли;
	
	СтатьиКРаспределению.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
	СтатьиКРаспределению.Колонки.Добавить("ИДСтроки", Новый ОписаниеТипов("Число"));
	СтатьиКРаспределению.ЗаполнитьЗначения(КонецМесяца(ПараметрыРасчета.ПериодРегистрации), "Дата");
	
	ПараметрыРаспределения = Новый Структура("ПараметрыРасходов", СтатьиКРаспределению);
	
	Попытка
		РезультатыФормирования = СформироватьДокументы(ПараметрыРаспределения, Неопределено);
	Исключение
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось создать документы распределения расходов за период %1 из-за ошибки:
				|%2';
				|en = 'Cannot generate cost allocation documents for the period %1 due to the error:
				|%2'", ОбщегоНазначения.КодОсновногоЯзыка()),
			РасчетСебестоимостиПротоколРасчета.ПредставлениеПериодаРасчета(ПараметрыРасчета.ПериодРегистрации),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗакрытиеМесяцаСервер.ЗафиксироватьНаличиеПроблемыПриВыполненииРасчета(
			ПараметрыОбработчика,
			ТекстОшибки);
		
	КонецПопытки;
	
	Для Каждого РезультатПоОрганизации Из РезультатыФормирования Цикл
		
		ДетализацияФормирования = РезультатПоОрганизации.Значение;
		
		Если Не ДетализацияФормирования.ОписаниеОшибок.Количество() Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого ОписаниеОшибки Из ДетализацияФормирования.ОписаниеОшибок Цикл
			ЗакрытиеМесяцаСервер.ЗафиксироватьНаличиеПроблемыПриВыполненииРасчета(
				ПараметрыОбработчика,
				ОписаниеОшибки.ТекстОшибки,
				РезультатПоОрганизации.Ключ,
				,
				,
				ОписаниеОшибки.СтатьяРасходов);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Проверки состояния системы, относящиеся к этапу.

Процедура ОписаниеПроверок_ОформлениеДокументовРаспределенияРасходовПоНаправлениямДеятельности(ТаблицаПроверок) Экспорт
	
	// Настройка распределения расходов.
	ОписаниеПроверки = ЗакрытиеМесяцаСервер.ДобавитьОписаниеНовойПроверки(ТаблицаПроверок,
		"НеВыполненаОформлениеДокументовРаспределенияРасходовПоНаправлениямДеятельности",
		Перечисления.ОперацииЗакрытияМесяца.ОформлениеДокументовРаспределенияРасходовПоНаправлениямДеятельности,
		Перечисления.МоментЗапускаПроверкиОперацииЗакрытияМесяца.ПослеРасчета,
		"Документы.РаспределениеПрочихЗатрат.ПроверкаНеобходимостиОформлениеДокументовРаспределенияРасходовПоНаправлениямДеятельности");
	
	ЗакрытиеМесяцаСервер.ЗаполнитьПредставлениеНовойПроверки(ОписаниеПроверки,
		НСтр("ru = 'Есть неоформленные документы распределения расходов';
			|en = 'There are unregistered cost allocation documents'", ОбщегоНазначения.КодОсновногоЯзыка()),
		НСтр("ru = 'Все производственные расходы должны быть распределены по направлениям деятельности.';
			|en = 'All production expenses must be allocated by lines of business.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
КонецПроцедуры

Процедура ПроверкаНеобходимостиОформлениеДокументовРаспределенияРасходовПоНаправлениямДеятельности(ПараметрыПроверки) Экспорт
	
	Если НЕ ЗакрытиеМесяцаСервер.ПроверкаВыполняетсяМеханизмомЗакрытияМесяца(ПараметрыПроверки) Тогда
		Возврат;
	КонецЕсли;
	
	СписокПолей = Новый СписокЗначений;
	СписокПолей.Добавить("Организация", 			НСтр("ru = 'Организация';
														|en = 'Company'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("Подразделение", 			НСтр("ru = 'Подразделение';
															|en = 'Business unit'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("НаправлениеДеятельности", НСтр("ru = 'Направление деятельности';
														|en = 'Line of business'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("СтатьяРасходов", 			НСтр("ru = 'Статья расходов';
															|en = 'Expense item'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("АналитикаРасходов", 		НСтр("ru = 'Аналитика расходов';
															|en = 'Expense dimension'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	ПараметрыРегистрации = ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыРегистрацииПроблемПроверки(
		"СостояниеРаспределенияРасходов",
		НСтр("ru = 'Есть неоформленные документы распределения расходов по организации ""%1"" на конец периода %2';
			|en = 'There are unregistered cost allocation documents for the company ""%1"" at the end of the period %2'", ОбщегоНазначения.КодОсновногоЯзыка()),
		СписокПолей);
	
	ЗакрытиеМесяцаСервер.ЗарегистрироватьПроблемыВыполненияПроверки(
	 	ПараметрыПроверки,
		ПараметрыРегистрации);
		
КонецПроцедуры

#КонецОбласти

#Область РаспределениеРасходовПоНаправлениямДеятельности

// Добавляет этап в таблицу этапов закрытия месяца.
// Элементы данной таблицы являются элементами второго уровня в дереве этапов в форме закрытия месяца.
//
// Параметры:
// 	ТаблицаЭтапов - см. Обработки.ОперацииЗакрытияМесяца.ЗаполнитьОписаниеЭтаповЗакрытияМесяца
// 	ТекущийРодитель - Строка - идентификатор группы.
Процедура ДобавитьЭтап_РаспределениеРасходовПоНаправлениямДеятельности(ТаблицаЭтапов,ТекущийРодитель) Экспорт
	
	НоваяСтрока = ЗакрытиеМесяцаСервер.ДобавитьЭтапВТаблицу(ТаблицаЭтапов, ТекущийРодитель,
		Перечисления.ОперацииЗакрытияМесяца.РаспределениеРасходовПоНаправлениямДеятельности);
	НоваяСтрока.ПредшествующиеЭтапы.Добавить(Перечисления.ОперацииЗакрытияМесяца.РасчетПартийИСебестоимости);
	НоваяСтрока.ПредшествующиеЭтапы.Добавить(Перечисления.ОперацииЗакрытияМесяца.РаспределениеНДС);
	НоваяСтрока.ПредшествующиеЭтапы.Добавить(Перечисления.ОперацииЗакрытияМесяца.ОформлениеДокументовРаспределенияРасходовПоНаправлениямДеятельности);
	НоваяСтрока.ТекстВыполнить = НСтр("ru = 'Распределить';
										|en = 'Allocate'");
	НоваяСтрока.ДействиеИспользование = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"Документы.РаспределениеПрочихЗатрат.Использование_РаспределениеРасходовПоНаправлениямДеятельности");
	НоваяСтрока.ДействиеВыполнить  = ЗакрытиеМесяцаСервер.ОписаниеДействия_ВыполнитьРасчет(
		"Документы.РаспределениеПрочихЗатрат.Выполнить_РаспределениеРасходовПоНаправлениямДеятельности");
	НоваяСтрока.ДействиеПодробнее = ЗакрытиеМесяцаСервер.ОписаниеДействия_ОткрытьФорму(
		Метаданные.Документы.РаспределениеПрочихЗатрат.Формы.ФормаРабочееМесто.ПолноеИмя());
		
КонецПроцедуры

// Обработчики этапа.

Процедура Использование_РаспределениеРасходовПоНаправлениямДеятельности(ПараметрыОбработчика) Экспорт
	
	Если ПараметрыОбработчика.ДанныеЭтапа.Состояние = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется Тогда
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос;
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(Запрос, ПараметрыОбработчика);
	
	ПараметрыЗапроса = ОписаниеПараметровЗапросаПолученияДанныхДляРаспределения();
	ПараметрыЗапроса.ВариантРаспределения =
		ОбщегоНазначенияУТКлиентСервер.Массив(Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности);
	
	ИнициализироватьЗапросПолученияДанныхДляРаспределения(Запрос, ПараметрыЗапроса);
	
	Запрос.Текст = ТекстЗапросаДанныеДляРаспределения();
	Результат = Запрос.Выполнить();	
	
	РазмерыВременныхТаблиц = ЗакрытиеМесяцаСервер.РазмерыВременныхТаблиц(Запрос, ПараметрыОбработчика);
	
	ЕстьЗаданиеКРасчету = ЗакрытиеМесяцаСервер.ПроверитьНаличиеЗаданийКЗакрытиюМесяца(ПараметрыОбработчика, Ложь);
	Если РазмерыВременныхТаблиц.ВТДанныеДляРаспределения = 0 И Не ЕстьЗаданиеКРасчету Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеТребуется(
			ПараметрыОбработчика,
			НСтр("ru = 'Нет данных для распределения расходов.';
				|en = 'No data for cost allocation.'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
		Возврат;
		
	КонецЕсли;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	*
		|ПОМЕСТИТЬ ВТОстаткиПрочихРасходов
		|ИЗ
		|	ВТДанныеДляРаспределения КАК Т
		|ГДЕ
		|	НЕ Т.РаспределеноВУУ
		|		И Т.ДокументУпр.НазначениеНастройкиРаспределения = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаФинансовыйРезультат)
		|	ИЛИ НЕ Т.РаспределеноВБУ
		|		И Т.ДокументРегл.НазначениеНастройкиРаспределения = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаФинансовыйРезультат)
		//++ НЕ УТ
		//++ Локализация
		|	ИЛИ НЕ Т.РаспределеноВНУ
		|		И Т.ДокументНал.НазначениеНастройкиРаспределения = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаФинансовыйРезультат)
		|	ИЛИ НЕ Т.РаспределеноВНУ
		|		И Т.ДокументРегл.НазначениеНастройкиРаспределения = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеСтатейРасходовПоЭтапамПроизводства)
		|		И Т.СтатьяРасходов.ВариантРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|	ИЛИ НЕ Т.РаспределеноВНУ
		|		И Т.ДокументРегл.НазначениеНастройкиРаспределения = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеСтатейРасходовПоЭтапамПроизводства)
		|		И Т.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
		|		И Т.АналитикаРасходов.ВариантРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|	ИЛИ НЕ Т.РаспределеноВБУ
		|		И НЕ Т.ПостояннаяРазница = 0
		|		И Т.СтатьяРасходов.ВидРасходов В (&ВидыРасходовНормируемые)
		|		И Т.ДокументРегл.НазначениеНастройкиРаспределения = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеСтатейРасходовПоЭтапамПроизводства)
		//-- Локализация
		//-- НЕ УТ
		|;	
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	*
		|ИЗ
		|	ВТОстаткиПрочихРасходов";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеВыполнен(
			ПараметрыОбработчика,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Требуется пересчет за период %1.';
					|en = 'Recalculation for period %1 is required.'", ОбщегоНазначения.КодОсновногоЯзыка()),
				РасчетСебестоимостиПротоколРасчета.ПредставлениеПериодаРасчета(ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации)));
			
	КонецЕсли;
			
КонецПроцедуры

Процедура Выполнить_РаспределениеРасходовПоНаправлениямДеятельности(ПараметрыОбработчика) Экспорт
	
	ПараметрыРасчета = ПараметрыОбработчика.ПараметрыРасчета;
	
	НачалоПериода = НачалоМесяца(ПараметрыРасчета.ПериодРегистрации);
	КонецПериода  = КонецМесяца(ПараметрыРасчета.ПериодРегистрации);
	
	НомерЗаданияДоРасчета = ЗакрытиеМесяцаСервер.УвеличитьНомерЗадания();
	
	Попытка
		РассчитанныеОрганизации = Документы.РаспределениеПрочихЗатрат.РаспределитьРасходыНаФинансовыйРезультат(
			ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации, 
			ПараметрыОбработчика.ПараметрыРасчета.МассивОрганизаций);
	Исключение
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Распределение расходов по направлениям деятельности за период %1 завершилось с ошибкой:
					|%2';
					|en = 'Expense allocation by lines of business for the period %1 ended with the error:
					|%2'", ОбщегоНазначения.КодОсновногоЯзыка()),
				РасчетСебестоимостиПротоколРасчета.ПредставлениеПериодаРасчета(ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации),
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ЗакрытиеМесяцаСервер.ЗафиксироватьНаличиеПроблемыПриВыполненииРасчета(
			ПараметрыОбработчика,
			ТекстОшибки);
				
	КонецПопытки;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", 		НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", 		КонецПериода);
	Запрос.УстановитьПараметр("ГраницаПериода", 	Новый Граница(КонецПериода, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организации", 		ПараметрыОбработчика.ПараметрыРасчета.МассивОрганизаций);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Т.Организация КАК Организация,
		|	Т.Подразделение КАК Подразделение,
		|	Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Т.СтатьяРасходов КАК СтатьяРасходов,
		|	Т.АналитикаРасходов КАК АналитикаРасходов
		|ПОМЕСТИТЬ СписаниеНУ
		|ИЗ
		|	РегистрНакопления.ПрочиеРасходы КАК Т
		|ГДЕ
		|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеНормируемыхРасходовПоНУ)
		|	И Т.Организация В(&Организации)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Организация КАК Организация,
		|	МАКСИМУМ(Т.БылоСписаниеНУ) КАК БылоСписаниеНУ
		|ИЗ
		|	(ВЫБРАТЬ
		|		СписаниеНУ.Организация КАК Организация,
		|		ИСТИНА КАК БылоСписаниеНУ
		|	ИЗ
		|		СписаниеНУ КАК СписаниеНУ
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы.Остатки(
		|					&ГраницаПериода,
		|					(Организация, Подразделение, НаправлениеДеятельности, СтатьяРасходов, АналитикаРасходов) В
		|						(ВЫБРАТЬ
		|							Аналитики.Организация,
		|							Аналитики.Подразделение,
		|							Аналитики.НаправлениеДеятельности,
		|							Аналитики.СтатьяРасходов,
		|							Аналитики.АналитикаРасходов
		|						ИЗ
		|							СписаниеНУ КАК Аналитики)) КАК Остатки
		|			ПО СписаниеНУ.Организация = Остатки.Организация
		|				И СписаниеНУ.Подразделение = Остатки.Подразделение
		|				И СписаниеНУ.НаправлениеДеятельности = Остатки.НаправлениеДеятельности
		|				И СписаниеНУ.СтатьяРасходов = Остатки.СтатьяРасходов
		|				И СписаниеНУ.АналитикаРасходов = Остатки.АналитикаРасходов
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		Т.Организация,
		|		ЛОЖЬ
		|	ИЗ
		|		РегистрСведений.ЗаданияКЗакрытиюМесяца КАК Т
		|			ЛЕВОЕ СОЕДИНЕНИЕ СписаниеНУ КАК СписаниеНУ
		|			ПО Т.Организация = СписаниеНУ.Организация
		|	ГДЕ
		|		Т.Месяц = &НачалоПериода
		|		И Т.Организация В(&Организации)
		|		И Т.Операция = ЗНАЧЕНИЕ(Перечисление.ОперацииЗакрытияМесяца.РаспределениеРасходовПоНаправлениямДеятельности)
		|		И СписаниеНУ.Организация ЕСТЬ NULL) КАК Т
		|
		|СГРУППИРОВАТЬ ПО
		|	Т.Организация";
	
	ДанныеПоСписаниюНУ = Запрос.Выполнить().Выгрузить();
	Если ДанныеПоСписаниюНУ.Количество() > 0 Тогда
		
		НачатьТранзакцию();
		
		Попытка
			
			ЗакрытиеМесяцаСервер.ЗаблокироватьРегистрЗаданий(
				НомерЗаданияДоРасчета,
				ДанныеПоСписаниюНУ.ВыгрузитьКолонку("Организация"),
				ПараметрыОбработчика.ДанныеЭтапа.Код);
				
			Для Каждого Данные Из ДанныеПоСписаниюНУ Цикл
					
				// Очистка текущих заданий.
				НаборЗаписей = РегистрыСведений.ЗаданияКЗакрытиюМесяца.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Месяц.Установить(НачалоПериода);
				НаборЗаписей.Отбор.Организация.Установить(Данные.Организация);
				НаборЗаписей.Отбор.Операция.Установить(ПараметрыОбработчика.ДанныеЭтапа.Код);
				НаборЗаписей.Записать();
				
				Если Данные.БылоСписаниеНУ Тогда
					РегистрыСведений.ЗаданияКЗакрытиюМесяца.СоздатьЗаписьРегистра(
						КонецПериода + 1, 
						,
						Данные.Организация, 
						ПараметрыОбработчика.ДанныеЭтапа.Код);
				КонецЕсли;
						
			КонецЦикла;

			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При выполнении расчета за период %1 произошла ошибка:
					|%2';
					|en = 'An error occurred when calculating for the %1 period:
					|%2'"),
				РасчетСебестоимостиПротоколРасчета.ПредставлениеПериодаРасчета(ПараметрыРасчета.ПериодРегистрации),
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗакрытиеМесяцаСервер.ЗафиксироватьНаличиеПроблемыПриВыполненииРасчета(
				ПараметрыОбработчика,
				ТекстОшибки,
				ДанныеПоСписаниюНУ.ВыгрузитьКолонку("Организация"),
				ПараметрыРасчета.ПериодРегистрации);
			
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

// Проверки состояния системы, относящиеся к этапу.

Процедура ОписаниеПроверок_РаспределениеРасходовПоНаправлениямДеятельности(ТаблицаПроверок) Экспорт
	
	// Настройка распределения расходов.
	ОписаниеПроверки = ЗакрытиеМесяцаСервер.ДобавитьОписаниеНовойПроверки(ТаблицаПроверок,
		"ПроверкаОстатковПрочихРасходов",
		Перечисления.ОперацииЗакрытияМесяца.РаспределениеРасходовПоНаправлениямДеятельности,
		Перечисления.МоментЗапускаПроверкиОперацииЗакрытияМесяца.ПослеРасчета,
		"Документы.РаспределениеПрочихЗатрат.ПроверкаОстатковПрочихРасходов");
	
	ЗакрытиеМесяцаСервер.ЗаполнитьПредставлениеНовойПроверки(ОписаниеПроверки,
		НСтр("ru = 'Не распределены прочие расходы на финансовый результат';
			|en = 'Other expenses for the financial results are not allocated'", ОбщегоНазначения.КодОсновногоЯзыка()),
		НСтр("ru = 'Все прочие расходы должны быть распределены на финансовый результат.';
			|en = 'All other expenses must be allocated for the financial result.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
КонецПроцедуры

Процедура ПроверкаОстатковПрочихРасходов(ПараметрыПроверки) Экспорт
	
	Если НЕ ЗакрытиеМесяцаСервер.ПроверкаВыполняетсяМеханизмомЗакрытияМесяца(ПараметрыПроверки) Тогда
		Возврат;
	КонецЕсли;
	
	СписокПолей = Новый СписокЗначений;
	СписокПолей.Добавить("Организация", 			НСтр("ru = 'Организация';
														|en = 'Company'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("Подразделение", 			НСтр("ru = 'Подразделение';
															|en = 'Business unit'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("НаправлениеДеятельности", НСтр("ru = 'Направление деятельности';
														|en = 'Line of business'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("СтатьяРасходов", 			НСтр("ru = 'Статья расходов';
															|en = 'Expense item'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("АналитикаРасходов", 		НСтр("ru = 'Аналитика расходов';
															|en = 'Expense dimension'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	ПараметрыРегистрации = ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыРегистрацииПроблемПроверки(
		"ВТОстаткиПрочихРасходов",
		НСтр("ru = 'По организации ""%1"" на конец периода %2 есть остатки по регистру прочих расходов.';
			|en = 'There is balance in the register of other expenses for the ""%1"" company as of the end of period %2.'", ОбщегоНазначения.КодОсновногоЯзыка()),
		СписокПолей,
		,
		Метаданные.РегистрыНакопления.ПрочиеРасходы.ПолноеИмя());
	
	ЗакрытиеМесяцаСервер.ЗарегистрироватьПроблемыВыполненияПроверки(
	 	ПараметрыПроверки,
		ПараметрыРегистрации);
		
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область Проведение

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	Документ.РаспределениеПрочихЗатрат КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ИдентификаторМетаданных",
								ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Тип("ДокументСсылка.РаспределениеПрочихЗатрат")));
								
	Запрос.УстановитьПараметр("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка						КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО				КАК РазделительЗаписи,
	|	&Период						КАК ДатаДокументаИБ,
	|	&Номер						КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных	КАК ТипСсылки,
	|	&Организация				КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства) КАК ХозяйственнаяОперация,
	|	&НаправлениеДеятельности	КАК НаправлениеДеятельности,
	|	&Подразделение				КАК Подразделение,
	|	&Ответственный				КАК Ответственный,
	|	&Комментарий				КАК Комментарий,
	|	0							КАК Сумма,
	|	&Проведен					КАК Проведен,
	|	&ПометкаУдаления			КАК ПометкаУдаления,
	|	ЛОЖЬ						КАК ДополнительнаяЗапись,
	|	""""						КАК Дополнительно,
	|	&Период						КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать				КАК НомерПервичногоДокумента,
	|	&Подразделение				КАК МестоХранения,
	|	&Период						КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО                КАК Приоритет
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента      = "Документ.РаспределениеПрочихЗатрат";
	СинонимТаблицыДокумента = "";
	ВЗапросеЕстьИсточник    = Истина;
	
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("ИдентификаторМетаданных",
		ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПолноеИмяДокумента));
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		ВЗапросеЕстьИсточник = Ложь;
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ВЗапросеЕстьИсточник,
			ПереопределениеРасчетаПараметров);
		
	Иначе
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ПереопределениеРасчетаПараметров);
		
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ФормированиеГиперссылкиВЖурналеПроизводства
//++ НЕ УТ
// Формирует текст гиперссылки для рабочих мест обслуживающих документ о необходимости оформления документов.
// Возвращаемое значение:
//	ФорматированнаяСтрока - представление гиперссылки.
Функция СформироватьГиперссылкуКОформлению(Параметры) Экспорт
	
	Если Не ПравоДоступа("Изменение", Метаданные.Документы.РаспределениеПрочихЗатрат) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыЗапроса = ОписаниеПараметровЗапросаПолученияДанныхДляРаспределения();
	ПараметрыЗапроса.НачалоПериода = Дата(1, 1, 1);
	ПараметрыЗапроса.КонецПериода = КонецМесяца(ТекущаяДатаСеанса());
	ПараметрыЗапроса.Организации = ОбщегоНазначенияУТКлиентСервер.Массив(Параметры.Организация);
	ПараметрыЗапроса.Подразделения = ОбщегоНазначенияУТКлиентСервер.Массив(Параметры.Подразделение);
	ПараметрыЗапроса.Состояние = Перечисления.СостоянияРаспределенияРасходов.ТребуетсяСформироватьДокумент;
	ПараметрыЗапроса.ИсключитьТранспортныеРасходы = Истина;
	ПараметрыЗапроса.ВариантРаспределения = 
		ОбщегоНазначенияУТКлиентСервер.Массив(Перечисления.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ИнициализироватьЗапросПолученияДанныхДляРаспределения(Запрос, ПараметрыЗапроса);
	
	Запрос.Текст = ТекстЗапросаДанныеДляРаспределения(1);	
	ТекстГиперссылки = НСтр("ru = 'Распределение расходов';
							|en = 'Expense аllocation'");
	
	УстановитьПривилегированныйРежим(Истина);
	Если Запрос.Выполнить().Пустой() Тогда
		Возврат Новый ФорматированнаяСтрока(ТекстГиперссылки,,ЦветаСтиля.НезаполненноеПолеТаблицы,,
			"Документ.РаспределениеПрочихЗатрат.Форма.ФормаРабочееМесто");
	Иначе
		Возврат Новый ФорматированнаяСтрока(ТекстГиперссылки,,,,
			"Документ.РаспределениеПрочихЗатрат.Форма.ФормаРабочееМесто");
	КонецЕсли;
	
КонецФункции
//-- НЕ УТ

Процедура СформироватьГиперссылкуКОформлениюФоновоеЗадание(Параметры, АдресХранилища) Экспорт
	
	КОформлению = ОбщегоНазначенияУТ.СформироватьГиперссылкуКОформлению(Параметры[0], Параметры[1]);
	ПоместитьВоВременноеХранилище(КОформлению, АдресХранилища);
	
КонецПроцедуры

#КонецОбласти

#Область Заполнение

Функция КонвертироватьДанные(ДанныеПоРасходам)
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	ТаблицаДанных.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("ИДСтроки", Новый ОписаниеТипов("Число"));
	ТаблицаДанных.Колонки.Добавить("ДокументУпр", Новый ОписаниеТипов("ДокументСсылка.РаспределениеПрочихЗатрат"));
	ТаблицаДанных.Колонки.Добавить("ДокументРегл", Новый ОписаниеТипов("ДокументСсылка.РаспределениеПрочихЗатрат"));
	//++ НЕ УТ
	//++ Локализация
	ТаблицаДанных.Колонки.Добавить("ДокументНал", Новый ОписаниеТипов("ДокументСсылка.РаспределениеПрочихЗатрат"));
	//-- Локализация
	//-- НЕ УТ
	ТаблицаДанных.Колонки.Добавить("СостояниеУпр", Новый ОписаниеТипов("ПеречислениеСсылка.СостоянияРаспределенияРасходов"));
	ТаблицаДанных.Колонки.Добавить("СостояниеРегл", Новый ОписаниеТипов("ПеречислениеСсылка.СостоянияРаспределенияРасходов"));
	//++ НЕ УТ
	//++ Локализация
	ТаблицаДанных.Колонки.Добавить("СостояниеНал", Новый ОписаниеТипов("ПеречислениеСсылка.СостоянияРаспределенияРасходов"));
	//-- Локализация
	//-- НЕ УТ
	ТаблицаДанных.Колонки.Добавить("СтатьяРасходов", Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.СтатьиРасходов"));
	ТаблицаДанных.Колонки.Добавить("АналитикаРасходов", Метаданные.ПланыВидовХарактеристик.СтатьиРасходов.Тип);
	ТаблицаДанных.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаДанных.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия"));
	ТаблицаДанных.Колонки.Добавить("НаправлениеДеятельности", Новый ОписаниеТипов("СправочникСсылка.НаправленияДеятельности"));

	Для Каждого ДанныеПоРасходу Из ДанныеПоРасходам Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаДанных.Добавить(), ДанныеПоРасходу);
	КонецЦикла;
	
	Возврат ТаблицаДанных;
	
КонецФункции

Функция ФорматДанныхРаспределенияСтатьиРасходов()
	
	СтруктураЗаполнения = Новый Структура;
	СтруктураЗаполнения.Вставить("Дата");
	СтруктураЗаполнения.Вставить("ИДСтроки");
	СтруктураЗаполнения.Вставить("ДокументУпр");
	СтруктураЗаполнения.Вставить("ДокументРегл");
	//++ НЕ УТ
	//++ Локализация
	СтруктураЗаполнения.Вставить("ДокументНал");
	//-- Локализация
	//-- НЕ УТ
	СтруктураЗаполнения.Вставить("СостояниеУпр");
	СтруктураЗаполнения.Вставить("СостояниеРегл");
	//++ НЕ УТ
	//++ Локализация
	СтруктураЗаполнения.Вставить("СостояниеНал");
	//-- Локализация
	//-- НЕ УТ
	СтруктураЗаполнения.Вставить("СтатьяРасходов");
	СтруктураЗаполнения.Вставить("АналитикаРасходов");
	СтруктураЗаполнения.Вставить("СтатьяКалькуляции");
	СтруктураЗаполнения.Вставить("ВариантРаспределенияРегл");
	//++ НЕ УТ
	//++ Локализация
	СтруктураЗаполнения.Вставить("ВариантРаспределенияНал");
	//-- Локализация
	//-- НЕ УТ
	СтруктураЗаполнения.Вставить("ВариантРаспределенияУпр");
	СтруктураЗаполнения.Вставить("Организация");
	СтруктураЗаполнения.Вставить("Подразделение");
	СтруктураЗаполнения.Вставить("НаправлениеДеятельности");
	СтруктураЗаполнения.Вставить("ПравилоРаспределенияУпр");
	СтруктураЗаполнения.Вставить("ПравилоРаспределенияРегл");
	//++ НЕ УТ
	//++ Локализация
	СтруктураЗаполнения.Вставить("ПравилоРаспределенияНал");
	//-- Локализация
	//-- НЕ УТ
	
	Возврат СтруктураЗаполнения;
	
КонецФункции

#КонецОбласти

#Область БлокировкаПриОбновленииИБ

Процедура ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ПредставлениеОперации)
	
	ВходящиеДанные = Новый Соответствие;
	
	ВходящиеДанные.Вставить(Метаданные.Документы.РаспределениеПрочихЗатрат);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.МатериалыИРаботыВПроизводстве);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПрочиеРасходы);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.СебестоимостьТоваров);
	//++ НЕ УТ
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПрочиеРасходыНезавершенногоПроизводства);
	//-- НЕ УТ
	
	ЗакрытиеМесяцаСервер.ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ВходящиеДанные, ПредставлениеОперации);
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	//++ НЕ УТ
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Документы.РаспределениеПрочихЗатрат.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.7.237";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("e2ac78d8-6fbd-49da-9ca4-87e8ed14b563");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.РаспределениеПрочихЗатрат.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Заполняет служебные реквизиты документа.
	|Заполняет реквизит ""Настройка счетов учета"".
	|Заполняет реквизит ""Налоговый учет"".';
	|en = 'Fills in service attributes of the document.
	|Fills in the ""Ledger account settings"" attribute.
	|Fills in the ""Tax accounting"" attribute.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.РаспределениеПрочихЗатрат.ПолноеИмя());
	Читаемые.Добавить(Метаданные.ПланыВидовХарактеристик.СтатьиРасходов.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Документы.РаспределениеПрочихЗатрат.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "МультиязычностьСервер.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "ПланыВидовХарактеристик.СтатьиРасходов.ЗаполнитьЭлементыНачальнымиДанными";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "ПланыВидовХарактеристик.СтатьиРасходов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";
	//++ Локализация
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыСведений.ПорядокОтраженияНаСчетахУчета.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";
	//-- Локализация
	//-- НЕ УТ

КонецПроцедуры

//++ НЕ УТ

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = "Документ.РаспределениеПрочихЗатрат";
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Дата УБЫВ");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	Параметры.ПараметрыВыборки.ДополнительныеИсточникиДанных.Вставить("СтатьяРасходов");

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.РаспределениеПрочихЗатрат КАК Реквизиты
		|ГДЕ
		|	Реквизиты.НазначениеНастройкиРаспределения = ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.РаспределениеПрочихЗатрат КАК Реквизиты
		|ГДЕ
		|	ИСТИНА В
		|		(ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			Документ.РаспределениеПрочихЗатрат.Списание КАК ТабЧастьСписание
		|		ГДЕ
		|			ТабЧастьСписание.Ссылка = Реквизиты.Ссылка
		|			И ТабЧастьСписание.УдалитьСчетУчета <> &СчетУчетаПустаяСсылка
		|			И ТабЧастьСписание.НастройкаСчетовУчета = ЗНАЧЕНИЕ(Справочник.НастройкиСчетовУчетаПрочихОпераций.ПустаяСсылка))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Т.Ссылка
		|ИЗ
		|	Документ.РаспределениеПрочихЗатрат КАК Т
		|ГДЕ
		|	Т.НазначениеНастройкиРаспределения В (ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеСтатейРасходовПоЭтапамПроизводства),
		|											ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.ПустаяСсылка))
		|	И НЕ Т.НаправлениеРаспределения = ЗНАЧЕНИЕ(Перечисление.НаправлениеРаспределенияПоПодразделениям.ПустаяСсылка)
		|	И НЕ Т.НастройкиНаправленияРаспределенияИзменены
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Т.Ссылка
		|ИЗ
		|	Документ.РаспределениеПрочихЗатрат.ОтборПоГруппамПродукции КАК Т
		|ГДЕ
		|	НЕ Т.Ссылка.НастройкиБазыРаспределенияПоПартиямИзменены
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Т.Ссылка
		|ИЗ
		|	Документ.РаспределениеПрочихЗатрат.ОтборПоМатериалам КАК Т
		|ГДЕ
		|	НЕ Т.Ссылка.НастройкиБазыРаспределенияПоПартиямИзменены
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Т.Ссылка
		|ИЗ
		|	Документ.РаспределениеПрочихЗатрат.ОтборПоВидамРабот КАК Т
		|ГДЕ
		|	НЕ Т.Ссылка.НастройкиБазыРаспределенияПоПартиямИзменены
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Т.Ссылка
		|ИЗ
		|	Документ.РаспределениеПрочихЗатрат.ОтборПоПродукции КАК Т
		|ГДЕ
		|	НЕ Т.Ссылка.НастройкиБазыРаспределенияПоПартиямИзменены
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Т.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.РаспределениеПрочихЗатрат КАК Т
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК ТНУ
		|		ПО Т.Организация = ТНУ.Организация
		|			И Т.Подразделение = ТНУ.Подразделение
		|			И Т.НаправлениеДеятельности = ТНУ.НаправлениеДеятельности
		|			И Т.СтатьяРасходов = ТНУ.СтатьяРасходов
		|			И Т.АналитикаРасходов = ТНУ.АналитикаРасходов
		|			И НАЧАЛОПЕРИОДА(Т.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ТНУ.Дата, МЕСЯЦ)
		|			И ТНУ.Проведен
		|			И ТНУ.НалоговыйУчет
		|			И НЕ ТНУ.РегламентированныйУчет
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыВозникновенияЗатрат КАК ОВЗ
		|		ПО ОВЗ.Ссылка = Т.АналитикаРасходов
		|	ЛЕВОЕ СОЕДИНЕНИЕ 
		|		(ВЫБРАТЬ
		|			СтатьиРасходов.Ссылка КАК Ссылка,
		|			СтатьиРасходов.ВариантРаспределенияРасходовРегл КАК ВариантРаспределенияРасходовРегл,
		|			СтатьиРасходов.ПравилоРаспределенияРасходовРегл КАК ПравилоРаспределенияРасходовРегл,
		|			ВЫБОР
		|			КОГДА СтатьиРасходов.ВариантРаспределенияРасходовНУ <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка)
		|				ТОГДА СтатьиРасходов.ВариантРаспределенияРасходовНУ
		|			
		|			КОГДА СтатьиРасходов.ВариантРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка)
		|				И СтатьиРасходов.ВариантРаспределенияРасходовРегл В (
		|					ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы),
		|					ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты),
		|					ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства)
		|				)
		|				И СтатьиРасходов.ВидРасходов В (
		|					ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ДобровольноеЛичноеСтрахование),
		|					ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ДобровольноеСтрахованиеПоДоговорамДолгосрочногоСтрахованияЖизниРаботников),
		|					ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.РасходыНаВозмещениеЗатратРаботниковПоУплатеПроцентов),
		|					ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ДобровольноеЛичноеСтрахованиеНаСлучайСмертиИлиУтратыРаботоспособности),
		|					ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ПредставительскиеРасходы),
		|					ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.РасходыНаРекламуНормируемые))
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			
		|			КОГДА 
		|				СтатьиРасходов.ВариантРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка)
		|				И СтатьиРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты) 
		|				И СтатьиРасходов.КосвенныеЗатратыНУ
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			
		|			КОГДА 
		|				СтатьиРасходов.ВариантРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка)
		|			ТОГДА СтатьиРасходов.ВариантРаспределенияРасходовРегл
		|			
		|			ИНАЧЕ СтатьиРасходов.ВариантРаспределенияРасходовРегл
		|			КОНЕЦ КАК ВариантРаспределенияРасходовНУ,
		|			
		|			ВЫБОР
		|			
		|			КОГДА 
		|				СтатьиРасходов.ВариантРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка)
		|				И НЕ СтатьиРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			ТОГДА СтатьиРасходов.ПравилоРаспределенияРасходовРегл
		|			
		|			КОГДА 
		|				СтатьиРасходов.ВариантРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка)
		|				И СтатьиРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.ПравилаРаспределенияРасходов.ПустаяСсылка)
		|			
		|			ИНАЧЕ СтатьиРасходов.ПравилоРаспределенияРасходовНУ
		|			КОНЕЦ КАК ПравилоРаспределенияРасходовНУ
		|			
		|			ИЗ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
		|		) КАК СтатьяРасходовНУ
		|		ПО СтатьяРасходовНУ.Ссылка = Т.СтатьяРасходов
		|ГДЕ
		|	Т.РегламентированныйУчет
		|	И НЕ Т.НалоговыйУчет
		|	И Т.Проведен
		|	И ТНУ.Ссылка ЕСТЬ NULL
		|	И ВЫБОР
		|		КОГДА НЕ ОВЗ.Ссылка ЕСТЬ NULL
		|			ТОГДА ОВЗ.ВариантРаспределенияРасходовНУ = ОВЗ.ВариантРаспределенияРасходовРегл
		|				И (ОВЗ.ПравилоРаспределенияРасходовНУ = ОВЗ.ПравилоРаспределенияРасходовРегл
		|					ИЛИ ОВЗ.ВариантРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|						И ОВЗ.ПравилоРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Справочник.ПравилаРаспределенияРасходов.ПустаяСсылка)
		|					ИЛИ ОВЗ.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|					ИЛИ ОВЗ.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства)
		|					ИЛИ НЕ ОВЗ.ВариантРаспределенияРасходовНУ В (
		|						ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаРасходыБудущихПериодов),
		|						ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)))
		|
		|		КОГДА НЕ СтатьяРасходовНУ.Ссылка ЕСТЬ NULL
		|			ТОГДА СтатьяРасходовНУ.ВариантРаспределенияРасходовНУ = СтатьяРасходовНУ.ВариантРаспределенияРасходовРегл
		|				И (СтатьяРасходовНУ.ПравилоРаспределенияРасходовНУ = СтатьяРасходовНУ.ПравилоРаспределенияРасходовРегл
		|					ИЛИ СтатьяРасходовНУ.ВариантРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
		|						И СтатьяРасходовНУ.ПравилоРаспределенияРасходовНУ = ЗНАЧЕНИЕ(Справочник.ПравилаРаспределенияРасходов.ПустаяСсылка)
		|					ИЛИ СтатьяРасходовНУ.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
		|					ИЛИ СтатьяРасходовНУ.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства)
		|					ИЛИ НЕ СтатьяРасходовНУ.ВариантРаспределенияРасходовНУ В (
		|						ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаРасходыБудущихПериодов),
		|						ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)))
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ";
		
	Запрос.УстановитьПараметр("СчетУчетаПустаяСсылка", РеглУчетКлиентСервер.СсылкаПланСчетов());
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МакетыБазРаспределений = Новый Структура;
	МакетыБазРаспределений.Вставить("НаправлениеРаспределения",
		Перечисления.ТипыБазыРаспределенияРасходов.ПолучитьМакет("НаправлениеРаспределения"));
	МакетыБазРаспределений.Вставить("МатериальныеЗатраты",
		Перечисления.ТипыБазыРаспределенияРасходов.ПолучитьМакет("МатериальныеЗатраты"));
	МакетыБазРаспределений.Вставить("Продукция",
		Перечисления.ТипыБазыРаспределенияРасходов.ПолучитьМакет("Продукция"));
	МакетыБазРаспределений.Вставить("Трудозатраты",
		Перечисления.ТипыБазыРаспределенияРасходов.ПолучитьМакет("Трудозатраты"));
	МакетыБазРаспределений.Вставить("МатериальныеИТрудозатраты",
		Перечисления.ТипыБазыРаспределенияРасходов.ПолучитьМакет("МатериальныеИТрудозатраты"));
	МакетыБазРаспределений.Вставить("Товары",
		Перечисления.ТипыБазыРаспределенияРасходов.ПолучитьМакет("Товары"));
		
	ПолноеИмяОбъекта = "Документ.РаспределениеПрочихЗатрат";
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Для Каждого ОбновляемыйЭлемент Из ОбновляемыеДанные Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", ОбновляемыйЭлемент.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			Объект = ОбновляемыйЭлемент.Ссылка.ПолучитьОбъект(); // ДокументОбъект.РаспределениеПрочихЗатрат - 
			Если Объект = Неопределено Тогда
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ОбновляемыйЭлемент.Ссылка);
				ЗафиксироватьТранзакцию();
				Продолжить;
			КонецЕсли;

			Если Не ЗначениеЗаполнено(Объект.НазначениеНастройкиРаспределения) Тогда
				
				Объект.НазначениеНастройкиРаспределения = Перечисления.НазначениеПравилРаспределенияРасходов.РаспределениеСтатейРасходовПоЭтапамПроизводства;
				
				ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.СтатьяРасходов, "ВариантРаспределенияРасходовРегл, ВариантРаспределенияРасходовУпр");
				Объект.РегламентированныйУчет = 
					(ЗначенияРеквизитов.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты);
				Объект.УправленческийУчет = 
					(ЗначенияРеквизитов.ВариантРаспределенияРасходовУпр = Перечисления.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты);
			
			КонецЕсли;
			ЗатратыСервер.ОбработатьЗаполнениеНастроекКомпоновки(Объект, МакетыБазРаспределений);
			
			НастройкаСчетовУчетаСервер.ЗаполнитьНастройкиСчетовУчета(Объект);
			
			Если Объект.РегламентированныйУчет Тогда
				УстановитьПризнакиБУиНУ(Объект, "РегламентированныйУчет");
			КонецЕсли;
			
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект,,, РежимЗаписиДокумента.Запись);
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), ОбновляемыйЭлемент.Ссылка);
			
			ВызватьИсключение;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры
//-- НЕ УТ

#КонецОбласти

#Область РаспределениеНаФинансовыйРезультат

Процедура ПодготовитьСлужебныеТаблицы(Период, Знач Организации, Знач Запрос, ИсключитьТранспортныеРасходы = Ложь) Экспорт
	
	//++ НЕ УТ
	//++ Локализация
	Если Организации.Количество() = 0 Тогда
		
		ЗапросОрганизаций = Новый Запрос;
		ЗапросОрганизаций.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Организации.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Организации КАК Организации
			|ГДЕ
			|	Организации.Ссылка <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)";
		
		Организации = ЗапросОрганизаций.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		
	КонецЕсли;
	
	Если ИсключитьТранспортныеРасходы Тогда
		
		НалоговыйУчет.СоздатьПустуюВременнуюТаблицуДолиТранспортныхРасходов(Запрос.МенеджерВременныхТаблиц);
		
	Иначе
		УстановитьПривилегированныйРежим(Истина);
		НалоговыйУчет.ДоляТранспортныхРасходовТекущегоМесяца(Период, Организации, Запрос.МенеджерВременныхТаблиц);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	НалоговыйУчет.СоздатьВременнуюТаблицуДолиСписанияКосвенныхРасходов(
		Период, Организации, Запрос.МенеджерВременныхТаблиц);
	УстановитьПривилегированныйРежим(Ложь);
	//-- Локализация
	//-- НЕ УТ
	
	Если Не РасчетСебестоимостиПрикладныеАлгоритмы.ВременнаяТаблицаСуществует(Запрос.МенеджерВременныхТаблиц, "ВтДолиСписанияКосвенныхРасходов") Тогда
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
			|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
			|	ЛОЖЬ КАК НовыйАлгоритмСписания,
			|	ЛОЖЬ КАК Нормируемые,
			|	ЛОЖЬ КАК Транспортные,
			|	1 КАК ДоляСписания
			|ПОМЕСТИТЬ ВтДолиСписанияКосвенныхРасходов";
		Запрос.Выполнить();
	КонецЕсли;
	
	//++ НЕ УТ
	Если Не РасчетСебестоимостиПрикладныеАлгоритмы.ВременнаяТаблицаСуществует(Запрос.МенеджерВременныхТаблиц, "ВтОВЗ") Тогда
		Запрос.Текст = РасчетСебестоимостиПостатейныеЗатраты.ТекстВТпоОВЗ();
		УстановитьПривилегированныйРежим(Истина);
		Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;

	Если Не РасчетСебестоимостиПрикладныеАлгоритмы.ВременнаяТаблицаСуществует(Запрос.МенеджерВременныхТаблиц, "ЗначенияПоказателейОВЗ") Тогда
		Запрос.Текст = РасчетСебестоимостиПостатейныеЗатраты.ТекстЗначенияПоказателейРаспределенияНаОВЗ();
		УстановитьПривилегированныйРежим(Истина);
		Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	//-- НЕ УТ
	
	Если Не РасчетСебестоимостиПрикладныеАлгоритмы.ВременнаяТаблицаСуществует(Запрос.МенеджерВременныхТаблиц, "Регистраторы") Тогда
		ПодготовитьТаблицыКРасчету(Неопределено, Запрос);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область КорректировкаУстаревшихДокументов

Функция ЕстьУстаревшиеДокументыСРасходами(Запрос)
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	РаспределениеДоходовПоНаправлениямДеятельностиРасходы.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.РаспределениеДоходовПоНаправлениямДеятельности.Расходы КАК РаспределениеДоходовПоНаправлениямДеятельностиРасходы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеДоходовПоНаправлениямДеятельности КАК РаспределениеДоходовПоНаправлениямДеятельности
		|		ПО РаспределениеДоходовПоНаправлениямДеятельностиРасходы.Ссылка = РаспределениеДоходовПоНаправлениямДеятельности.Ссылка
		|ГДЕ
		|	РаспределениеДоходовПоНаправлениямДеятельности.Организация В(&МассивОрганизаций)
		|	И РаспределениеДоходовПоНаправлениямДеятельности.Проведен
		|	И РаспределениеДоходовПоНаправлениямДеятельности.Дата МЕЖДУ &НачалоПериода И &КонецПериода";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#Область ПоступилоПрочихРасходов

// Возвращает текст источника данных с заменой по шаблону.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ЗаменитьПоШаблону(ТекстЗапроса, Шаблон, Настройка) Экспорт

	ТекстЗапросаРезультат = ТекстЗапроса;
	Замены = Неопределено;
	Шаблон.Свойство(Настройка, Замены);
	Для Каждого Замена Из Замены Цикл
		ТекстЗапросаРезультат = СтрЗаменить(ТекстЗапросаРезультат, Замена.Ключ, Замена.Значение);
	КонецЦикла;
	Возврат ТекстЗапросаРезультат;
	
КонецФункции

// Возвращает текст подзапроса для получения источников данных о прочих расходах.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаПоступилоПрочихРасходовИсточники(ВариантРасчета, РежимОтладки = Ложь) Экспорт
	
	ШаблонЗамен = ШаблонЗамен();

	#Область ТекстОписаниеДанных
	ТекстОписаниеДанных = "
	|	ВЫБРАТЬ ПЕРВЫЕ 0
	|		"""" КАК ЗапросИсточник,
	|		ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|		ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)  КАК НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
	|		НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|		0 КАК Сумма,
	|		0 КАК СуммаБезНДС,
	|		0 КАК СуммаРегл,
	|		0 КАК ПостояннаяРазница,
	|		0 КАК ВременнаяРазница,
	|		0 КАК СуммаУпр,
	|		0 КАК СуммаРеглРасход,
	|		0 КАК ПостояннаяРазницаРасход,
	|		0 КАК ВременнаяРазницаРасход,
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДляСвязи,
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходовДляСвязи
	|";
	Если РежимОтладки Тогда
		ТекстОписаниеДанных	= ТекстОписаниеДанных + "
		|	ПОМЕСТИТЬ ВтИсточникиРасходов
		|";
	КонецЕсли;
	#КонецОбласти

	#Область ТекстОстаткиПрочихРасходов
	ТекстОстаткиПрочихРасходов = "
	|	ВЫБРАТЬ
	|		""ТекстОстаткиПрочихРасходов"",
	|		Расходы.Организация,
	|		Расходы.Подразделение,
	|		Расходы.НаправлениеДеятельности,
	|		Расходы.СтатьяРасходов,
	|		Расходы.АналитикаРасходов,
	|		Расходы.СуммаОстаток,
	|		Расходы.СуммаБезНДСОстаток,
	|		Расходы.СуммаРеглОстаток,
	|		Расходы.ПостояннаяРазницаОстаток,
	|		Расходы.ВременнаяРазницаОстаток,
	|		Расходы.СуммаУпрОстаток,
	|		0,
	|		0,
	|		0,
	|		Расходы.НаправлениеДеятельности,
	|		Расходы.СтатьяРасходов
	|	ИЗ
	|		РегистрНакопления.ПрочиеРасходы.Остатки(
	|				&ГраницаКонецПериода,
	|				(&ПоВсемОрганизациям
	|					ИЛИ Организация В (&МассивОрганизаций))
	|					И (&ПоВсемПодразделениям
	|						ИЛИ Подразделение В (&СписокПодразделений))
	|					И СтатьяРасходов В
	|						(ВЫБРАТЬ
	|							Т.Ссылка
	|						ИЗ
	|							СтатьиРасходовКРаспределению КАК Т)
	|	
	|		) КАК Расходы
	|";
	ТекстОстаткиПрочихРасходовУпр =		ЗаменитьПоШаблону(ТекстОстаткиПрочихРасходов, ШаблонЗамен, "ТекстОстаткиПрочихРасходовУпр");
	ТекстОстаткиПрочихРасходовРегл =	ЗаменитьПоШаблону(ТекстОстаткиПрочихРасходов, ШаблонЗамен, "ТекстОстаткиПрочихРасходовРегл");
	//++ НЕ УТ
	ТекстОстаткиПрочихРасходовНУ =		ЗаменитьПоШаблону(ТекстОстаткиПрочихРасходов, ШаблонЗамен, "ТекстОстаткиПрочихРасходовНУ");
	//-- НЕ УТ
	#КонецОбласти
	
	#Область ТекстДвиженияПрочихРасходов
	ТекстДвиженияПрочихРасходов = "
	|	ВЫБРАТЬ
	|		""ТекстДвиженияПрочихРасходов"",
	|		Движения.Организация,
	|		Движения.Подразделение,
	|		Движения.НаправлениеДеятельности,
	|		Движения.СтатьяРасходов,
	|		Движения.АналитикаРасходов,
	|		Движения.Сумма,
	|		Движения.СуммаБезНДС,
	|		Движения.СуммаРегл,
	|		Движения.ПостояннаяРазница,
	|		Движения.ВременнаяРазница,
	|		Движения.СуммаУпр,
	|		Движения.СуммаРегл,
	|		Движения.ПостояннаяРазница,
	|		Движения.ВременнаяРазница,
	|		ЕСТЬNULL(ДД.НаправлениеДеятельности, Движения.НаправлениеДеятельности),
	|		ЕСТЬNULL(ДД.СтатьяРасходов, Движения.СтатьяРасходов)
	|	ИЗ
	|		РегистрНакопления.ПрочиеРасходы КАК Движения
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтатьиРасходовКРаспределению КАК СтатьиКРаспределению
	|			ПО Движения.СтатьяРасходов = СтатьиКРаспределению.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК ДД
	|			ПО Движения.Регистратор = ДД.Ссылка 
	|				И ДД.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	ГДЕ
	|		Движения.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Движения.Активность
	|		И (ТИПЗНАЧЕНИЯ(Движения.Регистратор) В (
	|			ТИП(Документ.РасчетСебестоимостиТоваров), 
	|			ТИП(Документ.РаспределениеПрочихЗатрат), 
	|			ТИП(Документ.РаспределениеДоходовПоНаправлениямДеятельности))
	//++ НЕ УТ
	|			И НЕ Движения.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ТранзитРасходовМеждуОВЗ)
	//++ Локализация
	|			ИЛИ ТИПЗНАЧЕНИЯ(Движения.Регистратор) = ТИП(Документ.РегламентнаяОперация)
	|				И НЕ Движения.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеКосвенныхРасходов)
	//-- Локализация
	//-- НЕ УТ
	|		ИЛИ СтатьиКРаспределению.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|		)
	|		И Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И (&ПоВсемОрганизациям
	|			ИЛИ Движения.Организация В (&МассивОрганизаций))
	|		И (&ПоВсемПодразделениям
	|			ИЛИ Движения.Подразделение В (&СписокПодразделений))
	|";
	ТекстДвиженияПрочихРасходовУпр = ЗаменитьПоШаблону(ТекстДвиженияПрочихРасходов, ШаблонЗамен, "ТекстДвиженияПрочихРасходовУпр");
	ТекстДвиженияПрочихРасходовРегл = ЗаменитьПоШаблону(ТекстДвиженияПрочихРасходов, ШаблонЗамен, "ТекстДвиженияПрочихРасходовРегл");
	//++ НЕ УТ
	ТекстДвиженияПрочихРасходовНУ = ЗаменитьПоШаблону(ТекстДвиженияПрочихРасходов, ШаблонЗамен, "ТекстДвиженияПрочихРасходовНУ");
	//-- НЕ УТ
	#КонецОбласти
	
	//++ НЕ УТ
	#Область ТекстДвиженияПрочихРасходовВторичногоКонтура
	ТекстДвиженияПрочихРасходовВторичногоКонтура = "
	|	ВЫБРАТЬ
	|		""ТекстДвиженияПрочихРасходовВторичногоКонтура"",
	|		Движения.Организация,
	|		Движения.Подразделение,
	|		Движения.НаправлениеДеятельности,
	|		Движения.СтатьяРасходов,
	|		Движения.АналитикаРасходов,
	|		-Движения.Сумма,
	|		-Движения.СуммаБезНДС,
	|		-Движения.СуммаРегл,
	|		-Движения.ПостояннаяРазница,
	|		-Движения.ВременнаяРазница,
	|		-Движения.СуммаУпр,
	|		-Движения.СуммаРегл,
	|		-Движения.ПостояннаяРазница,
	|		-Движения.ВременнаяРазница,
	|		Движения.НаправлениеДеятельности,
	|		Движения.СтатьяРасходов
	|	ИЗ
	|		РегистрНакопления.ПрочиеРасходы КАК Движения
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтатьиРасходовКРаспределению КАК СтатьиКРаспределению
	|			ПО Движения.СтатьяРасходов = СтатьиКРаспределению.Ссылка
	|	ГДЕ
	|		Движения.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Движения.Активность
	|		И (ТИПЗНАЧЕНИЯ(Движения.Регистратор) В (
	|			ТИП(Документ.РасчетСебестоимостиТоваров), 
	|			ТИП(Документ.РаспределениеПрочихЗатрат), 
	|			ТИП(Документ.РаспределениеДоходовПоНаправлениямДеятельности))
	|		И СтатьиКРаспределению.ВариантРаспределенияРасходов В (
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты),
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства)
	|			)
	|		И СтатьиКРаспределению.Предопределенный
	|		)
	|		И Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И (&ПоВсемОрганизациям
	|			ИЛИ Движения.Организация В (&МассивОрганизаций))
	|		И (&ПоВсемПодразделениям
	|			ИЛИ Движения.Подразделение В (&СписокПодразделений))
	|";
	ТекстДвиженияПрочихРасходовВторичногоКонтураУпр = ЗаменитьПоШаблону(ТекстДвиженияПрочихРасходовВторичногоКонтура, ШаблонЗамен, "ТекстДвиженияПрочихРасходовУпр");
	ТекстДвиженияПрочихРасходовВторичногоКонтураРегл = ЗаменитьПоШаблону(ТекстДвиженияПрочихРасходовВторичногоКонтура, ШаблонЗамен, "ТекстДвиженияПрочихРасходовРегл");
	ТекстДвиженияПрочихРасходовВторичногоКонтураНУ = ЗаменитьПоШаблону(ТекстДвиженияПрочихРасходовВторичногоКонтура, ШаблонЗамен, "ТекстДвиженияПрочихРасходовНУ");
	#КонецОбласти
	
	#Область ТекстДвиженияНаСебестоимостьПроизводства
	ТекстДвиженияНаСебестоимостьПроизводства = "
	|	ВЫБРАТЬ
	|		""ТекстДвиженияНаСебестоимостьПроизводства"",
	|		Движения.Организация,
	|		Движения.Подразделение,
	|		Движения.НаправлениеДеятельности,
	|		Движения.СтатьяРасходов,
	|		Движения.АналитикаРасходов,
	|		Движения.Сумма,
	|		Движения.СуммаБезНДС,
	|		Движения.СуммаРегл,
	|		Движения.ПостояннаяРазница,
	|		Движения.ВременнаяРазница,
	|		Движения.СуммаУпр,
	|		Движения.СуммаРегл,
	|		Движения.ПостояннаяРазница,
	|		Движения.ВременнаяРазница,
	|		Движения.НаправлениеДеятельности,
	|		Движения.СтатьяРасходов
	|	ИЗ
	|		РегистрНакопления.ПрочиеРасходы КАК Движения
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтатьиРасходовКРаспределению КАК СтатьиКРаспределению
	|			ПО Движения.СтатьяРасходов = СтатьиКРаспределению.Ссылка
	|	ГДЕ
	|		Движения.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Движения.Активность
	|		И (ТИПЗНАЧЕНИЯ(Движения.Регистратор) В (
	//++ Локализация
	|			ТИП(Документ.ВыпускПродукции),
	//-- Локализация
	//++ НЕ УТКА
	|			ТИП(Документ.ЭтапПроизводства2_2),
	//-- НЕ УТКА
	|			ТИП(Документ.ПроизводствоБезЗаказа),
	|			ТИП(Документ.ОтчетПереработчика)
	|			)
	|		)
	|		И Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Движения.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовНаСебестоимостьПроизводства)
	|		И (&ПоВсемОрганизациям
	|			ИЛИ Организация В (&МассивОрганизаций))
	|		И (&ПоВсемПодразделениям
	|			ИЛИ Подразделение В (&СписокПодразделений))
	|";
	ТекстДвиженияНаСебестоимостьПроизводстваУпр = ЗаменитьПоШаблону(ТекстДвиженияНаСебестоимостьПроизводства, ШаблонЗамен, "ТекстДвиженияПрочихРасходовУпр");
	ТекстДвиженияНаСебестоимостьПроизводстваРегл = ЗаменитьПоШаблону(ТекстДвиженияНаСебестоимостьПроизводства, ШаблонЗамен, "ТекстДвиженияПрочихРасходовРегл");
	ТекстДвиженияНаСебестоимостьПроизводстваНУ = ЗаменитьПоШаблону(ТекстДвиженияНаСебестоимостьПроизводства, ШаблонЗамен, "ТекстДвиженияПрочихРасходовНУ");
	#КонецОбласти
	//-- НЕ УТ
	
	#Область ТекстСписаниеРаспределениеТМЦ
	ТекстСписаниеРаспределениеТМЦ = "
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""ТекстСписаниеРаспределениеТМЦ"",
	|		СебестоимостьТоваров.Организация,
	|		СебестоимостьТоваров.Подразделение,
	|		СебестоимостьТоваров.КорНаправлениеДеятельности,
	|		СебестоимостьТоваров.СтатьяРасходовСписания,
	|		СебестоимостьТоваров.АналитикаРасходов,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		СебестоимостьТоваров.КорНаправлениеДеятельности,
	|		СебестоимостьТоваров.СтатьяРасходовСписания
	|	ИЗ
	|		РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтатьиРасходовКРаспределению КАК Статьи
	|			ПО (Статьи.Ссылка = СебестоимостьТоваров.СтатьяРасходовСписания)
	|	ГДЕ
	|		СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И СебестоимостьТоваров.Активность
	|		И ТИПЗНАЧЕНИЯ(СебестоимостьТоваров.Регистратор) В (
	|			ТИП(Документ.ВнутреннееПотреблениеТоваров),
	//++ НЕ УТ
	//++ Устарело_Производство21
	|			ТИП(Документ.ВыпускПродукции),
	//-- Устарело_Производство21
	|			ТИП(Документ.РаспределениеПроизводственныхЗатрат),
	|			ТИП(Документ.ПроизводствоБезЗаказа),
	|			ТИП(Документ.ОтчетПереработчика),
	//-- НЕ УТ
	//++ НЕ УТКА
	|			ТИП(Документ.ЭтапПроизводства2_2),
	//-- НЕ УТКА
	|			ТИП(Документ.ПрочееОприходованиеТоваров),
	|			ТИП(Документ.СписаниеНедостачТоваров))
	|		И НЕ СебестоимостьТоваров.РасчетСебестоимости
	|		И (&ПоВсемОрганизациям
	|				ИЛИ СебестоимостьТоваров.Организация В (&МассивОрганизаций))
	|		И (&ПоВсемПодразделениям
	|				ИЛИ СебестоимостьТоваров.Подразделение В (&СписокПодразделений))
	|";
	#КонецОбласти
	
	#Область ТекстСписаниеРаспределениеТМЦвДругуюОрганизацию
	ТекстСписаниеРаспределениеТМЦвДругуюОрганизацию = "
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""ТекстСписаниеРаспределениеТМЦвДругуюОрганизацию"",
	|		СебестоимостьТоваров.КорОрганизация,
	|		СебестоимостьТоваров.Подразделение,
	|		СебестоимостьТоваров.КорНаправлениеДеятельности,
	|		СебестоимостьТоваров.СтатьяРасходовСписания,
	|		СебестоимостьТоваров.АналитикаРасходов,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		СебестоимостьТоваров.КорНаправлениеДеятельности,
	|		СебестоимостьТоваров.СтатьяРасходовСписания
	|	ИЗ
	|		РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтатьиРасходовКРаспределению КАК Статьи
	|			ПО (Статьи.Ссылка = СебестоимостьТоваров.СтатьяРасходовСписания)
	|	ГДЕ
	|		СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И СебестоимостьТоваров.Активность
	|		И ТИПЗНАЧЕНИЯ(СебестоимостьТоваров.Регистратор) В (
	|			ТИП(Документ.ВнутреннееПотреблениеТоваров),
	//++ НЕ УТ
	//++ Устарело_Производство21
	|			ТИП(Документ.ВыпускПродукции),
	//-- Устарело_Производство21
	|			ТИП(Документ.РаспределениеПроизводственныхЗатрат),
	|			ТИП(Документ.ПроизводствоБезЗаказа),
	|			ТИП(Документ.ОтчетПереработчика),
	//-- НЕ УТ
	//++ НЕ УТКА
	|			ТИП(Документ.ЭтапПроизводства2_2),
	//-- НЕ УТКА
	|			ТИП(Документ.ПрочееОприходованиеТоваров),
	|			ТИП(Документ.СписаниеНедостачТоваров))
	|		И НЕ СебестоимостьТоваров.РасчетСебестоимости
	|		И НЕ СебестоимостьТоваров.КорОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		И НЕ СебестоимостьТоваров.КорОрганизация = СебестоимостьТоваров.Организация
	|		И (&ПоВсемОрганизациям
	|				ИЛИ СебестоимостьТоваров.КорОрганизация В (&МассивОрганизаций))
	|		И (&ПоВсемПодразделениям
	|				ИЛИ СебестоимостьТоваров.Подразделение В (&СписокПодразделений))
	|";
	#КонецОбласти
	
	//++ НЕ УТ

	#Область ТекстРаспределениеМатериалыИРаботы
	ТекстРаспределениеМатериалыИРаботы = "
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""ТекстРаспределениеМатериалыИРаботы"",
	|		Затраты.Организация,
	|		Затраты.Подразделение,
	|		ЕСТЬNULL(Затраты.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
	|		ВЫРАЗИТЬ(Затраты.СтатьяРасходов КАК ПланВидовХарактеристик.СтатьиРасходов),
	|		Затраты.АналитикаРасходов,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		ЕСТЬNULL(Затраты.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
	|		ВЫРАЗИТЬ(Затраты.СтатьяРасходов КАК ПланВидовХарактеристик.СтатьиРасходов)
	|	ИЗ
	|		РегистрНакопления.МатериалыИРаботыВПроизводстве КАК Затраты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтатьиРасходовКРаспределению КАК Статьи
	|			ПО (Статьи.Ссылка = Затраты.СтатьяРасходов)
	|	ГДЕ
	|		Затраты.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Затраты.Активность
	|		И Затраты.Регистратор ССЫЛКА Документ.РаспределениеПроизводственныхЗатрат
	|		И (&ПоВсемОрганизациям
	|				ИЛИ Затраты.Организация В (&МассивОрганизаций))
	|		И (&ПоВсемПодразделениям
	|				ИЛИ Затраты.Подразделение В (&СписокПодразделений))
	|";
	#КонецОбласти
	
	#Область ТекстОстаткиПрочихРасходовОВЗ
	ТекстОстаткиПрочихРасходовОВЗ = "
	|	ВЫБРАТЬ 
	|		""ТекстОстаткиПрочихРасходовОВЗ"",
	|		Расходы.Организация,
	|		Расходы.Подразделение,
	|		Расходы.НаправлениеДеятельности,
	|		Расходы.СтатьяРасходов,
	|		Расходы.АналитикаРасходов,
	|		Расходы.СуммаОстаток,
	|		Расходы.СуммаБезНДСОстаток,
	|		Расходы.СуммаРеглОстаток,
	|		Расходы.ПостояннаяРазницаОстаток,
	|		Расходы.ВременнаяРазницаОстаток,
	|		Расходы.СуммаУпрОстаток,
	|		0,
	|		0,
	|		0,
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка),
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	ИЗ
	|		РегистрНакопления.ПрочиеРасходы.Остатки(
	|				&ГраницаКонецПериода,
	|				(&ПоВсемОрганизациям
	|					ИЛИ Организация В (&МассивОрганизаций))
	|					И (&ПоВсемПодразделениям
	|						ИЛИ Подразделение В (&СписокПодразделений))
	|					И АналитикаРасходов В
	|						(ВЫБРАТЬ
	|							Т.ОВЗ
	|						ИЗ
	|							ОВЗКРаспределению КАК Т)
	|		) КАК Расходы
	|";
	ТекстОстаткиПрочихРасходовОВЗУпр = ЗаменитьПоШаблону(ТекстОстаткиПрочихРасходовОВЗ, ШаблонЗамен, "ТекстОстаткиПрочихРасходовУпр");
	ТекстОстаткиПрочихРасходовОВЗРегл = ЗаменитьПоШаблону(ТекстОстаткиПрочихРасходовОВЗ, ШаблонЗамен, "ТекстОстаткиПрочихРасходовРегл");
	ТекстОстаткиПрочихРасходовОВЗНУ = ЗаменитьПоШаблону(ТекстОстаткиПрочихРасходовОВЗ, ШаблонЗамен, "ТекстОстаткиПрочихРасходовНУ");
	#КонецОбласти
	
	#Область ТекстДвиженияПрочихРасходовИзОВЗ
	ТекстДвиженияПрочихРасходовИзОВЗ = "
	|	ВЫБРАТЬ 
	|		""ТекстДвиженияПрочихРасходовИзОВЗ"",
	|		Движения.Организация,
	|		Движения.Подразделение,
	|		Движения.НаправлениеДеятельности,
	|		Движения.СтатьяРасходов,
	|		Движения.АналитикаРасходов,
	|		Движения.Сумма,
	|		Движения.СуммаБезНДС,
	|		Движения.СуммаРегл,
	|		Движения.ПостояннаяРазница,
	|		Движения.ВременнаяРазница,
	|		Движения.СуммаУпр,
	|		Движения.СуммаРегл,
	|		Движения.ПостояннаяРазница,
	|		Движения.ВременнаяРазница,
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка),
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	ИЗ
	|		РегистрНакопления.ПрочиеРасходы КАК Движения
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОВЗКРаспределению КАК ОВЗКРаспределению
	|			ПО Движения.АналитикаРасходов = ОВЗКРаспределению.ОВЗ
	|	ГДЕ
	|		Движения.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Движения.Активность
	|		И (ТИПЗНАЧЕНИЯ(Движения.Регистратор) В (
	|			ТИП(Документ.РаспределениеПрочихЗатрат), 
	|			ТИП(Документ.РаспределениеДоходовПоНаправлениямДеятельности))
	//++ Локализация
	|		ИЛИ ТИПЗНАЧЕНИЯ(Движения.Регистратор) = ТИП(Документ.РегламентнаяОперация)
	|			И НЕ Движения.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеКосвенныхРасходов)
	//-- Локализация
	|		)
	|		И Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И (&ПоВсемОрганизациям
	|			ИЛИ Организация В (&МассивОрганизаций))
	|		И (&ПоВсемПодразделениям
	|			ИЛИ Подразделение В (&СписокПодразделений))
	|";
	ТекстДвиженияПрочихРасходовИзОВЗУпр = ЗаменитьПоШаблону(ТекстДвиженияПрочихРасходовИзОВЗ, ШаблонЗамен, "ТекстДвиженияПрочихРасходовУпр");
	ТекстДвиженияПрочихРасходовИзОВЗРегл = ЗаменитьПоШаблону(ТекстДвиженияПрочихРасходовИзОВЗ, ШаблонЗамен, "ТекстДвиженияПрочихРасходовРегл");
	ТекстДвиженияПрочихРасходовИзОВЗНУ = ЗаменитьПоШаблону(ТекстДвиженияПрочихРасходовИзОВЗ, ШаблонЗамен, "ТекстДвиженияПрочихРасходовНУ");
	#КонецОбласти
	
	#Область ТекстСписаниеРаспределениеТМЦНаОВЗ
	ТекстСписаниеРаспределениеТМЦНаОВЗ = "
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ 
	|		""ТекстСписаниеРаспределениеТМЦНаОВЗ"",
	|		СебестоимостьТоваров.Организация,
	|		СебестоимостьТоваров.Подразделение,
	|		СебестоимостьТоваров.КорНаправлениеДеятельности,
	|		СебестоимостьТоваров.СтатьяРасходовСписания,
	|		СебестоимостьТоваров.АналитикаРасходов,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка),
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	ИЗ
	|		РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОВЗКРаспределению КАК ОВЗКРаспределению
	|			ПО СебестоимостьТоваров.АналитикаРасходов = ОВЗКРаспределению.ОВЗ
	|	ГДЕ
	|		СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И СебестоимостьТоваров.Активность
	|		И ТИПЗНАЧЕНИЯ(СебестоимостьТоваров.Регистратор) В (
	|			ТИП(Документ.ВнутреннееПотреблениеТоваров),
	|			ТИП(Документ.РаспределениеПроизводственныхЗатрат),
	|			ТИП(Документ.ПроизводствоБезЗаказа),
	|			ТИП(Документ.ОтчетПереработчика),
	//++ НЕ УТКА
	|			ТИП(Документ.ЭтапПроизводства2_2),
	//-- НЕ УТКА
	|			ТИП(Документ.ПрочееОприходованиеТоваров),
	|			ТИП(Документ.СписаниеНедостачТоваров))
	|		И НЕ СебестоимостьТоваров.РасчетСебестоимости
	|		И (&ПоВсемОрганизациям
	|				ИЛИ СебестоимостьТоваров.Организация В (&МассивОрганизаций))
	|		И (&ПоВсемПодразделениям
	|				ИЛИ СебестоимостьТоваров.Подразделение В (&СписокПодразделений))
	|";
	#КонецОбласти
	
	#Область ТекстРаспределениеМатериалыИРаботыНаОВЗ
	ТекстРаспределениеМатериалыИРаботыНаОВЗ = "
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ 
	|		""ТекстРаспределениеМатериалыИРаботыНаОВЗ"",
	|		Затраты.Организация,
	|		Затраты.Подразделение,
	|		ЕСТЬNULL(Затраты.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
	|		ВЫРАЗИТЬ(Затраты.СтатьяРасходов КАК ПланВидовХарактеристик.СтатьиРасходов),
	|		Затраты.АналитикаРасходов,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка),
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	ИЗ
	|		РегистрНакопления.МатериалыИРаботыВПроизводстве КАК Затраты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОВЗКРаспределению КАК ОВЗКРаспределению
	|			ПО Затраты.АналитикаРасходов = ОВЗКРаспределению.ОВЗ
	|	ГДЕ
	|		Затраты.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Затраты.Активность
	|		И Затраты.Регистратор ССЫЛКА Документ.РаспределениеПроизводственныхЗатрат
	|		И (&ПоВсемОрганизациям
	|				ИЛИ Затраты.Организация В (&МассивОрганизаций))
	|		И (&ПоВсемПодразделениям
	|				ИЛИ Затраты.Подразделение В (&СписокПодразделений))
	|";
	#КонецОбласти
	
	#Область ТекстДвиженияПрочихРасходовМеждуОВЗРасход
	ТекстДвиженияПрочихРасходовМеждуОВЗРасход = "
	|	ВЫБРАТЬ 
	|		""ТекстДвиженияПрочихРасходовМеждуОВЗРасход"",
	|		Движения.Организация,
	|		Движения.Подразделение,
	|		Движения.НаправлениеДеятельности,
	|		Движения.СтатьяРасходов,
	|		Движения.АналитикаРасходов,
	|		Движения.Сумма,
	|		Движения.СуммаБезНДС,
	|		Движения.СуммаРегл,
	|		Движения.ПостояннаяРазница,
	|		Движения.ВременнаяРазница,
	|		Движения.СуммаУпр,
	|		Движения.СуммаРегл,
	|		Движения.ПостояннаяРазница,
	|		Движения.ВременнаяРазница,
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка),
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	ИЗ
	|		РегистрНакопления.ПрочиеРасходы КАК Движения
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыВозникновенияЗатрат КАК ОВЗ
	|			ПО Движения.АналитикаРасходов = ОВЗ.Ссылка
	|	ГДЕ
	|		Движения.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Движения.Активность
	|		И Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ТИПЗНАЧЕНИЯ(Движения.Регистратор) = ТИП(Документ.РаспределениеПрочихЗатрат)
	|		И Движения.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ТранзитРасходовМеждуОВЗ)
	|		И (&ПоВсемОрганизациям
	|			ИЛИ Движения.Организация В (&МассивОрганизаций))
	|		И (&ПоВсемПодразделениям
	|			ИЛИ Движения.Подразделение В (&СписокПодразделений))
	|";
	#КонецОбласти
	
	#Область ТекстПрочихДвиженияРасходовМеждуОВЗПриход
	ТекстДвиженияПрочихРасходовМеждуОВЗПриход = "
	|	ВЫБРАТЬ 
	|		""ТекстДвиженияПрочихРасходовМеждуОВЗПриход"",
	|		Движения.Организация,
	|		Движения.Подразделение,
	|		Движения.НаправлениеДеятельности,
	|		Движения.СтатьяРасходов,
	|		Движения.АналитикаРасходов,
	|		-Движения.Сумма,
	|		-Движения.СуммаБезНДС,
	|		-Движения.СуммаРегл,
	|		-Движения.ПостояннаяРазница,
	|		-Движения.ВременнаяРазница,
	|		-Движения.СуммаУпр,
	|		-Движения.СуммаРегл,
	|		-Движения.ПостояннаяРазница,
	|		-Движения.ВременнаяРазница,
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка),
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	ИЗ
	|		РегистрНакопления.ПрочиеРасходы КАК Движения
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыВозникновенияЗатрат КАК ОВЗ
	|			ПО Движения.АналитикаРасходов = ОВЗ.Ссылка
	|	ГДЕ
	|		Движения.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Движения.Активность
	|		И Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И ТИПЗНАЧЕНИЯ(Движения.Регистратор) = ТИП(Документ.РаспределениеПрочихЗатрат)
	|		И Движения.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ТранзитРасходовМеждуОВЗ)
	|		И (&ПоВсемОрганизациям
	|			ИЛИ Движения.Организация В (&МассивОрганизаций))
	|		И (&ПоВсемПодразделениям
	|			ИЛИ Движения.Подразделение В (&СписокПодразделений))
	|";
	#КонецОбласти
	
	#Область ТекстТранзитРасходовМеждуОВЗпоГрафу
	ТекстТранзитРасходовМеждуОВЗпоГрафу = "
	|	 ВЫБРАТЬ 
	|		""ТекстТранзитРасходовМеждуОВЗпоГрафу"",
	|	 	Расходы.Организация,
	|	 	Расходы.КорПодразделение,
	|	 	Расходы.НаправлениеДеятельности,
	|	 	Расходы.СтатьяРасходов,
	|	 	Расходы.КорАналитикаРасходов,
	|	 	0,
	|	 	0,
	|	 	0,
	|	 	0,
	|	 	0,
	|	 	0,
	|	 	0,
	|	 	0,
	|	 	0,
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка),
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	 ИЗ
	|	 	ВтТранзитРасходовМеждуОВЗ КАК Расходы
	|	 		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОВЗКРаспределению КАК ОВЗКРаспределению
	|	 			ПО Расходы.КорАналитикаРасходов = ОВЗКРаспределению.ОВЗ
	|";
	#КонецОбласти
	
	//-- НЕ УТ

	ТекстыЗапроса = Новый Массив;
	ТекстыЗапроса.Добавить(ТекстОписаниеДанных);
	
	ТекстыЗапроса.Добавить(ТекстОстаткиПрочихРасходовУпр);
	ТекстыЗапроса.Добавить(ТекстОстаткиПрочихРасходовРегл);
	//++ НЕ УТ
	ТекстыЗапроса.Добавить(ТекстОстаткиПрочихРасходовНУ);
	//-- НЕ УТ

	ТекстыЗапроса.Добавить(ТекстДвиженияПрочихРасходовУпр);
	ТекстыЗапроса.Добавить(ТекстДвиженияПрочихРасходовРегл);
	//++ НЕ УТ
	ТекстыЗапроса.Добавить(ТекстДвиженияПрочихРасходовНУ);
	//-- НЕ УТ

	ТекстыЗапроса.Добавить(ТекстСписаниеРаспределениеТМЦ);
	ТекстыЗапроса.Добавить(ТекстСписаниеРаспределениеТМЦвДругуюОрганизацию);

	//++ НЕ УТ
	ТекстыЗапроса.Добавить(ТекстДвиженияНаСебестоимостьПроизводстваУпр);
	ТекстыЗапроса.Добавить(ТекстДвиженияНаСебестоимостьПроизводстваРегл);
	ТекстыЗапроса.Добавить(ТекстДвиженияНаСебестоимостьПроизводстваНУ);
	ТекстыЗапроса.Добавить(ТекстРаспределениеМатериалыИРаботы);
	ТекстыЗапроса.Добавить(ТекстДвиженияПрочихРасходовВторичногоКонтураУпр);
	ТекстыЗапроса.Добавить(ТекстДвиженияПрочихРасходовВторичногоКонтураРегл);
	ТекстыЗапроса.Добавить(ТекстДвиженияПрочихРасходовВторичногоКонтураНУ);

	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОбъектамВозникновенияЗатрат") Тогда
		ТекстыЗапроса.Добавить(ТекстОстаткиПрочихРасходовОВЗУпр);
		ТекстыЗапроса.Добавить(ТекстОстаткиПрочихРасходовОВЗРегл);
		ТекстыЗапроса.Добавить(ТекстОстаткиПрочихРасходовОВЗНУ);
		ТекстыЗапроса.Добавить(ТекстДвиженияПрочихРасходовИзОВЗУпр);
		ТекстыЗапроса.Добавить(ТекстДвиженияПрочихРасходовИзОВЗРегл);
		ТекстыЗапроса.Добавить(ТекстДвиженияПрочихРасходовИзОВЗНУ);
		ТекстыЗапроса.Добавить(ТекстСписаниеРаспределениеТМЦНаОВЗ);
		ТекстыЗапроса.Добавить(ТекстРаспределениеМатериалыИРаботыНаОВЗ);
		ТекстыЗапроса.Добавить(ТекстДвиженияПрочихРасходовМеждуОВЗРасход);
		ТекстыЗапроса.Добавить(ТекстДвиженияПрочихРасходовМеждуОВЗПриход);
		Если ВариантРасчета = "ФактическийРасчет" Тогда
			ТекстыЗапроса.Добавить(ТекстТранзитРасходовМеждуОВЗпоГрафу);
		КонецЕсли;
		
	КонецЕсли;
	//-- НЕ УТ
	
	Возврат СтрСоединить(ТекстыЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

Функция ШаблонЗамен()
	
	СтруктураВозврата = Новый Структура;
	
	СтатьиРасходовКРаспределениюУпр = Новый Соответствие;
	СтатьиРасходовКРаспределениюУпр.Вставить("СтатьиРасходовКРаспределению", "СтатьиРасходовКРаспределениюУпр");
	СтруктураВозврата.Вставить("СтатьиРасходовКРаспределениюУпр", СтатьиРасходовКРаспределениюУпр);
	
	СтатьиРасходовКРаспределениюРегл = Новый Соответствие;
	СтатьиРасходовКРаспределениюРегл.Вставить("СтатьиРасходовКРаспределению", "СтатьиРасходовКРаспределениюРегл");
	СтруктураВозврата.Вставить("СтатьиРасходовКРаспределениюРегл", СтатьиРасходовКРаспределениюРегл);
	
	СтатьиРасходовКРаспределениюНУ = Новый Соответствие;
	СтатьиРасходовКРаспределениюНУ.Вставить("СтатьиРасходовКРаспределению", "СтатьиРасходовКРаспределениюРегл");
	СтруктураВозврата.Вставить("СтатьиРасходовКРаспределениюРегл", СтатьиРасходовКРаспределениюРегл);
	
	ТекстОстаткиПрочихРасходовУпр = Новый Соответствие;
	ТекстОстаткиПрочихРасходовУпр.Вставить("Расходы.СуммаРеглОстаток,", "0,");
	ТекстОстаткиПрочихРасходовУпр.Вставить("Расходы.ПостояннаяРазницаОстаток,", "0,");
	ТекстОстаткиПрочихРасходовУпр.Вставить("Расходы.ВременнаяРазницаОстаток,", "0,");
	ТекстОстаткиПрочихРасходовУпр.Вставить("СтатьиРасходовКРаспределению", "СтатьиРасходовКРаспределениюУпр");
	ТекстОстаткиПрочихРасходовУпр.Вставить("ОВЗКРаспределению", "ОВЗКРаспределениюУпр");
	СтруктураВозврата.Вставить("ТекстОстаткиПрочихРасходовУпр", ТекстОстаткиПрочихРасходовУпр);
	
	ТекстОстаткиПрочихРасходовРегл = Новый Соответствие;
	ТекстОстаткиПрочихРасходовРегл.Вставить("Расходы.СуммаОстаток,", "0,");
	ТекстОстаткиПрочихРасходовРегл.Вставить("Расходы.СуммаБезНДСОстаток,", "0,");
	ТекстОстаткиПрочихРасходовРегл.Вставить("Расходы.СуммаУпрОстаток,", "0,");
	ТекстОстаткиПрочихРасходовРегл.Вставить("Расходы.ПостояннаяРазницаОстаток,", "0,");
	ТекстОстаткиПрочихРасходовРегл.Вставить("Расходы.ВременнаяРазницаОстаток,", "Расходы.СуммаРеглОстаток ,"); // пробел перед запятой для исключения замены
	ТекстОстаткиПрочихРасходовРегл.Вставить("СтатьиРасходовКРаспределению", "СтатьиРасходовКРаспределениюРегл");
	ТекстОстаткиПрочихРасходовРегл.Вставить("ОВЗКРаспределению", "ОВЗКРаспределениюРегл");
	СтруктураВозврата.Вставить("ТекстОстаткиПрочихРасходовРегл", ТекстОстаткиПрочихРасходовРегл);
	
	ТекстОстаткиПрочихРасходовНУ = Новый Соответствие;
	ТекстОстаткиПрочихРасходовНУ.Вставить("Расходы.СуммаОстаток,", "0,");
	ТекстОстаткиПрочихРасходовНУ.Вставить("Расходы.СуммаБезНДСОстаток,", "0,");
	ТекстОстаткиПрочихРасходовНУ.Вставить("Расходы.СуммаУпрОстаток,", "0,");
	ТекстОстаткиПрочихРасходовНУ.Вставить("Расходы.СуммаРеглОстаток,", "0,");
	ТекстОстаткиПрочихРасходовНУ.Вставить("Расходы.ВременнаяРазницаОстаток,", "Расходы.ВременнаяРазницаОстаток - Расходы.СуммаРеглОстаток ,"); // пробел перед запятой для исключения замены
	ТекстОстаткиПрочихРасходовНУ.Вставить("СтатьиРасходовКРаспределению", "СтатьиРасходовКРаспределениюНУ");
	ТекстОстаткиПрочихРасходовНУ.Вставить("ОВЗКРаспределению", "ОВЗКРаспределениюНУ");
	СтруктураВозврата.Вставить("ТекстОстаткиПрочихРасходовНУ", ТекстОстаткиПрочихРасходовНУ);
	
	ТекстДвиженияПрочихРасходовУпр = Новый Соответствие;
	ТекстДвиженияПрочихРасходовУпр.Вставить("Движения.СуммаРегл,", "0,");
	ТекстДвиженияПрочихРасходовУпр.Вставить("Движения.ПостояннаяРазница,", "0,");
	ТекстДвиженияПрочихРасходовУпр.Вставить("Движения.ВременнаяРазница,", "0,");
	ТекстДвиженияПрочихРасходовУпр.Вставить("СтатьиРасходовКРаспределению", "СтатьиРасходовКРаспределениюУпр");
	ТекстДвиженияПрочихРасходовУпр.Вставить("ОВЗКРаспределению", "ОВЗКРаспределениюУпр");
	СтруктураВозврата.Вставить("ТекстДвиженияПрочихРасходовУпр", ТекстДвиженияПрочихРасходовУпр);
	
	ТекстДвиженияПрочихРасходовРегл = Новый Соответствие;
	ТекстДвиженияПрочихРасходовРегл.Вставить("Движения.Сумма,", "0,");
	ТекстДвиженияПрочихРасходовРегл.Вставить("Движения.СуммаБезНДС,", "0,");
	ТекстДвиженияПрочихРасходовРегл.Вставить("Движения.СуммаУпр,", "0,");
	ТекстДвиженияПрочихРасходовРегл.Вставить("Движения.ПостояннаяРазница,", "0,");
	ТекстДвиженияПрочихРасходовРегл.Вставить("Движения.ВременнаяРазница,", "Движения.СуммаРегл ,"); // пробел перед запятой для исключения замены
	ТекстДвиженияПрочихРасходовРегл.Вставить("СтатьиРасходовКРаспределению", "СтатьиРасходовКРаспределениюРегл");
	ТекстДвиженияПрочихРасходовРегл.Вставить("ОВЗКРаспределению", "ОВЗКРаспределениюРегл");
	СтруктураВозврата.Вставить("ТекстДвиженияПрочихРасходовРегл", ТекстДвиженияПрочихРасходовРегл);
	
	ТекстДвиженияПрочихРасходовНУ = Новый Соответствие;
	ТекстДвиженияПрочихРасходовНУ.Вставить("Движения.Сумма,", "0,");
	ТекстДвиженияПрочихРасходовНУ.Вставить("Движения.СуммаБезНДС,", "0,");
	ТекстДвиженияПрочихРасходовНУ.Вставить("Движения.СуммаУпр,", "0,");
	ТекстДвиженияПрочихРасходовНУ.Вставить("Движения.СуммаРегл,", "0,");
	ТекстДвиженияПрочихРасходовНУ.Вставить("Движения.ВременнаяРазница,", "Движения.ВременнаяРазница - Движения.СуммаРегл ,"); // пробел перед запятой для исключения замены 
	ТекстДвиженияПрочихРасходовНУ.Вставить("СтатьиРасходовКРаспределению", "СтатьиРасходовКРаспределениюНУ");
	ТекстДвиженияПрочихРасходовНУ.Вставить("ОВЗКРаспределению", "ОВЗКРаспределениюНУ");
	СтруктураВозврата.Вставить("ТекстДвиженияПрочихРасходовНУ", ТекстДвиженияПрочихРасходовНУ);

	Возврат СтруктураВозврата
	
КонецФункции

#КонецОбласти

#Область ФормированиеДокументов

Функция ПодобратьПодходящийДокумент(ДокументыКПроведению, ВидУчета, ПараметрыЗаполнения)
	
	ПодходящийДокумент = Неопределено;
	
	Для Каждого ДокументРаспределения Из ДокументыКПроведению Цикл
		
		ДокументНайден = Ложь;
		
		Если ВидУчета = "УправленческийУчет" Тогда
			Если ДокументРаспределения.РегламентированныйУчет Тогда
				ДокументНайден = ПроверитьСоответствиеНастроекУпрРегл(ПараметрыЗаполнения);
			//++ НЕ УТ
			//++ Локализация
			ИначеЕсли ДокументРаспределения.НалоговыйУчет Тогда
				ДокументНайден = ПроверитьСоответствиеНастроекУпрНал(ПараметрыЗаполнения);
			//-- Локализация
			//-- НЕ УТ
			КонецЕсли;
		ИначеЕсли ВидУчета = "РегламентированныйУчет" Тогда
			Если ДокументРаспределения.УправленческийУчет Тогда
				ДокументНайден = ПроверитьСоответствиеНастроекУпрРегл(ПараметрыЗаполнения);
			//++ НЕ УТ
			//++ Локализация
			ИначеЕсли ДокументРаспределения.НалоговыйУчет Тогда
				ДокументНайден = ПроверитьСоответствиеНастроекРеглНал(ПараметрыЗаполнения);
			//-- Локализация
			//-- НЕ УТ
			КонецЕсли;	
		//++ НЕ УТ
		//++ Локализация
		ИначеЕсли ВидУчета = "НалоговыйУчет" Тогда
			Если ДокументРаспределения.РегламентированныйУчет Тогда
				ДокументНайден = ПроверитьСоответствиеНастроекРеглНал(ПараметрыЗаполнения);
			ИначеЕсли ДокументРаспределения.УправленческийУчет Тогда
				ДокументНайден = ПроверитьСоответствиеНастроекУпрНал(ПараметрыЗаполнения);
			КонецЕсли;
		//-- Локализация
		//-- НЕ УТ	
		КонецЕсли;
		
		Если ДокументНайден Тогда
			ПодходящийДокумент = ДокументРаспределения;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ПодходящийДокумент;
	
КонецФункции

Функция ПроверитьСоответствиеНастроекУпрРегл(ПараметрыЗаполнения)
	
	НастройкиСоответствуют = 
		Не РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии22(ПараметрыЗаполнения.Дата)
		И ПараметрыЗаполнения.ВариантРаспределенияРегл = ПараметрыЗаполнения.ВариантРаспределенияУпр
		И ПараметрыЗаполнения.ВариантРаспределенияРегл = Перечисления.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты
			ИЛИ
		ПараметрыЗаполнения.ВариантРаспределенияРегл = ПараметрыЗаполнения.ВариантРаспределенияУпр
		И ПараметрыЗаполнения.ПравилоРаспределенияРегл = ПараметрыЗаполнения.ПравилоРаспределенияУпр
		И ПараметрыЗаполнения.ДокументУпр = ПараметрыЗаполнения.ДокументРегл;
		
	Возврат НастройкиСоответствуют;
	
КонецФункции

//++ НЕ УТ
//++ Локализация
		
Функция ПроверитьСоответствиеНастроекУпрНал(ПараметрыЗаполнения)
	
	НастройкиСоответствуют = 
		Не РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии22(ПараметрыЗаполнения.Дата)
		И ПараметрыЗаполнения.ВариантРаспределенияНал = ПараметрыЗаполнения.ВариантРаспределенияУпр
		И ПараметрыЗаполнения.ВариантРаспределенияНал = Перечисления.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты
			ИЛИ
		ПараметрыЗаполнения.ВариантРаспределенияНал = ПараметрыЗаполнения.ВариантРаспределенияУпр
		И (ПараметрыЗаполнения.ПравилоРаспределенияНал = ПараметрыЗаполнения.ПравилоРаспределенияУпр
			ИЛИ НЕ Перечисления.ВариантыРаспределенияРасходов.ВариантИспользуетПравилоРаспределенияНУ(ПараметрыЗаполнения.ВариантРаспределенияНал))
		И ПараметрыЗаполнения.ДокументУпр = ПараметрыЗаполнения.ДокументНал;
		
	Возврат НастройкиСоответствуют;
	
КонецФункции
		
Функция ПроверитьСоответствиеНастроекРеглНал(ПараметрыЗаполнения)
	
	НастройкиСоответствуют = 
		Не РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии22(ПараметрыЗаполнения.Дата)
		И ПараметрыЗаполнения.ВариантРаспределенияНал = ПараметрыЗаполнения.ВариантРаспределенияРегл
		И ПараметрыЗаполнения.ВариантРаспределенияНал = Перечисления.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты
		ИЛИ	ПараметрыЗаполнения.ВариантРаспределенияНал = ПараметрыЗаполнения.ВариантРаспределенияРегл
		И (ПараметрыЗаполнения.ПравилоРаспределенияНал = ПараметрыЗаполнения.ПравилоРаспределенияРегл
			ИЛИ НЕ Перечисления.ВариантыРаспределенияРасходов.ВариантИспользуетПравилоРаспределенияНУ(ПараметрыЗаполнения.ВариантРаспределенияНал)
			ИЛИ ПараметрыЗаполнения.ВариантРаспределенияРегл = Перечисления.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров
			ИЛИ ПараметрыЗаполнения.ВариантРаспределенияРегл = Перечисления.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства)
		И ПараметрыЗаполнения.ДокументРегл = ПараметрыЗаполнения.ДокументНал;
		
	Возврат НастройкиСоответствуют;
	
КонецФункции

//-- Локализация
//-- НЕ УТ
	
Функция ПолучитьНастройкиРаспределенияПоДокументу(ДокументРаспределения)
	
	ИсточникВариантовРаспределения = ДокументРаспределения.СтатьяРасходов;
	
	Если ДокументРаспределения.НазначениеНастройкиРаспределения = 
		ПредопределенноеЗначение("Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеРасходовНаОВЗ") 
		Тогда
		ИсточникВариантовРаспределения = ДокументРаспределения.АналитикаРасходов;
	КонецЕсли;
	
	НастройкиРаспределения = ОбщегоНазначенияУТВызовСервера.ЗначенияРеквизитовОбъекта(ИсточникВариантовРаспределения, 
		"ВариантРаспределенияРасходовУпр,ВариантРаспределенияРасходовРегл,ПравилоРаспределенияРасходовУпр,ПравилоРаспределенияРасходовРегл
		//++ НЕ УТ
		//++ Локализация
		|,ВариантРаспределенияРасходовНУ,ПравилоРаспределенияРасходовНУ
		//-- Локализация
		//-- НЕ УТ
		|");	
		
	Возврат НастройкиРаспределения;
	
КонецФункции
	
#КонецОбласти

#КонецОбласти

#КонецЕсли
