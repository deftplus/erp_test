// Обновляет отображаемые данные на текущей форме.
&НаСервере
Процедура ОбновитьДанныеФормы()
	// Отбор в списке.
	Список.Отбор.Элементы.Очистить();
	Если ЗначениеЗаполнено(ОтборШаблонПроцесса) Тогда
		ОбщегоНазначенияКлиентСерверУХ.УстановитьЭлементОтбора(Список.Отбор,"ШаблонПроцесса", ОтборШаблонПроцесса, ВидСравненияКомпоновкиДанных.Равно, , Истина);	
	Иначе
		// Не устанавливаем отбор по шаблону процесса.
	КонецЕсли;
	Если ЗначениеЗаполнено(ОтборСостояниеПроцесса) Тогда
		ОбщегоНазначенияКлиентСерверУХ.УстановитьЭлементОтбора(Список.Отбор,"СостояниеВыполнения", ОтборСостояниеПроцесса, ВидСравненияКомпоновкиДанных.Равно, , Истина);	
	Иначе
		// Не устанавливаем отбор по состоянию процесса.
	КонецЕсли;
	Если ЗначениеЗаполнено(ОтборВидОбъекта) Тогда
		ОбщегоНазначенияКлиентСерверУХ.УстановитьЭлементОтбора(Список.Отбор,"ТипОбъектаСогласования", ОтборВидОбъекта, ВидСравненияКомпоновкиДанных.Равно, , Истина);	
	Иначе
		// Не устанавливаем отбор по виду объекта согласования.
	КонецЕсли;
	// Обновление списка.
	Элементы.Список.Обновить();	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьСогласование(Команда)
	ТекДанные = Элементы.Список.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		ОбъектСсылка = ТекДанные.КлючевойОбъектПроцесса;
		МодульУправленияПроцессамиУХ.ОтменитьСогласование(, ОбъектСсылка);
		ОбновитьДанныеФормы();
	Иначе
		// Строка не выбрана. Ничего не делаем.
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанные(Команда)
	ОбновитьДанныеФормы();
КонецПроцедуры

&НаКлиенте
Процедура ОтборШаблонПроцессаПриИзменении(Элемент)
	ОбновитьДанныеФормы();
КонецПроцедуры

&НаКлиенте
Процедура ОтборСостояниеПроцессаПриИзменении(Элемент)
	ОбновитьДанныеФормы();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// Параметр Шаблон процесса.
	Если ЗначениеЗаполнено(Параметры.ШаблонПроцесса) Тогда
		ОтборШаблонПроцесса = Параметры.ШаблонПроцесса;
	Иначе
		ОтборШаблонПроцесса = Справочники.ШаблоныПроцессов.ПустаяСсылка();
	КонецЕсли;
	// Параметр Состояние процесса.
	Если ЗначениеЗаполнено(Параметры.СостояниеПроцесса) Тогда
		ОтборСостояниеПроцесса = Параметры.СостояниеПроцесса;
	Иначе
		ОтборСостояниеПроцесса = Перечисления.СостоянияПроцесса.ПустаяСсылка();
	КонецЕсли;
	// Обновление формы.
	ОбновитьДанныеФормы();
КонецПроцедуры

&НаКлиенте
Процедура ХодПроцесса(Команда)
	ТекДанные = Элементы.Список.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		ОбъектСсылка = ТекДанные.КлючевойОбъектПроцесса;
		Согласующий = ТекДанные.ШаблонПроцесса;
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("СогласуемыйОбъект", ОбъектСсылка);
		ПараметрыФормы.Вставить("ШаблонПроцесса", Согласующий);
		ПараметрыФормы.Вставить("Режим", "УправлениеСогласованием");
		ОткрытьФорму("Обработка.КонсольУправленияПроцессом.Форма.Форма", ПараметрыФормы);
	Иначе
		// Строка не выбрана. Ничего не делаем.
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИсторияСогаласования(Команда)
	ТекДанные = Элементы.Список.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		ОбъектСсылка = ТекДанные.КлючевойОбъектПроцесса;
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("КлючевойОбъектПроцесса", ОбъектСсылка);
		ОткрытьФорму("Отчет.ИсторияСогласования.Форма.ФормаОтчета", ПараметрыФормы);
	Иначе
		// Строка не выбрана. Ничего не делаем.
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКлючевойОбъект(Команда)
	ТекДанные = Элементы.Список.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		ЗначениеКлючевойОбъект = ТекДанные.КлючевойОбъектПроцесса;
		ПоказатьЗначение(, ЗначениеКлючевойОбъект);
	Иначе
		// Строка не выбрана. Ничего не делаем.
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтборКлючевойОбъектПриИзменении(Элемент)
	ОбновитьДанныеФормы();
КонецПроцедуры
