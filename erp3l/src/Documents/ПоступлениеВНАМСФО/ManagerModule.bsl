
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти
	
#Область ПодготовкаПараметровПроведенияДокумента

Функция ПодготовитьПараметрыПроведения(ДополнительныеСвойства, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;	
	Запрос.УстановитьПараметр("Ссылка", ДополнительныеСвойства.ДляПроведения.Ссылка);
	
	Реквизиты = МСФОУХ.РеквизитыДокумента(Запрос, "РежимЗаполнения",, Отказ);
	Реквизиты.Вставить("КэшВидыУчета",			УчетМСФОПовтИспУХ.ПолучитьВидыУчетаРежимаЗаполнения(Реквизиты.РежимЗаполнения));
	ДополнительныеСвойства.ДляПроведения.Вставить("Реквизиты", Реквизиты);
	Если Отказ Тогда
		Возврат ДополнительныеСвойства;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Период",			Реквизиты.Период);
	Запрос.УстановитьПараметр("ДатаГраница",	Новый Граница(Реквизиты.Период, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация",	Реквизиты.Организация);
	Запрос.УстановитьПараметр("Сценарий",		Реквизиты.Сценарий);
		
	Запрос.УстановитьПараметр("ЕстьНСБУ", 						Реквизиты.КэшВидыУчета.ЕстьНСБУ);
	Запрос.УстановитьПараметр("ЕстьМСФО", 						Реквизиты.КэшВидыУчета.ЕстьМСФО);
	Запрос.УстановитьПараметр("ВидУчетаНСБУ", 					Реквизиты.КэшВидыУчета.НСБУ);
	Запрос.УстановитьПараметр("ВидУчетаМСФО", 					Реквизиты.КэшВидыУчета.МСФО);
	Запрос.УстановитьПараметр("ФормироватьПроводкиМСФО", 		Реквизиты.ФормироватьПроводкиМСФО);
	Запрос.УстановитьПараметр("ФункциональнаяВалюта",			Реквизиты.ФункциональнаяВалюта);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаТаблицаВНА(НомераТаблиц) + " ; " + ТекстЗапроса_ТаблицаОшибок(НомераТаблиц);
	ПроведениеСерверУХ.ДополнитьТаблицамиИзПакетаЗапросов(ДополнительныеСвойства.ТаблицыДляДвижений, Запрос, НомераТаблиц);
	
КонецФункции

Функция ТекстЗапросаТаблицаВНА(НомераТаблиц)

	НомераТаблиц.Вставить("втТаблицаВНА", 			НомераТаблиц.Количество());	
	НомераТаблиц.Вставить("втПараметрыУчетаВНА",	НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаВНА", 			НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаПроводки", 		НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	ТаблицаВНА.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыУчета.МСФО) КАК ВидУчета,
	|	ТаблицаВНА.ВНА КАК ВНА,
	|	ТаблицаВНА.Ссылка КАК Ссылка,
	|	ТаблицаВНА.ГруппаВНАМСФО КАК ГруппаВНА,
	|	ТаблицаВНА.КоличествоНСБУ КАК КоличествоНСБУ,
	|	ТаблицаВНА.КоличествоМСФО КАК КоличествоМСФО,
	|	ТаблицаВНА.СуммаНСБУ КАК СуммаНСБУ,
	|	ТаблицаВНА.КоррСчетМСФО КАК КоррСчетМСФО,
	|	ТаблицаВНА.КоррСчетСубконто1МСФО КАК КоррСчетСубконто1МСФО,
	|	ТаблицаВНА.КоррСчетСубконто2МСФО КАК КоррСчетСубконто2МСФО,
	|	ТаблицаВНА.КоррСчетСубконто3МСФО КАК КоррСчетСубконто3МСФО,
	|	ТаблицаВНА.СчетУчетаМСФО КАК СчетУчетаМСФО,
	|	ТаблицаВНА.СчетУчетаСубконто1МСФО КАК СчетУчетаСубконто1МСФО,
	|	ТаблицаВНА.СчетУчетаСубконто2МСФО КАК СчетУчетаСубконто2МСФО,
	|	ТаблицаВНА.СчетУчетаСубконто3МСФО КАК СчетУчетаСубконто3МСФО,
	|	ТаблицаВНА.СуммаМСФО КАК СуммаМСФО,
	|	ТаблицаВНА.СпособПоступленияВНА КАК СпособПоступленияВНА,
	|	ВЫБОР
	|		КОГДА ТаблицаВНА.Валюта <> ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|				И ТаблицаВНА.КоррСчетМСФО.Валютный
	|			ТОГДА ТаблицаВНА.СуммаВВалютеНСБУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВВалютеНСБУ,
	|	ВЫБОР
	|		КОГДА ТаблицаВНА.Валюта <> ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|				И ТаблицаВНА.КоррСчетМСФО.Валютный
	|			ТОГДА ТаблицаВНА.СуммаВВалютеМСФО
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВВалютеМСФО,
	|	ТаблицаВНА.Валюта КАК Валюта,
	|	ТаблицаВНА.СторнироватьДанныеНСБУ КАК СторнироватьДанныеНСБУ,
	|	ТаблицаВНА.Подразделение КАК Подразделение,
	|	ТаблицаВНА.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ втТаблицаВНА
	|ИЗ
	|	Документ.ПоступлениеВНАМСФО.ВНА КАК ТаблицаВНА
	|ГДЕ
	|	ТаблицаВНА.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТаблицаВНА.ВНА КАК ВНА,
	|	&Период КАК ДатаСобытияНСБУ,
	|	&Период КАК ДатаСобытияМСФО,
	|	МАКСИМУМ(втТаблицаВНА.ГруппаВНА) КАК ГруппаВНА,
	|	СУММА(втТаблицаВНА.СуммаНСБУ) КАК СуммаНСБУ,
	|	СУММА(втТаблицаВНА.КоличествоНСБУ) КАК КоличествоНСБУ,
	|	МАКСИМУМ(втТаблицаВНА.СчетУчетаМСФО) КАК СчетУчетаМСФО,
	|	МАКСИМУМ(втТаблицаВНА.СчетУчетаСубконто2МСФО) КАК СчетУчетаСубконто2МСФО,
	|	МАКСИМУМ(втТаблицаВНА.СчетУчетаСубконто3МСФО) КАК СчетУчетаСубконто3МСФО,
	|	СУММА(втТаблицаВНА.СуммаМСФО) КАК СуммаМСФО,
	|	СУММА(втТаблицаВНА.КоличествоМСФО) КАК КоличествоМСФО,
	|	МАКСИМУМ(втТаблицаВНА.ГруппаВНА) КАК ГруппаВНАНСБУ,
	|	МАКСИМУМ(втТаблицаВНА.СпособПоступленияВНА) КАК СпособПоступленияВНА,
	|	втТаблицаВНА.Подразделение КАК Подразделение,
	|	втТаблицаВНА.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ втПараметрыУчетаВНА
	|ИЗ
	|	втТаблицаВНА КАК втТаблицаВНА
	|
	|СГРУППИРОВАТЬ ПО
	|	втТаблицаВНА.ВНА,
	|	втТаблицаВНА.Подразделение,
	|	втТаблицаВНА.НаправлениеДеятельности
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТаблицаВНА.ВНА КАК ВНА,
	|	&ВидУчетаНСБУ КАК ВидУчета,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПринятиеКУчетуВНА) КАК ВидОперации,
	|	втТаблицаВНА.ДатаСобытияНСБУ КАК ДатаСостояния,
	|	втТаблицаВНА.ДатаСобытияНСБУ КАК Период,
	|	втТаблицаВНА.ГруппаВНА КАК ГруппаВНА,
	|	втТаблицаВНА.ГруппаВНА.КлассВНА КАК КлассВНА,
	|	втТаблицаВНА.ВНА.ИнвентарныйНомерМСФО КАК ИнвентарныйНомер,
	|	0 КАК ЛиквидационнаяСтоимость,
	|	втТаблицаВНА.СуммаНСБУ КАК ПервоначальнаяСтоимость,
	|	втТаблицаВНА.СпособПоступленияВНА КАК СпособПоступления,
	|	втТаблицаВНА.ГруппаВНА.СчетУчетаПервоначальнойСтоимости КАК СчетУчетаПервоначальнойСтоимости,
	|	втТаблицаВНА.ГруппаВНА.СчетУчетаПервоначальнойСтоимостиСубконто2 КАК СчетУчетаПервоначальнойСтоимостиСубконто2,
	|	втТаблицаВНА.ГруппаВНА.СчетУчетаПервоначальнойСтоимостиСубконто3 КАК СчетУчетаПервоначальнойСтоимостиСубконто3,
	|	втТаблицаВНА.СуммаНСБУ КАК СтоимостьДляВычисленияАмортизации,
	|	втТаблицаВНА.СуммаНСБУ КАК СуммаВВалютеУчета,
	|	втТаблицаВНА.КоличествоНСБУ КАК Количество,
	|	ИСТИНА КАК ПоступилВПериоде,
	|	втТаблицаВНА.Подразделение КАК Подразделение,
	|	втТаблицаВНА.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	втПараметрыУчетаВНА КАК втТаблицаВНА
	|ГДЕ
	|	втТаблицаВНА.СуммаНСБУ <> 0
	|	И &ЕстьНСБУ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втТаблицаВНА.ВНА,
	|	&ВидУчетаМСФО,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПринятиеКУчетуВНА),
	|	втТаблицаВНА.ДатаСобытияМСФО,
	|	втТаблицаВНА.ДатаСобытияМСФО,
	|	втТаблицаВНА.ГруппаВНА,
	|	втТаблицаВНА.ГруппаВНА.КлассВНА,
	|	втТаблицаВНА.ВНА.ПервоначальныйИнвентарныйНомерМСФО,
	|	0,
	|	втТаблицаВНА.СуммаМСФО,
	|	втТаблицаВНА.СпособПоступленияВНА,
	|	втТаблицаВНА.ГруппаВНА.СчетУчетаПервоначальнойСтоимости,
	|	втТаблицаВНА.ГруппаВНА.СчетУчетаПервоначальнойСтоимостиСубконто2,
	|	втТаблицаВНА.ГруппаВНА.СчетУчетаПервоначальнойСтоимостиСубконто3,
	|	втТаблицаВНА.СуммаМСФО,
	|	втТаблицаВНА.СуммаМСФО,
	|	втТаблицаВНА.КоличествоМСФО,
	|	ИСТИНА,
	|	втТаблицаВНА.Подразделение,
	|	втТаблицаВНА.НаправлениеДеятельности
	|ИЗ
	|	втПараметрыУчетаВНА КАК втТаблицаВНА
	|ГДЕ
	|	втТаблицаВНА.СуммаМСФО <> 0
	|	И &ЕстьМСФО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВНА.НомерСтроки КАК НомерСтроки,
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПринятиеКУчетуВНА) КАК ВидОперации,
	|	ТаблицаВНА.ВНА КАК ВНА,
	|	ТаблицаВНА.ГруппаВНА КАК ГруппаВНА,
	|	ТаблицаВНА.СчетУчетаМСФО КАК СчетДт,
	|	ТаблицаВНА.СчетУчетаМСФО.СчетСсылка КАК СчетДтМСФО,
	|	ТаблицаВНА.СчетУчетаСубконто1МСФО КАК СубконтоДт1,
	|	ТаблицаВНА.СчетУчетаСубконто2МСФО КАК СубконтоДт2,
	|	ТаблицаВНА.СчетУчетаСубконто3МСФО КАК СубконтоДт3,
	|	ТаблицаВНА.КоррСчетМСФО КАК СчетКт,
	|	ТаблицаВНА.КоррСчетМСФО.СчетСсылка КАК СчетКтМСФО,
	|	ТаблицаВНА.КоррСчетСубконто1МСФО КАК СубконтоКт1,
	|	ТаблицаВНА.КоррСчетСубконто2МСФО КАК СубконтоКт2,
	|	ТаблицаВНА.КоррСчетСубконто3МСФО КАК СубконтоКт3,
	|	ВЫБОР
	|		КОГДА &ЕстьНСБУ
	|				И НЕ &ФормироватьПроводкиМСФО
	|				И ТаблицаВНА.СторнироватьДанныеНСБУ
	|			ТОГДА ТаблицаВНА.СуммаНСБУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаОперацииНСБУ,
	|	ВЫБОР
	|		КОГДА ТаблицаВНА.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.МСФО)
	|			ТОГДА ТаблицаВНА.СуммаМСФО
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаОперацииМСФО,
	|	ВЫБОР
	|		КОГДА &ЕстьНСБУ
	|				И НЕ &ФормироватьПроводкиМСФО
	|				И ТаблицаВНА.СторнироватьДанныеНСБУ
	|			ТОГДА ТаблицаВНА.СуммаНСБУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПервоначальнаяСтоимостьНСБУ,
	|	ВЫБОР
	|		КОГДА ТаблицаВНА.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.МСФО)
	|			ТОГДА ТаблицаВНА.СуммаМСФО
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПервоначальнаяСтоимостьМСФО,
	|	ВЫБОР
	|		КОГДА &ЕстьНСБУ
	|				И НЕ &ФормироватьПроводкиМСФО
	|				И ТаблицаВНА.СторнироватьДанныеНСБУ
	|			ТОГДА ТаблицаВНА.КоличествоНСБУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоДт,
	|	ВЫБОР
	|		КОГДА ТаблицаВНА.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.МСФО)
	|			ТОГДА ТаблицаВНА.КоличествоМСФО
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоКт,
	|	ВЫБОР
	|		КОГДА &ЕстьНСБУ
	|				И НЕ &ФормироватьПроводкиМСФО
	|				И ТаблицаВНА.СторнироватьДанныеНСБУ
	|			ТОГДА ТаблицаВНА.СуммаВВалютеНСБУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВВалютеНСБУ,
	|	ВЫБОР
	|		КОГДА ТаблицаВНА.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.МСФО)
	|			ТОГДА ТаблицаВНА.СуммаВВалютеМСФО
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВВалютеМСФО,
	|	ТаблицаВНА.Валюта КАК Валюта,
	|	ТаблицаВНА.Подразделение КАК ПодразделениеДт,
	|	ТаблицаВНА.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	ТаблицаВНА.Подразделение КАК ПодразделениеКт,
	|	ТаблицаВНА.НаправлениеДеятельности КАК НаправлениеДеятельностиКт
	|ИЗ
	|	втТаблицаВНА КАК ТаблицаВНА
	|ГДЕ
	|	НЕ(ТаблицаВНА.СчетУчетаМСФО = ТаблицаВНА.КоррСчетМСФО
	|				И ТаблицаВНА.СчетУчетаСубконто1МСФО = ТаблицаВНА.КоррСчетСубконто1МСФО
	|				И ТаблицаВНА.СчетУчетаСубконто2МСФО = ТаблицаВНА.КоррСчетСубконто2МСФО
	|				И ТаблицаВНА.СчетУчетаСубконто3МСФО = ТаблицаВНА.КоррСчетСубконто3МСФО)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";	

КонецФункции

Функция ТекстЗапроса_ТаблицаОшибок(НомераТаблиц)

	ТекстЗапроса = Новый Массив;
	ТекстЗапроса.Добавить(МСФОВНАУХ.ТекстЗапроса_ПроверкаДублейВНА());	
	
	Возврат СтрСоединить(ТекстЗапроса, " ОБЪЕДИНИТЬ ВСЕ ");

КонецФункции

Процедура СформироватьДвижения(Движения, ДополнительныеСвойства, Отказ) Экспорт
	
	Реквизиты 			= ДополнительныеСвойства.ДляПроведения.Реквизиты;
	ТаблицаВНА 			= ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаВНА;
	ТаблицаПроводки		= ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаПроводки;
	
	Если МСФОВНАУХ.ЕстьОшибкиПроведения(ДополнительныеСвойства, Отказ) Тогда
		Возврат;
	КонецЕсли;

	НастройкиОтражения = МСФОВНАУХ.ПолучитьНастройкиОтражения();
	НастройкиОтражения.Вставить("ПервоначальныеСведенияВНАМСФО", 	Истина);
	НастройкиОтражения.Вставить("ПараметрыУчетаВНАМСФО", 			Истина);
	//НастройкиОтражения.Вставить("ПараметрыАмортизацииВНАМСФО", 		Истина);
	//НастройкиОтражения.Вставить("ПараметрыРасчетаАмортизацииВНА",	Истина);
	НастройкиОтражения.Вставить("СостоянияВНАОрганизацийМСФО", 		Истина);
	НастройкиОтражения.Вставить("СтоимостьВНАМСФО", 				Истина);
		
	МСФОВНАУХ.ОтразитьДвиженияПараметровУчетаВНА(Движения, Реквизиты, ТаблицаВНА, НастройкиОтражения);
	
	МСФОВНАУХ.ОтразитьПроводкиВНА(Движения, Реквизиты, ТаблицаПроводки, Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеДокумента

//КоллекцияСтрок - ТабличнаяЧасть,ТаблицаЗначений,ДанныеФормыКоллекция, СтрокаТабличнойЧасти
//Источник - ДокументОбъект, ДанныеФормыСтруктура, УправляемаяФорма
//ПараметрыЗаполнения - Структура(ЗаполнитьРеквизиты = Ложь,  ЗаполнитьНСБУ = Ложь, ЗаполнитьМСФО = Ложь)
Процедура ЗаполнитьСтроки(КоллекцияСтрок, Источник, ПараметрыЗаполненияТЧ = Неопределено) Экспорт

	ПараметрыЗаполнения = МСФОУХ.ПолучитьПараметрыЗаполнения(Источник,,, Ложь);
	ЗаполняемыеРеквизитыТаблицыФормы = МСФОУХ.ПолучитьЗаполняемыеРеквизитыТаблицыФормы(Источник);	
		
	//ТаблицаВНА = МСФОУХ.ПолучитьТаблицуВНА(КоллекцияСтрок, Источник);
		
	Для каждого СтрокаДляЗаполнения Из КоллекцияСтрок Цикл
		
		Если ЗаполняемыеРеквизитыТаблицыФормы <> Неопределено Тогда
			МСФОКлиентСерверУХ.ЗаполнитьРасчетныеРеквизитыСтроки(СтрокаДляЗаполнения, ЗаполняемыеРеквизитыТаблицыФормы);			
		КонецЕсли;
		
	КонецЦикла;	

КонецПроцедуры 

Процедура ЗаполнитьЗависимостиРеквизитовДокумента(ФормаДокумента) Экспорт 

	ЗависимостиРеквизитов = Новый Массив;
	
	Реквизиты = Новый Структура;
	
	Реквизиты.Вставить("СчетУчетаМСФО", 			"СчетУчетаПервоначальнойСтоимости");
	Реквизиты.Вставить("СчетУчетаСубконто1МСФО", 	"СчетУчетаПервоначальнойСтоимостиСубконто1");
	Реквизиты.Вставить("СчетУчетаСубконто2МСФО", 	"СчетУчетаПервоначальнойСтоимостиСубконто2");
	Реквизиты.Вставить("СчетУчетаСубконто3МСФО", 	"СчетУчетаПервоначальнойСтоимостиСубконто3");

	Для каждого Реквизит Из Реквизиты Цикл
	
		НоваяСтрока = Новый Структура;
		НоваяСтрока.Вставить("РеквизитГруппы",	Реквизит.Значение);
		НоваяСтрока.Вставить("Приемник", 		Реквизит.Ключ);
		НоваяСтрока.Вставить("Источник",  		"ГруппаВНАМСФО");
		
		ЗависимостиРеквизитов.Добавить(НоваяСтрока);
		
	КонецЦикла;
	
	ФормаДокумента.КэшируемыеЗначения.Вставить("ЗависимостиРеквизитов", ЗависимостиРеквизитов);
	
КонецПроцедуры

Функция ПолучитьИменаСубконто() Экспорт 

	Результат = Новый Структура;
	
	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "КоррСчетСубконто1НСБУ");
	Субконто.Вставить(2, "КоррСчетСубконто2НСБУ");
	Субконто.Вставить(3, "КоррСчетСубконто3НСБУ");
	
	Результат.Вставить("КоррСчетНСБУ", Субконто);

	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетУчетаСубконто1НСБУ");
	Субконто.Вставить(2, "СчетУчетаСубконто2НСБУ");
	Субконто.Вставить(3, "СчетУчетаСубконто3НСБУ");
	
	Результат.Вставить("СчетУчетаНСБУ", Субконто);
	
	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "КоррСчетСубконто1МСФО");
	Субконто.Вставить(2, "КоррСчетСубконто2МСФО");
	Субконто.Вставить(3, "КоррСчетСубконто3МСФО");
	
	Результат.Вставить("КоррСчетМСФО", Субконто);	
	
	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетУчетаСубконто1МСФО");
	Субконто.Вставить(2, "СчетУчетаСубконто2МСФО");
	Субконто.Вставить(3, "СчетУчетаСубконто3МСФО");
	
	Результат.Вставить("СчетУчетаМСФО", Субконто);
		
	Результат.Вставить("ИсключенияСкрытияСубконто", Новый Структура("КоррСчетМСФО,КоррСчетНСБУ"));

	Возврат Результат;
	
КонецФункции

Функция ПолучитьСтруктуруДействий(ДляФормы = Ложь) Экспорт

	СтруктураДействий = Новый Структура;
	
	ИменаСубконто = ПолучитьИменаСубконто();	
	
	ЗаполнитьЗависимыеРеквизитыГруппыВНА = Новый Структура;
	ЗаполнитьЗависимыеРеквизитыГруппыВНА.Вставить("СчетУчетаМСФО", 			"СчетУчетаПервоначальнойСтоимости");
	ЗаполнитьЗависимыеРеквизитыГруппыВНА.Вставить("СчетУчетаСубконто1МСФО", "СчетУчетаПервоначальнойСтоимостиСубконто1");
	ЗаполнитьЗависимыеРеквизитыГруппыВНА.Вставить("СчетУчетаСубконто2МСФО", "СчетУчетаПервоначальнойСтоимостиСубконто2");
	ЗаполнитьЗависимыеРеквизитыГруппыВНА.Вставить("СчетУчетаСубконто3МСФО", "СчетУчетаПервоначальнойСтоимостиСубконто3");
	
	СоответствиеКолонок = Новый Структура;
	СоответствиеКолонок.Вставить("КоличествоМСФО", 	"КоличествоНСБУ");	
	СоответствиеКолонок.Вставить("СуммаМСФО", 		"СуммаНСБУ");
	СоответствиеКолонок.Вставить("СуммаВВалютеМСФО", "СуммаВВалютеНСБУ");
	
	ЗаполнитьПоКолонкамИсточникам = Новый Структура;
	ЗаполнитьПоКолонкамИсточникам.Вставить("СоответствиеКолонок", СоответствиеКолонок);
	ЗаполнитьПоКолонкамИсточникам.Вставить("ЗаполнитьКолонкуИзменения", Ложь);
	ЗаполнитьПоКолонкамИсточникам.Вставить("ТолькоНезаполненные", Истина);
	
	ЗаполнитьСубконтоИзТрансляции = Новый Структура;
	ЗаполнитьСубконтоИзТрансляции.Вставить(
			"СчетУчетаМСФО", 
			Новый Структура("КолонкаСчетИсточник,СубконтоПриемник,СубконтоИсточник", 
							"СчетУчетаНСБУ", ИменаСубконто.СчетУчетаМСФО, ИменаСубконто.СчетУчетаНСБУ)
		);
	
	СтруктураДействий.Вставить("ЗаполнитьСчетаМСФОПоНеклассифицированным", 	Новый Структура("СчетУчетаНСБУ", "КоррСчетМСФО"));
	СтруктураДействий.Вставить("ЗаполнитьЗависимыеРеквизитыГруппыВНА", 		Новый Структура("ГруппаВНАМСФО", ЗаполнитьЗависимыеРеквизитыГруппыВНА));	
	СтруктураДействий.Вставить("ЗаполнитьПоКолонкамИсточникам",				ЗаполнитьПоКолонкамИсточникам);
	СтруктураДействий.Вставить("ЗаполнитьСубконтоИзТрансляции",				ЗаполнитьСубконтоИзТрансляции);	
	
	Если ДляФормы Тогда	
		СтруктураДействий.Вставить("ЗаполнитьДоступностьПоИменамСубконто",	ИменаСубконто);
	КонецЕсли;

	Возврат СтруктураДействий;
	
КонецФункции

#КонецОбласти

#КонецЕсли