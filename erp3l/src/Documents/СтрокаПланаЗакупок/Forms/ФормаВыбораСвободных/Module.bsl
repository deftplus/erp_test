#Область ОбработкаОсновныхСобытийФормы


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОтборОрганизаторЗакупки = Параметры.ОрганизаторЗакупки;
	ОтборОрганизацияДляЗаключенияДоговора = Параметры.ОрганизацияДляЗаключенияДоговора;
	Если ЗначениеЗаполнено(Параметры.ОрганизацииДляЗаключенияДоговора) Тогда
		ОтборОрганизацийДляЗаключенияДоговора.ЗагрузитьЗначения(
			Параметры.ОрганизацииДляЗаключенияДоговора);
	КонецЕсли;
	флУстановленОтборПоОрганизациям =
		ОтборОрганизацийДляЗаключенияДоговора.Количество() > 0;
	ОтборВалютаДокумента = Параметры.ВалютаДокумента;
	ПериодЗакупок = Параметры.ПериодЗакупок;
	Если ЗначениеЗаполнено(Параметры.ДатаНачала) Тогда
		ОтборДатаНачала = Параметры.ДатаНачала;
	Иначе
		ОтборДатаНачала = ПериодЗакупок.ДатаНачала;
	КонецЕсли;
	Если ЗначениеЗаполнено(Параметры.ДатаОкончания) Тогда
		ОтборДатаОкончания = Параметры.ДатаОкончания;
	ИначеЕсли ЗначениеЗаполнено(Параметры.ПериодЗакупок) Тогда
		ОтборДатаОкончания = ПериодЗакупок.ДатаОкончания;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбработатьИзменениеРеквизитовОтбора();
КонецПроцедуры


#КонецОбласти


#Область ОбработкаСобытийЭлементовФормы


&НаКлиенте
Процедура РеквизитОтбораПриИзменении(Элемент)
	ОбработатьИзменениеРеквизитовОтбора();
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОтборПоОрганизациямНажатие(Элемент)
	ПоказатьЗначение(, ОтборОрганизацийДляЗаключенияДоговора);
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыНаКлиенте


&НаКлиенте
Процедура ОбработатьИзменениеРеквизитовОтбора()
	ОбновитьОтбор();
	УстановитьВидимостьЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтбор()
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(
		Список, 
		"ПериодЗакупок", 
		ПериодЗакупок,
		Истина);
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(
		Список, 
		"ОрганизаторЗакупки", 
		ОтборОрганизаторЗакупки, 
		ЗначениеЗаполнено(ОтборОрганизаторЗакупки));
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(
		Список, 
		"ОрганизацияДляЗаключенияДоговора", 
		ОтборОрганизацияДляЗаключенияДоговора, 
		ЗначениеЗаполнено(ОтборОрганизацияДляЗаключенияДоговора));
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(
		Список, 
		"ОрганизацияДляЗаключенияДоговора", 
		ОтборОрганизацийДляЗаключенияДоговора, 
		флУстановленОтборПоОрганизациям 
			И НЕ ЗначениеЗаполнено(ОтборОрганизацияДляЗаключенияДоговора),
		ВидСравненияКомпоновкиДанных.ВСписке);
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(
		Список, 
		"ВалютаДокумента", 
		ОтборВалютаДокумента, 
		ЗначениеЗаполнено(ОтборВалютаДокумента));
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(
		Список, 
		"ДатаНачалаПодготовкиЗакупки", 
		ОтборДатаНачала, 
		ЗначениеЗаполнено(ОтборДатаНачала),
		ВидСравненияКомпоновкиДанных.БольшеИлиРавно);
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(
		Список, 
		"ДатаНачалаПодготовкиЗакупки", 
		ОтборДатаОкончания, 
		ЗначениеЗаполнено(ОтборДатаОкончания),
		ВидСравненияКомпоновкиДанных.МеньшеИлиРавно);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьЭлементов()
	Элементы.ОрганизаторЗакупки.Видимость =
		НЕ ЗначениеЗаполнено(ОтборОрганизаторЗакупки);
	Элементы.ОрганизацияДляЗаключенияДоговора.Видимость =
		НЕ ЗначениеЗаполнено(ОтборОрганизацияДляЗаключенияДоговора);
	Элементы.ДекорацияОтборПоОрганизациям.Видимость =
		флУстановленОтборПоОрганизациям;
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыНаСервере


#КонецОбласти
