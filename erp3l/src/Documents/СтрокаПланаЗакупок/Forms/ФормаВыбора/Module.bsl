
#Область ОбработкаОсновныхСобытийФормы


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// Считывание параметров.
	ОтборОрганизаторЗакупки = Параметры.ОрганизаторЗакупки;
	ОтборОрганизацияДляЗаключенияДоговора = Параметры.ОрганизацияДляЗаключенияДоговора;
	ОтборДатаНачала = Параметры.ДатаНачала;
	ОтборДатаОкончания = Параметры.ДатаОкончания;
	ТолькоВПланеЗакупок = Параметры.ТолькоВПланеЗакупок;
	ТолькоУтвержденные = Параметры.ТолькоУтвержденные;
	ПериодЗакупок = Параметры.ПериодЗакупок;
	ОтборВалюта = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры.Отбор, "ВалютаДокумента", Справочники.Валюты.ПустаяСсылка());
	ИсключатьВалюту = Параметры.ИсключатьВалюту;
	// Редактирование текста запроса.
	Если ТолькоВПланеЗакупок Тогда
		ТекстПоиска = "ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтрокиПланаЗакупок.СрезПоследних";
		ТекстЗамены = "ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтрокиПланаЗакупок.СрезПоследних";
		Список.ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, ТекстПоиска, ТекстЗамены);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьОтбор();
КонецПроцедуры


#КонецОбласти


#Область ОбработкаСобытийЭлементовФормы


&НаКлиенте
Процедура ПериодОтбораПриИзменении(Элемент)
	ОбновитьОтбор();
КонецПроцедуры

&НаКлиенте
Процедура РеквизитОтбораПриИзменении(Элемент)
	ОбновитьОтбор();
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыНаКлиенте

&НаКлиенте
Процедура ПериодЗакупок1ПриИзменении(Элемент)
	ОбновитьОтбор();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтбор()
	МассивСтатусовУтвержденных = Новый Массив;
	МассивСтатусовУтвержденных.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыВыгружаемыхОбъектовЕИС.Утвержден"));
	МассивСтатусовУтвержденных.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыВыгружаемыхОбъектовЕИС.Выгружен"));
	МассивСтатусовУтвержденных.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыВыгружаемыхОбъектовЕИС.ОшибкиВыгрузки"));
	МассивСтатусовУтвержденных.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыВыгружаемыхОбъектовЕИС.Размещен"));
	Если ИсключатьВалюту Тогда
		ВидСравненияВалюта = ВидСравненияКомпоновкиДанных.НеРавно;
	Иначе
		ВидСравненияВалюта = ВидСравненияКомпоновкиДанных.Равно;
	КонецЕсли;
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(
		Список, 
		"ОрганизаторЗакупки", 
		ОтборОрганизаторЗакупки, 
		ЗначениеЗаполнено(ОтборОрганизаторЗакупки));
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(
		Список, 
		"ОрганизацияДляЗаключенияДоговора", 
		ОтборОрганизацияДляЗаключенияДоговора, 
		ЗначениеЗаполнено(ОтборОрганизацияДляЗаключенияДоговора));
	ОтборыСписковКлиентСерверУХ.УстановитьОтборСпискаМежду(
		Список, 
		"ДатаНачалаПодготовкиЗакупки", 
		ПериодОтбора.ДатаНачала,
		ПериодОтбора.ДатаОкончания);
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(
		Список, 
		"ПериодЗакупок", 
		ПериодЗакупок, 
		ЗначениеЗаполнено(ПериодОтбора));
		
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(
		Список, 
		"СостояниеОбъекта", 
		МассивСтатусовУтвержденных, 
		ТолькоУтвержденные,
		ВидСравненияКомпоновкиДанных.ВСписке);
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(
		Список, 
		"ВалютаДокумента", 
		ОтборВалюта, 
		ЗначениеЗаполнено(ОтборВалюта),
		ВидСравненияВалюта);	
	УстановитьВидимостьЭлементов(ЭтаФорма);
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыНаСервере


&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьЭлементов(Форма)
	Форма.Элементы.ОрганизаторЗакупки.Видимость = 
		ЗначениеЗаполнено(Форма.ОтборОрганизаторЗакупки);
	Форма.Элементы.ОрганизацияДляЗаключенияДоговора.Видимость = 
		ЗначениеЗаполнено(Форма.ОтборОрганизацияДляЗаключенияДоговора);
КонецПроцедуры



#КонецОбласти
