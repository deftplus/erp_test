
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Перем ЗначениеОтбора;
	
	// Возврат при получении формы для анализа.
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСписокРаспоряжений();
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Закрыть(Элементы.СписокРаспоряжений.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если ОбщегоНазначенияУТКлиент.ПроверитьНаличиеВыделенныхВСпискеСтрок(Элементы.СписокРаспоряжений) Тогда
		Закрыть(Элементы.СписокРаспоряжений.ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокРаспоряжений()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказКлиента.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВтОтобранныеЗаказыКлиентов
	|ИЗ
	|	Документ.ЗаказКлиента КАК ЗаказКлиента
	|ГДЕ
	|	НЕ ЗаказКлиента.ПометкаУдаления
	|	И ЗаказКлиента.Партнер = &Партнер
	|		И ЗаказКлиента.Контрагент = &Контрагент
	|		И ВЫБОР
	|			КОГДА НЕ &ИспользоватьСоглашенияСКлиентами
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЗаказКлиента.Соглашение = &Соглашение
	|		КОНЕЦ
	|		И ЗаказКлиента.ХозяйственнаяОперация = &ХозяйственнаяОперацияЗаказа
	|		И ЗаказКлиента.Организация = &Организация
	|		И ЗаказКлиента.Договор = &Договор
	|		И (Склад = &Склад
	|			ИЛИ Склад В ИЕРАРХИИ (&Склад)
	|			ИЛИ Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|		И ЗаказКлиента.Валюта = &Валюта
	|		И ЗаказКлиента.Сделка = &Сделка
	|		И ВЫБОР
	|			КОГДА НЕ &ИспользоватьНаправленияДеятельности
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЗаказКлиента.НаправлениеДеятельности = &НаправлениеДеятельности
	|		КОНЕЦ
	|		И ЗаказКлиента.ЦенаВключаетНДС = &ЦенаВключаетНДС
	|		И ЗаказКлиента.ВернутьМногооборотнуюТару = &ВернутьМногооборотнуюТару
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаказКлиента.Ссылка
	|
	|;
	|
	|ВЫБРАТЬ
	|	ЗаявкаНаВозврат.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВтОтобранныеЗаявки
	|ИЗ
	|	Документ.ЗаявкаНаВозвратТоваровОтКлиента КАК ЗаявкаНаВозврат
	|ГДЕ
	|	НЕ ЗаявкаНаВозврат.ПометкаУдаления
	|	И ЗаявкаНаВозврат.Партнер = &Партнер
	|		И ЗаявкаНаВозврат.Контрагент = &Контрагент
	|		И ВЫБОР
	|			КОГДА НЕ &ИспользоватьСоглашенияСКлиентами
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЗаявкаНаВозврат.Соглашение = &Соглашение
	|		КОНЕЦ
	|		И ЗаявкаНаВозврат.ХозяйственнаяОперация = &ХозяйственнаяОперацияЗаявки
	|		И ЗаявкаНаВозврат.Организация = &Организация
	|		И ЗаявкаНаВозврат.Договор = &Договор
	|		И (Склад = &Склад
	|			ИЛИ Склад В ИЕРАРХИИ (&Склад)
	|			ИЛИ Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|		И ЗаявкаНаВозврат.Валюта = &Валюта
	|		И ЗаявкаНаВозврат.Сделка = &Сделка
	|		И ВЫБОР
	|			КОГДА НЕ &ИспользоватьНаправленияДеятельности
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЗаявкаНаВозврат.НаправлениеДеятельности = &НаправлениеДеятельности
	|		КОНЕЦ
	|		И ЗаявкаНаВозврат.ЦенаВключаетНДС = &ЦенаВключаетНДС
	|		И ЗаявкаНаВозврат.ВернутьМногооборотнуюТару = &ВернутьМногооборотнуюТару
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаявкаНаВозврат.Ссылка
	|
	|;
	|
	|ВЫБРАТЬ
	|	ВТТаблицаЗаказы.ЗаказКлиента       КАК ЗаказКлиента,
	|	СУММА(ВТТаблицаЗаказы.КОформлению) КАК КОформлению
	|ПОМЕСТИТЬ ТаблицаЗаказы
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаказыОстатки.ЗаказКлиента       КАК ЗаказКлиента,
	|		ЗаказыОстатки.КОформлениюОстаток КАК КОформлению
	|	ИЗ
	|		РегистрНакопления.ЗаказыКлиентов.Остатки(
	|				,
	|				ЗаказКлиента В (
	|					ВЫБРАТЬ
	|						СписокЗаказов.Ссылка
	|					ИЗ
	|						ВтОтобранныеЗаказыКлиентов КАК СписокЗаказов)) КАК ЗаказыОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗаказКлиента.Ссылка,
	|		ЗаказКлиентаТовары.Количество
	|	ИЗ
	|		ВтОтобранныеЗаказыКлиентов КАК ЗаказКлиента
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
	|			ПО ЗаказКлиента.Ссылка = ЗаказКлиентаТовары.Ссылка
	|	ГДЕ
	|		НЕ ЗаказКлиентаТовары.Отменено
	|		И НЕ &ИспользоватьРасширенныеВозможностиЗаказаКлиента
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗаказыДвижения.ЗаказКлиента,
	|		ВЫБОР
	|			КОГДА ЗаказыДвижения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ЗаказыДвижения.КОформлению
	|			ИНАЧЕ ЗаказыДвижения.КОформлению
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ЗаказыКлиентов КАК ЗаказыДвижения
	|	ГДЕ
	|		ЗаказыДвижения.Регистратор = &Регистратор
	|		И ЗаказыДвижения.Активность
	|		И ЗаказыДвижения.ЗаказКлиента.Партнер = &Партнер
	|		И ЗаказыДвижения.ЗаказКлиента.Контрагент = &Контрагент
	|		И ВЫБОР
	|			КОГДА НЕ &ИспользоватьСоглашенияСКлиентами
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЗаказыДвижения.ЗаказКлиента.Соглашение = &Соглашение
	|		КОНЕЦ
	|		И ЗаказыДвижения.ЗаказКлиента.ХозяйственнаяОперация = &ХозяйственнаяОперацияЗаказа
	|		И ЗаказыДвижения.ЗаказКлиента.Организация = &Организация
	|		И ЗаказыДвижения.ЗаказКлиента.Договор = &Договор
	|		И (ЗаказыДвижения.Склад = &Склад
	|			ИЛИ ЗаказыДвижения.Склад В ИЕРАРХИИ (&Склад)
	|			ИЛИ ЗаказыДвижения.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|		И ЗаказыДвижения.ЗаказКлиента.Валюта = &Валюта
	|		И ЗаказыДвижения.ЗаказКлиента.Сделка = &Сделка
	|		И ВЫБОР
	|			КОГДА НЕ &ИспользоватьНаправленияДеятельности
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЗаказыДвижения.ЗаказКлиента.НаправлениеДеятельности = &НаправлениеДеятельности
	|		КОНЕЦ
	|		И ЗаказыДвижения.ЗаказКлиента.ЦенаВключаетНДС = &ЦенаВключаетНДС
	|		И ЗаказыДвижения.ЗаказКлиента.ВернутьМногооборотнуюТару = &ВернутьМногооборотнуюТару) КАК ВТТаблицаЗаказы
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТТаблицаЗаказы.ЗаказКлиента
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	ВТТаблицаЗаказы.ЗаказКлиента       КАК ЗаказКлиента,
	|	СУММА(ВТТаблицаЗаказы.КОформлению) КАК КОформлению
	|ПОМЕСТИТЬ ТаблицаЗаявки
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаказыОстатки.ЗаказКлиента КАК ЗаказКлиента,
	|		ЗаказыОстатки.КОформлениюОстаток КАК КОформлению
	|	ИЗ
	|		РегистрНакопления.ЗаказыКлиентов.Остатки(
	|				,
	|				ЗаказКлиента В (
	|					ВЫБРАТЬ
	|						СписокЗаказов.Ссылка
	|					ИЗ
	|						ВтОтобранныеЗаявки КАК СписокЗаказов)) КАК ЗаказыОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗаказКлиента.Ссылка,
	|		ЗаказКлиентаТовары.Количество
	|	ИЗ
	|		ВтОтобранныеЗаявки КАК ЗаказКлиента
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаВозвратТоваровОтКлиента.ЗаменяющиеТовары КАК ЗаказКлиентаТовары
	|			ПО ЗаказКлиента.Ссылка = ЗаказКлиентаТовары.Ссылка
	|	ГДЕ
	|		НЕ ЗаказКлиентаТовары.Отменено
	|		И НЕ &ИспользоватьРасширенныеВозможностиЗаказаКлиента
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗаказыДвижения.ЗаказКлиента,
	|		ВЫБОР
	|			КОГДА ЗаказыДвижения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ЗаказыДвижения.КОформлению
	|			ИНАЧЕ ЗаказыДвижения.КОформлению
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ЗаказыКлиентов КАК ЗаказыДвижения
	|	ГДЕ
	|		ЗаказыДвижения.Регистратор = &Регистратор
	|		И ЗаказыДвижения.Активность
	|		И ЗаказыДвижения.ЗаказКлиента.Партнер = &Партнер
	|		И ЗаказыДвижения.ЗаказКлиента.Контрагент = &Контрагент
	|		И ВЫБОР
	|			КОГДА НЕ &ИспользоватьСоглашенияСКлиентами
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЗаказыДвижения.ЗаказКлиента.Соглашение = &Соглашение
	|		КОНЕЦ
	|		И ЗаказыДвижения.ЗаказКлиента.ХозяйственнаяОперация = &ХозяйственнаяОперацияЗаявки
	|		И ЗаказыДвижения.ЗаказКлиента.Организация = &Организация
	|		И ЗаказыДвижения.ЗаказКлиента.Договор = &Договор
	|		И (ЗаказыДвижения.Склад = &Склад
	|			ИЛИ ЗаказыДвижения.Склад В ИЕРАРХИИ (&Склад)
	|			ИЛИ ЗаказыДвижения.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|		И ЗаказыДвижения.ЗаказКлиента.Валюта = &Валюта
	|		И ЗаказыДвижения.ЗаказКлиента.Сделка = &Сделка
	|		И ВЫБОР
	|			КОГДА НЕ &ИспользоватьНаправленияДеятельности
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЗаказыДвижения.ЗаказКлиента.НаправлениеДеятельности = &НаправлениеДеятельности
	|		КОНЕЦ
	|		И ЗаказыДвижения.ЗаказКлиента.ЦенаВключаетНДС = &ЦенаВключаетНДС
	|		И ЗаказыДвижения.ЗаказКлиента.ВернутьМногооборотнуюТару = &ВернутьМногооборотнуюТару) КАК ВТТаблицаЗаказы
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТТаблицаЗаказы.ЗаказКлиента
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Заказы.Ссылка              КАК Ссылка,
	|	ТИПЗНАЧЕНИЯ(Заказы.Ссылка) КАК ТипРаспоряжения,
	|	Заказы.Приоритет           КАК Приоритет,
	|	ВЫБОР
	|		КОГДА Заказы.Приоритет В
	|				(ВЫБРАТЬ ПЕРВЫЕ 1
	|					Приоритеты.Ссылка КАК Приоритет
	|				ИЗ
	|					Справочник.Приоритеты КАК Приоритеты
	|				УПОРЯДОЧИТЬ ПО
	|					Приоритеты.РеквизитДопУпорядочивания)
	|			ТОГДА 0
	|		КОГДА Заказы.Приоритет В
	|				(ВЫБРАТЬ ПЕРВЫЕ 1
	|					Приоритеты.Ссылка КАК Приоритет
	|				ИЗ
	|					Справочник.Приоритеты КАК Приоритеты
	|				УПОРЯДОЧИТЬ ПО
	|					Приоритеты.РеквизитДопУпорядочивания УБЫВ)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ                      КАК КартинкаПриоритета,
	|	Заказы.Дата                КАК Дата,
	|	Заказы.Номер               КАК Номер,
	|	Заказы.Статус              КАК Статус,
	|	Заказы.Партнер             КАК Партнер,
	|	Заказы.Контрагент          КАК Контрагент,
	|	Заказы.Соглашение          КАК Соглашение,
	|	Заказы.Организация         КАК Организация,
	|	Заказы.Договор             КАК Договор,
	|	Заказы.Склад               КАК Склад,
	|	Заказы.Валюта              КАК Валюта,
	|	Заказы.Менеджер            КАК Менеджер,
	|	Заказы.ЦенаВключаетНДС     КАК ЦенаВключаетНДС,
	|	Заказы.Комментарий         КАК Комментарий,
	|	Заказы.СуммаДокумента      КАК СуммаДокумента
	|ИЗ
	|	Документ.ЗаказКлиента КАК Заказы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЗаказы КАК ТаблицаЗаказы
	|		ПО Заказы.Ссылка = ТаблицаЗаказы.ЗаказКлиента
	|ГДЕ
	|	ТаблицаЗаказы.КОформлению > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Заявки.Ссылка              КАК Ссылка,
	|	ТИПЗНАЧЕНИЯ(Заявки.Ссылка) КАК ТипРаспоряжения,
	|	Заявки.Приоритет           КАК Приоритет,
	|	ВЫБОР
	|		КОГДА Заявки.Приоритет В
	|				(ВЫБРАТЬ ПЕРВЫЕ 1
	|					Приоритеты.Ссылка КАК Приоритет
	|				ИЗ
	|					Справочник.Приоритеты КАК Приоритеты
	|				УПОРЯДОЧИТЬ ПО
	|					Приоритеты.РеквизитДопУпорядочивания)
	|			ТОГДА 0
	|		КОГДА Заявки.Приоритет В
	|				(ВЫБРАТЬ ПЕРВЫЕ 1
	|					Приоритеты.Ссылка КАК Приоритет
	|				ИЗ
	|					Справочник.Приоритеты КАК Приоритеты
	|				УПОРЯДОЧИТЬ ПО
	|					Приоритеты.РеквизитДопУпорядочивания УБЫВ)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ                      КАК КартинкаПриоритета,
	|	Заявки.Дата                КАК Дата,
	|	Заявки.Номер               КАК Номер,
	|	Заявки.Статус              КАК Статус,
	|	Заявки.Партнер             КАК Партнер,
	|	Заявки.Контрагент          КАК Контрагент,
	|	Заявки.Соглашение          КАК Соглашение,
	|	Заявки.Организация         КАК Организация,
	|	Заявки.Договор             КАК Договор,
	|	Заявки.Склад               КАК Склад,
	|	Заявки.Валюта              КАК Валюта,
	|	Заявки.Менеджер            КАК Менеджер,
	|	Заявки.ЦенаВключаетНДС     КАК ЦенаВключаетНДС,
	|	Заявки.Комментарий         КАК Комментарий,
	|	Заявки.СуммаДокумента      КАК СуммаДокумента
	|ИЗ
	|	Документ.ЗаявкаНаВозвратТоваровОтКлиента КАК Заявки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЗаявки КАК ТаблицаЗаказы
	|		ПО Заявки.Ссылка = ТаблицаЗаказы.ЗаказКлиента
	|ГДЕ
	|	ТаблицаЗаказы.КОформлению > 0";
	
	Запрос.УстановитьПараметр("Регистратор",				Параметры.Регистратор);
	Запрос.УстановитьПараметр("Партнер",					Параметры.Отбор.Партнер);
	Запрос.УстановитьПараметр("Контрагент",					Параметры.Отбор.Контрагент);
	Запрос.УстановитьПараметр("Соглашение",					Параметры.Отбор.Соглашение);
	Запрос.УстановитьПараметр("Организация",				Параметры.Отбор.Организация);
	Запрос.УстановитьПараметр("Договор",					Параметры.Отбор.Договор);
	Запрос.УстановитьПараметр("Склад",						Параметры.Склад);
	Запрос.УстановитьПараметр("Валюта",						Параметры.Отбор.Валюта);
	Запрос.УстановитьПараметр("Сделка",						Параметры.Отбор.Сделка);
	Запрос.УстановитьПараметр("НаправлениеДеятельности",	Параметры.Отбор.НаправлениеДеятельности);
	Запрос.УстановитьПараметр("ЦенаВключаетНДС",			Параметры.Отбор.ЦенаВключаетНДС);
	Запрос.УстановитьПараметр("ВернутьМногооборотнуюТару",	Параметры.Отбор.ВернутьМногооборотнуюТару);
	
	Запрос.УстановитьПараметр("ХозяйственнаяОперацияЗаказа",
								Перечисления.ХозяйственныеОперации.ПередачаНаХранениеСПравомПродажи);
	Запрос.УстановитьПараметр("ХозяйственнаяОперацияЗаявки",
								Перечисления.ХозяйственныеОперации.ВозвратОтХранителя);
	Запрос.УстановитьПараметр("ИспользоватьСоглашенияСКлиентами",
								ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами"));
	Запрос.УстановитьПараметр("ИспользоватьРасширенныеВозможностиЗаказаКлиента",
								ПолучитьФункциональнуюОпцию("ИспользоватьРасширенныеВозможностиЗаказаКлиента"));
	Запрос.УстановитьПараметр("ИспользоватьНаправленияДеятельности",
								ПолучитьФункциональнуюОпцию("ИспользоватьУчетДоходовПоНаправлениямДеятельности"));
	
	Результат = Запрос.Выполнить();
	
	СписокРаспоряжений.Загрузить(Результат.Выгрузить());
	
КонецПроцедуры

#КонецОбласти

