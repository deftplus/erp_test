#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	МеханизмыДокумента.Добавить("ОсновныеСредства");
	МеханизмыДокумента.Добавить("ОсновныеСредстваПоМСФО");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ОсновныеСредстваМеждународныйУчет(ТекстыЗапроса, Регистры);
		Международный(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
	
КонецПроцедуры

// Добавляет команду создания документа "Принятие к учету ОС (международный учет)".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.ПринятиеКУчетуОСМеждународныйУчет)
		И ПолучитьФункциональнуюОпцию("ИспользоватьДокументыВнеоборотныхАктивовМеждународныйУчет") Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ПринятиеКУчетуОСМеждународныйУчет.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ПринятиеКУчетуОСМеждународныйУчет);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ОтображатьВнеоборотныеАктивыМеждународныйУчет2_2";
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.ПринятиеКУчетуОСМеждународныйУчет";
	
	ВЗапросеЕстьИсточник = Истина;
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "";
		ВЗапросеЕстьИсточник = Ложь;
		
	ИначеЕсли ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Дата,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.ПометкаУдаления,
	|	ДанныеДокумента.Проведен,
	|	ДанныеДокумента.Комментарий,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.Подразделение,
	|	ДанныеДокумента.Ответственный,
	|	ДанныеДокумента.ПорядокУчета,
	|	ДанныеДокумента.ВидАктива,
	|	ДанныеДокумента.МетодНачисленияАмортизации,
	|	ДанныеДокумента.СрокИспользования,
	|	ДанныеДокумента.СчетКапитальныхВложений,
	|	ДанныеДокумента.СчетУчета,
	|	ДанныеДокумента.СчетКапитальныхВложений.УчетПоПодразделениям КАК СчетКапВложений_УчетПоПодразделениям,
	|	ДанныеДокумента.СчетКапитальныхВложений.УчетПоНаправлениямДеятельности КАК СчетКапВложений_УчетПоНаправлениям,
	|	ДанныеДокумента.СчетУчета.УчетПоПодразделениям КАК СчетУчета_УчетПоПодразделениям,
	|	ДанныеДокумента.СчетУчета.УчетПоНаправлениямДеятельности КАК СчетУчета_УчетПоНаправлениям,
	|	ДанныеДокумента.СчетАмортизации,
	|	ДанныеДокумента.СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов,
	|	ДанныеДокумента.ПоказательНаработки,
	|	ДанныеДокумента.ОбъемНаработки,
	|	ДанныеДокумента.КоэффициентУскорения,
	|	ДанныеДокумента.ЛиквидационнаяСтоимость,
	|	ДанныеДокумента.ЛиквидационнаяСтоимостьПредставления,
	|	ДанныеДокумента.ПервоначальнаяСтоимость,
	|	ДанныеДокумента.ПервоначальнаяСтоимостьПредставления
	|ИЗ
	|	Документ.ПринятиеКУчетуОСМеждународныйУчет КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("Граница", Новый Граница(Реквизиты.Дата, ВидГраницы.Исключая));
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ПринятиеКУчетуОСМеждународныйУчет"));
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПринятиеКУчетуОС);

	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Процедура ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса)
	
	ИмяТаблицы = "ТаблицаОС";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица ТаблицаОС
	|"+
	"ВЫБРАТЬ
	|	ТаблицаОС.Ссылка,
	|	ТаблицаОС.НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство,
	|	ТаблицаОС.НаправлениеДеятельности,
	|	ТаблицаОС.ИнвентарныйНомер
	|ПОМЕСТИТЬ ТаблицаОС
	|ИЗ
	|	Документ.ПринятиеКУчетуОСМеждународныйУчет.ОсновныеСредства КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы);
	
КонецПроцедуры

Процедура ОсновныеСредстваМеждународныйУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОсновныеСредстваМеждународныйУчет";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ОсновныеСредстваМеждународныйУчет
	|"+
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	&Дата КАК Период,
	|	
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА &ПорядокУчета = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.СписыватьПриПринятииКУчету)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|	КОНЕЦ КАК Состояние,
	|	ТаблицаОС.ИнвентарныйНомер,
	|	ТаблицаОС.НаправлениеДеятельности,
	|	&СчетУчета КАК СчетУчета,
	|	&ВидАктива КАК ВидАктива,
	|	&ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	&ЛиквидационнаяСтоимостьПредставления КАК ЛиквидационнаяСтоимостьПредставления,
	|	&ПорядокУчета КАК ПорядокУчета,
	|	&МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
	|	&СчетАмортизации КАК СчетАмортизации,
	|	&СрокИспользования КАК СрокИспользования,
	|	&ПоказательНаработки КАК ПоказательНаработки,
	|	&ОбъемНаработки КАК ОбъемНаработки,
	|	&КоэффициентУскорения КАК КоэффициентУскорения,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра);
	
КонецПроцедуры

Процедура Международный(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "Международный";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица Международный
	|"+
	"ВЫБРАТЬ
	|	&Дата КАК Период,
	|	&Ссылка КАК Регистратор,
	|
	|	ЗНАЧЕНИЕ(Справочник.ПланыСчетовМеждународногоУчета.Международный) КАК ПланСчетов,
	|	&Организация КАК Организация,
	|	&ПервоначальнаяСтоимость КАК Сумма,
	|	&ПервоначальнаяСтоимостьПредставления КАК СуммаПредставления,
	|	
	|	&СчетУчета КАК СчетДт,
	|	ВЫБОР КОГДА &СчетУчета_УчетПоПодразделениям
	|		ТОГДА &Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ПодразделениеДт,
	|	ВЫБОР КОГДА &СчетУчета_УчетПоНаправлениям
	|		ТОГДА ТаблицаОС.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	ТаблицаОС.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства) КАК ВидСубконтоДт1,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка) КАК ВидСубконтоДт2,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка) КАК ВидСубконтоДт3,
	|	0 КАК ВалютнаяСуммаДт,
	|	
	|	&СчетКапитальныхВложений КАК СчетКт,
	|	ВЫБОР КОГДА &СчетКапВложений_УчетПоПодразделениям
	|		ТОГДА &Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ПодразделениеКт,
	|	ВЫБОР КОГДА &СчетКапВложений_УчетПоНаправлениям
	|		ТОГДА ТаблицаОС.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	ТаблицаОС.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ОсновныеСредства) КАК ВидСубконтоКт1,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка) КАК ВидСубконтоКт2,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка) КАК ВидСубконтоКт3,
	|	0 КАК ВалютнаяСуммаКт
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	НЕ (&ПервоначальнаяСтоимость=0 И &ПервоначальнаяСтоимостьПредставления=0)
	|";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Дата                                   КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&Ответственный                          КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&Дата                                   КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	ЛОЖЬ                                    КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО                            КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО                            КАК ИсправляемыйДокумент,
	|	&Дата                                   КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО                            КАК Приоритет";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоОС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0) КАК НомерЗаписи,
	|	&Ссылка                    КАК Ссылка,
	|	&Дата                      КАК Дата,
	|	&Организация               КАК Организация,
	|	&Подразделение             КАК Подразделение,
	|	&Проведен                  КАК Проведен,
	|	&ХозяйственнаяОперация     КАК ХозяйственнаяОперация,
	|	&ИдентификаторМетаданных   КАК ТипСсылки,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство
	|ИЗ
	|	Документ.ПринятиеКУчетуОСМеждународныйУчет КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОСМеждународныйУчет.ОсновныеСредства КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ПроведениеМеждународныйУчет

Процедура ЗаполнитьТаблицуДокументаКОтражениюВМеждународномУчете(ТаблицаРегистрации, ДокументОбъект) Экспорт

	НоваяСтрока = ТаблицаРегистрации.Добавить();
	НоваяСтрока.Организация = ДокументОбъект.Организация;
	НоваяСтрока.Период      = ДокументОбъект.Дата;
	НоваяСтрока.ДатаОтражения = НачалоДня(ДокументОбъект.Дата);

КонецПроцедуры

Функция ТекстОтраженияВМеждународномУчете() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Ссылка.Дата КАК Дата,
	|	ДанныеДокумента.Ссылка.Организация КАК Организация,
	|	ДанныеДокумента.Ссылка.Подразделение КАК Подразделение,
	|	ДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеДокумента.ОсновноеСредство КАК Объект,
	|	ДанныеДокумента.Ссылка.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
	|	ДанныеДокумента.Ссылка.ПервоначальнаяСтоимостьПредставления КАК ПервоначальнаяСтоимостьПредставления,
	|	ДанныеДокумента.Ссылка.ПорядокУчета КАК ПорядокУчета,
	|	ДанныеДокумента.Ссылка.СчетКапитальныхВложений КАК СчетКапитальныхВложений,
	|	ДанныеДокумента.Ссылка.СчетУчета КАК СчетУчета,
	|	ДанныеДокумента.Ссылка.СчетРасходов КАК СчетРасходов,
	|	ДанныеДокумента.Ссылка.СчетРасходовСубконто1 КАК СчетРасходовСубконто1,
	|	ДанныеДокумента.Ссылка.СчетРасходовСубконто2 КАК СчетРасходовСубконто2,
	|	ДанныеДокумента.Ссылка.СчетРасходовСубконто3 КАК СчетРасходовСубконто3
	|ПОМЕСТИТЬ Операции
	|ИЗ
	|	Документ.ПринятиеКУчетуОСМеждународныйУчет.ОсновныеСредства КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО ДанныеДокумента.Ссылка = ДокументыКОтражению.Регистратор
	|			И ДанныеДокумента.Ссылка.Организация = ДокументыКОтражению.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
	|		ПО ДанныеДокумента.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	ДанныеДокумента.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Операции.Объект КАК Объект,
	|	СУММА(КапитальныеВложения.Сумма) КАК Сумма,
	|	СУММА(КапитальныеВложения.СуммаПредставления) КАК СуммаПредставления
	|ПОМЕСТИТЬ КапитальныеВложения
	|ИЗ
	|	Операции КАК Операции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Международный.ДвиженияССубконто(
	|				,
	|				,
	|				НЕ Регистратор В
	|							(ВЫБРАТЬ
	|								Операции.Регистратор
	|							ИЗ
	|								Операции)
	|					И (Организация, Подразделение, СчетДт, Субконто1) В
	|						(ВЫБРАТЬ
	|							Операции.Организация,
	|							Операции.Подразделение,
	|							Операции.СчетКапитальныхВложений,
	|							Операции.Объект
	|						ИЗ
	|							Операции КАК Операции),
	|				,
	|				) КАК КапитальныеВложения
	|		ПО Операции.Объект = КапитальныеВложения.СубконтоДт1
	|			И (КОНЕЦПЕРИОДА(Операции.Дата, МЕСЯЦ) > КапитальныеВложения.Период)
	|			И Операции.СчетКапитальныхВложений = КапитальныеВложения.СчетДт
	|			И (КапитальныеВложения.Активность)
	|
	|СГРУППИРОВАТЬ ПО
	|	Операции.Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 КАК ТипПроводки, // Передача стоимости со счета капитальных затрат на счет учета
	|	Операции.Дата КАК Период,
	|	Операции.Регистратор КАК Регистратор,
	|	
	|	Операции.Организация КАК Организация,
	|	0 КАК Сумма,
	|	0 КАК СуммаПредставления,
	|	
	|	Операции.СчетУчета КАК СчетДт,
	|	Операции.Подразделение КАК ПодразделениеДт,
	|	Операции.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	Операции.Объект КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	0 КАК ВалютнаяСуммаДт,
	|	
	|	Операции.СчетКапитальныхВложений КАК СчетКт,
	|	Операции.Подразделение КАК ПодразделениеКт,
	|	Операции.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операции.Объект КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	0 КАК ВалютнаяСуммаКт
	|	
	|ПОМЕСТИТЬ Проводки
	|ИЗ
	|	Операции КАК Операции
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1 КАК ТипПроводки, // Передача стоимости со счета капитальных затрат на счет учета
	|	Операции.Дата КАК Период,
	|	Операции.Регистратор КАК Регистратор,
	|	
	|	Операции.Организация КАК Организация,
	|	Операции.ПервоначальнаяСтоимость КАК Сумма,
	|	Операции.ПервоначальнаяСтоимостьПредставления КАК СуммаПредставления,
	|	
	|	Операции.СчетУчета КАК СчетДт,
	|	Операции.Подразделение КАК ПодразделениеДт,
	|	Операции.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	Операции.Объект КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	0 КАК ВалютнаяСуммаДт,
	|	
	|	Операции.СчетКапитальныхВложений КАК СчетКт,
	|	Операции.Подразделение КАК ПодразделениеКт,
	|	Операции.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операции.Объект КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	0 КАК ВалютнаяСуммаКт
	|	
	|ИЗ
	|	Операции КАК Операции
	|ГДЕ
	|	Операции.ПервоначальнаяСтоимость <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1 КАК ТипПроводки, // Передача стоимости со счета капитальных затрат на счет учета
	|	Операции.Дата КАК Период,
	|	Операции.Регистратор КАК Регистратор,
	|	
	|	Операции.Организация КАК Организация,
	|	ЕСТЬNULL(КапитальныеВложения.Сумма, 0) - Операции.ПервоначальнаяСтоимость КАК Сумма,
	|	ЕСТЬNULL(КапитальныеВложения.СуммаПредставления, 0) - Операции.ПервоначальнаяСтоимостьПредставления КАК СуммаПредставления,
	|	
	|	Операции.СчетУчета КАК СчетДт,
	|	Операции.Подразделение КАК ПодразделениеДт,
	|	Операции.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	Операции.Объект КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	0 КАК ВалютнаяСуммаДт,
	|	
	|	Операции.СчетКапитальныхВложений КАК СчетКт,
	|	Операции.Подразделение КАК ПодразделениеКт,
	|	Операции.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операции.Объект КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	0 КАК ВалютнаяСуммаКт
	|	
	|ИЗ
	|	Операции КАК Операции
	|		ЛЕВОЕ СОЕДИНЕНИЕ КапитальныеВложения КАК КапитальныеВложения
	|		ПО Операции.Объект = КапитальныеВложения.Объект
	|ГДЕ
	|	ЕСТЬNULL(КапитальныеВложения.Сумма, 0) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2 КАК ТипПроводки, // Списание стоимости со счета учета на счет расходов
	|	Операции.Дата КАК Период,
	|	Операции.Регистратор КАК Регистратор,
	|	
	|	Операции.Организация КАК Организация,
	|	ЕСТЬNULL(КапитальныеВложения.Сумма, 0) КАК Сумма,
	|	ЕСТЬNULL(КапитальныеВложения.СуммаПредставления, 0) КАК СуммаПредставления,
	|	
	|	Операции.СчетРасходов КАК СчетДт,
	|	Операции.Подразделение КАК ПодразделениеДт,
	|	Операции.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	Операции.СчетРасходовСубконто1 КАК СубконтоДт1,
	|	Операции.СчетРасходовСубконто2 КАК СубконтоДт2,
	|	Операции.СчетРасходовСубконто3 КАК СубконтоДт3,
	|	0 КАК ВалютнаяСуммаДт,
	|	
	|	Операции.СчетУчета КАК СчетКт,
	|	Операции.Подразделение КАК ПодразделениеКт,
	|	Операции.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операции.Объект КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	0 КАК ВалютнаяСуммаКт
	|ИЗ
	|	Операции КАК Операции
	|	ЛЕВОЕ СОЕДИНЕНИЕ КапитальныеВложения КАК КапитальныеВложения
	|		ПО Операции.Объект = КапитальныеВложения.Объект
	|ГДЕ
	|	Операции.ПорядокУчета = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.СписыватьПриПринятииКУчету)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР 
	|		КОГДА ПланСчетовМеждународный.УчетВФункциональнойВалюте = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВФункциональнойВалюте.ВВалютеРегл)
	|			ТОГДА Проводки.Организация.ВалютаРегламентированногоУчета
	|		ИНАЧЕ &ВалютаУправленческогоУчета
	|	КОНЕЦ КАК ВалютаХраненияСуммыФункциональной,
	|	ПланСчетовМеждународный.ВалютаПредставления КАК ВалютаХраненияСуммыПредставления,
	|	Проводки.ТипПроводки КАК ТипПроводки,
	|	Проводки.Период,
	|	Проводки.Регистратор,
	|	Проводки.Организация,
	|	СУММА(Проводки.Сумма) КАК Сумма,
	|	СУММА(Проводки.СуммаПредставления) КАК СуммаПредставления,
	|	Проводки.СчетДт,
	|	ВЫБОР КОГДА Проводки.СчетДт.УчетПоПодразделениям
	|		ТОГДА Проводки.ПодразделениеДт
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ПодразделениеДт,
	|	ВЫБОР КОГДА Проводки.СчетДт.УчетПоНаправлениямДеятельности
	|		ТОГДА Проводки.НаправлениеДеятельностиДт
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельностиДт,
	|	Проводки.ВалютаДт,
	|	Проводки.СубконтоДт1,
	|	Проводки.СубконтоДт2,
	|	Проводки.СубконтоДт3,
	|	СУММА(Проводки.ВалютнаяСуммаДт) КАК ВалютнаяСуммаДт,
	|	Проводки.СчетКт,
	|	ВЫБОР КОГДА Проводки.СчетКт.УчетПоПодразделениям
	|		ТОГДА Проводки.ПодразделениеКт
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ПодразделениеКт,
	|	ВЫБОР КОГДА Проводки.СчетКт.УчетПоНаправлениямДеятельности
	|		ТОГДА Проводки.НаправлениеДеятельностиКт
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельностиКт,
	|	Проводки.ВалютаКт,
	|	Проводки.СубконтоКт1,
	|	Проводки.СубконтоКт2,
	|	Проводки.СубконтоКт3,
	|	СУММА(Проводки.ВалютнаяСуммаКт) КАК ВалютнаяСуммаКт
	|ИЗ
	|	Проводки КАК Проводки
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ПланыСчетовМеждународногоУчета КАК ПланСчетовМеждународный
	|	ПО
	|		ПланСчетовМеждународный.Ссылка = ЗНАЧЕНИЕ(Справочник.ПланыСчетовМеждународногоУчета.Международный)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР 
	|		КОГДА ПланСчетовМеждународный.УчетВФункциональнойВалюте = ЗНАЧЕНИЕ(Перечисление.ВидыУчетаВФункциональнойВалюте.ВВалютеРегл)
	|			ТОГДА Проводки.Организация.ВалютаРегламентированногоУчета
	|		ИНАЧЕ &ВалютаУправленческогоУчета
	|	КОНЕЦ,
	|	ПланСчетовМеждународный.ВалютаПредставления,
	|	Проводки.ТипПроводки,
	|	Проводки.Период,
	|	Проводки.Регистратор,
	|	Проводки.Организация,
	|	Проводки.СчетДт,
	|	ВЫБОР КОГДА Проводки.СчетДт.УчетПоПодразделениям
	|		ТОГДА Проводки.ПодразделениеДт
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР КОГДА Проводки.СчетДт.УчетПоНаправлениямДеятельности
	|		ТОГДА Проводки.НаправлениеДеятельностиДт
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	Проводки.ВалютаДт,
	|	Проводки.СубконтоДт1,
	|	Проводки.СубконтоДт2,
	|	Проводки.СубконтоДт3,
	|	Проводки.СчетКт,
	|	ВЫБОР КОГДА Проводки.СчетКт.УчетПоПодразделениям
	|		ТОГДА Проводки.ПодразделениеКт
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР КОГДА Проводки.СчетКт.УчетПоНаправлениямДеятельности
	|		ТОГДА Проводки.НаправлениеДеятельностиКт
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	Проводки.ВалютаКт,
	|	Проводки.СубконтоКт1,
	|	Проводки.СубконтоКт2,
	|	Проводки.СубконтоКт3
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТипПроводки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Операции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Проводки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ КапитальныеВложения";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ПроверитьЗаполнениеПроводкиМеждународногоУчета(Проводка, Выборка) Экспорт
	
	Если Проводка.Сумма = 0 Тогда
		
		Если Выборка.ТипПроводки = 1 Тогда // Передача первоначальной стоимости на счет учета
			
			Проводка.Статус = МеждународныйУчетСерверПовтИсп.ОпределитьСтатус(
				Проводка.Статус,
				Перечисления.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчете);
			
			ТекстОшибки = НСтр("ru = 'Первоначальная стоимость ОС ""%Объект%"" на счете капитальных вложений отсутствует';
								|en = 'Initial cost of FA ""%Объект%"" on the capital investment account is missing'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Объект%", Проводка.СубконтоДт1);
			Проводка.МассивОшибок.Добавить(ТекстОшибки);
			
		КонецЕсли;
		
		Если Выборка.ТипПроводки = 2 Тогда // Передача первоначальной стоимости на счет учета
			
			Проводка.Статус = МеждународныйУчетСерверПовтИсп.ОпределитьСтатус(
				Проводка.Статус,
				Перечисления.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчете);
			
			ТекстОшибки = НСтр("ru = 'Стоимость ОС ""%Объект%"" на счете учета отсутствует';
								|en = 'Cost of FA ""%Объект%"" is missing in GL account'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Объект%", Проводка.СубконтоКт1);
			Проводка.МассивОшибок.Добавить(ТекстОшибки);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ПараметрыВыбораСтатейИАналитик() Экспорт
	
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходов");
	
	Возврат ПараметрыВыбора;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Документы.ПринятиеКУчетуОСМеждународныйУчет.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.3.24";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("67a029c5-d395-421a-956f-ab815b8eda9b");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.ПринятиеКУчетуОСМеждународныйУчет.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Обновляет документы ""Принятие к учету ОС (международный учет)"":
	|- заполняет новый реквизит ""Направление деятельности"".';
	|en = 'Updates the ""FA recognition (international accounting)"" documents:
	|- fills in a new attribute ""Line of business"".'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.ПринятиеКУчетуОСМеждународныйУчет.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ОбъектыЭксплуатации.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Документы.ПринятиеКУчетуОСМеждународныйУчет.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Документы.ПринятиеКУчетуОСМеждународныйУчет.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ОбъектыЭксплуатации.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт

	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = "Документ.ПринятиеКУчетуОСМеждународныйУчет";
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Дата УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Дата УБЫВ");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТабличнаяЧасть.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПринятиеКУчетуОСМеждународныйУчет.ОсновныеСредства КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	И ТабличнаяЧасть.ОсновноеСредство.УдалитьНаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	СписокСсылок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, СписокСсылок);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт

	ПолноеИмяОбъекта = "Документ.ПринятиеКУчетуОСМеждународныйУчет";
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаДанных.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТДляОбработки
	|ИЗ
	|	&ТаблицаОбновляемыхДанных КАК ТаблицаДанных
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СсылкиДляОбработки.Ссылка КАК Ссылка,
	|	ТаблицаОбъекта.ВерсияДанных КАК ВерсияДанных
	|ПОМЕСТИТЬ ВсеОбъекты
	|ИЗ
	|	ВТДляОбработки КАК СсылкиДляОбработки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОСМеждународныйУчет КАК ТаблицаОбъекта
	|		ПО (ТаблицаОбъекта.Ссылка = СсылкиДляОбработки.Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеОбъекты.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТЗаблокированныеСсылки
	|ИЗ
	|	ВсеОбъекты КАК ВсеОбъекты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОСМеждународныйУчет.ОсновныеСредства КАК ТабличнаяЧасть
	|		ПО (ТабличнаяЧасть.Ссылка = ВсеОбъекты.Ссылка)
	|ГДЕ
	|	ТабличнаяЧасть.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				ВТЗаблокированныеДанные.Ссылка
	|			ИЗ
	|				ВТЗаблокированныеДанные КАК ВТЗаблокированныеДанные)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТабличнаяЧасть.ОсновноеСредство КАК ОсновноеСредство,
	|	ТабличнаяЧасть.ОсновноеСредство.УдалитьНаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	Документ.ПринятиеКУчетуОСМеждународныйУчет.ОсновныеСредства КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	И ТабличнаяЧасть.ОсновноеСредство.УдалитьНаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	И ТабличнаяЧасть.Ссылка В
	|			(ВЫБРАТЬ
	|				ВсеОбъекты.Ссылка
	|			ИЗ
	|				ВсеОбъекты КАК ВсеОбъекты)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеОбъекты.Ссылка КАК Ссылка,
	|	ВсеОбъекты.ВерсияДанных КАК ВерсияДанных
	|ИЗ
	|	ВсеОбъекты КАК ВсеОбъекты
	|ГДЕ
	|	НЕ ВсеОбъекты.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТЗаблокированныеСсылки.Ссылка
	|				ИЗ
	|					ВТЗаблокированныеСсылки КАК ВТЗаблокированныеСсылки)";
	
	ЧитаемыеДанные = Новый Массив;
	ЧитаемыеДанные.Добавить("Справочник.ОбъектыЭксплуатации");
	ДополнительныеПараметрыПроцедуры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДополнительныеПараметрыПроцедуры.ИмяВременнойТаблицы = "ВТЗаблокированныеДанные";
	ПараметрыТаблицы = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияСсылок(
							Параметры.Очередь, ЧитаемыеДанные, МенеджерВременныхТаблиц, ДополнительныеПараметрыПроцедуры);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаОбновляемыхДанных", ОбновляемыеДанные);
	
	Результат = Запрос.ВыполнитьПакет();
	
	МенеджерВременныхТаблиц.Закрыть();
	
	ЗначенияРеквизитовОС  = Результат[Результат.ВГраница()-1].Выгрузить();
	ЗначенияРеквизитовОС.Индексы.Добавить("ОсновноеСредство");
	
	Выборка = Результат[Результат.ВГраница()].Выбрать();
 	Пока Выборка.Следующий() Цикл
 		
 		НачатьТранзакцию();
		
 		Попытка
			
 			Блокировка = Новый БлокировкаДанных;
 			ЭлементБлокировки = Блокировка.Добавить("Документ.ПринятиеКУчетуОСМеждународныйУчет");
 			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
 			Блокировка.Заблокировать();
 			
			ДанныеОбъекта = ОбновлениеИнформационнойБазыУТ.ПроверитьПолучитьОбъект(Выборка.Ссылка, Выборка.ВерсияДанных, Параметры.Очередь);
			Если ДанныеОбъекта = Неопределено Тогда
				ЗафиксироватьТранзакцию();
 				Продолжить;
 			КонецЕсли;
			
			Для каждого ДанныеСтроки Из ДанныеОбъекта.ОсновныеСредства Цикл
				
				Если ЗначениеЗаполнено(ДанныеСтроки.ОсновноеСредство) Тогда
					ЗначенияРеквизитов = ЗначенияРеквизитовОС.Найти(ДанныеСтроки.ОсновноеСредство, "ОсновноеСредство");
					Если ЗначенияРеквизитов <> Неопределено Тогда
						ЗаполнитьЗначенияСвойств(ДанныеСтроки, ЗначенияРеквизитов);
					КонецЕсли; 
				КонецЕсли;
				
			КонецЦикла; 
			
			Если ДанныеОбъекта.Модифицированность() Тогда
	 			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДанныеОбъекта);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
			КонецЕсли; 
	
			ЗафиксироватьТранзакцию();
			
 		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru = 'Не удалось обработать объект: %Ссылка% по причине: %Причина%';
									|en = 'Cannot process the object: %Ссылка%. Reason: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Выборка.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
									УровеньЖурналаРегистрации.Предупреждение,
									Выборка.Ссылка.Метаданные(),
									Выборка.Ссылка,
									ТекстСообщения);
									
 		КонецПопытки;
 
 	КонецЦикла;
 		
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
