#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область ОбработчикиСобытийДокумента


Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	ДатаРегистрации = Дата;
	ПроверитьСообщитьДокументНеУникальный(Отказ);
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если НЕ ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Дата = ТекущаяДатаСеанса();
	Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	// Установим размер в регистр РазмерыКонтрагентовДляЕИС.
	НаборЗаписей = РегистрыСведений.РазмерыКонтрагентовДляЕИС.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Период.Установить(ДатаРегистрации);
	Для Каждого Строка Из РазмерыКонтрагентов Цикл
		Движение = НаборЗаписей.Добавить();
		Движение.Период = ДатаРегистрации;
		Движение.Контрагент = Строка.Контрагент;
		Движение.Размер = Строка.Размер;
	КонецЦикла;
	НаборЗаписей.Записать(Истина);
	// Установим флаг СубъектМалогоИлиСреднегоПредпринимательстваУХ в справочнике Контрагенты.
	Для Каждого ТекРазмерыКонтрагентов Из РазмерыКонтрагентов Цикл
		// Определим, что текущий размер относится к МСП.
		РазмерыСМсп = Перечисления.РазмерыКонтрагентовДляЕИС.ПолучитьРазмерыСМСП();
		ТекРазмер = ТекРазмерыКонтрагентов.Размер;
		ЭтоРазмерМСП = (РазмерыСМсп.Найти(ТекРазмер) <> Неопределено);
		// Перезапишем справочник.
		СправочникСсылка = ТекРазмерыКонтрагентов.Контрагент;
		Если ЗначениеЗаполнено(СправочникСсылка) Тогда
			СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
			СправочникОбъект.СубъектМалогоИлиСреднегоПредпринимательстваУХ = ЭтоРазмерМСП;
			СправочникОбъект.Записать();
		Иначе
			// Контрагент не указан. Пропускаем.
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры


#КонецОбласти


#Область ПрограммныйИнтерфейс


Процедура ПроверитьСообщитьДокументНеУникальный(Отказ)
	мДокументов = ЦентрализованныеЗакупкиУХ.ПолучитьДокументыПоОтбору(
		"РегистрацияРазмераКонтрагентаДляЕИС",
		Новый Структура("ДатаРегистрации, Проведен",
						 ДатаРегистрации, Истина),
		Ссылка);
	Если мДокументов.Количество() > 0 Тогда
		ОбщегоНазначенияУХ.СообщитьОбОшибке(
			СтрШаблон(НСтр(
				"ru='Регистрация размера контрагентов уже зарегистрирована на дату %1!""'"),
		 		ДатаРегистрации),
			Отказ,
			,
			СтатусСообщения.Важное);
	КонецЕсли;
КонецПроцедуры


#КонецОбласти


#КонецЕсли