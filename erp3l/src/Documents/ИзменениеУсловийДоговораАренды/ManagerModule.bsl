
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	
	МеханизмыДокумента.Добавить("ОсновныеСредства");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	
	ИзменениеУсловийДоговораАрендыЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
	#Область УХ_Встраивание
	МеханизмыДокумента.Добавить("ГрафикиРасчетовУХ");
	#КонецОбласти
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура:
//     * Ключ - Строка - Имя таблицы.
//     * Значение - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаСтоимостьОС(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаАмортизацияОС(Запрос, ТекстыЗапроса, Регистры);

		ТекстЗапросаТаблицаАрендованныеОС(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаУсловияДоговоровАренды(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаГрафикОплатУслугПоАренде(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаГрафикНачисленияУслугПоАренде(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаГрафикНачисленияПроцентовПоАренде(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРасчетыПоФинансовымИнструментам(Запрос, ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры);
		
		//++ НЕ УТКА
		ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Регистры);
		//-- НЕ УТКА
		
		ИзменениеУсловийДоговораАрендыЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
		
		#Область УХ_Встраивание
		ВстраиваниеУХДоговорыАренды.ТекстЗапросаВерсииРасчетов(ТекстыЗапроса, Регистры);
		ВстраиваниеУХДоговорыАренды.ТекстЗапросаТаблицаРасчетыСКонтрагентамиГрафики(ТекстыЗапроса, Регистры, "ИзменениеУсловийДоговораАренды");
		#КонецОбласти
		
	КонецЕсли;
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

// Формирует таблицы движений при отложенном проведении.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ИзменениеУсловийДоговораАренды - Документ, для которого формируются движения.
//  МенеджерВременныхТаблиц	- МенеджерВременныхТаблиц - Содержит вспомогательные временные таблицы, которые могут использоваться для формирования движений.
//
// Возвращаемое значение:
//  см. ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения
//
Функция ТаблицыОтложенногоФормированияДвижений(ДокументСсылка, МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(Запрос, ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаСтоимостьОС(Запрос, ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаАмортизацияОС(Запрос, ТекстыЗапроса, Неопределено);

	ТекстЗапросаТаблицаУсловияДоговоровАренды(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаРасчетыПоФинансовымИнструментам(Запрос, ТекстыЗапроса, Неопределено);
	
	ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса, Неопределено);
	
	ИзменениеУсловийДоговораАрендыЛокализация.ТаблицыОтложенногоФормированияДвижений(ТекстыЗапроса);
	
	ТаблицыДляДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(
							Запрос,
							ТекстыЗапроса,
							Неопределено);
	
	Возврат ТаблицыДляДвижений;
	
КонецФункции

#КонецОбласти

#Область Команды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.ИзменениеПараметровОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);

КонецПроцедуры

// Добавляет команду создания документа "Изменение условий договора аренды".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Порядок - Число -
//
// Возвращаемое значение:
//	СтрокаТаблицыЗначений - добавленная команда
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании, Порядок = 0) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.ИзменениеУсловийДоговораАренды) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ИзменениеУсловийДоговораАренды.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ИзменениеУсловийДоговораАренды);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьВнеоборотныеАктивы2_4";
		КомандаСоздатьНаОсновании.Порядок = Порядок;
	
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки	 - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область Прочее

// Возвращает параметры механизма взаиморасчетов.
//
// Параметры:
// 	ДанныеЗаполнения - ДокументОбъект, СправочникОбъект, Структура, ДанныеФормыСтруктура - Объект или коллекция для
//              расчета параметров взаиморасчетов.
//
// Возвращаемое значение:
// 	Структура - Структура параметров функций механизма взаиморасчетов (См. ВзаиморасчетыСервер.ПараметрыМеханизма)
//
Функция ПараметрыВзаиморасчеты(ДанныеЗаполнения = Неопределено) Экспорт
	
	СтруктураПараметров = ВзаиморасчетыСервер.ПараметрыМеханизма();
	
	СтруктураПараметров.ЭтоПродажаЗакупка                = Истина;
	СтруктураПараметров.ИзменяетПланОплаты               = Ложь;
	СтруктураПараметров.ИзменяетПланОтгрузкиПоставки     = Ложь;

	#Область НеобязательныеПараметры
	
	СтруктураПараметров.ВалютаВзаиморасчетов             = "Объект.ВалютаВзаиморасчетов";
	СтруктураПараметров.СуммаВзаиморасчетов              = "Объект.СуммаВзаиморасчетов";
	СтруктураПараметров.НаправлениеДеятельности          = "";
	СтруктураПараметров.ПорядокРасчетов                  = "";
	СтруктураПараметров.ПутьКДаннымТЧ                    = "";
	СтруктураПараметров.Соглашение                       = "";
	СтруктураПараметров.БанковскийСчетОрганизации        = "";
	СтруктураПараметров.БанковскийСчетКонтрагента        = "";
	СтруктураПараметров.Касса                            = "";
	СтруктураПараметров.ФормаОплаты                      = "";
	СтруктураПараметров.ОплатаВВалюте                    = "";
	СтруктураПараметров.ИдентификаторПлатежа             = "";
	СтруктураПараметров.Договор                          = "";
	
	// Реквизиты для объекта расчетов, используются в проведении.
	СтруктураПараметров.ГруппаФинансовогоУчета           = "";
	СтруктураПараметров.Подразделение                    = "";
	СтруктураПараметров.КурсЧислитель                    = "Объект.КурсЧислитель";
	СтруктураПараметров.КурсЗнаменатель                  = "Объект.КурсЗнаменатель";
	
	СтруктураПараметров.ЭлементыФормы.НадписьВалюты = "ДекорацияВалюты";
	СтруктураПараметров.ВалютыИКурсДокументаТолькоПросмотр = Ложь;
	СтруктураПараметров.ВалютаДокументаТолькоПросмотр = Ложь;
	
	СтруктураПараметров.ВалютаВзаиморасчетовТолькоПросмотр = Истина;
	
	#КонецОбласти
	
	Возврат СтруктураПараметров;

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Процедура ИнициализироватьКлючиАналитикиУчетаПоПартнерам(Запрос)
	
	Если Запрос.Параметры.Свойство("КлючиАналитикиУчетаПоПартнерамИнициализированы") Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация                    КАК Организация,
	|	ДанныеДокумента.Партнер                        КАК Партнер,
	|	ДанныеДокумента.Контрагент                     КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК Договор,
	|	ДанныеДокумента.Договор.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|		ПО ДанныеДокумента.Организация = РегистрАналитикаУчетаПоПартнерам.Организация
	|			И ДанныеДокумента.Партнер = РегистрАналитикаУчетаПоПартнерам.Партнер
	|			И ДанныеДокумента.Контрагент = РегистрАналитикаУчетаПоПартнерам.Контрагент
	|			И ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) = РегистрАналитикаУчетаПоПартнерам.Договор
	|			И ДанныеДокумента.Договор.НаправлениеДеятельности = РегистрАналитикаУчетаПоПартнерам.НаправлениеДеятельности
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И РегистрАналитикаУчетаПоПартнерам.КлючАналитики ЕСТЬ NULL";
	
	ЗапросАналитик = Новый Запрос(ТекстЗапроса);
	ЗапросАналитик.УстановитьПараметр("Ссылка", Запрос.Параметры.Ссылка);
	РезультатЗапроса = ЗапросАналитик.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.АналитикаУчетаПоПартнерам.СоздатьКлючАналитики(Выборка);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("КлючиАналитикиУчетаПоПартнерамИнициализированы", Истина);
	
КонецПроцедуры

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.ИзменениеУсловийДоговораАренды";
	ТекстыЗапросаВременныхТаблиц = Новый Соответствие();
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("Подразделение", "ДанныеДокумента.Договор.Подразделение");
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		ВЗапросеЕстьИсточник = Ложь;
		
	ИначеЕсли ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	КонецЕсли;
		
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров,
										ТекстыЗапросаВременныхТаблиц);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                                     КАК Ссылка,
	|	ДанныеДокумента.Дата                                       КАК Период,
	|	ДанныеДокумента.Номер                                      КАК Номер,
	|	ДанныеДокумента.Организация                                КАК Организация,
	|	ДанныеДокумента.Партнер                                    КАК Партнер,
	|	ДанныеДокумента.Контрагент                                 КАК Контрагент,
	|	ДанныеДокумента.Договор                                    КАК Договор,
	|	ДанныеДокумента.Договор.ТипДоговора                        КАК ТипДоговора,
	|	ДанныеДокумента.Договор.Балансодержатель                   КАК Балансодержатель,
	|	ДанныеДокумента.Договор.НаправлениеДеятельности            КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(ДанныеДокумента.Договор.УчитыватьПредметыАрендыВРеглУчете, ЛОЖЬ)  КАК УчитыватьПредметыАрендыВРеглУчете,
	|	ЕСТЬNULL(ДанныеДокумента.Договор.УчитыватьПредметыАрендыВУпрУчете, ЛОЖЬ)   КАК УчитыватьПредметыАрендыВУпрУчете,
	|	ДанныеДокумента.Договор.Подразделение                      КАК Подразделение,
	|	ДанныеДокумента.ВалютаВзаиморасчетов                       КАК ВалютаВзаиморасчетов,
	|	ДанныеДокумента.СуммаВзаиморасчетов                        КАК СуммаВзаиморасчетов,
	|	ДанныеДокумента.СуммаДокумента                             КАК СуммаДокумента,
	|	ДанныеДокумента.Валюта                                     КАК Валюта,
	|	ДанныеДокумента.Ответственный                              КАК Ответственный,
	|	ДанныеДокумента.Комментарий                                КАК Комментарий,
	|	ДанныеДокумента.Проведен                                   КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления                            КАК ПометкаУдаления,
	|	ДанныеДокумента.Сумма                                      КАК Сумма,
	|	ДанныеДокумента.СуммаОбеспечительногоПлатежа               КАК СуммаОбеспечительногоПлатежа,
	|	ДанныеДокумента.СуммаУслугПоАренде                         КАК СуммаУслугПоАренде,
	|	ДанныеДокумента.СуммаВыкупаПредметовАренды                 КАК СуммаВыкупаПредметовАренды,
	|	ДанныеДокумента.СуммаНДСВыкупаПредметовАренды              КАК СуммаНДСВыкупаПредметовАренды,
	|	ДанныеДокумента.СуммаПроцентов                             КАК СуммаПроцентов,
	|	ДанныеДокумента.СуммаСНДС                                  КАК СуммаСНДС,
	|	ДанныеДокумента.СуммаНДС                                   КАК СуммаНДС,
	|	ДанныеДокумента.СтавкаНДС                                  КАК СтавкаНДС,
	|	ДанныеДокумента.СрокЛет                                    КАК СрокЛет,
	|	ДанныеДокумента.СрокМес                                    КАК СрокМес,
	|	ДанныеДокумента.СрокДней                                   КАК СрокДней,
	|	ДанныеДокумента.ДатаНачалаАренды                           КАК ДатаНачалаАренды,
	|	ДанныеДокумента.ДатаПервогоПлатежа                         КАК ДатаПервогоПлатежа,
	|	ДанныеДокумента.ДатаПервогоОбеспечительногоПлатежа         КАК ДатаПервогоОбеспечительногоПлатежа,
	|	ДанныеДокумента.ДатаВыкупа                                 КАК ДатаВыкупа,
	|	ДанныеДокумента.ПроцентнаяСтавка                           КАК ПроцентнаяСтавка,
	|	ДанныеДокумента.ПериодичностьОплат                         КАК ПериодичностьОплат,
	|	ДанныеДокумента.ПериодичностьНачислений                    КАК ПериодичностьНачислений,
	|	ДанныеДокумента.СтоимостьПредметовАренды                   КАК СтоимостьПредметовАренды,
	|	ДанныеДокумента.ОплатаУслугиПоАренде                       КАК ОплатаУслугиПоАренде,
	|	ДанныеДокумента.НачислениеУслугиПоАренде                   КАК НачислениеУслугиПоАренде,
	|	ДанныеДокумента.ИдентификаторДокумента                     КАК ИдентификаторДокумента,
	|	ДанныеДокумента.СтатьяРасходов                             КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов                          КАК АналитикаРасходов,
	|	ДанныеДокумента.АналитикаАктивовПассивовРасходы            КАК АналитикаАктивовПассивовРасходы,
	|	ДанныеДокумента.НастройкаСчетовУчетаРасходы                КАК НастройкаСчетовУчетаРасходы,
	|	ДанныеДокумента.СтатьяДоходов                              КАК СтатьяДоходов,
	|	ДанныеДокумента.АналитикаДоходов                           КАК АналитикаДоходов,
	|	ДанныеДокумента.Договор.СтатьяРасходовПроценты             КАК СтатьяРасходовПроценты,
	|	ДанныеДокумента.Договор.АналитикаРасходовПроценты          КАК АналитикаРасходовПроценты,
	|	ДанныеДокумента.Договор.ПодразделениеПроценты              КАК ПодразделениеПроценты,
	|	ДанныеДокумента.ГрафикНачисленияПроцентовВведенВручную     КАК ГрафикНачисленияПроцентовВведенВручную,
	|	ДанныеДокумента.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета
	|	//++УХ_Встраивание
	|,
	|	ЕСТЬNULL(ДанныеДокумента.Договор.ОсновнойЦФО, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК ЦФО,
	|	ЕСТЬNULL(ДанныеДокумента.Договор.ОсновнойПроект, ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)) КАК Проект,
	|	ЕСТЬNULL(ДанныеДокумента.Договор.БанковскийСчет, ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаОрганизаций.ПустаяСсылка)) КАК БанковскийСчет,
	|	ЕСТЬNULL(ДанныеДокумента.Договор.БанковскийСчетКонтрагента, ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаКонтрагентов.ПустаяСсылка)) КАК БанковскийСчетКонтрагента
	|	//--УХ_Встраивание
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ВнеоборотныеАктивы.ДополнитьЗначенияПараметровПроведенияДокументаАренды(ЗначенияПараметровПроведения);

	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ИзменениеУсловийДоговораАренды"));
	ЗначенияПараметровПроведения.Вставить("ВедетсяРегламентированныйУчетВНА", ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА());
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ИзменениеУсловийДоговораАренды);
	ЗначенияПараметровПроведения.Вставить("ВалютаУправленческогоУчета", Константы.ВалютаУправленческогоУчета.Получить());
	ЗначенияПараметровПроведения.Вставить("ОтражатьВРеглУчете", Истина);
	ЗначенияПараметровПроведения.Вставить("ОтражатьВУпрУчете", Истина);

	ЗначенияПараметровПроведения.Вставить("ХО_УвеличениеНакопленнойАмортизации", Перечисления.ХозяйственныеОперации.УвеличениеНакопленнойАмортизацииОС);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_УвеличениеНакопленнойАмортизации", Справочники.НастройкиХозяйственныхОпераций.УвеличениеНакопленнойАмортизацииОС);

	ЗначенияПараметровПроведения.Вставить("ХО_УменьшениеНакопленнойАмортизации", Перечисления.ХозяйственныеОперации.УменьшениеНакопленнойАмортизацииОС);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_УменьшениеНакопленнойАмортизации", Справочники.НастройкиХозяйственныхОпераций.УменьшениеНакопленнойАмортизацииОС);

	Если Реквизиты <> Неопределено Тогда
		
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
		ЗначенияПараметровПроведения.Вставить("НачалоМесяца", НачалоМесяца(Реквизиты.Период));
		ЗначенияПараметровПроведения.Вставить("КонецМесяца", КонецМесяца(Реквизиты.Период));
		
		ЗначенияПараметровПроведения.Вставить("ИспользуетсяУчетАрендыПоФСБУ25_2018",
			ВнеоборотныеАктивы.ИспользуетсяУчетАрендыПоФСБУ25_2018(Реквизиты.Организация, Реквизиты.Период));
		
		Коэффициенты = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентыПересчетаВалюты(
			Реквизиты.Валюта, Реквизиты.ВалютаВзаиморасчетов, Реквизиты.Период, Реквизиты.Организация);
		ЗначенияПараметровПроведения.Вставить("КоэффициентПересчетаВВалютуУпр",  Коэффициенты.КоэффициентПересчетаВВалютуУпр);
		ЗначенияПараметровПроведения.Вставить("КоэффициентПересчетаВВалютуРегл", Коэффициенты.КоэффициентПересчетаВВалютуРегл);
		
		ПараметрыУчетаПредметовАренды = ВнеоборотныеАктивыКлиентСервер.ПараметрыУчетаПредметовАренды(
			ЗначенияПараметровПроведения.ИспользуетсяУчетАрендыПоФСБУ25_2018, Реквизиты);
		
		ЗначенияПараметровПроведения.Вставить("ЕстьУчетУУ", ПараметрыУчетаПредметовАренды.ЕстьУчетУУ);
		ЗначенияПараметровПроведения.Вставить("ЕстьУчетБУ", ПараметрыУчетаПредметовАренды.ЕстьУчетБУ);
		ЗначенияПараметровПроведения.Вставить("ЕстьУчетНУ", ПараметрыУчетаПредметовАренды.ЕстьУчетНУ);
		
		ЗначенияПараметровПроведения.Вставить("СозданДляПерехода",
			КонецДня(Реквизиты.Период) + 1 = ВнеоборотныеАктивы.НачалоУчетаАрендыПоФСБУ25_2018(Реквизиты.Организация));
		
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

#Область ТекстыЗапросаДляПроведения

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыАмортизацииОСУУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПустыеТаблицыОтложенныхДвижений(ТекстыЗапроса, Запрос);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыАмортизацииОСУУ(ТекстыЗапроса, "Документ.ИзменениеУсловийДоговораАренды");
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаПереоценки(ТекстыЗапроса, "ОС", "Документ.ИзменениеУсловийДоговораАренды");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Ссылка                                                        КАК Регистратор,
	|	&Период                                                        КАК Период,
	|	&Организация                                                   КАК Организация,
	|	ТаблицаОС.ОсновноеСредство                                     КАК ОсновноеСредство,
	|
	|	ДанныеРегистра.КоэффициентУскорения                            КАК КоэффициентУскорения,
	|	ДанныеРегистра.МетодНачисленияАмортизации                      КАК МетодНачисленияАмортизации,
	|	ДанныеРегистра.ЛиквидационнаяСтоимость                         КАК ЛиквидационнаяСтоимость,
	|	ДанныеРегистра.ЛиквидационнаяСтоимостьРегл                     КАК ЛиквидационнаяСтоимостьРегл,
	|	ДанныеРегистра.ДатаПоследнегоИзменения                         КАК ДатаПоследнегоИзменения,
	|	ДанныеРегистра.ОбъемПродукцииРаботДляВычисленияАмортизации     КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|
	|	ТаблицаОС.СрокИспользованияУУ                                  КАК СрокИспользования,
	|
	|	ВЫБОР
	|		КОГДА ДанныеРегистра.СрокИспользования <> ДанныеРегистра.СрокИспользованияДляВычисленияАмортизации
	|			ТОГДА ДанныеРегистра.СрокИспользованияДляВычисленияАмортизации + (ТаблицаОС.СрокИспользованияУУ - ДанныеРегистра.СрокИспользования)
	|		ИНАЧЕ ТаблицаОС.СрокИспользованияУУ
	|	КОНЕЦ КАК СрокИспользованияДляВычисленияАмортизации,
	|
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиСтоимостиУУ, 0) = 0
	|				И ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиСтоимостиУУ, 0) = 0
	|			ТОГДА ДанныеРегистра.СтоимостьДляВычисленияАмортизации
	|		КОГДА ДанныеРегистра.СрокИспользования <> ДанныеРегистра.СрокИспользованияДляВычисленияАмортизации
	|			ТОГДА ТаблицаОС.СтоимостьУУ 
	|					- (ЕСТЬNULL(ТаблицаПереоценки.НакопленнаяАмортизацияУУ, 0)
	|						+ ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ, 0)
	|						- ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиАмортизацииУУ, 0))
	|		ИНАЧЕ ДанныеРегистра.СтоимостьДляВычисленияАмортизации 
	|				+ ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиСтоимостиУУ, 0)
	|				- ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиСтоимостиУУ, 0)
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизации,
	|
	|	ВЫБОР 
	|		КОГДА &ВедетсяРегламентированныйУчетВНА
	|			ТОГДА 0
	|		КОГДА ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиСтоимостиУУ, 0) = 0
	|				И ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиСтоимостиУУ, 0) = 0
	|			ТОГДА ДанныеРегистра.СтоимостьДляВычисленияАмортизацииРегл
	|		КОГДА ДанныеРегистра.СрокИспользования <> ДанныеРегистра.СрокИспользованияДляВычисленияАмортизации
	|			ТОГДА ТаблицаОС.СтоимостьБУ 
	|					- (ЕСТЬNULL(ТаблицаПереоценки.НакопленнаяАмортизацияБУ, 0)
	|						+ ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ, 0)
	|						- ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиАмортизацииБУ, 0))
	|		ИНАЧЕ ДанныеРегистра.СтоимостьДляВычисленияАмортизациирегл 
	|				+ ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиСтоимостиБУ, 0)
	|				- ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиСтоимостиБУ, 0)
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизацииРегл
	|
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ОС КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПараметрыАмортизацииОСУУ КАК ДанныеРегистра
	|		ПО ДанныеРегистра.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПереоценки КАК ТаблицаПереоценки
	|		ПО ТаблицаПереоценки.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И (ТаблицаОС.СрокИспользованияУУ <> ДанныеРегистра.СрокИспользования
	|		ИЛИ ЕСТЬNULL(ТаблицаПереоценки.СуммаДооценкиСтоимостиУУ, 0) <> 0
	|		ИЛИ ЕСТЬNULL(ТаблицаПереоценки.СуммаУценкиСтоимостиУУ, 0) <> 0)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);

КонецПроцедуры

Процедура ТекстЗапросаТаблицаСтоимостьОС(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "СтоимостьОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;

	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса, "Документ.ИзменениеУсловийДоговораАренды", "втСведенияОбУчете");
	
	ТекстЗапросаТаблицаВтУвеличениеСтоимости(Запрос, ТекстыЗапроса);
	ТекстЗапросаТаблицаВтУменьшениеСтоимости(Запрос, ТекстыЗапроса);

	СписокЗапросовОбъединение = Новый Массив;
	
	#Область УвеличениеСтоимостиОС // Дт <01.03> - Кт <76.07.1>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                КАК ВидДвижения,
	|	&Ссылка                                               КАК Регистратор,
	|	&Период                                               КАК Период,
	|	&Организация                                          КАК Организация,
	|	УвеличениеСтоимости.ОсновноеСредство                  КАК ОсновноеСредство,
	|	СведенияОбУчете.Местонахождение                       КАК Подразделение,
	|	СведенияОбУчете.ГруппаФинансовогоУчета                КАК ГруппаФинансовогоУчета,
	|	СведенияОбУчете.НаправлениеДеятельности               КАК НаправлениеДеятельности,
	|	СведенияОбУчете.Арендатор                             КАК Арендатор,
	|
	|	УвеличениеСтоимости.Стоимость                         КАК Стоимость,
	|	УвеличениеСтоимости.СтоимостьРегл                     КАК СтоимостьРегл,
	|	УвеличениеСтоимости.СтоимостьНУ                       КАК СтоимостьНУ,
	|
	|	УвеличениеСтоимости.СтоимостьРегл
	|	 - УвеличениеСтоимости.СтоимостьНУ                    КАК СтоимостьВР,
	|
	|	УвеличениеСтоимости.ЗалоговаяСтоимость                КАК ЗалоговаяСтоимость,
	|	0                                                     КАК НеУчитываемаяСтоимостьНУ,
	|	
	|	УвеличениеСтоимости.ИдентификаторСтроки               КАК ИдентификаторФинЗаписи,
	|	&ХО_УвеличениеСтоимостиАрендованныхОС                 КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УвеличениеСтоимостиАрендованныхОС        КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	втУвеличениеСтоимости КАК УвеличениеСтоимости
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.ОсновноеСредство = УвеличениеСтоимости.ОсновноеСредство";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УменьшениеСтоимостиОС // Дт <76.07.1> - Кт <01.03>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                КАК ВидДвижения,
	|	&Ссылка                                               КАК Регистратор,
	|	&Период                                               КАК Период,
	|	&Организация                                          КАК Организация,
	|	УменьшениеСтоимости.ОсновноеСредство                  КАК ОсновноеСредство,
	|	СведенияОбУчете.Местонахождение                       КАК Подразделение,
	|	СведенияОбУчете.ГруппаФинансовогоУчета                КАК ГруппаФинансовогоУчета,
	|	СведенияОбУчете.НаправлениеДеятельности               КАК НаправлениеДеятельности,
	|	СведенияОбУчете.Арендатор                             КАК Арендатор,
	|
	|	УменьшениеСтоимости.Стоимость                         КАК Стоимость,
	|	УменьшениеСтоимости.СтоимостьРегл                     КАК СтоимостьРегл,
	|	УменьшениеСтоимости.СтоимостьНУ                       КАК СтоимостьНУ,
	|
	|	УменьшениеСтоимости.СтоимостьРегл
	|	 - УменьшениеСтоимости.СтоимостьНУ                    КАК СтоимостьВР,
	|
	|	УменьшениеСтоимости.ЗалоговаяСтоимость                КАК ЗалоговаяСтоимость,
	|	0                                                     КАК НеУчитываемаяСтоимостьНУ,
	|	
	|	УменьшениеСтоимости.ИдентификаторСтроки               КАК ИдентификаторФинЗаписи,
	|	&ХО_УменьшениеСтоимостиАрендованныхОС                 КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УменьшениеСтоимостиАрендованныхОС        КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	втУменьшениеСтоимости КАК УменьшениеСтоимости
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.ОсновноеСредство = УменьшениеСтоимости.ОсновноеСредство";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаАмортизацияОС(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "АмортизацияОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтТаблицаОС(ТекстыЗапроса, "Документ.ИзменениеУсловийДоговораАренды", "втСведенияОбУчете");
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПустыеТаблицыОтложенныхДвижений(ТекстыЗапроса, Запрос);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаПереоценки(ТекстыЗапроса, "ОС", "Документ.ИзменениеУсловийДоговораАренды");

	СписокЗапросовОбъединение = Новый Массив;
	
	#Область УвеличениеНакопленнойАмортизации
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)         КАК ВидДвижения,
	|	&Период                                        КАК Период,
	|	&Организация                                   КАК Организация,
	|	СведенияОбУчете.Местонахождение                КАК Подразделение,
	|	ТаблицаПереоценки.ОсновноеСредство             КАК ОсновноеСредство,
	|	ТаблицаПереоценки.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	ТаблицаПереоценки.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Амортизация,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.НачислятьИзнос
	|			ТОГДА 0
	|		КОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК АмортизацияРегл,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииНУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияНУ,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.НачислятьИзнос
	|			ТОГДА 0
	|		КОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ
	|		ИНАЧЕ 0 
	|	КОНЕЦ 
	|		- ВЫБОР 
	|			КОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииНУ > 0
	|				ТОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииНУ
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК АмортизацияВР,
	|
	|	ТаблицаПереоценки.ИдентификаторФинЗаписи       КАК ИдентификаторФинЗаписи,
	|	&ХО_УвеличениеНакопленнойАмортизации           КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УвеличениеНакопленнойАмортизации  КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ТаблицаПереоценки КАК ТаблицаПереоценки
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.ОсновноеСредство = ТаблицаПереоценки.ОсновноеСредство
	|ГДЕ
	|	(ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ > 0
	|		ИЛИ ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ > 0
	|		ИЛИ ТаблицаПереоценки.СуммаДооценкиАмортизацииНУ > 0)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УменьшениеНакопленнойАмортизации
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)         КАК ВидДвижения,
	|	&Период                                        КАК Период,
	|	&Организация                                   КАК Организация,
	|	СведенияОбУчете.Местонахождение                КАК Подразделение,
	|	ТаблицаПереоценки.ОсновноеСредство             КАК ОсновноеСредство,
	|	ТаблицаПереоценки.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	ТаблицаПереоценки.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииУУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииУУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Амортизация,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииБУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииБУ
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК АмортизацияРегл,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииНУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК АмортизацияНУ,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииБУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииБУ
	|		ИНАЧЕ 0 
	|	КОНЕЦ
	|		- ВЫБОР 
	|			КОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииНУ > 0
	|				ТОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииНУ
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК АмортизацияВР,
	|
	|	ТаблицаПереоценки.ИдентификаторФинЗаписи       КАК ИдентификаторФинЗаписи,
	|	&ХО_УменьшениеНакопленнойАмортизации           КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УменьшениеНакопленнойАмортизации  КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ТаблицаПереоценки КАК ТаблицаПереоценки
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.ОсновноеСредство = ТаблицаПереоценки.ОсновноеСредство
	|ГДЕ
	|	(ТаблицаПереоценки.СуммаУценкиАмортизацииУУ > 0
	|		ИЛИ ТаблицаПереоценки.СуммаУценкиАмортизацииБУ > 0
	|		ИЛИ ТаблицаПереоценки.СуммаУценкиАмортизацииНУ > 0)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаАрендованныеОС(ТекстыЗапроса, Регистры)

	ИмяРегистра = "АрендованныеОС";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтАрендованныеОС(ТекстыЗапроса, "Документ.ИзменениеУсловийДоговораАренды");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                       КАК Период,
	|	ТаблицаОС.ОсновноеСредство    КАК ОсновноеСредство,
	|	ТаблицаОС.Стоимость           КАК Стоимость,
	|	&Партнер                      КАК Партнер,
	|	&Контрагент                   КАК Контрагент,
	|	&Договор                      КАК Договор,
	|	ВтАрендованныеОС.Состояние    КАК Состояние
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИзменениеУсловийДоговораАренды.ОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтАрендованныеОС КАК ВтАрендованныеОС
	|		ПО ВтАрендованныеОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И ТаблицаОС.Стоимость <> ВтАрендованныеОС.Стоимость";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаУсловияДоговоровАренды(ТекстыЗапроса, Регистры)

	ИмяРегистра = "УсловияДоговоровАренды";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Ссылка                                КАК Регистратор,
	|	&Период                                КАК Период,
	|	&Договор                               КАК Договор,
	|	&СуммаОбеспечительногоПлатежа          КАК СуммаОбеспечительногоПлатежа,
	|	&СуммаУслугПоАренде                    КАК СуммаУслугПоАренде,
	|	&СуммаВыкупаПредметовАренды            КАК СуммаВыкупаПредметовАренды,
	|	&СуммаПроцентов                        КАК СуммаПроцентов,
	|	&СуммаНДС                              КАК СуммаНДС,
	|	&СтавкаНДС                             КАК СтавкаНДС,
	|	&СрокЛет                               КАК СрокЛет,
	|	&СрокМес                               КАК СрокМес,
	|	&СрокДней                              КАК СрокДней,
	|	&ДатаНачалаАренды                      КАК ДатаНачалаАренды,
	|	&ДатаПервогоПлатежа                    КАК ДатаПервогоПлатежа,
	|	&ДатаПервогоОбеспечительногоПлатежа    КАК ДатаПервогоОбеспечительногоПлатежа,
	|	&ДатаВыкупа                            КАК ДатаВыкупа,
	|	&ПроцентнаяСтавка                      КАК ПроцентнаяСтавка,
	|	&ПериодичностьОплат                    КАК ПериодичностьОплат,
	|	&ПериодичностьНачислений               КАК ПериодичностьНачислений,
	|	&СтоимостьПредметовАренды              КАК СтоимостьПредметовАренды,
	|	&ОплатаУслугиПоАренде                  КАК ОплатаУслугиПоАренде,
	|	&НачислениеУслугиПоАренде              КАК НачислениеУслугиПоАренде,
	|	&ГрафикНачисленияПроцентовВведенВручную КАК ГрафикНачисленияПроцентовВведенВручную,
	|	&Ссылка                                КАК АктуальныеУсловияДоговора,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияДоговоровКонтрагентов.Действует) КАК Состояние
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
КонецПроцедуры

Функция ТекстЗапросаТаблицаГрафикОплатУслугПоАренде(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ГрафикОплатУслугПоАренде";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                 КАК ДатаИзменения,
	|	&Договор                                КАК Договор,
	|	&Ссылка                                 КАК АктуальныеУсловияДоговора,
	|	ТаблицаГрафика.Дата                     КАК Дата,
	|	ТаблицаГрафика.УслугаПоАренде           КАК УслугаПоАренде,
	|	ТаблицаГрафика.УслугаПоАрендеНДС        КАК УслугаПоАрендеНДС,
	|	ТаблицаГрафика.ОбеспечительныйПлатеж    КАК ОбеспечительныйПлатеж,
	|	ТаблицаГрафика.ОбеспечительныйПлатежНДС КАК ОбеспечительныйПлатежНДС,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаГрафика.Дата >= &ДатаВыкупа
	|			ТОГДА ТаблицаГрафика.ВыкупнаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВыкупнаяСтоимость,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаГрафика.Дата < &ДатаВыкупа
	|			ТОГДА ТаблицаГрафика.ВыкупнаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВыкупнаяСтоимостьАванс,
	|
	|	ТаблицаГрафика.ВыкупнаяСтоимостьНДС КАК ВыкупнаяСтоимостьНДС
	|
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ГрафикОплатУслуг КАК ТаблицаГрафика
	|ГДЕ
	|	ТаблицаГрафика.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаГрафикНачисленияУслугПоАренде(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ГрафикНачисленияУслугПоАренде";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                 КАК ДатаИзменения,
	|	&Договор                                КАК Договор,
	|	&Ссылка                                 КАК АктуальныеУсловияДоговора,
	|	ТаблицаГрафика.Дата                     КАК Дата,
	|	ТаблицаГрафика.УслугаПоАренде           КАК УслугаПоАренде,
	|	ТаблицаГрафика.УслугаПоАрендеНДС        КАК УслугаПоАрендеНДС,
	|	ТаблицаГрафика.ОбеспечительныйПлатеж    КАК ОбеспечительныйПлатеж,
	|	ТаблицаГрафика.ОбеспечительныйПлатежНДС КАК ОбеспечительныйПлатежНДС,
	|	ВЫБОР 
	|		КОГДА &ДатаВыкупа = ТаблицаГрафика.Дата
	|			ТОГДА &СуммаВыкупаПредметовАренды
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВыкупнаяСтоимость,
	|	ВЫБОР 
	|		КОГДА &ДатаВыкупа = ТаблицаГрафика.Дата
	|			ТОГДА &СуммаНДСВыкупаПредметовАренды
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВыкупнаяСтоимостьНДС
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ГрафикНачисленияУслуг КАК ТаблицаГрафика
	|ГДЕ
	|	ТаблицаГрафика.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                 КАК ДатаИзменения,
	|	&Договор                                КАК Договор,
	|	&Ссылка                                 КАК АктуальныеУсловияДоговора,
	|	&ДатаВыкупа                             КАК Дата,
	|	0                                       КАК УслугаПоАренде,
	|	0                                       КАК УслугаПоАрендеНДС,
	|	0                                       КАК ОбеспечительныйПлатеж,
	|	0                                       КАК ОбеспечительныйПлатежНДС,
	|	&СуммаВыкупаПредметовАренды             КАК ВыкупнаяСтоимость,
	|	&СуммаНДСВыкупаПредметовАренды          КАК ВыкупнаяСтоимостьНДС
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеУсловийДоговораАренды.ГрафикНачисленияУслуг КАК ТаблицаГрафика
	|		ПО ТаблицаГрафика.Ссылка = ДанныеДокумента.Ссылка
	|			И ТаблицаГрафика.Дата = ДанныеДокумента.ДатаВыкупа
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ТаблицаГрафика.Ссылка ЕСТЬ NULL
	|	И &СуммаВыкупаПредметовАренды <> 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаГрафикНачисленияПроцентовПоАренде(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ГрафикНачисленияПроцентовПоАренде";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                    КАК ДатаИзменения,
	|	&Договор                   КАК Договор,
	|	&Ссылка                    КАК АктуальныеУсловияДоговора,
	|	ТаблицаГрафика.Дата        КАК Дата,
	|	ТаблицаГрафика.Проценты    КАК Проценты
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ГрафикНачисленияПроцентов КАК ТаблицаГрафика
	|ГДЕ
	|	ТаблицаГрафика.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаРасчетыПоФинансовымИнструментам(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РасчетыПоФинансовымИнструментам";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ИнициализироватьКлючиАналитикиУчетаПоПартнерам(Запрос);

	ТекстЗапросаТаблицаВтУвеличениеСтоимости(Запрос, ТекстыЗапроса);
	ТекстЗапросаТаблицаВтУменьшениеСтоимости(Запрос, ТекстыЗапроса);
	ТекстЗапросаТаблицаВтКорректировкаРасчетов(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтИзлишнеНачисленныеПроценты(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтНедоначисленныеПроценты(ТекстыЗапроса);
	
	СписокЗапросовОбъединение = Новый Массив;
	
	#Область УвеличениеСтоимостиОС // Дт <01.03> - Кт <76.07.1>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)       КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	УвеличениеСтоимости.Стоимость                                           КАК Сумма,
	|	УвеличениеСтоимости.Стоимость                                           КАК СуммаУпр,
	|	УвеличениеСтоимости.СтоимостьРегл                                       КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	УвеличениеСтоимости.ИдентификаторСтроки                                 КАК ИдентификаторСтроки,	
	|	УвеличениеСтоимости.ИдентификаторСтроки                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УвеличениеСтоимостиАрендованныхОС                                   КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УвеличениеСтоимостиАрендованныхОС                          КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втУвеличениеСтоимости КАК УвеличениеСтоимости
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	(УвеличениеСтоимости.Стоимость <> 0
	|		ИЛИ УвеличениеСтоимости.СтоимостьРегл <> 0)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область УменьшениеСтоимостиОС // Дт <76.07.1> - Кт <01.03>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)       КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	УменьшениеСтоимости.Стоимость                                           КАК Сумма,
	|	УменьшениеСтоимости.Стоимость                                           КАК СуммаУпр,
	|	УменьшениеСтоимости.СтоимостьРегл                                       КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	УменьшениеСтоимости.ИдентификаторСтроки                                 КАК ИдентификаторСтроки,	
	|	УменьшениеСтоимости.ИдентификаторСтроки                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УменьшениеСтоимостиАрендованныхОС                                   КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УменьшениеСтоимостиАрендованныхОС                          КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втУменьшениеСтоимости КАК УменьшениеСтоимости
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	(УменьшениеСтоимости.Стоимость <> 0
	|		ИЛИ УменьшениеСтоимости.СтоимостьРегл <> 0)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область УвеличениеНДС // Дт <76.07.9> - Кт <76.07.1>
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.НДС)                         КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	Расчеты.СуммаНДС                                                        КАК Сумма,
	|	Расчеты.СуммаНДСУпр                                                     КАК СуммаУпр,
	|	Расчеты.СуммаНДСРегл                                                    КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УвеличениеНДСПоАренде                                               КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УвеличениеНДСПоАренде                                      КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаРасчетов КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	Расчеты.СуммаНДС > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)       КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	Расчеты.СуммаНДС                                                        КАК Сумма,
	|	Расчеты.СуммаНДСУпр                                                     КАК СуммаУпр,
	|	Расчеты.СуммаНДСРегл                                                    КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УвеличениеНДСПоАренде                                               КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УвеличениеНДСПоАренде                                      КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаРасчетов КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	Расчеты.СуммаНДС > 0";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УменьшениеНДС // Дт <76.07.1> - Кт <76.07.9>
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)       КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	-Расчеты.СуммаНДС                                                       КАК Сумма,
	|	-Расчеты.СуммаНДСУпр                                                    КАК СуммаУпр,
	|	-Расчеты.СуммаНДСРегл                                                   КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УменьшениеНДСПоАренде                                               КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УменьшениеНДСПоАренде                                      КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаРасчетов КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	Расчеты.СуммаНДС < 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.НДС)                         КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	-Расчеты.СуммаНДС                                                       КАК Сумма,
	|	-Расчеты.СуммаНДСУпр                                                    КАК СуммаУпр,
	|	-Расчеты.СуммаНДСРегл                                                   КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УменьшениеНДСПоАренде                                               КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УменьшениеНДСПоАренде                                      КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаРасчетов КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	Расчеты.СуммаНДС < 0";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область УвеличениеПроцентов // Дт <76.07.5> - Кт <76.07.1>
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.Проценты)                    КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	Расчеты.СуммаПроцентов                                                  КАК Сумма,
	|	Расчеты.СуммаПроцентовУпр                                               КАК СуммаУпр,
	|	Расчеты.СуммаПроцентовРегл                                              КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УвеличениеПроцентовПоАренде                                         КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УвеличениеПроцентовПоАренде                                КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаРасчетов КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	Расчеты.СуммаПроцентов > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)       КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	Расчеты.СуммаПроцентов                                                  КАК Сумма,
	|	Расчеты.СуммаПроцентовУпр                                               КАК СуммаУпр,
	|	Расчеты.СуммаПроцентовРегл                                              КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УвеличениеПроцентовПоАренде                                         КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УвеличениеПроцентовПоАренде                                КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаРасчетов КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	Расчеты.СуммаПроцентов > 0";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УменьшениеПроцентов // Дт <76.07.1> - Кт <76.07.5>
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.АрендныеОбязательства)       КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	-Расчеты.СуммаПроцентов                                                 КАК Сумма,
	|	-Расчеты.СуммаПроцентовУпр                                              КАК СуммаУпр,
	|	-Расчеты.СуммаПроцентовРегл                                             КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УменьшениеПроцентовПоАренде                                         КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УменьшениеПроцентовПоАренде                                КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаРасчетов КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	Расчеты.СуммаПроцентов < 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.Проценты)                    КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	-Расчеты.СуммаПроцентов                                                 КАК Сумма,
	|	-Расчеты.СуммаПроцентовУпр                                              КАК СуммаУпр,
	|	-Расчеты.СуммаПроцентовРегл                                             КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_УменьшениеПроцентовПоАренде                                         КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УменьшениеПроцентовПоАренде                                КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втКорректировкаРасчетов КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности
	|ГДЕ
	|	Расчеты.СуммаПроцентов < 0";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ИзлишнеНачисленныеПроценты // Дт <76.07.5> - Кт <91.01>
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.Проценты)                    КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	Расчеты.Сумма                                                           КАК Сумма,
	|	Расчеты.СуммаУпр                                                        КАК СуммаУпр,
	|	Расчеты.СуммаРегл                                                       КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_ИзлишнеНачисленныеПроцентыПоАренде                                  КАК ХозяйственнаяОперация,
	|	&НастройкаХО_ИзлишнеНачисленныеПроцентыПоАренде                         КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втИзлишнеНачисленныеПроценты КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область НедоначисленныеПроценты // Дт <91.02> - Кт <76.07.5>
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период                                                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                                  КАК ВидДвижения,
	|	
	|	ЕСТЬNULL(АналитикаПоПартнерам.КлючАналитики, НЕОПРЕДЕЛЕНО)              КАК АналитикаУчетаПоПартнерам,
	|	&Договор                                                                КАК Договор,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.Проценты)                    КАК ТипСуммы,
	|	НЕОПРЕДЕЛЕНО                                                            КАК РасчетныйДокумент,
	|	&ВалютаВзаиморасчетов                                                   КАК Валюта,
	|	
	|	Расчеты.Сумма                                                           КАК Сумма,
	|	Расчеты.СуммаУпр                                                        КАК СуммаУпр,
	|	Расчеты.СуммаРегл                                                       КАК СуммаРегл,
	|	
	|	НЕОПРЕДЕЛЕНО                                                            КАК ТипГрафика,
	|	НЕОПРЕДЕЛЕНО                                                            КАК СтатьяАналитики,
	|	НЕОПРЕДЕЛЕНО                                                            КАК ИдентификаторСтроки,
	|	&ИдентификаторДокумента                                                 КАК ИдентификаторФинЗаписи,
	|	&ХО_НедоначисленныеПроцентыПоАренде                                     КАК ХозяйственнаяОперация,
	|	&НастройкаХО_НедоначисленныеПроцентыПоАренде                            КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втНедоначисленныеПроценты КАК Расчеты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПоПартнерам
	|		ПО АналитикаПоПартнерам.Организация = &Организация
	|			И АналитикаПоПартнерам.Партнер = &Партнер
	|			И АналитикаПоПартнерам.Контрагент = &Контрагент
	|			И АналитикаПоПартнерам.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			И АналитикаПоПартнерам.НаправлениеДеятельности = &НаправлениеДеятельности";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ДвиженияПоПрочимАктивамПассивам";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтНедоначисленныеПроценты(ТекстыЗапроса);

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                           КАК Период,
	|	&Организация                      КАК Организация,
	|	СведенияОбУчете.Местонахождение   КАК Подразделение,
	|	&НаправлениеДеятельности          КАК НаправлениеДеятельности,
	|	&СтатьяРасходов                   КАК Статья,
	|	&АналитикаАктивовПассивовРасходы  КАК Аналитика,
	|	&НастройкаСчетовУчетаРасходы      КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаРегл,
	|
	|	0 КАК ПостояннаяРазница,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВременнаяРазница,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУпр,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаСНДС,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБезНДС,
	|
	|	ТаблицаПереоценки.ИдентификаторФинЗаписи       КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_УвеличениеНакопленнойАмортизации  КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ТаблицаПереоценки КАК ТаблицаПереоценки
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.ОсновноеСредство = ТаблицаПереоценки.ОсновноеСредство
	|ГДЕ
	|	(ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ > 0
	|		ИЛИ ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ > 0)
	|	И ТИПЗНАЧЕНИЯ(&СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                           КАК Период,
	|	&Организация                      КАК Организация,
	|	&ПодразделениеПроценты            КАК Подразделение,
	|	&НаправлениеДеятельности          КАК НаправлениеДеятельности,
	|	&СтатьяРасходов                   КАК Статья,
	|	&АналитикаАктивовПассивовРасходы  КАК Аналитика,
	|	&НастройкаСчетовУчетаРасходы      КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	ТаблицаРасходы.СуммаРегл КАК СуммаРегл,
	|
	|	0 КАК ПостояннаяРазница,
	|
	|	ТаблицаРасходы.СуммаРегл КАК ВременнаяРазница,
	|
	|	ТаблицаРасходы.СуммаУпр КАК СуммаУпр,
	|	ТаблицаРасходы.СуммаУпр КАК СуммаСНДС,
	|	ТаблицаРасходы.СуммаУпр КАК СуммаБезНДС,
	|
	|	&ИдентификаторДокумента КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_НедоначисленныеПроцентыПоАренде КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втНедоначисленныеПроценты КАК ТаблицаРасходы
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	ИмяРегистра = "ПартииПрочихРасходов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПрочиеДоходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;

	ТекстЗапросаТаблицаВтИзлишнеНачисленныеПроценты(ТекстыЗапроса);
	
	СписокЗапросовОбъединение = Новый Массив;
	
	#Область ИзлишнеНачисленныеПроценты
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)            КАК ВидДвижения,
	|	&Период                                           КАК Период,
	|	&Организация                                      КАК Организация,
	|	&Подразделение                                    КАК Подразделение,
	|	&НаправлениеДеятельности                          КАК НаправлениеДеятельности,
	|	&СтатьяДоходов                                    КАК СтатьяДоходов,
	|	&АналитикаДоходов                                 КАК АналитикаДоходов,
	|
	|	ТаблицаДоходы.Сумма                               КАК Сумма,
	|	ТаблицаДоходы.СуммаУпр                            КАК СуммаУпр,
	|	ТаблицаДоходы.СуммаРегл                           КАК СуммаРегл,
	|
	|	&ИдентификаторДокумента                           КАК ИдентификаторФинЗаписи,
	|	&ХО_ИзлишнеНачисленныеПроцентыПоАренде            КАК ХозяйственнаяОперация,
	|	&НастройкаХО_ИзлишнеНачисленныеПроцентыПоАренде   КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втИзлишнеНачисленныеПроценты КАК ТаблицаДоходы
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&СтатьяДоходов) = ТИП(ПланВидовХарактеристик.СтатьиДоходов)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УменьшениеНакопленнойАмортизации
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)            КАК ВидДвижения,
	|	&Период                                           КАК Период,
	|	&Организация                                      КАК Организация,
	|	СведенияОбУчете.Местонахождение                   КАК Подразделение,
	|	&НаправлениеДеятельности                          КАК НаправлениеДеятельности,
	|	&СтатьяДоходов                                    КАК СтатьяДоходов,
	|	&АналитикаДоходов                                 КАК АналитикаДоходов,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииУУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииУУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Сумма,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииУУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииУУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУпр,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииБУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаУценкиАмортизацииБУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаРегл,
	|
	|	ТаблицаПереоценки.ИдентификаторФинЗаписи          КАК ИдентификаторФинЗаписи,
	|	&ХО_УменьшениеНакопленнойАмортизации              КАК ХозяйственнаяОперация,
	|	&НастройкаХО_УменьшениеНакопленнойАмортизации     КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ТаблицаПереоценки КАК ТаблицаПереоценки
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.ОсновноеСредство = ТаблицаПереоценки.ОсновноеСредство
	|ГДЕ
	|	(ТаблицаПереоценки.СуммаУценкиАмортизацииУУ > 0
	|		ИЛИ ТаблицаПереоценки.СуммаУценкиАмортизацииБУ > 0)
	|	И ТИПЗНАЧЕНИЯ(&СтатьяДоходов) = ТИП(ПланВидовХарактеристик.СтатьиДоходов)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&Организация                            КАК Организация,
	|	&Партнер                                КАК Партнер,
	|	&Контрагент                             КАК Контрагент,
	|	&Договор                                КАК Договор,
	|	&Подразделение                          КАК Подразделение,
	|	&Ответственный                          КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	&СуммаДокумента                         КАК Сумма,
	|	&Валюта                                 КАК Валюта,
	|	&Период                                 КАК ДатаОтраженияВУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоОС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0)    КАК НомерЗаписи,
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК Дата,
	|	&Организация                            КАК Организация,
	|	&Подразделение                          КАК Подразделение,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Проведен                               КАК Проведен,
	|	ИСТИНА                                  КАК ОтражатьВРеглУчете,
	|	ИСТИНА                                  КАК ОтражатьВУпрУчете,
	|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеУсловийДоговораАренды.ОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
	Возврат ТекстЗапроса;
	
КонецФункции

//++ НЕ УТКА

Функция ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВМеждународномУчете";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                        КАК Период,
	|	&Организация                   КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)   КАК ДатаОтражения,
	|	&ХозяйственнаяОперация         КАК ХозяйственнаяОперация";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

//-- НЕ УТКА

#КонецОбласти

#Область ФормированиеВременныхТаблицыДляПроведения

Процедура ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяТаблицы = "ВтПрочиеРасходы";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяТаблицы = "ВтИсходныеПрочиеРасходы";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтНедоначисленныеПроценты(ТекстыЗапроса);
	
	СписокЗапросовОбъединение = Новый Массив;
	СписокЗапросовОбъединение.Добавить(РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы());
	
	#Область НедоначисленныеПроценты // Дт <91.02> - Кт <76.07.5>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&ПодразделениеПроценты КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	
	|	ТаблицаРасходы.СуммаУпр КАК СуммаСНДС,
	|	ТаблицаРасходы.СуммаУпр КАК СуммаБезНДС,
	|	ТаблицаРасходы.СуммаУпр КАК СуммаБезНДСУпр,
	|	ТаблицаРасходы.СуммаРегл КАК СуммаСНДСРегл,
	|	ТаблицаРасходы.СуммаРегл КАК СуммаБезНДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	ТаблицаРасходы.СуммаРегл КАК ВременнаяРазница,
	|	
	|	&ХО_НедоначисленныеПроцентыПоАренде КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|
	|	&ИдентификаторДокумента КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_НедоначисленныеПроцентыПоАренде КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втНедоначисленныеПроценты КАК ТаблицаРасходы
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область УвеличениеНакопленнойАмортизации // Дт <91.02> - Кт <02.03>
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	СведенияОбУчете.Местонахождение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаСНДС,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБезНДС,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБезНДСУпр,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаСНДСРегл,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБезНДСРегл,
	|
	|	0 КАК ПостояннаяРазница,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ > 0
	|			ТОГДА ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВременнаяРазница,
	|	
	|	&ХО_УвеличениеНакопленнойАмортизации КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|
	|	ТаблицаПереоценки.ИдентификаторФинЗаписи       КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_УвеличениеНакопленнойАмортизации  КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ТаблицаПереоценки КАК ТаблицаПереоценки
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСведенияОбУчете КАК СведенияОбУчете
	|		ПО СведенияОбУчете.ОсновноеСредство = ТаблицаПереоценки.ОсновноеСредство
	|ГДЕ
	|	(ТаблицаПереоценки.СуммаДооценкиАмортизацииУУ > 0
	|		ИЛИ ТаблицаПереоценки.СуммаДооценкиАмортизацииБУ > 0)
	|	И ТИПЗНАЧЕНИЯ(&СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяТаблицы = "ВтПартииПрочихРасходов";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаВтПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяТаблицы = "ВтИсходныеПартииПрочихРасходов";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;

	ТекстЗапросаТаблицаВтНедоначисленныеПроценты(ТекстыЗапроса);
	
	СписокЗапросовОбъединение = Новый Массив;
	СписокЗапросовОбъединение.Добавить(РегистрыНакопления.ПартииПрочихРасходов.ТекстОписаниеВтИсходныеПартииПрочихРасходов());

	#Область НедоначисленныеПроценты
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                         КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	&Организация                                    КАК Организация,
	|	&ПодразделениеПроценты                          КАК Подразделение,
	|	&СтатьяРасходовПроценты                         КАК СтатьяРасходов,
	|	&АналитикаРасходовПроценты                      КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                                    КАК АналитикаАктивовПассивов,
	|	&Ссылка                                         КАК ДокументПоступленияРасходов,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
	|	&НаправлениеДеятельности                        КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                    КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО                                    КАК ВидДеятельностиНДС,
	|
	|	ТаблицаРасходы.СуммаУпр                         КАК Стоимость,
	|	ТаблицаРасходы.СуммаУпр                         КАК СтоимостьБезНДС,
	|	0                                               КАК НДСУпр,
	|	ТаблицаРасходы.СуммаРегл                        КАК СтоимостьРегл,
	|	0                                               КАК НДСРегл,
	|	0                                               КАК ПостояннаяРазница,
	|	ТаблицаРасходы.СуммаРегл                        КАК ВременнаяРазница,
	|
	|	&ХО_НедоначисленныеПроцентыПоАренде             КАК ХозяйственнаяОперация,
	|	&ИдентификаторДокумента                         КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_НедоначисленныеПроцентыПоАренде    КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	втНедоначисленныеПроценты КАК ТаблицаРасходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК ПВХСтатьиРасходов
	|		ПО &СтатьяРасходовПроценты = ПВХСтатьиРасходов.Ссылка
	|ГДЕ
	|	ПВХСтатьиРасходов.ВариантРаспределенияРасходовРегл В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров))";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);

КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтУвеличениеСтоимости(Запрос, ТекстыЗапроса)

	ИмяТаблицы = "ВтУвеличениеСтоимости";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтКорректировкаСтоимости(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КорректировкаСтоимости.ОсновноеСредство     КАК ОсновноеСредство,
	|	КорректировкаСтоимости.ИдентификаторСтроки  КАК ИдентификаторСтроки,
	|
	// Стоимость
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.Стоимость > 0
	|			ТОГДА КорректировкаСтоимости.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Стоимость,
	|
	// СтоимостьРегл
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьРегл > 0
	|			ТОГДА КорректировкаСтоимости.СтоимостьРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	// СтоимостьНУ
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьНУ > 0
	|			ТОГДА КорректировкаСтоимости.СтоимостьНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьНУ,
	|
	// ЗалоговаяСтоимость
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.ЗалоговаяСтоимость > 0
	|			ТОГДА КорректировкаСтоимости.ЗалоговаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЗалоговаяСтоимость
	|
	|ПОМЕСТИТЬ ВтУвеличениеСтоимости
	|ИЗ
	|	ВтКорректировкаСтоимости КАК КорректировкаСтоимости
	|ГДЕ
	|	КорректировкаСтоимости.Стоимость > 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьРегл > 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьНУ > 0
	|		ИЛИ КорректировкаСтоимости.ЗалоговаяСтоимость > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтУменьшениеСтоимости(Запрос, ТекстыЗапроса)

	ИмяТаблицы = "ВтУменьшениеСтоимости";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтКорректировкаСтоимости(Запрос, ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КорректировкаСтоимости.ОсновноеСредство     КАК ОсновноеСредство,
	|	КорректировкаСтоимости.ИдентификаторСтроки  КАК ИдентификаторСтроки,
	|
	// Стоимость
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.Стоимость < 0
	|			ТОГДА -КорректировкаСтоимости.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК Стоимость,
	|
	// СтоимостьРегл
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьРегл < 0
	|			ТОГДА -КорректировкаСтоимости.СтоимостьРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьРегл,
	|
	// СтоимостьНУ
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.СтоимостьНУ < 0
	|			ТОГДА -КорректировкаСтоимости.СтоимостьНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК СтоимостьНУ,
	|
	// ЗалоговаяСтоимость
	|	ВЫБОР 
	|		КОГДА КорректировкаСтоимости.ЗалоговаяСтоимость < 0
	|			ТОГДА -КорректировкаСтоимости.ЗалоговаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ                                       КАК ЗалоговаяСтоимость
	|
	|ПОМЕСТИТЬ ВтУменьшениеСтоимости
	|ИЗ
	|	ВтКорректировкаСтоимости КАК КорректировкаСтоимости
	|ГДЕ
	|	КорректировкаСтоимости.Стоимость < 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьРегл < 0
	|		ИЛИ КорректировкаСтоимости.СтоимостьНУ < 0
	|		ИЛИ КорректировкаСтоимости.ЗалоговаяСтоимость < 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтКорректировкаСтоимости(Запрос, ТекстыЗапроса)

	ИмяТаблицы = "втКорректировкаСтоимости";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПустыеТаблицыОтложенныхДвижений(ТекстыЗапроса, Запрос);

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОС.ОсновноеСредство    КАК ОсновноеСредство,
	|	ТаблицаОС.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|
	// Стоимость
	|	ВЫБОР 
	|		КОГДА &ЕстьУчетУУ 
	|			ТОГДА ТаблицаОС.СтоимостьУУ - ЕСТЬNULL(СтоимостьОС.Стоимость, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Стоимость,
	|
	// СтоимостьРегл
	|	ВЫБОР 
	|		КОГДА &ЕстьУчетБУ
	|			ТОГДА ТаблицаОС.СтоимостьБУ - ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) - ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	// СтоимостьНУ
	|	ВЫБОР 
	|		КОГДА &ЕстьУчетНУ
	|			ТОГДА ТаблицаОС.СтоимостьНУ - ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьНУ,
	|
	// ЗалоговаяСтоимость
	|	ВЫБОР 
	|		КОГДА &Балансодержатель = ЗНАЧЕНИЕ(Перечисление.БалансодержательПредметовАренды.Арендодатель)
	|				И НЕ &ИспользуетсяУчетАрендыПоФСБУ25_2018
	|			ТОГДА ТаблицаОС.Стоимость - ЕСТЬNULL(СтоимостьОС.ЗалоговаяСтоимость, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЗалоговаяСтоимость
	|
	|ПОМЕСТИТЬ втКорректировкаСтоимости
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И СтоимостьОС.Ссылка = &Ссылка
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И (ЕСТЬNULL(СтоимостьОС.Стоимость, 0) <> ТаблицаОС.СтоимостьУУ 
	|				И &ЕстьУчетУУ 
	|
	|		ИЛИ (ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0)) <> ТаблицаОС.СтоимостьБУ 
	|				И &ЕстьУчетБУ 
	|
	|		ИЛИ ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0) <> ТаблицаОС.СтоимостьНУ
	|
	|		ИЛИ ТаблицаОС.Стоимость <> ЕСТЬNULL(СтоимостьОС.ЗалоговаяСтоимость, 0)
	|				И &Балансодержатель = ЗНАЧЕНИЕ(Перечисление.БалансодержательПредметовАренды.Арендодатель)
	|				И НЕ &ИспользуетсяУчетАрендыПоФСБУ25_2018)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтИзлишнеНачисленныеПроценты(ТекстыЗапроса)
	
	ИмяТаблицы = "втИзлишнеНачисленныеПроценты";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтКорректировкаНачисленныхПроцентов(ТекстыЗапроса);

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	-Т.Сумма КАК Сумма,
	|	-Т.СуммаУпр КАК СуммаУпр,
	|	-Т.СуммаРегл КАК СуммаРегл
	|
	|ПОМЕСТИТЬ втИзлишнеНачисленныеПроценты
	|ИЗ
	|	втКорректировкаНачисленныхПроцентов КАК Т
	|ГДЕ
	|	Т.Сумма < 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтНедоначисленныеПроценты(ТекстыЗапроса)
	
	ИмяТаблицы = "втНедоначисленныеПроценты";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтКорректировкаНачисленныхПроцентов(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Т.Сумма КАК Сумма,
	|	Т.СуммаУпр КАК СуммаУпр,
	|	Т.СуммаРегл КАК СуммаРегл
	|
	|ПОМЕСТИТЬ втНедоначисленныеПроценты
	|ИЗ
	|	втКорректировкаНачисленныхПроцентов КАК Т
	|ГДЕ
	|	Т.Сумма > 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтКорректировкаНачисленныхПроцентов(ТекстыЗапроса)
	
	ИмяТаблицы = "втКорректировкаНачисленныхПроцентов";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтПредыдущиеУсловияДоговора(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СУММА(Графики.Сумма) КАК Сумма,
	|
	|	ВЫБОР
	|		КОГДА НЕ &ЕстьУчетУУ
	|			ТОГДА 0
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|			ТОГДА СУММА(Графики.Сумма)
	|		ИНАЧЕ СУММА(ВЫРАЗИТЬ(Графики.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2)))
	|	КОНЕЦ КАК СуммаУпр,
	|
	|	ВЫБОР
	|		КОГДА НЕ &ЕстьУчетБУ
	|			ТОГДА 0
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|			ТОГДА СУММА(Графики.Сумма)
	|		ИНАЧЕ СУММА(ВЫРАЗИТЬ(Графики.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2)))
	|	КОНЕЦ КАК СуммаРегл
	|
	|ПОМЕСТИТЬ втКорректировкаНачисленныхПроцентов
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТекущийГрафик.Проценты КАК Сумма
	|	ИЗ
	|		Документ.ИзменениеУсловийДоговораАренды.ГрафикНачисленияПроцентов КАК ТекущийГрафик
	|	ГДЕ
	|		ТекущийГрафик.Ссылка = &Ссылка
	|		И (ТекущийГрафик.Дата < &НачалоМесяца
	|				ИЛИ &СозданДляПерехода И ТекущийГрафик.Дата <= КОНЕЦПЕРИОДА(&Период, ДЕНЬ))
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		-ПредыдущийГрафик.Проценты
	|	ИЗ
	|		РегистрСведений.ГрафикНачисленияПроцентовПоАренде КАК ПредыдущийГрафик
	|	ГДЕ
	|		ПредыдущийГрафик.Дата < &НачалоМесяца
	|		И ПредыдущийГрафик.Активность
	|		И ПредыдущийГрафик.Договор = &Договор
	|		И ПредыдущийГрафик.АктуальныеУсловияДоговора В (
	|			ВЫБРАТЬ
	|				Т.АктуальныеУсловияДоговора 
	|			ИЗ 
	|				втПредыдущиеУсловияДоговора КАК Т)
	|
	|	) КАК Графики";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтКорректировкаРасчетов(ТекстыЗапроса)
	
	ИмяТаблицы = "втКорректировкаРасчетов";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтПредыдущиеУсловияДоговора(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтСуммаПроцентов(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	УсловияДоговоровАренды.АктуальныеУсловияДоговора,
	|	
	|	&СуммаНДС - УсловияДоговоровАренды.СуммаНДС КАК СуммаНДС,
	|	ЕСТЬNULL(втСуммаПроцентов.Сумма, 0) - УсловияДоговоровАренды.СуммаПроцентов КАК СуммаПроцентов,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|			ТОГДА &СуммаНДС - УсловияДоговоровАренды.СуммаНДС
	|		ИНАЧЕ ВЫРАЗИТЬ((&СуммаНДС - УсловияДоговоровАренды.СуммаНДС) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаНДСУпр,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|			ТОГДА &СуммаНДС - УсловияДоговоровАренды.СуммаНДС
	|		ИНАЧЕ ВЫРАЗИТЬ((&СуммаНДС - УсловияДоговоровАренды.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаНДСРегл,
	|
	|	ВЫБОР
	|		КОГДА НЕ &ЕстьУчетУУ
	|			ТОГДА 0
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|			ТОГДА ЕСТЬNULL(втСуммаПроцентов.Сумма, 0) - УсловияДоговоровАренды.СуммаПроцентов
	|		ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втСуммаПроцентов.Сумма, 0) - УсловияДоговоровАренды.СуммаПроцентов) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаПроцентовУпр,
	|
	|	ВЫБОР
	|		КОГДА НЕ &ЕстьУчетБУ
	|			ТОГДА 0
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|			ТОГДА ЕСТЬNULL(втСуммаПроцентов.Сумма, 0) - УсловияДоговоровАренды.СуммаПроцентов
	|		ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втСуммаПроцентов.Сумма, 0) - УсловияДоговоровАренды.СуммаПроцентов) * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаПроцентовРегл
	|
	|ПОМЕСТИТЬ втКорректировкаРасчетов
	|ИЗ
	|	втПредыдущиеУсловияДоговора КАК УсловияДоговоровАренды
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСуммаПроцентов КАК втСуммаПроцентов
	|		ПО ИСТИНА";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтПредыдущиеУсловияДоговора(ТекстыЗапроса)
	
	ИмяТаблицы = "втПредыдущиеУсловияДоговора";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	УсловияДоговоровАренды.АктуальныеУсловияДоговора,
	|	УсловияДоговоровАренды.СуммаНДС КАК СуммаНДС,
	|	УсловияДоговоровАренды.СуммаПроцентов КАК СуммаПроцентов
	|ПОМЕСТИТЬ втПредыдущиеУсловияДоговора
	|ИЗ
	|	РегистрСведений.УсловияДоговоровАренды.СрезПоследних(&Период, Договор = &Договор И Регистратор <> &Ссылка) КАК УсловияДоговоровАренды";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтСуммаПроцентов(ТекстыЗапроса)

	ИмяТаблицы = "втСуммаПроцентов";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&СуммаПроцентов КАК Сумма
	|ПОМЕСТИТЬ втСуммаПроцентов
	|ГДЕ
	|	НЕ &СозданДляПерехода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(ТекущийГрафик.Проценты)
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ГрафикНачисленияПроцентов КАК ТекущийГрафик
	|ГДЕ
	|	ТекущийГрафик.Ссылка = &Ссылка
	|	И ТекущийГрафик.Дата > &КонецМесяца
	|	И &СозданДляПерехода";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат ИзменениеУсловийДоговораАрендыЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат "";

КонецФункции

#КонецОбласти

#Область Заполнение

// Заполняет документ на основании договора аренды.
// 
// Параметры:
// 	Объект - ДокументОбъект.ИзменениеУсловийДоговораАренды - 
Процедура ЗаполнитьНаОснованииДоговораАренды(Объект) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Объект.Договор) Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ОС.Очистить();
	Объект.ГрафикОплатУслуг.Очистить();
	Объект.ГрафикНачисленияУслуг.Очистить();
	Объект.ГрафикНачисленияПроцентов.Очистить();
	
	ТекстыПакетаЗапросов = Новый Массив;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	АрендованныеОС.ОсновноеСредство КАК ОсновноеСредство,
	|	АрендованныеОС.Стоимость КАК Стоимость
	|ПОМЕСТИТЬ втАрендованныеОС
	|ИЗ
	|	РегистрСведений.АрендованныеОС.СрезПоследних(&Дата) КАК АрендованныеОС
	|ГДЕ
	|	АрендованныеОС.Договор = &Договор
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УсловияДоговоровАренды.Договор.Организация КАК Организация,
	|	УсловияДоговоровАренды.Договор.Партнер КАК Партнер,
	|	УсловияДоговоровАренды.Договор.Контрагент КАК Контрагент,
	|	УсловияДоговоровАренды.Договор.ВалютаВзаиморасчетов КАК Валюта,
	|	УсловияДоговоровАренды.Договор.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	УсловияДоговоровАренды.СтавкаНДС КАК СтавкаНДС,
	|	УсловияДоговоровАренды.СуммаОбеспечительногоПлатежа КАК СуммаОбеспечительногоПлатежа,
	|	УсловияДоговоровАренды.СуммаУслугПоАренде КАК СуммаУслугПоАренде,
	|	УсловияДоговоровАренды.СуммаВыкупаПредметовАренды КАК СуммаВыкупаПредметовАренды,
	|	УсловияДоговоровАренды.СуммаПроцентов КАК СуммаПроцентов,
	|	УсловияДоговоровАренды.СтоимостьПредметовАренды КАК СтоимостьПредметовАренды,
	|	УсловияДоговоровАренды.СуммаНДС КАК СуммаНДС,
	|	УсловияДоговоровАренды.СрокЛет КАК СрокЛет,
	|	УсловияДоговоровАренды.СрокМес КАК СрокМес,
	|	УсловияДоговоровАренды.СрокДней КАК СрокДней,
	|	УсловияДоговоровАренды.ДатаНачалаАренды КАК ДатаНачалаАренды,
	|	УсловияДоговоровАренды.ДатаПервогоПлатежа КАК ДатаПервогоПлатежа,
	|	УсловияДоговоровАренды.ДатаПервогоОбеспечительногоПлатежа КАК ДатаПервогоОбеспечительногоПлатежа,
	|	УсловияДоговоровАренды.ДатаВыкупа КАК ДатаВыкупа,
	|	УсловияДоговоровАренды.ПроцентнаяСтавка КАК ПроцентнаяСтавка,
	|	УсловияДоговоровАренды.ПериодичностьОплат КАК ПериодичностьОплат,
	|	УсловияДоговоровАренды.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	УсловияДоговоровАренды.ОплатаУслугиПоАренде КАК ОплатаУслугиПоАренде,
	|	УсловияДоговоровАренды.НачислениеУслугиПоАренде КАК НачислениеУслугиПоАренде,
	|	УсловияДоговоровАренды.АктуальныеУсловияДоговора КАК АктуальныеУсловияДоговора
	|ПОМЕСТИТЬ втУсловияДоговоровАренды
	|ИЗ
	|	РегистрСведений.УсловияДоговоровАренды.СрезПоследних(
	|		&Дата, 
	|		Регистратор <> &Ссылка
	|			И Договор = &Договор) КАК УсловияДоговоровАренды
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УсловияДоговоровАренды.Организация КАК Организация,
	|	УсловияДоговоровАренды.Партнер КАК Партнер,
	|	УсловияДоговоровАренды.Контрагент КАК Контрагент,
	|	УсловияДоговоровАренды.Валюта КАК Валюта,
	|	УсловияДоговоровАренды.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	УсловияДоговоровАренды.СтавкаНДС КАК СтавкаНДС,
	|	УсловияДоговоровАренды.СуммаОбеспечительногоПлатежа КАК СуммаОбеспечительногоПлатежа,
	|	УсловияДоговоровАренды.СуммаУслугПоАренде КАК СуммаУслугПоАренде,
	|	УсловияДоговоровАренды.СуммаВыкупаПредметовАренды КАК СуммаВыкупаПредметовАренды,
	|	УсловияДоговоровАренды.СуммаПроцентов КАК СуммаПроцентов,
	|	УсловияДоговоровАренды.СтоимостьПредметовАренды КАК СтоимостьПредметовАренды,
	|	УсловияДоговоровАренды.СуммаНДС КАК СуммаНДС,
	|	УсловияДоговоровАренды.СрокЛет КАК СрокЛет,
	|	УсловияДоговоровАренды.СрокМес КАК СрокМес,
	|	УсловияДоговоровАренды.СрокДней КАК СрокДней,
	|	УсловияДоговоровАренды.ДатаНачалаАренды КАК ДатаНачалаАренды,
	|	УсловияДоговоровАренды.ДатаПервогоПлатежа КАК ДатаПервогоПлатежа,
	|	УсловияДоговоровАренды.ДатаПервогоОбеспечительногоПлатежа КАК ДатаПервогоОбеспечительногоПлатежа,
	|	УсловияДоговоровАренды.ДатаВыкупа КАК ДатаВыкупа,
	|	УсловияДоговоровАренды.ПроцентнаяСтавка КАК ПроцентнаяСтавка,
	|	УсловияДоговоровАренды.ПериодичностьОплат КАК ПериодичностьОплат,
	|	УсловияДоговоровАренды.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	УсловияДоговоровАренды.ОплатаУслугиПоАренде КАК ОплатаУслугиПоАренде,
	|	УсловияДоговоровАренды.НачислениеУслугиПоАренде КАК НачислениеУслугиПоАренде
	|ИЗ
	|	втУсловияДоговоровАренды КАК УсловияДоговоровАренды
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеГрафика.Дата КАК Дата,
	|	ДанныеГрафика.УслугаПоАренде КАК УслугаПоАренде,
	|	ДанныеГрафика.ОбеспечительныйПлатеж КАК ОбеспечительныйПлатеж,
	|	ДанныеГрафика.ВыкупнаяСтоимость + ДанныеГрафика.ВыкупнаяСтоимостьАванс КАК ВыкупнаяСтоимость
	|ИЗ
	|	РегистрСведений.ГрафикОплатУслугПоАренде КАК ДанныеГрафика
	|ГДЕ
	|	ДанныеГрафика.АктуальныеУсловияДоговора В (ВЫБРАТЬ Т.АктуальныеУсловияДоговора ИЗ втУсловияДоговоровАренды КАК Т)
	|	И ДанныеГрафика.Договор = &Договор
	|	И ДанныеГрафика.Активность
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеГрафика.Дата КАК Дата,
	|	ДанныеГрафика.УслугаПоАренде КАК УслугаПоАренде,
	|	ДанныеГрафика.ОбеспечительныйПлатеж КАК ОбеспечительныйПлатеж
	|ИЗ
	|	РегистрСведений.ГрафикНачисленияУслугПоАренде КАК ДанныеГрафика
	|ГДЕ
	|	ДанныеГрафика.АктуальныеУсловияДоговора В (ВЫБРАТЬ Т.АктуальныеУсловияДоговора ИЗ втУсловияДоговоровАренды КАК Т)
	|	И ДанныеГрафика.Договор = &Договор
	|	И ДанныеГрафика.Активность
	|	И (ДанныеГрафика.УслугаПоАренде <> 0 
	|		ИЛИ ДанныеГрафика.ОбеспечительныйПлатеж <> 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеГрафика.Дата КАК Дата,
	|	ДанныеГрафика.Проценты КАК Проценты
	|ИЗ
	|	РегистрСведений.ГрафикНачисленияПроцентовПоАренде КАК ДанныеГрафика
	|ГДЕ
	|	ДанныеГрафика.АктуальныеУсловияДоговора В (ВЫБРАТЬ Т.АктуальныеУсловияДоговора ИЗ втУсловияДоговоровАренды КАК Т)
	|	И ДанныеГрафика.Договор = &Договор
	|	И ДанныеГрафика.Активность
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата";
	ТекстыПакетаЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = ИзменениеУсловийДоговораАрендыЛокализация.ТекстЗапросаЗаполнениеТЧПоДоговору();
	Если ТекстЗапроса = Неопределено Тогда
		ТекстЗапроса =	
		"ВЫБРАТЬ
		|	АрендованныеОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ЕСТЬNULL(СтоимостьОС.СтоимостьОстаток, 0) КАК СтоимостьУУ,
		|	ЕСТЬNULL(СтоимостьОС.СтоимостьРеглОстаток, 0) КАК СтоимостьБУ,
		|	ЕСТЬNULL(ПараметрыАмортизацииОСУУ.СрокИспользования, 0) КАК СрокИспользованияУУ
		|ИЗ
		|	втАрендованныеОС КАК АрендованныеОС
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС.Остатки(
		|			&ПериодГраница, 
		|			Организация = &Организация
		|				И ОсновноеСредство В (
		|					ВЫБРАТЬ 
		|						втАрендованныеОС.ОсновноеСредство 
		|					ИЗ 
		|						втАрендованныеОС КАК втАрендованныеОС)) КАК СтоимостьОС
		|		ПО СтоимостьОС.ОсновноеСредство = АрендованныеОС.ОсновноеСредство
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСУУ.СрезПоследних(
		|			&Дата, 
		|			Регистратор <> &Ссылка
		|				И ОсновноеСредство В (
		|					ВЫБРАТЬ 
		|						втАрендованныеОС.ОсновноеСредство 
		|					ИЗ 
		|						втАрендованныеОС КАК втАрендованныеОС)) КАК ПараметрыАмортизацииОСУУ
		|		ПО ПараметрыАмортизацииОСУУ.ОсновноеСредство = АрендованныеОС.ОсновноеСредство
		|
		|УПОРЯДОЧИТЬ ПО
		|	АрендованныеОС.ОсновноеСредство.Наименование,
		|	АрендованныеОС.ОсновноеСредство.ИнвентарныйНомер";
	КонецЕсли;
	ТекстыПакетаЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(ТекстыПакетаЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	ДатаДок = ?(Объект.Дата <> '000101010000', Объект.Дата, ТекущаяДатаСеанса());
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Договор", Объект.Договор);
	Запрос.УстановитьПараметр("Дата", ДатаДок);
	Запрос.УстановитьПараметр("ПериодГраница", Новый Граница(Новый МоментВремени(ДатаДок, Объект.Ссылка), ВидГраницы.Исключая));
	
	Результат = Запрос.ВыполнитьПакет();
	Если Результат[0].Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат[Результат.ВГраница()-4].Выбрать();
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(Объект, Выборка);
	
	Объект.ГрафикОплатУслуг.Загрузить(Результат[Результат.ВГраница()-3].Выгрузить());
	Объект.ГрафикНачисленияУслуг.Загрузить(Результат[Результат.ВГраница()-2].Выгрузить());
	Объект.ГрафикНачисленияПроцентов.Загрузить(Результат[Результат.ВГраница()-1].Выгрузить());
	Объект.ОС.Загрузить(Результат[Результат.ВГраница()].Выгрузить());
	
	ВнеоборотныеАктивыКлиентСервер.ПересчитатьСуммуДокументаАренды(Объект);
	ВнеоборотныеАктивыКлиентСервер.ПересчитатьСуммуНДСВДокументеАренды(Объект);
	
КонецПроцедуры

#КонецОбласти

#Область Печать

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = Справочники.ДоговорыАренды.ДобавитьКомандуРасшифровкиРасчетаПроцентов(КомандыПечати);
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "ГрафикНачисленияПроцентовВведенВручную", Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ПараметрыВыбораСтатейИАналитик() Экспорт

	СписокПараметров = Новый Массив;
	
	#Область Расходы
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	ПараметрыВыбора.ТипСтатьи = "ТипСтатьиРасходы";
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивовРасходы";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("АналитикаАктивовПассивовРасходы");
	
	СписокПараметров.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	#Область Доходы
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяДоходов";
	
	ПараметрыВыбора.ВыборСтатьиДоходов = Истина;
	ПараметрыВыбора.АналитикаДоходов = "АналитикаДоходов";

	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяДоходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаДоходов.Добавить("АналитикаДоходов");
	
	СписокПараметров.Добавить(ПараметрыВыбора);
	#КонецОбласти

	Возврат СписокПараметров;
	
КонецФункции

Функция ПараметрыНастройкиСчетовУчета() Экспорт
	
	СписокПараметров = Новый Массив;

	#Область Расходы
	ПараметрыНастройки = НастройкаСчетовУчетаСервер.ПараметрыНастройки();
	ПараметрыНастройки.ПутьКДанным = "Объект";
	ПараметрыНастройки.Представление = "ПредставлениеОтраженияВРеглУчетеРасходы";
	ПараметрыНастройки.ТипСтатьи = "ТипСтатьиРасходы";
	ПараметрыНастройки.НастройкаСчетовУчета = "НастройкаСчетовУчетаРасходы";
	
	ПараметрыНастройки.Организация = "Объект.Организация";
	ПараметрыНастройки.АналитикаАктивовПассивов = "АналитикаАктивовПассивовРасходы";

	ПараметрыНастройки.ЭлементыФормы.Добавить("ПредставлениеОтраженияВРеглУчетеРасходы");
	
	СписокПараметров.Добавить(ПараметрыНастройки);
	#КонецОбласти
	
	Возврат СписокПараметров;
	
КонецФункции

Процедура РассчитатьСтавкуСтоимостьПроценты(Объект, РеквизитыДоговора) Экспорт

	ПараметрыРасчета = ВнеоборотныеАктивы.ПараметрыРасчетаСтавкиСтоимостиПроцентов(Объект, РеквизитыДоговора);
	ПараметрыРасчета.ТаблицаОС = Объект.ОС;
	
	Если РеквизитыДоговора.ТипДоговора = ПредопределенноеЗначение("Перечисление.ТипыДоговоровАренды.Аренда")
		И РеквизитыДоговора.СпособОпределенияСтоимостиАктивов = ПредопределенноеЗначение("Перечисление.СпособыОпределенияСтоимостиПредметовАренды.ПоФактическойСтоимости") Тогда
		ПараметрыРасчета.СтоимостьПредметовАренды = Объект.ОС.Итог("СправедливаяСтоимость") - Объект.ОС.Итог("НегарантированнаяСтоимость");
	Иначе
		ПараметрыРасчета.СтоимостьПредметовАренды = Объект.ОС.Итог("Стоимость");
	КонецЕсли;
	
	ВнеоборотныеАктивы.РассчитатьСтавкуСтоимостьПроценты(ПараметрыРасчета);
	
	Объект.ПроцентнаяСтавка = ПараметрыРасчета.ПроцентнаяСтавка;
	Объект.СуммаПроцентов = ПараметрыРасчета.СуммаПроцентов;
	Объект.ПриведеннаяСтоимость = ПараметрыРасчета.ПриведеннаяСтоимость;
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Документы.ИзменениеУсловийДоговораАренды.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.7.271";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("75df1205-a6a0-44f4-b4c1-74e26b0fc9f9");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.ИзменениеУсловийДоговораАренды.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Обновляет документы ""Изменение условий договора аренды"":
	|- Заполняет суммы НДС в графике оплат и начислений
	|- Заполняет приведенную стоимость';
	|en = 'Updates the ""Lease term adjustment"" documents:- Fills in the ""VAT rate"" attribute
	|- Fills in VAT amounts in the schedule of payments and accruals
	|- Fills in the present value'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.ИзменениеУсловийДоговораАренды.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Документы.ИзменениеУсловийДоговораАренды.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Документы.ИзменениеУсловийДоговораАренды.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");

КонецПроцедуры

// Регистрирует данные к обработке при переходе на новую версию.
// 
// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = "Документ.ИзменениеУсловийДоговораАренды";
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Ссылка.Дата УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Ссылка");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокДокументов.Ссылка
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДанныеДокумента.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.ИзменениеУсловийДоговораАренды КАК ДанныеДокумента
	|	ГДЕ
	|		ДанныеДокумента.Проведен
	|		И ДанныеДокумента.ПриведеннаяСтоимость = 0
	|		И ДанныеДокумента.СуммаПроцентов <> 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ГрафикОплатУслуг.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.ИзменениеУсловийДоговораАренды.ГрафикОплатУслуг КАК ГрафикОплатУслуг
	|	ГДЕ
	|		ГрафикОплатУслуг.Ссылка.Проведен
	|		И ГрафикОплатУслуг.Ссылка.СтавкаНДС <> ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
	|		И (ГрафикОплатУслуг.УслугаПоАренде <> 0
	|				И ГрафикОплатУслуг.УслугаПоАрендеНДС = 0
	|			ИЛИ ГрафикОплатУслуг.ОбеспечительныйПлатеж <> 0
	|				И ГрафикОплатУслуг.ОбеспечительныйПлатежНДС = 0
	|			ИЛИ ГрафикОплатУслуг.ВыкупнаяСтоимость <> 0
	|				И ГрафикОплатУслуг.ВыкупнаяСтоимостьНДС = 0)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ГрафикНачисленияУслуг.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.ИзменениеУсловийДоговораАренды.ГрафикНачисленияУслуг КАК ГрафикНачисленияУслуг
	|	ГДЕ
	|		ГрафикНачисленияУслуг.Ссылка.Проведен
	|		И ГрафикНачисленияУслуг.Ссылка.СтавкаНДС <> ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
	|		И (ГрафикНачисленияУслуг.УслугаПоАренде <> 0
	|				И ГрафикНачисленияУслуг.УслугаПоАрендеНДС = 0
	|			ИЛИ ГрафикНачисленияУслуг.ОбеспечительныйПлатеж <> 0
	|				И ГрафикНачисленияУслуг.ОбеспечительныйПлатежНДС = 0)
	|
	|	) КАК СписокДокументов";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "Документ.ИзменениеУсловийДоговораАренды";
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Для Каждого Выборка Из ОбновляемыеДанные Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект(); // ДокументОбъект.ИзменениеУсловийДоговораАренды - 
			Если ДокументОбъект = Неопределено Тогда
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
				ЗафиксироватьТранзакцию();
 				Продолжить;
 			КонецЕсли;
			
			ВнеоборотныеАктивы.ЗаполнитьСуммыНДСПриОбновлении(ДокументОбъект);
			ВнеоборотныеАктивы.ЗаполнитьПриведеннуюСтоимостьПриОбновлении(ДокументОбъект);
			
			//
			Если ДокументОбъект.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Выборка.Ссылка);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область УХ_Встраивание

#Область КонтрольНаличияПериодов_УХ
	
// Функция возвращает текст запроса, который формирует временную таблицу запроса ВТ_ДатыОпераций с колонками
//   Дата 		- Дата - Дата операции по документу
//	 ВидБюджета - ПеречислениеСсылка.ПредназначенияЭлементовСтруктурыОтчета - Вид бюджета, по которому будет операция
// Текст запроса уникален для каждого вида документа
//
Функция ТексЗапроса_ДатыОперацийДокумента() экспорт
	
	Возврат
	
	"ВЫБРАТЬ
	|	ИзменениеУсловийДоговораАрендыГрафикОплатУслуг.Дата КАК Дата,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыБюджетов.БюджетДвиженияДенежныхСредств) КАК ВидБюджета
	|	ПОМЕСТИТЬ ВТ_ДатыОпераций
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ГрафикОплатУслуг КАК ИзменениеУсловийДоговораАрендыГрафикОплатУслуг
	|ГДЕ
	|	ИзменениеУсловийДоговораАрендыГрафикОплатУслуг.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИзменениеУсловийДоговораАрендыГрафикНачисленияУслуг.Дата,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыБюджетов.БюджетДоходовИРасходов)
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды.ГрафикНачисленияУслуг КАК ИзменениеУсловийДоговораАрендыГрафикНачисленияУслуг
	|ГДЕ
	|	ИзменениеУсловийДоговораАрендыГрафикНачисленияУслуг.Ссылка = &Ссылка";
	
КонецФункции

#КонецОбласти 

#Область ИсточникиДляКонтроляДокумента_УХ
	
Функция ИсточникиДокумента(Объект, ДопПараметры = неопределено) Экспорт
	
	Источники = Новый Соответствие;
	
	// 1. Источник для контроля бюджетных лимитов и резервов
	КонтрольУХ.ДобавитьИсточник(
		Источники, 
		КонтрольУХБюджетныеЛимиты, 
		Источник_БюджетныеЛимитыРезервы(Объект, ДопПараметры));
		
	//// 2. Планы по взаиморасчетам с контрагентом
	//КонтрольУХ.ДобавитьИсточник(
	//	Источники, 
	//	КонтрольУХВзаиморасчетыПоКонтрагенту, 
	//	Источник_ВзаиморасчетыПоКонтрагенту(Объект));
		
	Возврат Источники;
	
КонецФункции

Функция Источник_БюджетныеЛимитыРезервы(Объект, ДопПараметры) Экспорт
	
	// Доп.параметры
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Дата", 				Объект.Дата);
	ДопПараметры.Вставить("Ссылка", 			Объект.Ссылка);
	ДопПараметры.Вставить("ЭтоНовый", 			НЕ ЗначениеЗаполнено(Объект.Ссылка));
	ДопПараметры.Вставить("ДатаКонтроляЛимита", Объект.Дата);
	
	Если Объект.РежимИспользованияГрафика = Перечисления.РежимыИспользованияГрафика.КонтрольЛимитовИСозданиеПозиций Тогда
		ДанныеДляПроверки = ПланыДокумента(Объект);
	Иначе
		ДанныеДляПроверки = КонтрольЛимитовУХ.Новый_ТаблицаПланыДокумента();
	КонецЕсли;
	
	//
	ДанныеДляПроверки.Колонки.Сумма.Имя = "Заявлено";
	ДанныеДляПроверки.Колонки.Добавить("Лимит", РаботаСКурсамиВалют.ОписаниеТипаДенежногоПоля());
	ДанныеДляПроверки.Колонки.Добавить("Зарезервировано", РаботаСКурсамиВалют.ОписаниеТипаДенежногоПоля());
	ДанныеДляПроверки.Колонки.Добавить("Исполнено", РаботаСКурсамиВалют.ОписаниеТипаДенежногоПоля());
	ДанныеДляПроверки.ЗаполнитьЗначения(Объект.Дата, "Период");
	
	//
	Источник = Новый Структура;
	Источник.Вставить("Параметры",		ДопПараметры);
	Источник.Вставить("ПланыДокумента", ДанныеДляПроверки);
	
	Возврат Источник;
	
КонецФункции

Функция Источник_ВзаиморасчетыПоКонтрагенту(Объект)
	
	// Если незаполнен контрагент, то контроль не выполняется
	Если Не ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Возврат неопределено;
	КонецЕсли;
	
	// Если роль определить не удалось, то контроль не выполняется
	РольКонтрагента = Перечисления.РолиКонтрагентов.Поставщики;
	Если РольКонтрагента = неопределено Тогда
		Возврат неопределено;
	КонецЕсли;
	
	//
	Результат = Новый Структура;
	Результат.Вставить("Заявка", Объект.Ссылка);
	Результат.Вставить("Контрагент", Объект.Контрагент);
	Результат.Вставить("СуммаПлатежа", Объект.Сумма);
	Результат.Вставить("Валюта", Объект.ВалютаВзаиморасчетов);
	Результат.Вставить("РольКонтрагента", РольКонтрагента);
	
	Возврат Результат;
	
КонецФункции

#Область ПланыДокумента_УХ

// Процедура возвращает таблицу планов документа
//
// Параметры:
//  Источник  - ДокументСсылка, ДокументОбъект, Форма.Объект - Документ планирования
//
// Возвращаемое значение:
//   ТаблицаЗначений   - Таблица планов документа
//
Функция ПланыДокумента(Источник) Экспорт	
	Возврат ВстраиваниеУХДоговорыАренды.ПланыДокумента(Источник);	
КонецФункции

Функция ДанныеОбъектаПоСсылке(Источник) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Источник);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИзменениеУсловийДоговораАренды.Ссылка КАК Ссылка,
	|	ИзменениеУсловийДоговораАренды.Дата КАК Дата,
	|	ИзменениеУсловийДоговораАренды.Организация КАК Организация,
	|	ИзменениеУсловийДоговораАренды.Партнер КАК Партнер,
	|	ИзменениеУсловийДоговораАренды.Контрагент КАК Контрагент,
	|	ИзменениеУсловийДоговораАренды.Договор КАК Договор,
	|	ИзменениеУсловийДоговораАренды.Валюта КАК Валюта,
	|	ИзменениеУсловийДоговораАренды.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ИзменениеУсловийДоговораАренды.Курс КАК Курс,
	|	ИзменениеУсловийДоговораАренды.Кратность КАК Кратность,
	|	ИзменениеУсловийДоговораАренды.СуммаДокумента КАК СуммаДокумента,
	|	ИзменениеУсловийДоговораАренды.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ИзменениеУсловийДоговораАренды.Сумма КАК Сумма,
	|	ИзменениеУсловийДоговораАренды.СуммаСНДС КАК СуммаСНДС,
	|	ИзменениеУсловийДоговораАренды.СуммаНДС КАК СуммаНДС,
	|	ИзменениеУсловийДоговораАренды.СтавкаНДС КАК СтавкаНДС,
	|	ИзменениеУсловийДоговораАренды.СрокЛет КАК СрокЛет,
	|	ИзменениеУсловийДоговораАренды.СрокМес КАК СрокМес,
	|	ИзменениеУсловийДоговораАренды.СрокДней КАК СрокДней,
	|	ИзменениеУсловийДоговораАренды.СтоимостьПредметовАренды КАК СтоимостьПредметовАренды,
	|	ИзменениеУсловийДоговораАренды.СуммаОбеспечительногоПлатежа КАК СуммаОбеспечительногоПлатежа,
	|	ИзменениеУсловийДоговораАренды.СуммаУслугПоАренде КАК СуммаУслугПоАренде,
	|	ИзменениеУсловийДоговораАренды.СуммаВыкупаПредметовАренды КАК СуммаВыкупаПредметовАренды,
	|	ИзменениеУсловийДоговораАренды.СуммаПроцентов КАК СуммаПроцентов,
	|	ИзменениеУсловийДоговораАренды.ПроцентнаяСтавка КАК ПроцентнаяСтавка,
	|	ИзменениеУсловийДоговораАренды.ПериодичностьОплат КАК ПериодичностьОплат,
	|	ИзменениеУсловийДоговораАренды.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ИзменениеУсловийДоговораАренды.ОплатаУслугиПоАренде КАК ОплатаУслугиПоАренде,
	|	ИзменениеУсловийДоговораАренды.НачислениеУслугиПоАренде КАК НачислениеУслугиПоАренде,
	|	ИзменениеУсловийДоговораАренды.ДатаНачалаАренды КАК ДатаНачалаАренды,
	|	ИзменениеУсловийДоговораАренды.ДатаПервогоПлатежа КАК ДатаПервогоПлатежа,
	|	ИзменениеУсловийДоговораАренды.ДатаПервогоОбеспечительногоПлатежа КАК ДатаПервогоОбеспечительногоПлатежа,
	|	ИзменениеУсловийДоговораАренды.ДатаВыкупа КАК ДатаВыкупа,
	|	ИзменениеУсловийДоговораАренды.Комментарий КАК Комментарий,
	|	ИзменениеУсловийДоговораАренды.РежимИспользованияГрафика КАК РежимИспользованияГрафика,
	|	ИзменениеУсловийДоговораАренды.ИсточникЛимитовБДДС КАК ИсточникЛимитовБДДС,
	|	ИзменениеУсловийДоговораАренды.ИсточникЛимитовИсполнение КАК ИсточникЛимитовИсполнение,
	|	ИзменениеУсловийДоговораАренды.РежимАктуализацииЗаявок КАК РежимАктуализацииЗаявок,
	|	ИзменениеУсловийДоговораАренды.ЕстьПревышениеЛимитов КАК ЕстьПревышениеЛимитов,
	|	ИзменениеУсловийДоговораАренды.ГрафикОплатУслуг.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		Дата КАК Дата,
	|		УслугаПоАренде КАК УслугаПоАренде,
	|		ОбеспечительныйПлатеж КАК ОбеспечительныйПлатеж,
	|		ВыкупнаяСтоимость КАК ВыкупнаяСтоимость
	|	) КАК ГрафикОплатУслуг,
	|	ИзменениеУсловийДоговораАренды.ГрафикНачисленияУслуг.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		Дата КАК Дата,
	|		УслугаПоАренде КАК УслугаПоАренде,
	|		ОбеспечительныйПлатеж КАК ОбеспечительныйПлатеж
	|	) КАК ГрафикНачисленияУслуг,
	|	ИзменениеУсловийДоговораАренды.ПараметрыОпераций.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		ОперацияГрафика КАК ОперацияГрафика,
	|		СтатьяБюджета КАК СтатьяБюджета,
	|		Аналитика1 КАК Аналитика1,
	|		Аналитика2 КАК Аналитика2,
	|		Аналитика3 КАК Аналитика3,
	|		Аналитика4 КАК Аналитика4,
	|		Аналитика5 КАК Аналитика5,
	|		Аналитика6 КАК Аналитика6
	|	) КАК ПараметрыОпераций
	|ИЗ
	|	Документ.ИзменениеУсловийДоговораАренды КАК ИзменениеУсловийДоговораАренды
	|ГДЕ
	|	ИзменениеУсловийДоговораАренды.Ссылка = &Ссылка";
	РезультатЗапроса = Запрос.Выполнить();
	
	Результат = Новый Структура;
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		Результат.Вставить(Колонка.Имя);
	КонецЦикла;
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка,, "ГрафикОплатУслуг, ГрафикНачисленияУслуг, ПараметрыОпераций");
		Результат.ГрафикОплатУслуг = Выборка.ГрафикОплатУслуг.Выгрузить();
		Результат.ГрафикНачисленияУслуг = Выборка.ГрафикНачисленияУслуг.Выгрузить();
		Результат.ПараметрыОпераций = Выборка.ПараметрыОпераций.Выгрузить();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти 

#КонецОбласти 

#КонецОбласти

#КонецЕсли
