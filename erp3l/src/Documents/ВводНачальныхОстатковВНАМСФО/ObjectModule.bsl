
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);	
	
	Если ДанныеЗаполнения = Неопределено Тогда 
		
		ИнициализироватьДокумент();
		
	ИначеЕсли ТипДанныхЗаполнения = Тип("Структура") Тогда 
		
		Если ДанныеЗаполнения.Свойство("ИсточникЗаполнения") 
			И ДанныеЗаполнения.ИсточникЗаполнения = "ДанныеНСБУ" Тогда
			
			ВстраиваниеУХ.ЗаполнитьИзПодсистемыНСБУ(ЭтотОбъект, Неопределено);
			
		ИначеЕсли ДанныеЗаполнения.Свойство("ИсточникЗаполнения")
			И ДанныеЗаполнения.ИсточникЗаполнения = "ДокументыОснования" Тогда
			
			ЗаполнитьПоОснованиям(ДокументыОснования.ВыгрузитьКолонку("ДокументОснование"));
			
		ИначеЕсли ДанныеЗаполнения.Свойство("АдресТаблицы") Тогда
			
			МСФОВызовСервераУХ.ЗаполнитьПоТаблицеЗагрузки(ЭтотОбъект, ДанныеЗаполнения);
			
		Иначе
			
			ИнициализироватьДокумент();
			
		КонецЕсли;	
				
	ИначеЕсли ТипЗнч(Ссылка) = ТипДанныхЗаполнения Тогда
		
		ИнициализироватьДокумент(ДанныеЗаполнения);
		ЗаполнитьПоОснованиям(ДанныеЗаполнения, Истина);
		
	ИначеЕсли Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
		
		ЗаполнитьПоОснованиям(ДанныеЗаполнения);
		
	Иначе
		
		ИнициализироватьДокумент();
		
	КонецЕсли;
		
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ИнициализироватьДокумент();
			
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МСФОВНАВызовСервераУХ.ОбновитьПроверяемыеРеквизитыТаблицыПоРежимуЗаполнения(ПроверяемыеРеквизиты, ЭтотОбъект, "ВНА");
	
	КонтекстФормы = Новый Структура("КэшируемыеЗначения", Новый Структура);
	РеквизитыАмортизации = Документы.ВводНачальныхОстатковВНАМСФО.ПолучитьРеквизитыАмортизации();
	
	МСФОВНАВызовСервераУХ.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, РеквизитыАмортизации);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)	
	ПроведениеСерверУХ.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);		
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСерверУХ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	Документы.ВводНачальныхОстатковВНАМСФО.ПодготовитьПараметрыПроведения(ДополнительныеСвойства, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСерверУХ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	Документы.ВводНачальныхОстатковВНАМСФО.СформироватьДвижения(Движения, ДополнительныеСвойства, Отказ);
	ПроведениеСерверУХ.ОбработкаПроведения_ЗаписьИКонтроль(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	ПроведениеСерверУХ.ОбработкаУдаленияПроведения(ЭтотОбъект, Отказ);	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

Процедура ИнициализироватьДокумент(ДокументИсточник = Неопределено)
	
	МСФОУХ.ОбработкаЗаполнения(ЭтотОбъект, ДокументИсточник, Перечисления.РежимЗаполненияВидовУчета.МСФО);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиЗаполнения

Процедура ЗаполнитьПоОснованиям(МассивДокументыОснования, ЗаполнитьДокумент = Ложь)
	
	Если ЭтотОбъект.РежимЗаполнения = ПредопределенноеЗначение("Перечисление.РежимЗаполненияВидовУчета.МСФО") Тогда
	
		Если ЗаполнитьДокумент Тогда
			МСФОВызовСервераУХ.ЗаполнитьКонтекстПоОснованиям(ЭтотОбъект, МассивДокументыОснования);
		КонецЕсли;
		
		МСФОВызовСервераУХ.ЗаполнитьДокументИзРежимаНСБУ(
								ЭтотОбъект, 
								МассивДокументыОснования, 
								"ВНА");
								
	Иначе
		ВстраиваниеУХ.ЗаполнитьИзПодсистемыНСБУ(ЭтотОбъект, МассивДокументыОснования);
	КонецЕсли;	
		
КонецПроцедуры

Функция ПолучитьГруппыВНА(КоллекцияСтрок, ПараметрыЗаполнения) Экспорт
	
	Граница = ПараметрыЗаполнения.ПериодОтчета.ДатаНачала;
	
	ОбъектыВНА = Новый Массив;
	Для каждого СтрокаВНА Из КоллекцияСтрок Цикл
		Если ЗначениеЗаполнено(СтрокаВНА.ВНА) Тогда
			ОбъектыВНА.Добавить(СтрокаВНА.ВНА);		
		КонецЕсли;		
	КонецЦикла;
	
	РеквизитыВНА = МСФОВНАВызовСервераУХ.ПолучитьРеквизитыВНА(Граница, ОбъектыВНА, 
						ПараметрыЗаполнения.Организация, Неопределено, ПараметрыЗаполнения.Сценарий);
	
	ГруппыВНА = Новый Соответствие;
	
	Для каждого УзелВНА Из РеквизитыВНА.МСФО Цикл
		
		ГруппыВНА.Вставить(УзелВНА.Ключ, УзелВНА.Значение.ГруппаВНА);
		ОбъектВНА = ОбъектыВНА.Найти(УзелВНА.Ключ);
		Если ОбъектВНА <> Неопределено Тогда
			ОбъектыВНА.Удалить(ОбъектВНА);
		КонецЕсли;
		
	КонецЦикла;
	
	ПоУмолчаниюГруппыВНА = Справочники.ГруппыВНАМСФО.ПолучитьТаблицуГруппВНАпоУмолчанию(ОбъектыВНА);
	Для каждого СтрокаВНА Из ПоУмолчаниюГруппыВНА Цикл
		ГруппыВНА.Вставить(СтрокаВНА.ВНА, СтрокаВНА.ПараметрыУчетаВНА);
	КонецЦикла;
	
	Возврат ГруппыВНА;

КонецФункции

#КонецОбласти

#КонецЕсли
