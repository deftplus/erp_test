
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ПодготовкаПараметровПроведенияДокумента

Функция ПодготовитьПараметрыПроведения(ДополнительныеСвойства, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;	
	Запрос.УстановитьПараметр("Ссылка", ДополнительныеСвойства.ДляПроведения.Ссылка);
	
	Реквизиты = МСФОУХ.РеквизитыДокумента(Запрос, "РежимЗаполнения", Отказ);
	Реквизиты.Вставить("ДанныеРежимаЗаполнения",	УчетМСФОПовтИспУХ.ПолучитьВидыУчетаРежимаЗаполнения(Реквизиты.РежимЗаполнения));
	ДополнительныеСвойства.ДляПроведения.Вставить("Реквизиты", Реквизиты);
	Если Отказ Тогда
		Возврат ДополнительныеСвойства;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ЕстьНСБУ", 					Реквизиты.ДанныеРежимаЗаполнения.ЕстьНСБУ);
	Запрос.УстановитьПараметр("ЕстьМСФО", 					Реквизиты.ДанныеРежимаЗаполнения.ЕстьМСФО);
	Запрос.УстановитьПараметр("ВидУчетаНСБУ", 				Реквизиты.ДанныеРежимаЗаполнения.НСБУ);	
	Запрос.УстановитьПараметр("ВидУчетаМСФО", 				Реквизиты.ДанныеРежимаЗаполнения.МСФО);
	Запрос.УстановитьПараметр("ФормироватьПроводкиМСФО",	Реквизиты.ФормироватьПроводкиМСФО);
	Запрос.УстановитьПараметр("КонецГода",					КонецГода(Реквизиты.Период));
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаТаблицаВНА(НомераТаблиц);	
	ПроведениеСерверУХ.ДополнитьТаблицамиИзПакетаЗапросов(ДополнительныеСвойства.ТаблицыДляДвижений, Запрос, НомераТаблиц);
	
КонецФункции

Функция ТекстЗапросаТаблицаВНА(НомераТаблиц)

	НомераТаблиц.Вставить("втТаблицаВНА", 		НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаВНА", 		НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаПроводки", 	НомераТаблиц.Количество());
	
	ТекстЗапроса = 	
	"ВЫБРАТЬ
	|	ТаблицаВНА.НомерСтроки КАК НомерСтроки,
	|	ТаблицаВНА.ВНА КАК ВНА,
	|	ТаблицаВНА.ГруппаВНАМСФО КАК ГруппаВНА,
	|	ТаблицаВНА.ПараметрыАмортизацииВНАМСФО КАК ПараметрыАмортизацииВНАМСФО,
	|	ТаблицаВНА.ДатаПринятияКУчетуНСБУ КАК ДатаПринятияКУчетуНСБУ,
	|	ТаблицаВНА.ДатаПринятияКУчетуМСФО КАК ДатаПринятияКУчетуМСФО,
	|	ТаблицаВНА.ВНА.ИнвентарныйНомерМСФО КАК ИнвентарныйНомерНСБУ,
	|	ТаблицаВНА.ВНА.ПервоначальныйИнвентарныйНомерМСФО КАК ИнвентарныйНомерМСФО,
	|	ТаблицаВНА.НакопленнаяАмортизацияНСБУ КАК НакопленнаяАмортизацияНСБУ,
	|	ТаблицаВНА.НакопленнаяАмортизацияМСФО КАК НакопленнаяАмортизацияМСФО,
	|	ТаблицаВНА.КоэффициентУскоренияМСФО КАК КоэффициентУскоренияМСФО,
	|	ТаблицаВНА.ЛиквидационнаяСтоимостьНСБУ КАК ЛиквидационнаяСтоимостьНСБУ,
	|	ТаблицаВНА.ЛиквидационнаяСтоимостьМСФО КАК ЛиквидационнаяСтоимостьМСФО,
	|	ТаблицаВНА.СпособНачисленияАмортизацииНСБУ КАК СпособНачисленияАмортизацииНСБУ,
	|	ТаблицаВНА.СпособНачисленияАмортизацииМСФО КАК СпособНачисленияАмортизацииМСФО,
	|	ТаблицаВНА.СрокПолезногоИспользованияНСБУ КАК СрокПолезногоИспользованияНСБУ,
	|	ТаблицаВНА.СрокПолезногоИспользованияМСФО КАК СрокПолезногоИспользованияМСФО,
	|	ТаблицаВНА.СчетУчетаНСБУ КАК СчетУчетаНСБУ,
	|	ТаблицаВНА.СчетУчетаМСФО КАК СчетУчетаМСФО,
	|	ТаблицаВНА.СчетАмортизацииНСБУ КАК СчетАмортизацииНСБУ,
	|	ТаблицаВНА.СчетАмортизацииМСФО КАК СчетАмортизацииМСФО,
	|	ТаблицаВНА.ТекущаяСтоимостьНСБУ КАК ТекущаяСтоимостьНСБУ,
	|	ТаблицаВНА.ТекущаяСтоимостьМСФО КАК ТекущаяСтоимостьМСФО,
	|	ТаблицаВНА.ПараметрВыработкиНСБУ КАК ПараметрВыработкиНСБУ,
	|	ТаблицаВНА.ПараметрВыработкиМСФО КАК ПараметрВыработкиМСФО,
	|	ТаблицаВНА.ПредполагаемыйОбъемПродукцииНСБУ КАК ПредполагаемыйОбъемПродукцииНСБУ,
	|	ТаблицаВНА.ПредполагаемыйОбъемПродукцииМСФО КАК ПредполагаемыйОбъемПродукцииМСФО,
	|	ТаблицаВНА.СчетУчетаСубконто1НСБУ КАК СчетУчетаСубконто1НСБУ,
	|	ТаблицаВНА.СчетУчетаСубконто2НСБУ КАК СчетУчетаСубконто2НСБУ,
	|	ТаблицаВНА.СчетУчетаСубконто3НСБУ КАК СчетУчетаСубконто3НСБУ,
	|	ТаблицаВНА.СчетУчетаСубконто1МСФО КАК СчетУчетаСубконто1МСФО,
	|	ТаблицаВНА.СчетУчетаСубконто2МСФО КАК СчетУчетаСубконто2МСФО,
	|	ТаблицаВНА.СчетУчетаСубконто3МСФО КАК СчетУчетаСубконто3МСФО,
	|	ТаблицаВНА.КоэффициентУскоренияНСБУ КАК КоэффициентУскоренияНСБУ,
	|	ТаблицаВНА.СчетАмортизацииСубконто1НСБУ КАК СчетАмортизацииСубконто1НСБУ,
	|	ТаблицаВНА.СчетАмортизацииСубконто2НСБУ КАК СчетАмортизацииСубконто2НСБУ,
	|	ТаблицаВНА.СчетАмортизацииСубконто3НСБУ КАК СчетАмортизацииСубконто3НСБУ,
	|	ТаблицаВНА.СчетАмортизацииСубконто1МСФО КАК СчетАмортизацииСубконто1МСФО,
	|	ТаблицаВНА.СчетАмортизацииСубконто2МСФО КАК СчетАмортизацииСубконто2МСФО,
	|	ТаблицаВНА.СчетАмортизацииСубконто3МСФО КАК СчетАмортизацииСубконто3МСФО,
	|	ТаблицаВНА.СпособОтраженияРасходовПоАмортизацииНСБУ КАК СпособОтраженияРасходовПоАмортизацииНСБУ,
	|	ТаблицаВНА.СпособОтраженияРасходовПоАмортизацииМСФО КАК СпособОтраженияРасходовПоАмортизацииМСФО,
	|	ТаблицаВНА.ПереоценкаМСФО КАК ПереоценкаМСФО,
	|	ТаблицаВНА.ПереоценкаАмортизацииМСФО КАК ПереоценкаАмортизацииМСФО,
	|	ТаблицаВНА.РезервПереоценкиМСФО КАК РезервПереоценкиМСФО,
	|	ТаблицаВНА.СпособПоступления КАК СпособПоступления,
	|	ВЫБОР
	|		КОГДА ИСТИНА В
	|				(ВЫБРАТЬ
	|					т.Значение
	|				ИЗ
	|					Константа.АлгоритмНачиcленияАмортизацииАналогичноРСБУ КАК т)
	|			ТОГДА ТаблицаВНА.СрокПолезногоИспользованияМСФО - ((ГОД(&КонецГода) - ГОД(ТаблицаВНА.ДатаПринятияКУчетуМСФО)) * 12 + МЕСЯЦ(&КонецГода) - МЕСЯЦ(ТаблицаВНА.ДатаПринятияКУчетуМСФО))
	|		ИНАЧЕ ТаблицаВНА.СрокПолезногоИспользованияМСФО - РАЗНОСТЬДАТ(ТаблицаВНА.ДатаПринятияКУчетуМСФО, &КонецГода, ДЕНЬ) * 12 / 365
	|	КОНЕЦ КАК СрокИспользованияДляВычисленияАмортизации,
	|	ВЫБОР
	|		КОГДА ТаблицаВНА.ГруппаВНАМСФО.МодельУчетаВНА = ЗНАЧЕНИЕ(Перечисление.МоделиУчетаВНАМСФО.УчетПоПереоцененнойСтоимости)
	|			ТОГДА ИСТИНА
	|		КОГДА ТаблицаВНА.ГруппаВНАМСФО.МодельУчетаВНА = ЗНАЧЕНИЕ(Перечисление.МоделиУчетаВНАМСФО.УчетПоФактическимЗатратам)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК МодельПереоценки,
	|	Вспомогательный.Счет КАК ВспомогательныйСчет,
	|	Вспомогательный.Счет.СчетСсылка КАК ВспомогательныйСчетСсылка,
	|	Вспомогательный.Субконто1 КАК ВспомогательныйСубконто1,
	|	Вспомогательный.Субконто2 КАК ВспомогательныйСубконто2,
	|	Вспомогательный.Субконто3 КАК ВспомогательныйСубконто3,
	|	ВЫБОР
	|		КОГДА Вспомогательный.Счет.СчетСсылка.УчетПоПодразделениям
	|			ТОГДА ТаблицаВНА.Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВспомогательныйПодразделение,
	|	ВЫБОР
	|		КОГДА Вспомогательный.Счет.СчетСсылка.УчетПоНаправлениямДеятельности
	|			ТОГДА ТаблицаВНА.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВспомогательныйНаправлениеДеятельности,
	|	ТаблицаВНА.Подразделение КАК Подразделение,
	|	ТаблицаВНА.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ втТаблицаВНА
	|ИЗ
	|	Документ.ВводНачальныхОстатковВНАМСФО.ВНА КАК ТаблицаВНА
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФиксированныеСчетаУчетаБД КАК Вспомогательный
	|		ПО (Вспомогательный.Ссылка = ЗНАЧЕНИЕ(Справочник.ФиксированныеСчетаУчетаБД.ВспомогательныйНачальныеОстаткиВНА))
	|ГДЕ
	|	ТаблицаВНА.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТаблицаВНА.ВНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИСТИНА КАК ПоступилВПериоде,
	|	втТаблицаВНА.ВНА КАК ВНА,
	|	&ВидУчетаНСБУ КАК ВидУчета,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ВводВЭксплуатацию) КАК ВидОперации,
	|	втТаблицаВНА.ДатаПринятияКУчетуНСБУ КАК ДатаСостояния,
	|	&КонецГода КАК Период,
	|	втТаблицаВНА.ГруппаВНА КАК ГруппаВНА,
	|	втТаблицаВНА.ГруппаВНА.КлассВНА КАК КлассВНА,
	|	втТаблицаВНА.ВНА.ИнвентарныйНомерМСФО КАК ИнветарныйНомер,
	|	втТаблицаВНА.ТекущаяСтоимостьНСБУ КАК СуммаВВалютеУчета,
	|	втТаблицаВНА.ТекущаяСтоимостьНСБУ КАК ПервоначальнаяСтоимость,
	|	втТаблицаВНА.СпособПоступления КАК СпособПоступления,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.СпособНачисленияАмортизацииНСБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииВНА.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК НачислятьАмортизацию,
	|	втТаблицаВНА.СпособНачисленияАмортизацииНСБУ КАК СпособНачисленияАмортизации,
	|	NULL КАК ПараметрыАмортизации,
	|	втТаблицаВНА.СрокПолезногоИспользованияНСБУ КАК СрокПолезногоИспользования,
	|	втТаблицаВНА.СрокПолезногоИспользованияНСБУ - ((ГОД(&КонецГода) - ГОД(втТаблицаВНА.ДатаПринятияКУчетуНСБУ)) * 12 + МЕСЯЦ(&КонецГода) - МЕСЯЦ(втТаблицаВНА.ДатаПринятияКУчетуНСБУ)) КАК СрокИспользованияДляВычисленияАмортизации,
	|	втТаблицаВНА.ТекущаяСтоимостьНСБУ - втТаблицаВНА.НакопленнаяАмортизацияНСБУ КАК СтоимостьДляВычисленияАмортизации,
	|	втТаблицаВНА.ЛиквидационнаяСтоимостьНСБУ КАК ЛиквидационнаяСтоимость,
	|	втТаблицаВНА.СпособОтраженияРасходовПоАмортизацииНСБУ КАК СпособОтраженияРасходовПоАмортизации,
	|	втТаблицаВНА.СчетУчетаНСБУ КАК СчетУчетаПервоначальнойСтоимости,
	|	втТаблицаВНА.СчетУчетаСубконто1НСБУ КАК СчетУчетаПервоначальнойСтоимостиСубконто1,
	|	втТаблицаВНА.СчетУчетаСубконто2НСБУ КАК СчетУчетаПервоначальнойСтоимостиСубконто2,
	|	втТаблицаВНА.СчетУчетаСубконто3НСБУ КАК СчетУчетаПервоначальнойСтоимостиСубконто3,
	|	втТаблицаВНА.СчетАмортизацииНСБУ КАК СчетНакопленнойАмортизации,
	|	втТаблицаВНА.СчетАмортизацииСубконто1НСБУ КАК СчетНакопленнойАмортизацииСубконто1,
	|	втТаблицаВНА.СчетАмортизацииСубконто2НСБУ КАК СчетНакопленнойАмортизацииСубконто2,
	|	втТаблицаВНА.СчетАмортизацииСубконто3НСБУ КАК СчетНакопленнойАмортизацииСубконто3,
	|	втТаблицаВНА.КоэффициентУскоренияНСБУ КАК КоэффициентУскорения,
	|	втТаблицаВНА.ПараметрВыработкиНСБУ КАК ПараметрВыработки,
	|	втТаблицаВНА.ПредполагаемыйОбъемПродукцииНСБУ КАК ОбъемПродукцииРабот,
	|	втТаблицаВНА.НакопленнаяАмортизацияНСБУ КАК Амортизация,
	|	0 КАК Переоценка,
	|	0 КАК Обесценение,
	|	0 КАК РезервПереоценки,
	|	0 КАК ПереоценкаАмортизации,
	|	втТаблицаВНА.Подразделение КАК Подразделение,
	|	втТаблицаВНА.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	втТаблицаВНА КАК втТаблицаВНА
	|ГДЕ
	|	&ЕстьНСБУ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА,
	|	втТаблицаВНА.ВНА,
	|	&ВидУчетаМСФО,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ВводВЭксплуатацию),
	|	втТаблицаВНА.ДатаПринятияКУчетуМСФО,
	|	&КонецГода,
	|	втТаблицаВНА.ГруппаВНА,
	|	втТаблицаВНА.ГруппаВНА.КлассВНА,
	|	втТаблицаВНА.ВНА.ИнвентарныйНомерМСФО,
	|	втТаблицаВНА.ТекущаяСтоимостьМСФО,
	|	втТаблицаВНА.ТекущаяСтоимостьМСФО,
	|	втТаблицаВНА.СпособПоступления,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.СпособНачисленияАмортизацииМСФО = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииВНА.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	втТаблицаВНА.СпособНачисленияАмортизацииМСФО,
	|	втТаблицаВНА.ПараметрыАмортизацииВНАМСФО,
	|	втТаблицаВНА.СрокПолезногоИспользованияМСФО,
	|	втТаблицаВНА.СрокИспользованияДляВычисленияАмортизации,
	|	втТаблицаВНА.ТекущаяСтоимостьМСФО - втТаблицаВНА.НакопленнаяАмортизацияМСФО + втТаблицаВНА.ПереоценкаМСФО + втТаблицаВНА.ПереоценкаАмортизацииМСФО,
	|	втТаблицаВНА.ЛиквидационнаяСтоимостьМСФО,
	|	втТаблицаВНА.СпособОтраженияРасходовПоАмортизацииМСФО,
	|	втТаблицаВНА.СчетУчетаМСФО,
	|	втТаблицаВНА.СчетУчетаСубконто1МСФО,
	|	втТаблицаВНА.СчетУчетаСубконто2МСФО,
	|	втТаблицаВНА.СчетУчетаСубконто3МСФО,
	|	втТаблицаВНА.СчетАмортизацииМСФО,
	|	втТаблицаВНА.СчетАмортизацииСубконто1МСФО,
	|	втТаблицаВНА.СчетАмортизацииСубконто2МСФО,
	|	втТаблицаВНА.СчетАмортизацииСубконто3МСФО,
	|	втТаблицаВНА.КоэффициентУскоренияМСФО,
	|	втТаблицаВНА.ПараметрВыработкиМСФО,
	|	втТаблицаВНА.ПредполагаемыйОбъемПродукцииМСФО,
	|	втТаблицаВНА.НакопленнаяАмортизацияМСФО,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.МодельПереоценки = ИСТИНА
	|			ТОГДА втТаблицаВНА.ПереоценкаМСФО
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.МодельПереоценки = ЛОЖЬ
	|			ТОГДА втТаблицаВНА.ПереоценкаМСФО
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.МодельПереоценки = ИСТИНА
	|			ТОГДА втТаблицаВНА.РезервПереоценкиМСФО
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.МодельПереоценки = ИСТИНА
	|			ТОГДА втТаблицаВНА.ПереоценкаАмортизацииМСФО
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	втТаблицаВНА.Подразделение,
	|	втТаблицаВНА.НаправлениеДеятельности
	|ИЗ
	|	втТаблицаВНА КАК втТаблицаВНА
	|ГДЕ
	|	&ЕстьМСФО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&КонецГода КАК Период,
	|	втТаблицаВНА.ВНА КАК ВНА,
	|	втТаблицаВНА.ГруппаВНА КАК ГруппаВНА,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ВводВЭксплуатацию) КАК ВидОперации,
	|	втТаблицаВНА.СчетУчетаМСФО КАК СчетДт,
	|	втТаблицаВНА.СчетУчетаМСФО.СчетСсылка КАК СчетДтМСФО,
	|	втТаблицаВНА.СчетУчетаСубконто1МСФО КАК СубконтоДт1,
	|	втТаблицаВНА.СчетУчетаСубконто2МСФО КАК СубконтоДт2,
	|	втТаблицаВНА.СчетУчетаСубконто3МСФО КАК СубконтоДт3,
	|	втТаблицаВНА.ВспомогательныйСчет КАК СчетКт,
	|	втТаблицаВНА.ВспомогательныйСчетСсылка КАК СчетКтМСФО,
	|	втТаблицаВНА.ВспомогательныйСубконто1 КАК СубконтоКт1,
	|	втТаблицаВНА.ВспомогательныйСубконто2 КАК СубконтоКт2,
	|	втТаблицаВНА.ВспомогательныйСубконто3 КАК СубконтоКт3,
	|	1 КАК КоличествоКт,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.СчетУчетаМСФО.СчетСсылка.УчетПоПодразделениям
	|			ТОГДА втТаблицаВНА.Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ПодразделениеДт,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.СчетУчетаМСФО.СчетСсылка.УчетПоНаправлениямДеятельности
	|			ТОГДА втТаблицаВНА.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельностиДт,
	|	втТаблицаВНА.ВспомогательныйПодразделение КАК ПодразделениеКт,
	|	втТаблицаВНА.ВспомогательныйНаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	втТаблицаВНА.ТекущаяСтоимостьНСБУ КАК СуммаОперацииНСБУ,
	|	втТаблицаВНА.ТекущаяСтоимостьМСФО КАК СуммаОперацииМСФО
	|ИЗ
	|	втТаблицаВНА КАК втТаблицаВНА
	|ГДЕ
	|	(втТаблицаВНА.ТекущаяСтоимостьНСБУ <> 0
	|			ИЛИ втТаблицаВНА.ТекущаяСтоимостьМСФО <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&КонецГода,
	|	втТаблицаВНА.ВНА,
	|	втТаблицаВНА.ГруппаВНА,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ВводВЭксплуатацию),
	|	втТаблицаВНА.ВспомогательныйСчет,
	|	втТаблицаВНА.ВспомогательныйСчетСсылка,
	|	втТаблицаВНА.ВспомогательныйСубконто1,
	|	втТаблицаВНА.ВспомогательныйСубконто1,
	|	втТаблицаВНА.ВспомогательныйСубконто3,
	|	втТаблицаВНА.СчетАмортизацииМСФО,
	|	втТаблицаВНА.СчетАмортизацииМСФО.СчетСсылка,
	|	втТаблицаВНА.СчетАмортизацииСубконто1МСФО,
	|	втТаблицаВНА.СчетАмортизацииСубконто2МСФО,
	|	втТаблицаВНА.СчетАмортизацииСубконто3МСФО,
	|	1,
	|	втТаблицаВНА.ВспомогательныйПодразделение,
	|	втТаблицаВНА.ВспомогательныйНаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.СчетАмортизацииМСФО.СчетСсылка.УчетПоПодразделениям
	|			ТОГДА втТаблицаВНА.Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.СчетАмортизацииМСФО.СчетСсылка.УчетПоНаправлениямДеятельности
	|			ТОГДА втТаблицаВНА.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	втТаблицаВНА.НакопленнаяАмортизацияНСБУ,
	|	втТаблицаВНА.НакопленнаяАмортизацияМСФО
	|ИЗ
	|	втТаблицаВНА КАК втТаблицаВНА
	|ГДЕ
	|	(втТаблицаВНА.НакопленнаяАмортизацияНСБУ <> 0
	|			ИЛИ втТаблицаВНА.НакопленнаяАмортизацияМСФО <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&КонецГода,
	|	втТаблицаВНА.ВНА,
	|	втТаблицаВНА.ГруппаВНА,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПереоценкаВНА),
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.МодельПереоценки = ИСТИНА
	|			ТОГДА втТаблицаВНА.ГруппаВНА.ПараметрыПереоценки.СчетНакопленнойПереоценки
	|		КОГДА втТаблицаВНА.МодельПереоценки = ЛОЖЬ
	|			ТОГДА втТаблицаВНА.ГруппаВНА.ПараметрыОбесценения.СчетНакопленногоОбесценения
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.МодельПереоценки = ИСТИНА
	|			ТОГДА втТаблицаВНА.ГруппаВНА.ПараметрыПереоценки.СчетНакопленнойПереоценки.СчетСсылка
	|		КОГДА втТаблицаВНА.МодельПереоценки = ЛОЖЬ
	|			ТОГДА втТаблицаВНА.ГруппаВНА.ПараметрыОбесценения.СчетНакопленногоОбесценения.СчетСсылка
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.МодельПереоценки = ИСТИНА
	|			ТОГДА втТаблицаВНА.ГруппаВНА.ПараметрыПереоценки.СчетНакопленнойПереоценкиСубконто1
	|		КОГДА втТаблицаВНА.МодельПереоценки = ЛОЖЬ
	|			ТОГДА втТаблицаВНА.ГруппаВНА.ПараметрыОбесценения.СчетНакопленногоОбесцененияСубконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.МодельПереоценки = ИСТИНА
	|			ТОГДА втТаблицаВНА.ГруппаВНА.ПараметрыПереоценки.СчетНакопленнойПереоценкиСубконто2
	|		КОГДА втТаблицаВНА.МодельПереоценки = ЛОЖЬ
	|			ТОГДА втТаблицаВНА.ГруппаВНА.ПараметрыОбесценения.СчетНакопленногоОбесцененияСубконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.МодельПереоценки = ИСТИНА
	|			ТОГДА втТаблицаВНА.ГруппаВНА.ПараметрыПереоценки.СчетНакопленнойПереоценкиСубконто3
	|		КОГДА втТаблицаВНА.МодельПереоценки = ЛОЖЬ
	|			ТОГДА втТаблицаВНА.ГруппаВНА.ПараметрыОбесценения.СчетНакопленногоОбесцененияСубконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	втТаблицаВНА.ВспомогательныйСчет,
	|	втТаблицаВНА.ВспомогательныйСчетСсылка,
	|	втТаблицаВНА.ВспомогательныйСубконто1,
	|	втТаблицаВНА.ВспомогательныйСубконто2,
	|	втТаблицаВНА.ВспомогательныйСубконто3,
	|	1,
	|	ВЫБОР
	|		КОГДА (ВЫБОР
	|				КОГДА втТаблицаВНА.МодельПереоценки = ИСТИНА
	|					ТОГДА втТаблицаВНА.ГруппаВНА.ПараметрыПереоценки.СчетНакопленнойПереоценки.СчетСсылка
	|				КОГДА втТаблицаВНА.МодельПереоценки = ЛОЖЬ
	|					ТОГДА втТаблицаВНА.ГруппаВНА.ПараметрыОбесценения.СчетНакопленногоОбесценения.СчетСсылка
	|				ИНАЧЕ NULL
	|			КОНЕЦ).УчетПоПодразделениям
	|			ТОГДА втТаблицаВНА.Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА (ВЫБОР
	|				КОГДА втТаблицаВНА.МодельПереоценки = ИСТИНА
	|					ТОГДА втТаблицаВНА.ГруппаВНА.ПараметрыПереоценки.СчетНакопленнойПереоценки.СчетСсылка
	|				КОГДА втТаблицаВНА.МодельПереоценки = ЛОЖЬ
	|					ТОГДА втТаблицаВНА.ГруппаВНА.ПараметрыОбесценения.СчетНакопленногоОбесценения.СчетСсылка
	|				ИНАЧЕ NULL
	|			КОНЕЦ).УчетПоНаправлениямДеятельности
	|			ТОГДА втТаблицаВНА.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	втТаблицаВНА.ВспомогательныйПодразделение,
	|	втТаблицаВНА.ВспомогательныйНаправлениеДеятельности,
	|	0,
	|	втТаблицаВНА.ПереоценкаМСФО
	|ИЗ
	|	втТаблицаВНА КАК втТаблицаВНА
	|ГДЕ
	|	втТаблицаВНА.ПереоценкаМСФО <> 0
	|	И втТаблицаВНА.МодельПереоценки <> НЕОПРЕДЕЛЕНО
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&КонецГода,
	|	втТаблицаВНА.ВНА,
	|	втТаблицаВНА.ГруппаВНА,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПереоценкаВНА),
	|	втТаблицаВНА.ВспомогательныйСчет,
	|	втТаблицаВНА.ВспомогательныйСчетСсылка,
	|	втТаблицаВНА.ВспомогательныйСубконто1,
	|	втТаблицаВНА.ВспомогательныйСубконто2,
	|	втТаблицаВНА.ВспомогательныйСубконто3,
	|	втТаблицаВНА.ГруппаВНА.ПараметрыПереоценки.СчетУчетаРезерва,
	|	втТаблицаВНА.ГруппаВНА.ПараметрыПереоценки.СчетУчетаРезерва.СчетСсылка,
	|	втТаблицаВНА.ГруппаВНА.ПараметрыПереоценки.СчетУчетаРезерваСубконто1,
	|	втТаблицаВНА.ГруппаВНА.ПараметрыПереоценки.СчетУчетаРезерваСубконто2,
	|	втТаблицаВНА.ГруппаВНА.ПараметрыПереоценки.СчетУчетаРезерваСубконто3,
	|	1,
	|	втТаблицаВНА.ВспомогательныйПодразделение,
	|	втТаблицаВНА.ВспомогательныйНаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ГруппаВНА.ПараметрыПереоценки.СчетУчетаРезерва.СчетСсылка.УчетПоПодразделениям
	|			ТОГДА втТаблицаВНА.Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ГруппаВНА.ПараметрыПереоценки.СчетУчетаРезерва.СчетСсылка.УчетПоНаправлениямДеятельности
	|			ТОГДА втТаблицаВНА.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	втТаблицаВНА.РезервПереоценкиМСФО
	|ИЗ
	|	втТаблицаВНА КАК втТаблицаВНА
	|ГДЕ
	|	втТаблицаВНА.РезервПереоценкиМСФО <> 0
	|	И втТаблицаВНА.МодельПереоценки = ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&КонецГода,
	|	втТаблицаВНА.ВНА,
	|	втТаблицаВНА.ГруппаВНА,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ВводВЭксплуатацию),
	|	втТаблицаВНА.ВспомогательныйСчет,
	|	втТаблицаВНА.ВспомогательныйСчетСсылка,
	|	втТаблицаВНА.ВспомогательныйСубконто1,
	|	втТаблицаВНА.ВспомогательныйСубконто2,
	|	втТаблицаВНА.ВспомогательныйСубконто3,
	|	втТаблицаВНА.СчетАмортизацииМСФО,
	|	втТаблицаВНА.СчетАмортизацииМСФО.СчетСсылка,
	|	втТаблицаВНА.СчетАмортизацииСубконто1МСФО,
	|	втТаблицаВНА.СчетАмортизацииСубконто2МСФО,
	|	втТаблицаВНА.СчетАмортизацииСубконто3МСФО,
	|	1,
	|	втТаблицаВНА.ВспомогательныйПодразделение,
	|	втТаблицаВНА.ВспомогательныйНаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.СчетАмортизацииМСФО.СчетСсылка.УчетПоПодразделениям
	|			ТОГДА втТаблицаВНА.Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.СчетАмортизацииМСФО.СчетСсылка.УчетПоНаправлениямДеятельности
	|			ТОГДА втТаблицаВНА.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	втТаблицаВНА.ПереоценкаАмортизацииМСФО
	|ИЗ
	|	втТаблицаВНА КАК втТаблицаВНА
	|ГДЕ
	|	втТаблицаВНА.ПереоценкаАмортизацииМСФО <> 0
	|	И втТаблицаВНА.МодельПереоценки = ИСТИНА";
	
	Документы.УчетнаяПолитикаМСФО.ЗаменитьФиксированныеСчетаУчетаБД(ТекстЗапроса);

	Возврат	ТекстЗапроса;
	
КонецФункции

Процедура СформироватьДвижения(Движения, ДополнительныеСвойства, Отказ) Экспорт
	
	ТаблицаПроводки = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаПроводки;
	ТаблицаВНА = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаВНА;
	Реквизиты = ДополнительныеСвойства.ДляПроведения.Реквизиты;

	Если МСФОВНАУХ.ЕстьОшибкиПроведения(ДополнительныеСвойства, Отказ) Тогда
		Возврат;
	КонецЕсли;

	//Движения сведений по ВНА
	НастройкиОтражения = МСФОВНАУХ.ПолучитьНастройкиОтражения();
	НастройкиОтражения.Вставить("ПервоначальныеСведенияВНАМСФО", 	Истина);
	НастройкиОтражения.Вставить("ПараметрыУчетаВНАМСФО", 			Истина);
	НастройкиОтражения.Вставить("ПараметрыАмортизацииВНАМСФО", 		Истина);
	НастройкиОтражения.Вставить("СтоимостьВНАМСФО", 				Истина);

	МСФОВНАУХ.ОтразитьДвиженияПараметровУчетаВНА(Движения, Реквизиты, ТаблицаВНА, НастройкиОтражения);

	//Движения по таблице проводок
	МСФОВНАУХ.ОтразитьПроводкиВНА(Движения, Реквизиты, ТаблицаПроводки, Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеДокумента

//КоллекцияСтрок - ТабличнаяЧасть,ТаблицаЗначений,ДанныеФормыКоллекция, СтрокаТабличнойЧасти
//Источник - ДокументОбъект, ДанныеФормыСтруктура, УправляемаяФорма
//ПараметрыЗаполнения - Структура(ЗаполнитьРеквизиты = Ложь,  ЗаполнитьНСБУ = Ложь, ЗаполнитьМСФО = Ложь)
Процедура ЗаполнитьСтроки(КоллекцияСтрок, Источник, ПараметрыЗаполненияТЧ = Неопределено) Экспорт

	ПараметрыЗаполнения = МСФОУХ.ПолучитьПараметрыЗаполнения(Источник);
	ЗаполняемыеРеквизитыТаблицыФормы = МСФОУХ.ПолучитьЗаполняемыеРеквизитыТаблицыФормы(Источник);	
		
	Для каждого СтрокаДляЗаполнения Из КоллекцияСтрок Цикл
		
		Если ЗаполняемыеРеквизитыТаблицыФормы <> Неопределено Тогда
			МСФОКлиентСерверУХ.ЗаполнитьРасчетныеРеквизитыСтроки(СтрокаДляЗаполнения, ЗаполняемыеРеквизитыТаблицыФормы);			
		КонецЕсли;
				
	КонецЦикла;	

КонецПроцедуры 

Процедура ЗаполнитьЗависимостиРеквизитовДокумента(ФормаДокумента) Экспорт 

	ЗависимостиРеквизитов = Новый Массив;
	
	Реквизиты = Новый Структура;
	
	Реквизиты.Вставить("СчетУчетаМСФО", 			"СчетУчетаПервоначальнойСтоимости");
	Реквизиты.Вставить("СчетУчетаСубконто1МСФО", 	"СчетУчетаПервоначальнойСтоимостиСубконто1");
	Реквизиты.Вставить("СчетУчетаСубконто2МСФО", 	"СчетУчетаПервоначальнойСтоимостиСубконто2");
	Реквизиты.Вставить("СчетУчетаСубконто3МСФО", 	"СчетУчетаПервоначальнойСтоимостиСубконто3");
	
	Реквизиты.Вставить("СчетАмортизацииМСФО", 			"СчетНакопленнойАмортизации");
	Реквизиты.Вставить("СчетАмортизацииСубконто1МСФО", 	"СчетНакопленнойАмортизацииСубконто1");
	Реквизиты.Вставить("СчетАмортизацииСубконто2МСФО", 	"СчетНакопленнойАмортизацииСубконто2");
	Реквизиты.Вставить("СчетАмортизацииСубконто3МСФО", 	"СчетНакопленнойАмортизацииСубконто3");
	
	Для каждого Реквизит Из Реквизиты Цикл
	
		НоваяСтрока = Новый Структура;
		НоваяСтрока.Вставить("РеквизитГруппы", Реквизит.Значение);
		НоваяСтрока.Вставить("Приемник", Реквизит.Ключ);
		НоваяСтрока.Вставить("Источник", "ГруппаВНАМСФО");
		
		ЗависимостиРеквизитов.Добавить(НоваяСтрока);
		
	КонецЦикла;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("СпособНачисленияАмортизацииМСФО", 			"СпособНачисленияАмортизации");              
	Реквизиты.Вставить("СрокПолезногоИспользованияМСФО", 			"СрокПолезногоИспользования");        
	Реквизиты.Вставить("СпособОтраженияРасходовПоАмортизацииМСФО", 	"СпособОтраженияРасходовПоАмортизации");        
	Реквизиты.Вставить("КоэффициентУскоренияМСФО", 					"КоэффициентУскорения");
	
	Для каждого Реквизит Из Реквизиты Цикл
	
		НоваяСтрока = Новый Структура;		
		НоваяСтрока.Вставить("РеквизитГруппы", Реквизит.Значение);
		НоваяСтрока.Вставить("Приемник", Реквизит.Ключ);
		НоваяСтрока.Вставить("Источник", "ПараметрыАмортизацииВНАМСФО");
		
		ЗависимостиРеквизитов.Добавить(НоваяСтрока);
		
	КонецЦикла;
	
	ФормаДокумента.КэшируемыеЗначения.Вставить("ЗависимостиРеквизитов", ЗависимостиРеквизитов);
	
КонецПроцедуры

Функция ПолучитьРеквизитыАмортизации() Экспорт

	Результат = Новый Структура;
	Результат.Вставить("ВНА", 							"ВНА");
	Результат.Вставить("ПараметрыУчетаВНА", 			"ГруппаВНАМСФО");
	Результат.Вставить("СпособНачисленияАмортизации",	"СпособНачисленияАмортизацииМСФО");
	Результат.Вставить("СПИ", 							"СрокПолезногоИспользованияМСФО");
	Результат.Вставить("КоэффициентУскорения", 			"КоэффициентУскоренияМСФО");
	Результат.Вставить("ОбъемВыработки", 				"ПредполагаемыйОбъемПродукцииМСФО");
	Результат.Вставить("ПараметрВыработки", 			"ПараметрВыработкиМСФО");
	Результат.Вставить("ПрофильРаспределения", 			"СпособОтраженияРасходовПоАмортизацииМСФО");

	Возврат Результат;
	
КонецФункции

Функция ПолучитьИменаСубконто() Экспорт 

	Результат = Новый Структура;
	
	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетУчетаСубконто1НСБУ");
	Субконто.Вставить(2, "СчетУчетаСубконто2НСБУ");
	Субконто.Вставить(3, "СчетУчетаСубконто3НСБУ");
	
	Результат.Вставить("СчетУчетаНСБУ", Субконто);
	
	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетУчетаСубконто1МСФО");
	Субконто.Вставить(2, "СчетУчетаСубконто2МСФО");
	Субконто.Вставить(3, "СчетУчетаСубконто3МСФО");
	
	Результат.Вставить("СчетУчетаМСФО", Субконто);

	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетАмортизацииСубконто1НСБУ");
	Субконто.Вставить(2, "СчетАмортизацииСубконто2НСБУ");
	Субконто.Вставить(3, "СчетАмортизацииСубконто3НСБУ");
	
	Результат.Вставить("СчетАмортизацииНСБУ", Субконто);

	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетАмортизацииСубконто1МСФО");
	Субконто.Вставить(2, "СчетАмортизацииСубконто2МСФО");
	Субконто.Вставить(3, "СчетАмортизацииСубконто3МСФО");
	
	Результат.Вставить("СчетАмортизацииМСФО", Субконто);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
