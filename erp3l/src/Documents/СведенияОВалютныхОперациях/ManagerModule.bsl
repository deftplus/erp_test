#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Дата");
	Поля.Добавить("БанковскийСчет");
	Поля.Добавить("ДатаЗаполнения");
	Поля.Добавить("ПорядковыйНомерКорректировки");
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Представление = СтрШаблон(НСтр("ru = 'Сведения о валютных операциях от %1'"),	Формат(Данные.Дата, "ДЛФ=D"));
	Если ЗначениеЗаполнено(Данные.БанковскийСчет) Тогда
		Представление = Представление + НСтр("ru = ' по счету '") + Данные.БанковскийСчет;
	КонецЕсли;
	
КонецПроцедуры
	
#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПолучитьАктуальныйДокумент(ДатаЗаполнения, Банк, Организация, БанковскийСчет) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СведенияОВалютныхОперациях.Ссылка
	|ИЗ
	|	Документ.СведенияОВалютныхОперациях КАК СведенияОВалютныхОперациях
	|ГДЕ
	|	СведенияОВалютныхОперациях.Проведен
	|	И СведенияОВалютныхОперациях.Дата = &ДатаЗаполнения
	|	И СведенияОВалютныхОперациях.Банк = &Банк
	|	И СведенияОВалютныхОперациях.Организация = &Организация
	|	И СведенияОВалютныхОперациях.БанковскийСчет = &БанковскийСчет
	|
	|УПОРЯДОЧИТЬ ПО
	|	СведенияОВалютныхОперациях.МоментВремени УБЫВ";
	Запрос.УстановитьПараметр("ДатаЗаполнения", ДатаЗаполнения);
	Запрос.УстановитьПараметр("Банк", Банк);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("БанковскийСчет", БанковскийСчет);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Ссылка;
	КонецЦикла;
	
	Возврат Документы.СведенияОВалютныхОперациях.ПустаяСсылка();
КонецФункции
	
#КонецОбласти

#КонецЕсли
