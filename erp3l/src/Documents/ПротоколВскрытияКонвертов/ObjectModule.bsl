#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Если НЕ Отменен
				И ПредложенияПоставщиков.Количество() = 0 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru='Не указаны поставщики!'"),
				,
				"Объект.ПротоколВскрытияКонвертов",
				,
				Отказ);
		КонецЕсли;
	КонецЕсли;
	мДокументов = ЦентрализованныеЗакупкиУХ.ПолучитьДокументыПоОтбору(
		"ПротоколВскрытияКонвертов",
		Новый Структура("ЗакупочнаяПроцедура, Проведен",
						ЗакупочнаяПроцедура, Истина),
		Ссылка);
	Если мДокументов.Количество() > 0 Тогда
		ОбщегоНазначенияУХ.СообщитьОбОшибке(
			СтрШаблон(НСтр(
				"ru='По закупочной процедуре ""%1"" уже введен протокол вскрытия конвертов!"),
		 		ЗакупочнаяПроцедура),
			Отказ,
			,
			СтатусСообщения.Важное);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ТипДанныхЗаполнения = Тип("СправочникСсылка.ЗакупочныеПроцедуры") Тогда
		Если НЕ Документы.ПротоколВскрытияКонвертов.МожноВвестиНаОснованииЗакупки(
					ДанныеЗаполнения) Тогда
			ВызватьИсключение СтрШаблон(НСтр(
				"ru='Для закупки ""%1"" нельзя ввести протокол вскрытия конвертов!'"),
				ДанныеЗаполнения);
		КонецЕсли;
		ЗаполнитьПоЗакупке(ДанныеЗаполнения);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если Отменен Тогда
		ПроверяемыеРеквизиты.Добавить("ПричинаОтмены");
	Иначе
		ПроверяемыеРеквизиты.Добавить("РезультатРаботыКомиссии");
	КонецЕсли;
КонецПроцедуры

Процедура ЗаполнитьПоЗакупке(ЗакупочнаяПроцедураДляЗаполнения)
	ЗакупочнаяПроцедура = ЗакупочнаяПроцедураДляЗаполнения;
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗакупочнаяПроцедураДляЗаполнения);
	ЗаполнитьПредложениямиПоставщиковПоУмолчанию();
КонецПроцедуры

Процедура ЗаполнитьПредложениямиПоставщиковПоУмолчанию() Экспорт
	Если НЕ ЗначениеЗаполнено(ЗакупочнаяПроцедура) Тогда
		Возврат;
	КонецЕсли;
	мПредложений = Документы.ПредложениеПоставщика.ПолучитьПоЗакупочнойПроцедуре(
		ЗакупочнаяПроцедура);
	Для Каждого ПредолжениеПоставщика Из мПредложений Цикл
		Документы.ПротоколВскрытияКонвертов.ДобавитьСтрокуПредложенияПоставщика(
				ЗакупочнаяПроцедура, 
				ПредложенияПоставщиков, 
				СведенияОПредоставленныхДокументах, 
				ПредолжениеПоставщика);
	КонецЦикла;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Ответственный = Пользователи.ТекущийПользователь();
	УИД_ЕИС = "";
	РегистрационныйНомерЕИС = 0;
КонецПроцедуры


#КонецЕсли

