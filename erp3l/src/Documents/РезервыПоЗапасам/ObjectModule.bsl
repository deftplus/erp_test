
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ДанныеЗаполнения <> Неопределено Тогда
	
		Если ТипДанныхЗаполнения = Тип("Структура") Тогда
			
			Если ДанныеЗаполнения.Свойство("ЗаполнитьПоНСБУ") И (ДанныеЗаполнения.ЗаполнитьПоНСБУ = Истина) Тогда
			
				ЗаполнитьПоНСБУ(ДанныеЗаполнения);
				
			ИначеЕсли ДанныеЗаполнения.Свойство("АдресТаблицы") Тогда
				
				МСФОВызовСервераУХ.ЗаполнитьПоТаблицеЗагрузки(ЭтотОбъект, ДанныеЗаполнения);
				
			КонецЕсли;
			
		ИначеЕсли Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда	
			
			ЗаполнитьПоДокументуОснованию(ДанныеЗаполнения);
			
		КонецЕсли;
		
	Иначе 
		
		ИнициализироватьДокумент(ДанныеЗаполнения);
	
	КонецЕсли;	
	
	Если НЕ ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ИнициализироватьДокумент(ОбъектКопирования);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ПроведениеСерверУХ.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСерверУХ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	Документы.РезервыПоЗапасам.ПодготовитьПараметрыПроведения(ДополнительныеСвойства, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСерверУХ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);	
	Документы.РезервыПоЗапасам.СформироватьДвиженияРезервыПоЗапасам(Движения, ДополнительныеСвойства, Отказ);	
	ПроведениеСерверУХ.ОбработкаПроведения_ЗаписьИКонтроль(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	ПроведениеСерверУХ.ОбработкаУдаленияПроведения(ЭтотОбъект, Отказ);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_Стандартные

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	МСФОУХ.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения);
КонецПроцедуры

Процедура ЗаполнитьПоДокументуОснованию(Основание)
	
	//УправлениеВнеоборотнымиАктивамиПереопределяемый.ЗаполнитьПоОснованию(ЭтотОбъект, Основание);
	
КонецПроцедуры

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции_Документа

Процедура ЗаполнитьПоНСБУ(ДанныеЗаполнения)

	РезультатРасчета = Документы.РезервыПоЗапасам.ПолучитьТаблицуРезервов(ЭтотОбъект);
	
	//тч Резервы
	ЭтотОбъект.Резервы.Загрузить(РезультатРасчета.ТаблицаРезервы);
	
	РеквизитыУП = МСФОВызовСервераУХ.ОсновныеРеквизитыУП(Организация, Дата, Сценарий);
	ИспользуютсяТК = Не РеквизитыУП.ФормироватьПроводкиМСФО;
	
	СтруктураДействий = Новый Структура;	
	СтруктураДействий.Вставить("РассчитатьВычисляемыеПоля", ЭтотОбъект.ПериодОтчета);
	Если ИспользуютсяТК Тогда
		
		ПараметрыЦен = Новый Структура("Дата,Организация", КонецДня(ЭтотОбъект.Дата), Организация);
		СтруктураДействий.Вставить("ЗаполнитьСчетУчетаМСФО", ДанныеЗаполнения.ШаблонТрансляции);
		СтруктураДействий.Вставить("ЗаполнитьСчетРезерваМСФО",);
		СтруктураДействий.Вставить("ЗаполнитьЧистуюЦенуПродажи", ПараметрыЦен);
				
		Если Не ЗначениеЗаполнено(ДанныеЗаполнения.ШаблонТрансляции) Тогда
			Сообщить(НСтр("ru = 'Не удалось определить основной шаблон трансляции.'"));
		КонецЕсли;

	КонецЕсли;
	
	КэшируемыеЗначения = Неопределено;
	
	ОбработкаТабличныхЧастейКлиентСерверУХ.ОбработатьТЧ_РезервыМПЗ(ЭтотОбъект.Резервы, СтруктураДействий, КэшируемыеЗначения);
	
	//тч ДвиженияРезервовНСБУ
	ЭтотОбъект.ДвиженияРезервовНСБУ.Загрузить(РезультатРасчета.ТаблицаИспользованныеРезервы);
	
	СтруктураДействий = Новый Структура;	
	СтруктураДействий.Вставить("ЗаполнитьСчетаПоВидуДвижений", Новый Структура("Контекст", ЭтотОбъект));
	
	КэшируемыеЗначения = Неопределено;
	
	ОбработкаТабличныхЧастейКлиентСерверУХ.ОбработатьТЧ_РезервыМПЗ(ЭтотОбъект.ДвиженияРезервовНСБУ, СтруктураДействий, КэшируемыеЗначения);
	
КонецПроцедуры

#КонецОбласти  

#КонецЕсли

