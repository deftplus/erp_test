#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ПодготовкаПараметровПроведенияДокумента

Функция ПодготовитьПараметрыПроведения(ДополнительныеСвойства, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДополнительныеСвойства.ДляПроведения.Ссылка);
	
	Реквизиты = МСФОУХ.РеквизитыДокумента(Запрос, "ПериодОтчета, ДатаНачала, ДатаОкончания, ВидОтчета", "ПериодОтчета", Отказ);
	
	Реквизиты.Вставить("ГраницыПериода", 		МСФОВНАВызовСервераУХ.ПолучитьГраницыПериодаДокумента(Реквизиты,,Истина));
	Реквизиты.Вставить("Период", 				КонецДня(Реквизиты.ГраницыПериода.ДатаОкончания));
	Реквизиты.Вставить("РеквизитыОшибки",		Новый Структура("Номенклатура", "Номенклатура"));
	Реквизиты.Вставить("ИсточникКорректировки",	"РасчетныеДанныеЗапасы");
				
	ДополнительныеСвойства.ДляПроведения.Вставить("Реквизиты", Реквизиты);
	
	Если Отказ Тогда
		Возврат ДополнительныеСвойства;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ФормироватьПроводкиМСФО", 		Реквизиты.ФормироватьПроводкиМСФО);		
	Запрос.УстановитьПараметр("Период",							Реквизиты.ГраницыПериода.ДатаОкончания);
	Запрос.УстановитьПараметр("ДатаНачалаПериода",				Реквизиты.ГраницыПериода.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончанияПериода",			Реквизиты.ГраницыПериода.ДатаОкончания);		
	Запрос.УстановитьПараметр("ДатаНачалаСледующегоПериода",	ОбщегоНазначенияУХ.ДобавитьДень(Реквизиты.ГраницыПериода.ДатаОкончания, 1));
	Запрос.УстановитьПараметр("ФункциональнаяВалюта",			Реквизиты.ФункциональнаяВалюта);		
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаТаблицаДляПроводок(НомераТаблиц);
	
	ПроведениеСерверУХ.ДополнитьТаблицамиИзПакетаЗапросов(ДополнительныеСвойства.ТаблицыДляДвижений, Запрос, НомераТаблиц);
	
КонецФункции

Функция ТекстЗапросаТаблицаДляПроводок(НомераТаблиц)

	НомераТаблиц.Вставить("втРезервы",					НомераТаблиц.Количество());
	НомераТаблиц.Вставить("втИспользованныеРезервы",	НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаПроводок",			НомераТаблиц.Количество());
	
    Возврат	
	"ВЫБРАТЬ
	|	Резервы.Номенклатура КАК Номенклатура,
	|	Резервы.СчетЗапасов КАК СчетЗапасов,
	|	Резервы.СчетРезерва КАК СчетРезерва,
	|	Резервы.КоличествоОстаткаЗапасовНаКонецПериода КАК КоличествоОстаткаЗапасовНаКонецПериода,
	|	Резервы.СтоимостьОстаткаЗапасовНаКонецПериодаМСФО КАК СтоимостьОстатокНСБУ,
	|	Резервы.ЧистаяЦенаПродажиЗаЕдиницу КАК ЧистаяЦенаПродажиЗаЕдиницу,
	|	Резервы.СтоимостьОстаткаПоЧистойЦенеПродажи КАК СтоимостьОстаткаПоЧистойЦенеПродажи,
	|	Резервы.СтоимостьОстаткаЗапасовНаНачалоПериода КАК СтоимостьОстаткаЗапасовНаНачалоПериода,
	|	Резервы.СписаниеЗапасовЗаПериодМСФО КАК СписаниеЗапасовЗаПериодМСФО,
	|	Резервы.Оборачиваемость КАК Оборачиваемость,
	|	Резервы.РезервПоОбесценениюМСФО КАК РезервПоОбесценениюМСФО,
	|	Резервы.РезервПоОборачиваемостиМСФО КАК РезервПоОборачиваемостиМСФО,
	|	Резервы.РезервИтогоМСФО КАК РезервИтогоМСФО,
	|	Резервы.Номенклатура.ПараметрыРасчетаРезерваМСФО.СчетРасхода КАК СчетРасхода,
	|	Резервы.Номенклатура.ПараметрыРасчетаРезерваМСФО.СчетРасходаСубконто1 КАК СчетРасходаСубконто1,
	|	Резервы.Номенклатура.ПараметрыРасчетаРезерваМСФО.СчетРасходаСубконто2 КАК СчетРасходаСубконто2,
	|	Резервы.Номенклатура.ПараметрыРасчетаРезерваМСФО.СчетРасходаСубконто3 КАК СчетРасходаСубконто3,
	|	Резервы.Субконто2 КАК Субконто2,
	|	Резервы.Субконто3 КАК Субконто3,
	|	Резервы.Подразделение КАК Подразделение,
	|	Резервы.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ втРезервы
	|ИЗ
	|	Документ.РезервыПоЗапасам.Резервы КАК Резервы
	|ГДЕ
	|	Резервы.Ссылка = &Ссылка
	|	И Резервы.РезервИтогоМСФО <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДвиженияРезервовНСБУ.Номенклатура КАК Номенклатура,
	|	ДвиженияРезервовНСБУ.СчетУчета КАК СчетУчета,
	|	ДвиженияРезервовНСБУ.Субконто2 КАК Субконто2,
	|	ДвиженияРезервовНСБУ.Субконто3 КАК Субконто3,
	|	ДвиженияРезервовНСБУ.ДатаОперации КАК ДатаОперации,
	|	ДвиженияРезервовНСБУ.ВидДвижения КАК ВидДвижения,
	|	ДвиженияРезервовНСБУ.СуммаОперацииВВалюте КАК СуммаОперацииВВалюте,
	|	ДвиженияРезервовНСБУ.СуммаОперации КАК СуммаОперации,
	|	ДвиженияРезервовНСБУ.СчетДтНСБУ КАК СчетДтНСБУ,
	|	ДвиженияРезервовНСБУ.СчетКтНСБУ КАК СчетКтНСБУ,
	|	ДвиженияРезервовНСБУ.СчетДтМСФО КАК СчетДтМСФО,
	|	ДвиженияРезервовНСБУ.СчетКтМСФО КАК СчетКтМСФО,
	|	ДвиженияРезервовНСБУ.ПодразделениеДт КАК ПодразделениеДт,
	|	ДвиженияРезервовНСБУ.НаправлениеДеятельностиДт КАК НаправлениеДеятельностиДт,
	|	ДвиженияРезервовНСБУ.ПодразделениеКт КАК ПодразделениеКт,
	|	ДвиженияРезервовНСБУ.НаправлениеДеятельностиКт КАК НаправлениеДеятельностиКт
	|ПОМЕСТИТЬ втДвиженияРезервовНСБУ
	|ИЗ
	|	Документ.РезервыПоЗапасам.ДвиженияРезервовНСБУ КАК ДвиженияРезервовНСБУ
	|ГДЕ
	|	ДвиженияРезервовНСБУ.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ДатаОкончанияПериода КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияРезервыЗапасы.НачислениеРезерваПоЗапасам) КАК Событие,
	|	СторноФИ.Номенклатура КАК Номенклатура,
	|	СторноФИ.Номенклатура.ПараметрыРасчетаРезерваМСФО.СчетРасхода КАК СчетДт,
	|	СторноФИ.Номенклатура.ПараметрыРасчетаРезерваМСФО.СчетРасходаСубконто1 КАК СубконтоДт1,
	|	СторноФИ.Номенклатура.ПараметрыРасчетаРезерваМСФО.СчетРасходаСубконто2 КАК СубконтоДт2,
	|	СторноФИ.Номенклатура.ПараметрыРасчетаРезерваМСФО.СчетРасходаСубконто3 КАК СубконтоДт3,
	|	СторноФИ.СчетРезерва КАК СчетКт,
	|	СторноФИ.Номенклатура КАК СубконтоКт1,
	|	СторноФИ.Субконто2 КАК СубконтоКт2,
	|	СторноФИ.Субконто3 КАК СубконтоКт3,
	|	СторноФИ.РезервИтогоМСФО КАК СуммаВВалюте,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ОперацииДляЦелейОтчетности) КАК ВидОперации,
	|	&ФункциональнаяВалюта КАК Валюта,
	|	СторноФИ.Подразделение КАК ПодразделениеДт,
	|	СторноФИ.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	СторноФИ.Подразделение КАК ПодразделениеКт,
	|	СторноФИ.НаправлениеДеятельности КАК НаправлениеДеятельностиКт
	|ИЗ
	|	втРезервы КАК СторноФИ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&ДатаНачалаСледующегоПериода,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияРезервыЗапасы.СторноРезерваПоЗапасам),
	|	СторноФИ.Номенклатура,
	|	СторноФИ.СчетРасхода,
	|	СторноФИ.СчетРасходаСубконто1,
	|	СторноФИ.СчетРасходаСубконто2,
	|	СторноФИ.СчетРасходаСубконто3,
	|	СторноФИ.СчетРезерва,
	|	СторноФИ.Номенклатура,
	|	СторноФИ.Субконто2,
	|	СторноФИ.Субконто3,
	|	-СторноФИ.РезервИтогоМСФО,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ОперацииДляЦелейОтчетности),
	|	&ФункциональнаяВалюта,
	|	СторноФИ.Подразделение,
	|	СторноФИ.НаправлениеДеятельности,
	|	СторноФИ.Подразделение,
	|	СторноФИ.НаправлениеДеятельности
	|ИЗ
	|	втРезервы КАК СторноФИ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втДвиженияРезервовНСБУ.ДатаОперации,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияРезервыЗапасы.КорректировкаОтменыСторноПериодаПоЗапасам),
	|	втДвиженияРезервовНСБУ.Номенклатура,
	|	втДвиженияРезервовНСБУ.СчетДтМСФО,
	|	NULL,
	|	NULL,
	|	NULL,
	|	втДвиженияРезервовНСБУ.СчетКтМСФО,
	|	втДвиженияРезервовНСБУ.Номенклатура,
	|	NULL,
	|	NULL,
	|	-втДвиженияРезервовНСБУ.СуммаОперации,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций.УчетныеОперации),
	|	&ФункциональнаяВалюта,
	|	втДвиженияРезервовНСБУ.ПодразделениеДт,
	|	втДвиженияРезервовНСБУ.НаправлениеДеятельностиДт,
	|	втДвиженияРезервовНСБУ.ПодразделениеКт,
	|	втДвиженияРезервовНСБУ.НаправлениеДеятельностиКт
	|ИЗ
	|	втДвиженияРезервовНСБУ КАК втДвиженияРезервовНСБУ
	|ГДЕ
	|	&ФормироватьПроводкиМСФО = ЛОЖЬ";

КонецФункции
     	
#КонецОбласти

#Область ЗаполнениеДокумента_Стандартные

Функция ПолучитьРазделыСчетов() Экспорт

	РазделЗапасы = Справочники.РазделыПланаСчетов.Запасы;
	
	РазделыСчетов = Новый Структура;
	РазделыСчетов.Вставить("Резервы", Новый Структура());
	
	РазделыСчетов.Резервы.Вставить("СчетЗапасов",	РазделЗапасы);
	РазделыСчетов.Резервы.Вставить("СчетРезерва", 	РазделЗапасы);
	
	РазделыСчетов.Вставить("ИспользованныеРезервы", Новый Структура());
	
	РазделыСчетов.ИспользованныеРезервы.Вставить("СчетУчета",	РазделЗапасы);	

	Возврат РазделыСчетов;
	
КонецФункции

Функция ПолучитьИменаСубконто() Экспорт 

	ИменаСубконто = Новый Структура;
	
	ИменаСубконто.Вставить("Резервы", Новый Соответствие);
	
	ИменаСубконто.Резервы.Вставить("СчетЗапасовНСБУ", Новый Соответствие);
	
	Субконто = Новый Соответствие;
	//Субконто.Вставить(1, "Номенклатура");
	Субконто.Вставить(2, "Субконто2");
	Субконто.Вставить(3, "Субконто3");
	
	ИменаСубконто.Резервы.Вставить("СчетЗапасов", Субконто);	
	
	Субконто = Новый Соответствие;
	//Субконто.Вставить(1, "Номенклатура");
	//Субконто.Вставить(2, "Субконто2");
	//Субконто.Вставить(3, "Субконто3");
	
	ИменаСубконто.Резервы.Вставить("СчетРезерва", Субконто);
	
	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "ПараметрыРасчетаСчетРасходаСубконто1");
	Субконто.Вставить(2, "ПараметрыРасчетаСчетРасходаСубконто2");
	Субконто.Вставить(3, "ПараметрыРасчетаСчетРасходаСубконто3");
		
	ИменаСубконто.Резервы.Вставить("ПараметрыРасчетаСчетРасхода", Субконто);
		
	Возврат ИменаСубконто;	
	
КонецФункции

#КонецОбласти

#Область ЗаполнениеДокумента

Функция ПолучитьТаблицуРезервов(Контекст) Экспорт

	ДатаУП = Контекст.ПериодОтчета.ДатаОкончания;
	
	ПутиУП = МСФОВызовСервераУХ.ПутиРеквизитовУП("ФормироватьПроводкиМСФО, ПланСчетовМСФО, ФункциональнаяВалюта, ИсточникДляЧистойЦеныПродажиЗапасов");
	РеквизитыУП = МСФОВызовСервераУХ.ЗначенияПоОрганизацииУП(
		МСФОВызовСервераУХ.ЗначенияУП(ПутиУП, Контекст.Организация, ДатаУП, Контекст.Сценарий));
    
	Если РеквизитыУП.ФормироватьПроводкиМСФО Тогда
		
		НомераТаблиц = Новый Структура;
		
		ТекстЗапроса = Новый Массив;
		ТекстЗапроса.Добавить(ТекстЗапроса_втСчетаРезерва(НомераТаблиц));
		ТекстЗапроса.Добавить(ТекстЗапроса_втОстаткиОборотыНоменклатуры(НомераТаблиц));
		ТекстЗапроса.Добавить(ТекстЗапроса_втСчетаБД(НомераТаблиц));
		ТекстЗапроса.Добавить(ВстраиваниеУХ.ТекстЗапроса_втЦеныНоменклатуры(НомераТаблиц));
		ТекстЗапроса.Добавить(ТекстЗапроса_ТаблицаРезервы(НомераТаблиц));
		ТекстЗапроса.Добавить(ТекстЗапроса_ТаблицаИспользованныеРезервы(НомераТаблиц));
		
		Запрос = Новый Запрос(СтрСоединить(ТекстЗапроса, ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета()));
		
		ГраницаДоДокумента = МСФОВызовСервераУХ.ПолучитьГраницуДоДокумента(Контекст.Дата, Контекст.Ссылка);
		
		Запрос.УстановитьПараметр("ДатаНачалаПериода", 						Контекст.ПериодОтчета.ДатаНачала);
		Запрос.УстановитьПараметр("ДатаОкончанияПериода", 					КонецДня(Контекст.ПериодОтчета.ДатаОкончания));
		Запрос.УстановитьПараметр("ГраницаОкончанияПериода", 				ГраницаДоДокумента);
		Запрос.УстановитьПараметр("Организация", 							Контекст.Организация);
		Запрос.УстановитьПараметр("Сценарий", 								Контекст.Сценарий);	
		Запрос.УстановитьПараметр("ФункциональнаяВалюта",					РеквизитыУП.ФункциональнаяВалюта);
		Запрос.УстановитьПараметр("ПланСчетовПриемник",						РеквизитыУП.ПланСчетовМСФО);
		Запрос.УстановитьПараметр("ИсточникДляЧистойЦеныПродажиЗапасов",	РеквизитыУП.ИсточникДляЧистойЦеныПродажиЗапасов);
			
		РезультатыЗапроса = Запрос.ВыполнитьПакет();	
		ЧислоПодзапросов = РезультатыЗапроса.ВГраница();
		
		ТаблицаРезервы 					= РезультатыЗапроса.Получить(НомераТаблиц.ТаблицаРезервы).Выгрузить();
		ТаблицаИспользованныеРезервы 	= РезультатыЗапроса.Получить(НомераТаблиц.ТаблицаИспользованныеРезервы).Выгрузить();
		
	Иначе
		
		ГруппаАналитик = Справочники.ФиксированныеГруппыАналитик.РезервыМПЗ_ДвижениеРезервовНСБУ;
		ОтборАналитик = Новый Соответствие;
		ПоляВыборки = Новый Соответствие;
		
		ТаблицаИспользованныеРезервы = Справочники.ВидыОтчетов.ПолучитьТаблицуТК(Контекст, ГруппаАналитик,,ОтборАналитик);
		
		ГруппаАналитик = Справочники.ФиксированныеГруппыАналитик.РезервыМПЗ_НачислениеРезерваМСФО;
				
		ОтборАналитик = Новый Соответствие;
				
		ПоляВыборки = Новый Соответствие;
		ПоляВыборки.Вставить("СчетУчетаЗапасовМСФО", "СчетБД");
		
		//свернуть по показателям
		ТаблицаРезервы = Справочники.ВидыОтчетов.ПолучитьТаблицуТК(Контекст, ГруппаАналитик, ПоляВыборки, ОтборАналитик);
								
	КонецЕсли;
	
	Возврат Новый Структура("ТаблицаРезервы,ТаблицаИспользованныеРезервы", ТаблицаРезервы, ТаблицаИспользованныеРезервы);	

КонецФункции

Функция ТекстЗапроса_втСчетаРезерва(НомераТаблиц)

	НомераТаблиц.Вставить("НомераТаблиц", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	СчетаУчетаЗапасовИРезервовМСФО.СчетЗапасов КАК СчетЗапасов,
	|	СчетаУчетаЗапасовИРезервовМСФО.СчетЗапасов.СчетСсылка КАК СчетЗапасовМСФО,
	|	СчетаУчетаЗапасовИРезервовМСФО.СчетРезерва КАК СчетРезерва,
	|	СчетаУчетаЗапасовИРезервовМСФО.СчетРезерва.СчетСсылка КАК СчетРезерваМСФО,
	|	СчетаУчетаЗапасовИРезервовМСФО.ПараметрыРасчетаРезерва КАК ПараметрыРасчетаРезерва
	|ПОМЕСТИТЬ втСчетаРезерва
	|ИЗ
	|	РегистрСведений.СчетаУчетаЗапасовИРезервовМСФО КАК СчетаУчетаЗапасовИРезервовМСФО";

КонецФункции

Функция ТекстЗапроса_втОстаткиОборотыНоменклатуры(НомераТаблиц)

	НомераТаблиц.Вставить("втОстаткиОборотыНоменклатуры", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	ХозрасчетныйОстаткиИОбороты.Счет КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстаткиИОбороты.Субконто1 ССЫЛКА Справочник.Номенклатура
	|			ТОГДА ХозрасчетныйОстаткиИОбороты.Субконто1
	|		КОГДА ХозрасчетныйОстаткиИОбороты.Субконто2 ССЫЛКА Справочник.Номенклатура
	|			ТОГДА ХозрасчетныйОстаткиИОбороты.Субконто2
	|		КОГДА ХозрасчетныйОстаткиИОбороты.Субконто3 ССЫЛКА Справочник.Номенклатура
	|			ТОГДА ХозрасчетныйОстаткиИОбороты.Субконто3
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ КАК Номенклатура,
	|	(ВЫБОР
	|		КОГДА ХозрасчетныйОстаткиИОбороты.Субконто1 ССЫЛКА Справочник.Номенклатура
	|			ТОГДА ХозрасчетныйОстаткиИОбороты.Субконто1
	|		КОГДА ХозрасчетныйОстаткиИОбороты.Субконто2 ССЫЛКА Справочник.Номенклатура
	|			ТОГДА ХозрасчетныйОстаткиИОбороты.Субконто2
	|		КОГДА ХозрасчетныйОстаткиИОбороты.Субконто3 ССЫЛКА Справочник.Номенклатура
	|			ТОГДА ХозрасчетныйОстаткиИОбороты.Субконто3
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ).ПараметрыРасчетаРезерваМСФО КАК ПараметрыРасчетаРезерва,
	|	ХозрасчетныйОстаткиИОбороты.Субконто2 КАК Субконто2,
	|	ХозрасчетныйОстаткиИОбороты.Субконто3 КАК Субконто3,
	|	ХозрасчетныйОстаткиИОбороты.СуммаВВалютеУчетаНачальныйОстаток КАК СтоимостьОстаткаЗапасовНаНачалоПериода,
	|	ХозрасчетныйОстаткиИОбороты.СуммаВВалютеУчетаОборотКт КАК СписаниеЗапасовЗаПериодМСФО,
	|	ХозрасчетныйОстаткиИОбороты.СуммаВВалютеУчетаКонечныйОстаток КАК СтоимостьОстаткаЗапасовНаКонецПериодаМСФО,
	|	ХозрасчетныйОстаткиИОбороты.КоличествоКонечныйОстаток КАК КоличествоОстаткаЗапасовНаКонецПериода,
	|	ХозрасчетныйОстаткиИОбороты.Подразделение КАК Подразделение,
	|	ХозрасчетныйОстаткиИОбороты.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ втОстаткиОборотыНоменклатуры
	|ИЗ
	|	РегистрБухгалтерии.МСФО.ОстаткиИОбороты(
	|			&ДатаНачалаПериода,
	|			&ГраницаОкончанияПериода,
	|			,
	|			,
	|			Счет В ИЕРАРХИИ
	|				(ВЫБРАТЬ
	|					т.СчетЗапасовМСФО
	|				ИЗ
	|					втСчетаРезерва КАК т),
	|			,
	|			Организация = &Организация
	|				И Сценарий = &Сценарий
	|				И ВидОперации <> ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ОперацииДляЦелейОтчетности)) КАК ХозрасчетныйОстаткиИОбороты
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ХозрасчетныйОстаткиИОбороты.Субконто1 ССЫЛКА Справочник.Номенклатура
	|				ТОГДА ХозрасчетныйОстаткиИОбороты.Субконто1
	|			КОГДА ХозрасчетныйОстаткиИОбороты.Субконто2 ССЫЛКА Справочник.Номенклатура
	|				ТОГДА ХозрасчетныйОстаткиИОбороты.Субконто2
	|			КОГДА ХозрасчетныйОстаткиИОбороты.Субконто3 ССЫЛКА Справочник.Номенклатура
	|				ТОГДА ХозрасчетныйОстаткиИОбороты.Субконто3
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|		КОНЕЦ <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)";

КонецФункции

Функция ТекстЗапроса_втСчетаБД(НомераТаблиц)

	НомераТаблиц.Вставить("втСчетаБД", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	МАКСИМУМ(СчетаБД.Ссылка) КАК СчетБД,
	|	СчетаБД.СчетСсылка КАК СчетПланаСчетов
	|ПОМЕСТИТЬ втСчетаБД
	|ИЗ
	|	Справочник.СчетаБД КАК СчетаБД
	|ГДЕ
	|	СчетаБД.Владелец = &ПланСчетовПриемник
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетаБД.СчетСсылка";

КонецФункции

Функция ТекстЗапроса_ТаблицаРезервы(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаРезервы", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	втСчетаБД.СчетБД КАК СчетЗапасов,
	|	втОстаткиОборотыНоменклатуры.Номенклатура КАК Номенклатура,
	|	втОстаткиОборотыНоменклатуры.Подразделение КАК Подразделение,
	|	втОстаткиОборотыНоменклатуры.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втОстаткиОборотыНоменклатуры.Субконто2 КАК Субконто2,
	|	втОстаткиОборотыНоменклатуры.Субконто3 КАК Субконто3,
	|	втОстаткиОборотыНоменклатуры.СтоимостьОстаткаЗапасовНаНачалоПериода КАК СтоимостьОстаткаЗапасовНаНачалоПериода,
	|	втОстаткиОборотыНоменклатуры.СписаниеЗапасовЗаПериодМСФО КАК СписаниеЗапасовЗаПериодМСФО,
	|	втОстаткиОборотыНоменклатуры.СтоимостьОстаткаЗапасовНаКонецПериодаМСФО КАК СтоимостьОстаткаЗапасовНаКонецПериодаМСФО,
	|	ВЫБОР
	|		КОГДА втОстаткиОборотыНоменклатуры.КоличествоОстаткаЗапасовНаКонецПериода > 0
	|			ТОГДА втОстаткиОборотыНоменклатуры.КоличествоОстаткаЗапасовНаКонецПериода
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоОстаткаЗапасовНаКонецПериода,
	|	ЕСТЬNULL(втЦеныНоменклатуры.Цена, 0) КАК ЧистаяЦенаПродажиЗаЕдиницу,
	|	ЕСТЬNULL(втСчетаРезерваПоПараметрамРасчета.СчетРезерва, втСчетаРезерваОбщие.СчетРезерва) КАК СчетРезерва
	|ИЗ
	|	втОстаткиОборотыНоменклатуры КАК втОстаткиОборотыНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСчетаБД КАК втСчетаБД
	|		ПО втОстаткиОборотыНоменклатуры.СчетУчета = втСчетаБД.СчетПланаСчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ втЦеныНоменклатуры КАК втЦеныНоменклатуры
	|		ПО втОстаткиОборотыНоменклатуры.Номенклатура = втЦеныНоменклатуры.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСчетаРезерва КАК втСчетаРезерваПоПараметрамРасчета
	|		ПО втОстаткиОборотыНоменклатуры.СчетУчета = втСчетаРезерваПоПараметрамРасчета.СчетЗапасовМСФО
	|			И втОстаткиОборотыНоменклатуры.ПараметрыРасчетаРезерва = втСчетаРезерваПоПараметрамРасчета.ПараметрыРасчетаРезерва
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСчетаРезерва КАК втСчетаРезерваОбщие
	|		ПО (втОстаткиОборотыНоменклатуры.СчетУчета В (втСчетаРезерваОбщие.СчетЗапасовМСФО, втСчетаРезерваОбщие.СчетЗапасовМСФО.Родитель))
	|			И (втСчетаРезерваОбщие.ПараметрыРасчетаРезерва = ЗНАЧЕНИЕ(Справочник.ПараметрыРасчетаРезервовПоЗапасам.ПустаяСсылка))";

КонецФункции

Функция ТекстЗапроса_ТаблицаИспользованныеРезервы(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаИспользованныеРезервы", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	ИспользованныеРезервыЗаПериод.Период КАК ДатаИспользования,
	|	ИспользованныеРезервыЗаПериод.Субконто1 КАК Номенклатура,
	|	втСчетаРезерва.СчетЗапасов КАК СчетУчета,
	|	ИспользованныеРезервыЗаПериод.Счет КАК СчетУчетаПланаСчетовМСФО,
	|	ИспользованныеРезервыЗаПериод.Субконто2 КАК Субконто2,
	|	ИспользованныеРезервыЗаПериод.Субконто3 КАК Субконто3,
	|	-ИспользованныеРезервыЗаПериод.СуммаВВалютеУчетаОборот КАК СуммаИспользованногоРезерва,
	|	ИспользованныеРезервыЗаПериод.Подразделение КАК Подразделение,
	|	ИспользованныеРезервыЗаПериод.ПодразделениеКор КАК ПодразделениеКор,
	|	ИспользованныеРезервыЗаПериод.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ИспользованныеРезервыЗаПериод.НаправлениеДеятельностиКор КАК НаправлениеДеятельностиКор
	|ИЗ
	|	РегистрБухгалтерии.МСФО.Обороты(
	|			&ДатаНачалаПериода,
	|			&ГраницаОкончанияПериода,
	|			День,
	|			Счет В ИЕРАРХИИ
	|				(ВЫБРАТЬ
	|					т.СчетЗапасовМСФО
	|				ИЗ
	|					втСчетаРезерва КАК т),
	|			,
	|			Организация = &Организация
	|				И Сценарий = &Сценарий,
	|			КорСчет В ИЕРАРХИИ
	|				(ВЫБРАТЬ
	|					т.СчетРезерваМСФО
	|				ИЗ
	|					втСчетаРезерва КАК т),
	|			) КАК ИспользованныеРезервыЗаПериод
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСчетаРезерва КАК втСчетаРезерва
	|		ПО ИспользованныеРезервыЗаПериод.Счет = втСчетаРезерва.СчетЗапасовМСФО
	|ГДЕ
	|	ИспользованныеРезервыЗаПериод.СуммаВВалютеУчетаОборот <> 0";

КонецФункции

#КонецОбласти

#Область Движения

Процедура СформироватьДвиженияРезервыПоЗапасам(Движения, ДополнительныеСвойства, Отказ) Экспорт

	Реквизиты 		= ДополнительныеСвойства.ДляПроведения.Реквизиты;	
	ТаблицаПроводок = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаПроводок;
	
	ТаблицаПроводок.Колонки.Добавить("Организация");
	ТаблицаПроводок.Колонки.Добавить("Сценарий");
	ТаблицаПроводок.Колонки.Добавить("ПериодОтчета");
	ТаблицаПроводок.Колонки.Добавить("СуммаВФункциональнойВалюте");
	
	МСФОВызовСервераУХ.ОтразитьДвиженияПоТаблицеПроводок(Движения, ТаблицаПроводок, Реквизиты, Отказ, Ложь);
	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;	
	
	МСФОВызовСервераУХ.ЗаписатьДвижения(Движения);// ТК должна видеть движения
	
	Если НЕ Реквизиты.ФормироватьПроводкиМСФО Тогда		
		МодульТК = ОбщегоНазначения.ОбщийМодуль("ТрансформационныеКорректировкиУХ");
		МодульТК.СформироватьКорректировку(Реквизиты, Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
