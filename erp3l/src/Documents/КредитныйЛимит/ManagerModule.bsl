#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
Функция ВидыФИ() Экспорт

	ВидыФИ = Новый СписокЗначений;
	
	ВидыФИ.Добавить(Перечисления.ВидыФинансовыхИнструментов.КредитПолученный);
	ВидыФИ.Добавить(Перечисления.ВидыФинансовыхИнструментов.АккредитивВыданный);
	ВидыФИ.Добавить(Перечисления.ВидыФинансовыхИнструментов.ГарантияВыданная);
	ВидыФИ.Добавить(Перечисления.ВидыФинансовыхИнструментов.Овердрафт);	
	
	Возврат ВидыФИ;
	
КонецФункции

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт
	
	ТаблицыДляДвижений = Новый Структура;		
	ДополнительныеСвойства = Новый Структура;
	НомераТаблиц = Новый Структура;
	
	// реквизиты документа
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента();
	Реквизиты = ПроведениеСерверУХ.СтрокаТаблицыЗначенийВСтруктуру(Запрос.Выполнить().Выгрузить()[0]);
			
	ДополнительныеСвойства.Вставить("Реквизиты", Реквизиты);
	
	Если Отказ Тогда
		Возврат ДополнительныеСвойства;
	КонецЕсли;

	Запрос.УстановитьПараметр("Период", 		Реквизиты.Период);
	Запрос.УстановитьПараметр("Ссылка", 		Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("Контрагент", 	Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("ДатаНачала", 	Реквизиты.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", 	Реквизиты.ДатаОкончания);
			
	Запрос.Текст = ТекстЗапросаКредитныеЛимиты(НомераТаблиц);
 				
	Результат = Запрос.ВыполнитьПакет();

	Для Каждого НомерТаблицы Из НомераТаблиц Цикл
		ТаблицыДляДвижений.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
			
	ДополнительныеСвойства.Вставить("ТаблицыДляДвижений", ТаблицыДляДвижений);
	
	Возврат ДополнительныеСвойства;

КонецФункции

Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц = Неопределено) Экспорт 

	Если НомераТаблиц <> Неопределено Тогда
		НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());	
	КонецЕсли;	
	
	Возврат 
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.СуммаЛимита КАК СуммаЛимита,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.ДатаНачала,
	|	Реквизиты.ДатаОкончания
	|ИЗ
	|	Документ.КредитныйЛимит КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
КонецФункции
	
#КонецОбласти	

#Область СлужебныеПроцедурыИФункции
Функция ТекстЗапросаКредитныеЛимиты(НомераТаблиц)

	НомераТаблиц.Вставить("КредитныеЛимиты", НомераТаблиц.Количество());	
	
	Возврат
	"ВЫБРАТЬ
	|	&ДатаНачала КАК Период,
	|	&Контрагент КАК Контрагент,
	|	&Ссылка КАК Лимит,
	|	ИСТИНА КАК Использовать
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&ДатаОкончания,
	|	&Контрагент,
	|	&Ссылка,
	|	ЛОЖЬ";

КонецФункции
#КонецОбласти

#Область ОбработчикиОбновленияИнформационнойБазы

Процедура ЗаменитьКредитнуюЛиниюНаКредит() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КредитныйЛимитВидыФинансовыхИнструментов.Ссылка КАК Ссылка,
	|	СУММА(ВЫБОР
	|			КОГДА КредитныйЛимитВидыФинансовыхИнструментов.ВидФинансовогоИнструмента = ЗНАЧЕНИЕ(Перечисление.ВидыФинансовыхИнструментов.КредитнаяЛиния)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ЕстьКредитнаяЛиния,
	|	СУММА(ВЫБОР
	|			КОГДА КредитныйЛимитВидыФинансовыхИнструментов.ВидФинансовогоИнструмента = ЗНАЧЕНИЕ(Перечисление.ВидыФинансовыхИнструментов.КредитПолученный)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ЕстьКредит
	|ИЗ
	|	Документ.КредитныйЛимит.ВидыФинансовыхИнструментов КАК КредитныйЛимитВидыФинансовыхИнструментов
	|
	|СГРУППИРОВАТЬ ПО
	|	КредитныйЛимитВидыФинансовыхИнструментов.Ссылка
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР
	|			КОГДА КредитныйЛимитВидыФинансовыхИнструментов.ВидФинансовогоИнструмента = ЗНАЧЕНИЕ(Перечисление.ВидыФинансовыхИнструментов.КредитнаяЛиния)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) > 0
	|";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Объект = Выборка.Ссылка.ПолучитьОбъект();
		
		СтрокаКредитнаяЛиния = Объект.ВидыФинансовыхИнструментов.Найти(Перечисления.ВидыФинансовыхИнструментов.КредитнаяЛиния);
		Если СтрокаКредитнаяЛиния <> Неопределено Тогда
			Если Выборка.ЕстьКредит Тогда
				Объект.ВидыФинансовыхИнструментов.Удалить(СтрокаКредитнаяЛиния);
			Иначе
				СтрокаКредитнаяЛиния.ВидФинансовогоИнструмента = Перечисления.ВидыФинансовыхИнструментов.КредитПолученный;
			КонецЕсли;
		КонецЕсли;
		
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли