#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
	Организация = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ТекущаяОрганизация", "");
	Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	ПланСчетов = Справочники.ПланыСчетовМеждународногоУчета.ПланСчетовПоУмолчанию();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ТипОперации = Перечисления.ТипыРегламентныхОперацийМеждународныйУчет.ЗакрытиеСчетовДоходовРасходов Тогда
		ПроверитьДублиДокументовТекущегоПериода(Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	НастройкаФормированияПроводокЗаполнена();
	
	ТипыОпераций = Перечисления.ТипыРегламентныхОперацийМеждународныйУчет;
	Если ТипОперации = ТипыОпераций.ЗакрытиеСчетовДоходовРасходов Тогда
		ЗакрытиеСчетовУчетаДоходовРасходов(Отказ);
		
	Иначе
		РасчетКурсовыхРазниц();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура НастройкаФормированияПроводокЗаполнена()
	
	Отбор = Новый Структура("ПланСчетов, Организация", ПланСчетов, Организация);
	НастройкиФормированияПроводок = РегистрыСведений.ПланыСчетовМеждународногоУчетаОрганизаций.СрезПоследних(КонецДня(Дата),Отбор);
	Если НастройкиФормированияПроводок.Количество() Тогда
		НастройкаФормированияПроводокОрганизации = НастройкиФормированияПроводок[0].НастройкаФормированияПроводок;
	Иначе
		Текст = СтрШаблон(
					НСтр("ru = 'Для организации ""%1"" не задана настройка формирования проводок по плану счетов ""%2""';
						|en = 'The setting for generating entries according to chart of accounts ""%2"" is not specified for company ""%1""'"),
					Организация,
					ПланСчетов);
		ВызватьИсключение Текст;
	КонецЕсли;
	
	Если ТипОперации = Перечисления.ТипыРегламентныхОперацийМеждународныйУчет.ЗакрытиеСчетовДоходовРасходов Тогда
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаФормированияПроводокОрганизации,"СчетУчетаДоходов,СчетУчетаРасходов,СчетУчетаНераспределеннойПрибылиУбытка,СчетаДоходов,СчетаРасходов,СчетаПрибылейУбытков");
		Если НЕ (ЗначениеЗаполнено(Реквизиты.СчетУчетаДоходов) И ЗначениеЗаполнено(Реквизиты.СчетаДоходов)
			ИЛИ ЗначениеЗаполнено(Реквизиты.СчетУчетаРасходов) И ЗначениеЗаполнено(Реквизиты.СчетаРасходов)
			ИЛИ ЗначениеЗаполнено(Реквизиты.СчетУчетаНераспределеннойПрибылиУбытка) И ЗначениеЗаполнено(Реквизиты.СчетаПрибылейУбытков)) Тогда
			
			Текст = СтрШаблон(
						НСтр("ru = 'Для организации ""%1"" в настройке формирования проводок ""%2"" по плану счетов ""%3"" не заданы счета закрытия периода';
							|en = 'Period-end closing accounts are not set for the company ""%1"" in posting schema ""%2"" for the chart of accounts ""%3""'"),
						Организация,
						НастройкаФормированияПроводокОрганизации,
						ПланСчетов);
						
			ДополнительныеСвойства.Вставить("НастройкаФормированияПроводокСОшибкой", НастройкаФормированияПроводокОрганизации);
			
			ВызватьИсключение Текст;
			
		КонецЕсли;
		
	Иначе
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаФормированияПроводокОрганизации,"ДоходыКР,РасходыКР,ДоходыОтРазницПриПересчетеВП,РасходыОтРазницПриПересчетеВП");
		Если НЕ ЗначениеЗаполнено(Реквизиты.ДоходыКР) 
				ИЛИ НЕ ЗначениеЗаполнено(Реквизиты.РасходыКР)
				ИЛИ НЕ ЗначениеЗаполнено(Реквизиты.ДоходыОтРазницПриПересчетеВП) 
				ИЛИ НЕ ЗначениеЗаполнено(Реквизиты.РасходыОтРазницПриПересчетеВП) Тогда
			
			Текст = СтрШаблон(
						НСтр("ru = 'Для организации ""%1"" в настройке формирования проводок ""%2"" по плану счетов ""%3"" не заданы счета учета курсовых разниц';
							|en = 'Ledger accounts of exchange rate differences are not set for the company ""%1"" in posting schema ""%2"" for the chart of accounts ""%3""'"),
						Организация,
						НастройкаФормированияПроводокОрганизации,
						ПланСчетов);
			
			ДополнительныеСвойства.Вставить("НастройкаФормированияПроводокСОшибкой", НастройкаФормированияПроводокОрганизации);
			ВызватьИсключение Текст;
			
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьДублиДокументовТекущегоПериода(Отказ)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Т.Ссылка
		|ИЗ
		|	Документ.РегламентнаяОперацияМеждународныйУчет КАК Т
		|ГДЕ
		|	Т.Проведен
		|	И Т.ПланСчетов = &ПланСчетов
		|	И Т.Организация = &Организация
		|	И Т.Дата МЕЖДУ &ПериодНачало И &ПериодОкончание
		|	И НЕ Т.Ссылка = &ТекущийДокумент
		|	И Т.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОперацийМеждународныйУчет.ЗакрытиеСчетовДоходовРасходов)");
	
	Запрос.УстановитьПараметр("ПланСчетов", ПланСчетов);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ТекущийДокумент", Ссылка);
	Запрос.УстановитьПараметр("ПериодНачало", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("ПериодОкончание", КонецМесяца(Дата));
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'В указанном периоде уже существует аналогичный документ. Операция не выполнена.';
				|en = 'There is a similar document in the specified period. Operation is not executed.'"),,,,
			Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗакрытиеСчетовУчетаДоходовРасходов(Отказ)
	
	НаДату = КонецМесяца(Дата);
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ПланСчетов", ПланСчетов);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Дата", НаДату);
	Запрос.УстановитьПараметр("Граница", Новый Граница(НаДату, ВидГраницы.Включая));
	
	Запрос.Текст = ТекстЗапросаПроводок("СчетУчетаДоходов", "СчетаДоходов", НСтр("ru = 'Закрытие доходов';
																				|en = 'Close income'"));
	ЗаписатьПроводки(Запрос, Отказ, Истина);
	
	Запрос.Текст = ТекстЗапросаПроводок("СчетУчетаРасходов", "СчетаРасходов", НСтр("ru = 'Закрытие расходов';
																					|en = 'Close expenses'"));
	ЗаписатьПроводки(Запрос, Отказ);
	
	Запрос.Текст = ТекстЗапросаПроводок("СчетУчетаНераспределеннойПрибылиУбытка", "СчетаПрибылейУбытков", НСтр("ru = 'Нераспредленная прибыль\убыток';
																												|en = 'Retained profit\loss'"));
	ЗаписатьПроводки(Запрос, Отказ);
	
	Движения.Международный.Записывать = Ложь;
	
КонецПроцедуры

Процедура РасчетКурсовыхРазниц()
	
	ВалютаМеждународногоУчета = МеждународныйУчетОбщегоНазначения.УчетныеВалюты(ПланСчетов, Организация);
	ВалютаРегламентированногоУчета = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация);
	ФункциональнаяВалюта = РаботаСКурсамиВалютУТ.ПолучитьКурсВалюты(ВалютаМеждународногоУчета.Функциональная, Дата, ВалютаРегламентированногоУчета);
	ВалютаПредставления = РаботаСКурсамиВалютУТ.ПолучитьКурсВалюты(ВалютаМеждународногоУчета.Представления, Дата, ВалютаРегламентированногоУчета);
	
	НаДату = КонецДня(Дата);
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПланСчетов", ПланСчетов);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
	Запрос.УстановитьПараметр("Дата", НаДату);
	Запрос.УстановитьПараметр("Граница", Новый Граница(НаДату, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КурсЧислительФВ", ФункциональнаяВалюта.КурсЧислитель);
	Запрос.УстановитьПараметр("КурсЗнаменательФВ", ФункциональнаяВалюта.КурсЗнаменатель);
	Запрос.УстановитьПараметр("КурсЧислительВП", ВалютаПредставления.КурсЧислитель);
	Запрос.УстановитьПараметр("КурсЗнаменательВП", ВалютаПредставления.КурсЗнаменатель);
	
	ТипыОпераций = Перечисления.ТипыРегламентныхОперацийМеждународныйУчет;
	КоличествоВПорции = 1000;
	Если ТипОперации = ТипыОпераций.РасчетКурсовыхРазницВалютаПредставления Тогда
		Запрос.Текст = ТекстЗапросаКурсовыхРазницВалютаПредставления();
		ВсегоПорций = 
			Документы.РегламентнаяОперацияМеждународныйУчет.НеПересчитаноВВалютуПредставления(ПланСчетов, Организация, Дата)
			/ КоличествоВПорции;
	Иначе
		Запрос.Текст = ТекстЗапросаКурсовыхРазницФункциональнаяВалюта();
		ВсегоПорций = 
			Документы.РегламентнаяОперацияМеждународныйУчет.НезакрытыеКурсовыеРазницы(ПланСчетов, Организация, Дата)
			/ КоличествоВПорции;
	КонецЕсли;
	
	Замещать = Истина;
	Для НомерПорции = 0 По ВсегоПорций Цикл
		
		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			Прервать;
		КонецЕсли;

		// Обход порции результата запроса
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Проводка = СформироватьПроводкуПоКурсовойРазнице(Выборка);
			Если ТипОперации = ТипыОпераций.РасчетКурсовыхРазницВалютаПредставления Тогда
				Проводка.Содержание = НСтр("ru = 'Корректировка курса валюты представления';
											|en = 'Adjustment of presentation currency exchange rate'");
				Проводка.Сумма = 0;
			Иначе
				Проводка.Содержание = НСтр("ru = 'Курсовая разница';
											|en = 'Exchange difference'");
				Проводка.СуммаПредставления = РаботаСКурсамиВалютУТКлиентСервер.ПересчитатьПоКурсу(Проводка.Сумма,ФункциональнаяВалюта,ВалютаПредставления);
			КонецЕсли;
			
		КонецЦикла;// обработки порции
		
		Движения.Международный.Записать(Замещать);
		Если Замещать Тогда
		    Движения.Международный.Очистить();
		КонецЕсли;
		Замещать = Ложь;

	КонецЦикла;// по всем порциям
	
	Движения.Международный.Записывать = Ложь;
	
КонецПроцедуры

// Параметры:
// 	Выборка - ВыборкаИзРезультатаЗапроса - Описание:
// 	 *ВидыСубконтоСчета - ТаблицаЗначений - :
// 	  **НомерСтроки - Число - 
// Возвращаемое значение:
// 	РегистрБухгалтерииЗапись.Международный - Описание
Функция СформироватьПроводкуПоКурсовойРазнице(Выборка)
	
	Проводка = Движения.Международный.Добавить();
	Проводка.Активность = Истина;
	Проводка.Период = Выборка.Период;
	Проводка.Организация = Выборка.Организация;
	Проводка.ПланСчетов = Выборка.ПланСчетов;

	Если Выборка.КурсоваяРазница > 0 Тогда// отразим прибыль
		Счет = "Дт";
		Кор = "Кт";
		КорСчет = Выборка.Доходы;
		КорСубконто = Выборка.СтатьяДоходов;
		КорВидСубконто = Выборка.ВидСубконтоСтатьиДоходов;
		КорВалютный = Выборка.ДоходыВалютный;
		Если Выборка.ДоходыПоПодразделениям Тогда
			Подразделение = Выборка.Подразделение;
		КонецЕсли;
		Если Выборка.ДоходыПоНаправлениям Тогда
			НаправлениеДеятельности = Выборка.НаправлениеДеятельности;
		КонецЕсли;
	Иначе// отразим убытки
		Счет = "Кт";
		Кор = "Дт";
		КорСчет = Выборка.Расходы;
		КорСубконто = Выборка.СтатьяРасходов;
		КорВидСубконто = Выборка.ВидСубконтоСтатьиРасходов;
		КорВалютный = Выборка.РасходыВалютный;
		Если Выборка.РасходыПоПодразделениям Тогда
			Подразделение = Выборка.Подразделение;
		КонецЕсли;
		Если Выборка.РасходыПоНаправлениям Тогда
			НаправлениеДеятельности = Выборка.НаправлениеДеятельности;
		КонецЕсли;
		
	КонецЕсли;
	
	// заполним счет и аналитику возникновения курсовой разницы
	Проводка["Счет"+Счет] = Выборка.Счет;
	Проводка["Валюта"+Счет] = Выборка.Валюта;
	Проводка["Подразделение"+Счет] = Выборка.Подразделение;
	Проводка["НаправлениеДеятельности"+Счет] = Выборка.НаправлениеДеятельности;
	ВидыСубконтоСчета = Выборка.ВидыСубконтоСчета.Выгрузить(); // ТабличнаяЧасть - 
	Для Каждого СтрокаТаблицы Из ВидыСубконтоСчета Цикл
		Если Счет = "Дт" Тогда
			Проводка.СубконтоДт[СтрокаТаблицы.ВидСубконто] = Выборка["Субконто" + СтрокаТаблицы.НомерСтроки];
		Иначе
			Проводка.СубконтоКт[СтрокаТаблицы.ВидСубконто] = Выборка["Субконто" + СтрокаТаблицы.НомерСтроки];
		КонецЕсли;
	КонецЦикла;
	
	// заполним счет учета прибыли\убытка от курсовой разницы
	Проводка["Счет" + Кор] = КорСчет;
	Если КорВалютный Тогда
		Проводка["Валюта"+Кор] = Выборка.Валюта;
	КонецЕсли;
	Проводка["Подразделение"+Кор] = Подразделение;
	Проводка["НаправлениеДеятельности"+Кор] = НаправлениеДеятельности;
	Если Кор = "Дт" Тогда
		Проводка.СубконтоДт[КорВидСубконто] = КорСубконто;
	Иначе
		Проводка.СубконтоКт[КорВидСубконто] = КорСубконто;
	КонецЕсли;
	
	Проводка.Сумма = Макс(Выборка.КурсоваяРазница, -Выборка.КурсоваяРазница);
	Проводка.СуммаПредставления = Проводка.Сумма;
	Возврат Проводка;
	
КонецФункции

Процедура ЗаписатьПроводки(Запрос, Отказ, Замещать = Ложь)
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВидыСубконто = РезультатЗапроса[3].Выгрузить(); // ТаблицаЗначений - 
	ВидыСубконто.Индексы.Добавить("Счет");
	ДанныеПроводок = РезультатЗапроса[4].Выгрузить();
	Если ДанныеПроводок.Количество() Тогда
		НаборЗаписей = Движения.Международный;
		НаборЗаписей.Очистить();
		Для Каждого Данные Из ДанныеПроводок Цикл
			
			НоваяПроводка = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяПроводка, Данные);
			УстановитьСубконтоПоТипу(НоваяПроводка, Данные, "Дт", ВидыСубконто);
			УстановитьСубконтоПоТипу(НоваяПроводка, Данные, "Кт", ВидыСубконто);
			
		КонецЦикла;
		НаборЗаписей.Записать(Замещать);
	КонецЕсли;
	
КонецПроцедуры

// Заполняет субконто не по совпадению номеров субконто, а по совпадению типов
Процедура УстановитьСубконтоПоТипу(НоваяПроводка, Данные, ВидДвижения, ВидыСубконто)
	
	Отбор = Новый Структура("Счет", Данные["Счет" + ВидДвижения]);
	ВидыСубконтоСчета = ВидыСубконто.НайтиСтроки(Отбор);
	МаксКоличествоСубконто = МеждународныйУчетСерверПовтИсп.МаксКоличествоСубконто();
	ИмяСубконто = "Субконто" + ВидДвижения;
	Для Каждого ВидСубконто Из ВидыСубконтоСчета Цикл
		Для НомерСубконто = 1 По МаксКоличествоСубконто Цикл
			НовоеЗначениеСубконто = Данные[ИмяСубконто + НомерСубконто];
			ТипыСовпадают = Ложь;
			Для Каждого ТипВидаСубконто Из ВидСубконто.ТипЗначения.Типы() Цикл
				Если ТипВидаСубконто = ТипЗнч(НовоеЗначениеСубконто) Тогда
					ТипыСовпадают = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если ТипыСовпадают Тогда
				НоваяПроводка[ИмяСубконто][ВидСубконто.ВидСубконто] = НовоеЗначениеСубконто;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстЗапросаПроводок(ИмяПоляСчета, ИмяТабЧастиСчетов, СодержаниеПроводки)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НастройкаФормированияПроводокОрганизацийМУ.ПланСчетов,
	|	НастройкаФормированияПроводокОрганизацийМУ.Организация,
	|	НастройкаФормированияПроводокОрганизацийМУ.НастройкаФормированияПроводок,
	|	НастройкиФормированияПроводок.СчетУчетаДоходов КАК СчетУчета,
	|	НастройкиФормированияПроводок.СчетУчетаДоходов.Вид КАК ВидСчетаУчета,
	|	НастройкиФормированияПроводок.СчетУчетаДоходов.УчетПоПодразделениям КАК УчетПоПодразделениям,
	|	НастройкиФормированияПроводок.СчетУчетаДоходов.УчетПоНаправлениямДеятельности КАК УчетПоНаправлениям
	|ПОМЕСТИТЬ втСчетЗакрытия
	|ИЗ
	|	РегистрСведений.ПланыСчетовМеждународногоУчетаОрганизаций.СрезПоследних(&Дата, 
	|			ПланСчетов = &ПланСчетов И Организация = &Организация) КАК НастройкаФормированияПроводокОрганизацийМУ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.НастройкиФормированияПроводокМеждународногоУчета КАК НастройкиФормированияПроводок
	|	ПО
	|		НастройкаФормированияПроводокОрганизацийМУ.НастройкаФормированияПроводок = НастройкиФормированияПроводок.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСчетЗакрытия.НастройкаФормированияПроводок,
	|	ЗакрываемыеСчета.Счет,
	|	ЗакрываемыеСчета.Счет.УчетПоПодразделениям КАК УчетПоПодразделениям,
	|	ЗакрываемыеСчета.Счет.УчетПоНаправлениямДеятельности КАК УчетПоНаправлениям
	|ПОМЕСТИТЬ втЗакрываемыеСчета
	|ИЗ
	|	втСчетЗакрытия КАК втСчетЗакрытия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НастройкиФормированияПроводокМеждународногоУчета.СчетаДоходов КАК ЗакрываемыеСчета
	|		ПО втСчетЗакрытия.НастройкаФормированияПроводок = ЗакрываемыеСчета.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МеждународныйОстатки.Счет,
	|	МеждународныйОстатки.ПланСчетов,
	|	МеждународныйОстатки.Организация,
	|	ВЫБОР КОГДА МеждународныйОстатки.Счет.УчетПоПодразделениям
	|		ТОГДА МеждународныйОстатки.Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Подразделение,
	|	ВЫБОР КОГДА МеждународныйОстатки.Счет.УчетПоНаправлениямДеятельности
	|		ТОГДА МеждународныйОстатки.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА МеждународныйОстатки.Счет.Валютный
	|			ТОГДА МеждународныйОстатки.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Валюта,
	|	МеждународныйОстатки.Субконто1 КАК Субконто1,
	|	МеждународныйОстатки.Субконто2 КАК Субконто2,
	|	МеждународныйОстатки.Субконто3 КАК Субконто3,
	|	МеждународныйОстатки.СуммаОстатокДт - МеждународныйОстатки.СуммаОстатокКт КАК Сумма,
	|	МеждународныйОстатки.СуммаОстатокДт КАК СуммаДт,
	|	МеждународныйОстатки.СуммаОстатокКт КАК СуммаКт,
	|	МеждународныйОстатки.СуммаПредставленияОстатокДт - МеждународныйОстатки.СуммаПредставленияОстатокКт КАК СуммаПредставления,
	|	МеждународныйОстатки.СуммаПредставленияОстатокДт КАК СуммаПредставленияДт,
	|	МеждународныйОстатки.СуммаПредставленияОстатокКт КАК СуммаПредставленияКт,
	|	МеждународныйОстатки.ВалютнаяСуммаОстатокДт - МеждународныйОстатки.ВалютнаяСуммаОстатокКт КАК ВалютнаяСумма,
	|	МеждународныйОстатки.ВалютнаяСуммаОстатокДт КАК ВалютнаяСуммаДт,
	|	МеждународныйОстатки.ВалютнаяСуммаОстатокКт КАК ВалютнаяСуммаКт,
	|	МеждународныйОстатки.Счет.Вид КАК ВидСчета
	|ПОМЕСТИТЬ втОстаткиСчетов
	|ИЗ
	|	РегистрБухгалтерии.Международный.Остатки(
	|			&Граница,
	|			Счет В (ВЫБРАТЬ Т.Счет ИЗ втЗакрываемыеСчета КАК Т),
	|			,
	|			ПланСчетов = &ПланСчетов И Организация = &Организация) КАК МеждународныйОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Счета.СчетУчета КАК Счет,
	|	ВидыСубконто.НомерСтроки КАК НомерСтроки,
	|	ВидыСубконто.ВидСубконто КАК ВидСубконто,
	|	ВидыСубконто.ВидСубконто.Наименование КАК Наименование,
	|	ВидыСубконто.ВидСубконто.ТипЗначения КАК ТипЗначения,
	|	ВидыСубконто.ТолькоОбороты КАК ТолькоОбороты
	|ИЗ
	|	втСчетЗакрытия КАК Счета
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК ВидыСубконто
	|	ПО Счета.СчетУчета = ВидыСубконто.Ссылка
	|ГДЕ
	|	НЕ ВидыСубконто.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Счета.Счет КАК Счет,
	|	ВидыСубконто.НомерСтроки КАК НомерСтроки,
	|	ВидыСубконто.ВидСубконто КАК ВидСубконто,
	|	ВидыСубконто.ВидСубконто.Наименование КАК Наименование,
	|	ВидыСубконто.ВидСубконто.ТипЗначения КАК ТипЗначения,
	|	ВидыСубконто.ТолькоОбороты КАК ТолькоОбороты
	|ИЗ
	|	втЗакрываемыеСчета КАК Счета
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК ВидыСубконто
	|	ПО Счета.Счет = ВидыСубконто.Ссылка
	|ГДЕ
	|	НЕ ВидыСубконто.Ссылка ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	Счет,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// Закроем счета
	|ВЫБРАТЬ
	|	&Дата КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ОстаткиСчетов.ПланСчетов КАК ПланСчетов,
	|	ОстаткиСчетов.Организация КАК Организация,
	|	
	|	ВЫБОР КОГДА ОстаткиСчетов.Сумма > 0
	|		ТОГДА
	|			ВЫБОР КОГДА СчетЗакрытия.УчетПоПодразделениям
	|				ТОГДА ОстаткиСчетов.Подразделение
	|				ИНАЧЕ НЕОПРЕДЕЛЕНО
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ОстаткиСчетов.Подразделение
	|	КОНЕЦ КАК ПодразделениеДт,
	|	ВЫБОР КОГДА ОстаткиСчетов.Сумма > 0
	|		ТОГДА
	|			ВЫБОР КОГДА СчетЗакрытия.УчетПоНаправлениям
	|				ТОГДА ОстаткиСчетов.НаправлениеДеятельности
	|				ИНАЧЕ НЕОПРЕДЕЛЕНО
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ОстаткиСчетов.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельностиДт,
	|	ВЫБОР КОГДА ОстаткиСчетов.Сумма > 0
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ОстаткиСчетов.Валюта
	|	КОНЕЦ КАК ВалютаДт,
	|	
	|	ВЫБОР КОГДА ОстаткиСчетов.Сумма > 0
	|		ТОГДА СчетЗакрытия.СчетУчета
	|		ИНАЧЕ ОстаткиСчетов.Счет
	|	КОНЕЦ КАК СчетДт,
	|	
	|	ОстаткиСчетов.Субконто1 КАК СубконтоДт1,
	|	ОстаткиСчетов.Субконто2 КАК СубконтоДт2,
	|	ОстаткиСчетов.Субконто3 КАК СубконтоДт3,
	|	
	|	ВЫБОР КОГДА ОстаткиСчетов.Сумма > 0
	|		ТОГДА
	|			ОстаткиСчетов.Подразделение
	|		ИНАЧЕ
	|			ВЫБОР КОГДА СчетЗакрытия.УчетПоПодразделениям
	|				ТОГДА ОстаткиСчетов.Подразделение
	|				ИНАЧЕ НЕОПРЕДЕЛЕНО
	|			КОНЕЦ
	|	КОНЕЦ КАК ПодразделениеКт,
	|	ВЫБОР КОГДА ОстаткиСчетов.Сумма > 0
	|		ТОГДА
	|			ОстаткиСчетов.НаправлениеДеятельности
	|		ИНАЧЕ
	|			ВЫБОР КОГДА СчетЗакрытия.УчетПоНаправлениям
	|				ТОГДА ОстаткиСчетов.НаправлениеДеятельности
	|				ИНАЧЕ НЕОПРЕДЕЛЕНО
	|			КОНЕЦ
	|	КОНЕЦ КАК НаправлениеДеятельностиКт,
	|	ВЫБОР КОГДА ОстаткиСчетов.Сумма > 0
	|		ТОГДА ОстаткиСчетов.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаКт,
	|	
	|	ВЫБОР КОГДА ОстаткиСчетов.Сумма > 0
	|		ТОГДА ОстаткиСчетов.Счет
	|		ИНАЧЕ СчетЗакрытия.СчетУчета
	|	КОНЕЦ КАК СчетКт,
	|	
	|	ОстаткиСчетов.Субконто1 КАК СубконтоКт1,
	|	ОстаткиСчетов.Субконто2 КАК СубконтоКт2,
	|	ОстаткиСчетов.Субконто3 КАК СубконтоКт3,
	|	
	|	ВЫБОР КОГДА  ОстаткиСчетов.Сумма < 0
	|		ТОГДА -ОстаткиСчетов.Сумма
	|		ИНАЧЕ ОстаткиСчетов.Сумма
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР КОГДА ОстаткиСчетов.СуммаПредставления < 0
	|		ТОГДА -ОстаткиСчетов.СуммаПредставления
	|		ИНАЧЕ ОстаткиСчетов.СуммаПредставления
	|	КОНЕЦ КАК СуммаПредставления,
	|	
	|	0 КАК ВалютнаяСумма,
	|	ВЫБОР КОГДА ОстаткиСчетов.Сумма > 0
	|		ТОГДА 0
	|		ИНАЧЕ ОстаткиСчетов.ВалютнаяСумма
	|	КОНЕЦ КАК ВалютнаяСуммаДт,
	|	ВЫБОР КОГДА ОстаткиСчетов.Сумма > 0
	|		ТОГДА ОстаткиСчетов.ВалютнаяСумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВалютнаяСуммаКт,
	|	
	|	&Содержание КАК Содержание,
	|	НЕОПРЕДЕЛЕНО КАК ШаблонПроводки,
	|	НЕОПРЕДЕЛЕНО КАК ТипПроводки,
	|	ОстаткиСчетов.ВидСчета
	|ИЗ
	|	втОстаткиСчетов КАК ОстаткиСчетов
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетЗакрытия КАК СчетЗакрытия
	|	ПО ИСТИНА";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СчетУчетаДоходов" , ИмяПоляСчета);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СчетаДоходов"		, ИмяТабЧастиСчетов);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Содержание", """"+СодержаниеПроводки+"""");
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаКурсовыхРазницФункциональнаяВалюта()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НастройкаФормированияПроводокОрганизацийМУ.ПланСчетов КАК ПланСчетов,
	|	НастройкаФормированияПроводокОрганизацийМУ.Организация КАК Организация,
	|	НастройкаФормированияПроводокОрганизацийМУ.НастройкаФормированияПроводок,
	|	НастройкиФормированияПроводок.ДоходыКР КАК ДоходыКР,
	|	НастройкиФормированияПроводок.СтатьяДоходовКР КАК СтатьяДоходовКР,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.СтатьиДоходов) КАК ВидСубконтоСтатьиДоходов,
	|	НастройкиФормированияПроводок.РасходыКР КАК РасходыКР,
	|	НастройкиФормированияПроводок.СтатьяРасходовКР КАК СтатьяРасходовКР,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.СтатьиРасходов) КАК ВидСубконтоСтатьиРасходов,
	|	НастройкиФормированияПроводок.ДоходыКР.Валютный КАК ДоходыВалютный,
	|	НастройкиФормированияПроводок.РасходыКР.Валютный КАК РасходыВалютный
	|ПОМЕСТИТЬ втСчетаУчетаКР
	|ИЗ
	|	РегистрСведений.ПланыСчетовМеждународногоУчетаОрганизаций.СрезПоследних(&Дата, 
	|			ПланСчетов = &ПланСчетов И Организация = &Организация) КАК НастройкаФормированияПроводокОрганизацийМУ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НастройкиФормированияПроводокМеждународногоУчета КАК НастройкиФормированияПроводок
	|		ПО НастройкаФормированияПроводокОрганизацийМУ.НастройкаФормированияПроводок = НастройкиФормированияПроводок.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Период,
	|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
	|	КурсыВалютСрезПоследних.КурсЧислитель,
	|	КурсыВалютСрезПоследних.КурсЗнаменатель
	|ПОМЕСТИТЬ втКурсВалют
	|ИЗ
	|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&Дата, БазоваяВалюта = &ВалютаРегламентированногоУчета) КАК КурсыВалютСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МеждународныйОстатки.Счет,
	|	МеждународныйОстатки.ПланСчетов,
	|	МеждународныйОстатки.Организация,
	|	МеждународныйОстатки.Подразделение,
	|	МеждународныйОстатки.НаправлениеДеятельности,
	|	МеждународныйОстатки.Валюта,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто1,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто2,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто3,
	|	СУММА(МеждународныйОстатки.СуммаОстаток) КАК СуммаОстаток,
	|	СУММА(МеждународныйОстатки.ВалютнаяСуммаОстаток) КАК ВалютнаяСуммаОстаток
	|ПОМЕСТИТЬ втОстаткиСчетов
	|ИЗ
	|	РегистрБухгалтерии.Международный.Остатки(
	|			&Граница,
	|			Счет.Валютный
	|				И НЕ Счет.ИсключитьИзРасчетаКурсовыхРазниц,
	|			,
	|			ПланСчетов = &ПланСчетов И Организация = &Организация) КАК МеждународныйОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто1
	|		ПО МеждународныйОстатки.Счет = МеждународныйВидыСубконто1.Ссылка
	|			И (МеждународныйВидыСубконто1.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто2
	|		ПО МеждународныйОстатки.Счет = МеждународныйВидыСубконто2.Ссылка
	|			И (МеждународныйВидыСубконто2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто3
	|		ПО МеждународныйОстатки.Счет = МеждународныйВидыСубконто3.Ссылка
	|			И (МеждународныйВидыСубконто3.НомерСтроки = 3)
	|
	|СГРУППИРОВАТЬ ПО
	|	МеждународныйОстатки.Счет,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	МеждународныйОстатки.ПланСчетов,
	|	МеждународныйОстатки.Организация,
	|	МеждународныйОстатки.Подразделение,
	|	МеждународныйОстатки.НаправлениеДеятельности,
	|	МеждународныйОстатки.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1000
	|	&Дата КАК Период,
	|	ОстаткиСчетов.Счет,
	|	ОстаткиСчетов.Счет.Вид КАК ВидСчета,
	|	ОстаткиСчетов.ПланСчетов,
	|	ОстаткиСчетов.Организация,
	|	ВЫБОР КОГДА ОстаткиСчетов.Счет.УчетПоПодразделениям
	|		ТОГДА ОстаткиСчетов.Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Подразделение,
	|	ВЫБОР КОГДА ОстаткиСчетов.Счет.УчетПоНаправлениямДеятельности
	|		ТОГДА ОстаткиСчетов.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ОстаткиСчетов.Счет.ВидыСубконто.(
	|		НомерСтроки КАК НомерСтроки,
	|		ВидСубконто КАК ВидСубконто
	|	) КАК ВидыСубконтоСчета,
	|	ОстаткиСчетов.Субконто1,
	|	ОстаткиСчетов.Субконто2,
	|	ОстаткиСчетов.Субконто3,
	|	втКурсВалют.КурсЧислитель,
	|	втКурсВалют.КурсЗнаменатель,
	|	ОстаткиСчетов.Валюта КАК Валюта,
	|	ОстаткиСчетов.ВалютнаяСуммаОстаток,
	|	ВЫРАЗИТЬ(ОстаткиСчетов.ВалютнаяСуммаОстаток * втКурсВалют.КурсЧислитель / втКурсВалют.КурсЗнаменатель / &КурсЧислительФВ * &КурсЗнаменательФВ КАК ЧИСЛО(31,2)) КАК ВалСуммаФВ,
	|	ОстаткиСчетов.СуммаОстаток,
	|	(ВЫРАЗИТЬ(ОстаткиСчетов.ВалютнаяСуммаОстаток * втКурсВалют.КурсЧислитель / втКурсВалют.КурсЗнаменатель / &КурсЧислительФВ * &КурсЗнаменательФВ КАК ЧИСЛО(31,2))) - ОстаткиСчетов.СуммаОстаток КАК КурсоваяРазница,
	|	ОстаткиСчетов.Счет.Порядок КАК СчетПорядок,
	|	втСчетаУчетаКР.ДоходыКР КАК Доходы,
	|	втСчетаУчетаКР.СтатьяДоходовКР КАК СтатьяДоходов,
	|	втСчетаУчетаКР.ВидСубконтоСтатьиДоходов,
	|	ЕСТЬNULL(втСчетаУчетаКР.ДоходыВалютный, ЛОЖЬ) КАК ДоходыВалютный,
	|	ЕСТЬNULL(втСчетаУчетаКР.ДоходыКР.УчетПоПодразделениям, ЛОЖЬ) КАК ДоходыПоПодразделениям,
	|	ЕСТЬNULL(втСчетаУчетаКР.ДоходыКР.УчетПоНаправлениямДеятельности, ЛОЖЬ) КАК ДоходыПоНаправлениям,
	|	втСчетаУчетаКР.РасходыКР КАК Расходы,
	|	втСчетаУчетаКР.СтатьяРасходовКР КАК СтатьяРасходов,
	|	втСчетаУчетаКР.ВидСубконтоСтатьиРасходов,
	|	ЕСТЬNULL(втСчетаУчетаКР.РасходыВалютный, ЛОЖЬ) КАК РасходыВалютный,
	|	ЕСТЬNULL(втСчетаУчетаКР.РасходыКР.УчетПоПодразделениям, ЛОЖЬ) КАК РасходыПоПодразделениям,
	|	ЕСТЬNULL(втСчетаУчетаКР.РасходыКР.УчетПоНаправлениямДеятельности, ЛОЖЬ) КАК РасходыПоНаправлениям
	|ИЗ
	|	втОстаткиСчетов КАК ОстаткиСчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсВалют КАК втКурсВалют
	|		ПО ОстаткиСчетов.Валюта = втКурсВалют.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСчетаУчетаКР КАК втСчетаУчетаКР
	|		ПО ОстаткиСчетов.ПланСчетов = втСчетаУчетаКР.ПланСчетов
	|			И ОстаткиСчетов.Организация = втСчетаУчетаКР.Организация
	|ГДЕ
	|	(ВЫРАЗИТЬ(ОстаткиСчетов.ВалютнаяСуммаОстаток * втКурсВалют.КурсЧислитель / втКурсВалют.КурсЗнаменатель / &КурсЧислительФВ * &КурсЗнаменательФВ КАК ЧИСЛО(31,2))) - ОстаткиСчетов.СуммаОстаток <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетПорядок";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаКурсовыхРазницВалютаПредставления()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НастройкаФормированияПроводокОрганизацийМУ.ПланСчетов,
	|	НастройкаФормированияПроводокОрганизацийМУ.Организация,
	|	НастройкаФормированияПроводокОрганизацийМУ.НастройкаФормированияПроводок,
	|	НастройкиФормированияПроводок.ДоходыОтРазницПриПересчетеВП КАК Доходы,
	|	НастройкиФормированияПроводок.СтатьяДоходовОтРазницПересчета КАК СтатьяДоходов,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.СтатьиДоходов) КАК ВидСубконтоСтатьиДоходов,
	|	НастройкиФормированияПроводок.РасходыОтРазницПриПересчетеВП КАК Расходы,
	|	НастройкиФормированияПроводок.СтатьяРасходовОтРазницПересчета КАК СтатьяРасходов,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.СтатьиРасходов) КАК ВидСубконтоСтатьиРасходов,
	|	НастройкиФормированияПроводок.ДоходыОтРазницПриПересчетеВП.Валютный КАК ДоходыВалютный,
	|	НастройкиФормированияПроводок.РасходыОтРазницПриПересчетеВП.Валютный КАК РасходыВалютный
	|ПОМЕСТИТЬ втСчетаУчета
	|ИЗ
	|	РегистрСведений.ПланыСчетовМеждународногоУчетаОрганизаций.СрезПоследних(&Дата, ПланСчетов = &ПланСчетов И Организация = &Организация) КАК НастройкаФормированияПроводокОрганизацийМУ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НастройкиФормированияПроводокМеждународногоУчета КАК НастройкиФормированияПроводок
	|		ПО НастройкаФормированияПроводокОрганизацийМУ.НастройкаФормированияПроводок = НастройкиФормированияПроводок.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкаФормированияПроводокОрганизацийМУ.ПланСчетов,
	|	НастройкаФормированияПроводокОрганизацийМУ.Организация,
	|	НастройкаФормированияПроводокОрганизацийМУ.НастройкаФормированияПроводок,
	|	НастройкиФормированияПроводокИсключитьИзПересчетаВП.Счет
	|ПОМЕСТИТЬ втСчетаИсключения
	|ИЗ
	|	РегистрСведений.ПланыСчетовМеждународногоУчетаОрганизаций.СрезПоследних(&Дата, ПланСчетов = &ПланСчетов И Организация = &Организация) КАК НастройкаФормированияПроводокОрганизацийМУ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НастройкиФормированияПроводокМеждународногоУчета.ИсключитьИзПересчетаВП КАК НастройкиФормированияПроводокИсключитьИзПересчетаВП
	|		ПО НастройкаФормированияПроводокОрганизацийМУ.НастройкаФормированияПроводок = НастройкиФормированияПроводокИсключитьИзПересчетаВП.Ссылка
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	втСчетаУчета.ПланСчетов,
	|	втСчетаУчета.Организация,
	|	втСчетаУчета.НастройкаФормированияПроводок,
	|	втСчетаУчета.Доходы
	|ИЗ
	|	втСчетаУчета КАК втСчетаУчета
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	втСчетаУчета.ПланСчетов,
	|	втСчетаУчета.Организация,
	|	втСчетаУчета.НастройкаФормированияПроводок,
	|	втСчетаУчета.Расходы
	|ИЗ
	|	втСчетаУчета КАК втСчетаУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Период,
	|	КурсыВалютСрезПоследних.Валюта,
	|	КурсыВалютСрезПоследних.КурсЧислитель,
	|	КурсыВалютСрезПоследних.КурсЗнаменатель
	|ПОМЕСТИТЬ втКурсВалют
	|ИЗ
	|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&Дата, БазоваяВалюта = &ВалютаРегламентированногоУчета) КАК КурсыВалютСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МеждународныйОстатки.Счет,
	|	МеждународныйОстатки.ПланСчетов,
	|	МеждународныйОстатки.Организация,
	|	МеждународныйОстатки.Подразделение,
	|	МеждународныйОстатки.НаправлениеДеятельности,
	|	МеждународныйОстатки.Валюта,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто1,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто2,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто3,
	|	СУММА(МеждународныйОстатки.СуммаОстаток) КАК СуммаОстаток,
	|	СУММА(МеждународныйОстатки.ВалютнаяСуммаОстаток) КАК ВалютнаяСуммаОстаток,
	|	СУММА(МеждународныйОстатки.СуммаПредставленияОстаток) КАК СуммаПредставленияОстаток
	|ПОМЕСТИТЬ втОстаткиСчетов
	|ИЗ
	|	РегистрБухгалтерии.Международный.Остатки(
	|			&Граница,
	|			НЕ Счет В (ВЫБРАТЬ втСчетаИсключения.Счет ИЗ втСчетаИсключения),
	|			,
	|			ПланСчетов = &ПланСчетов И Организация = &Организация) КАК МеждународныйОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто1
	|		ПО МеждународныйОстатки.Счет = МеждународныйВидыСубконто1.Ссылка
	|			И (МеждународныйВидыСубконто1.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто2
	|		ПО МеждународныйОстатки.Счет = МеждународныйВидыСубконто2.Ссылка
	|			И (МеждународныйВидыСубконто2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто3
	|		ПО МеждународныйОстатки.Счет = МеждународныйВидыСубконто3.Ссылка
	|			И (МеждународныйВидыСубконто3.НомерСтроки = 3)
	|
	|СГРУППИРОВАТЬ ПО
	|	МеждународныйОстатки.Счет,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	МеждународныйОстатки.ПланСчетов,
	|	МеждународныйОстатки.Организация,
	|	МеждународныйОстатки.Подразделение,
	|	МеждународныйОстатки.НаправлениеДеятельности,
	|	МеждународныйОстатки.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1000
	|	&Дата КАК Период,
	|	ОстаткиСчетов.Счет,
	|	ОстаткиСчетов.Счет.Вид КАК ВидСчета,
	|	ОстаткиСчетов.Счет.Валютный КАК Валютный,
	|	ОстаткиСчетов.ПланСчетов,
	|	ОстаткиСчетов.Организация,
	|	ВЫБОР КОГДА ОстаткиСчетов.Счет.УчетПоПодразделениям
	|		ТОГДА ОстаткиСчетов.Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Подразделение,
	|	ВЫБОР КОГДА ОстаткиСчетов.Счет.УчетПоНаправлениямДеятельности
	|		ТОГДА ОстаткиСчетов.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ОстаткиСчетов.Счет.ВидыСубконто.(
	|		НомерСтроки КАК НомерСтроки,
	|		ВидСубконто КАК ВидСубконто
	|	) КАК ВидыСубконтоСчета,
	|	ОстаткиСчетов.Субконто1,
	|	ОстаткиСчетов.Субконто2,
	|	ОстаткиСчетов.Субконто3,
	|	ОстаткиСчетов.Валюта,
	|	ОстаткиСчетов.СуммаОстаток,
	|	ВЫРАЗИТЬ(ОстаткиСчетов.СуммаОстаток * &КурсЧислительФВ / &КурсЗнаменательФВ / &КурсЧислительВП * &КурсЗнаменательВП КАК ЧИСЛО(31,2)) КАК СуммаВП,
	|	ОстаткиСчетов.СуммаПредставленияОстаток КАК СуммаВПОстаток,
	|	(ВЫРАЗИТЬ(ОстаткиСчетов.СуммаОстаток * &КурсЧислительФВ / &КурсЗнаменательФВ / &КурсЧислительВП * &КурсЗнаменательВП КАК ЧИСЛО(31,2))) - ОстаткиСчетов.СуммаПредставленияОстаток КАК КурсоваяРазница,
	|	ОстаткиСчетов.Счет.Порядок КАК СчетПорядок,
	|	втСчетаУчета.Доходы,
	|	ЕСТЬNULL(втСчетаУчета.ДоходыВалютный,ЛОЖЬ) КАК ДоходыВалютный,
	|	ЕСТЬNULL(втСчетаУчета.Доходы.УчетПоПодразделениям, ЛОЖЬ) КАК ДоходыПоПодразделениям,
	|	ЕСТЬNULL(втСчетаУчета.Доходы.УчетПоНаправлениямДеятельности, ЛОЖЬ) КАК ДоходыПоНаправлениям,
	|	втСчетаУчета.СтатьяДоходов,
	|	втСчетаУчета.ВидСубконтоСтатьиДоходов,
	|	втСчетаУчета.Расходы,
	|	ЕСТЬNULL(втСчетаУчета.РасходыВалютный,ЛОЖЬ) КАК РасходыВалютный,
	|	ЕСТЬNULL(втСчетаУчета.Расходы.УчетПоПодразделениям, ЛОЖЬ) КАК РасходыПоПодразделениям,
	|	ЕСТЬNULL(втСчетаУчета.Расходы.УчетПоНаправлениямДеятельности, ЛОЖЬ) КАК РасходыПоНаправлениям,
	|	втСчетаУчета.СтатьяРасходов,
	|	втСчетаУчета.ВидСубконтоСтатьиРасходов
	|ИЗ
	|	втОстаткиСчетов КАК ОстаткиСчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсВалют КАК втКурсВалют
	|		ПО ОстаткиСчетов.Валюта = втКурсВалют.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСчетаУчета КАК втСчетаУчета
	|		ПО ОстаткиСчетов.ПланСчетов = втСчетаУчета.ПланСчетов
	|			И ОстаткиСчетов.Организация = втСчетаУчета.Организация
	|ГДЕ
	|	(ВЫРАЗИТЬ(ВЫБОР
	|				КОГДА ОстаткиСчетов.СуммаОстаток < 0
	|					ТОГДА -ОстаткиСчетов.СуммаОстаток
	|				ИНАЧЕ ОстаткиСчетов.СуммаОстаток
	|			КОНЕЦ * &КурсЧислительФВ / &КурсЗнаменательФВ / &КурсЧислительВП * &КурсЗнаменательВП КАК ЧИСЛО(31,2))) - ВЫБОР
	|			КОГДА ОстаткиСчетов.СуммаПредставленияОстаток < 0
	|				ТОГДА -ОстаткиСчетов.СуммаПредставленияОстаток
	|			ИНАЧЕ ОстаткиСчетов.СуммаПредставленияОстаток
	|		КОНЕЦ <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетПорядок";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецЕсли
