
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

Функция ПодготовитьПараметрыПроведения(ДополнительныеСвойства, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДополнительныеСвойства.ДляПроведения.Ссылка);
	
	Реквизиты = МСФОУХ.РеквизитыДокумента(Запрос, "ПериодОтчета, ДатаНачала, ДатаОкончания, ВидОтчета", "ПериодОтчета", Отказ);
	ДополнительныеСвойства.ДляПроведения.Вставить("Реквизиты", Реквизиты);
	Если Отказ Тогда
		Возврат ДополнительныеСвойства;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Период",						Реквизиты.Период);
	Запрос.УстановитьПараметр("НачалоПериода",				Реквизиты.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода",				Новый Граница(КонецДня(Реквизиты.ДатаОкончания), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация",				Реквизиты.Организация);
	Запрос.УстановитьПараметр("Сценарий",					Реквизиты.Сценарий);
	Запрос.УстановитьПараметр("ПериодОтчета", 				Реквизиты.ПериодОтчета);
	Запрос.УстановитьПараметр("ФормироватьПроводкиМСФО", 	Реквизиты.ФормироватьПроводкиМСФО);
	Запрос.УстановитьПараметр("УчетнаяПолитика", 			Реквизиты.УчетнаяПолитика);
	
	Запрос.УстановитьПараметр("Содержание1", 		НСтр("ru = 'Перевод на счет отложенного налога НСБУ'"));
	Запрос.УстановитьПараметр("Содержание2", 		НСтр("ru = 'Перевод на счет условного налога'"));
	
	НомераТаблиц = Новый Структура;
	ТекстЗапроса = Новый Массив;
	ТекстЗапроса.Добавить(ТекстЗапроса_СчетаУчета(НомераТаблиц));
	ТекстЗапроса.Добавить(ТекстЗапроса_ВидыСубконтоСчетов(НомераТаблиц));
	ТекстЗапроса.Добавить(ТекстЗапроса_ОборотыТУ(НомераТаблиц));
	ТекстЗапроса.Добавить(ТекстЗапроса_ТаблицаПроводок(НомераТаблиц, Реквизиты.ФормироватьПроводкиМСФО));
	Запрос.Текст = СтрСоединить(ТекстЗапроса, ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета());
	
	ПроведениеСерверУХ.ДополнитьТаблицамиИзПакетаЗапросов(ДополнительныеСвойства.ТаблицыДляДвижений, Запрос, НомераТаблиц);
	
КонецФункции

Функция ТекстЗапроса_СчетаУчета(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаДляПроводок", НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	т.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА т.Ссылка = ЗНАЧЕНИЕ(Справочник.ФиксированныеСчетаУчетаБД.НалогНаПрибыль)
	|			ТОГДА ""НалогНаПрибыль""
	|		КОГДА т.Ссылка = ЗНАЧЕНИЕ(Справочник.ФиксированныеСчетаУчетаБД.ОтложенныйНалогПоНСБУ)
	|			ТОГДА ""ОтложенныйНалогПоНСБУ""
	|		КОГДА т.Ссылка = ЗНАЧЕНИЕ(Справочник.ФиксированныеСчетаУчетаБД.ОтложенноеНалоговоеОбязательство)
	|			ТОГДА ""ОтложенноеНалоговоеОбязательство""
	|		КОГДА т.Ссылка = ЗНАЧЕНИЕ(Справочник.ФиксированныеСчетаУчетаБД.ОтложенныйНалоговыйАктив)
	|			ТОГДА ""ОтложенныйНалоговыйАктив""
	|		КОГДА т.Ссылка = ЗНАЧЕНИЕ(Справочник.ФиксированныеСчетаУчетаБД.УсловныйНалогНаПрибыль)
	|			ТОГДА ""УсловныйНалогНаПрибыль""
	|	КОНЕЦ КАК ТипСчета,
	|	т.Счет КАК СчетИБ,
	|	т.Счет.СчетСсылка КАК СчетМСФО,
	|	ВЫБОР
	|		КОГДА &ФормироватьПроводкиМСФО
	|			ТОГДА т.Счет.СчетСсылка
	|		ИНАЧЕ т.Счет
	|	КОНЕЦ КАК Счет,
	|	ЕСТЬNULL(ВидыСубконто1.ВидСубконтоСсылка, ЗНАЧЕНИЕ(ПланВИдовХарактеристик.ВидыСубконтоКорпоративные.ПустаяСсылка)) КАК ВидСубконто1,
	|	ЕСТЬNULL(ВидыСубконто2.ВидСубконтоСсылка, ЗНАЧЕНИЕ(ПланВИдовХарактеристик.ВидыСубконтоКорпоративные.ПустаяСсылка)) КАК ВидСубконто2,
	|	ЕСТЬNULL(ВидыСубконто3.ВидСубконтоСсылка, ЗНАЧЕНИЕ(ПланВИдовХарактеристик.ВидыСубконтоКорпоративные.ПустаяСсылка)) КАК ВидСубконто3,
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	т.Субконто1 КАК Субконто1,
	|	т.Субконто2 КАК Субконто2,
	|	т.Субконто3 КАК Субконто3,
	|	т.Счет.Валютный КАК Валютный
	|ПОМЕСТИТЬ втСчетаУчета
	|ИЗ
	|	Справочник.ФиксированныеСчетаУчетаБД КАК т
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СчетаБД.ВидыСубконто КАК ВидыСубконто1
	|		ПО т.Счет = ВидыСубконто1.Ссылка
	|			И (ВидыСубконто1.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СчетаБД.ВидыСубконто КАК ВидыСубконто2
	|		ПО т.Счет = ВидыСубконто2.Ссылка
	|			И (ВидыСубконто2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СчетаБД.ВидыСубконто КАК ВидыСубконто3
	|		ПО т.Счет = ВидыСубконто3.Ссылка
	|			И (ВидыСубконто3.НомерСтроки = 3)
	|ГДЕ
	|	т.Ссылка В (ЗНАЧЕНИЕ(Справочник.ФиксированныеСчетаУчетаБД.НалогНаПрибыль), ЗНАЧЕНИЕ(Справочник.ФиксированныеСчетаУчетаБД.ОтложенныйНалогПоНСБУ), ЗНАЧЕНИЕ(Справочник.ФиксированныеСчетаУчетаБД.ОтложенноеНалоговоеОбязательство), ЗНАЧЕНИЕ(Справочник.ФиксированныеСчетаУчетаБД.ОтложенныйНалоговыйАктив), ЗНАЧЕНИЕ(Справочник.ФиксированныеСчетаУчетаБД.УсловныйНалогНаПрибыль))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТипСчета";
	
	Возврат Документы.УчетнаяПолитикаМСФО.ЗаменитьФиксированныеСчетаУчетаБД(ТекстЗапроса);

КонецФункции

Функция ТекстЗапроса_ВидыСубконтоСчетов(НомераТаблиц)

	НомераТаблиц.Вставить("втВидыСубконтоСчетов", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	т.Счет КАК Счет,
	|	МАКСИМУМ(т.ВидСубконто1) КАК ВидСубконто1,
	|	МАКСИМУМ(т.ВидСубконто2) КАК ВидСубконто2,
	|	МАКСИМУМ(т.ВидСубконто3) КАК ВидСубконто3
	|ПОМЕСТИТЬ втВидыСубконтоСчетов
	|ИЗ
	|	втСчетаУчета КАК т
	|
	|СГРУППИРОВАТЬ ПО
	|	т.Счет
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет";
	
КонецФункции

Функция ТекстЗапроса_ОборотыТУ(НомераТаблиц)

	НомераТаблиц.Вставить("втОбороты", НомераТаблиц.Количество());
	
	Возврат 
	"ВЫБРАТЬ
	|	т.Счет КАК Счет,
	|	т.СуммаВВалютеУчетаОборот > 0 КАК СуммаБольшеНуля,
	|	т.Субконто1 КАК Субконто1,
	|	т.Субконто2 КАК Субконто2,
	|	т.Субконто3 КАК Субконто3,
	|	втВидыСубконтоСчетов.ВидСубконто1 КАК ВидСубконто1,
	|	втВидыСубконтоСчетов.ВидСубконто2 КАК ВидСубконто2,
	|	втВидыСубконтоСчетов.ВидСубконто3 КАК ВидСубконто3,
	|	т.Подразделение КАК Подразделение,
	|	т.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	т.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА т.СуммаВВалютеУчетаОборот > 0
	|			ТОГДА т.СуммаВВалютеУчетаОборот
	|		ИНАЧЕ -т.СуммаВВалютеУчетаОборот
	|	КОНЕЦ КАК СуммаВВалютеУчета,
	|	ВЫБОР
	|		КОГДА т.СуммаВВалютеУчетаОборот > 0
	|			ТОГДА т.СуммаВВалютеОборот
	|		ИНАЧЕ -т.СуммаВВалютеОборот
	|	КОНЕЦ КАК СуммаВВалюте,
	|	ВЫБОР
	|		КОГДА т.СуммаВВалютеУчетаОборот > 0
	|			ТОГДА т.СуммаВВалютеОтчетностиОборот
	|		ИНАЧЕ -т.СуммаВВалютеОтчетностиОборот
	|	КОНЕЦ КАК СуммаВВалютеОтчетности
	|ПОМЕСТИТЬ втОбороты
	|ИЗ
	|	РегистрБухгалтерии.МСФО.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Период,
	|			Счет В ИЕРАРХИИ
	|				(ВЫБРАТЬ
	|					т.СчетМСФО
	|				ИЗ
	|					втСчетаУчета КАК т
	|				ГДЕ
	|					т.ТипСчета В (""ОтложенныйНалоговыйАктив"", ""ОтложенноеНалоговоеОбязательство"")),
	|			,
	|			Сценарий = &Сценарий
	|				И Организация = &Организация
	|				И НЕ ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.РеклассОтложенныхНалогов))
	|				И &ФормироватьПроводкиМСФО,
	|			КорСчет В ИЕРАРХИИ
	|				(ВЫБРАТЬ
	|					т.СчетМСФО
	|				ИЗ
	|					втСчетаУчета КАК т
	|				ГДЕ
	|					т.ТипСчета В (""НалогНаПрибыль"")),
	|			) КАК т
	|		ЛЕВОЕ СОЕДИНЕНИЕ втВидыСубконтоСчетов КАК втВидыСубконтоСчетов
	|		ПО т.Счет = втВидыСубконтоСчетов.Счет";

КонецФункции

Функция ТекстЗапроса_ТаблицаПроводок(НомераТаблиц, ФормироватьПроводкиМСФО = Истина)

	Если ФормироватьПроводкиМСФО Тогда
		НомераТаблиц.Вставить("МСФО", НомераТаблиц.Количество());
	Иначе 
		НомераТаблиц.Вставить("ПроводкиТК", НомераТаблиц.Количество());		
	КонецЕсли;
		
	Возврат
	"ВЫБРАТЬ
	|	КорСчет.СчетМСФО КАК СчетДт,
	|	КорСчет.Подразделение КАК ПодразделениеДт,
	|	КорСчет.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	ВЫБОР
	|		КОГДА КорСчет.Валютный
	|			ТОГДА т.Валюта
	|		ИНАЧЕ т.Валюта
	|	КОНЕЦ КАК ВалютаДт,
	|	ВЫБОР
	|		КОГДА КорСчет.Валютный
	|			ТОГДА т.СуммаВВалюте
	|	КОНЕЦ КАК СуммаВВалютеДт,
	|	КорСчет.Субконто1 КАК СубконтоДт1,
	|	КорСчет.Субконто2 КАК СубконтоДт2,
	|	КорСчет.Субконто3 КАК СубконтоДт3,
	|	КорСчет.ВидСубконто1 КАК ВидСубконтоДт1,
	|	КорСчет.ВидСубконто2 КАК ВидСубконтоДт2,
	|	КорСчет.ВидСубконто3 КАК ВидСубконтоДт3,
	|	т.Счет КАК СчетКт,
	|	т.Подразделение КАК ПодразделениеКт,
	|	т.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	т.Валюта КАК ВалютаКт,
	|	т.СуммаВВалюте КАК СуммаВВалютеКт,
	|	т.Субконто1 КАК СубконтоКт1,
	|	т.Субконто2 КАК СубконтоКт2,
	|	т.Субконто3 КАК СубконтоКт3,
	|	т.ВидСубконто1 КАК ВидСубконтоКт1,
	|	т.ВидСубконто2 КАК ВидСубконтоКт2,
	|	т.ВидСубконто3 КАК ВидСубконтоКт3,
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	&Сценарий КАК Сценарий,
	|	&Содержание1 КАК Содержание,
	|	т.СуммаВВалютеУчета КАК СуммаВВалютеУчета,
	|	т.СуммаВВалютеОтчетности КАК СуммаВВалютеОтчетности
	|ИЗ
	|	втОбороты КАК т
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСчетаУчета КАК КорСчет
	|		ПО (КорСчет.ТипСчета = ""ОтложенныйНалогПоНСБУ"")
	|ГДЕ
	|	НЕ т.СуммаБольшеНуля
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	т.Счет,
	|	т.Подразделение,
	|	т.НаправлениеДеятельности,
	|	т.Валюта,
	|	т.СуммаВВалюте,
	|	т.Субконто1,
	|	т.Субконто2,
	|	т.Субконто3,
	|	т.ВидСубконто1,
	|	т.ВидСубконто2,
	|	т.ВидСубконто3,
	|	КорСчет.СчетМСФО,
	|	КорСчет.Подразделение,
	|	КорСчет.НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА КорСчет.Валютный
	|			ТОГДА т.Валюта
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА КорСчет.Валютный
	|			ТОГДА т.СуммаВВалюте
	|	КОНЕЦ,
	|	КорСчет.Субконто1,
	|	КорСчет.Субконто2,
	|	КорСчет.Субконто3,
	|	КорСчет.ВидСубконто1,
	|	КорСчет.ВидСубконто2,
	|	КорСчет.ВидСубконто3,
	|	&Период,
	|	&Организация,
	|	&Сценарий,
	|	&Содержание1,
	|	т.СуммаВВалютеУчета,
	|	т.СуммаВВалютеОтчетности
	|ИЗ
	|	втОбороты КАК т
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСчетаУчета КАК КорСчет
	|		ПО (КорСчет.ТипСчета = ""ОтложенныйНалогПоНСБУ"")
	|ГДЕ
	|	т.СуммаБольшеНуля
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	т.Счет,
	|	т.Подразделение,
	|	т.НаправлениеДеятельности,
	|	т.Валюта,
	|	т.СуммаВВалюте,
	|	т.Субконто1,
	|	т.Субконто2,
	|	т.Субконто3,
	|	т.ВидСубконто1,
	|	т.ВидСубконто2,
	|	т.ВидСубконто3,
	|	КорСчет.СчетМСФО,
	|	КорСчет.Подразделение,
	|	т.НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА КорСчет.Валютный
	|			ТОГДА т.Валюта
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА КорСчет.Валютный
	|			ТОГДА т.СуммаВВалюте
	|	КОНЕЦ,
	|	КорСчет.Субконто1,
	|	КорСчет.Субконто2,
	|	КорСчет.Субконто3,
	|	КорСчет.ВидСубконто1,
	|	КорСчет.ВидСубконто2,
	|	КорСчет.ВидСубконто3,
	|	&Период,
	|	&Организация,
	|	&Сценарий,
	|	&Содержание2,
	|	т.СуммаВВалютеУчета,
	|	т.СуммаВВалютеОтчетности
	|ИЗ
	|	втОбороты КАК т
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСчетаУчета КАК КорСчет
	|		ПО (КорСчет.ТипСчета = ""УсловныйНалогНаПрибыль"")
	|ГДЕ
	|	НЕ т.СуммаБольшеНуля
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорСчет.СчетМСФО,
	|	КорСчет.Подразделение,
	|	КорСчет.НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА КорСчет.Валютный
	|			ТОГДА т.Валюта
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА КорСчет.Валютный
	|			ТОГДА т.СуммаВВалюте
	|	КОНЕЦ,
	|	КорСчет.Субконто1,
	|	КорСчет.Субконто2,
	|	КорСчет.Субконто3,
	|	КорСчет.ВидСубконто1,
	|	КорСчет.ВидСубконто2,
	|	КорСчет.ВидСубконто3,
	|	т.Счет,
	|	т.Подразделение,
	|	т.НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА НЕ т.СуммаБольшеНуля
	|			ТОГДА т.Валюта
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ т.СуммаБольшеНуля
	|			ТОГДА т.СуммаВВалюте
	|	КОНЕЦ,
	|	т.Субконто1,
	|	т.Субконто2,
	|	т.Субконто3,
	|	т.ВидСубконто1,
	|	т.ВидСубконто2,
	|	т.ВидСубконто3,
	|	&Период,
	|	&Организация,
	|	&Сценарий,
	|	&Содержание2,
	|	т.СуммаВВалютеУчета,
	|	т.СуммаВВалютеОтчетности
	|ИЗ
	|	втОбороты КАК т
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСчетаУчета КАК КорСчет
	|		ПО (КорСчет.ТипСчета = ""УсловныйНалогНаПрибыль"")
	|ГДЕ
	|	т.СуммаБольшеНуля";

КонецФункции

#КонецЕсли