#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("НематериальныеАктивы");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("СебестоимостьИПартионныйУчет");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("ОборотныеРегистрыУправленческогоУчета");
	МеханизмыДокумента.Добавить("ОбесценениеВНА");
	
	АмортизацияНМАЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура:
//     * Ключ - Строка - Имя таблицы.
//     * Значение - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		ЗаполнитьПараметрыИнициализации(Запрос, Документ, ДопПараметры);
		
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);

		ТекстЗапросаТаблицаАмортизацияНМА(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаСтоимостьНМА(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаОбесценениеВНА(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
		
		АмортизацияНМАЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

#Область Команды

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	Отчеты.СправкаРасчетАмортизацииНМА_МУ.ДобавитьКомандуОтчетаПоДокументуАмортизацииНМА(КомандыОтчетов);
	
	АмортизацияНМАЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	АмортизацияНМАЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	АмортизацияНМАЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область Прочее

// Формирует тексты запросов, которые создают таблицы объектов учета, по которым требуется расчет амортизации.
//
// Параметры:
//  СписокЗапросов	 - Массив	 - Список запросов, в который нужно добавить текст запроса.
//  ПараметрыРасчета - Структура - Содержит параметры расчета амортизации.
//  ПрефиксТаблицы	 - Строка	 - Используется для добавления префикса к созданным таблицам.
//
Процедура ТекстЗапросаСписокВсехНМА(СписокЗапросов, ПараметрыРасчета, ПрефиксТаблицы = "") Экспорт
		
	АмортизацияНМАЛокализация.ДополнитьТекстЗапросаСписокВсехНМА(СписокЗапросов, ПараметрыРасчета, ПрефиксТаблицы);

	#Область СписокОбъектовУчета_Доначисление_УУ
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СтоимостьНМА.Организация         КАК Организация,
	|	СтоимостьНМА.НематериальныйАктив КАК НематериальныйАктив
	|ПОМЕСТИТЬ СписокОбъектовУчета_Доначисление_УУ
	|ИЗ
	|	РегистрНакопления.СтоимостьНМА КАК СтоимостьНМА
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&КонецПредыдущегоМесяца) КАК УчетнаяПолитика
	|		ПО УчетнаяПолитика.Организация = СтоимостьНМА.Организация.ГоловнаяОрганизация
	|ГДЕ
	|	СтоимостьНМА.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
	|	И СтоимостьНМА.Активность
	|	И СтоимостьНМА.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|	И (ЕСТЬNULL(УчетнаяПолитика.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) 
	|		= ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ))
	|
	|	И СтоимостьНМА.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуНМА))
	|
	|	И (СтоимостьНМА.Организация, СтоимостьНМА.НематериальныйАктив) В
	|				(ВЫБРАТЬ
	|					ПакетыАмортизации.Организация,
	|					ПакетыАмортизации.ОбъектУчета
	|				ИЗ
	|					ПакетыАмортизации КАК ПакетыАмортизации)
	|
	|СГРУППИРОВАТЬ ПО
	|	СтоимостьНМА.Организация,
	|	СтоимостьНМА.НематериальныйАктив
	|
	|ИМЕЮЩИЕ
	|	СУММА(СтоимостьНМА.Стоимость - СтоимостьНМА.ПредварительнаяСтоимость) > 0
	|		ИЛИ НЕ &ВедетсяРегламентированныйУчетВНА 
	|			И СУММА(СтоимостьНМА.СтоимостьРегл - СтоимостьНМА.ПредварительнаяСтоимостьРегл) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив,
	|	Организация";
	
	Если ЗначениеЗаполнено(ПрефиксТаблицы) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СписокОбъектовУчета_Доначисление_УУ", "СписокОбъектовУчета_Доначисление_УУ" + ПрефиксТаблицы);
	КонецЕсли; 
	
	СписокЗапросов.Добавить(ТекстЗапроса);
		
	#КонецОбласти
	
	#Область СписокОбъектовУчета_УУ
		
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&КонецМесяца                        КАК ПериодНачисления,
	|	&НачалоМесяца                       КАК НачалоМесяца,
	|	&КонецМесяца                        КАК КонецМесяца,
	|	&КонецПредыдущегоМесяца             КАК Период,
	|	ЛОЖЬ                                КАК ЭтоДоначисление,
	|	ПорядокУчетаНМА.Организация         КАК Организация,
	|	ПорядокУчетаНМА.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ПорядокУчетаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|
	|	ПорядокУчетаНМА.НачислятьАмортизациюУУ КАК НачислятьАмортизациюУУ,
	|
	|	ПорядокУчетаНМА.СпособОтраженияРасходов КАК СпособОтраженияРасходовУУ,
	|
	|	ПорядокУчетаНМА.СтатьяРасходов    КАК СтатьяРасходовУУ,
	|	ПорядокУчетаНМА.АналитикаРасходов КАК АналитикаРасходовУУ,
	|
	|	ПорядокУчетаНМА.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюУУ,
	|	ПорядокУчетаНМА.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходовУУ,
	|
	|	ЕСТЬNULL(УчетнаяПолитика.ВариантПроводокПоОбесценениюВНА, ЗНАЧЕНИЕ(Перечисление.ВариантыПроводокПоОбесценениюВНА.ДоначислятьКорректировкуОбесценения)) КАК ВариантПроводокПоОбесценениюВНА,
	|
	|	(ЕСТЬNULL(УчетнаяПолитика.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) 
	|		= ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) КАК АмортизацияНачисляетсяПоСтандартамМУ
	|
	|ПОМЕСТИТЬ СписокОбъектовУчета_УУ
	|ИЗ
	|	РегистрСведений.ПорядокУчетаНМАУУ.СрезПоследних(
	|			&КонецПредыдущегоМесяца,
	|			(Организация, НематериальныйАктив) В
	|				(ВЫБРАТЬ
	|					ПакетыАмортизации.Организация,
	|					ПакетыАмортизации.ОбъектУчета
	|				ИЗ
	|					ПакетыАмортизации КАК ПакетыАмортизации)) КАК ПорядокУчетаНМА
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&КонецМесяца) КАК УчетнаяПолитика
	|		ПО УчетнаяПолитика.Организация = ПорядокУчетаНМА.Организация.ГоловнаяОрганизация
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	&КонецМесяца                        КАК ПериодНачисления,
	|	&НачалоПредыдущегоМесяца            КАК НачалоМесяца,
	|	&КонецПредыдущегоМесяца             КАК КонецМесяца,
	|	&НачалоПредыдущегоМесяца            КАК Период,
	|	ИСТИНА                              КАК ЭтоДоначисление,
	|	ПорядокУчетаНМА.Организация         КАК Организация,
	|	ПорядокУчетаНМА.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ПорядокУчетаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|
	|	ПорядокУчетаНМА.НачислятьАмортизациюУУ КАК НачислятьАмортизациюУУ,
	|
	|	ПорядокУчетаНМА.СпособОтраженияРасходов КАК СпособОтраженияРасходовУУ,
	|
	|	ПорядокУчетаНМА.СтатьяРасходов    КАК СтатьяРасходовУУ,
	|	ПорядокУчетаНМА.АналитикаРасходов КАК АналитикаРасходовУУ,
	|
	|	ПорядокУчетаНМА.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюУУ,
	|	ПорядокУчетаНМА.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходовУУ,
	|
	|	ЕСТЬNULL(УчетнаяПолитика.ВариантПроводокПоОбесценениюВНА, ЗНАЧЕНИЕ(Перечисление.ВариантыПроводокПоОбесценениюВНА.ДоначислятьКорректировкуОбесценения)) КАК ВариантПроводокПоОбесценениюВНА,
	|
	|	(ЕСТЬNULL(УчетнаяПолитика.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) 
	|		= ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) КАК АмортизацияНачисляетсяПоСтандартамМУ
	|ИЗ
	|	РегистрСведений.ПорядокУчетаНМАУУ.СрезПоследних(
	|			&КонецПозапрошлогоМесяца,
	|			(Организация, НематериальныйАктив) В
	|				(ВЫБРАТЬ
	|					СписокНМА.Организация,
	|					СписокНМА.НематериальныйАктив
	|				ИЗ
	|					СписокОбъектовУчета_Доначисление_УУ КАК СписокНМА)) КАК ПорядокУчетаНМА
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&КонецПредыдущегоМесяца) КАК УчетнаяПолитика
	|		ПО УчетнаяПолитика.Организация = ПорядокУчетаНМА.Организация.ГоловнаяОрганизация
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	&КонецМесяца                        КАК ПериодНачисления,
	|	&НачалоМесяца                       КАК НачалоМесяца,
	|	&КонецМесяца                        КАК КонецМесяца,
	|	НАЧАЛОПЕРИОДА(ПорядокУчетаНМА.Период, ДЕНЬ) КАК Период,
	|	ЛОЖЬ                                КАК ЭтоДоначисление,
	|	ПорядокУчетаНМА.Организация         КАК Организация,
	|	ПорядокУчетаНМА.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ПорядокУчетаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|
	|	ПорядокУчетаНМА.НачислятьАмортизациюУУ КАК НачислятьАмортизациюУУ,
	|
	|	ПорядокУчетаНМА.СпособОтраженияРасходов КАК СпособОтраженияРасходовУУ,
	|
	|	ПорядокУчетаНМА.СтатьяРасходов    КАК СтатьяРасходовУУ,
	|	ПорядокУчетаНМА.АналитикаРасходов КАК АналитикаРасходовУУ,
	|
	|	ПорядокУчетаНМА.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюУУ,
	|	ПорядокУчетаНМА.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходовУУ,
	|
	|	ЕСТЬNULL(УчетнаяПолитика.ВариантПроводокПоОбесценениюВНА, ЗНАЧЕНИЕ(Перечисление.ВариантыПроводокПоОбесценениюВНА.ДоначислятьКорректировкуОбесценения)) КАК ВариантПроводокПоОбесценениюВНА,
	|
	|	(ЕСТЬNULL(УчетнаяПолитика.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) 
	|		= ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) КАК АмортизацияНачисляетсяПоСтандартамМУ
	|ИЗ
	|	РегистрСведений.ПорядокУчетаНМАУУ КАК ПорядокУчетаНМА
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&КонецМесяца) КАК УчетнаяПолитика
	|		ПО УчетнаяПолитика.Организация = ПорядокУчетаНМА.Организация.ГоловнаяОрганизация
	|ГДЕ
	|	ПорядокУчетаНМА.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
	|	И ПорядокУчетаНМА.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА2_4
	|	И (ПорядокУчетаНМА.Организация, ПорядокУчетаНМА.НематериальныйАктив) В
	|				(ВЫБРАТЬ
	|					ПакетыАмортизации.Организация,
	|					ПакетыАмортизации.ОбъектУчета
	|				ИЗ
	|					ПакетыАмортизации КАК ПакетыАмортизации)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	&КонецМесяца                        КАК ПериодНачисления,
	|	&НачалоПредыдущегоМесяца            КАК НачалоМесяца,
	|	&КонецПредыдущегоМесяца             КАК КонецМесяца,
	|	НАЧАЛОПЕРИОДА(ПорядокУчетаНМА.Период, ДЕНЬ) КАК Период,
	|	ИСТИНА                              КАК ЭтоДоначисление,
	|	ПорядокУчетаНМА.Организация         КАК Организация,
	|	ПорядокУчетаНМА.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ПорядокУчетаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|
	|	ПорядокУчетаНМА.НачислятьАмортизациюУУ КАК НачислятьАмортизациюУУ,
	|
	|	ПорядокУчетаНМА.СпособОтраженияРасходов КАК СпособОтраженияРасходовУУ,
	|
	|	ПорядокУчетаНМА.СтатьяРасходов    КАК СтатьяРасходовУУ,
	|	ПорядокУчетаНМА.АналитикаРасходов КАК АналитикаРасходовУУ,
	|
	|	ПорядокУчетаНМА.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюУУ,
	|	ПорядокУчетаНМА.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходовУУ,
	|
	|	ЕСТЬNULL(УчетнаяПолитика.ВариантПроводокПоОбесценениюВНА, ЗНАЧЕНИЕ(Перечисление.ВариантыПроводокПоОбесценениюВНА.ДоначислятьКорректировкуОбесценения)) КАК ВариантПроводокПоОбесценениюВНА,
	|
	|	(ЕСТЬNULL(УчетнаяПолитика.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) 
	|		= ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) КАК АмортизацияНачисляетсяПоСтандартамМУ
	|ИЗ
	|	РегистрСведений.ПорядокУчетаНМАУУ КАК ПорядокУчетаНМА
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&КонецПредыдущегоМесяца) КАК УчетнаяПолитика
	|		ПО УчетнаяПолитика.Организация = ПорядокУчетаНМА.Организация.ГоловнаяОрганизация
	|ГДЕ
	|	ПорядокУчетаНМА.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
	|	И ПорядокУчетаНМА.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА2_4
	|	И (ПорядокУчетаНМА.Организация, ПорядокУчетаНМА.НематериальныйАктив) В
	|				(ВЫБРАТЬ
	|					СписокНМА.Организация,
	|					СписокНМА.НематериальныйАктив
	|				ИЗ
	|					СписокОбъектовУчета_Доначисление_УУ КАК СписокНМА)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив, 
	|	Организация";
	
	Если ЗначениеЗаполнено(ПрефиксТаблицы) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СписокОбъектовУчета_УУ", "СписокОбъектовУчета_УУ" + ПрефиксТаблицы);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СписокОбъектовУчета_Доначисление_УУ", "СписокОбъектовУчета_Доначисление_УУ" + ПрефиксТаблицы);
	КонецЕсли; 
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область СписокОбъектовУчета_Доначисление_Все
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СписокОбъектовУчета.Организация КАК Организация,
	|	СписокОбъектовУчета.НематериальныйАктив КАК НематериальныйАктив
	|
	|ПОМЕСТИТЬ СписокОбъектовУчета_Доначисление_Все
	|ИЗ
	|	СписокОбъектовУчета_Доначисление_УУ КАК СписокОбъектовУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив,
	|	Организация";
	
	Если ЗначениеЗаполнено(ПрефиксТаблицы) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СписокОбъектовУчета_Доначисление_УУ", "СписокОбъектовУчета_Доначисление_УУ" + ПрефиксТаблицы);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СписокОбъектовУчета_Доначисление_Все", "СписокОбъектовУчета_Доначисление_Все" + ПрефиксТаблицы);
	КонецЕсли;
	 
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Представление = СтрШаблон(НСтр("ru = 'Амортизация НМА %1 за %2';
									|en = 'The %1 Intangible assets — Amortization charge for %2'"), Данные.Номер, Формат(Данные.Дата, НСтр("ru = 'ДФ=''ММММ гггг''';
																												|en = 'DF=''MMMM yyyy'''")));
	
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет расчет амортизации в заданном месяце и записывает его с помощью проведения документа "Амортизация НМА".
//
// Параметры:
//  ИсходныеПараметрыРасчета - см. ВнеоборотныеАктивы.НовыеПараметрыРасчетаАмортизации.
//  АдресХранилища - Строка - Адрес временного хранилища.
//
Процедура РассчитатьАмортизацию(ИсходныеПараметрыРасчета, АдресХранилища) Экспорт
	
	ОписаниеЗамера = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("ЗакрытиеМесяца.НачислениеАмортизацииНМА");
	
	ПараметрыРасчета = ВнеоборотныеАктивыСлужебный.СкопироватьПараметрыРасчетаАмортизации(ИсходныеПараметрыРасчета);
	ВнеоборотныеАктивыСлужебный.ДополнитьПараметрамиУчетаВнеоборотныхАктивов(ПараметрыРасчета);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ИсходныеПараметрыРасчета.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	// Основной расчет амортизации выполняется с помощью запроса
	ТекстЗапроса = ТекстЗапросаДляРасчетаАмортизации(ПараметрыРасчета);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ВнеоборотныеАктивы.УстановитьПараметрыЗапросаДляРасчетаАмортизации(Запрос, ПараметрыРасчета);
	
	КоличествоОбработанныхДанных = 0;
	
	Попытка
	
		УстановитьПривилегированныйРежим(Истина);
		РезультатЗапроса = Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
		
		// Расчет доначисления амортизации за прошлый месяц
		АмортизацияИзменений = РассчитатьАмортизациюИзменений(РезультатЗапроса, ПараметрыРасчета);
		
		Запрос.Текст = ТекстЗапросаДляРасчетаАмортизацииСУчетомДоначисления(ПараметрыРасчета);
		Запрос.УстановитьПараметр("АмортизацияИзменений_УУ", АмортизацияИзменений);
		РезультатЗапроса = Запрос.Выполнить();
		
		АмортизацияИзменений = РассчитатьАмортизациюИзменений(РезультатЗапроса, ПараметрыРасчета);
		
		Запрос.Текст = ТекстЗапросаЗавершитьРасчетАмортизации(ПараметрыРасчета);
		Запрос.УстановитьПараметр("АмортизацияИзменений_УУ", АмортизацияИзменений);
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		КоличествоОбработанныхДанных = Выборка.КоличествоДанных;
		
		ОбщегоНазначенияУТ.ДобавитьИдентификаторыВоВременнуюТаблицу("НачисленнаяАмортизация", МенеджерВременныхТаблиц);
		
		ЗарегистрироватьОшибкиРасчетаАмортизации(ПараметрыРасчета, МенеджерВременныхТаблиц);
		
		АмортизацияНМАЛокализация.ПослеРасчетаАмортизации(ИсходныеПараметрыРасчета, МенеджерВременныхТаблиц);
		
		ОбщегоНазначенияУТ.УничтожитьВременныеТаблицы(МенеджерВременныхТаблиц,, "НачисленнаяАмортизация,НомераПакетовСФилиалами");
		
		Если ПараметрыРасчета.ЗаписатьДанные Тогда
			ЗаписатьРасчетАмортизации(ПараметрыРасчета, МенеджерВременныхТаблиц);
		КонецЕсли;
		
	Исключение
		
		ПараметрыРасчета.ЕстьОшибки = Истина;
		ПараметрыРасчета.ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
			ПараметрыРасчета.ЭтапРасчета, 
			ПараметрыРасчета.Период, 
			ПараметрыРасчета.ПакетыАмортизации.ВыгрузитьКолонку("Организация"),
			ПараметрыРасчета.ТекстОшибки);
	
	КонецПопытки; 
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоОбработанныхДанных / 100);
	
	Если ЭтоАдресВременногоХранилища(АдресХранилища) Тогда
		РезультатРасчета = Новый Структура;
		РезультатРасчета.Вставить("ЕстьОшибки", ПараметрыРасчета.ЕстьОшибки);
		РезультатРасчета.Вставить("ТекстОшибки", ПараметрыРасчета.ТекстОшибки);
		ПоместитьВоВременноеХранилище(РезультатРасчета, АдресХранилища);
	КонецЕсли;
	
КонецПроцедуры

Функция НомерПакетаПоУмолчанию(Организация) Экспорт

	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат 0;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(ПакетыАмортизацииНМА.НомерПакета) КАК НомерПакета,
	|	ЕСТЬNULL(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПакетыАмортизацииНМА.НомерПакета), 0) КАК КоличествоНомеров
	|ИЗ
	|	РегистрСведений.ПакетыАмортизацииНМА КАК ПакетыАмортизацииНМА
	|ГДЕ
	|	ПакетыАмортизацииНМА.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ПакетыАмортизацииНМА.Организация";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	НомерПакета = 0;
	Если Выборка.Следующий() Тогда
		Если Выборка.КоличествоНомеров = 1 Тогда
			НомерПакета = Выборка.НомерПакета;
		КонецЕсли; 
	КонецЕсли;

	Возврат НомерПакета;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	Возврат ИсточникиДанных;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.АмортизацияНМА2_4";
	
	ВЗапросеЕстьИсточник = Истина;
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		ВЗапросеЕстьИсточник = Ложь;
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры)
	
	МенеджерВременныхТаблиц = Неопределено;
	
	Если ДопПараметры <> Неопределено
		И ДопПараметры.Свойство("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц) Тогда
		
		МенеджерВременныхТаблиц = ?(МенеджерВременныхТаблиц = Неопределено,
									Новый МенеджерВременныхТаблиц,
									МенеджерВременныхТаблиц);
		
	Иначе
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	КонецЕсли;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка           КАК Ссылка,
	|	ДанныеДокумента.Дата             КАК Период,
	|	ДанныеДокумента.Номер            КАК Номер,
	|	ДанныеДокумента.Проведен         КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления  КАК ПометкаУдаления,
	|	ДанныеДокумента.Комментарий      КАК Комментарий,
	|	ДанныеДокумента.Ответственный    КАК Ответственный,
	|	ДанныеДокумента.Организация      КАК Организация,
	|	ДанныеДокумента.НомерПакета      КАК НомерПакета
	|ИЗ
	|	Документ.АмортизацияНМА2_4 КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.АмортизацияНМА2_4"));
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.АмортизацияНМА);
	ЗначенияПараметровПроведения.Вставить("СтатьяАП_НМА", ПланыВидовХарактеристик.СтатьиАктивовПассивов.НематериальныеАктивы);

	ЗначенияПараметровПроведения.Вставить(
		"ДоначислятьКорректировкуОбесценения", 
		Перечисления.ВариантыПроводокПоОбесценениюВНА.ДоначислятьКорректировкуОбесценения);
	
	ЗначенияПараметровПроведения.Вставить(
		"СторнироватьКорректировкуОбесценения", 
		Перечисления.ВариантыПроводокПоОбесценениюВНА.СторнироватьКорректировкуОбесценения);
	
	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ТекстЗапросаТаблицаАмортизацияНМА(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "АмортизацияНМА";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстыЗапросаОбъединение = Новый Массив;
	
	#Область Амортизация // Дт 91 - Кт 04.ам
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	&Ссылка                                         КАК Регистратор,
	|	НачисленнаяАмортизация.ПериодРасчета            КАК Период,
	|	НачисленнаяАмортизация.Организация              КАК Организация,
	|	НачисленнаяАмортизация.Подразделение            КАК Подразделение,
	|	НачисленнаяАмортизация.НематериальныйАктив      КАК НематериальныйАктив,
	|	НачисленнаяАмортизация.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|
	|	НачисленнаяАмортизация.СуммаУУ    КАК Амортизация,
	|	НачисленнаяАмортизация.СуммаБУ    КАК АмортизацияРегл,
	|	НачисленнаяАмортизация.СуммаНУ    КАК АмортизацияНУ,
	|	НачисленнаяАмортизация.СуммаПР    КАК АмортизацияПР,
	|	НачисленнаяАмортизация.СуммаВР    КАК АмортизацияВР,
	|	НачисленнаяАмортизация.СуммаЦФ    КАК АмортизацияЦФ,
	|	НачисленнаяАмортизация.СуммаНУЦФ  КАК АмортизацияНУЦФ,
	|	НачисленнаяАмортизация.СуммаПРЦФ  КАК АмортизацияПРЦФ,
	|	НачисленнаяАмортизация.СуммаВРЦФ  КАК АмортизацияВРЦФ,
	|
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов КАК КорОрганизация,
	|	НачисленнаяАмортизация.КорПодразделение              КАК КорПодразделение,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	НачисленнаяАмортизация.СтатьяРасходов                КАК КорСтатьяРасходов,
	|	НачисленнаяАмортизация.АналитикаРасходов             КАК КорАналитикаРасходов,
	|
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|	НачисленнаяАмортизация.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	НачисленнаяАмортизация.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|	НачисленнаяАмортизация.ОстатокОбесцененияРегл КАК ОстатокОбесцененияРегл,
	|	НачисленнаяАмортизация.ОстатокОбесцененияУпр КАК ОстатокОбесцененияУпр,
	|	НачисленнаяАмортизация.ЭтоДоначисление                   КАК ЭтоДоначисление,
	|	
	|	НачисленнаяАмортизация.ИдентификаторСтроки               КАК ИдентификаторФинЗаписи,
	|
	|	ВЫБОР 
	|		КОГДА НачисленнаяАмортизация.ПередаватьРасходыВДругуюОрганизацию
	|				И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.АмортизацияНМАвДругуюОрганизацию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.АмортизацияНМА)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|
	|	ВЫБОР 
	|		КОГДА НачисленнаяАмортизация.ПередаватьРасходыВДругуюОрганизацию
	|				И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.АмортизацияНМАвДругуюОрганизацию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.АмортизацияНМА)
	|	КОНЕЦ КАК НастройкаХозяйственнойОперации,
	|	
	|	ЕСТЬNULL(УчетнаяПолитикаФинансовогоУчета.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) КАК ПорядокУчетаУУ
	|	
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&Период) КАК УчетнаяПолитикаФинансовогоУчета
	|		ПО УчетнаяПолитикаФинансовогоУчета.Организация = НачисленнаяАмортизация.ГоловнаяОрганизация
	|
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|	И НачисленнаяАмортизация.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив)
	|
	|	И (НачисленнаяАмортизация.СуммаУУ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаБУ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаНУ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаПР <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаВР <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаЦФ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаНУЦФ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаПРЦФ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаВРЦФ <> 0)";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область КорректировкаОбесценения // Дт 02.об - Кт 04.ам
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	&Ссылка                                         КАК Регистратор,
	|	НачисленнаяАмортизация.ПериодРасчета            КАК Период,
	|	НачисленнаяАмортизация.Организация              КАК Организация,
	|	НачисленнаяАмортизация.Подразделение            КАК Подразделение,
	|	НачисленнаяАмортизация.НематериальныйАктив      КАК НематериальныйАктив,
	|	НачисленнаяАмортизация.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|
	|	НачисленнаяАмортизация.ОбесценениеУУ            КАК Амортизация,
	|	НачисленнаяАмортизация.ОбесценениеБУ            КАК АмортизацияРегл,
	|	0                                               КАК АмортизацияНУ,
	|	0                                               КАК АмортизацияПР,
	|	НачисленнаяАмортизация.ОбесценениеБУ            КАК АмортизацияВР,
	|	НачисленнаяАмортизация.ОбесценениеЦФ            КАК АмортизацияЦФ,
	|	0                                               КАК АмортизацияНУЦФ,
	|	0                                               КАК АмортизацияПРЦФ,
	|	НачисленнаяАмортизация.ОбесценениеЦФ            КАК АмортизацияВРЦФ,
	|
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов КАК КорОрганизация,
	|	НачисленнаяАмортизация.КорПодразделение              КАК КорПодразделение,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	НачисленнаяАмортизация.СтатьяРасходов                КАК КорСтатьяРасходов,
	|	НачисленнаяАмортизация.АналитикаРасходов             КАК КорАналитикаРасходов,
	|
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|	НачисленнаяАмортизация.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	НачисленнаяАмортизация.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|	НачисленнаяАмортизация.ОстатокОбесцененияРегл КАК ОстатокОбесцененияРегл,
	|	НачисленнаяАмортизация.ОстатокОбесцененияУпр КАК ОстатокОбесцененияУпр,
	|	НачисленнаяАмортизация.ЭтоДоначисление                   КАК ЭтоДоначисление,
	|	
	|	НачисленнаяАмортизация.ИдентификаторСтроки               КАК ИдентификаторФинЗаписи,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаОбесцененияНМА) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КорректировкаОбесцененияНМА) КАК НастройкаХозяйственнойОперации,
	|	
	|	ЕСТЬNULL(УчетнаяПолитикаФинансовогоУчета.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) КАК ПорядокУчетаУУ
	|	
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&Период) КАК УчетнаяПолитикаФинансовогоУчета
	|		ПО УчетнаяПолитикаФинансовогоУчета.Организация = НачисленнаяАмортизация.ГоловнаяОрганизация
	|
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|	И НачисленнаяАмортизация.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив)
	|
	|	И НачисленнаяАмортизация.ВариантПроводокПоОбесценениюВНА = &ДоначислятьКорректировкуОбесценения
	|
	|	И (НачисленнаяАмортизация.ОбесценениеУУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеБУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеЦФ <> 0)";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	ТекстЗапроса = СтрСоединить(ТекстыЗапросаОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаСтоимостьНМА(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "СтоимостьНМА";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстыЗапросаОбъединение = Новый Массив;
	
	#Область Амортизация // Дт 91 - Кт 04.ниокр
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	&Ссылка                                         КАК Регистратор,
	|	НачисленнаяАмортизация.ПериодРасчета            КАК Период,
	|	НачисленнаяАмортизация.Организация              КАК Организация,
	|	НачисленнаяАмортизация.Подразделение            КАК Подразделение,
	|	НачисленнаяАмортизация.НематериальныйАктив      КАК НематериальныйАктив,
	|	НачисленнаяАмортизация.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|
	|	НачисленнаяАмортизация.СуммаУУ    КАК Стоимость,
	|	НачисленнаяАмортизация.СуммаБУ    КАК СтоимостьРегл,
	|	НачисленнаяАмортизация.СуммаНУ    КАК СтоимостьНУ,
	|	НачисленнаяАмортизация.СуммаПР    КАК СтоимостьПР,
	|	НачисленнаяАмортизация.СуммаВР    КАК СтоимостьВР,
	|	НачисленнаяАмортизация.СуммаЦФ    КАК СтоимостьЦФ,
	|	НачисленнаяАмортизация.СуммаНУЦФ  КАК СтоимостьНУЦФ,
	|	НачисленнаяАмортизация.СуммаПРЦФ  КАК СтоимостьПРЦФ,
	|	НачисленнаяАмортизация.СуммаВРЦФ  КАК СтоимостьВРЦФ,
	|
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов КАК КорОрганизация,
	|	НачисленнаяАмортизация.КорПодразделение              КАК КорПодразделение,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	НачисленнаяАмортизация.СтатьяРасходов                КАК КорСтатьяРасходов,
	|	НачисленнаяАмортизация.АналитикаРасходов             КАК КорАналитикаРасходов,
	|
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|	НачисленнаяАмортизация.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	НачисленнаяАмортизация.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|	НачисленнаяАмортизация.ЭтоДоначисление                   КАК ЭтоДоначисление,
	|	
	|	НачисленнаяАмортизация.ИдентификаторСтроки               КАК ИдентификаторФинЗаписи,
	|
	|	ВЫБОР 
	|		КОГДА НачисленнаяАмортизация.ПередаватьРасходыВДругуюОрганизацию
	|				И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНаРасходыНИОКРВДругуюОрганизацию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.АмортизацияНМА)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|
	|	ВЫБОР 
	|		КОГДА НачисленнаяАмортизация.ПередаватьРасходыВДругуюОрганизацию
	|				И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеНаРасходыНИОКРВДругуюОрганизацию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.АмортизацияНМА)
	|	КОНЕЦ КАК НастройкаХозяйственнойОперации,
	|	
	|	ЕСТЬNULL(УчетнаяПолитикаФинансовогоУчета.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) КАК ПорядокУчетаУУ
	|
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&Период) КАК УчетнаяПолитикаФинансовогоУчета
	|		ПО УчетнаяПолитикаФинансовогоУчета.Организация = НачисленнаяАмортизация.ГоловнаяОрганизация
	|
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|	И НачисленнаяАмортизация.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР)
	|
	|	И (НачисленнаяАмортизация.СуммаУУ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаБУ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаНУ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаПР <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаВР <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаЦФ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаНУЦФ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаПРЦФ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаВРЦФ <> 0)";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область КорректировкаОбесценения // Дт 02.об - Кт 04.ниокр
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	&Ссылка                                         КАК Регистратор,
	|	НачисленнаяАмортизация.ПериодРасчета            КАК Период,
	|	НачисленнаяАмортизация.Организация              КАК Организация,
	|	НачисленнаяАмортизация.Подразделение            КАК Подразделение,
	|	НачисленнаяАмортизация.НематериальныйАктив      КАК НематериальныйАктив,
	|	НачисленнаяАмортизация.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|
	|	НачисленнаяАмортизация.ОбесценениеУУ            КАК Стоимость,
	|	НачисленнаяАмортизация.ОбесценениеБУ            КАК СтоимостьРегл,
	|	0                                               КАК СтоимостьНУ,
	|	0                                               КАК СтоимостьПР,
	|	НачисленнаяАмортизация.ОбесценениеБУ            КАК СтоимостьВР,
	|	НачисленнаяАмортизация.ОбесценениеЦФ            КАК СтоимостьЦФ,
	|	0                                               КАК СтоимостьНУЦФ,
	|	0                                               КАК СтоимостьПРЦФ,
	|	НачисленнаяАмортизация.ОбесценениеЦФ            КАК СтоимостьВРЦФ,
	|
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов КАК КорОрганизация,
	|	НачисленнаяАмортизация.КорПодразделение              КАК КорПодразделение,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	НачисленнаяАмортизация.СтатьяРасходов                КАК КорСтатьяРасходов,
	|	НачисленнаяАмортизация.АналитикаРасходов             КАК КорАналитикаРасходов,
	|
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	НачисленнаяАмортизация.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|	НачисленнаяАмортизация.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	НачисленнаяАмортизация.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|	НачисленнаяАмортизация.ЭтоДоначисление                   КАК ЭтоДоначисление,
	|	
	|	НачисленнаяАмортизация.ИдентификаторСтроки               КАК ИдентификаторФинЗаписи,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаОбесцененияНМА) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КорректировкаОбесцененияНМА) КАК НастройкаХозяйственнойОперации,
	|	
	|	ЕСТЬNULL(УчетнаяПолитикаФинансовогоУчета.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) КАК ПорядокУчетаУУ
	|
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&Период) КАК УчетнаяПолитикаФинансовогоУчета
	|		ПО УчетнаяПолитикаФинансовогоУчета.Организация = НачисленнаяАмортизация.ГоловнаяОрганизация
	|
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|	И НачисленнаяАмортизация.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР)
	|
	|	И (НачисленнаяАмортизация.ОбесценениеУУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеБУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеЦФ <> 0)";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	ТекстЗапроса = СтрСоединить(ТекстыЗапросаОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Процедура ТекстЗапросаТаблицаОбесценениеВНА(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ОбесценениеВНА";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапросаОбъединение = Новый Массив;
	
	#Область СторноОбесценения // Дт 91 - Кт 04.об (сторно)
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	&Ссылка                                         КАК Регистратор,
	|	НачисленнаяАмортизация.ПериодРасчета            КАК Период,
	|	НачисленнаяАмортизация.НематериальныйАктив      КАК ВнеоборотныйАктив,
	|	НачисленнаяАмортизация.Организация              КАК Организация,
	|	НачисленнаяАмортизация.Подразделение            КАК Подразделение,
	|	НачисленнаяАмортизация.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|	НачисленнаяАмортизация.ЕГДС                     КАК ЕГДС,
	|	НачисленнаяАмортизация.ИдентификаторСтроки      КАК ИдентификаторФинЗаписи,
	|
	|	ВЫБОР 
	|		КОГДА НачисленнаяАмортизация.ПередаватьРасходыВДругуюОрганизацию
	|				И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА НачисленнаяАмортизация.ВидВНА = ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.НМА)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноОбесцененияНМАВДругуюОрганизацию)
	|					КОГДА НачисленнаяАмортизация.ВидВНА = ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.НИОКР)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноОбесцененияНИОКРВДругуюОрганизацию)
	|				КОНЕЦ
	|		КОГДА НачисленнаяАмортизация.ВидВНА = ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.НМА)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноОбесцененияНМА)
	|		КОГДА НачисленнаяАмортизация.ВидВНА = ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.НИОКР)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноОбесцененияНИОКР)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|
	|	ВЫБОР 
	|		КОГДА НачисленнаяАмортизация.ПередаватьРасходыВДругуюОрганизацию
	|				И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА НачисленнаяАмортизация.ВидВНА = ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.НМА)
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СторноОбесцененияНМАВДругуюОрганизацию)
	|					КОГДА НачисленнаяАмортизация.ВидВНА = ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.НИОКР)
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СторноОбесцененияНИОКРВДругуюОрганизацию)
	|				КОНЕЦ
	|		КОГДА НачисленнаяАмортизация.ВидВНА = ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.НМА)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СторноОбесцененияНМА)
	|		КОГДА НачисленнаяАмортизация.ВидВНА = ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.НИОКР)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СторноОбесцененияНИОКР)
	|	КОНЕЦ КАК НастройкаХозяйственнойОперации,
	|
	|	-(НачисленнаяАмортизация.ОбесценениеБУ + НачисленнаяАмортизация.ОбесценениеЦФ) КАК ОбесценениеРегл,
	|	-НачисленнаяАмортизация.ОбесценениеУУ КАК ОбесценениеУпр
	|	
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|
	|	И НачисленнаяАмортизация.ВариантПроводокПоОбесценениюВНА = &СторнироватьКорректировкуОбесценения
	|
	|	И (НачисленнаяАмортизация.ОбесценениеУУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеБУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеЦФ <> 0)";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область КорректировкаОбесценения // Дт 04.об - Кт 04.ам
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	&Ссылка                                         КАК Регистратор,
	|	НачисленнаяАмортизация.ПериодРасчета            КАК Период,
	|	НачисленнаяАмортизация.НематериальныйАктив      КАК ВнеоборотныйАктив,
	|	НачисленнаяАмортизация.Организация              КАК Организация,
	|	НачисленнаяАмортизация.Подразделение            КАК Подразделение,
	|	НачисленнаяАмортизация.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|	НачисленнаяАмортизация.ЕГДС                     КАК ЕГДС,
	|
	|	НачисленнаяАмортизация.ИдентификаторСтроки      КАК ИдентификаторФинЗаписи,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаОбесцененияНМА) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КорректировкаОбесцененияНМА) КАК НастройкаХозяйственнойОперации,
	|
	|	НачисленнаяАмортизация.ОбесценениеБУ + НачисленнаяАмортизация.ОбесценениеЦФ КАК ОбесценениеРегл,
	|	НачисленнаяАмортизация.ОбесценениеУУ КАК ОбесценениеУпр
	|	
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|
	|	И НачисленнаяАмортизация.ВариантПроводокПоОбесценениюВНА = &ДоначислятьКорректировкуОбесценения
	|
	|	И (НачисленнаяАмортизация.ОбесценениеУУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеБУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеЦФ <> 0)
	|";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапросаОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ДвиженияПоПрочимАктивамПассивам";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	СписокЗапросовОбъединение = Новый Массив;
	
	#Область Амортизация
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НачисленнаяАмортизация.ПериодРасчета                 КАК Период,
	|	НачисленнаяАмортизация.Организация                   КАК Организация,
	|	НачисленнаяАмортизация.Подразделение                 КАК Подразделение,
	|	НачисленнаяАмортизация.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыПоВыделенномуИмуществу) КАК Статья,
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов КАК Аналитика,
	|	НЕОПРЕДЕЛЕНО                                         КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	НачисленнаяАмортизация.СуммаБУ КАК СуммаРегл,
	|	НачисленнаяАмортизация.СуммаПР КАК ПостояннаяРазница,
	|	НачисленнаяАмортизация.СуммаВР КАК ВременнаяРазница,
	|	НачисленнаяАмортизация.СуммаУУ КАК СуммаУпр,
	|	НачисленнаяАмортизация.СуммаУУ КАК СуммаСНДС,
	|	НачисленнаяАмортизация.СуммаУУ КАК СуммаБезНДС,
	|	
	|	НачисленнаяАмортизация.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.АмортизацияНМАвДругуюОрганизацию) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|	И НачисленнаяАмортизация.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив)
	|	И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И (НачисленнаяАмортизация.СуммаБУ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаПР <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаВР <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаУУ <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НачисленнаяАмортизация.ПериодРасчета                  КАК Период,
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов  КАК Организация,
	|	НачисленнаяАмортизация.КорПодразделение               КАК Подразделение,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыПоВыделенномуИмуществу) КАК Статья,
	|	НачисленнаяАмортизация.Организация                    КАК Аналитика,
	|	НЕОПРЕДЕЛЕНО                                          КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|
	|	НачисленнаяАмортизация.СуммаБУ КАК СуммаРегл,
	|	НачисленнаяАмортизация.СуммаПР КАК ПостояннаяРазница,
	|	НачисленнаяАмортизация.СуммаВР КАК ВременнаяРазница,
	|	НачисленнаяАмортизация.СуммаУУ КАК СуммаУпр,
	|	НачисленнаяАмортизация.СуммаУУ КАК СуммаСНДС,
	|	НачисленнаяАмортизация.СуммаУУ КАК СуммаБезНДС,
	|	
	|	НачисленнаяАмортизация.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.АмортизацияНМАИзДругойОрганизации) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|	И НачисленнаяАмортизация.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив)
	|	И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И (НачисленнаяАмортизация.СуммаБУ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаПР <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаВР <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаУУ <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НачисленнаяАмортизация.ПериодРасчета                 КАК Период,
	|	НачисленнаяАмортизация.Организация                   КАК Организация,
	|	НачисленнаяАмортизация.Подразделение                 КАК Подразделение,
	|	НачисленнаяАмортизация.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыПоВыделенномуИмуществу) КАК Статья,
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов КАК Аналитика,
	|	НЕОПРЕДЕЛЕНО                                         КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	НачисленнаяАмортизация.СуммаБУ КАК СуммаРегл,
	|	НачисленнаяАмортизация.СуммаПР КАК ПостояннаяРазница,
	|	НачисленнаяАмортизация.СуммаВР КАК ВременнаяРазница,
	|	НачисленнаяАмортизация.СуммаУУ КАК СуммаУпр,
	|	НачисленнаяАмортизация.СуммаУУ КАК СуммаСНДС,
	|	НачисленнаяАмортизация.СуммаУУ КАК СуммаБезНДС,
	|	
	|	НачисленнаяАмортизация.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеНаРасходыНИОКРВДругуюОрганизацию) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|	И НачисленнаяАмортизация.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР)
	|	И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И (НачисленнаяАмортизация.СуммаБУ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаПР <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаВР <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаУУ <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НачисленнаяАмортизация.ПериодРасчета                  КАК Период,
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов  КАК Организация,
	|	НачисленнаяАмортизация.КорПодразделение               КАК Подразделение,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыПоВыделенномуИмуществу) КАК Статья,
	|	НачисленнаяАмортизация.Организация                    КАК Аналитика,
	|	НЕОПРЕДЕЛЕНО                                          КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|
	|	НачисленнаяАмортизация.СуммаБУ КАК СуммаРегл,
	|	НачисленнаяАмортизация.СуммаПР КАК ПостояннаяРазница,
	|	НачисленнаяАмортизация.СуммаВР КАК ВременнаяРазница,
	|	НачисленнаяАмортизация.СуммаУУ КАК СуммаУпр,
	|	НачисленнаяАмортизация.СуммаУУ КАК СуммаСНДС,
	|	НачисленнаяАмортизация.СуммаУУ КАК СуммаБезНДС,
	|	
	|	НачисленнаяАмортизация.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеНаРасходыНИОКРИзДругойОрганизации) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|	И НачисленнаяАмортизация.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР)
	|	И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И (НачисленнаяАмортизация.СуммаБУ <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаПР <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаВР <> 0
	|		ИЛИ НачисленнаяАмортизация.СуммаУУ <> 0)
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область СторноОбесценения
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НачисленнаяАмортизация.ПериодРасчета                 КАК Период,
	|	НачисленнаяАмортизация.Организация                   КАК Организация,
	|	НачисленнаяАмортизация.Подразделение                 КАК Подразделение,
	|	НачисленнаяАмортизация.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыПоВыделенномуИмуществу) КАК Статья,
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов КАК Аналитика,
	|	НЕОПРЕДЕЛЕНО                                         КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	-НачисленнаяАмортизация.ОбесценениеБУ КАК СуммаРегл,
	|	0 КАК ПостояннаяРазница,
	|	-НачисленнаяАмортизация.ОбесценениеБУ КАК ВременнаяРазница,
	|	-НачисленнаяАмортизация.ОбесценениеУУ КАК СуммаУпр,
	|	-НачисленнаяАмортизация.ОбесценениеУУ КАК СуммаСНДС,
	|	-НачисленнаяАмортизация.ОбесценениеУУ КАК СуммаБезНДС,
	|	
	|	НачисленнаяАмортизация.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СторноОбесцененияНМАВДругуюОрганизацию) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|	И НачисленнаяАмортизация.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив)
	|	И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|
	|	И НачисленнаяАмортизация.ВариантПроводокПоОбесценениюВНА = &СторнироватьКорректировкуОбесценения
	|
	|	И (НачисленнаяАмортизация.ОбесценениеУУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеБУ <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НачисленнаяАмортизация.ПериодРасчета                  КАК Период,
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов  КАК Организация,
	|	НачисленнаяАмортизация.КорПодразделение               КАК Подразделение,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыПоВыделенномуИмуществу) КАК Статья,
	|	НачисленнаяАмортизация.Организация                    КАК Аналитика,
	|	НЕОПРЕДЕЛЕНО                                          КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|
	|	-НачисленнаяАмортизация.ОбесценениеБУ КАК СуммаРегл,
	|	0 КАК ПостояннаяРазница,
	|	-НачисленнаяАмортизация.ОбесценениеБУ КАК ВременнаяРазница,
	|	-НачисленнаяАмортизация.ОбесценениеУУ КАК СуммаУпр,
	|	-НачисленнаяАмортизация.ОбесценениеУУ КАК СуммаСНДС,
	|	-НачисленнаяАмортизация.ОбесценениеУУ КАК СуммаБезНДС,
	|	
	|	НачисленнаяАмортизация.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СторноОбесцененияНМАизДругойОрганизации) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|	И НачисленнаяАмортизация.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив)
	|	И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|
	|	И НачисленнаяАмортизация.ВариантПроводокПоОбесценениюВНА = &СторнироватьКорректировкуОбесценения
	|
	|	И (НачисленнаяАмортизация.ОбесценениеУУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеБУ <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НачисленнаяАмортизация.ПериодРасчета                 КАК Период,
	|	НачисленнаяАмортизация.Организация                   КАК Организация,
	|	НачисленнаяАмортизация.Подразделение                 КАК Подразделение,
	|	НачисленнаяАмортизация.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыПоВыделенномуИмуществу) КАК Статья,
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов КАК Аналитика,
	|	НЕОПРЕДЕЛЕНО                                         КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	-НачисленнаяАмортизация.ОбесценениеБУ КАК СуммаРегл,
	|	0 КАК ПостояннаяРазница,
	|	-НачисленнаяАмортизация.ОбесценениеБУ КАК ВременнаяРазница,
	|	-НачисленнаяАмортизация.ОбесценениеУУ КАК СуммаУпр,
	|	-НачисленнаяАмортизация.ОбесценениеУУ КАК СуммаСНДС,
	|	-НачисленнаяАмортизация.ОбесценениеУУ КАК СуммаБезНДС,
	|	
	|	НачисленнаяАмортизация.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СторноОбесцененияНИОКРВДругуюОрганизацию) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|	И НачисленнаяАмортизация.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР)
	|	И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|
	|	И НачисленнаяАмортизация.ВариантПроводокПоОбесценениюВНА = &СторнироватьКорректировкуОбесценения
	|
	|	И (НачисленнаяАмортизация.ОбесценениеУУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеБУ <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НачисленнаяАмортизация.ПериодРасчета                  КАК Период,
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов  КАК Организация,
	|	НачисленнаяАмортизация.КорПодразделение               КАК Подразделение,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.РасчетыПоВыделенномуИмуществу) КАК Статья,
	|	НачисленнаяАмортизация.Организация                    КАК Аналитика,
	|	НЕОПРЕДЕЛЕНО                                          КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|
	|	-НачисленнаяАмортизация.ОбесценениеБУ КАК СуммаРегл,
	|	0 КАК ПостояннаяРазница,
	|	-НачисленнаяАмортизация.ОбесценениеБУ КАК ВременнаяРазница,
	|	-НачисленнаяАмортизация.ОбесценениеУУ КАК СуммаУпр,
	|	-НачисленнаяАмортизация.ОбесценениеУУ КАК СуммаСНДС,
	|	-НачисленнаяАмортизация.ОбесценениеУУ КАК СуммаБезНДС,
	|	
	|	НачисленнаяАмортизация.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СторноОбесцененияНИОКРИзДругойОрганизации) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|	И НачисленнаяАмортизация.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР)
	|	И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|
	|	И НачисленнаяАмортизация.ВариантПроводокПоОбесценениюВНА = &СторнироватьКорректировкуОбесценения
	|
	|	И (НачисленнаяАмортизация.ОбесценениеУУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеБУ <> 0)
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти

	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Процедура ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапросаОбъединение = Новый Массив;
	
	#Область СторноОбесценения // Дт 91 - Кт 04.об (сторно)
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НачисленнаяАмортизация.ПериодРасчета КАК Период,
	|	НачисленнаяАмортизация.Организация КАК Организация,
	|
	|	НачисленнаяАмортизация.Подразделение КАК Подразделение,
	|	НачисленнаяАмортизация.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_НМА КАК Статья,
	|	НачисленнаяАмортизация.НематериальныйАктив КАК АналитикаАктивовПассивов,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|
	|	НачисленнаяАмортизация.КорПодразделение КАК КорПодразделение,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	НачисленнаяАмортизация.СтатьяРасходов КАК КорСтатья,
	|	НачисленнаяАмортизация.АналитикаРасходов КАК КорАналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК КорАналитикаАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО КАК КорГруппаФинансовогоУчета,
	|
	|	-НачисленнаяАмортизация.ОбесценениеУУ КАК Сумма,
	|	-НачисленнаяАмортизация.ОбесценениеУУ КАК СуммаУпр,
	|	-(НачисленнаяАмортизация.ОбесценениеБУ + НачисленнаяАмортизация.ОбесценениеЦФ) КАК СуммаРегл,
	|
	|	ВЫБОР 
	|		КОГДА НачисленнаяАмортизация.ПередаватьРасходыВДругуюОрганизацию
	|				И НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА НачисленнаяАмортизация.ВидВНА = ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.НМА)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноОбесцененияНМАВДругуюОрганизацию)
	|					КОГДА НачисленнаяАмортизация.ВидВНА = ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.НИОКР)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноОбесцененияНИОКРВДругуюОрганизацию)
	|				КОНЕЦ
	|		КОГДА НачисленнаяАмортизация.ВидВНА = ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.НМА)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноОбесцененияНМА)
	|		КОГДА НачисленнаяАмортизация.ВидВНА = ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.НИОКР)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноОбесцененияНИОКР)
	|	КОНЕЦ КАК ХозяйственнаяОперация
	|
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|
	|	И НачисленнаяАмортизация.ВариантПроводокПоОбесценениюВНА = &СторнироватьКорректировкуОбесценения
	|
	|	И (НачисленнаяАмортизация.ОбесценениеУУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеБУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеЦФ <> 0)";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область КорректировкаОбесценения // Дт 04.об - Кт 04.ам
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НачисленнаяАмортизация.ПериодРасчета КАК Период,
	|	НачисленнаяАмортизация.Организация КАК Организация,
	|
	|	НачисленнаяАмортизация.Подразделение КАК Подразделение,
	|	НачисленнаяАмортизация.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_НМА КАК Статья,
	|	НачисленнаяАмортизация.НематериальныйАктив КАК АналитикаАктивовПассивов,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|
	|	НачисленнаяАмортизация.Подразделение КАК КорПодразделение,
	|	НачисленнаяАмортизация.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_НМА КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО КАК КорАналитикаРасходов,
	|	НачисленнаяАмортизация.НематериальныйАктив КАК КорАналитикаАктивовПассивов,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета КАК КорГруппаФинансовогоУчета,
	|
	|	НачисленнаяАмортизация.ОбесценениеУУ КАК Сумма,
	|	НачисленнаяАмортизация.ОбесценениеУУ КАК СуммаУпр,
	|	НачисленнаяАмортизация.ОбесценениеБУ + НачисленнаяАмортизация.ОбесценениеЦФ КАК СуммаРегл,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаОбесцененияНМА) КАК ХозяйственнаяОперация
	|
	|ИЗ
	|	НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|
	|ГДЕ
	|	НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета
	|
	|	И НачисленнаяАмортизация.ВариантПроводокПоОбесценениюВНА = &ДоначислятьКорректировкуОбесценения
	|
	|	И (НачисленнаяАмортизация.ОбесценениеУУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеБУ <> 0
	|		ИЛИ НачисленнаяАмортизация.ОбесценениеЦФ <> 0)";
	ТекстыЗапросаОбъединение.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапросаОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПартииПрочихРасходов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&Ответственный                          КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	ЛОЖЬ                                    КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО                            КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО                            КАК ИсправляемыйДокумент,
	|	&Период                                 КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО                            КАК Приоритет";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	СписокЗапросовОбъединение = Новый Массив;
	
	СписокЗапросовОбъединение.Добавить(РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы());
	СписокЗапросовОбъединение.Добавить(ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеНачисленнойАмортизацииВПрочихРасходах());
	СписокЗапросовОбъединение.Добавить(ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеОбесцененияВПрочихРасходах());
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтПартииПрочихРасходов";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаВтПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПартииПрочихРасходов";
	
	СписокЗапросовОбъединение = Новый Массив;
	СписокЗапросовОбъединение.Добавить(РегистрыНакопления.ПартииПрочихРасходов.ТекстОписаниеВтИсходныеПартииПрочихРасходов());
	СписокЗапросовОбъединение.Добавить(ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеНачисленнойАмортизацииВПартияхПрочихРасходов());
	СписокЗапросовОбъединение.Добавить(ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеОбесцененияВПартияхПрочихРасходах());
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат АмортизацияНМАЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат АмортизацияНМАЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

#КонецОбласти

#Область РасчетАмортизации

Процедура ЗаписатьРасчетАмортизации(ПараметрыРасчета, МенеджерВременныхТаблиц)

	НачатьТранзакцию();
	
	Попытка
	
		// Нужно заблокировать документы, чтобы не создать дублей
		Блокировка = Новый БлокировкаДанных;
		
		ЭлементБлокировки = Блокировка.Добавить("Документ.АмортизацияНМА2_4");
		ЭлементБлокировки.УстановитьЗначение("Дата", Новый Диапазон(НачалоМесяца(ПараметрыРасчета.Период),КонецМесяца(ПараметрыРасчета.Период)));
		ЭлементБлокировки.ИсточникДанных = ПараметрыРасчета.ПакетыАмортизации;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Организация", "Организация");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("НомерПакета", "НомерПакета");
		
		Блокировка.Заблокировать(); 
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	НомераПакетовСФилиалами.Организация,
		|	НомераПакетовСФилиалами.НомерПакета,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ЕСТЬNULL(РегламентныйДокумент.Проведен, ЛОЖЬ) = ИСТИНА
		|				ТОГДА РегламентныйДокумент.Ссылка
		|			ИНАЧЕ ЗНАЧЕНИЕ(Документ.АмортизацияНМА2_4.ПустаяСсылка)
		|		КОНЕЦ) КАК СсылкаПроведен,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ЕСТЬNULL(РегламентныйДокумент.Проведен, НЕОПРЕДЕЛЕНО) = ЛОЖЬ
		|				ТОГДА РегламентныйДокумент.Ссылка
		|			ИНАЧЕ ЗНАЧЕНИЕ(Документ.АмортизацияНМА2_4.ПустаяСсылка)
		|		КОНЕЦ) КАК СсылкаНеПроведен,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ЕСТЬNULL(РегламентныйДокумент.ПометкаУдаления, ЛОЖЬ) = ИСТИНА
		|				ТОГДА РегламентныйДокумент.Ссылка
		|			ИНАЧЕ ЗНАЧЕНИЕ(Документ.АмортизацияНМА2_4.ПустаяСсылка)
		|		КОНЕЦ) КАК СсылкаУдален
		|ИЗ
		|	НомераПакетовСФилиалами КАК НомераПакетовСФилиалами
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АмортизацияНМА2_4 КАК РегламентныйДокумент
		|		ПО (РегламентныйДокумент.Организация = НомераПакетовСФилиалами.Организация)
		|			И (РегламентныйДокумент.НомерПакета = НомераПакетовСФилиалами.НомерПакета)
		|			И (РегламентныйДокумент.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца)
		|ГДЕ
		|	НомераПакетовСФилиалами.Организация В(&СписокОрганизаций)
		|
		|СГРУППИРОВАТЬ ПО
		|	НомераПакетовСФилиалами.Организация,
		|	НомераПакетовСФилиалами.НомерПакета";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(ПараметрыРасчета.Период));
		Запрос.УстановитьПараметр("КонецМесяца",  КонецМесяца(ПараметрыРасчета.Период));
		Запрос.УстановитьПараметр("СписокОрганизаций", ПараметрыРасчета.СписокОрганизаций);
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.НомерПакета = ПараметрыРасчета.НомерПакета Тогда
				
				// Расчет выполняется из формы документа.
				Если ЗначениеЗаполнено(ПараметрыРасчета.Ссылка) Тогда
					ДокументОбъект = ПараметрыРасчета.Ссылка.ПолучитьОбъект();
				Иначе 
					ДокументОбъект = ВнеоборотныеАктивыСлужебный.ПолучитьРегламентныйДокумент(
						Выборка.СсылкаПроведен, Неопределено, Неопределено, Документы.АмортизацияНМА2_4);
				КонецЕсли;
				
				ДокументОбъект.НомерПакета   = Выборка.НомерПакета;
				ДокументОбъект.Организация   = Выборка.Организация;
				ДокументОбъект.Комментарий   = ПараметрыРасчета.Комментарий;
				ДокументОбъект.Ответственный = ПараметрыРасчета.Ответственный;
				
			Иначе	
				
				ДокументОбъект = ВнеоборотныеАктивыСлужебный.ПолучитьРегламентныйДокумент(
					Выборка.СсылкаПроведен, Выборка.СсылкаНеПроведен, Выборка.СсылкаУдален, Документы.АмортизацияНМА2_4);
					
				ДокументОбъект.НомерПакета   = Выборка.НомерПакета;
				ДокументОбъект.Организация   = Выборка.Организация;
				ДокументОбъект.Ответственный = Пользователи.ТекущийПользователь();
				
			КонецЕсли; 
				
			ДокументОбъект.Дата = КонецМесяца(ПараметрыРасчета.Период);
			
			ДокументОбъект.ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
			
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			
			ВнеоборотныеАктивыСлужебный.УничтожитьВременныеТаблицыПослеЗаписиРасчетаАмортизации(МенеджерВременныхТаблиц, ПараметрыРасчета);
			
		КонецЦикла; 
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстОшибки;
	КонецПопытки; 
	
КонецПроцедуры

Функция РассчитатьАмортизациюИзменений(РезультатЗапроса, ПараметрыРасчета)

	АмортизацияИзменений = РезультатЗапроса.Выгрузить();
	АмортизацияИзменений.Сортировать("НематериальныйАктив,Период");
	АмортизацияИзменений.Колонки.Добавить("ИдентификаторСтроки", ОбщегоНазначения.ОписаниеТипаСтрока(36));
	
	ТекущееНМА = Неопределено;
	НачисленнаяАмортизация = 0;
	НачисленнаяАмортизацияРегл = 0;
	РесурсВыработанныйЗаПрошлыйПериод = 0;
	РесурсНазначенный = 0;
	
	// Хранит параметры расчета, которые использовались при предыдущем расчете.
	ЗначенияПараметровРасчетаИзменений = Новый Соответствие;
	
	Для каждого ДанныеСтроки Из АмортизацияИзменений Цикл
		
		ЕстьОшибки = Ложь;
		
		ТекстСообщения = НСтр("ru = 'Для объекта учета ""%1"" (код %2) не определена остаточная стоимость. Необходимо проверить первичные документы.';
								|en = 'Net book value is not determined for the %1 accounting object (code %2). Check source documents.'");
		ПроверитьПараметрРасчета(ДанныеСтроки.ОстаточнаяСтоимость, ТекстСообщения, ДанныеСтроки, ПараметрыРасчета, ЕстьОшибки);
		
		ТекстСообщения = НСтр("ru = 'Для объекта учета ""%1"" (код %2) не определен срок использования. Необходимо проверить первичные документы.';
								|en = 'Useful life is not determined for the %1 accounting object (code %2). Check source documents.'");
		ПроверитьПараметрРасчета(ДанныеСтроки.РесурсНазначенный, ТекстСообщения, ДанныеСтроки, ПараметрыРасчета, ЕстьОшибки);
		
		ТекстСообщения = НСтр("ru = 'Для объекта учета ""%1"" (код %2) не определено количество прошедших месяцев.
                               |Необходимо проверить первичные документы.';
                               |en = 'Number of past months is not determined for the %1 accounting object (code %2). 
                               |Check source documents.'");
		ПроверитьПараметрРасчета(ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод, ТекстСообщения, ДанныеСтроки, ПараметрыРасчета, ЕстьОшибки);
		
		ТекстСообщения = НСтр("ru = 'Для объекта учета ""%1"" (код %2) не определена ликвидационная стоимость. Необходимо проверить первичные документы.';
								|en = 'Residual value is not determined for the %1 accounting object (code %2). Check source documents.'");
		ПроверитьПараметрРасчета(ДанныеСтроки.ЛиквидационнаяСтоимость, ТекстСообщения, ДанныеСтроки, ПараметрыРасчета, ЕстьОшибки);
		
		ТекстСообщения = НСтр("ru = 'Для объекта учета ""%1"" (код %2) не определен метод начисления амортизации. Необходимо проверить первичные документы.';
								|en = 'Depreciation method not specified for asset %1, ID: %2. Please check the source documents.'");
		ПроверитьПараметрРасчета(ДанныеСтроки.МетодНачисленияАмортизации, ТекстСообщения, ДанныеСтроки, ПараметрыРасчета, ЕстьОшибки);
		
		Если ЕстьОшибки ИЛИ ДанныеСтроки.НачислятьАмортизацию <> Истина Тогда
			Продолжить;
		КонецЕсли;
		
		ЗначенияПараметров = ЗначенияПараметровРасчетаИзменений.Получить(ДанныеСтроки.НематериальныйАктив);
		
		Если ЗначенияПараметров = Неопределено Тогда
			
			// Накопленное обесценение c начала месяца на дату расчета.
			// Используется для прекращения расчета, если она превышает остаточную сумму обесценения.
			ЗначенияПараметров = Новый Структура;
			ЗначенияПараметров.Вставить("ОбесценениеЗаМесяцУпр", 0);  
			ЗначенияПараметров.Вставить("ОбесценениеЗаМесяцРегл", 0);
			
			ЗначенияПараметровРасчетаИзменений.Вставить(ДанныеСтроки.НематериальныйАктив, ЗначенияПараметров);
			
		КонецЕсли;
			
		Если РесурсНазначенный <> ДанныеСтроки.РесурсНазначенный Тогда
			РесурсВыработанныйЗаПрошлыйПериод = 0;
			РесурсНазначенный = ДанныеСтроки.РесурсНазначенный;
		КонецЕсли;
		
		Если ДанныеСтроки.НематериальныйАктив <> ТекущееНМА Тогда
			НачисленнаяАмортизация = 0;
			НачисленнаяАмортизацияРегл = 0;
			РесурсВыработанныйЗаПрошлыйПериод = 0;
			ТекущееНМА = ДанныеСтроки.НематериальныйАктив;
		КонецЕсли; 
		
		Если ДанныеСтроки.ЭтоИзменения Тогда

			Если ТипЗнч(ДанныеСтроки.Регистратор) = Тип("ДокументСсылка.КорректировкаСтоимостиИАмортизацииНМА") Тогда
				ДанныеСтроки.ОстаточнаяСтоимость = ДанныеСтроки.СтоимостьИзменение - ДанныеСтроки.АмортизацияИзменение;
				ДанныеСтроки.ОстаточнаяСтоимостьРегл = ДанныеСтроки.СтоимостьРеглИзменение - ДанныеСтроки.АмортизацияРеглИзменение;
				НачисленнаяАмортизация = 0;
				НачисленнаяАмортизацияРегл = 0;
			Иначе
				ДанныеСтроки.ОстаточнаяСтоимость = Макс(ДанныеСтроки.ОстаточнаяСтоимость - НачисленнаяАмортизация, 0);
				ДанныеСтроки.ОстаточнаяСтоимостьРегл = Макс(ДанныеСтроки.ОстаточнаяСтоимостьРегл - НачисленнаяАмортизацияРегл, 0);
			КонецЕсли;
		
			ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод = ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод + РесурсВыработанныйЗаПрошлыйПериод;
			
			// Расчет суммы амортизации по формуле.
			ДанныеСтроки.СуммаУУ = РассчитатьСуммуАмортизацииУУ(
										ДанныеСтроки, 
										ДанныеСтроки.ОстаточнаяСтоимость, 
										ДанныеСтроки.ЛиквидационнаяСтоимость,
										ДанныеСтроки.ОстатокОбесцененияУпр);
			
			ДанныеСтроки.СуммаБУ = РассчитатьСуммуАмортизацииУУ(
										ДанныеСтроки, 
										ДанныеСтроки.ОстаточнаяСтоимостьРегл, 
										ДанныеСтроки.ЛиквидационнаяСтоимостьРегл,
										ДанныеСтроки.ОстатокОбесцененияРегл);
			
			// Расчет суммы обесценения по формуле.
			ДанныеСтроки.ОбесценениеУУ = РассчитатьСуммуОбесценения(
										ДанныеСтроки, 
										ДанныеСтроки.ОстатокОбесцененияУпр,
										ЗначенияПараметров.ОбесценениеЗаМесяцУпр);
			
			ДанныеСтроки.ОбесценениеБУ = РассчитатьСуммуОбесценения(
										ДанныеСтроки, 
										ДанныеСтроки.ОстатокОбесцененияРегл,
										ЗначенияПараметров.ОбесценениеЗаМесяцРегл);
			
			// Нужно запомнить стоимость, которая использовалась для расчета амортизации, чтобы записать ее в регистр.
			ДанныеСтроки.СтоимостьДляВычисленияАмортизации = ДанныеСтроки.ОстаточнаяСтоимость;
			ДанныеСтроки.СтоимостьДляВычисленияАмортизацииРегл = ДанныеСтроки.ОстаточнаяСтоимостьРегл;
			
		КонецЕсли;
		
		НачисленнаяАмортизация = НачисленнаяАмортизация + ДанныеСтроки.СуммаУУ;
		НачисленнаяАмортизацияРегл = НачисленнаяАмортизацияРегл + ДанныеСтроки.СуммаБУ;
		
		РесурсВыработанныйЗаПрошлыйПериод = РесурсВыработанныйЗаПрошлыйПериод + ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод;
		
		ДанныеСтроки.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
		
		// Накопленное обесценение за месяц.
		ЗначенияПараметров.ОбесценениеЗаМесяцУпр  = ЗначенияПараметров.ОбесценениеЗаМесяцУпр  + ДанныеСтроки.ОбесценениеУУ;
		ЗначенияПараметров.ОбесценениеЗаМесяцРегл = ЗначенияПараметров.ОбесценениеЗаМесяцРегл + ДанныеСтроки.ОбесценениеБУ;
		
	КонецЦикла; 
	
	Возврат АмортизацияИзменений;

КонецФункции

Функция РассчитатьСуммуАмортизацииУУ(ДанныеСтроки, ОстаточнаяСтоимость, ЛиквидационнаяСтоимость, ОстатокОбесценения)

	СуммаУУ = 0;
	
	ОстаточнаяСтоимостьПослеНачисленияАмортизации = ОстаточнаяСтоимость - ЛиквидационнаяСтоимость - ОстатокОбесценения;
		
	Если НЕ ДанныеСтроки.НачислятьАмортизацию 
		ИЛИ ОстаточнаяСтоимостьПослеНачисленияАмортизации <= 0 Тогда
		
		СуммаУУ = 0;
		
	ИначеЕсли (ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод + ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод) >= ДанныеСтроки.РесурсНазначенный Тогда
		
		СуммаУУ = ОстаточнаяСтоимость;
		
	ИначеЕсли ДанныеСтроки.МетодНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.Линейный
		ИЛИ ДанныеСтроки.МетодНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции
		ИЛИ ДанныеСтроки.МетодНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка
			И (ДанныеСтроки.РесурсНазначенный
				- ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод
				- ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод) <= 12 Тогда
		
		СуммаУУ = ОстаточнаяСтоимость
					* (ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод)
					/ (ДанныеСтроки.РесурсНазначенный - ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод);
		
	ИначеЕсли ДанныеСтроки.МетодНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка Тогда
		
		СуммаУУ = ОстаточнаяСтоимость
					* ДанныеСтроки.КоэффициентУскорения
					* ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод
					/ (ДанныеСтроки.РесурсНазначенный - ДанныеСтроки.КоэффициентУскорения 
							*(ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод - ДанныеСтроки.ОтработанныхМесяцевПолныхЛет));
	КонецЕсли; 
	
	Если СуммаУУ > ОстаточнаяСтоимостьПослеНачисленияАмортизации
		ИЛИ (ОстаточнаяСтоимостьПослеНачисленияАмортизации - СуммаУУ) < ВнеоборотныеАктивыСлужебный.МинимальнаяОстаточнаяСтоимость() Тогда
		СуммаУУ = Макс(ОстаточнаяСтоимостьПослеНачисленияАмортизации, 0);
	КонецЕсли; 
	
	Возврат Окр(СуммаУУ, 2);
	
КонецФункции

Функция РассчитатьСуммуОбесценения(ДанныеСтроки, ОстатокОбесценения, НачисленноеОбесценениеЗаМесяц) Экспорт

	СуммаОбесценения = 0;
	
	ОстатокОбесцененияПослеНачисления = ОстатокОбесценения - НачисленноеОбесценениеЗаМесяц;
	
	Если НЕ ДанныеСтроки.НачислятьАмортизацию 
		ИЛИ ОстатокОбесцененияПослеНачисления <= 0
		ИЛИ ОстатокОбесценения <= 0 Тогда
		
		СуммаОбесценения = 0;
		
	ИначеЕсли (ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод + ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод) >= ДанныеСтроки.РесурсНазначенный Тогда
		
		СуммаОбесценения = ОстатокОбесценения;
		
	ИначеЕсли ДанныеСтроки.МетодНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.Линейный
		ИЛИ ДанныеСтроки.МетодНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции Тогда
		
		СуммаОбесценения = СуммаОбесценения
					* (ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод)
					/ (ДанныеСтроки.РесурсНазначенный - ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод);
		
	КонецЕсли; 
	
	Если СуммаОбесценения > ОстатокОбесцененияПослеНачисления
		ИЛИ (ОстатокОбесцененияПослеНачисления - СуммаОбесценения) < ВнеоборотныеАктивыСлужебный.МинимальнаяОстаточнаяСтоимость() Тогда
		СуммаОбесценения = Макс(ОстатокОбесцененияПослеНачисления, 0);
	КонецЕсли; 
	
	Возврат Окр(СуммаОбесценения, 2);
	
КонецФункции
 
Процедура ПроверитьПараметрРасчета(ПараметрРасчета, ШаблонСообщения, Выборка, ПараметрыРасчета, ЕстьОшибки)

	Если ПараметрРасчета <> NULL
		ИЛИ Выборка.НачислятьАмортизацию <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьОшибки = Истина;
	
	ТекстОшибки = СтрШаблон(ШаблонСообщения, Выборка.НематериальныйАктив, Выборка.НематериальныйАктивКод);
	
	ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
		ПараметрыРасчета.ЭтапРасчета, 
		ПараметрыРасчета.Период,
		Выборка.Организация,
		ТекстОшибки,,
		Выборка.НематериальныйАктив,
		ПараметрыРасчета.ЕстьОшибки);
	
КонецПроцедуры

Процедура ЗарегистрироватьОшибкиРасчетаАмортизации(ПараметрыРасчета, МенеджерВременныхТаблиц) Экспорт

	ТекстЗапроса = АмортизацияНМАЛокализация.ТекстЗапросаЗарегистрироватьОшибкиРасчетаАмортизации(ПараметрыРасчета);
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПроверкаПараметровРасчета.Организация КАК Организация,
		|	ПроверкаПараметровРасчета.ОбъектУчета КАК ОбъектУчета,
		|	ВЫРАЗИТЬ(ПроверкаПараметровРасчета.ОбъектУчета КАК Справочник.НематериальныеАктивы).Наименование КАК ОбъектУчетаПредставление,
		|	ВЫРАЗИТЬ(ПроверкаПараметровРасчета.ОбъектУчета КАК Справочник.НематериальныеАктивы).Код КАК Код,
		|	ЛОЖЬ НеУказанаАмортизационнаяГруппа,
		|	ПроверкаПараметровРасчета.НеУказанОбъемНаработки КАК НеУказанОбъемНаработки
		|ИЗ
		|	ПроверкаПараметровРасчета_УУ КАК ПроверкаПараметровРасчета
		|ГДЕ
		|	ПроверкаПараметровРасчета.Организация В(&СписокОрганизаций)
		|УПОРЯДОЧИТЬ ПО
		|	ПроверкаПараметровРасчета.ОбъектУчета";
		
	КонецЕсли; 
		
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("СписокОрганизаций", ПараметрыРасчета.СписокОрганизаций);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ШаблонАмортизационнаяГруппа  = НСтр("ru = 'Для объекта учета ""%1"" (код %2) не указана амортизационная группа.';
										|en = 'Depreciation group is not specified for the ""%1"" accounting object (code %2).'");
	ШаблонОбъемНаработки         = НСтр("ru = 'Для объекта учета ""%1"" (код %2) не указан предполагаемый объем наработки (продукции, работ).';
										|en = 'Estimated asset activity value (of products, works) is not specified for the ""%1"" accounting object (code %2).'");
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.НеУказанОбъемНаработки Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонОбъемНаработки, Выборка.ОбъектУчетаПредставление, Выборка.Код);
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРасчета.ЭтапРасчета, 
				ПараметрыРасчета.Период,
				Выборка.Организация,
				ТекстОшибки,,
				Выборка.ОбъектУчета,
				ПараметрыРасчета.ЕстьОшибки);
				
		КонецЕсли; 
		
		АмортизацияНМАЛокализация.ЗарегистрироватьОшибкиРасчетаАмортизации(
			Выборка, ПараметрыРасчета, ШаблонАмортизационнаяГруппа);
	
	КонецЦикла;
	
КонецПроцедуры

#Область ТекстыЗапросовДляРасчетаАмортизации

Функция ТекстЗапросаДляРасчетаАмортизации(ПараметрыРасчета)

	СписокЗапросов = Новый Массив;
	
	// Тексты запросов, в которых подготавливаются параметры для расчета
	ТекстЗапросаПакетыАмортизации(СписокЗапросов);
	ТекстЗапросаСписокВсехНМА(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаСписокОбъектовУчета(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаПараметрыРасчетаАмортизации(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаПроверкаПараметровРасчета(СписокЗапросов, ПараметрыРасчета);
	
	// Тексты запроса, в которых вычисляется амортизация
	ТекстЗапросаРасчетАмортизации_УУ_Предварительный_Доначисление(СписокЗапросов, ПараметрыРасчета);
	АмортизацияНМАЛокализация.ДополнитьТекстЗапросаДляРасчетаАмортизации(ПараметрыРасчета, СписокЗапросов);
	
	// Выборка должна быть последней
	ТекстЗапросаВыборкаРасчетаАмортизацииИзменений_Доначисление(СписокЗапросов, ПараметрыРасчета);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаДляРасчетаАмортизацииСУчетомДоначисления(ПараметрыРасчета)

	СписокЗапросов = Новый Массив;
	
	// Тексты запроса, в которых вычисляется амортизация
	ТекстЗапросаРасчетАмортизации_УУ_Доначисление(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаРасчетАмортизации_УУ_ДоначислениеСгруппированная(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаРасчетАмортизации_УУ_Предварительный(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаВыборкаРасчетаАмортизацииИзменений(СписокЗапросов, ПараметрыРасчета);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаЗавершитьРасчетАмортизации(ПараметрыРасчета)

	СписокЗапросов = Новый Массив;
	
	ТекстЗапросаРасчетАмортизации_УУ(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаНачисленнаяАмортизация(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаКоличествоОбработанныхДанных(СписокЗапросов, ПараметрыРасчета);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаПакетыАмортизации(СписокЗапросов)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПакетыАмортизации.Организация КАК Организация,
	|	ПакетыАмортизации.НомерПакета КАК НомерПакета
	|ПОМЕСТИТЬ НомераПакетов
	|ИЗ
	|	&ПакетыАмортизации КАК ПакетыАмортизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НомераПакетов.Организация КАК Организация,
	|	НомераПакетов.НомерПакета КАК НомерПакета
	|ПОМЕСТИТЬ НомераПакетовСФилиалами
	|ИЗ
	|	НомераПакетов КАК НомераПакетов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Филиалы.Ссылка,
	|	НомераПакетов.НомерПакета
	|ИЗ
	|	НомераПакетов КАК НомераПакетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Филиалы
	|		ПО (Филиалы.ГоловнаяОрганизация = НомераПакетов.Организация)
	|			И (НЕ Филиалы.ПометкаУдаления)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	НомерПакета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПакетыАмортизации.Организация КАК Организация,
	|	ВЫРАЗИТЬ(ПакетыАмортизации.Организация КАК Справочник.Организации).ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ПакетыАмортизации.НематериальныйАктив КАК ОбъектУчета
	|ПОМЕСТИТЬ ПакетыАмортизации
	|ИЗ
	|	НомераПакетовСФилиалами КАК НомераПакетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыАмортизацииНМА КАК ПакетыАмортизации
	|		ПО (ПакетыАмортизации.Организация = НомераПакетов.Организация)
	|			И (ПакетыАмортизации.НомерПакета = НомераПакетов.НомерПакета)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета,
	|	Организация";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаСписокОбъектовУчета(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = АмортизацияНМАЛокализация.ТекстЗапросаСписокОбъектовУчета(ПараметрыРасчета);
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СписокНМА.НачалоМесяца                        КАК НачалоМесяца,
		|	СписокНМА.КонецМесяца                         КАК КонецМесяца,
		|	СписокНМА.Организация                         КАК Организация,
		|	СписокНМА.НематериальныйАктив                 КАК НематериальныйАктив,
		|	СписокНМА.СпособОтраженияРасходовУУ           КАК СпособОтраженияРасходов,
		|	ЕСТЬNULL(ПакетыАмортизацииНМА.НомерПакета, 0) КАК НомерПакета,
		|
		|	(ЕСТЬNULL(УчетнаяПолитика.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) 
		|		= ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) КАК АмортизацияНачисляетсяПоСтандартамМУ
		|
		|ПОМЕСТИТЬ СписокОбъектовУчета
		|
		|ИЗ
		|	СписокОбъектовУчета_УУ КАК СписокНМА
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыАмортизацииНМА КАК ПакетыАмортизацииНМА
		|		ПО ПакетыАмортизацииНМА.Организация = СписокНМА.Организация
		|			И ПакетыАмортизацииНМА.НематериальныйАктив = СписокНМА.НематериальныйАктив
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(
		|			&КонецМесяца,
		|			Организация В (ВЫБРАТЬ СписокНМА.ГоловнаяОрганизация ИЗ СписокОбъектовУчета_УУ КАК СписокНМА)) КАК УчетнаяПолитика
		|		ПО УчетнаяПолитика.Организация = СписокНМА.ГоловнаяОрганизация
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НематериальныйАктив,
		|	Организация";
		
	КонецЕсли; 
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаПараметрыРасчетаАмортизации(СписокЗапросов, ПараметрыРасчета)

	ОбъектУчета = "НематериальныйАктив";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПараметрыРасчетаАмортизации(
		"МестоУчетаНМА", "", ОбъектУчета, СписокЗапросов);
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПараметрыРасчетаАмортизации(
		"ПервоначальныеСведенияНМА", "", ОбъектУчета, СписокЗапросов);
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПараметрыРасчетаАмортизации(
		"ПорядокУчетаНМА", "", ОбъектУчета, СписокЗапросов);
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПараметрыРасчетаАмортизации(
		"ПараметрыАмортизацииНМАУУ", "", ОбъектУчета, СписокЗапросов);
	
	#Область СтоимостьНМАНаНачалоМесяца
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&НачалоМесяца                             КАК НачалоМесяца,
	|	СтоимостьНМА.НематериальныйАктив          КАК НематериальныйАктив,
	|	СтоимостьНМА.СтоимостьОстаток             КАК Стоимость,
	|	СтоимостьНМА.СтоимостьРеглОстаток         КАК СтоимостьРегл,
	|	СтоимостьНМА.СтоимостьНУОстаток           КАК СтоимостьНУ,
	|	СтоимостьНМА.СтоимостьПРОстаток           КАК СтоимостьПР,
	|	СтоимостьНМА.СтоимостьВРОстаток           КАК СтоимостьВР,
	|	СтоимостьНМА.СтоимостьЦФОстаток           КАК СтоимостьЦФ,
	|	СтоимостьНМА.СтоимостьНУЦФОстаток         КАК СтоимостьНУЦФ,
	|	СтоимостьНМА.СтоимостьПРЦФОстаток         КАК СтоимостьПРЦФ,
	|	СтоимостьНМА.СтоимостьВРЦФОстаток         КАК СтоимостьВРЦФ
	|ПОМЕСТИТЬ СтоимостьНМА_НаНачалоМесяца
	|ИЗ
	|	РегистрНакопления.СтоимостьНМА.Остатки(
	|			&КонецПредыдущегоМесяцаГраница,
	|			(Организация, НематериальныйАктив) В
	|				(ВЫБРАТЬ
	|					СписокНМА.Организация,
	|					СписокНМА.НематериальныйАктив
	|				ИЗ
	|					СписокОбъектовУчета КАК СписокНМА)) КАК СтоимостьНМА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СтоимостьНМА.НематериальныйАктив";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
		
	#Область ПредварительнаяСтоимостьНМА
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&НачалоМесяца                                     КАК НачалоМесяца,
	|	СтоимостьНМА.НематериальныйАктив                  КАК НематериальныйАктив,
	|	СУММА(СтоимостьНМА.ПредварительнаяСтоимость)      КАК Стоимость,
	|	СУММА(СтоимостьНМА.ПредварительнаяСтоимостьРегл)  КАК СтоимостьРегл
	|ПОМЕСТИТЬ ПредварительнаяСтоимостьНМА
	|ИЗ
	|	РегистрНакопления.СтоимостьНМА КАК СтоимостьНМА
	|ГДЕ
	|	СтоимостьНМА.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
	|
	|	И (СтоимостьНМА.ПредварительнаяСтоимость <> 0
	|		ИЛИ СтоимостьНМА.ПредварительнаяСтоимостьРегл <> 0)
	|
	|	И СтоимостьНМА.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуНМА),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуНМАпоИнвентаризации))
	|
	|	И СтоимостьНМА.НематериальныйАктив В
	|			(ВЫБРАТЬ
	|				СписокНМА.НематериальныйАктив
	|			ИЗ
	|				СписокОбъектовУчета КАК СписокНМА)
	|
	|СГРУППИРОВАТЬ ПО
	|	СтоимостьНМА.НематериальныйАктив	
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&НачалоПредыдущегоМесяца              КАК НачалоМесяца,
	|	СтоимостьНМА.НематериальныйАктив      КАК НематериальныйАктив,
	|	ВЫБОР
	|		КОГДА СУММА(СтоимостьНМА.Стоимость - СтоимостьНМА.ПредварительнаяСтоимость) > 0
	|			ТОГДА СУММА(СтоимостьНМА.Стоимость - СтоимостьНМА.ПредварительнаяСтоимость)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Стоимость,
	|	ВЫБОР
	|		КОГДА СУММА(СтоимостьНМА.СтоимостьРегл - СтоимостьНМА.ПредварительнаяСтоимостьРегл) > 0
	|			ТОГДА СУММА(СтоимостьНМА.СтоимостьРегл - СтоимостьНМА.ПредварительнаяСтоимостьРегл)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьРегл
	|ИЗ
	|	РегистрНакопления.СтоимостьНМА КАК СтоимостьНМА
	|ГДЕ
	|	СтоимостьНМА.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
	|
	|	И СтоимостьНМА.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуНМА),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуНМАпоИнвентаризации))
	|
	|	И СтоимостьНМА.НематериальныйАктив В
	|			(ВЫБРАТЬ
	|				СписокНМА.НематериальныйАктив
	|			ИЗ
	|				СписокОбъектовУчета_Доначисление_УУ КАК СписокНМА)
	|
	|СГРУППИРОВАТЬ ПО
	|	СтоимостьНМА.НематериальныйАктив
	|
	|ИМЕЮЩИЕ 
	|	СУММА(СтоимостьНМА.Стоимость - СтоимостьНМА.ПредварительнаяСтоимость) > 0
	|		ИЛИ СУММА(СтоимостьНМА.СтоимостьРегл - СтоимостьНМА.ПредварительнаяСтоимостьРегл) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив";
		
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область АмортизацияНМАНаНачалоМесяца
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&НачалоМесяца                           КАК НачалоМесяца,
	|	АмортизацияНМА.Организация              КАК Организация,
	|	АмортизацияНМА.НематериальныйАктив      КАК НематериальныйАктив,
	|	-АмортизацияНМА.АмортизацияОстаток      КАК Амортизация,
	|	-АмортизацияНМА.АмортизацияРеглОстаток  КАК АмортизацияРегл,
	|	-АмортизацияНМА.АмортизацияНУОстаток    КАК АмортизацияНУ,
	|	-АмортизацияНМА.АмортизацияПРОстаток    КАК АмортизацияПР,
	|	-АмортизацияНМА.АмортизацияВРОстаток    КАК АмортизацияВР,
	|	-АмортизацияНМА.АмортизацияЦФОстаток    КАК АмортизацияЦФ,
	|	-АмортизацияНМА.АмортизацияНУЦФОстаток  КАК АмортизацияНУЦФ,
	|	-АмортизацияНМА.АмортизацияПРЦФОстаток  КАК АмортизацияПРЦФ,
	|	-АмортизацияНМА.АмортизацияВРЦФОстаток  КАК АмортизацияВРЦФ
	|ПОМЕСТИТЬ АмортизацияНМА_НаНачалоМесяца
	|ИЗ
	|	РегистрНакопления.АмортизацияНМА.Остатки(
	|			&КонецПредыдущегоМесяцаГраница,
	|			(Организация, НематериальныйАктив) В
	|				(ВЫБРАТЬ
	|					СписокНМА.Организация,
	|					СписокНМА.НематериальныйАктив
	|				ИЗ
	|					СписокОбъектовУчета КАК СписокНМА)) КАК АмортизацияНМА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АмортизацияНМА.НематериальныйАктив,
	|	АмортизацияНМА.Организация";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
				
	#Область ОбесценениеВНА
	// В расчете амортизации и корректировки обесценения участвуют только те ВНА,
	// по которым ЕГДС на конец месяца расчета амортизации не изменилась.
	// По объектам ВНА, изменившим ЕГДС, происходит восстановление остатка обесценения,
	// которое производится документом, изменившим ЕГДС, и рассчитывается это после 
	// выполнения операции по расчету амортизации.
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СоставЕГД_НаКонецМесяцаРасчета.ВнеоборотныйАктив КАК ВнеоборотныйАктив,
	|	СоставЕГД_НаКонецМесяцаРасчета.ЕГДС КАК ЕГДСНаКонецМесяца
	|ПОМЕСТИТЬ ЕГДСВНАНаКонецМесяцаРасчета
	|ИЗ
	|	РегистрСведений.СоставЕГДС.СрезПоследних(КОНЕЦПЕРИОДА(&НачалоМесяца, МЕСЯЦ), 
	|											 (Организация, ВнеоборотныйАктив) В
	|												(ВЫБРАТЬ
	|													СписокНМА.Организация,
	|													СписокНМА.НематериальныйАктив
	|												ИЗ
	|													СписокОбъектовУчета КАК СписокНМА)
	|											) КАК СоставЕГД_НаКонецМесяцаРасчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВнеоборотныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&НачалоМесяца КАК НачалоМесяца,
	|	ОбесценениеВНА.ВнеоборотныйАктив КАК НематериальныйАктив,
	|	-СУММА(ОбесценениеВНА.ОбесценениеРеглКонечныйОстаток) КАК ОстатокОбесцененияРегл,
	|	-СУММА(ОбесценениеВНА.ОбесценениеУпрКонечныйОстаток) КАК ОстатокОбесцененияУпр,
	|	МАКСИМУМ(ОбесценениеВНА.ЕГДС) КАК ЕГДС,
	|	МАКСИМУМ(ЕСТЬNULL(ЕГДСВНАНаКонецМесяцаРасчета.ЕГДСНаКонецМесяца,
	|		ЗНАЧЕНИЕ(Справочник.ЕдиницыГенерирующиеДенежныеСредства.ПустаяСсылка))) КАК ЕГДСНаКонецМесяца
	|ПОМЕСТИТЬ ОбесценениеВНА_СИзменившимиЕГДС
	|ИЗ
	|	РегистрНакопления.ОбесценениеВНА.ОстаткиИОбороты(&КонецПредыдущегоМесяцаГраница,
	|													 &КонецПредыдущегоМесяцаГраница,,
	|													 ДвиженияИГраницыПериода,
	|													 (Организация, ВнеоборотныйАктив) В
	|														(ВЫБРАТЬ
	|															СписокНМА.Организация,
	|															СписокНМА.НематериальныйАктив
	|														ИЗ
	|															СписокОбъектовУчета КАК СписокНМА)
	|													) КАК ОбесценениеВНА
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЕГДСВНАНаКонецМесяцаРасчета КАК ЕГДСВНАНаКонецМесяцаРасчета
	|		ПО ОбесценениеВНА.ВнеоборотныйАктив = ЕГДСВНАНаКонецМесяцаРасчета.ВнеоборотныйАктив
	|СГРУППИРОВАТЬ ПО
	|	ОбесценениеВНА.ВнеоборотныйАктив
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив,
	|	ЕГДСНаКонецМесяца,
	|	ЕГДС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбесценениеВНА_СИзменившимиЕГДС.НачалоМесяца КАК НачалоМесяца,
	|	ОбесценениеВНА_СИзменившимиЕГДС.НематериальныйАктив КАК НематериальныйАктив,
	|	ОбесценениеВНА_СИзменившимиЕГДС.ОстатокОбесцененияРегл КАК ОстатокОбесцененияРегл,
	|	ОбесценениеВНА_СИзменившимиЕГДС.ОстатокОбесцененияУпр КАК ОстатокОбесцененияУпр,
	|	ОбесценениеВНА_СИзменившимиЕГДС.ЕГДС КАК ЕГДС
	|ПОМЕСТИТЬ ОбесценениеВНА
	|ИЗ
	|	ОбесценениеВНА_СИзменившимиЕГДС КАК ОбесценениеВНА_СИзменившимиЕГДС
	|ГДЕ
	|	ОбесценениеВНА_СИзменившимиЕГДС.ЕГДСНаКонецМесяца = ОбесценениеВНА_СИзменившимиЕГДС.ЕГДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ОбесценениеВНА_СИзменившимиЕГДС";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
				
	#Область ВыработкаНМА
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокНМА.Организация КАК Организация,
	|	СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&НачалоМесяца, МЕСЯЦ, -1), МЕСЯЦ) = НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуБУ, МЕСЯЦ)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПринятоКУчетуВПрошломМесяце
	|ПОМЕСТИТЬ СписокНМА_Выработка
	|ИЗ
	|	СписокОбъектовУчета КАК СписокНМА
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияНМА КАК ПервоначальныеСведенияНМА
	|		ПО (ПервоначальныеСведенияНМА.Организация = СписокНМА.Организация)
	|			И (ПервоначальныеСведенияНМА.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (ПервоначальныеСведенияНМА.НачалоМесяца = СписокНМА.НачалоМесяца)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаНМА КАК ПорядокУчетаНМА
	|		ПО (ПорядокУчетаНМА.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (ПорядокУчетаНМА.НачалоМесяца = СписокНМА.НачалоМесяца)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииНМАУУ КАК ПараметрыАмортизацииНМАУУ
	|		ПО (ПараметрыАмортизацииНМАУУ.Организация = СписокНМА.Организация)
	|			И (ПараметрыАмортизацииНМАУУ.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (ПараметрыАмортизацииНМАУУ.НачалоМесяца = СписокНМА.НачалоМесяца)
	|ГДЕ
	|	ПервоначальныеСведенияНМА.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции)
	|		ИЛИ ПараметрыАмортизацииНМАУУ.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции) 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыработкаНМА.НематериальныйАктив  КАК НематериальныйАктив,
	|	ВыработкаНМА.КоличествоОборот     КАК ОбъемВыработки
	|ПОМЕСТИТЬ ВыработкаНМА
	|ИЗ
	|	РегистрНакопления.ВыработкаНМА.Обороты(
	|			&НачалоМесяца,
	|			&КонецМесяца,,
	|			НематериальныйАктив В
	|				(ВЫБРАТЬ
	|					СписокНМА.НематериальныйАктив
	|				ИЗ
	|					СписокНМА_Выработка КАК СписокНМА)) КАК ВыработкаНМА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив";
	
	Если НЕ ОбщегоНазначенияКлиентСервер.РежимОтладки() Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СписокНМА_Выработка";
	КонецЕсли;
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область Изменения_УУ
	
	#Область ИзменениеПараметров
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НачалоМесяца КАК Период,
	|	ПакетыАмортизации.Организация КАК Организация
	|ПОМЕСТИТЬ ОрганизацииПоСтандартамМУ
	|ИЗ
	|	ПакетыАмортизации КАК ПакетыАмортизации
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&НачалоМесяца) КАК УчетнаяПолитикаФинансовогоУчета
	|		ПО УчетнаяПолитикаФинансовогоУчета.Организация = ПакетыАмортизации.ГоловнаяОрганизация
	|ГДЕ
	|	ЕСТЬNULL(УчетнаяПолитикаФинансовогоУчета.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НачалоПредыдущегоМесяца КАК Период,
	|	ПакетыАмортизации.Организация КАК Организация
	|ИЗ
	|	ПакетыАмортизации КАК ПакетыАмортизации
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&НачалоПредыдущегоМесяца) КАК УчетнаяПолитикаФинансовогоУчета
	|		ПО УчетнаяПолитикаФинансовогоУчета.Организация = ПакетыАмортизации.ГоловнаяОрганизация
	|ГДЕ
	|	ЕСТЬNULL(УчетнаяПолитикаФинансовогоУчета.ПорядокУчетаВНА, ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)) = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНА.ПоСтандартамМУ)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НачалоМесяца КАК НачалоМесяца,
	|	НАЧАЛОПЕРИОДА(ПараметрыАмортизацииНМАУУ.Период, ДЕНЬ) КАК Период,
	|	ПараметрыАмортизацииНМАУУ.Регистратор,
	|	ПараметрыАмортизацииНМАУУ.НематериальныйАктив,
	|	ПараметрыАмортизацииНМАУУ.Организация,
	|	ПараметрыАмортизацииНМАУУ.СрокИспользования КАК СрокИспользования,
	|	ПараметрыАмортизацииНМАУУ.КоэффициентУскорения КАК КоэффициентУскорения,
	|	ПараметрыАмортизацииНМАУУ.МетодНачисленияАмортизации,
	|	ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимость,
	|	ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимостьРегл
	|ПОМЕСТИТЬ ПараметрыАмортизацииНМАУУИзменения
	|ИЗ
	|	РегистрСведений.ПараметрыАмортизацииНМАУУ КАК ПараметрыАмортизацииНМАУУ
	|ГДЕ
	|	ПараметрыАмортизацииНМАУУ.Активность
	|	И ПараметрыАмортизацииНМАУУ.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
	|	И НЕ ПараметрыАмортизацииНМАУУ.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА2_4
	|	И НЕ ПараметрыАмортизацииНМАУУ.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
	|	И ПараметрыАмортизацииНМАУУ.НематериальныйАктив В
	|			(ВЫБРАТЬ
	|				СписокНМА.ОбъектУчета
	|			ИЗ
	|				ПакетыАмортизации КАК СписокНМА)
	|
	|	И ПараметрыАмортизацииНМАУУ.Организация В
	|			(ВЫБРАТЬ
	|				ОрганизацииПоСтандартамМУ.Организация
	|			ИЗ
	|				ОрганизацииПоСтандартамМУ КАК ОрганизацииПоСтандартамМУ
	|			ГДЕ
	|				ОрганизацииПоСтандартамМУ.Период = &НачалоМесяца)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НачалоПредыдущегоМесяца КАК НачалоМесяца,
	|	НАЧАЛОПЕРИОДА(ПараметрыАмортизацииНМАУУ.Период, ДЕНЬ) КАК Период,
	|	ПараметрыАмортизацииНМАУУ.Регистратор,
	|	ПараметрыАмортизацииНМАУУ.НематериальныйАктив,
	|	ПараметрыАмортизацииНМАУУ.Организация,
	|	ПараметрыАмортизацииНМАУУ.СрокИспользования КАК СрокИспользования,
	|	ПараметрыАмортизацииНМАУУ.КоэффициентУскорения КАК КоэффициентУскорения,
	|	ПараметрыАмортизацииНМАУУ.МетодНачисленияАмортизации,
	|	ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимость,
	|	ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимостьРегл
	|ИЗ
	|	РегистрСведений.ПараметрыАмортизацииНМАУУ КАК ПараметрыАмортизацииНМАУУ
	|ГДЕ
	|	ПараметрыАмортизацииНМАУУ.Активность
	|	И ПараметрыАмортизацииНМАУУ.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
	|	И НЕ ПараметрыАмортизацииНМАУУ.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА2_4
	|	И НЕ ПараметрыАмортизацииНМАУУ.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
	|	И ПараметрыАмортизацииНМАУУ.НематериальныйАктив В
	|			(ВЫБРАТЬ
	|				СписокНМА.НематериальныйАктив
	|			ИЗ
	|				СписокОбъектовУчета_Доначисление_УУ КАК СписокНМА)
	|
	|	И ПараметрыАмортизацииНМАУУ.Организация В
	|			(ВЫБРАТЬ
	|				ОрганизацииПоСтандартамМУ.Организация
	|			ИЗ
	|				ОрганизацииПоСтандартамМУ КАК ОрганизацииПоСтандартамМУ
	|			ГДЕ
	|				ОрганизацииПоСтандартамМУ.Период = &НачалоПредыдущегоМесяца)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НачалоМесяца КАК НачалоМесяца,
	|	НАЧАЛОПЕРИОДА(ПорядокУчетаНМА.Период, ДЕНЬ) КАК Период,
	|	ПорядокУчетаНМА.Регистратор,
	|	ПорядокУчетаНМА.НематериальныйАктив,
	|	ПорядокУчетаНМА.Организация,
	|	ПорядокУчетаНМА.ГруппаФинансовогоУчета,
	|	ПорядокУчетаНМА.НаправлениеДеятельности,
	|	ПорядокУчетаНМА.ОбъемНаработки
	|ПОМЕСТИТЬ ПорядокУчетаНМАИзменения
	|ИЗ
	|	РегистрСведений.ПорядокУчетаНМА КАК ПорядокУчетаНМА
	|ГДЕ
	|	ПорядокУчетаНМА.Активность
	|	И ПорядокУчетаНМА.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
	|	И НЕ ПорядокУчетаНМА.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА2_4
	|	И НЕ ПорядокУчетаНМА.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
	|	И ПорядокУчетаНМА.НематериальныйАктив В
	|			(ВЫБРАТЬ
	|				СписокНМА.ОбъектУчета
	|			ИЗ
	|				ПакетыАмортизации КАК СписокНМА)
	|
	|	И ПорядокУчетаНМА.Организация В
	|			(ВЫБРАТЬ
	|				ОрганизацииПоСтандартамМУ.Организация
	|			ИЗ
	|				ОрганизацииПоСтандартамМУ КАК ОрганизацииПоСтандартамМУ
	|			ГДЕ
	|				ОрганизацииПоСтандартамМУ.Период = &НачалоМесяца)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НачалоПредыдущегоМесяца КАК НачалоМесяца,
	|	НАЧАЛОПЕРИОДА(ПорядокУчетаНМА.Период, ДЕНЬ) КАК Период,
	|	ПорядокУчетаНМА.Регистратор,
	|	ПорядокУчетаНМА.НематериальныйАктив,
	|	ПорядокУчетаНМА.Организация,
	|	ПорядокУчетаНМА.ГруппаФинансовогоУчета,
	|	ПорядокУчетаНМА.НаправлениеДеятельности,
	|	ПорядокУчетаНМА.ОбъемНаработки
	|ИЗ
	|	РегистрСведений.ПорядокУчетаНМА КАК ПорядокУчетаНМА
	|ГДЕ
	|	ПорядокУчетаНМА.Активность
	|	И ПорядокУчетаНМА.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
	|	И НЕ ПорядокУчетаНМА.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА2_4
	|	И НЕ ПорядокУчетаНМА.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
	|	И ПорядокУчетаНМА.НематериальныйАктив В
	|			(ВЫБРАТЬ
	|				СписокНМА.НематериальныйАктив
	|			ИЗ
	|				СписокОбъектовУчета_Доначисление_УУ КАК СписокНМА)
	|
	|	И ПорядокУчетаНМА.Организация В
	|			(ВЫБРАТЬ
	|				ОрганизацииПоСтандартамМУ.Организация
	|			ИЗ
	|				ОрганизацииПоСтандартамМУ КАК ОрганизацииПоСтандартамМУ
	|			ГДЕ
	|				ОрганизацииПоСтандартамМУ.Период = &НачалоПредыдущегоМесяца)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НачалоМесяца КАК НачалоМесяца,
	|	НАЧАЛОПЕРИОДА(ПорядокУчетаНМАУУ.Период, ДЕНЬ) КАК Период,
	|	ПорядокУчетаНМАУУ.Регистратор,
	|	ПорядокУчетаНМАУУ.НематериальныйАктив,
	|	ПорядокУчетаНМАУУ.Организация,
	|	ПорядокУчетаНМАУУ.Состояние,
	|	ПорядокУчетаНМАУУ.НачислятьАмортизациюУУ,
	|	ПорядокУчетаНМАУУ.СпособОтраженияРасходов,
	|	ПорядокУчетаНМАУУ.СтатьяРасходов,
	|	ПорядокУчетаНМАУУ.АналитикаРасходов,
	|	ПорядокУчетаНМАУУ.ПередаватьРасходыВДругуюОрганизацию,
	|	ПорядокУчетаНМАУУ.ОрганизацияПолучательРасходов
	|ПОМЕСТИТЬ ПорядокУчетаНМАУУИзменения
	|ИЗ
	|	РегистрСведений.ПорядокУчетаНМАУУ КАК ПорядокУчетаНМАУУ
	|ГДЕ
	|	ПорядокУчетаНМАУУ.Активность
	|	И ПорядокУчетаНМАУУ.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
	|	И НЕ ПорядокУчетаНМАУУ.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА2_4
	|	И НЕ ПорядокУчетаНМАУУ.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
	|	И НЕ ПорядокУчетаНМАУУ.Регистратор ССЫЛКА Документ.АмортизацияНМА2_4
	|	И (ПорядокУчетаНМАУУ.Организация, ПорядокУчетаНМАУУ.НематериальныйАктив) В
	|			(ВЫБРАТЬ
	|				СписокНМА.Организация,
	|				СписокНМА.ОбъектУчета
	|			ИЗ
	|				ПакетыАмортизации КАК СписокНМА)
	|
	|	И ПорядокУчетаНМАУУ.Организация В
	|			(ВЫБРАТЬ
	|				ОрганизацииПоСтандартамМУ.Организация
	|			ИЗ
	|				ОрганизацииПоСтандартамМУ КАК ОрганизацииПоСтандартамМУ
	|			ГДЕ
	|				ОрганизацииПоСтандартамМУ.Период = &НачалоМесяца)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НачалоПредыдущегоМесяца КАК НачалоМесяца,
	|	НАЧАЛОПЕРИОДА(ПорядокУчетаНМАУУ.Период, ДЕНЬ) КАК Период,
	|	ПорядокУчетаНМАУУ.Регистратор,
	|	ПорядокУчетаНМАУУ.НематериальныйАктив,
	|	ПорядокУчетаНМАУУ.Организация,
	|	ПорядокУчетаНМАУУ.Состояние,
	|	ПорядокУчетаНМАУУ.НачислятьАмортизациюУУ,
	|	ПорядокУчетаНМАУУ.СпособОтраженияРасходов,
	|	ПорядокУчетаНМАУУ.СтатьяРасходов,
	|	ПорядокУчетаНМАУУ.АналитикаРасходов,
	|	ПорядокУчетаНМАУУ.ПередаватьРасходыВДругуюОрганизацию,
	|	ПорядокУчетаНМАУУ.ОрганизацияПолучательРасходов
	|ИЗ
	|	РегистрСведений.ПорядокУчетаНМАУУ КАК ПорядокУчетаНМАУУ
	|ГДЕ
	|	ПорядокУчетаНМАУУ.Активность
	|	И ПорядокУчетаНМАУУ.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
	|	И НЕ ПорядокУчетаНМАУУ.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА2_4
	|	И НЕ ПорядокУчетаНМАУУ.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
	|	И НЕ ПорядокУчетаНМАУУ.Регистратор ССЫЛКА Документ.АмортизацияНМА2_4
	|	И (ПорядокУчетаНМАУУ.Организация, ПорядокУчетаНМАУУ.НематериальныйАктив) В
	|			(ВЫБРАТЬ
	|				СписокНМА.Организация,
	|				СписокНМА.НематериальныйАктив
	|			ИЗ
	|				СписокОбъектовУчета_Доначисление_УУ КАК СписокНМА)
	|
	|	И ПорядокУчетаНМАУУ.Организация В
	|			(ВЫБРАТЬ
	|				ОрганизацииПоСтандартамМУ.Организация
	|			ИЗ
	|				ОрганизацииПоСтандартамМУ КАК ОрганизацииПоСтандартамМУ
	|			ГДЕ
	|				ОрганизацииПоСтандартамМУ.Период = &НачалоПредыдущегоМесяца)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НачалоМесяца КАК НачалоМесяца,
	|	НАЧАЛОПЕРИОДА(МестоУчетаНМА.Период, ДЕНЬ) КАК Период,
	|	МестоУчетаНМА.Регистратор,
	|	МестоУчетаНМА.НематериальныйАктив,
	|	МестоУчетаНМА.Организация,
	|	МестоУчетаНМА.Подразделение
	|ПОМЕСТИТЬ МестоУчетаНМАИзменения
	|ИЗ
	|	РегистрСведений.МестоУчетаНМА КАК МестоУчетаНМА
	|ГДЕ
	|	МестоУчетаНМА.Активность
	|	И МестоУчетаНМА.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
	|	И НЕ МестоУчетаНМА.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА2_4
	|	И НЕ МестоУчетаНМА.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
	|	И МестоУчетаНМА.НематериальныйАктив В
	|			(ВЫБРАТЬ
	|				СписокНМА.ОбъектУчета
	|			ИЗ
	|				ПакетыАмортизации КАК СписокНМА)
	|
	|	И МестоУчетаНМА.Организация В
	|			(ВЫБРАТЬ
	|				ОрганизацииПоСтандартамМУ.Организация
	|			ИЗ
	|				ОрганизацииПоСтандартамМУ КАК ОрганизацииПоСтандартамМУ
	|			ГДЕ
	|				ОрганизацииПоСтандартамМУ.Период = &НачалоМесяца)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&НачалоПредыдущегоМесяца КАК НачалоМесяца,
	|	НАЧАЛОПЕРИОДА(МестоУчетаНМА.Период, ДЕНЬ) КАК Период,
	|	МестоУчетаНМА.Регистратор,
	|	МестоУчетаНМА.НематериальныйАктив,
	|	МестоУчетаНМА.Организация,
	|	МестоУчетаНМА.Подразделение
	|ИЗ
	|	РегистрСведений.МестоУчетаНМА КАК МестоУчетаНМА
	|ГДЕ
	|	МестоУчетаНМА.Активность
	|	И МестоУчетаНМА.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
	|	И НЕ МестоУчетаНМА.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА2_4
	|	И НЕ МестоУчетаНМА.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
	|	И МестоУчетаНМА.НематериальныйАктив В
	|			(ВЫБРАТЬ
	|				СписокНМА.НематериальныйАктив
	|			ИЗ
	|				СписокОбъектовУчета_Доначисление_УУ КАК СписокНМА)
	|
	|	И МестоУчетаНМА.Организация В
	|			(ВЫБРАТЬ
	|				ОрганизацииПоСтандартамМУ.Организация
	|			ИЗ
	|				ОрганизацииПоСтандартамМУ КАК ОрганизацииПоСтандартамМУ
	|			ГДЕ
	|				ОрганизацииПоСтандартамМУ.Период = &НачалоПредыдущегоМесяца)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ИзменениеСтоимости
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СтоимостьНМА.НачалоМесяца КАК НачалоМесяца,
	|	СтоимостьНМА.Период КАК Период,
	|	СтоимостьНМА.Регистратор КАК Регистратор,
	|	СтоимостьНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	СтоимостьНМА.Организация КАК Организация,
	|	СУММА(СтоимостьНМА.Стоимость) КАК Стоимость,
	|	СУММА(СтоимостьНМА.СтоимостьРегл) КАК СтоимостьРегл
	|ПОМЕСТИТЬ СтоимостьНМАИзменения
	|ИЗ
	|	(ВЫБРАТЬ
	|		&НачалоМесяца КАК НачалоМесяца,
	|		СписаниеНМА2_4.Дата КАК Период,
	|		СписаниеНМА2_4.Ссылка КАК Регистратор,
	|		ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|		СписаниеНМА2_4.Организация КАК Организация,
	|		-ТаблицаНМА.СуммаСписанияУУ КАК Стоимость,
	|		-ТаблицаНМА.СуммаСписанияБУ КАК СтоимостьРегл
	|	ИЗ
	|		Документ.СписаниеНМА2_4 КАК СписаниеНМА2_4
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеНМА2_4.НМА КАК ТаблицаНМА
	|			ПО (ТаблицаНМА.Ссылка = СписаниеНМА2_4.Ссылка)
	|	ГДЕ
	|		СписаниеНМА2_4.Проведен
	|		И СписаниеНМА2_4.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
	|		И СписаниеНМА2_4.ОтражатьВУпрУчете
	|		И СписаниеНМА2_4.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНМАЧастичное)
	|		И ТаблицаНМА.СуммаСписанияУУ <> 0
	|		И (СписаниеНМА2_4.Организация, ТаблицаНМА.НематериальныйАктив) В
	|				(ВЫБРАТЬ
	|					СписокНМА.Организация,
	|					СписокНМА.ОбъектУчета
	|				ИЗ
	|					ПакетыАмортизации КАК СписокНМА)
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		&НачалоМесяца,
	|		ПереоценкаНМА.Дата,
	|		ПереоценкаНМА.Ссылка,
	|		ТаблицаНМА.НематериальныйАктив,
	|		ПереоценкаНМА.Организация,
	|		ТаблицаНМА.СтоимостьУУ,
	|		ТаблицаНМА.СтоимостьБУ
	|	ИЗ
	|		Документ.ПереоценкаНМА2_4 КАК ПереоценкаНМА
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПереоценкаНМА2_4.НМА КАК ТаблицаНМА
	|			ПО (ТаблицаНМА.Ссылка = ПереоценкаНМА.Ссылка)
	|	ГДЕ
	|		ПереоценкаНМА.Проведен
	|		И ПереоценкаНМА.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
	|		И ПереоценкаНМА.ОтражатьВУпрУчете
	|		И ТаблицаНМА.СтоимостьУУ <> 0
	|		И (ПереоценкаНМА.Организация, ТаблицаНМА.НематериальныйАктив) В
	|				(ВЫБРАТЬ
	|					СписокНМА.Организация,
	|					СписокНМА.ОбъектУчета
	|				ИЗ
	|					ПакетыАмортизации КАК СписокНМА)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		&НачалоМесяца,
	|		Корректировка.Дата,
	|		Корректировка.Ссылка,
	|		ТаблицаНМА.НематериальныйАктив,
	|		Корректировка.Организация,
	|		ТаблицаНМА.СтоимостьУУ,
	|		ТаблицаНМА.СтоимостьБУ
	|	ИЗ
	|		Документ.КорректировкаСтоимостиИАмортизацииНМА КАК Корректировка
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаСтоимостиИАмортизацииНМА.НМА КАК ТаблицаНМА
	|			ПО (ТаблицаНМА.Ссылка = Корректировка.Ссылка)
	|	ГДЕ
	|		Корректировка.Проведен
	|		И Корректировка.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
	|		И Корректировка.ОтражатьВУпрУчете
	|		И ТаблицаНМА.СтоимостьУУ <> 0
	|		И (Корректировка.Организация, ТаблицаНМА.НематериальныйАктив) В
	|				(ВЫБРАТЬ
	|					СписокНМА.Организация,
	|					СписокНМА.ОбъектУчета
	|				ИЗ
	|					ПакетыАмортизации КАК СписокНМА)
	|	
	|	) КАК СтоимостьНМА
	|
	|СГРУППИРОВАТЬ ПО
	|	СтоимостьНМА.НачалоМесяца,
	|	СтоимостьНМА.Период,
	|	СтоимостьНМА.Регистратор,
	|	СтоимостьНМА.НематериальныйАктив,
	|	СтоимостьНМА.Организация";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ИзменениеАмортизации
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&НачалоМесяца КАК НачалоМесяца,
	|	Корректировка.Дата КАК Период,
	|	Корректировка.Ссылка КАК Регистратор,
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	Корректировка.Организация КАК Организация,
	|	ТаблицаНМА.АмортизацияУУ КАК Амортизация,
	|	ТаблицаНМА.АмортизацияБУ КАК АмортизацияРегл
	|ПОМЕСТИТЬ АмортизацияНМАИзменения
	|ИЗ
	|	Документ.КорректировкаСтоимостиИАмортизацииНМА КАК Корректировка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаСтоимостиИАмортизацииНМА.НМА КАК ТаблицаНМА
	|		ПО (ТаблицаНМА.Ссылка = Корректировка.Ссылка)
	|ГДЕ
	|	Корректировка.Проведен
	|	И Корректировка.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
	|	И Корректировка.ОтражатьВУпрУчете
	|	И ТаблицаНМА.АмортизацияУУ <> 0
	|	И (Корректировка.Организация, ТаблицаНМА.НематериальныйАктив) В
	|			(ВЫБРАТЬ
	|				СписокНМА.Организация,
	|				СписокНМА.ОбъектУчета
	|			ИЗ
	|				ПакетыАмортизации КАК СписокНМА)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ОбъединениеВсехИзменений
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.НачалоМесяца КАК НачалоМесяца,
	|	ТаблицаИзменений.Регистратор КАК Регистратор,
	|	МАКСИМУМ(ТаблицаИзменений.Период) КАК Период,
	|	МАКСИМУМ(НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ТаблицаИзменений.Период, МЕСЯЦ, 1), МЕСЯЦ)) КАК НачалоСледующегоМесяца,
	|	ТаблицаИзменений.НематериальныйАктив КАК НематериальныйАктив,
	|	ТаблицаИзменений.Организация КАК Организация,
	|	МАКСИМУМ(ПараметрыАмортизацииНМАУУПоследнее.Период) КАК ПараметрыАмортизацииНМАУУПоследнееПериод,
	|	МАКСИМУМ(ПорядокУчетаНМАИзмененияПоследнее.Период) КАК ПорядокУчетаНМАИзмененияПоследнееПериод,
	|	МАКСИМУМ(ПорядокУчетаНМАУУИзмененияПоследнее.Период) КАК ПорядокУчетаНМАУУИзмененияПоследнееПериод,
	|	МАКСИМУМ(МестоУчетаНМАИзмененияПоследнее.Период) КАК МестоУчетаНМАИзмененияПоследнееПериод
	|ПОМЕСТИТЬ ТаблицаИзменений
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаИзменений.НачалоМесяца КАК НачалоМесяца,
	|		ТаблицаИзменений.Регистратор КАК Регистратор,
	|		ТаблицаИзменений.Период КАК Период,
	|		ТаблицаИзменений.НематериальныйАктив КАК НематериальныйАктив,
	|		ТаблицаИзменений.Организация КАК Организация
	|	ИЗ
	|		ПараметрыАмортизацииНМАУУИзменения КАК ТаблицаИзменений
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаИзменений.НачалоМесяца КАК НачалоМесяца,
	|		ТаблицаИзменений.Регистратор КАК Регистратор,
	|		ТаблицаИзменений.Период,
	|		ТаблицаИзменений.НематериальныйАктив,
	|		ТаблицаИзменений.Организация
	|	ИЗ
	|		ПорядокУчетаНМАИзменения КАК ТаблицаИзменений
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаИзменений.НачалоМесяца КАК НачалоМесяца,
	|		ТаблицаИзменений.Регистратор КАК Регистратор,
	|		ТаблицаИзменений.Период,
	|		ТаблицаИзменений.НематериальныйАктив,
	|		ТаблицаИзменений.Организация
	|	ИЗ
	|		ПорядокУчетаНМАУУИзменения КАК ТаблицаИзменений
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаИзменений.НачалоМесяца КАК НачалоМесяца,
	|		ТаблицаИзменений.Регистратор КАК Регистратор,
	|		ТаблицаИзменений.Период,
	|		ТаблицаИзменений.НематериальныйАктив,
	|		ТаблицаИзменений.Организация
	|	ИЗ
	|		МестоУчетаНМАИзменения КАК ТаблицаИзменений
	|
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаИзменений.НачалоМесяца,
	|		ТаблицаИзменений.Регистратор,
	|		ТаблицаИзменений.Период,
	|		ТаблицаИзменений.НематериальныйАктив,
	|		ТаблицаИзменений.Организация
	|	ИЗ
	|		СтоимостьНМАИзменения КАК ТаблицаИзменений
	|
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаИзменений.НачалоМесяца,
	|		ТаблицаИзменений.Регистратор,
	|		ТаблицаИзменений.Период,
	|		ТаблицаИзменений.НематериальныйАктив,
	|		ТаблицаИзменений.Организация
	|	ИЗ
	|		АмортизацияНМАИзменения КАК ТаблицаИзменений
	|
	|	) КАК ТаблицаИзменений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииНМАУУИзменения КАК ПараметрыАмортизацииНМАУУПоследнее
	|		ПО (ПараметрыАмортизацииНМАУУПоследнее.Организация = ТаблицаИзменений.Организация)
	|			И (ПараметрыАмортизацииНМАУУПоследнее.НематериальныйАктив = ТаблицаИзменений.НематериальныйАктив)
	|			И (ПараметрыАмортизацииНМАУУПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
	|			И (ПараметрыАмортизацииНМАУУПоследнее.Период <= ТаблицаИзменений.Период)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаНМАИзменения КАК ПорядокУчетаНМАИзмененияПоследнее
	|		ПО (ПорядокУчетаНМАИзмененияПоследнее.НематериальныйАктив = ТаблицаИзменений.НематериальныйАктив)
	|			И (ПорядокУчетаНМАИзмененияПоследнее.Организация = ТаблицаИзменений.Организация)
	|			И (ПорядокУчетаНМАИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
	|			И (ПорядокУчетаНМАИзмененияПоследнее.Период <= ТаблицаИзменений.Период)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаНМАУУИзменения КАК ПорядокУчетаНМАУУИзмененияПоследнее
	|		ПО (ПорядокУчетаНМАУУИзмененияПоследнее.Организация = ТаблицаИзменений.Организация)
	|			И (ПорядокУчетаНМАУУИзмененияПоследнее.НематериальныйАктив = ТаблицаИзменений.НематериальныйАктив)
	|			И (ПорядокУчетаНМАУУИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
	|			И (ПорядокУчетаНМАУУИзмененияПоследнее.Период <= ТаблицаИзменений.Период)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ МестоУчетаНМАИзменения КАК МестоУчетаНМАИзмененияПоследнее
	|		ПО (МестоУчетаНМАИзмененияПоследнее.Организация = ТаблицаИзменений.Организация)
	|			И (МестоУчетаНМАИзмененияПоследнее.НематериальныйАктив = ТаблицаИзменений.НематериальныйАктив)
	|			И (МестоУчетаНМАИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
	|			И (МестоУчетаНМАИзмененияПоследнее.Период <= ТаблицаИзменений.Период)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.НачалоМесяца,
	|	ТаблицаИзменений.Регистратор,
	|	ТаблицаИзменений.НематериальныйАктив,
	|	ТаблицаИзменений.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.НематериальныйАктив,
	|	ТаблицаИзменений.НачалоМесяца,
	|	ТаблицаИзменений.Регистратор,
	|	МИНИМУМ(ТаблицаИзмененийСледующее.Период) КАК ПериодСледующее
	|ПОМЕСТИТЬ ТаблицаИзмененийСледующее
	|ИЗ
	|	ТаблицаИзменений КАК ТаблицаИзменений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаИзменений КАК ТаблицаИзмененийСледующее
	|		ПО (ТаблицаИзмененийСледующее.Организация = ТаблицаИзменений.Организация)
	|			И (ТаблицаИзмененийСледующее.НематериальныйАктив = ТаблицаИзменений.НематериальныйАктив)
	|			И (ТаблицаИзмененийСледующее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
	|			И (ТаблицаИзмененийСледующее.Период > ТаблицаИзменений.Период
	|				ИЛИ ТаблицаИзмененийСледующее.Период = ТаблицаИзменений.Период
	|						И ТаблицаИзмененийСледующее.Регистратор > ТаблицаИзменений.Регистратор)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.НачалоМесяца,
	|	ТаблицаИзменений.Регистратор,
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.НематериальныйАктив,
	|	МИНИМУМ(ТаблицаИзменений.Период) КАК ПериодСледующее
	|ПОМЕСТИТЬ ТаблицаИзмененийВЭтомМесяце
	|ИЗ
	|	ТаблицаИзменений КАК ТаблицаИзменений
	|ГДЕ
	|	(ТаблицаИзменений.НачалоМесяца = &НачалоМесяца)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.НематериальныйАктив";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область Изменения_УУ
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.НачалоМесяца КАК НачалоМесяца,
	|	ТаблицаИзменений.Регистратор КАК Регистратор,
	|	КОНЕЦПЕРИОДА(ТаблицаИзменений.НачалоМесяца, МЕСЯЦ) КАК КонецМесяца,
	|	КОНЕЦПЕРИОДА(ТаблицаИзменений.Период, ДЕНЬ) КАК ДатаИзменений,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаИзменений.НачалоМесяца = &НачалоПредыдущегоМесяца
	|				И НАЧАЛОПЕРИОДА(ЕСТЬNULL(ТаблицаИзмененийВЭтомМесяце.ПериодСледующее, &КонецМесяца), ДЕНЬ) = &НачалоМесяца
	|			ТОГДА &НачалоМесяца 
	|		КОГДА ТаблицаИзменений.НачалоМесяца = &НачалоПредыдущегоМесяца
	|			ТОГДА ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ТаблицаИзмененийВЭтомМесяце.ПериодСледующее, ДЕНЬ, -1), &КонецМесяца) 
	|		ИНАЧЕ ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ТаблицаИзмененийСледующее.ПериодСледующее, ДЕНЬ, -1), &КонецМесяца) 
	|	КОНЕЦ КАК Период,
	|
	|	РАЗНОСТЬДАТ(ТаблицаИзменений.Период, ЕСТЬNULL(ТаблицаИзмененийСледующее.ПериодСледующее, ТаблицаИзменений.НачалоСледующегоМесяца), ДЕНЬ) КАК СрокДействия,
	|	ТаблицаИзменений.НематериальныйАктив КАК НематериальныйАктив,
	|	ТаблицаИзменений.Организация КАК Организация,
	|	ЕСТЬNULL(СтоимостьНМАИзменения.Стоимость, 0) КАК СтоимостьИзменение, // Изменение стоимости документом
	|	ЕСТЬNULL(СтоимостьНМАИзменения.СтоимостьРегл, 0) КАК СтоимостьРеглИзменение, // Изменение стоимости документом
	|	ЕСТЬNULL(АмортизацияНМАИзменения.Амортизация, 0) КАК АмортизацияИзменение, // Изменение амортизации документом
	|	ЕСТЬNULL(АмортизацияНМАИзменения.АмортизацияРегл, 0) КАК АмортизацияРеглИзменение, // Изменение амортизации документом
	|	ПараметрыАмортизацииНМАУУПоследнее.СрокИспользования,
	|	ПараметрыАмортизацииНМАУУПоследнее.КоэффициентУскорения,
	|	ПараметрыАмортизацииНМАУУПоследнее.МетодНачисленияАмортизации,
	|	ПараметрыАмортизацииНМАУУПоследнее.ЛиквидационнаяСтоимость,
	|	ПараметрыАмортизацииНМАУУПоследнее.ЛиквидационнаяСтоимостьРегл,
	|	ПорядокУчетаНМАИзмененияПоследнее.ГруппаФинансовогоУчета,
	|	ПорядокУчетаНМАИзмененияПоследнее.НаправлениеДеятельности,
	|	ПорядокУчетаНМАИзмененияПоследнее.ОбъемНаработки,
	|	ПорядокУчетаНМАУУИзмененияПоследнее.Состояние,
	|	ПорядокУчетаНМАУУИзмененияПоследнее.НачислятьАмортизациюУУ,
	|	ПорядокУчетаНМАУУИзмененияПоследнее.СпособОтраженияРасходов,
	|	ПорядокУчетаНМАУУИзмененияПоследнее.СтатьяРасходов,
	|	ПорядокУчетаНМАУУИзмененияПоследнее.АналитикаРасходов,
	|	ПорядокУчетаНМАУУИзмененияПоследнее.ПередаватьРасходыВДругуюОрганизацию,
	|	ПорядокУчетаНМАУУИзмененияПоследнее.ОрганизацияПолучательРасходов,
	|	МестоУчетаНМАИзмененияПоследнее.Подразделение
	|ПОМЕСТИТЬ Изменения_УУ
	|ИЗ
	|	ТаблицаИзменений КАК ТаблицаИзменений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаИзмененийСледующее КАК ТаблицаИзмененийСледующее
	|		ПО (ТаблицаИзмененийСледующее.Организация = ТаблицаИзменений.Организация)
	|			И (ТаблицаИзмененийСледующее.НематериальныйАктив = ТаблицаИзменений.НематериальныйАктив)
	|			И (ТаблицаИзмененийСледующее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
	|			И (ТаблицаИзмененийСледующее.Регистратор = ТаблицаИзменений.Регистратор)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаИзмененийВЭтомМесяце КАК ТаблицаИзмененийВЭтомМесяце
	|		ПО (ТаблицаИзмененийВЭтомМесяце.Организация = ТаблицаИзменений.Организация)
	|			И (ТаблицаИзмененийВЭтомМесяце.НематериальныйАктив = ТаблицаИзменений.НематериальныйАктив)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииНМАУУИзменения КАК ПараметрыАмортизацииНМАУУПоследнее
	|		ПО (ПараметрыАмортизацииНМАУУПоследнее.Организация = ТаблицаИзменений.Организация)
	|			И (ПараметрыАмортизацииНМАУУПоследнее.НематериальныйАктив = ТаблицаИзменений.НематериальныйАктив)
	|			И (ПараметрыАмортизацииНМАУУПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
	|			И (ПараметрыАмортизацииНМАУУПоследнее.Период = ТаблицаИзменений.ПараметрыАмортизацииНМАУУПоследнееПериод)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаНМАИзменения КАК ПорядокУчетаНМАИзмененияПоследнее
	|		ПО (ПорядокУчетаНМАИзмененияПоследнее.НематериальныйАктив = ТаблицаИзменений.НематериальныйАктив)
	|			И (ПорядокУчетаНМАИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
	|			И (ПорядокУчетаНМАИзмененияПоследнее.Период = ТаблицаИзменений.ПорядокУчетаНМАИзмененияПоследнееПериод)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаНМАУУИзменения КАК ПорядокУчетаНМАУУИзмененияПоследнее
	|		ПО (ПорядокУчетаНМАУУИзмененияПоследнее.Организация = ТаблицаИзменений.Организация)
	|			И (ПорядокУчетаНМАУУИзмененияПоследнее.НематериальныйАктив = ТаблицаИзменений.НематериальныйАктив)
	|			И (ПорядокУчетаНМАУУИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
	|			И (ПорядокУчетаНМАУУИзмененияПоследнее.Период = ТаблицаИзменений.ПорядокУчетаНМАУУИзмененияПоследнееПериод)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ МестоУчетаНМАИзменения КАК МестоУчетаНМАИзмененияПоследнее
	|		ПО (МестоУчетаНМАИзмененияПоследнее.Организация = ТаблицаИзменений.Организация)
	|			И (МестоУчетаНМАИзмененияПоследнее.НематериальныйАктив = ТаблицаИзменений.НематериальныйАктив)
	|			И (МестоУчетаНМАИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
	|			И (МестоУчетаНМАИзмененияПоследнее.Период = ТаблицаИзменений.МестоУчетаНМАИзмененияПоследнееПериод)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьНМАИзменения КАК СтоимостьНМАИзменения
	|		ПО (СтоимостьНМАИзменения.Организация = ТаблицаИзменений.Организация)
	|			И (СтоимостьНМАИзменения.НематериальныйАктив = ТаблицаИзменений.НематериальныйАктив)
	|			И (СтоимостьНМАИзменения.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
	|			И (СтоимостьНМАИзменения.Регистратор = ТаблицаИзменений.Регистратор)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ АмортизацияНМАИзменения КАК АмортизацияНМАИзменения
	|		ПО (АмортизацияНМАИзменения.Организация = ТаблицаИзменений.Организация)
	|			И (АмортизацияНМАИзменения.НематериальныйАктив = ТаблицаИзменений.НематериальныйАктив)
	|			И (АмортизацияНМАИзменения.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
	|			И (АмортизацияНМАИзменения.Регистратор = ТаблицаИзменений.Регистратор)";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	
	#КонецОбласти
	
	#Область ПериодИзменения_УУ
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Изменения_УУ.НачалоМесяца           КАК НачалоМесяца,
	|	Изменения_УУ.НематериальныйАктив    КАК НематериальныйАктив,
	|	Изменения_УУ.Организация            КАК Организация,
	|	МИНИМУМ(Изменения_УУ.ДатаИзменений) КАК Период
	|ПОМЕСТИТЬ ПериодИзменения_УУ
	|ИЗ
	|	Изменения_УУ КАК Изменения_УУ
	|
	|СГРУППИРОВАТЬ ПО
	|	Изменения_УУ.НачалоМесяца,
	|	Изменения_УУ.НематериальныйАктив,
	|	Изменения_УУ.Организация";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
		
	#Область СписокНМАСПараметрами_УУ
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВЫБОР 
	|		КОГДА ПериодИзмененияВТекущемМесяце.Период ЕСТЬ NULL
	|			ТОГДА СписокНМА.ПериодНачисления
	|		КОГДА НАЧАЛОПЕРИОДА(ПериодИзмененияВТекущемМесяце.Период, ДЕНЬ) = НАЧАЛОПЕРИОДА(ПериодИзмененияВТекущемМесяце.Период, МЕСЯЦ)
	|			ТОГДА ПериодИзмененияВТекущемМесяце.Период
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(ПериодИзмененияВТекущемМесяце.Период, ДЕНЬ, -1)
	|	КОНЕЦ КАК Период,
	|	СписокНМА.ЭтоДоначисление,
	|	СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	СписокНМА.НематериальныйАктив.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|	СписокНМА.Организация,
	|
	|	ПорядокУчетаНМА.ГруппаФинансовогоУчета,
	|	ПорядокУчетаНМА.НаправлениеДеятельности,
	|
	|	СписокНМА.АмортизацияНачисляетсяПоСтандартамМУ КАК АмортизацияНачисляетсяПоСтандартамМУ,
	|	СписокНМА.ВариантПроводокПоОбесценениюВНА КАК ВариантПроводокПоОбесценениюВНА,
	|
	// СтоимостьДляВычисленияАмортизации
	|	ВЫБОР 
	|		КОГДА НЕ СписокНМА.АмортизацияНачисляетсяПоСтандартамМУ
	|			ТОГДА ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьУУ
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ) = СписокНМА.НачалоМесяца
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьНМА.Стоимость, 0)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьНМА.Стоимость, 0) - ЕСТЬNULL(АмортизацияНМА.Амортизация, 0)
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизации,
	|
	// СтоимостьДляВычисленияАмортизацииРегл
	|	ВЫБОР 
	|		КОГДА НЕ СписокНМА.АмортизацияНачисляетсяПоСтандартамМУ
	|			ТОГДА ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ) = СписокНМА.НачалоМесяца
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьНМА.СтоимостьРегл, 0)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияРегл, 0)
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизацииРегл,
	|
	// РесурсНазначенный
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизацииНМАУУ.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции)
	|			ТОГДА ЕСТЬNULL(ПорядокУчетаНМА.ОбъемНаработки, 0)
	|		ИНАЧЕ ЕСТЬNULL(ПараметрыАмортизацииНМАУУ.СрокИспользования, 0)
	|	КОНЕЦ КАК РесурсНазначенный,
	|
	// РесурсВыработанныйЗаТекущийПериод
	|	ВЫРАЗИТЬ(ВЫБОР 
	|		КОГДА ПараметрыАмортизацииНМАУУ.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции)
	|			ТОГДА ВЫБОР 
	|					КОГДА НЕ ПериодИзменения_УУ.Период ЕСТЬ NULL
	|						ТОГДА ВЫБОР 
	|								КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ) = СписокНМА.НачалоМесяца
	|									ТОГДА (РАЗНОСТЬДАТ(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ПериодИзменения_УУ.Период, ДЕНЬ, -1), СписокНМА.КонецМесяца), ДЕНЬ) + 1)
	|									ИНАЧЕ (РАЗНОСТЬДАТ(СписокНМА.НачалоМесяца, ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ПериодИзменения_УУ.Период, ДЕНЬ, -1), СписокНМА.КонецМесяца), ДЕНЬ) + 1)
	|							КОНЕЦ / ДЕНЬ(СписокНМА.КонецМесяца)
	|					ИНАЧЕ 1
	|				КОНЕЦ * ЕСТЬNULL(ВыработкаНМА.ОбъемВыработки, 0)
	
	// Если расчет выполняется в месяце принятия к учету то ДеньПринятия/ВсегоДнейВМесяце.
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ) = СписокНМА.НачалоМесяца
	|			ТОГДА (РАЗНОСТЬДАТ(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ПериодИзменения_УУ.Период, ДЕНЬ, -1), СписокНМА.КонецМесяца), ДЕНЬ) + 1)
	|					/ ДЕНЬ(СписокНМА.КонецМесяца)
	
	// В остальных случаях КоличествоДнейДоПервогоИзмененияПараметров/ВсегоДнейВМесяце
	|		ИНАЧЕ (РАЗНОСТЬДАТ(СписокНМА.НачалоМесяца, ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ПериодИзменения_УУ.Период, ДЕНЬ, -1), СписокНМА.КонецМесяца), ДЕНЬ) + 1)
	|					/ ДЕНЬ(СписокНМА.КонецМесяца)
	|
	|	КОНЕЦ КАК ЧИСЛО(20, 8)) КАК РесурсВыработанныйЗаТекущийПериод,
	|	  
	|	ВЫРАЗИТЬ(ВЫБОР 
	|		КОГДА ПараметрыАмортизацииНМАУУ.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции)
	|			ТОГДА ЕСТЬNULL(ВыработкаНМА.ОбъемВыработки, 0)
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ) = СписокНМА.НачалоМесяца
	|				ИЛИ ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА КОНЕЦПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ) = КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(СписокНМА.НачалоМесяца, МЕСЯЦ, -1), МЕСЯЦ)
	|			ТОГДА (РАЗНОСТЬДАТ(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, КОНЕЦПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ), ДЕНЬ) + 1) / ДЕНЬ(КОНЕЦПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ))
	
	// 		Расшифровка формулы: [кол-во отработанных дней в мес принятия] / [всего дней в мес принятия] + [кол-во месяцев после принятия до начала месяца].
	|		ИНАЧЕ
	|			(РАЗНОСТЬДАТ(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, КОНЕЦПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ), ДЕНЬ) + 1) / ДЕНЬ(КОНЕЦПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ))
	|			+ РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ, 1), МЕСЯЦ), СписокНМА.КонецМесяца, МЕСЯЦ)
	|	КОНЕЦ КАК ЧИСЛО(20, 8)) КАК РесурсВыработанныйЗаПрошлыйПериод,
	|
	|	ВЫБОР
	|		КОГДА ГОД(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ) <> ГОД(СписокНМА.НачалоМесяца) ТОГДА
	|			13 - МЕСЯЦ(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ)
	|				+ 12 * (ГОД(СписокНМА.НачалоМесяца) - Год(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ) - 1)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОтработанныхМесяцевПолныхЛет,
	|
	|	СписокНМА.НачислятьАмортизациюУУ,
	|	СписокНМА.СтатьяРасходовУУ,
	|	СписокНМА.АналитикаРасходовУУ,
	|	СписокНМА.СпособОтраженияРасходовУУ,
	|	СписокНМА.ПередаватьРасходыВДругуюОрганизациюУУ,
	|	СписокНМА.ОрганизацияПолучательРасходовУУ,
	|
	// ОстаточнаяСтоимость
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ) = СписокНМА.НачалоМесяца
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьНМА.Стоимость, 0)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьНМА.Стоимость, 0) - ЕСТЬNULL(АмортизацияНМА.Амортизация, 0)
	|	КОНЕЦ КАК ОстаточнаяСтоимость,
	|
	// ОстаточнаяСтоимостьРегл
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ) = СписокНМА.НачалоМесяца
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьНМА.СтоимостьРегл, 0)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияРегл, 0)
	|	КОНЕЦ КАК ОстаточнаяСтоимостьРегл,
	|
	|	ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьУУ КАК ПервоначальнаяСтоимость,
	|	ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьРегл,
	|	&ФактическийСрокИспользования КАК ФактическийСрокИспользования,
	|
	|	ЕСТЬNULL(ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимость, 0) КАК ЛиквидационнаяСтоимость,
	|	ЕСТЬNULL(ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимостьРегл, 0) КАК ЛиквидационнаяСтоимостьРегл,
	|
	|	0 КАК СтоимостьНаНачалоГода,
	|
	|	ЕСТЬNULL(СтоимостьНМА.Стоимость, 0) КАК Стоимость,
	|	ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0) КАК СтоимостьРегл,
	|	ЕСТЬNULL(АмортизацияНМА.Амортизация, 0) КАК Амортизация,
	|	ЕСТЬNULL(АмортизацияНМА.АмортизацияРегл, 0) КАК АмортизацияРегл,
	|
	|	ЕСТЬNULL(ОбесценениеНаНачалоМесяца.ОстатокОбесцененияРегл, 0) КАК ОстатокОбесцененияРегл,
	|	ЕСТЬNULL(ОбесценениеНаНачалоМесяца.ОстатокОбесцененияУпр, 0)  КАК ОстатокОбесцененияУпр,
	|
	|	МестоУчетаНМА.Подразделение КАК Подразделение,
	|
	|	ЕСТЬNULL(ВыработкаНМА.ОбъемВыработки, 0) КАК ОбъемВыработки,
	|
	|	ПараметрыАмортизацииНМАУУ.СрокИспользования КАК СрокИспользования,
	|	ПараметрыАмортизацииНМАУУ.КоэффициентУскорения КАК КоэффициентУскорения,
	|	ПараметрыАмортизацииНМАУУ.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации
	|
	|ПОМЕСТИТЬ СписокНМАСПараметрами_УУ
	|
	|ИЗ
	|	СписокОбъектовУчета_УУ КАК СписокНМА
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПериодИзменения_УУ КАК ПериодИзменения_УУ
	|		ПО (ПериодИзменения_УУ.Организация = СписокНМА.Организация)
	|			И (ПериодИзменения_УУ.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (ПериодИзменения_УУ.НачалоМесяца = СписокНМА.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПериодИзменения_УУ КАК ПериодИзмененияВТекущемМесяце
	|		ПО (ПериодИзмененияВТекущемМесяце.Организация = СписокНМА.Организация)
	|			И (ПериодИзмененияВТекущемМесяце.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (ПериодИзмененияВТекущемМесяце.НачалоМесяца = &НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаНМА КАК ПорядокУчетаНМА
	|		ПО (ПорядокУчетаНМА.Организация = СписокНМА.Организация)
	|			И (ПорядокУчетаНМА.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (ПорядокУчетаНМА.НачалоМесяца = СписокНМА.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьНМА_НаНачалоМесяца КАК СтоимостьНМА
	|		ПО (СтоимостьНМА.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (СтоимостьНМА.НачалоМесяца = СписокНМА.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПредварительнаяСтоимостьНМА КАК ПредварительнаяСтоимостьНМА
	|		ПО (ПредварительнаяСтоимостьНМА.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (ПредварительнаяСтоимостьНМА.НачалоМесяца = СписокНМА.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ АмортизацияНМА_НаНачалоМесяца КАК АмортизацияНМА
	|		ПО (АмортизацияНМА.Организация = СписокНМА.Организация)
	|			И (АмортизацияНМА.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (АмортизацияНМА.НачалоМесяца = СписокНМА.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОбесценениеВНА КАК ОбесценениеНаНачалоМесяца
	|		ПО (ОбесценениеНаНачалоМесяца.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (ОбесценениеНаНачалоМесяца.НачалоМесяца = СписокНМА.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ МестоУчетаНМА КАК МестоУчетаНМА
	|		ПО (МестоУчетаНМА.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (НЕ СписокНМА.ЭтоДоначисление
	|					И СписокНМА.Период = &КонецПредыдущегоМесяца
	|					И НЕ МестоУчетаНМА.ДляДоначисления
	|					И МестоУчетаНМА.НачалоМесяца = &НачалоМесяца
	|				ИЛИ НЕ СписокНМА.ЭтоДоначисление
	|					И СписокНМА.Период > &КонецПредыдущегоМесяца
	|					И МестоУчетаНМА.Период = СписокНМА.Период
	|				ИЛИ СписокНМА.ЭтоДоначисление
	|					И СписокНМА.Период <> &КонецПредыдущегоМесяца
	|					И МестоУчетаНМА.Период = СписокНМА.Период)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияНМА КАК ПервоначальныеСведенияНМА
	|		ПО (ПервоначальныеСведенияНМА.Организация = СписокНМА.Организация)
	|			И (ПервоначальныеСведенияНМА.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (НЕ СписокНМА.ЭтоДоначисление
	|					И СписокНМА.Период = &КонецПредыдущегоМесяца
	|					И НЕ ПервоначальныеСведенияНМА.ДляДоначисления
	|					И ПервоначальныеСведенияНМА.НачалоМесяца = &НачалоМесяца
	|				ИЛИ НЕ СписокНМА.ЭтоДоначисление
	|					И СписокНМА.Период > &КонецПредыдущегоМесяца
	|					И ПервоначальныеСведенияНМА.Период = СписокНМА.Период
	|				ИЛИ СписокНМА.ЭтоДоначисление
	|					И СписокНМА.Период <> &КонецПредыдущегоМесяца
	|					И ПервоначальныеСведенияНМА.Период = СписокНМА.Период)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииНМАУУ КАК ПараметрыАмортизацииНМАУУ
	|		ПО (ПараметрыАмортизацииНМАУУ.Организация = СписокНМА.Организация)
	|			И (ПараметрыАмортизацииНМАУУ.НематериальныйАктив = СписокНМА.НематериальныйАктив)
	|			И (НЕ СписокНМА.ЭтоДоначисление
	|					И СписокНМА.Период = &КонецПредыдущегоМесяца
	|					И НЕ ПараметрыАмортизацииНМАУУ.ДляДоначисления
	|					И ПараметрыАмортизацииНМАУУ.НачалоМесяца = &НачалоМесяца
	|				ИЛИ НЕ СписокНМА.ЭтоДоначисление
	|					И СписокНМА.Период > &КонецПредыдущегоМесяца
	|					И ПараметрыАмортизацииНМАУУ.Период = СписокНМА.Период
	|				ИЛИ СписокНМА.ЭтоДоначисление
	|					И СписокНМА.Период <> &КонецПредыдущегоМесяца
	|					И ПараметрыАмортизацииНМАУУ.Период = СписокНМА.Период)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВыработкаНМА КАК ВыработкаНМА
	|		ПО (ВыработкаНМА.НематериальныйАктив = ПорядокУчетаНМА.НематериальныйАктив)";	
	
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса, 
		"&ФактическийСрокИспользования", 
		ВнеоборотныеАктивыСлужебный.ТекстПоляФактическийСрокИспользования("ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ"));
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область СписокНМАСИзмененнымиПараметрами_УУ
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Изменения_УУ.Период,
	|	Изменения_УУ.Регистратор,
	|	Изменения_УУ.НематериальныйАктив КАК НематериальныйАктив,
	|	(Изменения_УУ.НачалоМесяца = &НачалоПредыдущегоМесяца) КАК ЭтоДоначисление,
	|	ВЫРАЗИТЬ(Изменения_УУ.НематериальныйАктив КАК Справочник.НематериальныеАктивы).ВидОбъектаУчета КАК ВидОбъектаУчета,
	|	Изменения_УУ.Организация,
	|
	|	ЕСТЬNULL(Изменения_УУ.ГруппаФинансовогоУчета,  ПорядокУчетаНМА.ГруппаФинансовогоУчета)  КАК ГруппаФинансовогоУчета,
	|	ЕСТЬNULL(Изменения_УУ.НаправлениеДеятельности, ПорядокУчетаНМА.НаправлениеДеятельности) КАК НаправлениеДеятельности,
	|
	// РесурсНазначенный
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(Изменения_УУ.МетодНачисленияАмортизации, ПараметрыАмортизацииНМАУУ.МетодНачисленияАмортизации) = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции)
	|			ТОГДА ЕСТЬNULL(Изменения_УУ.ОбъемНаработки, ПорядокУчетаНМА.ОбъемНаработки)
	|		ИНАЧЕ ЕСТЬNULL(Изменения_УУ.СрокИспользования, ПараметрыАмортизацииНМАУУ.СрокИспользования)
	|	КОНЕЦ КАК РесурсНазначенный,
	|
	// РесурсВыработанныйЗаТекущийПериод
	|	ВЫРАЗИТЬ(ВЫБОР 
	|		КОГДА ЕСТЬNULL(Изменения_УУ.МетодНачисленияАмортизации, ПараметрыАмортизацииНМАУУ.МетодНачисленияАмортизации) = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции)
	|			ТОГДА ЕСТЬNULL(ВыработкаНМА.ОбъемВыработки, 0)
	|		ИНАЧЕ 1
	|	КОНЕЦ * Изменения_УУ.СрокДействия / ДЕНЬ(Изменения_УУ.КонецМесяца) КАК ЧИСЛО(20, 8)) КАК РесурсВыработанныйЗаТекущийПериод,
	|	  
	// РесурсВыработанныйЗаПрошлыйПериод
	|	ВЫРАЗИТЬ(ВЫБОР 
	|		КОГДА ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА ЕСТЬNULL(Изменения_УУ.МетодНачисленияАмортизации, ПараметрыАмортизацииНМАУУ.МетодНачисленияАмортизации) = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции)
	|			ТОГДА ЕСТЬNULL(ВыработкаНМА.ОбъемВыработки, 0)
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ) = Изменения_УУ.НачалоМесяца
	|			ТОГДА 0
	// 		Расшифровка формулы: [Кол-во отработанных дней в мес. принятия] / [всего дней в мес.принятия] + [кол-во мес. после принятия до начала месяца расчета].
	|		ИНАЧЕ
	|			(РАЗНОСТЬДАТ(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, КОНЕЦПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ), ДЕНЬ) + 1) / ДЕНЬ(КОНЕЦПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ))
	|			+ РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ, 1), МЕСЯЦ), Изменения_УУ.КонецМесяца, МЕСЯЦ)
	|	КОНЕЦ КАК ЧИСЛО(20,8)) КАК РесурсВыработанныйЗаПрошлыйПериод,
	|
	// ОтработанныхМесяцевПолныхЛет
	|	ВЫБОР
	|		КОГДА ГОД(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ) <> ГОД(Изменения_УУ.НачалоМесяца) ТОГДА
	|			13 - МЕСЯЦ(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ)
	|				+ 12 * (ГОД(Изменения_УУ.НачалоМесяца) - Год(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ) - 1)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОтработанныхМесяцевПолныхЛет,
	|
	|	ЕСТЬNULL(Изменения_УУ.НачислятьАмортизациюУУ, ЕСТЬNULL(СписокНМА.НачислятьАмортизациюУУ, ЛОЖЬ)) КАК НачислятьАмортизациюУУ,
	|	ЕСТЬNULL(Изменения_УУ.СтатьяРасходов, СписокНМА.СтатьяРасходовУУ) КАК СтатьяРасходовУУ,
	|	ЕСТЬNULL(Изменения_УУ.АналитикаРасходов, СписокНМА.АналитикаРасходовУУ) КАК АналитикаРасходовУУ,
	|	ЕСТЬNULL(Изменения_УУ.СпособОтраженияРасходов, СписокНМА.СпособОтраженияРасходовУУ) КАК СпособОтраженияРасходовУУ,
	|	ЕСТЬNULL(Изменения_УУ.ПередаватьРасходыВДругуюОрганизацию, ЕСТЬNULL(СписокНМА.ПередаватьРасходыВДругуюОрганизациюУУ, ЛОЖЬ)) КАК ПередаватьРасходыВДругуюОрганизациюУУ,
	|	ЕСТЬNULL(Изменения_УУ.ОрганизацияПолучательРасходов, СписокНМА.ОрганизацияПолучательРасходовУУ) КАК ОрганизацияПолучательРасходовУУ,
	|
	// ОстаточнаяСтоимость
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ) = Изменения_УУ.НачалоМесяца
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьНМА.Стоимость, 0)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьНМА.Стоимость, 0) - ЕСТЬNULL(АмортизацияНМА.Амортизация, 0)
	|	КОНЕЦ - ВЫБОР 
	|				КОГДА НЕ Изменения_УУ.ЛиквидационнаяСтоимость ЕСТЬ NULL
	|					ТОГДА Изменения_УУ.ЛиквидационнаяСтоимость
	|				КОГДА НЕ ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимость ЕСТЬ NULL
	|					ТОГДА ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимость
	|				ИНАЧЕ 0
	|			КОНЕЦ КАК ОстаточнаяСтоимость,	
	|
	// ОстаточнаяСтоимостьРегл
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ) = Изменения_УУ.НачалоМесяца
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьНМА.СтоимостьРегл, 0)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияРегл, 0)
	|	КОНЕЦ - ВЫБОР 
	|				КОГДА НЕ Изменения_УУ.ЛиквидационнаяСтоимостьРегл ЕСТЬ NULL
	|					ТОГДА Изменения_УУ.ЛиквидационнаяСтоимостьРегл
	|				КОГДА НЕ ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимостьРегл ЕСТЬ NULL
	|					ТОГДА ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимостьРегл
	|				ИНАЧЕ 0
	|			КОНЕЦ КАК ОстаточнаяСтоимостьРегл,	
	|
	|	0 КАК СтоимостьНаНачалоГода,
	|
	// Стоимость
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ) = Изменения_УУ.НачалоМесяца
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьНМА.Стоимость, 0)
	|		КОГДА Изменения_УУ.НачалоМесяца = &НачалоПредыдущегоМесяца
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьНМА.Стоимость, 0)
	|	КОНЕЦ КАК Стоимость,
	|
	// СтоимостьРегл
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ, МЕСЯЦ) = Изменения_УУ.НачалоМесяца
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьНМА.СтоимостьРегл, 0)
	|		КОГДА Изменения_УУ.НачалоМесяца = &НачалоПредыдущегоМесяца
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0)
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	|	ЕСТЬNULL(АмортизацияНМА.Амортизация, 0) КАК Амортизация,
	|	ЕСТЬNULL(АмортизацияНМА.АмортизацияРегл, 0) КАК АмортизацияРегл,
	|
	|	ЕСТЬNULL(ОбесценениеНаНачалоМесяца.ОстатокОбесцененияРегл, 0) КАК ОстатокОбесцененияРегл,
	|	ЕСТЬNULL(ОбесценениеНаНачалоМесяца.ОстатокОбесцененияУпр, 0) КАК ОстатокОбесцененияУпр,
	|
	|	Изменения_УУ.СтоимостьИзменение КАК СтоимостьИзменение,
	|	Изменения_УУ.СтоимостьРеглИзменение КАК СтоимостьРеглИзменение,
	|	Изменения_УУ.АмортизацияИзменение КАК АмортизацияИзменение,
	|	Изменения_УУ.АмортизацияРеглИзменение КАК АмортизацияРеглИзменение,
	|
	|	ВЫБОР
	|		КОГДА Изменения_УУ.НачалоМесяца = &НачалоПредыдущегоМесяца
	|			ТОГДА МестоУчетаНМАНаНачалоМесяца.Подразделение
	|		ИНАЧЕ ЕСТЬNULL(Изменения_УУ.Подразделение, МестоУчетаНМА.Подразделение) 
	|	КОНЕЦ КАК Подразделение,
	|
	|	ЕСТЬNULL(ВыработкаНМА.ОбъемВыработки, 0) КАК ОбъемВыработки,
	|
	|	ЕСТЬNULL(Изменения_УУ.СрокИспользования, ПараметрыАмортизацииНМАУУ.СрокИспользования) КАК СрокИспользования,
	|	ЕСТЬNULL(Изменения_УУ.КоэффициентУскорения, ПараметрыАмортизацииНМАУУ.КоэффициентУскорения) КАК КоэффициентУскорения,
	|	ЕСТЬNULL(Изменения_УУ.ЛиквидационнаяСтоимость, ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимость) КАК ЛиквидационнаяСтоимость,
	|	ЕСТЬNULL(Изменения_УУ.ЛиквидационнаяСтоимостьРегл, ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимостьРегл) КАК ЛиквидационнаяСтоимостьРегл,
	|	ЕСТЬNULL(Изменения_УУ.МетодНачисленияАмортизации, ПараметрыАмортизацииНМАУУ.МетодНачисленияАмортизации) КАК МетодНачисленияАмортизации
	|
	|ПОМЕСТИТЬ СписокНМАСИзмененнымиПараметрами_УУ
	|
	|ИЗ
	|	Изменения_УУ КАК Изменения_УУ
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ СписокОбъектовУчета_УУ КАК СписокНМА
	|		ПО (СписокНМА.Организация = Изменения_УУ.Организация)
	|			И (СписокНМА.НематериальныйАктив = Изменения_УУ.НематериальныйАктив)
	|			И (СписокНМА.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаНМА КАК ПорядокУчетаНМА
	|		ПО (ПорядокУчетаНМА.НематериальныйАктив = Изменения_УУ.НематериальныйАктив)
	|			И (ПорядокУчетаНМА.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьНМА_НаНачалоМесяца КАК СтоимостьНМА
	|		ПО (СтоимостьНМА.НематериальныйАктив = Изменения_УУ.НематериальныйАктив)
	|			И (СтоимостьНМА.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПредварительнаяСтоимостьНМА КАК ПредварительнаяСтоимостьНМА
	|		ПО (ПредварительнаяСтоимостьНМА.НематериальныйАктив = Изменения_УУ.НематериальныйАктив)
	|			И (ПредварительнаяСтоимостьНМА.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ АмортизацияНМА_НаНачалоМесяца КАК АмортизацияНМА
	|		ПО (АмортизацияНМА.Организация = Изменения_УУ.Организация)
	|			И (АмортизацияНМА.НематериальныйАктив = Изменения_УУ.НематериальныйАктив)
	|			И (АмортизацияНМА.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОбесценениеВНА КАК ОбесценениеНаНачалоМесяца
	|		ПО (ОбесценениеНаНачалоМесяца.НематериальныйАктив = Изменения_УУ.НематериальныйАктив)
	|			И (ОбесценениеНаНачалоМесяца.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ МестоУчетаНМА КАК МестоУчетаНМА
	|		ПО (МестоУчетаНМА.НематериальныйАктив = Изменения_УУ.НематериальныйАктив)
	|			И (МестоУчетаНМА.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ МестоУчетаНМА КАК МестоУчетаНМАНаНачалоМесяца
	|		ПО (МестоУчетаНМАНаНачалоМесяца.НематериальныйАктив = Изменения_УУ.НематериальныйАктив)
	|			И (МестоУчетаНМАНаНачалоМесяца.НачалоМесяца = &НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияНМА КАК ПервоначальныеСведенияНМА
	|		ПО (ПервоначальныеСведенияНМА.Организация = Изменения_УУ.Организация)
	|			И (ПервоначальныеСведенияНМА.НематериальныйАктив = Изменения_УУ.НематериальныйАктив)
	|			И (ПервоначальныеСведенияНМА.НачалоМесяца = СписокНМА.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииНМАУУ КАК ПараметрыАмортизацииНМАУУ
	|		ПО (ПараметрыАмортизацииНМАУУ.Организация = Изменения_УУ.Организация)
	|			И (ПараметрыАмортизацииНМАУУ.НематериальныйАктив = Изменения_УУ.НематериальныйАктив)
	|			И (ПараметрыАмортизацииНМАУУ.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВыработкаНМА КАК ВыработкаНМА
	|		ПО (ВыработкаНМА.НематериальныйАктив = ПорядокУчетаНМА.НематериальныйАктив)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЭтоДоначисление,
	|	НематериальныйАктив";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область СпособыОтраженияРасходов
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СписокВсехНМА.СпособОтраженияРасходов КАК Ссылка
	|ПОМЕСТИТЬ втСпособыОтраженияРасходов
	|ИЗ
	|	СписокОбъектовУчета КАК СписокВсехНМА
	|ГДЕ
	|	НЕ СписокВсехНМА.СпособОтраженияРасходов В (&ПустыеСпособыОтраженияРасходов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СписокВсехНМА.СпособОтраженияРасходов
	|ИЗ
	|	Изменения_УУ КАК СписокВсехНМА
	|ГДЕ
	|	НЕ СписокВсехНМА.СпособОтраженияРасходов В (&ПустыеСпособыОтраженияРасходов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаАмортизация.Ссылка                               КАК СпособОтраженияРасходов,
	|	ТаблицаАмортизация.Подразделение                        КАК Подразделение,
	|	ТаблицаАмортизация.НаправлениеДеятельности              КАК НаправлениеДеятельности,
	|	ТаблицаАмортизация.СтатьяРасходов                       КАК СтатьяРасходов,
	|	ТаблицаАмортизация.АналитикаРасходов                    КАК АналитикаРасходов,
	|	ТаблицаАмортизация.ПередаватьРасходыВДругуюОрганизацию  КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ТаблицаАмортизация.ОрганизацияПолучательРасходов        КАК ОрганизацияПолучательРасходов,
	|	ТаблицаАмортизация.Коэффициент                          КАК База,
	|	ТаблицаАмортизация.СуммаКоэффициентовПредыдущихСтрок    КАК БазаНакопленная,
	|	ТаблицаАмортизация.Ссылка.СуммаКоэффициентовАмортизации КАК СуммаБазы
	|ПОМЕСТИТЬ СпособыОтраженияРасходов
	|ИЗ
	|	Документ.ИзменениеПараметровНМА2_4.АмортизационныеРасходы КАК ТаблицаАмортизация
	|ГДЕ
	|	ТаблицаАмортизация.Ссылка В (ВЫБРАТЬ СпособыОтраженияРасходов.Ссылка ИЗ втСпособыОтраженияРасходов КАК СпособыОтраженияРасходов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СпособОтраженияРасходов";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	АмортизацияНМАЛокализация.ДополнитьТекстЗапросаПараметрыРасчетаАмортизации(ПараметрыРасчета, СписокЗапросов);
	
КонецПроцедуры

Процедура ТекстЗапросаПроверкаПараметровРасчета(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СписокНМА.Организация КАК Организация,
	|	СписокНМА.НематериальныйАктив КАК ОбъектУчета,
	|
	// НеУказанОбъемНаработки
	|	 СписокНМА.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции)
	|		И СписокНМА.РесурсНазначенный = 0 КАК НеУказанОбъемНаработки
	|
	|ПОМЕСТИТЬ ПроверкаПараметровРасчета_УУ
	|ИЗ
	|	СписокНМАСПараметрами_УУ КАК СписокНМА
	|ГДЕ
	|	СписокНМА.НачислятьАмортизациюУУ И СписокНМА.ОстаточнаяСтоимость > 0
	|	И СписокНМА.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции)
	|		И СписокНМА.РесурсНазначенный = 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	АмортизацияНМАЛокализация.ДополнитьТекстЗапросаПроверкаПараметровРасчета(ПараметрыРасчета, СписокЗапросов);
	
КонецПроцедуры

Процедура ТекстЗапросаРасчетАмортизации_УУ_Предварительный_Доначисление(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокНМА.Период КАК Период,
	|	СписокНМА.Организация КАК Организация,
	|	СписокНМА.Подразделение КАК Подразделение,
	|	СписокНМА.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	СписокНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	СписокНМА.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|	СписокНМА.СтатьяРасходовУУ КАК СтатьяРасходов,
	|	СписокНМА.АналитикаРасходовУУ КАК АналитикаРасходов,
	|	СписокНМА.СпособОтраженияРасходовУУ КАК СпособОтраженияРасходов,
	|	СписокНМА.ПередаватьРасходыВДругуюОрганизациюУУ КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокНМА.ОрганизацияПолучательРасходовУУ КАК ОрганизацияПолучательРасходов,
	|	СписокНМА.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокНМА.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|	СписокНМА.РесурсНазначенный КАК РесурсНазначенный,
	|	СписокНМА.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
	|	СписокНМА.Стоимость КАК Стоимость,
	|	СписокНМА.СтоимостьРегл КАК СтоимостьРегл,
	|	СписокНМА.Амортизация КАК Амортизация,
	|	СписокНМА.АмортизацияРегл КАК АмортизацияРегл,
	|	СписокНМА.ОстаточнаяСтоимостьРегл КАК ОстаточнаяСтоимостьРегл,
	|	СписокНМА.ОстаточнаяСтоимость КАК ОстаточнаяСтоимость,
	|	СписокНМА.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	СписокНМА.ЛиквидационнаяСтоимостьРегл КАК ЛиквидационнаяСтоимостьРегл,
	|	СписокНМА.КоэффициентУскорения КАК КоэффициентУскорения,
	|	СписокНМА.ОтработанныхМесяцевПолныхЛет КАК ОтработанныхМесяцевПолныхЛет,
	|	СписокНМА.НачислятьАмортизациюУУ КАК НачислятьАмортизацию,
	|	СписокНМА.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	СписокНМА.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|
	|	ВЫБОР
	|		КОГДА (СписокНМА.ОстаточнаяСтоимость - (&СуммаАмортизацииУУ)) > СписокНМА.ЛиквидационнаяСтоимость
	|			ТОГДА &СуммаАмортизацииУУ
	|		КОГДА (СписокНМА.ОстаточнаяСтоимость - СписокНМА.ЛиквидационнаяСтоимость) > 0
	|			ТОГДА (СписокНМА.ОстаточнаяСтоимость - СписокНМА.ЛиквидационнаяСтоимость)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУУ,
	|
	|	ВЫБОР
	|		КОГДА (СписокНМА.ОстаточнаяСтоимостьРегл - (&СуммаАмортизацииБУ)) > СписокНМА.ЛиквидационнаяСтоимостьРегл
	|			ТОГДА &СуммаАмортизацииБУ
	|		КОГДА (СписокНМА.ОстаточнаяСтоимостьРегл - СписокНМА.ЛиквидационнаяСтоимостьРегл) > 0
	|			ТОГДА (СписокНМА.ОстаточнаяСтоимостьРегл - СписокНМА.ЛиквидационнаяСтоимостьРегл)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБУ
	|
	|ПОМЕСТИТЬ РасчетАмортизации_УУ_Предварительный_Доначисление
	|
	|ИЗ
	|	СписокНМАСПараметрами_УУ КАК СписокНМА
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПроверкаПараметровРасчета_УУ КАК ПроверкаПараметровРасчета
	|		ПО ПроверкаПараметровРасчета.ОбъектУчета = СписокНМА.НематериальныйАктив
	|
	|ГДЕ
	|	СписокНМА.ЭтоДоначисление
	|	И ПроверкаПараметровРасчета.ОбъектУчета ЕСТЬ NULL";
	
	ПараметрыТекстаЗапроса = ПараметрыТекстаЗапросаСуммаАмортизацииУУ();
	ПараметрыТекстаЗапроса.ТекстСтоимость = "СписокНМА.Стоимость";
	ПараметрыТекстаЗапроса.ТекстАмортизация = "СписокНМА.Амортизация";
	ПараметрыТекстаЗапроса.ТекстОстаточнаяСтоимость = "СписокНМА.ОстаточнаяСтоимость";
	ПараметрыТекстаЗапроса.ТекстПервоначальнаяСтоимость = "СписокНМА.ПервоначальнаяСтоимость";
	ПараметрыТекстаЗапроса.ТекстЛиквидационнаяСтоимость = "СписокНМА.ЛиквидационнаяСтоимость";
	ПараметрыТекстаЗапроса.ТекстОбесценение = "СписокНМА.ОстатокОбесцененияУпр";
	ПараметрыТекстаЗапроса.ТекстСуммаДоначисления = "0";

	ТекстСуммаАмортизацииУУ = ТекстЗапросаСуммаАмортизацииУУ(ПараметрыТекстаЗапроса);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаАмортизацииУУ", ТекстСуммаАмортизацииУУ);
		
	//
	ПараметрыТекстаЗапроса = ПараметрыТекстаЗапросаСуммаАмортизацииУУ();
	ПараметрыТекстаЗапроса.ТекстСтоимость = "СписокНМА.СтоимостьРегл";
	ПараметрыТекстаЗапроса.ТекстАмортизация = "СписокНМА.АмортизацияРегл";
	ПараметрыТекстаЗапроса.ТекстОстаточнаяСтоимость = "СписокНМА.ОстаточнаяСтоимостьРегл";
	ПараметрыТекстаЗапроса.ТекстПервоначальнаяСтоимость = "СписокНМА.ПервоначальнаяСтоимостьРегл";
	ПараметрыТекстаЗапроса.ТекстЛиквидационнаяСтоимость = "СписокНМА.ЛиквидационнаяСтоимостьРегл";
	ПараметрыТекстаЗапроса.ТекстОбесценение = "СписокНМА.ОстатокОбесцененияРегл";
	ПараметрыТекстаЗапроса.ТекстСуммаДоначисления = "0";
	
	ТекстСуммаАмортизацииБУ = ТекстЗапросаСуммаАмортизацииУУ(ПараметрыТекстаЗапроса);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаАмортизацииБУ", ТекстСуммаАмортизацииБУ);
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаРасчетАмортизации_УУ_ДоначислениеСгруппированная(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	СУММА(СписокНМА.СуммаУУ) КАК СуммаУУ,
	|	СУММА(СписокНМА.СуммаБУ) КАК СуммаБУ
	|ПОМЕСТИТЬ РасчетАмортизации_УУ_ДоначислениеСгруппированная
	|ИЗ
	|	РасчетАмортизации_УУ_Доначисление КАК СписокНМА
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокНМА.НематериальныйАктив
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаРасчетАмортизации_УУ_Предварительный(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокНМА.Период КАК Период,
	|	СписокНМА.Организация КАК Организация,
	|	СписокНМА.Подразделение КАК Подразделение,
	|	СписокНМА.ГруппаФинансовогоУчета,
	|	СписокНМА.НаправлениеДеятельности,
	|	СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	СписокНМА.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|	СписокНМА.СтатьяРасходовУУ КАК СтатьяРасходов,
	|	СписокНМА.АналитикаРасходовУУ КАК АналитикаРасходов,
	|	СписокНМА.СпособОтраженияРасходовУУ КАК СпособОтраженияРасходов,
	|	СписокНМА.ПередаватьРасходыВДругуюОрганизациюУУ КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокНМА.ОрганизацияПолучательРасходовУУ КАК ОрганизацияПолучательРасходов,
	|	СписокНМА.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокНМА.РесурсВыработанныйЗаТекущийПериод,
	|	СписокНМА.РесурсНазначенный,
	|	СписокНМА.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
	|	СписокНМА.Стоимость,
	|	СписокНМА.СтоимостьРегл,
	|	СписокНМА.Амортизация,
	|	СписокНМА.АмортизацияРегл,
	|	СписокНМА.ОстаточнаяСтоимость,
	|	СписокНМА.ОстаточнаяСтоимостьРегл,
	|	СписокНМА.ЛиквидационнаяСтоимость,
	|	СписокНМА.ЛиквидационнаяСтоимостьРегл,
	|	СписокНМА.ОстатокОбесцененияРегл,
	|	СписокНМА.ОстатокОбесцененияУпр,
	|	СписокНМА.КоэффициентУскорения КАК КоэффициентУскорения,
	|	СписокНМА.ОтработанныхМесяцевПолныхЛет,
	|	СписокНМА.НачислятьАмортизациюУУ КАК НачислятьАмортизацию,
	|
	|	ВЫБОР
	|		КОГДА &ОбесценениеУУ > СписокНМА.ОстатокОбесцененияУпр
	|			ТОГДА СписокНМА.ОстатокОбесцененияУпр
	|		ИНАЧЕ &ОбесценениеУУ
	|	КОНЕЦ КАК ОбесценениеУУ,
	|
	|	ВЫБОР
	|		КОГДА &ОбесценениеБУ > СписокНМА.ОстатокОбесцененияРегл
	|			ТОГДА СписокНМА.ОстатокОбесцененияРегл
	|		ИНАЧЕ &ОбесценениеБУ
	|	КОНЕЦ КАК ОбесценениеБУ,
	|
	|	ВЫБОР 
	|		КОГДА (СписокНМА.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) - (&СуммаАмортизацииУУ)) > СписокНМА.ЛиквидационнаяСтоимость
	|			ТОГДА 
	|				ВЫБОР
	|					КОГДА СписокНМА.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) - (&СуммаАмортизацииУУ) - СписокНМА.ЛиквидационнаяСтоимость < &МинимальнаяОстаточнаяСтоимость
	|						И СписокНМА.Организация.ВалютаРегламентированногоУчета = &ВалютаУправленческогоУчета
	|						И &ВедетсяРегламентированныйУчетВНА
	|						И НЕ СписокНМА.АмортизацияНачисляетсяПоСтандартамМУ
	|
	|						ТОГДА СписокНМА.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) - СписокНМА.ЛиквидационнаяСтоимость
	|
	|					ИНАЧЕ &СуммаАмортизацииУУ
	|				КОНЕЦ
	|
	|		КОГДА (СписокНМА.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) - (&СуммаАмортизацииУУ)) > СписокНМА.ЛиквидационнаяСтоимость
	|			ТОГДА &СуммаАмортизацииУУ
	|
	|		КОГДА (СписокНМА.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) - СписокНМА.ЛиквидационнаяСтоимость) > 0
	|			ТОГДА (СписокНМА.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) - СписокНМА.ЛиквидационнаяСтоимость)
	|
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУУ,
	|
	|	ВЫБОР 
	|		КОГДА (СписокНМА.ОстаточнаяСтоимостьРегл - ЕСТЬNULL(Доначисление.СуммаБУ, 0) - (&СуммаАмортизацииБУ)) > СписокНМА.ЛиквидационнаяСтоимостьРегл
	|			ТОГДА &СуммаАмортизацииБУ
	|		КОГДА (СписокНМА.ОстаточнаяСтоимостьРегл - ЕСТЬNULL(Доначисление.СуммаБУ, 0) - СписокНМА.ЛиквидационнаяСтоимостьРегл) > 0
	|			ТОГДА (СписокНМА.ОстаточнаяСтоимостьРегл - ЕСТЬNULL(Доначисление.СуммаБУ, 0) - СписокНМА.ЛиквидационнаяСтоимостьРегл)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБУ
	|
	|ПОМЕСТИТЬ РасчетАмортизации_УУ_Предварительный
	|
	|ИЗ
	|	СписокНМАСПараметрами_УУ КАК СписокНМА
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РасчетАмортизации_УУ_ДоначислениеСгруппированная КАК Доначисление
	|		ПО Доначисление.НематериальныйАктив = СписокНМА.НематериальныйАктив
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПроверкаПараметровРасчета_УУ КАК ПроверкаПараметровРасчета
	|		ПО ПроверкаПараметровРасчета.ОбъектУчета = СписокНМА.НематериальныйАктив
	|ГДЕ
	|	НЕ СписокНМА.ЭтоДоначисление
	|	И ПроверкаПараметровРасчета.ОбъектУчета ЕСТЬ NULL";
	
	ПараметрыТекстаЗапроса = ПараметрыТекстаЗапросаСуммаАмортизацииУУ();
	ПараметрыТекстаЗапроса.ТекстСтоимость = "СписокНМА.Стоимость";
	ПараметрыТекстаЗапроса.ТекстАмортизация = "СписокНМА.Амортизация";
	ПараметрыТекстаЗапроса.ТекстОстаточнаяСтоимость = "СписокНМА.ОстаточнаяСтоимость";
	ПараметрыТекстаЗапроса.ТекстПервоначальнаяСтоимость = "СписокНМА.ПервоначальнаяСтоимость";
	ПараметрыТекстаЗапроса.ТекстЛиквидационнаяСтоимость = "СписокНМА.ЛиквидационнаяСтоимость";
	ПараметрыТекстаЗапроса.ТекстОбесценение = "СписокНМА.ОстатокОбесцененияУпр";
	ПараметрыТекстаЗапроса.ТекстСуммаДоначисления = "ЕСТЬNULL(Доначисление.СуммаУУ, 0)";
	
	ТекстСуммаАмортизацииУУ = ТекстЗапросаСуммаАмортизацииУУ(ПараметрыТекстаЗапроса);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаАмортизацииУУ", ТекстСуммаАмортизацииУУ);
		
	//
	ПараметрыТекстаЗапроса = ПараметрыТекстаЗапросаСуммаАмортизацииУУ();
	ПараметрыТекстаЗапроса.ТекстСтоимость = "СписокНМА.СтоимостьРегл";
	ПараметрыТекстаЗапроса.ТекстАмортизация = "СписокНМА.АмортизацияРегл";
	ПараметрыТекстаЗапроса.ТекстОстаточнаяСтоимость = "СписокНМА.ОстаточнаяСтоимостьРегл";
	ПараметрыТекстаЗапроса.ТекстПервоначальнаяСтоимость = "СписокНМА.ПервоначальнаяСтоимостьРегл";
	ПараметрыТекстаЗапроса.ТекстЛиквидационнаяСтоимость = "СписокНМА.ЛиквидационнаяСтоимостьРегл";
	ПараметрыТекстаЗапроса.ТекстОбесценение = "СписокНМА.ОстатокОбесцененияРегл";
	ПараметрыТекстаЗапроса.ТекстСуммаДоначисления = "ЕСТЬNULL(Доначисление.СуммаБУ, 0)";

	ТекстСуммаАмортизацииБУ = ТекстЗапросаСуммаАмортизацииУУ(ПараметрыТекстаЗапроса);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаАмортизацииБУ", ТекстСуммаАмортизацииБУ);
	
	//
	ТекстОбесценениеУУ = ТекстЗапросаСуммаОбесценения("СписокНМА.ОстатокОбесцененияУпр");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОбесценениеУУ", ТекстОбесценениеУУ);
	
	//
	ТекстОбесценениеБУ = ТекстЗапросаСуммаОбесценения("СписокНМА.ОстатокОбесцененияРегл");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОбесценениеБУ", ТекстОбесценениеБУ);
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаВыборкаРасчетаАмортизацииИзменений(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&НачалоМесяца КАК Период,
	|	НЕОПРЕДЕЛЕНО КАК Регистратор,
	|	СписокНМА.Организация КАК Организация,
	|	СписокНМА.Подразделение КАК Подразделение,
	|	СписокНМА.ГруппаФинансовогоУчета,
	|	СписокНМА.НаправлениеДеятельности,
	|	СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	ВЫРАЗИТЬ(СписокНМА.НематериальныйАктив КАК Справочник.НематериальныеАктивы).Код КАК НематериальныйАктивКод,
	|	СписокНМА.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|	СписокНМА.СтатьяРасходов КАК СтатьяРасходов,
	|	СписокНМА.АналитикаРасходов КАК АналитикаРасходов,
	|	СписокНМА.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	СписокНМА.СуммаУУ КАК СуммаУУ,
	|	СписокНМА.СуммаБУ КАК СуммаБУ,
	|	СписокНМА.ОбесценениеУУ КАК ОбесценениеУУ,
	|	СписокНМА.ОбесценениеБУ КАК ОбесценениеБУ,
	|	1 КАК Коэффициент,
	|	СписокНМА.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокНМА.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	СписокНМА.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокНМА.РесурсВыработанныйЗаТекущийПериод,
	|	СписокНМА.РесурсНазначенный,
	|	СписокНМА.МетодНачисленияАмортизации,
	|	СписокНМА.ОстаточнаяСтоимость,
	|	СписокНМА.ОстаточнаяСтоимостьРегл,
	|	СписокНМА.ЛиквидационнаяСтоимость,
	|	СписокНМА.ЛиквидационнаяСтоимостьРегл,
	|	СписокНМА.ОстатокОбесцененияРегл,
	|	СписокНМА.ОстатокОбесцененияУпр,
	|	СписокНМА.КоэффициентУскорения,
	|	СписокНМА.ОтработанныхМесяцевПолныхЛет,
	|	СписокНМА.НачислятьАмортизацию,
	|	СписокНМА.Стоимость - СписокНМА.Амортизация КАК СтоимостьДляВычисленияАмортизации,
	|	СписокНМА.СтоимостьРегл - СписокНМА.АмортизацияРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|	0 КАК СтоимостьИзменение,
	|	0 КАК СтоимостьРеглИзменение,
	|	0 КАК АмортизацияИзменение,
	|	0 КАК АмортизацияРеглИзменение,
	|	ЛОЖЬ КАК ЭтоИзменения
	|ИЗ
	|	РасчетАмортизации_УУ_Предварительный КАК СписокНМА
	|ГДЕ
	|	СписокНМА.НематериальныйАктив В
	|			(ВЫБРАТЬ
	|				СписокНМА.НематериальныйАктив
	|			ИЗ
	|				СписокНМАСИзмененнымиПараметрами_УУ КАК СписокНМА)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СписокНМА.Период,
	|	СписокНМА.Регистратор,
	|	СписокНМА.Организация,
	|	СписокНМА.Подразделение,
	|	СписокНМА.ГруппаФинансовогоУчета,
	|	СписокНМА.НаправлениеДеятельности,
	|	СписокНМА.НематериальныйАктив,
	|	ВЫРАЗИТЬ(СписокНМА.НематериальныйАктив КАК Справочник.НематериальныеАктивы).Код,
	|	СписокНМА.ВидОбъектаУчета,
	|	СписокНМА.СтатьяРасходовУУ,
	|	СписокНМА.АналитикаРасходовУУ,
	|	СписокНМА.СпособОтраженияРасходовУУ,
	|	0,
	|	0,
	|	0,
	|	0,
	|	1,
	|	СписокНМА.ПередаватьРасходыВДругуюОрганизациюУУ,
	|	СписокНМА.ОрганизацияПолучательРасходовУУ,
	|	СписокНМА.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокНМА.РесурсВыработанныйЗаТекущийПериод,
	|	СписокНМА.РесурсНазначенный,
	|	СписокНМА.МетодНачисленияАмортизации,
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(СписокНМА.Период, МЕСЯЦ )= &НачалоМесяца
	|			ТОГДА СписокНМА.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) 
	|		ИНАЧЕ СписокНМА.ОстаточнаяСтоимость 
	|	КОНЕЦ,
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(СписокНМА.Период, МЕСЯЦ )= &НачалоМесяца
	|			ТОГДА СписокНМА.ОстаточнаяСтоимостьРегл - ЕСТЬNULL(Доначисление.СуммаБУ, 0) 
	|		ИНАЧЕ СписокНМА.ОстаточнаяСтоимостьРегл 
	|	КОНЕЦ,
	|	СписокНМА.ЛиквидационнаяСтоимость,
	|	СписокНМА.ЛиквидационнаяСтоимостьРегл,
	|	СписокНМА.ОстатокОбесцененияРегл,
	|	СписокНМА.ОстатокОбесцененияУпр,
	|	СписокНМА.КоэффициентУскорения,
	|	СписокНМА.ОтработанныхМесяцевПолныхЛет,
	|	СписокНМА.НачислятьАмортизациюУУ,
	|	СписокНМА.Стоимость - СписокНМА.Амортизация,
	|	СписокНМА.СтоимостьРегл - СписокНМА.АмортизацияРегл,
	|	СписокНМА.СтоимостьИзменение,
	|	СписокНМА.СтоимостьРеглИзменение,
	|	СписокНМА.АмортизацияИзменение,
	|	СписокНМА.АмортизацияРеглИзменение,
	|	ИСТИНА
	|ИЗ
	|	СписокНМАСИзмененнымиПараметрами_УУ КАК СписокНМА
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РасчетАмортизации_УУ_ДоначислениеСгруппированная КАК Доначисление
	|		ПО Доначисление.НематериальныйАктив = СписокНМА.НематериальныйАктив
	|ГДЕ
	|	НЕ СписокНМА.ЭтоДоначисление";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаВыборкаРасчетаАмортизацииИзменений_Доначисление(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&НачалоМесяца КАК Период,
	|	НЕОПРЕДЕЛЕНО КАК Регистратор,
	|	СписокНМА.Организация КАК Организация,
	|	СписокНМА.Подразделение КАК Подразделение,
	|	СписокНМА.ГруппаФинансовогоУчета,
	|	СписокНМА.НаправлениеДеятельности,
	|	СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	ВЫРАЗИТЬ(СписокНМА.НематериальныйАктив КАК Справочник.НематериальныеАктивы).Код КАК НематериальныйАктивКод,
	|	СписокНМА.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|	СписокНМА.СтатьяРасходов КАК СтатьяРасходов,
	|	СписокНМА.АналитикаРасходов КАК АналитикаРасходов,
	|	СписокНМА.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	СписокНМА.СуммаУУ КАК СуммаУУ,
	|	СписокНМА.СуммаБУ КАК СуммаБУ,
	|	0 КАК ОбесценениеУУ,
	|	0 КАК ОбесценениеБУ,
	|	1 КАК Коэффициент,
	|	СписокНМА.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокНМА.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	СписокНМА.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокНМА.РесурсВыработанныйЗаТекущийПериод,
	|	СписокНМА.РесурсНазначенный,
	|	СписокНМА.МетодНачисленияАмортизации,
	|	СписокНМА.ОстаточнаяСтоимость,
	|	СписокНМА.ОстаточнаяСтоимостьРегл,
	|	СписокНМА.ЛиквидационнаяСтоимость,
	|	СписокНМА.ЛиквидационнаяСтоимостьРегл,
	|	0 КАК ОстатокОбесцененияРегл,
	|	0 КАК ОстатокОбесцененияУпр,
	|	СписокНМА.КоэффициентУскорения,
	|	СписокНМА.ОтработанныхМесяцевПолныхЛет,
	|	СписокНМА.НачислятьАмортизацию,
	|	0 КАК СтоимостьДляВычисленияАмортизации,
	|	0 КАК СтоимостьДляВычисленияАмортизацииРегл,
	|	0 КАК СтоимостьИзменение,
	|	0 КАК СтоимостьРеглИзменение,
	|	0 КАК АмортизацияИзменение,
	|	0 КАК АмортизацияРеглИзменение,
	|	ЛОЖЬ КАК ЭтоИзменения
	|ИЗ
	|	РасчетАмортизации_УУ_Предварительный_Доначисление КАК СписокНМА
	|ГДЕ
	|	СписокНМА.НематериальныйАктив В
	|			(ВЫБРАТЬ
	|				СписокНМА.НематериальныйАктив
	|			ИЗ
	|				СписокНМАСИзмененнымиПараметрами_УУ КАК СписокНМА)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СписокНМА.Период,
	|	СписокНМА.Регистратор,
	|	СписокНМА.Организация,
	|	СписокНМА.Подразделение,
	|	СписокНМА.ГруппаФинансовогоУчета,
	|	СписокНМА.НаправлениеДеятельности,
	|	СписокНМА.НематериальныйАктив,
	|	ВЫРАЗИТЬ(СписокНМА.НематериальныйАктив КАК Справочник.НематериальныеАктивы).Код,
	|	СписокНМА.ВидОбъектаУчета,
	|	СписокНМА.СтатьяРасходовУУ,
	|	СписокНМА.АналитикаРасходовУУ,
	|	СписокНМА.СпособОтраженияРасходовУУ,
	|	0,
	|	0,
	|	0,
	|	0,
	|	1,
	|	СписокНМА.ПередаватьРасходыВДругуюОрганизациюУУ,
	|	СписокНМА.ОрганизацияПолучательРасходовУУ,
	|	СписокНМА.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокНМА.РесурсВыработанныйЗаТекущийПериод,
	|	СписокНМА.РесурсНазначенный,
	|	СписокНМА.МетодНачисленияАмортизации,
	|	СписокНМА.ОстаточнаяСтоимость,
	|	СписокНМА.ОстаточнаяСтоимостьРегл,
	|	СписокНМА.ЛиквидационнаяСтоимость,
	|	СписокНМА.ЛиквидационнаяСтоимостьРегл,
	|	0,
	|	0,
	|	СписокНМА.КоэффициентУскорения,
	|	СписокНМА.ОтработанныхМесяцевПолныхЛет,
	|	СписокНМА.НачислятьАмортизациюУУ,
	|	0,
	|	0,
	|	СписокНМА.СтоимостьИзменение,
	|	СписокНМА.СтоимостьРеглИзменение,
	|	СписокНМА.АмортизацияИзменение,
	|	СписокНМА.АмортизацияРеглИзменение,
	|	ИСТИНА
	|ИЗ
	|	СписокНМАСИзмененнымиПараметрами_УУ КАК СписокНМА
	|ГДЕ
	|	СписокНМА.ЭтоДоначисление";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаРасчетАмортизации_УУ_Доначисление(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокНМА.Период КАК Период,
	|	СписокНМА.Организация КАК Организация,
	|	СписокНМА.Подразделение КАК Подразделение,
	|	СписокНМА.ГруппаФинансовогоУчета,
	|	СписокНМА.НаправлениеДеятельности,
	|	СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	СписокНМА.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|	СписокНМА.СтатьяРасходов КАК СтатьяРасходов,
	|	СписокНМА.АналитикаРасходов КАК АналитикаРасходов,
	|	СписокНМА.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	СписокНМА.СуммаУУ КАК СуммаУУ,
	|	СписокНМА.СуммаБУ КАК СуммаБУ,
	|	СписокНМА.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокНМА.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	СписокНМА.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокНМА.РесурсВыработанныйЗаТекущийПериод,
	|	СписокНМА.РесурсНазначенный,
	|	СписокНМА.СтоимостьДляВычисленияАмортизации,
	|	СписокНМА.СтоимостьДляВычисленияАмортизацииРегл
	|
	|ПОМЕСТИТЬ АмортизацияИзменений_УУ_Доначисление
	|
	|ИЗ
	|	&АмортизацияИзменений_УУ КАК СписокНМА
	|ГДЕ
	|	СписокНМА.ЭтоИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Амортизация.ПериодРасчета,
	|	Амортизация.Организация,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СпособыОтраженияРасходов.Подразделение, Амортизация.Подразделение) = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА Амортизация.Подразделение
	|		ИНАЧЕ ЕСТЬNULL(СпособыОтраженияРасходов.Подразделение, Амортизация.Подразделение) 
	|	КОНЕЦ КАК Подразделение,
	|	Амортизация.НематериальныйАктив,
	|	Амортизация.ВидОбъектаУчета,
	|	Амортизация.ГруппаФинансовогоУчета,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.НаправлениеДеятельности, Амортизация.НаправлениеДеятельности) КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.СтатьяРасходов, Амортизация.СтатьяРасходов) КАК СтатьяРасходов,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.АналитикаРасходов, Амортизация.АналитикаРасходов) КАК АналитикаРасходов,
	|	Амортизация.СпособОтраженияРасходов,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.ПередаватьРасходыВДругуюОрганизацию, Амортизация.ПередаватьРасходыВДругуюОрганизацию) КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.ОрганизацияПолучательРасходов, Амортизация.ОрганизацияПолучательРасходов) КАК ОрганизацияПолучательРасходов,
	|	Амортизация.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	Амортизация.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|	Амортизация.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	Амортизация.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|
	|	ВЫБОР
	|		КОГДА НЕ Амортизация.СпособОтраженияРасходов В (&ПустыеСпособыОтраженияРасходов)
	|			ТОГДА &РаспределениеСуммыУУ
	|		ИНАЧЕ Амортизация.СуммаУУ
	|	КОНЕЦ КАК СуммаУУ,
	|	ВЫБОР
	|		КОГДА НЕ Амортизация.СпособОтраженияРасходов В (&ПустыеСпособыОтраженияРасходов)
	|			ТОГДА &РаспределениеСуммыБУ
	|		ИНАЧЕ Амортизация.СуммаБУ
	|	КОНЕЦ КАК СуммаБУ
	|
	|ПОМЕСТИТЬ РасчетАмортизации_УУ_Доначисление
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		СписокНМА.Период КАК ПериодРасчета,
	|		СписокНМА.Организация КАК Организация,
	|		СписокНМА.Подразделение КАК Подразделение,
	|		СписокНМА.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|		СписокНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|		СписокНМА.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|		СписокНМА.СтатьяРасходов КАК СтатьяРасходов,
	|		СписокНМА.АналитикаРасходов КАК АналитикаРасходов,
	|		СписокНМА.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|		СписокНМА.СуммаУУ КАК СуммаУУ,
	|		СписокНМА.СуммаБУ КАК СуммаБУ,
	|		СписокНМА.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|		СписокНМА.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|		СписокНМА.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|		СписокНМА.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|		СписокНМА.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|		СписокНМА.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл
	|	ИЗ
	|		РасчетАмортизации_УУ_Предварительный_Доначисление КАК СписокНМА
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписокНМА.Период,
	|		СписокНМА.Организация,
	|		СписокНМА.Подразделение,
	|		СписокНМА.ГруппаФинансовогоУчета,
	|		СписокНМА.НаправлениеДеятельности,
	|		СписокНМА.НематериальныйАктив,
	|		СписокНМА.ВидОбъектаУчета,
	|		СписокНМА.СтатьяРасходов,
	|		СписокНМА.АналитикаРасходов,
	|		СписокНМА.СпособОтраженияРасходов,
	|		СписокНМА.СуммаУУ,
	|		СписокНМА.СуммаБУ,
	|		СписокНМА.ПередаватьРасходыВДругуюОрганизацию,
	|		СписокНМА.ОрганизацияПолучательРасходов,
	|		СписокНМА.РесурсВыработанныйЗаПрошлыйПериод,
	|		СписокНМА.РесурсВыработанныйЗаТекущийПериод,
	|		СписокНМА.СтоимостьДляВычисленияАмортизации,
	|		СписокНМА.СтоимостьДляВычисленияАмортизацииРегл
	|	ИЗ
	|		АмортизацияИзменений_УУ_Доначисление КАК СписокНМА
	|
	|	) КАК Амортизация
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпособыОтраженияРасходов КАК СпособыОтраженияРасходов
	|		ПО (СпособыОтраженияРасходов.СпособОтраженияРасходов = Амортизация.СпособОтраженияРасходов)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РаспределениеСуммыУУ", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаУУ"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РаспределениеСуммыБУ", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаБУ"));

	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаРасчетАмортизации_УУ(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокНМА.Период КАК Период,
	|	СписокНМА.Организация КАК Организация,
	|	СписокНМА.Подразделение КАК Подразделение,
	|	СписокНМА.ГруппаФинансовогоУчета,
	|	СписокНМА.НаправлениеДеятельности,
	|	СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	СписокНМА.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|	СписокНМА.СтатьяРасходов КАК СтатьяРасходов,
	|	СписокНМА.АналитикаРасходов КАК АналитикаРасходов,
	|	СписокНМА.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	СписокНМА.СуммаУУ КАК СуммаУУ,
	|	СписокНМА.СуммаБУ КАК СуммаБУ,
	|	СписокНМА.ОбесценениеУУ КАК ОбесценениеУУ,
	|	СписокНМА.ОбесценениеБУ КАК ОбесценениеБУ,
	|	СписокНМА.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокНМА.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	СписокНМА.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокНМА.РесурсВыработанныйЗаТекущийПериод,
	|	СписокНМА.РесурсНазначенный,
	|	СписокНМА.СтоимостьДляВычисленияАмортизации,
	|	СписокНМА.СтоимостьДляВычисленияАмортизацииРегл,
	|	СписокНМА.ОстатокОбесцененияРегл,
	|	СписокНМА.ОстатокОбесцененияУпр
	|ПОМЕСТИТЬ АмортизацияИзменений_УУ
	|ИЗ
	|	&АмортизацияИзменений_УУ КАК СписокНМА
	|ГДЕ
	|	СписокНМА.ЭтоИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Амортизация.ПериодРасчета,
	|	Амортизация.Организация,
	|	Амортизация.Подразделение КАК Подразделение,
	|	ОбесценениеВНА.ЕГДС КАК ЕГДС,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СпособыОтраженияРасходов.Подразделение, Амортизация.Подразделение) = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА Амортизация.Подразделение
	|		ИНАЧЕ ЕСТЬNULL(СпособыОтраженияРасходов.Подразделение, Амортизация.Подразделение) 
	|	КОНЕЦ КАК КорПодразделение,
	|	Амортизация.НематериальныйАктив,
	|	Амортизация.ВидОбъектаУчета,
	|	Амортизация.ГруппаФинансовогоУчета,
	|	Амортизация.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.НаправлениеДеятельности, Амортизация.НаправлениеДеятельности) КАК КорНаправлениеДеятельности,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.СтатьяРасходов, Амортизация.СтатьяРасходов) КАК СтатьяРасходов,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.АналитикаРасходов, Амортизация.АналитикаРасходов) КАК АналитикаРасходов,
	|	Амортизация.СпособОтраженияРасходов,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.ПередаватьРасходыВДругуюОрганизацию, Амортизация.ПередаватьРасходыВДругуюОрганизацию) КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.ОрганизацияПолучательРасходов, Амортизация.ОрганизацияПолучательРасходов) КАК ОрганизацияПолучательРасходов,
	|
	|	Амортизация.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	Амортизация.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|	Амортизация.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	Амортизация.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|	Амортизация.ОстатокОбесцененияРегл КАК ОстатокОбесцененияРегл,
	|	Амортизация.ОстатокОбесцененияУпр КАК ОстатокОбесцененияУпр,
	|	Амортизация.ЭтоДоначисление КАК ЭтоДоначисление,
	|	ЕСТЬNULL(УчетнаяПолитика.ВариантПроводокПоОбесценениюВНА, ЗНАЧЕНИЕ(Перечисление.ВариантыПроводокПоОбесценениюВНА.ДоначислятьКорректировкуОбесценения)) КАК ВариантПроводокПоОбесценениюВНА,
	|
	|	ВЫБОР
	|		КОГДА НЕ Амортизация.СпособОтраженияРасходов В (&ПустыеСпособыОтраженияРасходов)
	|			ТОГДА &РаспределениеСуммыУУ
	|		ИНАЧЕ Амортизация.СуммаУУ
	|	КОНЕЦ КАК СуммаУУ,
	|
	|	ВЫБОР
	|		КОГДА НЕ Амортизация.СпособОтраженияРасходов В (&ПустыеСпособыОтраженияРасходов)
	|			ТОГДА &РаспределениеСуммыБУ
	|		ИНАЧЕ Амортизация.СуммаБУ
	|	КОНЕЦ КАК СуммаБУ,
	|
	|	ВЫБОР
	|		КОГДА НЕ Амортизация.СпособОтраженияРасходов В (&ПустыеСпособыОтраженияРасходов)
	|			ТОГДА &РаспределениеОбесцененияУУ
	|		ИНАЧЕ Амортизация.ОбесценениеУУ
	|	КОНЕЦ КАК ОбесценениеУУ,
	|
	|	ВЫБОР
	|		КОГДА НЕ Амортизация.СпособОтраженияРасходов В (&ПустыеСпособыОтраженияРасходов)
	|			ТОГДА &РаспределениеОбесцененияБУ
	|		ИНАЧЕ Амортизация.ОбесценениеБУ
	|	КОНЕЦ КАК ОбесценениеБУ
	|
	|ПОМЕСТИТЬ РасчетАмортизации_УУ
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		СписокНМА.Период КАК ПериодРасчета,
	|		СписокНМА.Организация КАК Организация,
	|		ВЫРАЗИТЬ(СписокНМА.Организация КАК Справочник.Организации).ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|		СписокНМА.Подразделение КАК Подразделение,
	|		СписокНМА.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|		СписокНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|		СписокНМА.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|		СписокНМА.СтатьяРасходов КАК СтатьяРасходов,
	|		СписокНМА.АналитикаРасходов КАК АналитикаРасходов,
	|		СписокНМА.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|		СписокНМА.СуммаУУ КАК СуммаУУ,
	|		СписокНМА.СуммаБУ КАК СуммаБУ,
	|		СписокНМА.ОбесценениеУУ КАК ОбесценениеУУ,
	|		СписокНМА.ОбесценениеБУ КАК ОбесценениеБУ,
	|		СписокНМА.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|		СписокНМА.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|		СписокНМА.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|		СписокНМА.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|		СписокНМА.ОстаточнаяСтоимость КАК СтоимостьДляВычисленияАмортизации,
	|		СписокНМА.ОстаточнаяСтоимостьРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|		СписокНМА.ОстатокОбесцененияРегл КАК ОстатокОбесцененияРегл,
	|		СписокНМА.ОстатокОбесцененияУпр КАК ОстатокОбесцененияУпр,
	|		ЛОЖЬ КАК ЭтоДоначисление
	|	ИЗ
	|		РасчетАмортизации_УУ_Предварительный КАК СписокНМА
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписокНМА.ПериодРасчета КАК ПериодРасчета,
	|		СписокНМА.Организация КАК Организация,
	|		ВЫРАЗИТЬ(СписокНМА.Организация КАК Справочник.Организации).ГоловнаяОрганизация,
	|		СписокНМА.Подразделение КАК Подразделение,
	|		СписокНМА.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|		СписокНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		СписокНМА.НематериальныйАктив КАК НематериальныйАктив,
	|		СписокНМА.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|		СписокНМА.СтатьяРасходов КАК СтатьяРасходов,
	|		СписокНМА.АналитикаРасходов КАК АналитикаРасходов,
	|		СписокНМА.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|		СписокНМА.СуммаУУ КАК СуммаУУ,
	|		СписокНМА.СуммаБУ КАК СуммаБУ,
	|		0 КАК ОбесценениеУУ,
	|		0 КАК ОбесценениеБУ,
	|		СписокНМА.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|		СписокНМА.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|		СписокНМА.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|		СписокНМА.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|		СписокНМА.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|		СписокНМА.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|		0 КАК ОстатокОбесцененияРегл,
	|		0 КАК ОстатокОбесцененияУпр,
	|		ИСТИНА КАК ЭтоДоначисление
	|	ИЗ
	|		РасчетАмортизации_УУ_Доначисление КАК СписокНМА
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписокНМА.Период,
	|		СписокНМА.Организация,
	|		ВЫРАЗИТЬ(СписокНМА.Организация КАК Справочник.Организации).ГоловнаяОрганизация,
	|		СписокНМА.Подразделение,
	|		СписокНМА.ГруппаФинансовогоУчета,
	|		СписокНМА.НаправлениеДеятельности,
	|		СписокНМА.НематериальныйАктив,
	|		СписокНМА.ВидОбъектаУчета,
	|		СписокНМА.СтатьяРасходов,
	|		СписокНМА.АналитикаРасходов,
	|		СписокНМА.СпособОтраженияРасходов,
	|		СписокНМА.СуммаУУ,
	|		СписокНМА.СуммаБУ,
	|		СписокНМА.ОбесценениеУУ КАК ОбесценениеУУ,
	|		СписокНМА.ОбесценениеБУ КАК ОбесценениеБУ,
	|		СписокНМА.ПередаватьРасходыВДругуюОрганизацию,
	|		СписокНМА.ОрганизацияПолучательРасходов,
	|		СписокНМА.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|		СписокНМА.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|		СписокНМА.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|		СписокНМА.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|		СписокНМА.ОстатокОбесцененияРегл КАК ОстатокОбесцененияРегл,
	|		СписокНМА.ОстатокОбесцененияУпр КАК ОстатокОбесцененияУпр,
	|		ЛОЖЬ КАК ЭтоДоначисление
	|	ИЗ
	|		АмортизацияИзменений_УУ КАК СписокНМА
	|
	|	) КАК Амортизация
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпособыОтраженияРасходов КАК СпособыОтраженияРасходов
	|		ПО (СпособыОтраженияРасходов.СпособОтраженияРасходов = Амортизация.СпособОтраженияРасходов)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&КонецМесяца) КАК УчетнаяПолитика
	|		ПО УчетнаяПолитика.Организация = Амортизация.ГоловнаяОрганизация
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОбесценениеВНА КАК ОбесценениеВНА
	|		ПО ОбесценениеВНА.НематериальныйАктив = Амортизация.НематериальныйАктив
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РаспределениеСуммыУУ", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаУУ"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РаспределениеСуммыБУ", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаБУ"));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РаспределениеОбесцененияУУ", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.ОбесценениеУУ"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РаспределениеОбесцененияБУ", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.ОбесценениеБУ"));
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаНачисленнаяАмортизация(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВЫБОР 
	|		КОГДА НачисленнаяАмортизация.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.НИОКР)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.НМА)
	|	КОНЕЦ КАК ВидВНА,
	|	НачисленнаяАмортизация.ПериодРасчета,
	|	НачисленнаяАмортизация.Организация,
	|	ВЫРАЗИТЬ(НачисленнаяАмортизация.Организация КАК Справочник.Организации).ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ЕСТЬNULL(ПакетыАмортизацииНМА.НомерПакета, 0) КАК НомерПакета,
	|	НачисленнаяАмортизация.Подразделение,
	|	НачисленнаяАмортизация.ЕГДС КАК ЕГДС,
	|	НачисленнаяАмортизация.КорПодразделение,
	|	НачисленнаяАмортизация.НематериальныйАктив,
	|	НачисленнаяАмортизация.ВидОбъектаУчета,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета,
	|	НачисленнаяАмортизация.НаправлениеДеятельности,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности,
	|	НачисленнаяАмортизация.СтатьяРасходов,
	|	НачисленнаяАмортизация.АналитикаРасходов,
	|	НачисленнаяАмортизация.ПередаватьРасходыВДругуюОрганизацию,
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов,
	|	ЛОЖЬ КАК НачислятьИзнос,
	|
	|	МАКСИМУМ(НачисленнаяАмортизация.РесурсВыработанныйЗаПрошлыйПериод) КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	МАКСИМУМ(НачисленнаяАмортизация.РесурсВыработанныйЗаТекущийПериод) КАК РесурсВыработанныйЗаТекущийПериод,
	|	МАКСИМУМ(НачисленнаяАмортизация.СтоимостьДляВычисленияАмортизации) КАК СтоимостьДляВычисленияАмортизации,
	|	МАКСИМУМ(НачисленнаяАмортизация.СтоимостьДляВычисленияАмортизацииРегл) КАК СтоимостьДляВычисленияАмортизацииРегл,
	|
	|	МАКСИМУМ(НачисленнаяАмортизация.ОстатокОбесцененияРегл) КАК ОстатокОбесцененияРегл,
	|	МАКСИМУМ(НачисленнаяАмортизация.ОстатокОбесцененияУпр) КАК ОстатокОбесцененияУпр,
	|	МАКСИМУМ(НачисленнаяАмортизация.ВариантПроводокПоОбесценениюВНА) КАК ВариантПроводокПоОбесценениюВНА,
	|
	|	НачисленнаяАмортизация.ЭтоДоначисление КАК ЭтоДоначисление,
	|
	|	АВТОНОМЕРЗАПИСИ() КАК НомерЗаписи,
	|
	|	СУММА(НачисленнаяАмортизация.СуммаУУ) КАК СуммаУУ,
	|	СУММА(НачисленнаяАмортизация.СуммаБУ) КАК СуммаБУ,
	|	СУММА(НачисленнаяАмортизация.СуммаНУ) КАК СуммаНУ,
	|	СУММА(НачисленнаяАмортизация.СуммаПР) КАК СуммаПР,
	|	СУММА(НачисленнаяАмортизация.СуммаВР) КАК СуммаВР,
	|	СУММА(НачисленнаяАмортизация.СуммаЦФ) КАК СуммаЦФ,
	|	СУММА(НачисленнаяАмортизация.СуммаНУЦФ) КАК СуммаНУЦФ,
	|	СУММА(НачисленнаяАмортизация.СуммаПРЦФ) КАК СуммаПРЦФ,
	|	СУММА(НачисленнаяАмортизация.СуммаВРЦФ) КАК СуммаВРЦФ,
	|
	|	СУММА(НачисленнаяАмортизация.ОбесценениеУУ) КАК ОбесценениеУУ,
	|	СУММА(НачисленнаяАмортизация.ОбесценениеБУ) КАК ОбесценениеБУ,
	|	СУММА(НачисленнаяАмортизация.ОбесценениеЦФ) КАК ОбесценениеЦФ
	|	
	|ПОМЕСТИТЬ НачисленнаяАмортизация
	|
	|ИЗ
	|	&ТекстПодзапроса КАК НачисленнаяАмортизация
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыАмортизацииНМА КАК ПакетыАмортизацииНМА
	|		ПО ПакетыАмортизацииНМА.Организация = НачисленнаяАмортизация.Организация
	|			И ПакетыАмортизацииНМА.НематериальныйАктив = НачисленнаяАмортизация.НематериальныйАктив
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР 
	|		КОГДА НачисленнаяАмортизация.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.НИОКР)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыВнеоборотныхАктивов.НМА)
	|	КОНЕЦ,
	|	НачисленнаяАмортизация.ПериодРасчета,
	|	НачисленнаяАмортизация.Организация,
	|	ЕСТЬNULL(ПакетыАмортизацииНМА.НомерПакета, 0),
	|	НачисленнаяАмортизация.Подразделение,
	|	НачисленнаяАмортизация.ЕГДС,
	|	НачисленнаяАмортизация.КорПодразделение,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета,
	|	НачисленнаяАмортизация.НаправлениеДеятельности,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности,
	|	НачисленнаяАмортизация.НематериальныйАктив,
	|	НачисленнаяАмортизация.ВидОбъектаУчета,
	|	НачисленнаяАмортизация.СтатьяРасходов,
	|	НачисленнаяАмортизация.АналитикаРасходов,
	|	НачисленнаяАмортизация.ПередаватьРасходыВДругуюОрганизацию,
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов,
	|	НачисленнаяАмортизация.ЭтоДоначисление";
	
	СписокПодзапросов = Новый Массив;
	ТекстПодзапроса =  
	"	ВЫБРАТЬ
	|		РасчетАмортизации.ПериодРасчета КАК ПериодРасчета,
	|		РасчетАмортизации.Организация КАК Организация,
	|		РасчетАмортизации.Подразделение КАК Подразделение,
	|		РасчетАмортизации.ЕГДС КАК ЕГДС,
	|		РасчетАмортизации.КорПодразделение КАК КорПодразделение,
	|		РасчетАмортизации.НематериальныйАктив КАК НематериальныйАктив,
	|		РасчетАмортизации.ВидОбъектаУчета КАК ВидОбъектаУчета,
	|		РасчетАмортизации.ГруппаФинансовогоУчета,
	|		РасчетАмортизации.НаправлениеДеятельности,
	|		РасчетАмортизации.КорНаправлениеДеятельности,
	|		РасчетАмортизации.СтатьяРасходов КАК СтатьяРасходов,
	|		РасчетАмортизации.АналитикаРасходов КАК АналитикаРасходов,
	|		РасчетАмортизации.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|		РасчетАмортизации.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|		РасчетАмортизации.РесурсВыработанныйЗаПрошлыйПериод КАК РесурсВыработанныйЗаПрошлыйПериод,
	|		РасчетАмортизации.РесурсВыработанныйЗаТекущийПериод КАК РесурсВыработанныйЗаТекущийПериод,
	|		РасчетАмортизации.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|		РасчетАмортизации.СтоимостьДляВычисленияАмортизацииРегл КАК СтоимостьДляВычисленияАмортизацииРегл,
	|		РасчетАмортизации.ОстатокОбесцененияРегл КАК ОстатокОбесцененияРегл,
	|		РасчетАмортизации.ОстатокОбесцененияУпр КАК ОстатокОбесцененияУпр,
	|		РасчетАмортизации.ЭтоДоначисление КАК ЭтоДоначисление,
	|		РасчетАмортизации.ВариантПроводокПоОбесценениюВНА КАК ВариантПроводокПоОбесценениюВНА,
	|
	|		РасчетАмортизации.СуммаУУ КАК СуммаУУ,
	|
	|		ВЫБОР 
	|			КОГДА НЕ &ВедетсяРегламентированныйУчетВНА
	|				ТОГДА РасчетАмортизации.СуммаБУ
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК СуммаБУ,
	|
	|		0 КАК СуммаНУ,
	|		0 КАК СуммаПР,
	|		0 КАК СуммаВР,
	|		0 КАК СуммаЦФ,
	|		0 КАК СуммаНУЦФ,
	|		0 КАК СуммаПРЦФ,
	|		0 КАК СуммаВРЦФ,
	|
	|		РасчетАмортизации.ОбесценениеУУ КАК ОбесценениеУУ,
	|
	|		ВЫБОР 
	|			КОГДА НЕ &ВедетсяРегламентированныйУчетВНА
	|				ТОГДА РасчетАмортизации.ОбесценениеБУ
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ОбесценениеБУ,
	|
	|		0 КАК ОбесценениеЦФ
	|
	|	ИЗ
	|		РасчетАмортизации_УУ КАК РасчетАмортизации";
	СписокПодзапросов.Добавить(ТекстПодзапроса);
	
	АмортизацияНМАЛокализация.ДополнитьТекстПодзапросаНачисленнаяАмортизация(СписокПодзапросов, ПараметрыРасчета);
	
	ТекстПодзапроса = СтрСоединить(СписокПодзапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПодзапроса", "(" + ТекстПодзапроса + ")");
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаКоличествоОбработанныхДанных(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = АмортизацияНМАЛокализация.ТекстЗапросаКоличествоОбработанныхДанных(ПараметрыРасчета); 
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЕСТЬNULL(КОЛИЧЕСТВО(НачисленнаяАмортизация.НематериальныйАктив), 0) КАК КоличествоДанных
		|ИЗ
		|	РасчетАмортизации_УУ КАК НачисленнаяАмортизация";
		
	КонецЕсли; 
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Функция ТекстЗапросаСуммаАмортизацииУУ(ПараметрыТекстаЗапроса)
	
	ТекстЗапроса =
	"ВЫБОР 
	|	КОГДА НЕ СписокНМА.НачислятьАмортизациюУУ ИЛИ (&ОстаточнаяСтоимость - &СуммаДоначисления) <= 0
	|
	|		ТОГДА 0
	|
	|	КОГДА СписокНМА.АмортизацияНачисляетсяПоСтандартамМУ
	|			И (СписокНМА.РесурсВыработанныйЗаПрошлыйПериод + СписокНМА.РесурсВыработанныйЗаТекущийПериод) >= СписокНМА.РесурсНазначенный
	|
	|		ТОГДА &ОстаточнаяСтоимость - &СуммаДоначисления - &ЛиквидационнаяСтоимость - &ОстатокОбесценения
	|
		// Линейный, ПропорциональноОбъемуПродукции
	|	КОГДА СписокНМА.АмортизацияНачисляетсяПоСтандартамМУ
	|			И СписокНМА.МетодНачисленияАмортизации В (
	|					ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.Линейный),
	|					ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции))
	|	 
	|		ТОГДА ВЫБОР 
	|			КОГДА СписокНМА.ЭтоДоначисление
	|				ТОГДА ВЫРАЗИТЬ((&ОстаточнаяСтоимость - &СуммаДоначисления)
	|								* (ВЫРАЗИТЬ(СписокНМА.РесурсВыработанныйЗаТекущийПериод КАК ЧИСЛО(20,8))
	|										/ ВЫРАЗИТЬ(СписокНМА.РесурсНазначенный - СписокНМА.РесурсВыработанныйЗаПрошлыйПериод КАК ЧИСЛО(20,8))) КАК ЧИСЛО(31,2))
	| 
	|			ИНАЧЕ ВЫРАЗИТЬ((&ОстаточнаяСтоимость - &СуммаДоначисления - &ЛиквидационнаяСтоимость - &ОстатокОбесценения) 
	|								* (ВЫРАЗИТЬ(СписокНМА.РесурсВыработанныйЗаТекущийПериод КАК ЧИСЛО(20,8))
	|										/ ВЫРАЗИТЬ(СписокНМА.РесурсНазначенный - СписокНМА.РесурсВыработанныйЗаПрошлыйПериод КАК ЧИСЛО(20,8))) КАК ЧИСЛО(31,2))
	|		КОНЕЦ
	|
		// УменьшаемогоОстатка (первый год)
	|	КОГДА СписокНМА.АмортизацияНачисляетсяПоСтандартамМУ
	|			И СписокНМА.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка)
	|			И (СписокНМА.РесурсНазначенный - СписокНМА.РесурсВыработанныйЗаПрошлыйПериод - СписокНМА.РесурсВыработанныйЗаТекущийПериод) <= 12
	| 
	|		ТОГДА ВЫБОР 
	|			КОГДА СписокНМА.ЭтоДоначисление
	|				ТОГДА ВЫРАЗИТЬ((&ОстаточнаяСтоимость - &СуммаДоначисления)
	|								* (ВЫРАЗИТЬ(СписокНМА.РесурсВыработанныйЗаТекущийПериод КАК ЧИСЛО(20,8))
	|										/ ВЫРАЗИТЬ(СписокНМА.РесурсНазначенный - СписокНМА.РесурсВыработанныйЗаПрошлыйПериод КАК ЧИСЛО(20,8))) КАК ЧИСЛО(31,2))
	| 
	|			ИНАЧЕ ВЫРАЗИТЬ((&ОстаточнаяСтоимость - &СуммаДоначисления - &ЛиквидационнаяСтоимость) 
	|								* (ВЫРАЗИТЬ(СписокНМА.РесурсВыработанныйЗаТекущийПериод КАК ЧИСЛО(20,8))
	|										/ ВЫРАЗИТЬ(СписокНМА.РесурсНазначенный - СписокНМА.РесурсВыработанныйЗаПрошлыйПериод КАК ЧИСЛО(20,8))) КАК ЧИСЛО(31,2))
	|		КОНЕЦ
	|
		// УменьшаемогоОстатка (после первого года)
	|	КОГДА СписокНМА.АмортизацияНачисляетсяПоСтандартамМУ
	|			И СписокНМА.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка) 
	|   
	|		ТОГДА ВЫРАЗИТЬ((&ОстаточнаяСтоимость - &СуммаДоначисления)
	|				* СписокНМА.КоэффициентУскорения
	|				* ВЫРАЗИТЬ(СписокНМА.РесурсВыработанныйЗаТекущийПериод КАК ЧИСЛО(20,8))
	|				/ ВЫРАЗИТЬ(СписокНМА.РесурсНазначенный
	|					- СписокНМА.КоэффициентУскорения 
	|						* (СписокНМА.РесурсВыработанныйЗаПрошлыйПериод - СписокНМА.ОтработанныхМесяцевПолныхЛет) КАК ЧИСЛО(20,8)) КАК ЧИСЛО(31,2))
	|
		// Линейный
	|	КОГДА НЕ СписокНМА.АмортизацияНачисляетсяПоСтандартамМУ
	|			И СписокНМА.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.Линейный) 
	|
	|		ТОГДА ВЫРАЗИТЬ(&ПервоначальнаяСтоимость / ВЫРАЗИТЬ(СписокНМА.РесурсНазначенный КАК ЧИСЛО(20,8)) КАК ЧИСЛО(31,2))
	|   
		// ПропорциональноОбъемуПродукции
	|	КОГДА НЕ СписокНМА.АмортизацияНачисляетсяПоСтандартамМУ
	|			И СписокНМА.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции) 
	|		ТОГДА ВЫБОР 
	|			КОГДА СписокНМА.РесурсНазначенный <> 0 ТОГДА
	|				ВЫРАЗИТЬ(&ПервоначальнаяСтоимость 
	|								* ВЫРАЗИТЬ(СписокНМА.РесурсВыработанныйЗаТекущийПериод КАК ЧИСЛО(20,8)) 
	|								/ ВЫРАЗИТЬ(СписокНМА.РесурсНазначенный КАК ЧИСЛО(20,8)) КАК ЧИСЛО(31,2))
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|
		// УменьшаемогоОстатка
	|	КОГДА НЕ СписокНМА.АмортизацияНачисляетсяПоСтандартамМУ
	|			И СписокНМА.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка) 
	|		ТОГДА ВЫБОР 
	|			КОГДА (СписокНМА.РесурсНазначенный - СписокНМА.ФактическийСрокИспользования + 1) > 0 ТОГДА
	|				ВЫРАЗИТЬ((&Стоимость - &Амортизация)
	|							* СписокНМА.КоэффициентУскорения 
	|							/ ВЫРАЗИТЬ(СписокНМА.РесурсНазначенный - СписокНМА.ФактическийСрокИспользования + 1 КАК ЧИСЛО(20,8)) КАК ЧИСЛО(31,2))
	|			ИНАЧЕ 0
	|			
	|		КОНЕЦ
	|КОНЕЦ";				

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Стоимость", ПараметрыТекстаЗапроса.ТекстСтоимость);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Амортизация", ПараметрыТекстаЗапроса.ТекстАмортизация);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОстаточнаяСтоимость", ПараметрыТекстаЗапроса.ТекстОстаточнаяСтоимость);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПервоначальнаяСтоимость", ПараметрыТекстаЗапроса.ТекстПервоначальнаяСтоимость);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЛиквидационнаяСтоимость", ПараметрыТекстаЗапроса.ТекстЛиквидационнаяСтоимость);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаДоначисления", ПараметрыТекстаЗапроса.ТекстСуммаДоначисления);
	
	ТекстОбесценение = 
		СтрЗаменить(
			"(ВЫБОР КОГДА СписокНМА.ВариантПроводокПоОбесценениюВНА = &ДоначислятьКорректировкуОбесценения ТОГДА &ОстатокОбесценения ИНАЧЕ 0 КОНЕЦ)", 
			"&ОстатокОбесценения",
			ПараметрыТекстаЗапроса.ТекстОбесценение);			
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОстатокОбесценения", ТекстОбесценение);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаСуммаОбесценения(ТекстОстатокОбесценения)
	
	ТекстЗапроса = 
	"ВЫБОР 
	|	КОГДА НЕ СписокНМА.НачислятьАмортизациюУУ 
	|		ИЛИ &ОстатокОбесценения <= 0
	|		ИЛИ НЕ СписокНМА.АмортизацияНачисляетсяПоСтандартамМУ
	|
	|		ТОГДА 0
	|
	|	КОГДА (СписокНМА.РесурсВыработанныйЗаПрошлыйПериод + СписокНМА.РесурсВыработанныйЗаТекущийПериод) >= СписокНМА.РесурсНазначенный
	|
	|		ТОГДА &ОстатокОбесценения
	|
		// Линейный, ПропорциональноОбъемуПродукции
	|	КОГДА СписокНМА.МетодНачисленияАмортизации В (
	|						ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.Линейный),
	|						ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции)) 
	|
	|		ТОГДА ВЫРАЗИТЬ(&ОстатокОбесценения 
	|							* (ВЫРАЗИТЬ(СписокНМА.РесурсВыработанныйЗаТекущийПериод КАК ЧИСЛО(20,8))
	|							/ ВЫРАЗИТЬ(СписокНМА.РесурсНазначенный - СписокНМА.РесурсВыработанныйЗаПрошлыйПериод КАК ЧИСЛО(20,8))) КАК ЧИСЛО(31,2))
	|
	|	ИНАЧЕ 0
	|
	|КОНЕЦ";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОстатокОбесценения", ТекстОстатокОбесценения);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// 
// Возвращаемое значение:
//  Структура - Параметры текста запроса сумма амортизации УУ:
// * ТекстСтоимость - Строка - 
// * ТекстАмортизация - Строка -
// * ТекстОстаточнаяСтоимость - Строка -
// * ТекстПервоначальнаяСтоимость - Строка -
// * ТекстЛиквидационнаяСтоимость - Строка -
// * ТекстОбесценение - Строка -
// * ТекстСуммаДоначисления - Строка -
Функция ПараметрыТекстаЗапросаСуммаАмортизацииУУ()
	
	ПараметрыТекстаЗапроса = Новый Структура;
	ПараметрыТекстаЗапроса.Вставить("ТекстСтоимость");
	ПараметрыТекстаЗапроса.Вставить("ТекстАмортизация");
	ПараметрыТекстаЗапроса.Вставить("ТекстОстаточнаяСтоимость");
	ПараметрыТекстаЗапроса.Вставить("ТекстПервоначальнаяСтоимость");
	ПараметрыТекстаЗапроса.Вставить("ТекстЛиквидационнаяСтоимость");
	ПараметрыТекстаЗапроса.Вставить("ТекстОбесценение");
	ПараметрыТекстаЗапроса.Вставить("ТекстСуммаДоначисления");
	
	Возврат ПараметрыТекстаЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

#КонецЕсли
