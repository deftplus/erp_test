#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ЗакупочнаяПроцедура) Тогда
		ОбщегоНазначенияУХ.СообщитьОбОшибке(
			Нстр("ru = 'Не указана закупочная процедура!'"), 
			Отказ,
			, 
			СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	мДокументов = ЦентрализованныеЗакупкиУХ.ПолучитьДокументыПоОтбору(
		"ПротоколВыбораПобедителей",
		Новый Структура("ЗакупочнаяПроцедура, Проведен", ЗакупочнаяПроцедура, Истина),
		Ссылка);
	Если мДокументов.Количество() > 0 Тогда
		ОбщегоНазначенияУХ.СообщитьОбОшибке(СтрШаблон(Нстр("ru = 'Для закупочной процедуры %1 уже введен протокол выбора победителей!'"), 
				ЗакупочнаяПроцедура),
			Отказ,
			,
			СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	мДокументов = ЦентрализованныеЗакупкиУХ.ПолучитьДокументыПоОтбору(
		"ПротоколОтменыЗакупочнойПроцедуры",
		Новый Структура("ЗакупочнаяПроцедура, Проведен", ЗакупочнаяПроцедура, Истина),
		Ссылка);
	Если мДокументов.Количество() > 0 Тогда
		ОбщегоНазначенияУХ.СообщитьОбОшибке(СтрШаблон(Нстр("ru = 'Для закупочной процедуры %1 уже введен протокол отмены!'"), ЗакупочнаяПроцедура),
			Отказ,
			,
			СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ДанныеЗаполнения <> Неопределено
			И ТипДанныхЗаполнения = Тип("СправочникСсылка.ЗакупочныеПроцедуры") Тогда
		Ошибки = Неопределено;
		флНетОшибок = 
			Документы.ПротоколОтменыЗакупочнойПроцедуры.ПроверитьВозможностьСоздатьДляЗакупки(
				ДанныеЗаполнения,
				Ошибки);
		Если флНетОшибок Тогда
			ЗакупочнаяПроцедура = ДанныеЗаполнения;
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		Иначе
			ВызватьИсключение 
				ЦентрализованныеЗакупкиКлиентСерверУХ.МассивСообщенийВСтроку(
					Ошибки,
					НСтр("ru='Невозможно создать протокол отмены закупки!'"));
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Справочники.ЗакупочныеПроцедуры.УстановитьФлагЗавершена(ЗакупочнаяПроцедура, Истина, Ссылка);
КонецПроцедуры

#КонецЕсли


