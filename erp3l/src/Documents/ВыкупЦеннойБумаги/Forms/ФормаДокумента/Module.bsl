
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		ИнициализироватьФормуНаСервере();
	КонецЕсли;

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ИнициализироватьФормуНаСервере();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	ЗаполнитьДокумент();
КонецПроцедуры

&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	ЗаполнитьДокумент();
КонецПроцедуры

&НаКлиенте
Процедура ФинансовыйИнструментПриИзменении(Элемент)	
	
	ФинансовыйИнструментПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СтоимостьПриИзменении(Элемент)
	ЗаполнитьДокумент();
КонецПроцедуры

&НаКлиенте
Процедура НКДПриИзменении(Элемент)
	ЗаполнитьДокумент(Новый Структура("ЗаполнитьКэшируемыеЗначения,РассчитатьСтоимостьНоминала,РассчитатьПремию"));
КонецПроцедуры

&НаКлиенте
Процедура ПремияПриИзменении(Элемент)
	ЗаполнитьДокумент(Новый Структура("ЗаполнитьКэшируемыеЗначения,РассчитатьСтоимостьНоминала,РассчитатьСтоимость"));
КонецПроцедуры

&НаКлиенте
Процедура СчетаУчета(Команда)
	ФинансовыеИнструментыФормыКлиент.ОткрытьФормуВыбораСчетов(ЭтаФорма);
КонецПроцедуры

#КонецОбласти

#Область СтандартныеПодсистемы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область ВспомогательныеПроцедурыФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
		
	Элементы.ФинансовыйИнструментЦенаВыпуска.Видимость = Форма.ДисконтнаяЦБ;
	
	Элементы.Контрагент.Заголовок = ?(Форма.ЭтоБиржевойРынок, НСтр("ru = 'Андеррайтер'"), НСтр("ru = 'Контрагент'"));
	Элементы.Договор.Заголовок = ?(Форма.ЭтоБиржевойРынок, НСтр("ru = 'Договор с андеррайтером'"), НСтр("ru = 'Договор контрагента'"));
	
	ФинансовыеИнструментыФормыКлиентСервер.УправлениеФормой_СчетаУчета(Форма);
	ФинансовыеИнструментыФормыКлиентСервер.УправлениеФормойБюджетирование(Форма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДокумент(СтруктураДействий = Неопределено)
			
	Если Объект.ФинансовыйИнструмент.Пустая() Тогда
		Возврат;
	КонецЕсли;

	ЗаполнитьДокументНаСервере(СтруктураДействий);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДокументНаСервере(СтруктураДействий = Неопределено)
			
	РеквизитыФИ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ФинансовыйИнструмент, 
								"Эмитент,ПервыйПолучатель,
								|ВидФинансовогоИнструмента,ДисконтнаяЦБ,СрокПогашения,Должник,
								|ДатаВыпуска,ЦенаВыпуска,Номинал");
		
	Если СтруктураДействий = Неопределено Тогда
		СтруктураДействий = Новый Структура("ЗаполнитьКэшируемыеЗначения, ЗаполнитьНКД, РассчитатьСтоимостьНоминала, РассчитатьСтоимость, РассчитатьПремию");	
	КонецЕсли;
		
	Если ЗначениеЗаполнено(РеквизитыФИ.СрокПогашения) И (Объект.Дата > КонецДня(РеквизитыФИ.СрокПогашения)) Тогда
		Объект.Дата = РеквизитыФИ.СрокПогашения;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Дата выкупа не может быть позже срока погашения облигации'"), , "Объект.Дата");
	КонецЕсли;
			
	Объект.Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыФИ.Эмитент, "ОрганизационнаяЕдиница");
	
	Если СтруктураДействий.Свойство("РассчитатьСтоимость") Тогда
		Объект.Стоимость = РеквизитыФИ.Номинал * Объект.Количество;
	КонецЕсли;	

	ЭтаФорма.КэшируемыеЗначения.Вставить("КонтекстДокумента", Новый Структура("Дата", Объект.Дата));	
	ФинансовыеИнструментыФормыВызовСервера.ОбработатьСтрокуЦБ(Объект, СтруктураДействий, ЭтаФорма.КэшируемыеЗначения);
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ФинансовыйИнструментПриИзмененииНаСервере()

	ОперацииСЦеннымиБумагамиФормы.УстановитьПараметрыЦеннойБумаги(ЭтотОбъект);
	ФинансовыеИнструментыФормы.ПриСозданииНаСервере_СчетаУчета(ЭтотОбъект);

	УправлениеФормой(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура ИнициализироватьФормуНаСервере()
	
	ОперацииСЦеннымиБумагамиФормы.УстановитьПараметрыЦеннойБумаги(ЭтотОбъект);
	ФинансовыеИнструментыФормы.ПриСозданииНаСервере_СчетаУчета(ЭтотОбъект);

	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорПриИзменении(Элемент)
	ФинансовыеИнструментыФормыКлиентСервер.ЗаполнитьСчетВзаиморасчетов(ЭтаФорма);
КонецПроцедуры

#КонецОбласти

#Область ПараметрыОперацийГрафика
&НаКлиенте
Процедура НастроитьПараметрыОпераций(Команда)
	
	ПараметрыКоманды = ДанныеОткрытияПараметровОперацийГрафика();			
	ДоговорыКонтрагентовФормыУХКлиент.ОткрытьПараметрыОперацийГрафика(ЭтотОбъект, ПараметрыКоманды);	
	
КонецПроцедуры

&НаСервере
Функция ДанныеОткрытияПараметровОперацийГрафика()	
	
	НастройкиОпераций = Объект.ПараметрыОпераций.Выгрузить();
	АдресНастроек = ПоместитьВоВременноеХранилище(НастройкиОпераций, ЭтотОбъект.УникальныйИдентификатор);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("АдресНастроек", АдресНастроек);
	ПараметрыОткрытия.Вставить("ГруппаОперацийГрафика", ГруппаОперацийГрафика());
	ПараметрыОткрытия.Вставить("ТолькоПросмотр", Ложь);
	
	Возврат ПараметрыОткрытия;
	
КонецФункции

&НаСервере
Функция ГруппаОперацийГрафика()
	
	Возврат Справочники.ОперацииГрафиковДоговоров.ОблигацияВыпущенная;

КонецФункции	

&НаКлиенте
Процедура ЗавершитьНастройкуОпераций(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ЗагрузитьНастройкиОпераций(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройкиОпераций(Адрес)	
	
	ТаблицаНастроек = ПолучитьИзВременногоХранилища(Адрес);
	Объект.ПараметрыОпераций.Загрузить(ТаблицаНастроек);
	ЭтотОбъект.Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти