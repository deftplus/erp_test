
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Проводки.Параметры.УстановитьЗначениеПараметра("НадписьКоличество", НСтр("ru='Кол:'"));
	
	ПоказыватьПроводки = Ложь;
	УправлениеФормой(ЭтаФорма);
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
КонецПроцедуры

#КонецОбласти

#Область Команды

&НаКлиенте
Процедура ПоказатьСкрытьПроводки(Команда)
	
	ПоказыватьПроводки = НЕ ПоказыватьПроводки;
	УправлениеФормой(ЭтаФорма);
	Если ПоказыватьПроводки Тогда
		ПодключитьОбработчикОжидания("ОбновитьПроводки", 0.1, Истина);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПереключитьАктивностьПроводок(Команда)
	
	ТекущаяСтока = Элементы.Список.ТекущиеДанные;
	Если ТекущаяСтока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПереключитьАктивностьПроводокСервер(ТекущаяСтока.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(Список, "Организация", 
		ОтборОрганизация, ЗначениеЗаполнено(ОтборОрганизация));
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Если ПоказыватьПроводки Тогда
		ПодключитьОбработчикОжидания("ОбновитьПроводки", 0.1, Истина);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Форма.Элементы.ФормаПоказатьСкрытьПроводки.Пометка = Форма.ПоказыватьПроводки;
	
	Форма.Элементы.Проводки.Видимость = Форма.ПоказыватьПроводки;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПроводки()

	Регистратор = ?(Элементы.Список.ТекущиеДанные = Неопределено, Неопределено, Элементы.Список.ТекущиеДанные.Ссылка);
	Проводки.Параметры.УстановитьЗначениеПараметра("Регистратор", Регистратор);

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	МСФОУХ.УсловноеОформление_Проводки(ЭтаФорма, "Проводки", "Проводки");
		
КонецПроцедуры

&наСервере
Процедура ПереключитьАктивностьПроводокСервер(Ссылка)
	
	ТекущаяАктивность = Ложь;
	
	нз = РегистрыБухгалтерии.МСФО.СоздатьНаборЗаписей();
	нз.Отбор.Регистратор.Установить(Ссылка);
	нз.Прочитать();
	
	Для каждого Запись Из нз Цикл
		Запись.Активность = Не Запись.Активность;
	КонецЦикла;
	
	нз.Записать();
	
КонецПроцедуры

#КонецОбласти
