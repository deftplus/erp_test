
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ПодготовкаПараметровПроведенияДокумента

Функция ПодготовитьПараметрыПроведения(ДополнительныеСвойства, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДополнительныеСвойства.ДляПроведения.Ссылка);
	
	Реквизиты = МСФОУХ.РеквизитыДокумента(Запрос, "РежимЗаполнения, ПериодОтчета, ВыполнятьПроверкуОстатков", "ПериодОтчета", Отказ);
	Реквизиты.Вставить("ГраницыПериода",			МСФОВНАВызовСервераУХ.ПолучитьГраницыПериодаДокумента(Реквизиты,,Истина));
	Реквизиты.Вставить("ПредыдущийПериод",			МСФОВНАВызовСервераУХ.ПолучитьГраницыПериодаДокумента(Реквизиты, -1, Истина).ДатаОкончания);
	Реквизиты.Вставить("ДанныеРежимаЗаполнения", 	УчетМСФОПовтИспУХ.ПолучитьВидыУчетаРежимаЗаполнения(Реквизиты.РежимЗаполнения));
	
	ДополнительныеСвойства.ДляПроведения.Вставить("Реквизиты", Реквизиты);
	
	Если Отказ Тогда
		Возврат ДополнительныеСвойства;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Период",				Реквизиты.Период);	
	Запрос.УстановитьПараметр("ГраницаДоДокумента",	МСФОВызовСервераУХ.ПолучитьГраницуДоДокумента(Реквизиты.Период, Реквизиты.Ссылка));
	Запрос.УстановитьПараметр("Организация",		Реквизиты.Организация);
	Запрос.УстановитьПараметр("Сценарий",			Реквизиты.Сценарий);
	Запрос.УстановитьПараметр("ПериодОтчета", 		Реквизиты.ПериодОтчета);
	
	Запрос.УстановитьПараметр("ЕстьНСБУ", 					Реквизиты.ДанныеРежимаЗаполнения.ЕстьНСБУ);
	Запрос.УстановитьПараметр("ЕстьМСФО", 					Реквизиты.ДанныеРежимаЗаполнения.ЕстьМСФО);
	Запрос.УстановитьПараметр("ВидУчетаНСБУ", 				Реквизиты.ДанныеРежимаЗаполнения.НСБУ);	
	Запрос.УстановитьПараметр("ВидУчетаМСФО", 				Реквизиты.ДанныеРежимаЗаполнения.МСФО);
	Запрос.УстановитьПараметр("ОтборВидУчетаНСБУ", 			ПредопределенноеЗначение("Перечисление.ВидыУчета.НСБУ"));
	Запрос.УстановитьПараметр("ФормироватьПроводкиМСФО", 	Реквизиты.ФормироватьПроводкиМСФО);
	Запрос.УстановитьПараметр("ФункциональнаяВалюта",		Реквизиты.ФункциональнаяВалюта);
		
	Запрос.УстановитьПараметр("ПустаяДата",				Дата(1,1,1));
	Запрос.УстановитьПараметр("НачалоГода",				НачалоГода(Реквизиты.Период));
	Запрос.УстановитьПараметр("КонецГода",				КонецГода(Реквизиты.Период));
		
	Запрос.УстановитьПараметр("ДатаНачалаПериода",		НачалоДня(Реквизиты.ГраницыПериода.ДатаНачала));
	Запрос.УстановитьПараметр("ДатаОкончанияПериода",	КонецДня(Реквизиты.ГраницыПериода.ДатаОкончания));
	Запрос.УстановитьПараметр("ГраницаПредПериод",		Новый Граница(КонецДня(Реквизиты.ПредыдущийПериод), ВидГраницы.Включая));
	
	НомераТаблиц = Новый Структура;
	ТекстЗапроса = Новый Массив;
	
	ТекстЗапроса.Добавить(ТекстЗапросаТаблицаВНА(НомераТаблиц));
	Если Реквизиты.ВыполнятьПроверкуОстатков Тогда
		ТекстЗапроса.Добавить(ТекстЗапросаПроверкаОстатков(НомераТаблиц));
	КонецЕсли;
	
	ТекстЗапроса.Добавить(ТекстЗапросаОчередьСобытий(НомераТаблиц));
	ТекстЗапроса.Добавить(ТекстЗапросаПоступлениеВНАМСФО(НомераТаблиц));
	ТекстЗапроса.Добавить(ТекстЗапросаВводВНАВЭксплуатациюМСФО(НомераТаблиц));
	ТекстЗапроса.Добавить(ТекстЗапросаМодернизацияВНАМСФО(НомераТаблиц));
	ТекстЗапроса.Добавить(ТекстЗапросаПереоценкаВНАМСФО(НомераТаблиц));
	ТекстЗапроса.Добавить(ТекстЗапросаВыбытиеВНАМСФО(НомераТаблиц));
	ТекстЗапроса.Добавить(ТекстЗапросаНачислениеАмортизацииВНАМСФО(НомераТаблиц));
	
	Запрос.Текст = СтрСоединить(ТекстЗапроса, ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета());
	Результат = Запрос.ВыполнитьПакет();

	НомераТаблиц.Удалить("ТаблицаВНА");
	НомераТаблиц.Удалить("втДвиженияНСБУ");
	
	ТаблицыДляПроверки = Новый Структура;
	ТаблицыДляПроверки.Вставить("РасхождениеОстатковСумм");
	ТаблицыДляПроверки.Вставить("РасхождениеПараметровАмортизации");
	ТаблицыДляПроверки.Вставить("РасхождениеПараметровУчета");
	
	УдалитьКолонкиМСФО = НЕ Реквизиты.ДанныеРежимаЗаполнения.ЕстьМСФО;
	
	Для Каждого НомерТаблицы Из НомераТаблиц Цикл
		Если ТаблицыДляПроверки.Свойство(НомерТаблицы.Ключ) Тогда
		    ТаблицыДляПроверки.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
		Иначе	
			
			ТаблицаИсточник = Результат[НомерТаблицы.Значение].Выгрузить();
			Если УдалитьКолонкиМСФО Тогда
				МСФОВНАВызовСервераУХ.УдалитьКолонкиМСФО(ТаблицаИсточник);
			КонецЕсли; 
			
			ДополнительныеСвойства.ТаблицыДляДвижений.Вставить(НомерТаблицы.Ключ, ТаблицаИсточник);
			
		КонецЕсли;		
	КонецЦикла;
	
	ДополнительныеСвойства.ДляПроведения.Вставить("ТаблицыДляПроверки", ТаблицыДляПроверки);
	
	Возврат ДополнительныеСвойства;

КонецФункции

Функция ТекстЗапросаТаблицаВНА(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаВНА", НомераТаблиц.Количество());
	
	Возврат 	
	"ВЫБРАТЬ
	|	ТаблицаВНА.НомерСтроки КАК НомерСтроки,
	|	ТаблицаВНА.ВНА.ИнвентарныйНомерМСФО КАК ИнвентарныйНомерНСБУ,
	|	ТаблицаВНА.ВНА.ПервоначальныйИнвентарныйНомерМСФО КАК ИнвентарныйНомерМСФО,
	|	ТаблицаВНА.ВНА КАК ВНА,
	|	ТаблицаВНА.СпособПоступления КАК СпособПоступления,
	|	ТаблицаВНА.ВидОперацииВыбытия КАК ВидОперацииВыбытия,
	|	ТаблицаВНА.ГруппаВНА КАК ГруппаВНА,
	|	ТаблицаВНА.ПараметрыАмортизацииВНАМСФО КАК ПараметрыАмортизацииВНАМСФО,
	|	ТаблицаВНА.СтоимостьНСБУ КАК СтоимостьНСБУ,
	|	ТаблицаВНА.СтоимостьМСФО КАК СтоимостьМСФО,
	|	ТаблицаВНА.ЛиквидационнаяСтоимостьНСБУ КАК ЛиквидационнаяСтоимостьНСБУ,
	|	ТаблицаВНА.ЛиквидационнаяСтоимостьМСФО КАК ЛиквидационнаяСтоимостьМСФО,
	|	ТаблицаВНА.АмортизацияНСБУ КАК АмортизацияНСБУ,
	|	ТаблицаВНА.АмортизацияМСФО КАК АмортизацияМСФО,
	|	ТаблицаВНА.ПереоценкаНСБУ КАК ПереоценкаНСБУ,
	|	ТаблицаВНА.ПереоценкаМСФО КАК ПереоценкаМСФО,
	|	ТаблицаВНА.ВыбытиеНСБУ КАК ВыбытиеНСБУ,
	|	ТаблицаВНА.ВыбытиеМСФО КАК ВыбытиеМСФО,
	|	ТаблицаВНА.ГруппаВНА.НачислятьАмортизацию КАК НачислятьАмортизациюНСБУ,
	|	ТаблицаВНА.ГруппаВНА.НачислятьАмортизацию КАК НачислятьАмортизациюМСФО,
	|	ТаблицаВНА.СпособНачисленияАмортизацииНСБУ КАК СпособНачисленияАмортизацииНСБУ,
	|	ТаблицаВНА.СпособНачисленияАмортизацииМСФО КАК СпособНачисленияАмортизацииМСФО,
	|	ТаблицаВНА.СрокПолезногоИспользованияНСБУ КАК СрокПолезногоИспользованияНСБУ,
	|	ТаблицаВНА.СрокПолезногоИспользованияМСФО КАК СрокПолезногоИспользованияМСФО,
	|	ТаблицаВНА.СчетУчетаНСБУ КАК СчетУчетаНСБУ,
	|	ТаблицаВНА.СчетУчетаМСФО КАК СчетУчетаМСФО,
	|	ТаблицаВНА.СчетАмортизацииНСБУ КАК СчетАмортизацииНСБУ,
	|	ТаблицаВНА.СчетАмортизацииМСФО КАК СчетАмортизацииМСФО,
	|	ТаблицаВНА.СчетУчетаСубконто1НСБУ КАК СчетУчетаСубконто1НСБУ,
	|	ТаблицаВНА.СчетУчетаСубконто2НСБУ КАК СчетУчетаСубконто2НСБУ,
	|	ТаблицаВНА.СчетУчетаСубконто3НСБУ КАК СчетУчетаСубконто3НСБУ,
	|	ТаблицаВНА.СчетУчетаСубконто1МСФО КАК СчетУчетаСубконто1МСФО,
	|	ТаблицаВНА.СчетУчетаСубконто2МСФО КАК СчетУчетаСубконто2МСФО,
	|	ТаблицаВНА.СчетУчетаСубконто3МСФО КАК СчетУчетаСубконто3МСФО,
	|	ТаблицаВНА.СчетАмортизацииСубконто1НСБУ КАК СчетАмортизацииСубконто1НСБУ,
	|	ТаблицаВНА.СчетАмортизацииСубконто2НСБУ КАК СчетАмортизацииСубконто2НСБУ,
	|	ТаблицаВНА.СчетАмортизацииСубконто3НСБУ КАК СчетАмортизацииСубконто3НСБУ,
	|	ТаблицаВНА.СчетАмортизацииСубконто1МСФО КАК СчетАмортизацииСубконто1МСФО,
	|	ТаблицаВНА.СчетАмортизацииСубконто2МСФО КАК СчетАмортизацииСубконто2МСФО,
	|	ТаблицаВНА.СчетАмортизацииСубконто3МСФО КАК СчетАмортизацииСубконто3МСФО,
	|	ТаблицаВНА.СчетВыбытияВНАНСБУ КАК СчетВыбытияВНАНСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто1НСБУ КАК СчетВыбытияВНАСубконто1НСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто2НСБУ КАК СчетВыбытияВНАСубконто2НСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто3НСБУ КАК СчетВыбытияВНАСубконто3НСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАМСФО КАК СчетВыбытияВНАМСФО,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто1МСФО КАК СчетВыбытияВНАСубконто1МСФО,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто2МСФО КАК СчетВыбытияВНАСубконто2МСФО,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто3МСФО КАК СчетВыбытияВНАСубконто3МСФО,
	|	ТаблицаВНА.СчетЗатратНСБУ КАК СчетЗатратНСБУ,
	|	ТаблицаВНА.СчетЗатратСубконто1НСБУ КАК СчетЗатратСубконто1НСБУ,
	|	ТаблицаВНА.СчетЗатратСубконто2НСБУ КАК СчетЗатратСубконто2НСБУ,
	|	ТаблицаВНА.СчетЗатратСубконто3НСБУ КАК СчетЗатратСубконто3НСБУ,
	|	ТаблицаВНА.СчетЗатратМСФО КАК СчетЗатратМСФО,
	|	ТаблицаВНА.СчетЗатратСубконто1МСФО КАК СчетЗатратСубконто1МСФО,
	|	ТаблицаВНА.СчетЗатратСубконто2МСФО КАК СчетЗатратСубконто2МСФО,
	|	ТаблицаВНА.СчетЗатратСубконто3МСФО КАК СчетЗатратСубконто3МСФО,
	|	ТаблицаВНА.СпособОтраженияРасходовПоАмортизацииНСБУ КАК СпособОтраженияРасходовПоАмортизацииНСБУ,
	|	ТаблицаВНА.СпособОтраженияРасходовПоАмортизацииМСФО КАК СпособОтраженияРасходовПоАмортизацииМСФО,
	|	ТаблицаВНА.КоэффициентУскоренияНСБУ КАК КоэффициентУскоренияНСБУ,
	|	ТаблицаВНА.КоэффициентУскоренияМСФО КАК КоэффициентУскоренияМСФО,
	|	ТаблицаВНА.ОстатокБалансоваяСтоимостьНСБУ КАК ОстатокБалансоваяСтоимостьНСБУ,
	|	ТаблицаВНА.ОстатокАмортизацияНСБУ КАК ОстатокАмортизацияНСБУ,
	|	ТаблицаВНА.ДатаВводаНСБУ КАК ДатаВводаНСБУ,
	|	ТаблицаВНА.ДатаВводаМСФО КАК ДатаВводаМСФО,
	|	ТаблицаВНА.ДатаМодернизацииНСБУ КАК ДатаМодернизацииНСБУ,
	|	ТаблицаВНА.ДатаМодернизацииМСФО КАК ДатаМодернизацииМСФО,
	|	ТаблицаВНА.ДатаПереоценкиМСФО КАК ДатаПереоценкиМСФО,
	|	ТаблицаВНА.ДатаПереоценкиНСБУ КАК ДатаПереоценкиНСБУ,
	|	ТаблицаВНА.ДатаВыбытияНСБУ КАК ДатаВыбытияНСБУ,
	|	ТаблицаВНА.ДатаВыбытияМСФО КАК ДатаВыбытияМСФО,
	|	ТаблицаВНА.ЧастичноеВыбытиеНСБУ КАК ЧастичноеВыбытиеНСБУ,
	|	ТаблицаВНА.ЧастичноеВыбытиеМСФО КАК ЧастичноеВыбытиеМСФО,
	|	ТаблицаВНА.МодернизацияНСБУ КАК МодернизацияНСБУ,
	|	ТаблицаВНА.МодернизацияМСФО КАК МодернизацияМСФО,
	|	ТаблицаВНА.КоррСчетПриобретениеНСБУ КАК КоррСчетПриобретениеНСБУ,
	|	ТаблицаВНА.КоррСчетПриобретениеСубконто1НСБУ КАК КоррСчетПриобретениеСубконто1НСБУ,
	|	ТаблицаВНА.КоррСчетПриобретениеСубконто2НСБУ КАК КоррСчетПриобретениеСубконто2НСБУ,
	|	ТаблицаВНА.КоррСчетПриобретениеСубконто3НСБУ КАК КоррСчетПриобретениеСубконто3НСБУ,
	|	ТаблицаВНА.КоррСчетПриобретениеМСФО КАК КоррСчетПриобретениеМСФО,
	|	ТаблицаВНА.КоррСчетПриобретениеСубконто1МСФО КАК КоррСчетПриобретениеСубконто1МСФО,
	|	ТаблицаВНА.КоррСчетПриобретениеСубконто2МСФО КАК КоррСчетПриобретениеСубконто2МСФО,
	|	ТаблицаВНА.КоррСчетПриобретениеСубконто3МСФО КАК КоррСчетПриобретениеСубконто3МСФО,
	|	ТаблицаВНА.КоррСчетМодернизацияНСБУ КАК КоррСчетМодернизацияНСБУ,
	|	ТаблицаВНА.КоррСчетМодернизацияСубконто1НСБУ КАК КоррСчетМодернизацияСубконто1НСБУ,
	|	ТаблицаВНА.КоррСчетМодернизацияСубконто2НСБУ КАК КоррСчетМодернизацияСубконто2НСБУ,
	|	ТаблицаВНА.КоррСчетМодернизацияСубконто3НСБУ КАК КоррСчетМодернизацияСубконто3НСБУ,
	|	ТаблицаВНА.КоррСчетМодернизацияМСФО КАК КоррСчетМодернизацияМСФО,
	|	ТаблицаВНА.КоррСчетМодернизацияСубконто1МСФО КАК КоррСчетМодернизацияСубконто1МСФО,
	|	ТаблицаВНА.КоррСчетМодернизацияСубконто2МСФО КАК КоррСчетМодернизацияСубконто2МСФО,
	|	ТаблицаВНА.КоррСчетМодернизацияСубконто3МСФО КАК КоррСчетМодернизацияСубконто3МСФО,
	|	ТаблицаВНА.КоррСчетВыбытиеНСБУ КАК КоррСчетВыбытиеНСБУ,
	|	ТаблицаВНА.КоррСчетВыбытиеСубконто1НСБУ КАК КоррСчетВыбытиеСубконто1НСБУ,
	|	ТаблицаВНА.КоррСчетВыбытиеСубконто2НСБУ КАК КоррСчетВыбытиеСубконто2НСБУ,
	|	ТаблицаВНА.КоррСчетВыбытиеСубконто3НСБУ КАК КоррСчетВыбытиеСубконто3НСБУ,
	|	ТаблицаВНА.КоррСчетВыбытиеМСФО КАК КоррСчетВыбытиеМСФО,
	|	ТаблицаВНА.КоррСчетВыбытиеСубконто1МСФО КАК КоррСчетВыбытиеСубконто1МСФО,
	|	ТаблицаВНА.КоррСчетВыбытиеСубконто2МСФО КАК КоррСчетВыбытиеСубконто2МСФО,
	|	ТаблицаВНА.КоррСчетВыбытиеСубконто3МСФО КАК КоррСчетВыбытиеСубконто3МСФО,
	|	ТаблицаВНА.КоличествоНСБУ КАК КоличествоНСБУ
	|ПОМЕСТИТЬ ТаблицаВНА
	|ИЗ
	|	Документ.ВводСведенийВНАМСФО.ВНА КАК ТаблицаВНА
	|ГДЕ
	|	ТаблицаВНА.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВНА";

КонецФункции

Функция ТекстЗапросаПроверкаОстатков(НомераТаблиц)

	НомераТаблиц.Вставить("РасхождениеОстатковСумм", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("РасхождениеПараметровАмортизации", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("РасхождениеПараметровУчета", НомераТаблиц.Количество());
		
	Возврат
	"ВЫБРАТЬ
	|	РасхождениеСумм.ВНА,
	|	СУММА(РасхождениеСумм.БалансоваяСтоимостьНСБУ) КАК БалансоваяСтоимостьНСБУ,
	|	СУММА(РасхождениеСумм.АмортизацияНСБУ) КАК АмортизацияНСБУ,
	|	СУММА(РасхождениеСумм.БалансоваяСтоимостьУчет) КАК БалансоваяСтоимостьУчет,
	|	СУММА(РасхождениеСумм.АмортизацияУчет) КАК АмортизацияУчет,
	|	СУММА(РасхождениеСумм.БалансоваяСтоимостьДокумент) КАК БалансоваяСтоимостьДокумент,
	|	СУММА(РасхождениеСумм.АмортизацияДокумент) КАК АмортизацияДокумент
	|ИЗ
	|	(ВЫБРАТЬ
	|		СтоимостьВНАМСФООбороты.ВНА КАК ВНА,
	|		СтоимостьВНАМСФООбороты.СуммаВВалютеУчетаОборот КАК ОстаточнаяСтоимостьНСБУ,
	|		ВЫБОР
	|			КОГДА СтоимостьВНАМСФООбороты.ВидСобытия <> &ВидСобытияНачислениеАмортизации
	|				ТОГДА СтоимостьВНАМСФООбороты.СуммаВВалютеУчетаОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК БалансоваяСтоимостьНСБУ,
	|		ВЫБОР
	|			КОГДА СтоимостьВНАМСФООбороты.ВидСобытия = &ВидСобытияНачислениеАмортизации
	|				ТОГДА СтоимостьВНАМСФООбороты.СуммаВВалютеУчетаОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК АмортизацияНСБУ,
	|		ВЫБОР
	|			КОГДА СтоимостьВНАМСФООбороты.ВидСобытия <> &ВидСобытияНачислениеАмортизации
	|				ТОГДА СтоимостьВНАМСФООбороты.СуммаВВалютеУчетаОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК БалансоваяСтоимостьУчет,
	|		ВЫБОР
	|			КОГДА СтоимостьВНАМСФООбороты.ВидСобытия = &ВидСобытияНачислениеАмортизации
	|				ТОГДА СтоимостьВНАМСФООбороты.СуммаВВалютеУчетаОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК АмортизацияУчет,
	|		0 КАК БалансоваяСтоимостьДокумент,
	|		0 КАК АмортизацияДокумент
	|	ИЗ
	|		РегистрНакопления.СтоимостьВНАМСФО.Обороты(
	|				,
	|				&ГраницаПредПериод,
	|				,
	|				Сценарий = &Сценарий
	|					И Организация = &Организация
	|					И ВидУчета = &ВидУчетаНСБУ) КАК СтоимостьВНАМСФООбороты
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаВНА.ВНА,
	|		ТаблицаВНА.ОстатокБалансоваяСтоимостьНСБУ - ТаблицаВНА.ОстатокАмортизацияНСБУ,
	|		-ТаблицаВНА.ОстатокБалансоваяСтоимостьНСБУ,
	|		-ТаблицаВНА.ОстатокАмортизацияНСБУ,
	|		0,
	|		0,
	|		ТаблицаВНА.ОстатокБалансоваяСтоимостьНСБУ,
	|		ТаблицаВНА.ОстатокАмортизацияНСБУ
	|	ИЗ
	|		ТаблицаВНА КАК ТаблицаВНА) КАК РасхождениеСумм
	|
	|СГРУППИРОВАТЬ ПО
	|	РасхождениеСумм.ВНА
	|
	|ИМЕЮЩИЕ
	|	(СУММА(РасхождениеСумм.ОстаточнаяСтоимостьНСБУ) <> 0
	|		ИЛИ СУММА(РасхождениеСумм.АмортизацияНСБУ) <> 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВНА.ВНА,
	|	ТаблицаВНА.НачислятьАмортизацию КАК НачислятьАмортизациюУчет,
	|	ЕСТЬNULL(ПараметрыАмортизацииВНАМСФОСрезПоследних.НачислятьАмортизацию, ЛОЖЬ) КАК НачислятьАмортизациюДокумент,
	|	ТаблицаВНА.СрокПолезногоИспользованияНСБУ КАК СрокПолезногоИспользованияУчет,
	|	ЕСТЬNULL(ПараметрыАмортизацииВНАМСФОСрезПоследних.СрокПолезногоИспользования, 0) КАК СрокПолезногоИспользованияДокумент
	|ИЗ
	|	ТаблицаВНА КАК ТаблицаВНА
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииВНАМСФО.СрезПоследних(
	|				&ГраницаПредПериод,
	|				Сценарий = &Сценарий
	|					И Организация = &Организация
	|					И ВидУчета = &ВидУчетаНСБУ
	|					И ВНА В
	|						(ВЫБРАТЬ
	|							ТаблицаВНА.ВНА
	|						ИЗ
	|							ТаблицаВНА КАК ТаблицаВНА)) КАК ПараметрыАмортизацииВНАМСФОСрезПоследних
	|		ПО ТаблицаВНА.ВНА = ПараметрыАмортизацииВНАМСФОСрезПоследних.ВНА
	|ГДЕ
	|	ТаблицаВНА.СрокПолезногоИспользованияНСБУ <> ЕСТЬNULL(ПараметрыАмортизацииВНАМСФОСрезПоследних.СрокПолезногоИспользования, 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПараметрыУчетаВНАМСФО.ВНА,
	|	""ПараметрыУчетаВНАМСФО.СпособНачисленияАмортизации"" КАК СпособНачисленияАмортизации,
	|	ПараметрыУчетаВНАМСФО.СчетУчетаПервоначальнойСтоимости,
	|	ПараметрыУчетаВНАМСФО.СчетНакопленнойАмортизации
	|ИЗ
	|	РегистрСведений.ПараметрыУчетаВНАМСФО.СрезПоследних(
	|			&ГраницаПредПериод,
	|			Сценарий = &Сценарий
	|				И Организация = &Организация
	|				И ВидУчета = &ВидУчетаНСБУ
	|				И ВНА В
	|					(ВЫБРАТЬ
	|						ТаблицаВНА.ВНА
	|					ИЗ
	|						ТаблицаВНА КАК ТаблицаВНА)) КАК ПараметрыУчетаВНАМСФО";
		

КонецФункции

#Область ТекстЗапросаФормированиеСобытий

Функция ТекстЗапросаОчередьСобытий(НомераТаблиц)

	НомераТаблиц.Вставить("ОчередьСобытий", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОчередьСобытий.ИмяДокумента КАК ИмяДокумента,
	|	ОчередьСобытий.ИмяДокумента КАК ВидСобытияВНА,
	|	ОчередьСобытий.ДатаСобытия КАК ДатаСобытия,
	|	ЗНАЧЕНИЕ(Перечисление.РежимЗаполненияВидовУчета.НСБУИМСФО) КАК РежимЗаполнения,
	|	ОчередьСобытий.ПорядокСобытий КАК ПорядокСобытий
	|ИЗ
	|	(ВЫБРАТЬ
	|		""ПоступлениеВНАМСФО"" КАК ИмяДокумента,
	|		ТаблицаВНА.ДатаВводаНСБУ КАК ДатаСобытия,
	|		1 КАК ПорядокСобытий
	|	ИЗ
	|		ТаблицаВНА КАК ТаблицаВНА
	|	ГДЕ
	|		ТаблицаВНА.ДатаВводаНСБУ МЕЖДУ &ДатаНачалаПериода И &ДатаОкончанияПериода
	|		И ТаблицаВНА.ГруппаВНА.КлассВНА В (ЗНАЧЕНИЕ(Перечисление.КлассыВНА.НезавершенноеСтроительство))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		""ВводВНАВЭксплуатациюМСФО"",
	|		ТаблицаВНА.ДатаВводаНСБУ,
	|		2
	|	ИЗ
	|		ТаблицаВНА КАК ТаблицаВНА
	|	ГДЕ
	|		ТаблицаВНА.ДатаВводаНСБУ МЕЖДУ &ДатаНачалаПериода И &ДатаОкончанияПериода
	|		И ТаблицаВНА.ГруппаВНА.КлассВНА В (ЗНАЧЕНИЕ(Перечисление.КлассыВНА.ОсновноеСредство), ЗНАЧЕНИЕ(Перечисление.КлассыВНА.НематериальныйАктив), ЗНАЧЕНИЕ(Перечисление.КлассыВНА.БиологическийАктив), ЗНАЧЕНИЕ(Перечисление.КлассыВНА.ИнвестиционноеИмущество))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		""МодернизацияВНАМСФО"",
	|		ТаблицаВНА.ДатаМодернизацииНСБУ,
	|		4
	|	ИЗ
	|		ТаблицаВНА КАК ТаблицаВНА
	|	ГДЕ
	|		ТаблицаВНА.ДатаМодернизацииНСБУ <> &ПустаяДата
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		""ПереоценкаВНАМСФО"",
	|		ТаблицаВНА.ДатаПереоценкиНСБУ,
	|		6
	|	ИЗ
	|		ТаблицаВНА КАК ТаблицаВНА
	|	ГДЕ
	|		ТаблицаВНА.ДатаПереоценкиНСБУ <> &ПустаяДата
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		""ВыбытиеВНАМСФО"",
	|		ТаблицаВНА.ДатаВыбытияНСБУ,
	|		7
	|	ИЗ
	|		ТаблицаВНА КАК ТаблицаВНА
	|	ГДЕ
	|		ТаблицаВНА.ДатаВыбытияНСБУ <> &ПустаяДата
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		""НачислениеАмортизацииВНАМСФО"",
	|		ВЫБОР
	|			КОГДА ТаблицаВНА.ДатаВыбытияНСБУ = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА &ДатаОкончанияПериода
	|			ИНАЧЕ ДОБАВИТЬКДАТЕ(ТаблицаВНА.ДатаВыбытияНСБУ, ДЕНЬ, -1)
	|		КОНЕЦ,
	|		5
	|	ИЗ
	|		ТаблицаВНА КАК ТаблицаВНА
	|	ГДЕ
	|		ТаблицаВНА.АмортизацияНСБУ <> 0) КАК ОчередьСобытий
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаСобытия,
	|	ПорядокСобытий";

КонецФункции

Функция ТекстЗапросаПоступлениеВНАМСФО(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаВНА_ПоступлениеВНАМСФО", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	ТаблицаВНА.ДатаВводаНСБУ КАК ДатаДокумента,
	|	ТаблицаВНА.ВНА,
	|	ТаблицаВНА.СпособПоступления КАК СпособПоступленияВНА,
	|	ТаблицаВНА.ГруппаВНА КАК ГруппаВНАМСФО,
	|	ТаблицаВНА.КоличествоНСБУ КАК КоличествоНСБУ,
	|	ТаблицаВНА.КоличествоНСБУ КАК КоличествоМСФО,	
	|	ТаблицаВНА.СтоимостьНСБУ КАК СуммаНСБУ,
	|	ТаблицаВНА.СтоимостьМСФО КАК СуммаМСФО,
	|	ТаблицаВНА.СчетУчетаНСБУ КАК СчетУчетаНСБУ,
	|	ТаблицаВНА.СчетУчетаМСФО КАК СчетУчетаМСФО,
	|	ТаблицаВНА.СчетУчетаСубконто1НСБУ КАК СчетУчетаСубконто1НСБУ,
	|	ТаблицаВНА.СчетУчетаСубконто2НСБУ КАК СчетУчетаСубконто2НСБУ,
	|	ТаблицаВНА.СчетУчетаСубконто3НСБУ КАК СчетУчетаСубконто3НСБУ,
	|	ТаблицаВНА.СчетУчетаСубконто1МСФО КАК СчетУчетаСубконто1МСФО,
	|	ТаблицаВНА.СчетУчетаСубконто2МСФО КАК СчетУчетаСубконто2МСФО,
	|	ТаблицаВНА.СчетУчетаСубконто3МСФО КАК СчетУчетаСубконто3МСФО,
	|	ТаблицаВНА.КоррСчетПриобретениеНСБУ КАК КоррСчетНСБУ,
	|	ТаблицаВНА.КоррСчетПриобретениеМСФО КАК КоррСчетМСФО,
	|	ТаблицаВНА.КоррСчетПриобретениеСубконто1НСБУ КАК КоррСчетСубконто1НСБУ,
	|	ТаблицаВНА.КоррСчетПриобретениеСубконто2НСБУ КАК КоррСчетСубконто2НСБУ,
	|	ТаблицаВНА.КоррСчетПриобретениеСубконто3НСБУ КАК КоррСчетСубконто3НСБУ,
	|	ТаблицаВНА.КоррСчетПриобретениеСубконто1МСФО КАК КоррСчетСубконто1МСФО,
	|	ТаблицаВНА.КоррСчетПриобретениеСубконто2МСФО КАК КоррСчетСубконто2МСФО,
	|	ТаблицаВНА.КоррСчетПриобретениеСубконто3МСФО КАК КоррСчетСубконто3МСФО
	|ИЗ
	|	ТаблицаВНА КАК ТаблицаВНА
	|ГДЕ
	|	ТаблицаВНА.ДатаВводаНСБУ МЕЖДУ &ДатаНачалаПериода И &ДатаОкончанияПериода
	|	И ТаблицаВНА.ГруппаВНА.КлассВНА В (ЗНАЧЕНИЕ(Перечисление.КлассыВНА.НезавершенноеСтроительство))";	
	
КонецФункции

Функция ТекстЗапросаВводВНАВЭксплуатациюМСФО(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаВНА_ВводВНАВЭксплуатациюМСФО", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	ТаблицаВНА.ДатаВводаНСБУ КАК ДатаДокумента,
	|	ТаблицаВНА.ВНА,
	|	ТаблицаВНА.ГруппаВНА КАК ГруппаВНА,
	|	ТаблицаВНА.ПараметрыАмортизацииВНАМСФО,
	|	ТаблицаВНА.ИнвентарныйНомерНСБУ,
	|	ТаблицаВНА.ИнвентарныйНомерМСФО,	
	|	ТаблицаВНА.СтоимостьНСБУ КАК ПервоначальнаяСтоимостьНСБУ,
	|	ТаблицаВНА.СтоимостьМСФО КАК ПервоначальнаяСтоимостьМСФО,
	|	ТаблицаВНА.СпособНачисленияАмортизацииНСБУ,
	|	ТаблицаВНА.СпособНачисленияАмортизацииМСФО,
	|	ТаблицаВНА.НачислятьАмортизациюМСФО,
	|	ТаблицаВНА.НачислятьАмортизациюНСБУ,
	|	ТаблицаВНА.ЛиквидационнаяСтоимостьНСБУ,
	|	ТаблицаВНА.ЛиквидационнаяСтоимостьМСФО,
	|	ТаблицаВНА.СчетУчетаНСБУ КАК СчетУчетаНСБУ,
	|	ТаблицаВНА.СчетУчетаМСФО КАК СчетУчетаМСФО,
	|	ТаблицаВНА.СчетАмортизацииНСБУ КАК СчетАмортизацииНСБУ,
	|	ТаблицаВНА.СчетАмортизацииМСФО КАК СчетАмортизацииМСФО,
	|	ТаблицаВНА.СчетВыбытияВНАНСБУ КАК СчетВыбытияНСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАМСФО КАК СчетВыбытияМСФО,
	|	ТаблицаВНА.СчетУчетаСубконто1НСБУ КАК СчетУчетаСубконто1НСБУ,
	|	ТаблицаВНА.СчетУчетаСубконто2НСБУ КАК СчетУчетаСубконто2НСБУ,
	|	ТаблицаВНА.СчетУчетаСубконто3НСБУ КАК СчетУчетаСубконто3НСБУ,
	|	ТаблицаВНА.СчетУчетаСубконто1МСФО КАК СчетУчетаСубконто1МСФО,
	|	ТаблицаВНА.СчетУчетаСубконто2МСФО КАК СчетУчетаСубконто2МСФО,
	|	ТаблицаВНА.СчетУчетаСубконто3МСФО КАК СчетУчетаСубконто3МСФО,
	|	ТаблицаВНА.СчетАмортизацииСубконто1НСБУ КАК СчетАмортизацииСубконто1НСБУ,
	|	ТаблицаВНА.СчетАмортизацииСубконто2НСБУ КАК СчетАмортизацииСубконто2НСБУ,
	|	ТаблицаВНА.СчетАмортизацииСубконто3НСБУ КАК СчетАмортизацииСубконто3НСБУ,
	|	ТаблицаВНА.СчетАмортизацииСубконто1МСФО КАК СчетАмортизацииСубконто1МСФО,
	|	ТаблицаВНА.СчетАмортизацииСубконто2МСФО КАК СчетАмортизацииСубконто2МСФО,
	|	ТаблицаВНА.СчетАмортизацииСубконто3МСФО КАК СчетАмортизацииСубконто3МСФО,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто1НСБУ КАК СчетВыбытияСубконто1НСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто2НСБУ КАК СчетВыбытияСубконто2НСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто3НСБУ КАК СчетВыбытияСубконто3НСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто1МСФО КАК СчетВыбытияСубконто1МСФО,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто2МСФО КАК СчетВыбытияСубконто2МСФО,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто3МСФО КАК СчетВыбытияСубконто3МСФО,
	|	ТаблицаВНА.СпособОтраженияРасходовПоАмортизацииНСБУ,
	|	ТаблицаВНА.СпособОтраженияРасходовПоАмортизацииМСФО,
	|	ТаблицаВНА.СчетЗатратСубконто1НСБУ,
	|	ТаблицаВНА.СчетЗатратСубконто2НСБУ,
	|	ТаблицаВНА.СчетЗатратСубконто3НСБУ,
	|	ТаблицаВНА.СчетЗатратМСФО,
	|	ТаблицаВНА.НачислятьАмортизациюНСБУ КАК НачислятьАмортизациюНСБУ1,
	|	ТаблицаВНА.НачислятьАмортизациюМСФО КАК НачислятьАмортизациюМСФО1,
	|	ТаблицаВНА.СпособНачисленияАмортизацииНСБУ КАК СпособНачисленияАмортизацииНСБУ1,
	|	ТаблицаВНА.СпособНачисленияАмортизацииМСФО КАК СпособНачисленияАмортизацииМСФО1,
	|	ТаблицаВНА.СрокПолезногоИспользованияНСБУ,
	|	ТаблицаВНА.СрокПолезногоИспользованияМСФО,
	|	ТаблицаВНА.СчетЗатратСубконто1МСФО,
	|	ТаблицаВНА.СчетЗатратСубконто2МСФО,
	|	ТаблицаВНА.СчетЗатратСубконто3МСФО,
	|	ТаблицаВНА.КоэффициентУскоренияНСБУ,
	|	ТаблицаВНА.КоэффициентУскоренияМСФО,
	|	ТаблицаВНА.КоррСчетПриобретениеНСБУ КАК СчетПоступленияНСБУ,
	|	ТаблицаВНА.КоррСчетПриобретениеМСФО КАК СчетПоступленияМСФО,
	|	ТаблицаВНА.КоррСчетПриобретениеСубконто1НСБУ КАК СчетПоступленияСубконто1НСБУ,
	|	ТаблицаВНА.КоррСчетПриобретениеСубконто2НСБУ КАК СчетПоступленияСубконто2НСБУ,
	|	ТаблицаВНА.КоррСчетПриобретениеСубконто3НСБУ КАК СчетПоступленияСубконто3НСБУ,
	|	ТаблицаВНА.КоррСчетПриобретениеСубконто1МСФО КАК СчетПоступленияСубконто1МСФО,
	|	ТаблицаВНА.КоррСчетПриобретениеСубконто2МСФО КАК СчетПоступленияСубконто2МСФО,
	|	ТаблицаВНА.КоррСчетПриобретениеСубконто3МСФО КАК СчетПоступленияСубконто3МСФО
	|ИЗ
	|	ТаблицаВНА КАК ТаблицаВНА
	|ГДЕ
	|	ТаблицаВНА.ДатаВводаНСБУ МЕЖДУ &ДатаНачалаПериода И &ДатаОкончанияПериода
	|	И ТаблицаВНА.ГруппаВНА.КлассВНА В (ЗНАЧЕНИЕ(Перечисление.КлассыВНА.ОсновноеСредство), ЗНАЧЕНИЕ(Перечисление.КлассыВНА.НематериальныйАктив), ЗНАЧЕНИЕ(Перечисление.КлассыВНА.БиологическийАктив), ЗНАЧЕНИЕ(Перечисление.КлассыВНА.ИнвестиционноеИмущество))";	
	
КонецФункции

Функция ТекстЗапросаПереоценкаВНАМСФО(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаВНА_ПереоценкаВНАМСФО", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	ТаблицаВНА.ВНА,
	|	ТаблицаВНА.ГруппаВНА КАК ГруппаВНАМСФО,
	|	ТаблицаВНА.ГруппаВНА.МодельУчетаВНА КАК МодельУчетаВНА,
	|	ТаблицаВНА.ПараметрыАмортизацииВНАМСФО,
	|	ТаблицаВНА.ИнвентарныйНомерНСБУ,
	|	ТаблицаВНА.ИнвентарныйНомерМСФО,
	|	ТаблицаВНА.ДатаПереоценкиНСБУ КАК ДатаДокумента,
	|	ТаблицаВНА.ДатаПереоценкиНСБУ КАК ДатаПринятияКУчетуНСБУ,
	|	ТаблицаВНА.ДатаПереоценкиМСФО КАК ДатаПринятияКУчетуМСФО,
	|	ТаблицаВНА.ПереоценкаНСБУ КАК ПереоцененнаяСтоимостьНСБУ,
	|	ТаблицаВНА.ПереоценкаМСФО КАК ПереоцененнаяСтоимостьМСФО
	|ИЗ
	|	ТаблицаВНА КАК ТаблицаВНА
	|ГДЕ
	|	ТаблицаВНА.ДатаПереоценкиНСБУ МЕЖДУ &ДатаНачалаПериода И &ДатаОкончанияПериода";	
	
КонецФункции

Функция ТекстЗапросаМодернизацияВНАМСФО(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаВНА_МодернизацияВНАМСФО", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	ТаблицаВНА.ВНА,
	|	ТаблицаВНА.ГруппаВНА КАК ГруппаВНА,	
	|	ТаблицаВНА.ПараметрыАмортизацииВНАМСФО,
	|	ТаблицаВНА.ДатаМодернизацииНСБУ КАК ДатаДокумента,
	|	ТаблицаВНА.ДатаВводаНСБУ КАК ДатаВводаНСБУ,
	|	ТаблицаВНА.ДатаВводаМСФО КАК ДатаВводаМСФО,	
	|	ТаблицаВНА.ДатаМодернизацииНСБУ КАК ДатаСобытияНСБУ,
	|	ТаблицаВНА.ДатаМодернизацииМСФО КАК ДатаСобытияМСФО,	
	|	ТаблицаВНА.СрокПолезногоИспользованияНСБУ КАК НовыйСПИНСБУ,
	|	ТаблицаВНА.СрокПолезногоИспользованияМСФО КАК НовыйСПИМСФО,	
	|	ТаблицаВНА.МодернизацияНСБУ КАК СуммаМодернизацииНСБУ,
	|	ТаблицаВНА.МодернизацияМСФО КАК СуммаМодернизацииМСФО,
	|	ТаблицаВНА.КоррСчетМодернизацияНСБУ КАК СчетЗатратНСБУ,
	|	ТаблицаВНА.КоррСчетМодернизацияМСФО КАК СчетЗатратМСФО,
	|	ТаблицаВНА.КоррСчетМодернизацияСубконто1НСБУ КАК СчетЗатратСубконто1НСБУ,
	|	ТаблицаВНА.КоррСчетМодернизацияСубконто2НСБУ КАК СчетЗатратСубконто2НСБУ,
	|	ТаблицаВНА.КоррСчетМодернизацияСубконто3НСБУ КАК СчетЗатратСубконто3НСБУ,
	|	ТаблицаВНА.КоррСчетМодернизацияСубконто1МСФО КАК СчетЗатратСубконто1МСФО,
	|	ТаблицаВНА.КоррСчетМодернизацияСубконто2МСФО КАК СчетЗатратСубконто2МСФО,
	|	ТаблицаВНА.КоррСчетМодернизацияСубконто3МСФО КАК СчетЗатратСубконто3МСФО
	|ИЗ
	|	ТаблицаВНА КАК ТаблицаВНА
	|ГДЕ
	|	ТаблицаВНА.ДатаМодернизацииНСБУ МЕЖДУ &ДатаНачалаПериода И &ДатаОкончанияПериода";	
	
КонецФункции

Функция ТекстЗапросаВыбытиеВНАМСФО(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаВНА_ВыбытиеВНАМСФО", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	ТаблицаВНА.ВНА,
	|	ТаблицаВНА.ГруппаВНА КАК ГруппаВНАМСФО,
	|	ТаблицаВНА.ДатаВыбытияНСБУ КАК ДатаДокумента,
	|	ТаблицаВНА.ДатаВыбытияНСБУ КАК ДатаСобытияНСБУ,
	|	ТаблицаВНА.ДатаВыбытияМСФО КАК ДатаСобытияМСФО,
	|	ТаблицаВНА.ВидОперацииВыбытия КАК СобытиеВНА,
	|	ТаблицаВНА.ГруппаВНА,
	|	ТаблицаВНА.ГруппаВНА.МодельУчетаВНА КАК МодельУчетаВНА,
	|	ТаблицаВНА.ВыбытиеНСБУ КАК ВыбытиеСтоимостиНСБУ,
	|	ТаблицаВНА.ВыбытиеМСФО КАК ВыбытиеСтоимостиМСФО,
	|	ТаблицаВНА.АмортизацияНСБУ КАК ВыбытиеАмортизацииНСБУ,
	|	ТаблицаВНА.АмортизацияМСФО КАК ВыбытиеАмортизацииМСФО,
	|	ТаблицаВНА.ВыбытиеНСБУ КАК БалансоваяСтоимостьНСБУ,
	|	ТаблицаВНА.ВыбытиеМСФО КАК БалансоваяСтоимостьМСФО,
	|	ТаблицаВНА.АмортизацияНСБУ КАК АмортизацияЗаПериодНСБУ,
	|	ТаблицаВНА.АмортизацияМСФО КАК АмортизацияЗаПериодМСФО,
	|	0 КАК ВыбытиеПереоценкиНСБУ,
	|	0 КАК ВыбытиеПереоценкиМСФО,
	|	ТаблицаВНА.СчетВыбытияВНАНСБУ КАК СчетВыбытияВНАНСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАМСФО КАК СчетВыбытияВНАМСФО,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто1НСБУ КАК СчетВыбытияВНАСубконто1НСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто2НСБУ КАК СчетВыбытияВНАСубконто2НСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто3НСБУ КАК СчетВыбытияВНАСубконто3НСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто1МСФО КАК СчетВыбытияВНАСубконто1МСФО,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто2МСФО КАК СчетВыбытияВНАСубконто2МСФО,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто3МСФО КАК СчетВыбытияВНАСубконто3МСФО,
	|	ТаблицаВНА.КоррСчетВыбытиеНСБУ КАК СчетРасходаНСБУ,
	|	ТаблицаВНА.КоррСчетВыбытиеСубконто1НСБУ КАК СчетРасходаСубконто1НСБУ,
	|	ТаблицаВНА.КоррСчетВыбытиеСубконто2НСБУ КАК СчетРасходаСубконто2НСБУ,
	|	ТаблицаВНА.КоррСчетВыбытиеСубконто3НСБУ КАК СчетРасходаСубконто3НСБУ,
	|	ТаблицаВНА.КоррСчетВыбытиеМСФО КАК СчетРасходаМСФО,
	|	ТаблицаВНА.КоррСчетВыбытиеСубконто1МСФО КАК СчетРасходаСубконто1МСФО,
	|	ТаблицаВНА.КоррСчетВыбытиеСубконто2МСФО КАК СчетРасходаСубконто2МСФО,
	|	ТаблицаВНА.КоррСчетВыбытиеСубконто3МСФО КАК СчетРасходаСубконто3МСФО,
	|	ТаблицаВНА.ЧастичноеВыбытиеНСБУ,
	|	ТаблицаВНА.ЧастичноеВыбытиеМСФО
	|ИЗ
	|	ТаблицаВНА КАК ТаблицаВНА
	|ГДЕ
	|	ТаблицаВНА.ДатаВыбытияНСБУ МЕЖДУ &ДатаНачалаПериода И &ДатаОкончанияПериода";	
	
КонецФункции

Функция ТекстЗапросаНачислениеАмортизацииВНАМСФО(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаВНА_НачислениеАмортизацииВНАМСФО", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	ТаблицаВНА.ВНА,
	|	ТаблицаВНА.ГруппаВНА КАК ГруппаВНАМСФО,
	|	ВЫБОР
	|		КОГДА ТаблицаВНА.ДатаВыбытияНСБУ = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА &ДатаОкончанияПериода
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(ТаблицаВНА.ДатаВыбытияНСБУ, ДЕНЬ, -1)
	|	КОНЕЦ КАК ДатаДокумента,
	|	ВЫБОР
	|		КОГДА ТаблицаВНА.ДатаВыбытияНСБУ = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА &ДатаОкончанияПериода
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(ТаблицаВНА.ДатаВыбытияНСБУ, ДЕНЬ, -1)
	|	КОНЕЦ КАК ДатаСобытияНСБУ,
	|	ВЫБОР
	|		КОГДА ТаблицаВНА.ДатаВыбытияНСБУ = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА &ДатаОкончанияПериода
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(ТаблицаВНА.ДатаВыбытияНСБУ, ДЕНЬ, -1)
	|	КОНЕЦ КАК ДатаСобытияМСФО,
	|	ТаблицаВНА.СчетЗатратНСБУ КАК СчетЗатратНСБУ,
	|	ТаблицаВНА.СчетЗатратМСФО КАК СчетЗатратТрансляцииМСФО,
	|	ТаблицаВНА.СчетЗатратМСФО КАК СчетЗатратМСФО,
	|	ТаблицаВНА.СчетЗатратСубконто1НСБУ КАК СчетЗатратСубконто1НСБУ,
	|	ТаблицаВНА.СчетЗатратСубконто2НСБУ КАК СчетЗатратСубконто2НСБУ,
	|	ТаблицаВНА.СчетЗатратСубконто3НСБУ КАК СчетЗатратСубконто3НСБУ,
	|	ТаблицаВНА.СчетЗатратСубконто1МСФО КАК СчетЗатратСубконто1МСФО,
	|	ТаблицаВНА.СчетЗатратСубконто2МСФО КАК СчетЗатратСубконто2МСФО,
	|	ТаблицаВНА.СчетЗатратСубконто3МСФО КАК СчетЗатратСубконто3МСФО,
	|	ТаблицаВНА.АмортизацияНСБУ КАК АмортизацияЗаПериодНСБУ,
	|	ТаблицаВНА.АмортизацияМСФО КАК АмортизацияЗаПериодМСФО
	|ИЗ
	|	ТаблицаВНА КАК ТаблицаВНА
	|ГДЕ
	|	НЕ(ТаблицаВНА.АмортизацияНСБУ = 0
	|				И ТаблицаВНА.АмортизацияМСФО = 0)
	|	И ТаблицаВНА.ГруппаВНА.КлассВНА <> ЗНАЧЕНИЕ(Перечисление.КлассыВНА.НезавершенноеСтроительство)";	
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ЗаполнениеДокумента

Процедура ЗаполнитьЗависимостиРеквизитовДокумента(ФормаДокумента) Экспорт 
	
	ЗависимостиРеквизитов = Новый Массив;
	
	Реквизиты = Новый Структура;
		
	Реквизиты.Вставить("СчетУчетаМСФО", 			"СчетУчетаПервоначальнойСтоимости");
	Реквизиты.Вставить("СчетУчетаСубконто1МСФО", 	"СчетУчетаПервоначальнойСтоимостиСубконто1");
	Реквизиты.Вставить("СчетУчетаСубконто2МСФО", 	"СчетУчетаПервоначальнойСтоимостиСубконто2");
	Реквизиты.Вставить("СчетУчетаСубконто3МСФО", 	"СчетУчетаПервоначальнойСтоимостиСубконто3");
	
	Реквизиты.Вставить("СчетАмортизацииМСФО", 			"СчетНакопленнойАмортизации");
	Реквизиты.Вставить("СчетАмортизацииСубконто1МСФО", 	"СчетНакопленнойАмортизацииСубконто1");
	Реквизиты.Вставить("СчетАмортизацииСубконто2МСФО", 	"СчетНакопленнойАмортизацииСубконто2");
	Реквизиты.Вставить("СчетАмортизацииСубконто3МСФО", 	"СчетНакопленнойАмортизацииСубконто3");
	
	Для каждого Реквизит Из Реквизиты Цикл
	
		НоваяСтрока = Новый Структура;
		НоваяСтрока.Вставить("РеквизитГруппы", Реквизит.Значение);
		НоваяСтрока.Вставить("Приемник", Реквизит.Ключ);
		НоваяСтрока.Вставить("Источник", "ГруппаВНА");
		
		ЗависимостиРеквизитов.Добавить(НоваяСтрока);
		
	КонецЦикла;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("СпособНачисленияАмортизацииМСФО", 			"СпособНачисленияАмортизации");
	Реквизиты.Вставить("СрокПолезногоИспользованияМСФО", 			"СрокПолезногоИспользования");
	Реквизиты.Вставить("СпособОтраженияРасходовПоАмортизацииМСФО", 	"СпособОтраженияРасходовПоАмортизации");
	Реквизиты.Вставить("КоэффициентУскоренияМСФО", 					"КоэффициентУскорения");
	Реквизиты.Вставить("ПараметрВыработкиМСФО", 					"ПараметрВыработки");
	Реквизиты.Вставить("ПредполагаемыйОбъемВыработкиМСФО", 			"ПредполагаемыйОбъемПродукции");
	
	Для каждого Реквизит Из Реквизиты Цикл
	
		НоваяСтрока = Новый Структура;
		НоваяСтрока.Вставить("РеквизитГруппы", Реквизит.Значение);
		НоваяСтрока.Вставить("Приемник", Реквизит.Ключ);
		НоваяСтрока.Вставить("Источник", "ПараметрыАмортизацииВНАМСФО");
		
		ЗависимостиРеквизитов.Добавить(НоваяСтрока);
		
	КонецЦикла;
	
	ФормаДокумента.КэшируемыеЗначения.Вставить("ЗависимостиРеквизитов", ЗависимостиРеквизитов);
	
КонецПроцедуры                                                          

Функция ПолучитьИменаСубконто() Экспорт

	Результат = Новый Структура;
	
	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетУчетаСубконто1НСБУ");
	Субконто.Вставить(2, "СчетУчетаСубконто2НСБУ");
	Субконто.Вставить(3, "СчетУчетаСубконто3НСБУ");
	
	Результат.Вставить("СчетУчетаНСБУ", Субконто);
	
	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетУчетаСубконто1МСФО");
	Субконто.Вставить(2, "СчетУчетаСубконто2МСФО");
	Субконто.Вставить(3, "СчетУчетаСубконто3МСФО");
	
	Результат.Вставить("СчетУчетаМСФО", Субконто);

	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетАмортизацииСубконто1НСБУ");
	Субконто.Вставить(2, "СчетАмортизацииСубконто2НСБУ");
	Субконто.Вставить(3, "СчетАмортизацииСубконто3НСБУ");
	
	Результат.Вставить("СчетАмортизацииНСБУ", Субконто);

	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетАмортизацииСубконто1МСФО");
	Субконто.Вставить(2, "СчетАмортизацииСубконто2МСФО");
	Субконто.Вставить(3, "СчетАмортизацииСубконто3МСФО");
	
	Результат.Вставить("СчетАмортизацииМСФО", Субконто);
	
	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетВыбытияВНАСубконто1НСБУ");
	Субконто.Вставить(2, "СчетВыбытияВНАСубконто2НСБУ");
	Субконто.Вставить(3, "СчетВыбытияВНАСубконто3НСБУ");
	
	Результат.Вставить("СчетВыбытияВНАНСБУ", Субконто);

	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетВыбытияВНАСубконто1МСФО");
	Субконто.Вставить(2, "СчетВыбытияВНАСубконто2МСФО");
	Субконто.Вставить(3, "СчетВыбытияВНАСубконто3МСФО");
	
	Результат.Вставить("СчетВыбытияВНАМСФО", Субконто);

	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетЗатратСубконто1НСБУ");
	Субконто.Вставить(2, "СчетЗатратСубконто2НСБУ");
	Субконто.Вставить(3, "СчетЗатратСубконто3НСБУ");
	
	Результат.Вставить("СчетЗатратНСБУ", Субконто);

	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетЗатратСубконто1МСФО");
	Субконто.Вставить(2, "СчетЗатратСубконто2МСФО");
	Субконто.Вставить(3, "СчетЗатратСубконто3МСФО");
	
	Результат.Вставить("СчетЗатратМСФО", Субконто);
	
	#Область СубконтоКоррСчета
	
	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "КоррСчетПриобретениеСубконто1НСБУ");
	Субконто.Вставить(2, "КоррСчетПриобретениеСубконто2НСБУ");
	Субконто.Вставить(3, "КоррСчетПриобретениеСубконто3НСБУ");
	
	Результат.Вставить("КоррСчетПриобретениеНСБУ", Субконто);
	
	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "КоррСчетПриобретениеСубконто1МСФО");
	Субконто.Вставить(2, "КоррСчетПриобретениеСубконто2МСФО");
	Субконто.Вставить(3, "КоррСчетПриобретениеСубконто3МСФО");
	
	Результат.Вставить("КоррСчетПриобретениеМСФО", Субконто);

	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "КоррСчетМодернизацияСубконто1НСБУ");
	Субконто.Вставить(2, "КоррСчетМодернизацияСубконто2НСБУ");
	Субконто.Вставить(3, "КоррСчетМодернизацияСубконто3НСБУ");
	
	Результат.Вставить("КоррСчетМодернизацияНСБУ", Субконто);
	
	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "КоррСчетМодернизацияСубконто1МСФО");
	Субконто.Вставить(2, "КоррСчетМодернизацияСубконто2МСФО");
	Субконто.Вставить(3, "КоррСчетМодернизацияСубконто3МСФО");
	
	Результат.Вставить("КоррСчетМодернизацияМСФО", Субконто);

	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "КоррСчетВыбытиеСубконто1НСБУ");
	Субконто.Вставить(2, "КоррСчетВыбытиеСубконто2НСБУ");
	Субконто.Вставить(3, "КоррСчетВыбытиеСубконто3НСБУ");
	
	Результат.Вставить("КоррСчетВыбытиеНСБУ", Субконто);
	
	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "КоррСчетВыбытиеСубконто1МСФО");
	Субконто.Вставить(2, "КоррСчетВыбытиеСубконто2МСФО");
	Субконто.Вставить(3, "КоррСчетВыбытиеСубконто3МСФО");
	
	Результат.Вставить("КоррСчетВыбытиеМСФО", Субконто);

	#КонецОбласти
	
	ПоляИсключений = "КоррСчетВыбытиеМСФО,КоррСчетМодернизацияМСФО,КоррСчетПриобретениеМСФО,
					 |КоррСчетВыбытиеНСБУ,КоррСчетМодернизацияНСБУ,КоррСчетПриобретениеНСБУ";
	Результат.Вставить("ИсключенияСкрытияСубконто", Новый Структура(ПоляИсключений));
	
	Возврат Результат;
		
КонецФункции

#КонецОбласти

#КонецЕсли
