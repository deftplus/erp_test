#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ВыполнитьЗаменуДублей(ИсходныйЭлемент,ДублирующийЭлемент,Отказ)
	
	МассивДублей=Новый Массив;
	МассивДублей.Добавить(ДублирующийЭлемент);
	
	Отказ=НЕ ОбщегоНазначенияУХ.ВыполнитьЗаменуЭлементов(ИсходныйЭлемент,МассивДублей);
	
КонецПроцедуры // ВыполнитьЗаменуДублей 

Процедура ВыполнитьДействияПоОбъекту(Отказ,ОтменаОперации) Экспорт
	
	Если ВидОперации=Перечисления.ВидыОперацийИзмененияНСИ.УдалениеДубликатаОбъекта Тогда
		
		Если ОтменаОперации Тогда
			
			ВыполнитьЗаменуДублей(ИзменяемыйОбъект,ОбразцовыйОбъект,Отказ);
			
		Иначе
			
			ВыполнитьЗаменуДублей(ОбразцовыйОбъект,ИзменяемыйОбъект,Отказ);
			
		КонецЕсли;
		
	Иначе
		
		СправочникОбъект=ИзменяемыйОбъект.ПолучитьОбъект();
		
		Если ВидОперации=Перечисления.ВидыОперацийИзмененияНСИ.РегистрацияНовогоОбъекта Тогда
			
			СправочникОбъект.НСИ_НеАктивный=ОтменаОперации;
			
		ИначеЕсли ВидОперации=Перечисления.ВидыОперацийИзмененияНСИ.УдалениеОбъекта Тогда
			
			СправочникОбъект=ИзменяемыйОбъект.ПолучитьОбъект();
			СправочникОбъект.ПометкаУдаления=Не ОтменаОперации;
			
		ИначеЕсли ВидОперации=Перечисления.ВидыОперацийИзмененияНСИ.ИзменениеРеквизитовОбъекта Тогда
			
			СправочникОбъект=ИзменяемыйОбъект.ПолучитьОбъект();
			
			Для Каждого СтрИзменение Из ИзменениеРеквизитов Цикл
				
				СправочникОбъект[СтрИзменение.ИмяРеквизита]=?(ОтменаОперации,СтрИзменение.ИсходноеЗначение,СтрИзменение.НовоеЗначение);
				
			КонецЦикла;
			
		КонецЕсли;
		
		ТекстОшибки=УправлениеНСИ.ПроверитьКонтролируемыеРеквизитыОбъекта(СправочникОбъект);
		
		Если НЕ ПустаяСтрока(ТекстОшибки) Тогда
						
			ОбщегоНазначенияУХ.СообщитьОбОшибке(СтрШаблон(Нстр("ru = 'Запись объекта %1 отменена по причине:
			|%2'"), ИзменяемыйОбъект, ТекстОшибки),Отказ,,СтатусСообщения.Важное);
			
			Возврат
			
		КонецЕсли;
		
		ТекущийПользователь = Пользователи.АвторизованныйПользователь();
		УправлениеНСИ.УстановитьОтметкуРедактирования(СправочникОбъект,ТекущийПользователь);
		УправлениеНСИ.ОтметитьИзменениеКонтролируемогоЭлемента(СправочникОбъект,Отказ);
				
		Попытка
			
			СправочникОбъект.ОбменДанными.Загрузка=Истина;		
			СправочникОбъект.Записать();
			
		Исключение
			
			ОбщегоНазначенияУХ.СообщитьОбОшибке(СтрШаблон(Нстр("ru = 'Не удалось изменить объект %1
			|:%2'"), ИзменяемыйОбъект, ОписаниеОшибки()),Отказ,,СтатусСообщения.Важное);
			
		КонецПопытки;
		
	КонецЕсли;
		
КонецПроцедуры // ВыполнитьДействияПоОбъекту() 

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	//Проверим - является ли объект контролируемым
	Если НЕ СправочникБД.Контролируемый Тогда
		ОбщегоНазначенияУХ.СообщитьОбОшибке(Нстр("ru = 'Справочник не является контролируемым!
		|Работа через заявки НСИ возможна только для контролируемых справочников'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Состояние = УправлениеПроцессамиСогласованияУХ.ВернутьСтатусОбъекта(Ссылка);

	Если ПометкаУдаления И (НЕ Состояние=Перечисления.СостоянияСогласования.НаУтверждении) Тогда
		
		УправлениеПроцессамиСогласованияУХ.ПеревестиЗаявкуВСостояниеОтклонена(Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли
