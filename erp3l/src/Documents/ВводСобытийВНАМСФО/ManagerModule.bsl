
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ПодготовкаПараметровПроведенияДокумента

Функция ПодготовитьПараметрыПроведения(ДополнительныеСвойства, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДополнительныеСвойства.ДляПроведения.Ссылка);
	
	ДопРеквизиты = Новый Структура("ШаблонТрансляции, РежимЗаполнения, АлгоритмРСБУ, ПериодОтчета, ДатаНачала, ДатаОкончания, ВыполнятьПроверкуОстатков", 
									"УП.ШаблонТрансляции");
	Реквизиты = МСФОУХ.РеквизитыДокумента(Запрос, ДопРеквизиты, "ПериодОтчета", Отказ);
	Реквизиты.Вставить("ДанныеРежимаЗаполнения", 	УчетМСФОПовтИспУХ.ПолучитьВидыУчетаРежимаЗаполнения(Реквизиты.РежимЗаполнения));
	ДополнительныеСвойства.ДляПроведения.Вставить("Реквизиты", Реквизиты);
	
	Если Отказ Тогда
		Возврат ДополнительныеСвойства;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Период",				Реквизиты.Период);
	Запрос.УстановитьПараметр("ДатаГраница",		Новый Граница(Реквизиты.Период, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ГраницаДоДокумента",	Новый Граница(Реквизиты.Период, ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Организация",		Реквизиты.Организация);
	Запрос.УстановитьПараметр("Сценарий",			Реквизиты.Сценарий);
	Запрос.УстановитьПараметр("ПериодОтчета", 		Реквизиты.ПериодОтчета);
	
	Запрос.УстановитьПараметр("ЕстьНСБУ", 					Реквизиты.ДанныеРежимаЗаполнения.ЕстьНСБУ);
	Запрос.УстановитьПараметр("ЕстьМСФО", 					Реквизиты.ДанныеРежимаЗаполнения.ЕстьМСФО);
	Запрос.УстановитьПараметр("ВидУчетаНСБУ", 				Реквизиты.ДанныеРежимаЗаполнения.НСБУ);	
	Запрос.УстановитьПараметр("ВидУчетаМСФО", 				Реквизиты.ДанныеРежимаЗаполнения.МСФО);
	Запрос.УстановитьПараметр("ОтборВидУчетаНСБУ", 			ПредопределенноеЗначение("Перечисление.ВидыУчета.НСБУ"));
	Запрос.УстановитьПараметр("ФормироватьПроводкиМСФО", 	Реквизиты.ФормироватьПроводкиМСФО);
	Запрос.УстановитьПараметр("ФункциональнаяВалюта",		Реквизиты.ФункциональнаяВалюта);
		
	Запрос.УстановитьПараметр("ПустаяДата",				Дата(1,1,1));
	Запрос.УстановитьПараметр("НачалоГода",				НачалоГода(Реквизиты.Период));
	Запрос.УстановитьПараметр("КонецГода",				КонецГода(Реквизиты.Период));
	Запрос.УстановитьПараметр("ГраницаПредПериод",		МСФОВНАВызовСервераУХ.ПолучитьГраницыПериодаДокумента(Реквизиты, -1).ДатаОкончания );
	
	СобытияДокументов = МСФОВНАУХ.ПолучитьСобытияДокументовВНА();
	Для каждого ДокументСобытие Из СобытияДокументов Цикл
		Запрос.Параметры.Вставить("События" + ДокументСобытие.Ключ, ДокументСобытие.Значение);
	КонецЦикла;	
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаТаблицаВНА(НомераТаблиц)	
	+ ?(Реквизиты.ВыполнятьПроверкуОстатков, ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета()
	+ ТекстЗапросаПроверкаОстатков(НомераТаблиц), "")	
	+ ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета()
	+ ТекстЗапросаТабличныеЧасти(НомераТаблиц);
	
	Результат = Запрос.ВыполнитьПакет();

	НомераТаблиц.Удалить("ТаблицаВНА");
	НомераТаблиц.Удалить("втДвиженияНСБУ");
	
	ТаблицыДляПроверки = Новый Структура;
	ТаблицыДляПроверки.Вставить("РасхождениеОстатковСумм");
	ТаблицыДляПроверки.Вставить("РасхождениеПараметровАмортизации");
	ТаблицыДляПроверки.Вставить("РасхождениеПараметровУчета"); 
	
	УдалитьКолонкиМСФО = НЕ Реквизиты.ДанныеРежимаЗаполнения.ЕстьМСФО;
	
	Для Каждого НомерТаблицы Из НомераТаблиц Цикл
		
		Если ТаблицыДляПроверки.Свойство(НомерТаблицы.Ключ) Тогда
		    ТаблицыДляПроверки.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
		Иначе	
			
			ТаблицаИсточник = Результат[НомерТаблицы.Значение].Выгрузить();
			Если УдалитьКолонкиМСФО Тогда
				МСФОВНАВызовСервераУХ.УдалитьКолонкиМСФО(ТаблицаИсточник);
			КонецЕсли; 
			
			ДополнительныеСвойства.ТаблицыДляДвижений.Вставить(НомерТаблицы.Ключ, ТаблицаИсточник);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДополнительныеСвойства.ДляПроведения.Вставить("ТаблицыДляПроверки", ТаблицыДляПроверки);
	
	Если Реквизиты.ВыполнятьПроверкуОстатков Тогда
		ПроверитьОстаткиПредыдущегоПериода(ДополнительныеСвойства, Отказ);	
	КонецЕсли;
	
	Возврат ДополнительныеСвойства;

КонецФункции

Функция ТекстЗапросаТаблицаВНА(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаВНА", НомераТаблиц.Количество());
	
	Возврат 	
	"ВЫБРАТЬ
	|	ТаблицаВНА.ВНА КАК ВНА,
	|	ТаблицаВНА.ПараметрыУчетаВНА КАК ПараметрыУчетаВНА,
	|	ТаблицаВНА.НомерСтроки КАК НомерСтроки,
	|	ТаблицаВНА.ДатаСобытияНСБУ КАК ДатаСобытияНСБУ,
	|	ТаблицаВНА.ДатаСобытияМСФО КАК ДатаСобытияМСФО,
	|	ТаблицаВНА.ВидОперации КАК ВидОперации,
	|	ТаблицаВНА.ПараметрыАмортизацииВНАМСФО КАК ПараметрыАмортизацииВНАМСФО,
	|	ТаблицаВНА.КоличествоНСБУ КАК КоличествоНСБУ,
	|	ТаблицаВНА.СуммаНСБУ КАК СуммаНСБУ,
	|	ТаблицаВНА.ЛиквидационнаяСтоимостьНСБУ КАК ЛиквидационнаяСтоимостьНСБУ,
	|	ТаблицаВНА.ЛиквидационнаяСтоимостьМСФО КАК ЛиквидационнаяСтоимостьМСФО,
	|	ТаблицаВНА.ПараметрыУчетаВНА.НачислятьАмортизацию КАК НачислятьАмортизациюНСБУ,
	|	ТаблицаВНА.ПараметрыУчетаВНА.НачислятьАмортизацию КАК НачислятьАмортизациюМСФО,
	|	ТаблицаВНА.СпособНачисленияАмортизацииНСБУ КАК СпособНачисленияАмортизацииНСБУ,
	|	ТаблицаВНА.СпособНачисленияАмортизацииМСФО КАК СпособНачисленияАмортизацииМСФО,
	|	ТаблицаВНА.СрокПолезногоИспользованияНСБУ КАК СрокПолезногоИспользованияНСБУ,
	|	ТаблицаВНА.СрокПолезногоИспользованияМСФО КАК СрокПолезногоИспользованияМСФО,
	|	ТаблицаВНА.СчетУчетаНСБУ КАК СчетУчетаНСБУ,
	|	ТаблицаВНА.СчетУчетаМСФО КАК СчетУчетаМСФО,
	|	ТаблицаВНА.СчетАмортизацииНСБУ КАК СчетАмортизацииНСБУ,
	|	ТаблицаВНА.СчетАмортизацииМСФО КАК СчетАмортизацииМСФО,
	|	ТаблицаВНА.СчетВыбытияВНАНСБУ КАК СчетВыбытияВНАНСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАМСФО КАК СчетВыбытияВНАМСФО,
	|	ТаблицаВНА.СчетУчетаСубконто1НСБУ КАК СчетУчетаСубконто1НСБУ,
	|	ТаблицаВНА.СчетУчетаСубконто2НСБУ КАК СчетУчетаСубконто2НСБУ,
	|	ТаблицаВНА.СчетУчетаСубконто3НСБУ КАК СчетУчетаСубконто3НСБУ,
	|	ТаблицаВНА.СчетУчетаСубконто1МСФО КАК СчетУчетаСубконто1МСФО,
	|	ТаблицаВНА.СчетУчетаСубконто2МСФО КАК СчетУчетаСубконто2МСФО,
	|	ТаблицаВНА.СчетУчетаСубконто3МСФО КАК СчетУчетаСубконто3МСФО,
	|	ТаблицаВНА.СчетАмортизацииСубконто1НСБУ КАК СчетАмортизацииСубконто1НСБУ,
	|	ТаблицаВНА.СчетАмортизацииСубконто2НСБУ КАК СчетАмортизацииСубконто2НСБУ,
	|	ТаблицаВНА.СчетАмортизацииСубконто3НСБУ КАК СчетАмортизацииСубконто3НСБУ,
	|	ТаблицаВНА.СчетАмортизацииСубконто1МСФО КАК СчетАмортизацииСубконто1МСФО,
	|	ТаблицаВНА.СчетАмортизацииСубконто2МСФО КАК СчетАмортизацииСубконто2МСФО,
	|	ТаблицаВНА.СчетАмортизацииСубконто3МСФО КАК СчетАмортизацииСубконто3МСФО,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто1НСБУ КАК СчетВыбытияВНАСубконто1НСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто2НСБУ КАК СчетВыбытияВНАСубконто2НСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто3НСБУ КАК СчетВыбытияВНАСубконто3НСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто1МСФО КАК СчетВыбытияВНАСубконто1МСФО,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто2МСФО КАК СчетВыбытияВНАСубконто2МСФО,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто3МСФО КАК СчетВыбытияВНАСубконто3МСФО,
	|	ТаблицаВНА.СпособОтраженияРасходовПоАмортизацииНСБУ КАК СпособОтраженияРасходовПоАмортизацииНСБУ,
	|	ТаблицаВНА.СпособОтраженияРасходовПоАмортизацииМСФО КАК СпособОтраженияРасходовПоАмортизацииМСФО,
	|	ТаблицаВНА.КоэффициентУскоренияНСБУ КАК КоэффициентУскоренияНСБУ,
	|	ТаблицаВНА.КоэффициентУскоренияМСФО КАК КоэффициентУскоренияМСФО,
	|	ТаблицаВНА.ПараметрВыработкиНСБУ КАК ПараметрВыработкиНСБУ,
	|	ТаблицаВНА.ПараметрВыработкиМСФО КАК ПараметрВыработкиМСФО,
	|	ТаблицаВНА.ОбъемВыработкиНСБУ КАК ОбъемВыработкиНСБУ,
	|	ТаблицаВНА.ОбъемВыработкиМСФО КАК ОбъемВыработкиМСФО,
	|	ТаблицаВНА.КоррСчетНСБУ КАК КоррСчетНСБУ,
	|	ТаблицаВНА.КоррСчетСубконто1НСБУ КАК КоррСчетСубконто1НСБУ,
	|	ТаблицаВНА.КоррСчетСубконто2НСБУ КАК КоррСчетСубконто2НСБУ,
	|	ТаблицаВНА.КоррСчетСубконто3НСБУ КАК КоррСчетСубконто3НСБУ,
	|	ТаблицаВНА.КоррСчетМСФО КАК КоррСчетМСФО,
	|	ТаблицаВНА.КоррСчетСубконто1МСФО КАК КоррСчетСубконто1МСФО,
	|	ТаблицаВНА.КоррСчетСубконто2МСФО КАК КоррСчетСубконто2МСФО,
	|	ТаблицаВНА.КоррСчетСубконто3МСФО КАК КоррСчетСубконто3МСФО,
	|	ТаблицаВНА.СторнироватьДанныеНСБУ КАК СторнироватьДанныеНСБУ,
	|	ТаблицаВНА.Подразделение КАК Подразделение,
	|	ТаблицаВНА.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ ТаблицаВНА
	|ИЗ
	|	Документ.ВводСобытийВНАМСФО.ВНА КАК ТаблицаВНА
	|ГДЕ
	|	ТаблицаВНА.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТаблицаВНА.ВНА,
	|	ТаблицаВНА.ВидОперации";

КонецФункции

Функция ТекстЗапросаПроверкаОстатков(НомераТаблиц)

	НомераТаблиц.Вставить("РасхождениеОстатковСумм", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("РасхождениеПараметровАмортизации", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("РасхождениеПараметровУчета", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	РасхождениеСумм.ВНА КАК ВНА,
	|	СУММА(РасхождениеСумм.БалансоваяСтоимостьНСБУ) КАК БалансоваяСтоимостьНСБУ,
	|	СУММА(РасхождениеСумм.АмортизацияНСБУ) КАК АмортизацияНСБУ,
	|	СУММА(РасхождениеСумм.БалансоваяСтоимостьУчет) КАК БалансоваяСтоимостьУчет,
	|	СУММА(РасхождениеСумм.АмортизацияУчет) КАК АмортизацияУчет,
	|	СУММА(РасхождениеСумм.БалансоваяСтоимостьДокумент) КАК БалансоваяСтоимостьДокумент,
	|	СУММА(РасхождениеСумм.АмортизацияДокумент) КАК АмортизацияДокумент
	|ИЗ
	|	(ВЫБРАТЬ
	|		СтоимостьВНАМСФООбороты.ВНА КАК ВНА,
	|		СтоимостьВНАМСФООбороты.СуммаВВалютеУчетаОборот - СтоимостьВНАМСФООбороты.АмортизацияОборот КАК ОстаточнаяСтоимостьНСБУ,
	|		СтоимостьВНАМСФООбороты.СуммаВВалютеУчетаОборот КАК БалансоваяСтоимостьНСБУ,
	|		СтоимостьВНАМСФООбороты.АмортизацияОборот КАК АмортизацияНСБУ,
	|		СтоимостьВНАМСФООбороты.СуммаВВалютеУчетаОборот КАК БалансоваяСтоимостьУчет,
	|		СтоимостьВНАМСФООбороты.АмортизацияОборот КАК АмортизацияУчет,
	|		0 КАК БалансоваяСтоимостьДокумент,
	|		0 КАК АмортизацияДокумент
	|	ИЗ
	|		РегистрНакопления.СтоимостьВНАМСФО.Обороты(
	|				,
	|				&ГраницаПредПериод,
	|				,
	|				Сценарий = &Сценарий
	|					И Организация = &Организация
	|					И ВидУчета = &ВидУчетаНСБУ) КАК СтоимостьВНАМСФООбороты
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаВНА.ВНА,
	|		-ТаблицаВНА.СуммаНСБУ,
	|		0,
	|		0,
	|		0,
	|		0,
	|		ТаблицаВНА.СтоимостьНСБУ,
	|		ТаблицаВНА.АмортизацияНСБУ
	|	ИЗ
	|		ТаблицаВНА КАК ТаблицаВНА
	|	ГДЕ
	|		ТаблицаВНА.ВидОперации = ЗНАЧЕНИЕ(Справочник.ВидыОпераций.НачальныеОстаткиВНА)) КАК РасхождениеСумм
	|
	|СГРУППИРОВАТЬ ПО
	|	РасхождениеСумм.ВНА
	|
	|ИМЕЮЩИЕ
	|	СУММА(РасхождениеСумм.ОстаточнаяСтоимостьНСБУ) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВНА.ВНА КАК ВНА,
	|	ТаблицаВНА.НачислятьАмортизациюНСБУ КАК НачислятьАмортизациюУчет,
	|	ЕСТЬNULL(ПараметрыАмортизацииВНАМСФОСрезПоследних.НачислятьАмортизацию, ЛОЖЬ) КАК НачислятьАмортизациюДокумент,
	|	ТаблицаВНА.СрокПолезногоИспользованияНСБУ КАК СрокПолезногоИспользованияУчет,
	|	ЕСТЬNULL(ПараметрыАмортизацииВНАМСФОСрезПоследних.СрокПолезногоИспользования, 0) КАК СрокПолезногоИспользованияДокумент
	|ИЗ
	|	ТаблицаВНА КАК ТаблицаВНА
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииВНАМСФО.СрезПоследних(
	|				&ГраницаПредПериод,
	|				Сценарий = &Сценарий
	|					И Организация = &Организация
	|					И ВидУчета = &ВидУчетаНСБУ
	|					И ВНА В
	|						(ВЫБРАТЬ
	|							ТаблицаВНА.ВНА
	|						ИЗ
	|							ТаблицаВНА КАК ТаблицаВНА
	|						ГДЕ
	|							ТаблицаВНА.ВидОперации = ЗНАЧЕНИЕ(Справочник.ВидыОпераций.НачальныеОстаткиВНА))) КАК ПараметрыАмортизацииВНАМСФОСрезПоследних
	|		ПО ТаблицаВНА.ВНА = ПараметрыАмортизацииВНАМСФОСрезПоследних.ВНА
	|ГДЕ
	|	ТаблицаВНА.ВидОперации = ЗНАЧЕНИЕ(Справочник.ВидыОпераций.НачальныеОстаткиВНА)
	|	И ТаблицаВНА.СрокПолезногоИспользованияНСБУ <> ЕСТЬNULL(ПараметрыАмортизацииВНАМСФОСрезПоследних.СрокПолезногоИспользования, 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПараметрыУчетаВНАМСФО.ВНА КАК ВНА,
	|	""ПараметрыУчетаВНА.СпособНачисленияАмортизации"" КАК СпособНачисленияАмортизации,
	|	ПараметрыУчетаВНАМСФО.СчетУчетаПервоначальнойСтоимости КАК СчетУчетаПервоначальнойСтоимости,
	|	ПараметрыУчетаВНАМСФО.СчетНакопленнойАмортизации КАК СчетНакопленнойАмортизации
	|ИЗ
	|	РегистрСведений.ПараметрыУчетаВНАМСФО.СрезПоследних(
	|			&ГраницаПредПериод,
	|			Сценарий = &Сценарий
	|				И Организация = &Организация
	|				И ВидУчета = &ВидУчетаНСБУ
	|				И ВНА В
	|					(ВЫБРАТЬ
	|						ТаблицаВНА.ВНА
	|					ИЗ
	|						ТаблицаВНА КАК ТаблицаВНА
	|					ГДЕ
	|						ТаблицаВНА.ВидОперации = ЗНАЧЕНИЕ(Справочник.ВидыОпераций.НачальныеОстаткиВНА))) КАК ПараметрыУчетаВНАМСФО";

КонецФункции

Функция ТекстЗапросаТабличныеЧасти(НомераТаблиц = Неопределено) Экспорт
	
	НомераТаблиц.Вставить("ОчередьСобытий", 								НомераТаблиц.Количество());	
	НомераТаблиц.Вставить("ТаблицаВНА_ПоступлениеВНАМСФО", 					НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаВНА_ВводВНАВЭксплуатациюМСФО", 			НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаВНА_ВыбытиеВНАМСФО", 						НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаВНА_ИзменениеПараметровВНАМСФО", 			НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаВНА_МодернизацияВНАМСФО", 				НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаВНА_ПризнаниеРасходовПоАмортизацииНСБУ", 	НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаВНА_ПереоценкаВНАМСФО", 					НомераТаблиц.Количество());
			
	Возврат 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА ОчередьСобытий.ДатаСобытияМСФО = ДАТАВРЕМЯ(1, 1, 1)
	|				ИЛИ РежимЗаполнения.Ссылка = ЗНАЧЕНИЕ(Перечисление.РежимЗаполненияВидовУчета.НСБУ)
	|			ТОГДА ОчередьСобытий.ДатаСобытияНСБУ
	|		ИНАЧЕ ОчередьСобытий.ДатаСобытияМСФО
	|	КОНЕЦ КАК ДатаСобытия,
	|	ВЫБОР
	|		КОГДА ОчередьСобытий.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.СторноАмортизацииНСБУ))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РежимЗаполненияВидовУчета.ПустаяСсылка)
	|		КОГДА РежимЗаполнения.Ссылка = НЕОПРЕДЕЛЕНО
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РежимЗаполненияВидовУчета.ПустаяСсылка)
	|		ИНАЧЕ РежимЗаполнения.Ссылка
	|	КОНЕЦ КАК РежимЗаполнения,
	|	ВЫБОР
	|		КОГДА ОчередьСобытий.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ВводВЭксплуатацию))
	|			ТОГДА ""ВводВНАВЭксплуатациюМСФО""
	|		КОГДА ОчередьСобытий.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПринятиеКУчетуВНА))
	|			ТОГДА ""ПоступлениеВНАМСФО""
	|		КОГДА ОчередьСобытий.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ВыбытиеВНА))
	|			ТОГДА ""ВыбытиеВНАМСФО""
	|		КОГДА ОчередьСобытий.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ИзменениеПараметровАмортизации))
	|			ТОГДА ""ИзменениеПараметровВНАМСФО""
	|		КОГДА ОчередьСобытий.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.МодернизацияВНА))
	|			ТОГДА ""МодернизацияВНАМСФО""
	|		КОГДА ОчередьСобытий.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.СторноАмортизацииНСБУ))
	|			ТОГДА ""ПризнаниеРасходовПоАмортизацииНСБУ""
	|		КОГДА ОчередьСобытий.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПереоценкаВНА))
	|			ТОГДА ""ПереоценкаВНАМСФО""
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ИмяДокумента,
	|	ВЫБОР
	|		КОГДА ОчередьСобытий.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ВводВЭксплуатацию))
	|			ТОГДА ""ВводВНАВЭксплуатациюМСФО""
	|		КОГДА ОчередьСобытий.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПринятиеКУчетуВНА))
	|			ТОГДА ""ПоступлениеВНАМСФО""
	|		КОГДА ОчередьСобытий.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ВыбытиеВНА))
	|			ТОГДА ""ВыбытиеВНАМСФО""
	|		КОГДА ОчередьСобытий.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ИзменениеПараметровАмортизации))
	|			ТОГДА ""ИзменениеПараметровВНАМСФО""
	|		КОГДА ОчередьСобытий.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.МодернизацияВНА))
	|			ТОГДА ""МодернизацияВНАМСФО""
	|		КОГДА ОчередьСобытий.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.СторноАмортизацииНСБУ))
	|			ТОГДА ""ПризнаниеРасходовПоАмортизацииНСБУ""
	|		КОГДА ОчередьСобытий.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПереоценкаВНА))
	|			ТОГДА ""ПереоценкаВНАМСФО""
	|		ИНАЧЕ ОчередьСобытий.ВидОперации
	|	КОНЕЦ КАК ВидСобытияВНА,
	|	ВЫБОР
	|		КОГДА ОчередьСобытий.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПринятиеКУчетуВНА))
	|			ТОГДА 1
	|		КОГДА ОчередьСобытий.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ВводВЭксплуатацию))
	|			ТОГДА 2
	|		КОГДА ОчередьСобытий.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ИзменениеПараметровАмортизации))
	|			ТОГДА 3
	|		КОГДА ОчередьСобытий.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.МодернизацияВНА))
	|			ТОГДА 4
	|		КОГДА ОчередьСобытий.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.СторноАмортизацииНСБУ))
	|			ТОГДА 5
	|		КОГДА ОчередьСобытий.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПереоценкаВНА))
	|			ТОГДА 6
	|		КОГДА ОчередьСобытий.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ВыбытиеВНА))
	|			ТОГДА 7
	|		ИНАЧЕ 10
	|	КОНЕЦ КАК ПорядокСобытий
	|ИЗ
	|	ТаблицаВНА КАК ОчередьСобытий
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Перечисление.РежимЗаполненияВидовУчета КАК РежимЗаполнения
	|		ПО (ВЫБОР
	|				КОГДА РежимЗаполнения.Ссылка = ЗНАЧЕНИЕ(Перечисление.РежимЗаполненияВидовУчета.НСБУИМСФО)
	|					ТОГДА ОчередьСобытий.ДатаСобытияМСФО = ОчередьСобытий.ДатаСобытияНСБУ
	|				КОГДА РежимЗаполнения.Ссылка = ЗНАЧЕНИЕ(Перечисление.РежимЗаполненияВидовУчета.МСФО)
	|					ТОГДА ОчередьСобытий.ДатаСобытияМСФО <> ОчередьСобытий.ДатаСобытияНСБУ
	|							И ОчередьСобытий.ДатаСобытияМСФО <> ДАТАВРЕМЯ(1, 1, 1)
	|				КОГДА РежимЗаполнения.Ссылка = ЗНАЧЕНИЕ(Перечисление.РежимЗаполненияВидовУчета.НСБУ)
	|					ТОГДА ОчередьСобытий.ДатаСобытияМСФО <> ОчередьСобытий.ДатаСобытияНСБУ
	|							И ОчередьСобытий.ДатаСобытияНСБУ <> ДАТАВРЕМЯ(1, 1, 1)
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)
	|ГДЕ
	|	ОчередьСобытий.ВидОперации <> ЗНАЧЕНИЕ(Справочник.ВидыОпераций.НачальныеОстаткиВНА)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаСобытия,
	|	ПорядокСобытий
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ТаблицаВНА.ДатаСобытияМСФО = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ТаблицаВНА.ДатаСобытияНСБУ
	|		ИНАЧЕ ТаблицаВНА.ДатаСобытияМСФО
	|	КОНЕЦ КАК ДатаДокумента,
	|	ТаблицаВНА.НомерСтроки КАК НомерСтрокиИсточник,
	|	ТаблицаВНА.ВНА КАК ВНА,
	|	ТаблицаВНА.Подразделение КАК Подразделение,
	|	ТаблицаВНА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаВНА.ДатаСобытияНСБУ КАК ДатаСобытияНСБУ,
	|	ТаблицаВНА.ДатаСобытияМСФО КАК ДатаСобытияМСФО,
	|	ТаблицаВНА.СторнироватьДанныеНСБУ КАК СторнироватьДанныеНСБУ,
	|	ТаблицаВНА.ПараметрыУчетаВНА КАК ГруппаВНАМСФО,
	|	ТаблицаВНА.СуммаНСБУ КАК СуммаНСБУ,
	|	ТаблицаВНА.СуммаНСБУ КАК СуммаМСФО,
	|	ТаблицаВНА.КоличествоНСБУ КАК КоличествоНСБУ,
	|	ТаблицаВНА.КоличествоНСБУ КАК КоличествоМСФО,
	|	ТаблицаВНА.СчетУчетаНСБУ КАК СчетУчетаНСБУ,
	|	ТаблицаВНА.СчетУчетаМСФО КАК СчетУчетаМСФО,
	|	ТаблицаВНА.СчетУчетаСубконто1НСБУ КАК СчетУчетаСубконто1НСБУ,
	|	ТаблицаВНА.СчетУчетаСубконто2НСБУ КАК СчетУчетаСубконто2НСБУ,
	|	ТаблицаВНА.СчетУчетаСубконто3НСБУ КАК СчетУчетаСубконто3НСБУ,
	|	ТаблицаВНА.СчетУчетаСубконто1МСФО КАК СчетУчетаСубконто1МСФО,
	|	ТаблицаВНА.СчетУчетаСубконто2МСФО КАК СчетУчетаСубконто2МСФО,
	|	ТаблицаВНА.СчетУчетаСубконто3МСФО КАК СчетУчетаСубконто3МСФО,
	|	ТаблицаВНА.КоррСчетНСБУ КАК КоррСчетНСБУ,
	|	ТаблицаВНА.КоррСчетМСФО КАК КоррСчетМСФО,
	|	ТаблицаВНА.КоррСчетСубконто1НСБУ КАК КоррСчетСубконто1НСБУ,
	|	ТаблицаВНА.КоррСчетСубконто2НСБУ КАК КоррСчетСубконто2НСБУ,
	|	ТаблицаВНА.КоррСчетСубконто3НСБУ КАК КоррСчетСубконто3НСБУ,
	|	ТаблицаВНА.КоррСчетСубконто1МСФО КАК КоррСчетСубконто1МСФО,
	|	ТаблицаВНА.КоррСчетСубконто2МСФО КАК КоррСчетСубконто2МСФО,
	|	ТаблицаВНА.КоррСчетСубконто3МСФО КАК КоррСчетСубконто3МСФО
	|ИЗ
	|	ТаблицаВНА КАК ТаблицаВНА
	|ГДЕ
	|	ТаблицаВНА.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПринятиеКУчетуВНА))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ТаблицаВНА.ДатаСобытияМСФО = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ТаблицаВНА.ДатаСобытияНСБУ
	|		ИНАЧЕ ТаблицаВНА.ДатаСобытияМСФО
	|	КОНЕЦ КАК ДатаДокумента,
	|	ТаблицаВНА.СторнироватьДанныеНСБУ КАК СторнироватьДанныеНСБУ,
	|	ТаблицаВНА.НомерСтроки КАК НомерСтрокиИсточник,
	|	ТаблицаВНА.ВНА КАК ВНА,
	|	ТаблицаВНА.Подразделение КАК Подразделение,
	|	ТаблицаВНА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаВНА.ПараметрыУчетаВНА КАК ГруппаВНА,
	|	ТаблицаВНА.ВидОперации КАК ВидОперации,
	|	ТаблицаВНА.ПараметрыАмортизацииВНАМСФО КАК ПараметрыАмортизацииВНАМСФО,
	|	ТаблицаВНА.СуммаНСБУ КАК ПервоначальнаяСтоимостьНСБУ,
	|	ТаблицаВНА.СуммаНСБУ КАК ПервоначальнаяСтоимостьМСФО,
	|	ТаблицаВНА.ЛиквидационнаяСтоимостьНСБУ КАК ЛиквидационнаяСтоимостьНСБУ,
	|	ТаблицаВНА.ЛиквидационнаяСтоимостьМСФО КАК ЛиквидационнаяСтоимостьМСФО,
	|	ТаблицаВНА.НачислятьАмортизациюНСБУ КАК НачислятьАмортизациюНСБУ,
	|	ТаблицаВНА.НачислятьАмортизациюМСФО КАК НачислятьАмортизациюМСФО,
	|	ТаблицаВНА.СпособНачисленияАмортизацииНСБУ КАК СпособНачисленияАмортизацииНСБУ,
	|	ТаблицаВНА.СпособНачисленияАмортизацииМСФО КАК СпособНачисленияАмортизацииМСФО,
	|	ТаблицаВНА.СрокПолезногоИспользованияНСБУ КАК СрокПолезногоИспользованияНСБУ,
	|	ТаблицаВНА.СрокПолезногоИспользованияМСФО КАК СрокПолезногоИспользованияМСФО,
	|	ТаблицаВНА.СпособОтраженияРасходовПоАмортизацииНСБУ КАК СпособОтраженияРасходовПоАмортизацииНСБУ,
	|	ТаблицаВНА.СпособОтраженияРасходовПоАмортизацииМСФО КАК СпособОтраженияРасходовПоАмортизацииМСФО,
	|	ТаблицаВНА.КоэффициентУскоренияНСБУ КАК КоэффициентУскоренияНСБУ,
	|	ТаблицаВНА.КоэффициентУскоренияМСФО КАК КоэффициентУскоренияМСФО,
	|	ТаблицаВНА.ПараметрВыработкиНСБУ КАК ПараметрВыработкиНСБУ,
	|	ТаблицаВНА.ПараметрВыработкиМСФО КАК ПараметрВыработкиМСФО,
	|	ТаблицаВНА.ОбъемВыработкиНСБУ КАК ОбъемВыработкиНСБУ,
	|	ТаблицаВНА.ОбъемВыработкиМСФО КАК ОбъемВыработкиМСФО,
	|	ТаблицаВНА.СчетУчетаНСБУ КАК СчетУчетаНСБУ,
	|	ТаблицаВНА.СчетУчетаМСФО КАК СчетУчетаМСФО,
	|	ТаблицаВНА.СчетАмортизацииНСБУ КАК СчетАмортизацииНСБУ,
	|	ТаблицаВНА.СчетАмортизацииМСФО КАК СчетАмортизацииМСФО,
	|	ТаблицаВНА.СчетВыбытияВНАНСБУ КАК СчетВыбытияНСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАМСФО КАК СчетВыбытияМСФО,
	|	ТаблицаВНА.СчетУчетаСубконто1МСФО КАК СчетУчетаСубконто1МСФО,
	|	ТаблицаВНА.СчетУчетаСубконто2МСФО КАК СчетУчетаСубконто2МСФО,
	|	ТаблицаВНА.СчетУчетаСубконто3МСФО КАК СчетУчетаСубконто3МСФО,
	|	ТаблицаВНА.СчетАмортизацииСубконто1МСФО КАК СчетАмортизацииСубконто1МСФО,
	|	ТаблицаВНА.СчетАмортизацииСубконто2МСФО КАК СчетАмортизацииСубконто2МСФО,
	|	ТаблицаВНА.СчетАмортизацииСубконто3МСФО КАК СчетАмортизацииСубконто3МСФО,
	|	ТаблицаВНА.СчетАмортизацииСубконто1НСБУ КАК СчетАмортизацииСубконто1НСБУ,
	|	ТаблицаВНА.СчетАмортизацииСубконто2НСБУ КАК СчетАмортизацииСубконто2НСБУ,
	|	ТаблицаВНА.СчетАмортизацииСубконто3НСБУ КАК СчетАмортизацииСубконто3НСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто1НСБУ КАК СчетВыбытияСубконто1НСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто2НСБУ КАК СчетВыбытияСубконто2НСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто3НСБУ КАК СчетВыбытияСубконто3НСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто1МСФО КАК СчетВыбытияСубконто1МСФО,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто2МСФО КАК СчетВыбытияСубконто2МСФО,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто3МСФО КАК СчетВыбытияСубконто3МСФО,
	|	ТаблицаВНА.КоррСчетНСБУ КАК СчетПоступленияНСБУ,
	|	ТаблицаВНА.КоррСчетМСФО КАК СчетПоступленияМСФО,
	|	ТаблицаВНА.КоррСчетСубконто1НСБУ КАК СчетПоступленияСубконто1НСБУ,
	|	ТаблицаВНА.КоррСчетСубконто2НСБУ КАК СчетПоступленияСубконто2НСБУ,
	|	ТаблицаВНА.КоррСчетСубконто3НСБУ КАК СчетПоступленияСубконто3НСБУ,
	|	ТаблицаВНА.КоррСчетСубконто1МСФО КАК СчетПоступленияСубконто1МСФО,
	|	ТаблицаВНА.КоррСчетСубконто2МСФО КАК СчетПоступленияСубконто2МСФО,
	|	ТаблицаВНА.КоррСчетСубконто3МСФО КАК СчетПоступленияСубконто3МСФО
	|ИЗ
	|	ТаблицаВНА КАК ТаблицаВНА
	|ГДЕ
	|	ТаблицаВНА.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ВводВЭксплуатацию))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ТаблицаВНА.ДатаСобытияМСФО = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ТаблицаВНА.ДатаСобытияНСБУ
	|		ИНАЧЕ ТаблицаВНА.ДатаСобытияМСФО
	|	КОНЕЦ КАК ДатаДокумента,
	|	ТаблицаВНА.СторнироватьДанныеНСБУ КАК СторнироватьДанныеНСБУ,
	|	ТаблицаВНА.НомерСтроки КАК НомерСтрокиИсточник,
	|	ТаблицаВНА.ВНА КАК ВНА,
	|	ТаблицаВНА.ВидОперации КАК ВидОперации,
	|	ТаблицаВНА.ПараметрыУчетаВНА КАК ПараметрыУчетаВНА,
	|	0 КАК ВыбытиеСтоимостиНСБУ,
	|	0 КАК ВыбытиеСтоимостиМСФО,
	|	ТаблицаВНА.СчетУчетаНСБУ КАК СчетВыбытияОстаточнойСтоимостиНСБУ,
	|	ТаблицаВНА.СчетУчетаМСФО КАК СчетВыбытияОстаточнойСтоимостиМСФО,
	|	ТаблицаВНА.КоррСчетНСБУ КАК СчетРасходаНСБУ,
	|	ТаблицаВНА.КоррСчетМСФО КАК СчетРасходаМСФО,
	|	ТаблицаВНА.СчетВыбытияВНАНСБУ КАК СчетВыбытияВНАНСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАМСФО КАК СчетВыбытияВНАМСФО,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто1НСБУ КАК СчетВыбытияВНАСубконто1НСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто2НСБУ КАК СчетВыбытияВНАСубконто2НСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто3НСБУ КАК СчетВыбытияВНАСубконто3НСБУ,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто1МСФО КАК СчетВыбытияВНАСубконто1МСФО,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто2МСФО КАК СчетВыбытияВНАСубконто2МСФО,
	|	ТаблицаВНА.СчетВыбытияВНАСубконто3МСФО КАК СчетВыбытияВНАСубконто3МСФО,
	|	ТаблицаВНА.КоррСчетСубконто1НСБУ КАК СчетРасходаСубконто1НСБУ,
	|	ТаблицаВНА.КоррСчетСубконто2НСБУ КАК СчетРасходаСубконто2НСБУ,
	|	ТаблицаВНА.КоррСчетСубконто3НСБУ КАК СчетРасходаСубконто3НСБУ,
	|	ТаблицаВНА.КоррСчетСубконто1МСФО КАК СчетРасходаСубконто1МСФО,
	|	ТаблицаВНА.КоррСчетСубконто2МСФО КАК СчетРасходаСубконто2МСФО,
	|	ТаблицаВНА.КоррСчетСубконто3МСФО КАК СчетРасходаСубконто3МСФО
	|ИЗ
	|	ТаблицаВНА КАК ТаблицаВНА
	|ГДЕ
	|	ТаблицаВНА.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ВыбытиеВНА))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ТаблицаВНА.ДатаСобытияМСФО = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ТаблицаВНА.ДатаСобытияНСБУ
	|		ИНАЧЕ ТаблицаВНА.ДатаСобытияМСФО
	|	КОНЕЦ КАК ДатаДокумента,
	|	ТаблицаВНА.СторнироватьДанныеНСБУ КАК СторнироватьДанныеНСБУ,
	|	ТаблицаВНА.НомерСтроки КАК НомерСтрокиИсточник,
	|	ТаблицаВНА.ВНА КАК ВНА,
	|	ТаблицаВНА.Подразделение КАК Подразделение,
	|	ТаблицаВНА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаВНА.ВидОперации КАК ВидОперации,
	|	ТаблицаВНА.ПараметрыУчетаВНА КАК ГруппаВНА,
	|	ТаблицаВНА.ПараметрыАмортизацииВНАМСФО КАК ПараметрыАмортизацииВНАМСФО,
	|	ТаблицаВНА.ЛиквидационнаяСтоимостьНСБУ КАК ЛиквидационнаяСтоимостьНСБУ,
	|	ТаблицаВНА.ЛиквидационнаяСтоимостьМСФО КАК ЛиквидационнаяСтоимостьМСФО,
	|	ТаблицаВНА.НачислятьАмортизациюНСБУ КАК НачислятьАмортизациюНСБУ,
	|	ТаблицаВНА.НачислятьАмортизациюМСФО КАК НачислятьАмортизациюМСФО,
	|	ТаблицаВНА.СпособНачисленияАмортизацииНСБУ КАК МетодНачисленияАмортизацииНСБУ,
	|	ТаблицаВНА.СпособНачисленияАмортизацииМСФО КАК МетодНачисленияАмортизацииМСФО,
	|	ТаблицаВНА.СрокПолезногоИспользованияНСБУ КАК СрокПолезногоИспользованияНСБУ,
	|	ТаблицаВНА.СрокПолезногоИспользованияМСФО КАК СрокПолезногоИспользованияМСФО,
	|	ТаблицаВНА.СпособОтраженияРасходовПоАмортизацииНСБУ КАК СпособОтраженияРасходовПоАмортизацииНСБУ,
	|	ТаблицаВНА.СпособОтраженияРасходовПоАмортизацииМСФО КАК СпособОтраженияРасходовПоАмортизацииМСФО,
	|	ТаблицаВНА.КоэффициентУскоренияНСБУ КАК КоэффициентУскоренияНСБУ,
	|	ТаблицаВНА.КоэффициентУскоренияМСФО КАК КоэффициентУскоренияМСФО
	|ИЗ
	|	ТаблицаВНА КАК ТаблицаВНА
	|ГДЕ
	|	ТаблицаВНА.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ИзменениеПараметровАмортизации))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ТаблицаВНА.ДатаСобытияМСФО = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ТаблицаВНА.ДатаСобытияНСБУ
	|		ИНАЧЕ ТаблицаВНА.ДатаСобытияМСФО
	|	КОНЕЦ КАК ДатаДокумента,
	|	ТаблицаВНА.СторнироватьДанныеНСБУ КАК СторнироватьДанныеНСБУ,
	|	ТаблицаВНА.НомерСтроки КАК НомерСтрокиИсточник,
	|	ТаблицаВНА.ВНА КАК ВНА,
	|	ТаблицаВНА.ПараметрыУчетаВНА КАК ГруппаВНА,
	|	ТаблицаВНА.ВидОперации КАК ВидОперации,
	|	ТаблицаВНА.ПараметрыАмортизацииВНАМСФО КАК ПараметрыАмортизацииВНАМСФО,
	|	ТаблицаВНА.СуммаНСБУ КАК СуммаМодернизацииНСБУ,
	|	ТаблицаВНА.СуммаНСБУ КАК СуммаМодернизацииМСФО,
	|	ТаблицаВНА.ЛиквидационнаяСтоимостьНСБУ КАК ЛиквидационнаяСтоимостьНСБУ,
	|	ТаблицаВНА.ЛиквидационнаяСтоимостьМСФО КАК ЛиквидационнаяСтоимостьМСФО,
	|	ТаблицаВНА.СрокПолезногоИспользованияНСБУ КАК СПИНСБУ,
	|	ТаблицаВНА.СрокПолезногоИспользованияМСФО КАК СПИМСФО,
	|	ТаблицаВНА.КоррСчетНСБУ КАК СчетЗатратНСБУ,
	|	ТаблицаВНА.КоррСчетМСФО КАК СчетЗатратМСФО,
	|	ТаблицаВНА.КоррСчетСубконто1НСБУ КАК СчетЗатратСубконто1НСБУ,
	|	ТаблицаВНА.КоррСчетСубконто2НСБУ КАК СчетЗатратСубконто2НСБУ,
	|	ТаблицаВНА.КоррСчетСубконто3НСБУ КАК СчетЗатратСубконто3НСБУ,
	|	ТаблицаВНА.КоррСчетСубконто1МСФО КАК СчетЗатратСубконто1МСФО,
	|	ТаблицаВНА.КоррСчетСубконто2МСФО КАК СчетЗатратСубконто2МСФО,
	|	ТаблицаВНА.КоррСчетСубконто3МСФО КАК СчетЗатратСубконто3МСФО,
	|	ТаблицаВНА.КоэффициентУскоренияНСБУ КАК КоэффициентУскоренияНСБУ,
	|	ТаблицаВНА.КоэффициентУскоренияМСФО КАК КоэффициентУскоренияМСФО,
	|	ТаблицаВНА.ОбъемВыработкиМСФО КАК ОбъемВыработкиМСФО,
	|	ТаблицаВНА.ОбъемВыработкиНСБУ КАК ОбъемВыработкиНСБУ
	|ИЗ
	|	ТаблицаВНА КАК ТаблицаВНА
	|ГДЕ
	|	ТаблицаВНА.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.МодернизацияВНА))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ТаблицаВНА.ДатаСобытияМСФО = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ТаблицаВНА.ДатаСобытияНСБУ
	|		ИНАЧЕ ТаблицаВНА.ДатаСобытияМСФО
	|	КОНЕЦ КАК ДатаДокумента,
	|	ТаблицаВНА.СторнироватьДанныеНСБУ КАК СторнироватьДанныеНСБУ,
	|	ТаблицаВНА.НомерСтроки КАК НомерСтрокиИсточник,
	|	ТаблицаВНА.ВНА КАК ВНА,
	|	ТаблицаВНА.ПараметрыУчетаВНА КАК ГруппаВНА,
	|	ТаблицаВНА.ВидОперации КАК ВидОперации,
	|	ТаблицаВНА.СуммаНСБУ КАК Сумма,
	|	ВЫБОР
	|		КОГДА ТаблицаВНА.КоррСчетМСФО.СчетСсылка.УчетПоПодразделениям
	|			ТОГДА ТаблицаВНА.Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ПодразделениеДт,
	|	ВЫБОР
	|		КОГДА ТаблицаВНА.КоррСчетМСФО.СчетСсылка.УчетПоНаправлениямДеятельности
	|			ТОГДА ТаблицаВНА.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельностиДт,
	|	ВЫБОР
	|		КОГДА ТаблицаВНА.СчетАмортизацииМСФО.СчетСсылка.УчетПоПодразделениям
	|			ТОГДА ТаблицаВНА.Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ПодразделениеКт,
	|	ВЫБОР
	|		КОГДА ТаблицаВНА.СчетАмортизацииМСФО.СчетСсылка.УчетПоНаправлениямДеятельности
	|			ТОГДА ТаблицаВНА.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельностиКт,
	|	ТаблицаВНА.КоррСчетНСБУ КАК СчетДтНСБУ,
	|	ТаблицаВНА.КоррСчетСубконто1НСБУ КАК СчетДтСубконто1НСБУ,
	|	ТаблицаВНА.КоррСчетСубконто2НСБУ КАК СчетДтСубконто2НСБУ,
	|	ТаблицаВНА.КоррСчетСубконто3НСБУ КАК СчетДтСубконто3НСБУ,
	|	ТаблицаВНА.КоррСчетМСФО КАК СчетДтМСФО,
	|	ТаблицаВНА.КоррСчетСубконто1МСФО КАК СчетДтСубконто1МСФО,
	|	ТаблицаВНА.КоррСчетСубконто2МСФО КАК СчетДтСубконто2МСФО,
	|	ТаблицаВНА.КоррСчетСубконто3МСФО КАК СчетДтСубконто3МСФО,
	|	ТаблицаВНА.СчетАмортизацииНСБУ КАК СчетКтНСБУ,
	|	ТаблицаВНА.СчетАмортизацииСубконто1НСБУ КАК СчетКтСубконто1НСБУ,
	|	ТаблицаВНА.СчетАмортизацииСубконто2НСБУ КАК СчетКтСубконто2НСБУ,
	|	ТаблицаВНА.СчетАмортизацииСубконто3НСБУ КАК СчетКтСубконто3НСБУ,
	|	ТаблицаВНА.СчетАмортизацииМСФО КАК СчетКтМСФО,
	|	ТаблицаВНА.СчетАмортизацииСубконто1МСФО КАК СчетКтСубконто1МСФО,
	|	ТаблицаВНА.СчетАмортизацииСубконто2МСФО КАК СчетКтСубконто2МСФО,
	|	ТаблицаВНА.СчетАмортизацииСубконто3МСФО КАК СчетКтСубконто3МСФО
	|ИЗ
	|	ТаблицаВНА КАК ТаблицаВНА
	|ГДЕ
	|	ТаблицаВНА.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.СторноАмортизацииНСБУ))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ТаблицаВНА.ДатаСобытияМСФО = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ТаблицаВНА.ДатаСобытияНСБУ
	|		ИНАЧЕ ТаблицаВНА.ДатаСобытияМСФО
	|	КОНЕЦ КАК ДатаДокумента,
	|	ТаблицаВНА.НомерСтроки КАК НомерСтрокиИсточник,
	|	ТаблицаВНА.ВНА КАК ВНА,
	|	ТаблицаВНА.ПараметрыУчетаВНА КАК ГруппаВНА,
	|	ТаблицаВНА.ВидОперации КАК ВидОперации,
	|	ТаблицаВНА.СуммаНСБУ КАК СправедливаяСтоимость,
	|	ТаблицаВНА.КоррСчетНСБУ КАК СчетДтНСБУ,
	|	ТаблицаВНА.КоррСчетСубконто1НСБУ КАК СчетДтСубконто1НСБУ,
	|	ТаблицаВНА.КоррСчетСубконто2НСБУ КАК СчетДтСубконто2НСБУ,
	|	ТаблицаВНА.КоррСчетСубконто3НСБУ КАК СчетДтСубконто3НСБУ,
	|	ТаблицаВНА.КоррСчетМСФО КАК СчетДтМСФО,
	|	ТаблицаВНА.КоррСчетСубконто1МСФО КАК СчетДтСубконто1МСФО,
	|	ТаблицаВНА.КоррСчетСубконто2МСФО КАК СчетДтСубконто2МСФО,
	|	ТаблицаВНА.КоррСчетСубконто3МСФО КАК СчетДтСубконто3МСФО,
	|	ТаблицаВНА.СчетАмортизацииНСБУ КАК СчетКтНСБУ,
	|	ТаблицаВНА.СчетАмортизацииСубконто1НСБУ КАК СчетКтСубконто1НСБУ,
	|	ТаблицаВНА.СчетАмортизацииСубконто2НСБУ КАК СчетКтСубконто2НСБУ,
	|	ТаблицаВНА.СчетАмортизацииСубконто3НСБУ КАК СчетКтСубконто3НСБУ,
	|	ТаблицаВНА.СчетАмортизацииМСФО КАК СчетКтМСФО,
	|	ТаблицаВНА.СчетАмортизацииСубконто1МСФО КАК СчетКтСубконто1МСФО,
	|	ТаблицаВНА.СчетАмортизацииСубконто2МСФО КАК СчетКтСубконто2МСФО,
	|	ТаблицаВНА.СчетАмортизацииСубконто3МСФО КАК СчетКтСубконто3МСФО
	|ИЗ
	|	ТаблицаВНА КАК ТаблицаВНА
	|ГДЕ
	|	ТаблицаВНА.ВидОперации В (ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПереоценкаВНА))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента";

КонецФункции

Функция ПроверитьОстаткиПредыдущегоПериода(ДополнительныеСвойства, Отказ = Ложь) Экспорт
	                    
	ШаблонРасхождения = НСтр("ru = 'По внеоборотному активу %1 расходятся %2 по НСБУ: на конец предыдущего периода %3, по текущему документу %4'");
		
	Для каждого СтрокаВНА Из ДополнительныеСвойства.ТаблицыДляПроверки.РасхождениеОстатковСумм Цикл
		
		Если СтрокаВНА.БалансоваяСтоимостьУчет <> СтрокаВНА.БалансоваяСтоимостьДокумент Тогда
			СообщениеОшибки = СтроковыеФункцииКлиентСерверУХ.ПодставитьПараметрыВСтроку(ШаблонРасхождения, 
						СтрокаВНА.ВНА, НСтр("ru = 'стоимости'"), СтрокаВНА.БалансоваяСтоимостьУчет, СтрокаВНА.БалансоваяСтоимостьДокумент);
			ОбщегоНазначенияУХ.СообщитьОбОшибке(СообщениеОшибки, Отказ); 
		КонецЕсли;
		
		Если СтрокаВНА.АмортизацияУчет <> СтрокаВНА.АмортизацияДокумент Тогда
			СообщениеОшибки = СтроковыеФункцииКлиентСерверУХ.ПодставитьПараметрыВСтроку(ШаблонРасхождения, 
						СтрокаВНА.ВНА, НСтр("ru = 'накопленные амортизации'"), СтрокаВНА.АмортизацияУчет, СтрокаВНА.АмортизацияДокумент);
			ОбщегоНазначенияУХ.СообщитьОбОшибке(СообщениеОшибки, Отказ); 
		КонецЕсли;		
		
	КонецЦикла; 
	
	Для каждого СтрокаВНА Из ДополнительныеСвойства.ТаблицыДляПроверки.РасхождениеПараметровАмортизаци Цикл
		
		Если СтрокаВНА.СрокПолезногоИспользованияУчет <> СтрокаВНА.СрокПолезногоИспользованияДокумент Тогда
			СообщениеОшибки = СтроковыеФункцииКлиентСерверУХ.ПодставитьПараметрыВСтроку(ШаблонРасхождения, 
						СтрокаВНА.ВНА, НСтр("ru = 'сроки полезного использования'"), СтрокаВНА.СрокПолезногоИспользованияУчет, СтрокаВНА.СрокПолезногоИспользованияДокумент);
			ОбщегоНазначенияУХ.СообщитьОбОшибке(СообщениеОшибки, Отказ); 
		КонецЕсли;		
		
	КонецЦикла;
	
КонецФункции

#КонецОбласти

#Область ЗаполнениеДокумента

Процедура ПересчитатьВычисляемыеРеквизитыСтроки(СтрокаДляЗаполнения) Экспорт
	
КонецПроцедуры

//КоллекцияСтрок - ТабличнаяЧасть,ТаблицаЗначений,ДанныеФормыКоллекция, СтрокаТабличнойЧасти
//Источник - ДокументОбъект, ДанныеФормыСтруктура, УправляемаяФорма
//ПараметрыЗаполнения - Структура(ЗаполнитьРеквизиты = Ложь,  ЗаполнитьНСБУ = Ложь, ЗаполнитьМСФО = Ложь)
Процедура ЗаполнитьСтроки(КоллекцияСтрок, Источник, ПараметрыЗаполненияТЧ = Неопределено) Экспорт

	Перем ГруппыВНА;
	
	ПараметрыЗаполнения = МСФОУХ.ПолучитьПараметрыЗаполнения(Источник);
	ЗаполняемыеРеквизитыТаблицыФормы = МСФОУХ.ПолучитьЗаполняемыеРеквизитыТаблицыФормы(Источник);	
	
	Если ПараметрыЗаполненияТЧ.ЗаполнитьНСБУ Или ПараметрыЗаполненияТЧ.ЗаполнитьМСФО Тогда		
		ГруппыВНА = ПолучитьГруппыВНА(КоллекцияСтрок, ПараметрыЗаполнения);		
	КонецЕсли;
	
	СтрокиИзмененныхВНАНСБУ = Новый Соответствие;
	СтрокиИзмененныхВНАМСФО = Новый Соответствие;
	
	Для каждого СтрокаДляЗаполнения Из КоллекцияСтрок Цикл
		
		Если ПараметрыЗаполненияТЧ.ЗаполнитьМСФО Тогда			
			
			ПараметрыУчетаВНА = СтрокиИзмененныхВНАМСФО.Получить(СтрокаДляЗаполнения.ВНА);
			Если ПараметрыУчетаВНА = Неопределено Тогда
				ПараметрыУчетаВНА = ГруппыВНА.Получить(СтрокаДляЗаполнения.ВНА);
				Если ПараметрыУчетаВНА <> Неопределено Тогда
					ПараметрыУчетаВНА = Новый Структура("ПараметрыУчетаВНА,СчетУчета,СчетАмортизации,СуммаВВалютеУчета",
															ПараметрыУчетаВНА, 
															ПараметрыУчетаВНА.СчетУчетаПервоначальнойСтоимости,
															ПараметрыУчетаВНА.СчетНакопленнойАмортизации,
															0);
				КонецЕсли;
			КонецЕсли;
			
			Если ПараметрыУчетаВНА <> Неопределено Тогда
				ЗаполнитьРеквизитыВНАИзПараметровЗаполнения(ПараметрыУчетаВНА, СтрокаДляЗаполнения.ПараметрыУчетаВНА,
					СтрокаДляЗаполнения.СчетУчетаМСФО, СтрокаДляЗаполнения.СчетАмортизацииМСФО);			
			КонецЕсли;
				
			ТекущиеПараметры = Новый Структура;
			ТекущиеПараметры.Вставить("ПараметрыУчетаВНА", 	СтрокаДляЗаполнения.ПараметрыУчетаВНА);
			ТекущиеПараметры.Вставить("СчетУчета", 			СтрокаДляЗаполнения.СчетУчетаМСФО);
			ТекущиеПараметры.Вставить("СчетАмортизации",	СтрокаДляЗаполнения.СчетАмортизацииМСФО);
			
			СтрокиИзмененныхВНАМСФО.Вставить(СтрокаДляЗаполнения.ВНА, ТекущиеПараметры);
			
		КонецЕсли;
		
		Если ПараметрыЗаполненияТЧ.ЗаполнитьНСБУ Тогда			
			
			ПараметрыУчетаВНА = СтрокиИзмененныхВНАНСБУ.Получить(СтрокаДляЗаполнения.ВНА);
			Если ПараметрыУчетаВНА = Неопределено Тогда
				ПараметрыУчетаВНА = ГруппыВНА.Получить(СтрокаДляЗаполнения.ВНА);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ПараметрыУчетаВНА) Тогда
				ЗаполнитьРеквизитыВНАИзПараметровЗаполнения(ПараметрыУчетаВНА, Неопределено, 
					СтрокаДляЗаполнения.СчетУчетаНСБУ, СтрокаДляЗаполнения.СчетАмортизацииНСБУ, СтрокаДляЗаполнения.СуммаНСБУ);
			КонецЕсли;
				
			ТекущиеПараметры = Новый Структура;
			ТекущиеПараметры.Вставить("СчетУчета", 			СтрокаДляЗаполнения.СчетУчетаНСБУ);
			ТекущиеПараметры.Вставить("СчетАмортизации",	СтрокаДляЗаполнения.СчетАмортизацииНСБУ);
			ТекущиеПараметры.Вставить("СуммаВВалютеУчета", 	СтрокаДляЗаполнения.СуммаНСБУ);
			СтрокиИзмененныхВНАНСБУ.Вставить(СтрокаДляЗаполнения.ВНА, ТекущиеПараметры);
			
		КонецЕсли;
		
		Если ЗаполняемыеРеквизитыТаблицыФормы <> Неопределено Тогда
			МСФОКлиентСерверУХ.ЗаполнитьРасчетныеРеквизитыСтроки(СтрокаДляЗаполнения, ЗаполняемыеРеквизитыТаблицыФормы);			
		КонецЕсли;
		
		ПересчитатьВычисляемыеРеквизитыСтроки(СтрокаДляЗаполнения);
		
	КонецЦикла;	

КонецПроцедуры

Функция ПолучитьГруппыВНА(КоллекцияСтрок, ПараметрыЗаполнения)
	
	Граница = ПараметрыЗаполнения.ПериодОтчета.ДатаНачала;
	
	ОбъектыВНА = Новый Массив;
	Для каждого СтрокаВНА Из КоллекцияСтрок Цикл
		Если ЗначениеЗаполнено(СтрокаВНА.ВНА) Тогда
			ОбъектыВНА.Добавить(СтрокаВНА.ВНА);		
		КонецЕсли;		
	КонецЦикла;
	
	РеквизитыВНА = МСФОВНАВызовСервераУХ.ПолучитьРеквизитыВНА(Граница, ОбъектыВНА, 
						ПараметрыЗаполнения.Организация, Неопределено, ПараметрыЗаполнения.Сценарий);
	
	ГруппыВНА = Новый Соответствие;
	
	Для каждого УзелВНА Из РеквизитыВНА.МСФО Цикл
		
		ГруппыВНА.Вставить(УзелВНА.Ключ, УзелВНА.Значение.ГруппаВНА);
		Удаляемый = ОбъектыВНА.Найти(УзелВНА.Ключ);
		Если ЗначениеЗаполнено(Удаляемый) Тогда 
			ОбъектыВНА.Удалить(Удаляемый);	
		КонецЕсли;
		
	КонецЦикла;
	
	ПоУмолчаниюГруппыВНА = Справочники.ГруппыВНАМСФО.ПолучитьТаблицуГруппВНАпоУмолчанию(ОбъектыВНА);
	Для каждого СтрокаВНА Из ПоУмолчаниюГруппыВНА Цикл
		ГруппыВНА.Вставить(СтрокаВНА.ВНА, СтрокаВНА.ПараметрыУчетаВНА);
	КонецЦикла;
	
	Возврат ГруппыВНА;

КонецФункции

Функция ЗаполнитьРеквизитыВНАИзПараметровЗаполнения(СтрокаПараметры, ПараметрыУчетаВНА = Неопределено, СчетУчета = Неопределено, 
						СчетАмортизации = Неопределено, ПервоначальнаяСтоимость = Неопределено, ОстаточнаяСтоимость = Неопределено) Экспорт
						
	Если (ПараметрыУчетаВНА <> Неопределено) И ПараметрыУчетаВНА.Пустая() И (ПараметрыУчетаВНА <> СтрокаПараметры.ПараметрыУчетаВНА) Тогда
		ПараметрыУчетаВНА = СтрокаПараметры.ПараметрыУчетаВНА;
	КонецЕсли;					

	ЕстьДанныеВНА = (ТипЗнч(СтрокаПараметры) = Тип("Структура"));
	
	Если (СчетУчета <> Неопределено) И СчетУчета.Пустая() Тогда
		
		Если ЕстьДанныеВНА Тогда
			Если СчетУчета <> СтрокаПараметры.СчетУчета Тогда
				СчетУчета = СтрокаПараметры.СчетУчета;		
			КонецЕсли;			
		ИначеЕсли СчетУчета <> СтрокаПараметры.СчетУчетаПервоначальнойСтоимости Тогда //СтрокаПараметры == ПараметрыУчетаВНА
			СчетУчета = СтрокаПараметры.СчетУчетаПервоначальнойСтоимости;
		КонецЕсли;
		
	КонецЕсли;					
	
	Если (СчетАмортизации <> Неопределено) И СчетАмортизации.Пустая() Тогда
		
		СчетаАмортизацииИзПараметров = ?(ЕстьДанныеВНА, СтрокаПараметры.СчетАмортизации, СтрокаПараметры.СчетНакопленнойАмортизации);
			
		Если (СчетАмортизации <> СчетаАмортизацииИзПараметров) Тогда
			СчетАмортизации = СчетаАмортизацииИзПараметров;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЕстьДанныеВНА 
		И (ПервоначальнаяСтоимость <> Неопределено) 
		И (ПервоначальнаяСтоимость = 0) 
		И ПервоначальнаяСтоимость <> СтрокаПараметры.СуммаВВалютеУчета Тогда
		
		ПервоначальнаяСтоимость = СтрокаПараметры.СуммаВВалютеУчета;
		
	КонецЕсли;	

КонецФункции

Процедура ЗаполнитьЗависимостиРеквизитовДокумента(ФормаДокумента) Экспорт 

	ЗависимостиРеквизитов = Новый Массив;
	
	Реквизиты = Новый Структура;
	
	Реквизиты.Вставить("СчетУчетаМСФО", 			"СчетУчетаПервоначальнойСтоимости");
	Реквизиты.Вставить("СчетУчетаСубконто1МСФО", 	"СчетУчетаПервоначальнойСтоимостиСубконто1");
	Реквизиты.Вставить("СчетУчетаСубконто2МСФО", 	"СчетУчетаПервоначальнойСтоимостиСубконто2");
	Реквизиты.Вставить("СчетУчетаСубконто3МСФО", 	"СчетУчетаПервоначальнойСтоимостиСубконто3");
	
	Реквизиты.Вставить("СчетАмортизацииМСФО", 			"СчетНакопленнойАмортизации");
	Реквизиты.Вставить("СчетАмортизацииСубконто1МСФО", 	"СчетНакопленнойАмортизацииСубконто1");
	Реквизиты.Вставить("СчетАмортизацииСубконто2МСФО", 	"СчетНакопленнойАмортизацииСубконто2");
	Реквизиты.Вставить("СчетАмортизацииСубконто3МСФО", 	"СчетНакопленнойАмортизацииСубконто3");
	
	Для каждого Реквизит Из Реквизиты Цикл
	
		НоваяСтрока = Новый Структура;
		НоваяСтрока.Вставить("РеквизитГруппы", 	Реквизит.Значение);
		НоваяСтрока.Вставить("Приемник", 		Реквизит.Ключ);
		НоваяСтрока.Вставить("Источник", 		"ПараметрыУчетаВНА");
		
		ЗависимостиРеквизитов.Добавить(НоваяСтрока);
		
	КонецЦикла;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("СпособНачисленияАмортизацииМСФО", 			"СпособНачисленияАмортизации");
	Реквизиты.Вставить("СрокПолезногоИспользованияМСФО", 			"СрокПолезногоИспользования");
	Реквизиты.Вставить("СпособОтраженияРасходовПоАмортизацииМСФО", 	"СпособОтраженияРасходовПоАмортизации");
	Реквизиты.Вставить("КоэффициентУскоренияМСФО", 					"КоэффициентУскорения");
	
	Для каждого Реквизит Из Реквизиты Цикл
	
		НоваяСтрока = Новый Структура;
		НоваяСтрока.Вставить("РеквизитГруппы", Реквизит.Значение);
		НоваяСтрока.Вставить("Приемник", Реквизит.Ключ);
		НоваяСтрока.Вставить("Источник", "ПараметрыАмортизацииВНАМСФО");
		
		ЗависимостиРеквизитов.Добавить(НоваяСтрока);
		
	КонецЦикла;
	
	ФормаДокумента.КэшируемыеЗначения.Вставить("ЗависимостиРеквизитов", ЗависимостиРеквизитов);
		
КонецПроцедуры

Функция ПолучитьИменаСубконто() Экспорт

	Результат = Новый Структура;
	
	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетУчетаСубконто1НСБУ");
	Субконто.Вставить(2, "СчетУчетаСубконто2НСБУ");
	Субконто.Вставить(3, "СчетУчетаСубконто3НСБУ");
	
	Результат.Вставить("СчетУчетаНСБУ", Субконто);
	
	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетУчетаСубконто1МСФО");
	Субконто.Вставить(2, "СчетУчетаСубконто2МСФО");
	Субконто.Вставить(3, "СчетУчетаСубконто3МСФО");
	
	Результат.Вставить("СчетУчетаМСФО", Субконто);

	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетАмортизацииСубконто1НСБУ");
	Субконто.Вставить(2, "СчетАмортизацииСубконто2НСБУ");
	Субконто.Вставить(3, "СчетАмортизацииСубконто3НСБУ");
	
	Результат.Вставить("СчетАмортизацииНСБУ", Субконто);

	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетАмортизацииСубконто1МСФО");
	Субконто.Вставить(2, "СчетАмортизацииСубконто2МСФО");
	Субконто.Вставить(3, "СчетАмортизацииСубконто3МСФО");
	
	Результат.Вставить("СчетАмортизацииМСФО", Субконто);
	
	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетВыбытияВНАСубконто1НСБУ");
	Субконто.Вставить(2, "СчетВыбытияВНАСубконто2НСБУ");
	Субконто.Вставить(3, "СчетВыбытияВНАСубконто3НСБУ");
	
	Результат.Вставить("СчетВыбытияВНАНСБУ", Субконто);

	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетВыбытияВНАСубконто1МСФО");
	Субконто.Вставить(2, "СчетВыбытияВНАСубконто2МСФО");
	Субконто.Вставить(3, "СчетВыбытияВНАСубконто3МСФО");
	
	Результат.Вставить("СчетВыбытияВНАМСФО", Субконто);

	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "КоррСчетСубконто1НСБУ");
	Субконто.Вставить(2, "КоррСчетСубконто2НСБУ");
	Субконто.Вставить(3, "КоррСчетСубконто3НСБУ");
	
	Результат.Вставить("КоррСчетНСБУ", Субконто);

	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "КоррСчетСубконто1МСФО");
	Субконто.Вставить(2, "КоррСчетСубконто2МСФО");
	Субконто.Вставить(3, "КоррСчетСубконто3МСФО");
	
	Результат.Вставить("КоррСчетМСФО", Субконто);
		
	Результат.Вставить("ИсключенияСкрытияСубконто", Новый Структура("КоррСчетМСФО,КоррСчетНСБУ"));

	Возврат Результат;
		
КонецФункции

Функция ПолучитьСтруктуруДействий(ДляФорма = Ложь) Экспорт

	ИменаСубконто = ПолучитьИменаСубконто();
	
	СтруктураДействий = Новый Структура;
		
	Возврат СтруктураДействий;

КонецФункции

#КонецОбласти

#КонецЕсли

