#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		
		СсылкаОбъекта = Неопределено;
		Если ДополнительныеСвойства.Свойство("СсылкаНового") Тогда
			СсылкаОбъекта = ДополнительныеСвойства.СсылкаНового;
			УстановитьСсылкуНового(СсылкаОбъекта);
		ИначеЕсли ЗначениеЗаполнено(Ссылка) Тогда
			СсылкаОбъекта = Ссылка;
		Иначе
			СсылкаОбъекта = Документы.ДокументКадровогоЭДО.ПолучитьСсылку();
			УстановитьСсылкуНового(СсылкаОбъекта);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ИдентификаторДокумента) Тогда
			Если ЗначениеЗаполнено(ИдентификаторЗаявкиКабинетСотрудника) Тогда
				ИдентификаторДокумента = ИдентификаторЗаявкиКабинетСотрудника;
			ИначеЕсли ЗначениеЗаполнено(ОснованиеДокумента) Тогда
				ИдентификаторДокумента = Строка(ОснованиеДокумента.УникальныйИдентификатор());		
			Иначе
				ИдентификаторДокумента = Строка(СсылкаОбъекта.УникальныйИдентификатор());
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		
		Если ПометкаУдаления И Не ДополнительныеСвойства.Свойство("РазрешенаПометкаУдаления") Тогда
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Пометка на удаление объектов ""%1"" не предусмотрена';
					|en = 'Cannot mark the ""%1"" objects for deletion'"),
				Метаданные().Синоним);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, , , Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли