
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

Функция ПодготовитьПараметрыПроведения(ДополнительныеСвойства, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДополнительныеСвойства.ДляПроведения.Ссылка);
	
	Реквизиты = МСФОУХ.РеквизитыДокумента(Запрос, "ПериодОтчета, ДатаНачала, ВидОтчета", "ПериодОтчета", Отказ);
	ДополнительныеСвойства.ДляПроведения.Вставить("Реквизиты", Реквизиты);
	
	Если Отказ Тогда
		Возврат ДополнительныеСвойства;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ГраницаДоДокумента",		МСФОВызовСервераУХ.ПолучитьГраницуДоДокумента(Реквизиты.Период, Реквизиты.Ссылка));
	Запрос.УстановитьПараметр("Организация",			Реквизиты.Организация);
	Запрос.УстановитьПараметр("Сценарий",				Реквизиты.Сценарий);
	Запрос.УстановитьПараметр("ПериодОтчета", 			Реквизиты.ПериодОтчета);	
	Запрос.УстановитьПараметр("НачалоПериода", 			Реквизиты.ДатаНачала);
	Запрос.УстановитьПараметр("ПланСчетовМСФО", 		Реквизиты.ПланСчетовМСФО);
	Запрос.УстановитьПараметр("ВидОтчета", 				Реквизиты.ВидОтчета);
	Запрос.УстановитьПараметр("ФункциональнаяВалюта", 	Реквизиты.ФункциональнаяВалюта);
	Запрос.УстановитьПараметр("УчетнаяПолитика", 		Реквизиты.УчетнаяПолитика);
	
	НомераТаблиц = Новый Структура;
	ТекстЗапроса = Новый Массив;
	ТекстЗапроса.Добавить(ТекстЗапроса_СчетаУчета(НомераТаблиц, Реквизиты.ФормироватьПроводкиМСФО));
	Если Реквизиты.ФормироватьПроводкиМСФО Тогда
		ТекстЗапроса.Добавить(ТекстЗапроса_ОстаткиТУ(НомераТаблиц));
	Иначе	
		ТекстЗапроса.Добавить(МСФОВызовСервераУХ.ТекстЗапроса_АОСВ(НомераТаблиц, "СКД,СКК", Истина, "втСчетаУчета"));
	КонецЕсли;
	ТекстЗапроса.Добавить(ТекстЗапроса_ТаблицаПроводок(НомераТаблиц, Реквизиты.ФормироватьПроводкиМСФО));
	
	Запрос.Текст = СтрСоединить(ТекстЗапроса, ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета());
	Возврат ПроведениеСерверУХ.ДополнитьТаблицамиИзПакетаЗапросов(ДополнительныеСвойства.ТаблицыДляДвижений, Запрос, НомераТаблиц);

КонецФункции

Функция ТекстЗапроса_СчетаУчета(НомераТаблиц, ФормироватьПроводкиМСФО = Истина)

	НомераТаблиц.Вставить("втСчетаУчета", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СчетаБД.Счет.СчетСсылка КАК Счет,	
	|	ВЫБОР
	|		КОГДА СчетаБД.Ссылка = ЗНАЧЕНИЕ(Справочник.ФиксированныеСчетаУчетаБД.ТекущийФинансовыйРезультат)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоСчетФинансовогоРезультата,
	|	СчетаБД.Субконто1 КАК Субконто1,
	|	СчетаБД.Субконто2 КАК Субконто2,
	|	СчетаБД.Субконто3 КАК Субконто3
	|ПОМЕСТИТЬ втСчетаУчета
	|ИЗ
	|	Справочник.ФиксированныеСчетаУчетаБД КАК СчетаБД
	|ГДЕ
	|	СчетаБД.Ссылка В (ЗНАЧЕНИЕ(Справочник.ФиксированныеСчетаУчетаБД.ТекущийФинансовыйРезультат), ЗНАЧЕНИЕ(Справочник.ФиксированныеСчетаУчетаБД.НераспределеннаяПрибыльНепокрытыйУбытокОтчетногоПериода))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет";
	
	Документы.УчетнаяПолитикаМСФО.ЗаменитьФиксированныеСчетаУчетаБД(ТекстЗапроса);
	
	Возврат ?(ФормироватьПроводкиМСФО, ТекстЗапроса, СтрЗаменить(ТекстЗапроса, ".СчетСсылка", ""));

КонецФункции

Функция ТекстЗапроса_ОстаткиТУ(НомераТаблиц)

	НомераТаблиц.Вставить("втИтогиПоСчетам", НомераТаблиц.Количество());
	
	Возврат 
	"ВЫБРАТЬ
	|	МСФООбороты.СуммаВВалютеУчетаОстаток КАК Сумма,
	|	МСФООбороты.Счет КАК Счет,
	|	МСФООбороты.Субконто1 КАК Субконто1,
	|	МСФООбороты.Субконто2 КАК Субконто2,
	|	МСФООбороты.Субконто3 КАК Субконто3
	|ПОМЕСТИТЬ втИтогиПоСчетам
	|ИЗ
	|	РегистрБухгалтерии.МСФО.Остатки(
	|			&ГраницаДоДокумента,
	|			Счет В
	|				(ВЫБРАТЬ
	|					т.Счет
	|				ИЗ
	|					втСчетаУчета КАК т),
	|			,
	|			Сценарий = &Сценарий
	|				И Организация = &Организация) КАК МСФООбороты
	|";

КонецФункции

Функция ТекстЗапроса_ТаблицаПроводок(НомераТаблиц, ФормироватьПроводкиМСФО)
	
	НомераТаблиц.Вставить("ТаблицаПроводок", НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА втИтогиПоСчетам.Сумма > 0
	|			ТОГДА АналитикаКоррСчет.Счет.СчетСсылка
	|		ИНАЧЕ втИтогиПоСчетам.Счет
	|	КОНЕЦ КАК СчетДт,
	|	ВЫБОР
	|		КОГДА втИтогиПоСчетам.Сумма > 0
	|			ТОГДА АналитикаКоррСчет.Субконто1
	|		ИНАЧЕ втИтогиПоСчетам.Субконто1
	|	КОНЕЦ КАК СубконтоДт1,
	|	ВЫБОР
	|		КОГДА втИтогиПоСчетам.Сумма > 0
	|			ТОГДА АналитикаКоррСчет.Субконто2
	|		ИНАЧЕ втИтогиПоСчетам.Субконто2
	|	КОНЕЦ КАК СубконтоДт2,
	|	ВЫБОР
	|		КОГДА втИтогиПоСчетам.Сумма > 0
	|			ТОГДА АналитикаКоррСчет.Субконто3
	|		ИНАЧЕ втИтогиПоСчетам.Субконто3
	|	КОНЕЦ КАК СубконтоДт3,
	|	ВЫБОР
	|		КОГДА втИтогиПоСчетам.Сумма > 0
	|			ТОГДА втИтогиПоСчетам.Счет
	|		ИНАЧЕ АналитикаКоррСчет.Счет.СчетСсылка
	|	КОНЕЦ КАК СчетКт,
	|	ВЫБОР
	|		КОГДА втИтогиПоСчетам.Сумма > 0
	|			ТОГДА втИтогиПоСчетам.Субконто1
	|		ИНАЧЕ АналитикаКоррСчет.Субконто1
	|	КОНЕЦ КАК СубконтоКт1,
	|	ВЫБОР
	|		КОГДА втИтогиПоСчетам.Сумма > 0
	|			ТОГДА втИтогиПоСчетам.Субконто2
	|		ИНАЧЕ АналитикаКоррСчет.Субконто2
	|	КОНЕЦ КАК СубконтоКт2,
	|	ВЫБОР
	|		КОГДА втИтогиПоСчетам.Сумма > 0
	|			ТОГДА втИтогиПоСчетам.Субконто3
	|		ИНАЧЕ АналитикаКоррСчет.Субконто3
	|	КОНЕЦ КАК СубконтоКт3,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПереносПредыдущейНРП) КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА втИтогиПоСчетам.Сумма > 0
	|			ТОГДА втИтогиПоСчетам.Сумма
	|		ИНАЧЕ -втИтогиПоСчетам.Сумма
	|	КОНЕЦ КАК СуммаВФункциональнойВалюте
	|ИЗ
	|	втИтогиПоСчетам КАК втИтогиПоСчетам
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФиксированныеСчетаУчетаБД КАК АналитикаКоррСчет
	|		ПО (АналитикаКоррСчет.Ссылка = ЗНАЧЕНИЕ(Справочник.ФиксированныеСчетаУчетаБД.НераспределеннаяПрибыльНепокрытыйУбытокПредыдущихПериодов))
	|ГДЕ
	|	втИтогиПоСчетам.Сумма <> 0
	|	И втИтогиПоСчетам.Счет В
	|			(ВЫБРАТЬ
	|				т.Счет
	|			ИЗ
	|				втСчетаУчета КАК т
	|			ГДЕ
	|				НЕ т.ЭтоСчетФинансовогоРезультата)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА втИтогиПоСчетам.Сумма > 0
	|			ТОГДА АналитикаКоррСчет.Счет.СчетСсылка
	|		ИНАЧЕ втИтогиПоСчетам.Счет
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА втИтогиПоСчетам.Сумма > 0
	|			ТОГДА АналитикаКоррСчет.Субконто1
	|		ИНАЧЕ втИтогиПоСчетам.Субконто1
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА втИтогиПоСчетам.Сумма > 0
	|			ТОГДА АналитикаКоррСчет.Субконто2
	|		ИНАЧЕ втИтогиПоСчетам.Субконто2
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА втИтогиПоСчетам.Сумма > 0
	|			ТОГДА АналитикаКоррСчет.Субконто3
	|		ИНАЧЕ втИтогиПоСчетам.Субконто3
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА втИтогиПоСчетам.Сумма > 0
	|			ТОГДА втИтогиПоСчетам.Счет
	|		ИНАЧЕ АналитикаКоррСчет.Счет.СчетСсылка
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА втИтогиПоСчетам.Сумма > 0
	|			ТОГДА втИтогиПоСчетам.Субконто1
	|		ИНАЧЕ АналитикаКоррСчет.Субконто1
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА втИтогиПоСчетам.Сумма > 0
	|			ТОГДА втИтогиПоСчетам.Субконто2
	|		ИНАЧЕ АналитикаКоррСчет.Субконто2
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА втИтогиПоСчетам.Сумма > 0
	|			ТОГДА втИтогиПоСчетам.Субконто3
	|		ИНАЧЕ АналитикаКоррСчет.Субконто3
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций._06РеформацияБаланса),
	|	ВЫБОР
	|		КОГДА втИтогиПоСчетам.Сумма > 0
	|			ТОГДА втИтогиПоСчетам.Сумма
	|		ИНАЧЕ -втИтогиПоСчетам.Сумма
	|	КОНЕЦ
	|ИЗ
	|	втИтогиПоСчетам КАК втИтогиПоСчетам
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФиксированныеСчетаУчетаБД КАК АналитикаКоррСчет
	|		ПО (АналитикаКоррСчет.Ссылка = ЗНАЧЕНИЕ(Справочник.ФиксированныеСчетаУчетаБД.НераспределеннаяПрибыльНепокрытыйУбытокОтчетногоПериода))
	|ГДЕ
	|	втИтогиПоСчетам.Сумма <> 0
	|	И втИтогиПоСчетам.Счет В
	|			(ВЫБРАТЬ
	|				т.Счет
	|			ИЗ
	|				втСчетаУчета КАК т
	|			ГДЕ
	|				т.ЭтоСчетФинансовогоРезультата)";
	
	Документы.УчетнаяПолитикаМСФО.ЗаменитьФиксированныеСчетаУчетаБД(ТекстЗапроса);
	
	Возврат ?(ФормироватьПроводкиМСФО, ТекстЗапроса, СтрЗаменить(ТекстЗапроса, ".СчетСсылка", ""));
	
КонецФункции

#КонецЕсли