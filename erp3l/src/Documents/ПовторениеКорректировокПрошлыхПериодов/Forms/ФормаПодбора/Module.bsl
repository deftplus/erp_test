
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТиповыеОтчетыУХ.ДобавитьОтбор(ЭтаФорма.Корректировки.Отбор, "Сценарий", Параметры.Сценарий);
	ТиповыеОтчетыУХ.ДобавитьОтбор(ЭтаФорма.Корректировки.Отбор, "ПериодОтчета", Параметры.ПериодОтчета);
	ТиповыеОтчетыУХ.ДобавитьОтбор(ЭтаФорма.Корректировки.Отбор, "Организация", Параметры.Организация);
	
	СтруктураПроводка = Новый Структура("Корректировка,Ссылка");
	РеквизитыТЧ = Метаданные.Документы.ТрансформационнаяКорректировка.ТабличныеЧасти.Проводки.Реквизиты;
	Для каждого РеквизитТЧ Из РеквизитыТЧ Цикл
		СтруктураПроводка.Вставить(РеквизитТЧ.Имя);
	КонецЦикла;
	
	КэшируемыеЗначения = Новый Структура;
	КэшируемыеЗначения.Вставить("СтруктураПроводка", СтруктураПроводка);
	
	// Реквизиты ТЧ ПроводкиПовтора хранящие сумму проводки.
	// Для изменения знака.
	РеквизитыСуммы = Новый Массив;
	РеквизитыСуммы.Добавить("Значение");
	РеквизитыСуммы.Добавить("ЗначениеВалютаКт");
	РеквизитыСуммы.Добавить("ЗначениеВалютаДт");
	КэшируемыеЗначения.Вставить("РеквизитыСуммы", РеквизитыСуммы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура Выбрать(Команда)
	ОповеститьОВыборе(ПолучитьРезультатВыбора());
КонецПроцедуры

&НаКлиенте
Процедура КорректировкиПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("ЗаполнитьПроводкиТКНаКлиенте", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПроводкиТКВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОповеститьОВыборе(ПолучитьРезультатВыбора());
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеПроцедурыФункции

&НаКлиенте
Процедура ЗаполнитьПроводкиТКНаКлиенте() Экспорт
	ТекущаяСтрока = Элементы.Корректировки.ТекущиеДанные;
	
	Если ТекущаяСтрока = Неопределено Тогда
		ПроводкиТК.Очистить();
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПроводкиТК(ТекущаяСтрока.Ссылка);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПроводкиТК(ДокументТК)
	
	//запрос позволяет получить реквизит Ссылка
	Запрос = Новый Запрос(
	"ВЫБРАТЬ 
	|	*
	|ИЗ 
	|	Документ.ТрансформационнаяКорректировка.Проводки КАК ПроводкиТК
	|ГДЕ 
	|	ПроводкиТК.Ссылка = &Ссылка");	
	Запрос.УстановитьПараметр("Ссылка", ДокументТК);
	ПроводкиТК.Загрузить(Запрос.Выполнить().Выгрузить());
		
КонецПроцедуры

&НаСервере
Функция ПолучитьРезультатВыбора()
	
	Результат = Новый Массив;
	
	Для каждого Проводка Из Элементы.ПроводкиТК.ВыделенныеСтроки Цикл
		
		Приемник = ОбщегоНазначенияУХ.СкопироватьУниверсальнуюКоллекцию(КэшируемыеЗначения.СтруктураПроводка);
		ЗаполнитьЗначенияСвойств(Приемник, ПроводкиТК.НайтиПоИдентификатору(Проводка));
		Приемник.Корректировка = Приемник.Ссылка;
		
		ИзменитьЗнакПроводки(Приемник, КэшируемыеЗначения.РеквизитыСуммы);
		
		Результат.Добавить(Приемник);
		
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

&НаСервереБезКонтекста
Процедура ИзменитьЗнакПроводки(Проводка, РеквизитыСуммы)
	Для Каждого ИмяРеквизита Из РеквизитыСуммы Цикл
		Проводка[ИмяРеквизита] = -Проводка[ИмяРеквизита];
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

