

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// Получим аналитики подчиненных строк
	ПодчиненнаяРасшифровка = Параметры.ПодчиненнаяРасшифровка;
	ДанныеРасшифровки = ПолучитьИзВременногоХранилища(Параметры.АдресДанныхРасшифровки);
	ПодчиненныеЗначенияАналитик = Новый Соответствие();
	Для Каждого ИндексРасшифровки Из ПодчиненнаяРасшифровка Цикл
		Расшифровка = ДанныеРасшифровки[ИндексРасшифровки]; 
		Для Каждого КлючЗначение Из Расшифровка.ЗначенияАналитик Цикл
			Если ЗначениеЗаполнено(КлючЗначение.Значение) Тогда
				ПодчиненныеЗначенияАналитик.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

	МассивВидовАналитик = Параметры.МассивВидовАналитик;
	МассивПолученияЗначений = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(МассивВидовАналитик);
	Для Каждого КлючИЗначение Из Параметры.ЗначенияАналитик Цикл
		МассивПолученияЗначений.Добавить(КлючИЗначение.Ключ);
	КонецЦикла;
	РеквизитыАналитик = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивПолученияЗначений, "Наименование, ТипЗначения");
	Сч = 0;
	
	Для Каждого ВидАналитики Из МассивВидовАналитик Цикл
		Сч = Сч + 1;
		ЭтаФорма["ВидАналитики" + Сч] = ВидАналитики;
		Реквизиты = РеквизитыАналитик[ВидАналитики];
		Элементы["Аналитика" + Сч].Заголовок 		= Реквизиты.Наименование;
		Элементы["Аналитика" + Сч].ОграничениеТипа 	= Реквизиты.ТипЗначения;
		ЭтаФорма["Аналитика" + Сч] = БюджетированиеКлиентСервер.ПриведенноеЗначениеАналитики(Неопределено,
		                                                                                     Реквизиты.ТипЗначения);
		
		Если Реквизиты.ТипЗначения.Типы().Найти(Тип("СправочникСсылка.ЗначенияСвойствОбъектов")) <> Неопределено Тогда
			ПараметрВыбора = Новый ПараметрВыбора("Отбор.Владелец", ВидАналитики.ДополнительноеСвойство);
			Массив = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрВыбора);
			
			Элементы["Аналитика" + Сч].ПараметрыВыбора = Новый ФиксированныйМассив(Массив);
		КонецЕсли;
		
		ДобавитьПараметрыСвязиПоВладельцу(Сч, Реквизиты.ТипЗначения, РеквизитыАналитик, Параметры.ЗначенияАналитик, ПодчиненныеЗначенияАналитик);
	КонецЦикла;
	
	Для Сч = МассивВидовАналитик.Количество() + 1 По 6 Цикл
		Элементы["Аналитика" + Сч].Видимость = Ложь;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьСтроку(Команда)
	
	СоответствиеАналитик = Новый Соответствие;
	Для Сч = 1 По 6 Цикл
		ВидАналитики = ЭтаФорма["ВидАналитики" + Сч];
		Если ЗначениеЗаполнено(ВидАналитики) Тогда
			СоответствиеАналитик.Вставить(ВидАналитики, ЭтаФорма["Аналитика" + Сч]);
		КонецЕсли;
	КонецЦикла;
	
	Закрыть(СоответствиеАналитик);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДобавитьПараметрыСвязиПоВладельцу(Сч, ТипЗначения, РеквизитыАналитик, ЗначенияАналитик, ПодчиненныеЗначенияАналитик)
		
	СоответствиеВладельцев = ФинансоваяОтчетностьПовтИсп.СоответствиеОтборовПоВладельцу();
	Для Каждого Тип Из ТипЗначения.Типы() Цикл
		ТипыВладельца = СоответствиеВладельцев[Тип];
		Если ТипыВладельца <> Неопределено Тогда
			
			Для СчетчикОбратный = 1 По Сч - 1 Цикл
				ИндексПроверяемойАналитики = Сч - СчетчикОбратный;
				ВидАналитики = ЭтаФорма["ВидАналитики" + ИндексПроверяемойАналитики];
				Реквизиты = РеквизитыАналитик[ВидАналитики];
				Для Каждого СтрокаТипа Из ТипыВладельца Цикл
					Если Реквизиты.ТипЗначения.СодержитТип(СтрокаТипа.Тип) Тогда
						Если ЗначениеЗаполнено(ЭтотОбъект["Аналитика" + ИндексПроверяемойАналитики]) Тогда
							СвязьПараметров = Новый СвязьПараметраВыбора(
														"Отбор." + СтрокаТипа.Реквизит,
														"Аналитика" + ИндексПроверяемойАналитики);
							Массив = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СвязьПараметров);
							Элементы["Аналитика" + Сч].СвязиПараметровВыбора = Новый ФиксированныйМассив(Массив);
						КонецЕсли;
						Возврат;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
						
			// По аналитикам подчиненных строк
			Для Каждого КлючИЗначение Из ПодчиненныеЗначенияАналитик Цикл
				ВидАналитики = КлючИЗначение.Ключ;
				Реквизиты = РеквизитыАналитик[ВидАналитики];
				Для Каждого СтрокаТипа Из ТипыВладельца Цикл
					Если Реквизиты.ТипЗначения.СодержитТип(СтрокаТипа.Тип) Тогда
						Если ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
							ЭтаФорма["ВладелецАналитика" + Сч] = КлючИЗначение.Значение;
							СвязьПараметров = Новый СвязьПараметраВыбора(
														"Отбор." + СтрокаТипа.Реквизит,
														"ВладелецАналитика" + Сч);
							Массив = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СвязьПараметров);
							Элементы["Аналитика" + Сч].СвязиПараметровВыбора = Новый ФиксированныйМассив(Массив);
						КонецЕсли;
						Возврат;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			
			Для Каждого КлючИЗначение Из ЗначенияАналитик Цикл
				ВидАналитики = КлючИЗначение.Ключ;
				Реквизиты = РеквизитыАналитик[ВидАналитики];
				Для Каждого СтрокаТипа Из ТипыВладельца Цикл
					Если Реквизиты.ТипЗначения.СодержитТип(СтрокаТипа.Тип) Тогда
						Если ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
							ЭтаФорма["ВладелецАналитика" + Сч] = КлючИЗначение.Значение;
							СвязьПараметров = Новый СвязьПараметраВыбора(
														"Отбор." + СтрокаТипа.Реквизит,
														"ВладелецАналитика" + Сч);
							Массив = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СвязьПараметров);
							Элементы["Аналитика" + Сч].СвязиПараметровВыбора = Новый ФиксированныйМассив(Массив);
						КонецЕсли;
						Возврат;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти