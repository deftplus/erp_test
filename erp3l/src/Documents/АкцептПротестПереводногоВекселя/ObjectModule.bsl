#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив();

	Если ВидОперации = Перечисления.ВидыОперацийАкцептПротестПереводногоВекселя.ПредъявлениеВекселяКАкцепту Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорАкцепта");
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийАкцептПротестПереводногоВекселя.УведомлениеОПротестеПоВекселю Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СчетВзаиморасчетовСПлательщиком");
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорАкцепта");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

	ФинансовыеИнструментыУХ.ОбработкаПроверкиЗаполнения_СчетаУчета(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	ВстраиваниеУХФинансовыеИнструменты.ОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения);
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);

	Если ВидОперации = Перечисления.ВидыОперацийАкцептПротестПереводногоВекселя.УведомлениеОбАкцептеВекселя Тогда
		СтатусАкцепта = Перечисления.СтатусыАкцептаВекселя.Акцептован;
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийАкцептПротестПереводногоВекселя.УведомлениеОПротестеПоВекселю Тогда
		СтатусАкцепта = Перечисления.СтатусыАкцептаВекселя.ПротестВНеакцепте;
	КонецЕсли;
	
	ВстраиваниеУХФинансовыеИнструменты.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ВстраиваниеУХ.ОбработкаУдаленияПроведения(ЭтотОбъект, Отказ);	
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
	ФинансовыеИнструментыУХ.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.ВидОперации) Тогда
		ЭтотОбъект.ВидОперации = ЭтотОбъект.Метаданные().Реквизиты.ВидОперации.ЗначениеЗаполнения;
	КонецЕсли;
	
	ЗаполнитьПараметрыОперацийПоУмолчанию();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс	
Процедура ЗаполнитьПараметрыОперацийПоУмолчанию() Экспорт
	
	ТаблицаНастроек = РаботаСДоговорамиКонтрагентовУХ.ПараметрыОперацийГрафикаПоУмолчанию(ГруппаОперацийГрафика());
	ЭтотОбъект.ПараметрыОпераций.Загрузить(ТаблицаНастроек);
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Функция ГруппаОперацийГрафика()
	
	МассивГрупп = Новый Массив;
	МассивГрупп.Добавить(Справочники.ОперацииГрафиковДоговоров.ВексельПолученный);
	Если ЭтотОбъект.ВидОперации = Перечисления.ВидыОперацийАкцептПротестПереводногоВекселя.УведомлениеОПротестеПоВекселю Тогда
		МассивГрупп.Добавить(Справочники.ОперацииГрафиковДоговоров.ВексельВыданный);
	КонецЕсли;
	
	Возврат МассивГрупп;
	
КонецФункции
#КонецОбласти

#КонецЕсли