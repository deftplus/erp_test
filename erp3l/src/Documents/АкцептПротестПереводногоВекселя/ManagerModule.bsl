#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных мханизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт	
		
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда		
		
		НомераТаблиц = Новый Структура;
		ТекстЗапроса = Новый Массив;		
		ТекстЗапроса.Добавить(ТекстЗапросаРеквизитыДокумента(НомераТаблиц));
		Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВТ_ПараметрыОпераций", ТекстыЗапроса) Тогда
			ТекстЗапроса.Добавить(ТекстЗапросаПараметрыОпераций(НомераТаблиц));
		КонецЕсли;
		ТекстЗапроса.Добавить(ТекстЗапроса_ТаблицаГрафикИсходный(НомераТаблиц));		
		ТекстЗапроса.Добавить(ТекстЗапроса_ТаблицаЗадолженностьНаДатуАкцепта(НомераТаблиц));
		ТекстЗапроса.Добавить(ТекстЗапроса_ТаблицаОперацииВекселя(НомераТаблиц));
		ТекстЗапроса.Добавить(ТекстЗапросаГрафикРасчетов(НомераТаблиц, "РасчетыСКонтрагентамиГрафики"));
		ТекстЗапроса.Добавить(ТекстЗапросаСтатусыАкцептаВекселя(НомераТаблиц, "СтатусыАкцептаПереводныхВекселей"));
		ТекстЗапроса.Добавить(ТекстЗапросаРасчетыСКонтрагентамиФакт(НомераТаблиц, "РасчетыСКонтрагентамиФакт"));

		ВстраиваниеУХ.ДополнитьТекстыЗапроса(ТекстыЗапроса, НомераТаблиц, ТекстЗапроса);
		
		ВстраиваниеУХФинансовыеИнструменты.ПодготовитьЗапросФинансовыеИнструменты(Документ, Запрос, ТекстыЗапроса, Регистры);
		
	КонецЕсли;

	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	ВстраиваниеУХФинансовыеИнструменты.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента, Истина, Ложь);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц = Неопределено)

	Если НомераТаблиц <> Неопределено Тогда
		НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());	
	КонецЕсли;	
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.ФинансовыйИнструмент КАК ФинансовыйИнструмент,
	|	Реквизиты.СтатусАкцепта КАК СтатусАкцепта,
	|	Реквизиты.ФинансовыйИнструмент.ПараметрыЦеннойБумаги.Векселедатель КАК Векселедатель,
	|	Реквизиты.ФинансовыйИнструмент.ПараметрыЦеннойБумаги.Должник КАК Должник,
	|	Реквизиты.МоментВремени КАК МоментВремени,
	|	Реквизиты.ДоговорАкцепта КАК ДоговорАкцепта,
	|	Реквизиты.ОбъектРасчетовУХ КАК ОбъектРасчетов,
	|	Реквизиты.ВидОперации КАК ВидОперации,
	|	Реквизиты.СуммаОперации КАК СуммаДокумента,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.НачисленныйРасход КАК НачисленныйРасход,
	|	Реквизиты.СуммаПлатежа КАК СуммаПлатежа,
	|	Реквизиты.ДатаПлатежа КАК ДатаПлатежа,
	|	Реквизиты.Векселедержатель КАК Векселедержатель,
	|	Реквизиты.ЦФО КАК ЦФО,
	|	Реквизиты.Проект КАК Проект,
	|	Реквизиты.ДоговорАкцепта.ВидДоговораУХ КАК ВидДоговораУХ
	|ИЗ
	|	Документ.АкцептПротестПереводногоВекселя КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;

КонецФункции

#Область ВзаиморасчетыУХ

Функция ПараметрыВзаиморасчеты(ДанныеЗаполнения) Экспорт 

	Возврат ВстраиваниеУХФинансовыеИнструменты.ПараметрыВзаиморасчетыУХ(ДанныеЗаполнения);	

КонецФункции

Функция ТекстЗапросаВерсииРасчетов(НомераТаблиц, ИмяТаблицы)

	НомераТаблиц.Вставить(ИмяТаблицы, НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	втРеквизиты.Период КАК Период,
	|	втРеквизиты.Ссылка КАК ВерсияГрафика,
	|	втРеквизиты.ФинансовыйИнструмент КАК ПредметГрафика,
	|	втРеквизиты.Организация КАК Организация,
	|	ИСТИНА КАК ОпорныйГрафик
	|ИЗ
	|	втРеквизиты КАК втРеквизиты";
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапроса_ТаблицаГрафикИсходный(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаГрафикИсходный", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	РасчетыСКонтрагентамиГрафики.Период КАК Период,
	|	РасчетыСКонтрагентамиГрафики.Активность КАК Активность,
	|	РасчетыСКонтрагентамиГрафики.ВидДвижения КАК ВидДвижения,
	|	РасчетыСКонтрагентамиГрафики.ВерсияГрафика КАК ВерсияГрафика,
	|	РасчетыСКонтрагентамиГрафики.Организация КАК Организация,
	|	РасчетыСКонтрагентамиГрафики.Контрагент КАК Контрагент,
	|	РасчетыСКонтрагентамиГрафики.ПредметГрафика КАК ПредметГрафика,
	|	РасчетыСКонтрагентамиГрафики.ЭлементСтруктурыЗадолженности КАК ЭлементСтруктурыЗадолженности,
	|	РасчетыСКонтрагентамиГрафики.Валюта КАК Валюта,
	|	РасчетыСКонтрагентамиГрафики.Сумма КАК Сумма,
	|	РасчетыСКонтрагентамиГрафики.СтатьяБюджета КАК СтатьяБюджета,
	|	РасчетыСКонтрагентамиГрафики.ПриходРасход КАК ПриходРасход,
	|	втРеквизиты.СтатусАкцепта КАК СтатусАкцепта,
	|	втРеквизиты.Векселедатель КАК Векселедатель,
	|	втРеквизиты.Должник КАК Должник,
	|	РасчетыСКонтрагентамиГрафики.Операция КАК Операция,
	|	РасчетыСКонтрагентамиГрафики.Аналитика1 КАК Аналитика1,
	|	РасчетыСКонтрагентамиГрафики.Аналитика2 КАК Аналитика2,
	|	РасчетыСКонтрагентамиГрафики.Аналитика3 КАК Аналитика3,
	|	РасчетыСКонтрагентамиГрафики.Аналитика4 КАК Аналитика4,
	|	РасчетыСКонтрагентамиГрафики.Аналитика5 КАК Аналитика5,
	|	РасчетыСКонтрагентамиГрафики.Аналитика6 КАК Аналитика6,
	|	РасчетыСКонтрагентамиГрафики.ЦФО КАК ЦФО,
	|	РасчетыСКонтрагентамиГрафики.Проект КАК Проект,
	|	втРеквизиты.ВидДоговораУХ КАК ВидДоговораУХ
	|ПОМЕСТИТЬ ТаблицаГрафикИсходный
	|ИЗ
	|	РегистрНакопления.РасчетыСКонтрагентамиГрафики КАК РасчетыСКонтрагентамиГрафики
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ВерсииРасчетов.СрезПоследних(
	|				&ПериодГраница,
	|				Организация = &Организация
	|					И ПредметГрафика В
	|						(ВЫБРАТЬ
	|							т.ФинансовыйИнструмент
	|						ИЗ
	|							втРеквизиты КАК т)) КАК ТаблицаВекселя
	|		ПО РасчетыСКонтрагентамиГрафики.ВерсияГрафика = ТаблицаВекселя.ВерсияГрафика
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРеквизиты КАК втРеквизиты
	|		ПО (ИСТИНА)";

КонецФункции

Функция ТекстЗапроса_ТаблицаЗадолженностьНаДатуАкцепта(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаЗадолженностьНаДатуАкцепта", НомераТаблиц.Количество());

	Возврат
	"ВЫБРАТЬ
	|	ТаблицаГрафикИсходный.ВерсияГрафика КАК ВерсияГрафика,
	|	ТаблицаГрафикИсходный.ПредметГрафика КАК ПредметГрафика,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаГрафикИсходный.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТаблицаГрафикИсходный.Сумма
	|			ИНАЧЕ -ТаблицаГрафикИсходный.Сумма
	|		КОНЕЦ) КАК Сумма,
	|	ТаблицаГрафикИсходный.Организация КАК Организация,
	|	ТаблицаГрафикИсходный.Контрагент КАК Контрагент,
	|	ТаблицаГрафикИсходный.Валюта КАК Валюта,
	|	ТаблицаГрафикИсходный.Векселедатель КАК Векселедатель,
	|	ТаблицаГрафикИсходный.Должник КАК Должник
	|ПОМЕСТИТЬ ТаблицаЗадолженностьНаДатуАкцепта
	|ИЗ
	|	ТаблицаГрафикИсходный КАК ТаблицаГрафикИсходный
	|ГДЕ
	|	ТаблицаГрафикИсходный.Период < &Период
	|	И ТаблицаГрафикИсходный.СтатусАкцепта = ЗНАЧЕНИЕ(Перечисление.СтатусыАкцептаВекселя.Акцептован)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаГрафикИсходный.ПредметГрафика,
	|	ТаблицаГрафикИсходный.ВерсияГрафика,
	|	ТаблицаГрафикИсходный.Должник,
	|	ТаблицаГрафикИсходный.Контрагент,
	|	ТаблицаГрафикИсходный.Векселедатель,
	|	ТаблицаГрафикИсходный.Организация,
	|	ТаблицаГрафикИсходный.Валюта";
	
КонецФункции

Функция ТекстЗапроса_ТаблицаОперацииВекселя(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаОперацииВекселя", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	ОперацииФинансовыхИнструментов.Ссылка КАК Операция,
	|	ОперацииФинансовыхИнструментов.ЭлементСтруктурыЗадолженности КАК ЭлементСтруктурыЗадолженности,
	|	ВЫБОР
	|		КОГДА ОперацииФинансовыхИнструментов.НаправлениеВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Приход)
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	ОперацииФинансовыхИнструментов.ВидБюджета КАК ВидБюджета,
	|	ОперацииФинансовыхИнструментов.НаправлениеДвижения КАК ПриходРасход,
	|	ОперацииФинансовыхИнструментов.ВидОперацииУХ КАК ВидОперацииУХ,
	|	ВТ_ПараметрыОпераций.СтатьяБюджета КАК СтатьяБюджета,
	|	ВТ_ПараметрыОпераций.Аналитика1 КАК Аналитика1,
	|	ВТ_ПараметрыОпераций.Аналитика2 КАК Аналитика2,
	|	ВТ_ПараметрыОпераций.Аналитика3 КАК Аналитика3,
	|	ВТ_ПараметрыОпераций.Аналитика4 КАК Аналитика4,
	|	ВТ_ПараметрыОпераций.Аналитика5 КАК Аналитика5,
	|	ВТ_ПараметрыОпераций.Аналитика6 КАК Аналитика6,
	|	втРеквизиты.ЦФО КАК ЦФО,
	|	втРеквизиты.Проект КАК Проект	
	|ПОМЕСТИТЬ ТаблицаОперацииВекселя
	|ИЗ
	|	Справочник.ОперацииГрафиковДоговоров КАК ОперацииФинансовыхИнструментов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПараметрыОпераций КАК ВТ_ПараметрыОпераций
	|		ПО (ВТ_ПараметрыОпераций.ОперацияГрафика = ОперацииФинансовыхИнструментов.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРеквизиты КАК втРеквизиты
	|		ПО (ИСТИНА)	
	|ГДЕ
	|	ОперацииФинансовыхИнструментов.Родитель В (ЗНАЧЕНИЕ(Справочник.ОперацииГрафиковДоговоров.ВексельПолученный), ЗНАЧЕНИЕ(Справочник.ОперацииГрафиковДоговоров.ВексельВыданный))";

КонецФункции

Функция ТекстЗапросаГрафикРасчетов(НомераТаблиц, ИмяТаблицы)
	
	НомераТаблиц.Вставить(ИмяТаблицы, НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	ТаблицаГрафикИсходный.Период КАК Период,
	|	ТаблицаГрафикИсходный.ВидДоговораУХ КАК ВидДоговораУХ,
	|	ТаблицаГрафикИсходный.ВидДвижения КАК ВидДвижения,
	|	&Ссылка КАК ВерсияГрафика,
	|	ТаблицаГрафикИсходный.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаГрафикИсходный.Период < &Период
	|				ИЛИ ТаблицаГрафикИсходный.СтатусАкцепта = ЗНАЧЕНИЕ(Перечисление.СтатусыАкцептаВекселя.ПротестВНеакцепте)
	|			ТОГДА ТаблицаГрафикИсходный.Векселедатель
	|		ИНАЧЕ ТаблицаГрафикИсходный.Должник
	|	КОНЕЦ КАК Контрагент,
	|	ТаблицаГрафикИсходный.ПредметГрафика КАК ПредметГрафика,
	|	ТаблицаГрафикИсходный.ЭлементСтруктурыЗадолженности КАК ЭлементСтруктурыЗадолженности,
	|	ТаблицаГрафикИсходный.Валюта КАК Валюта,
	|	ТаблицаГрафикИсходный.Сумма КАК Сумма,
	|	ТаблицаГрафикИсходный.СтатьяБюджета КАК СтатьяБюджета,
	|	ТаблицаГрафикИсходный.ПриходРасход КАК ПриходРасход,
	|	ТаблицаГрафикИсходный.Операция КАК Операция,
	|	ТаблицаГрафикИсходный.Аналитика1 КАК Аналитика1,
	|	ТаблицаГрафикИсходный.Аналитика2 КАК Аналитика2,
	|	ТаблицаГрафикИсходный.Аналитика3 КАК Аналитика3,
	|	ТаблицаГрафикИсходный.Аналитика4 КАК Аналитика4,
	|	ТаблицаГрафикИсходный.Аналитика5 КАК Аналитика5,
	|	ТаблицаГрафикИсходный.Аналитика6 КАК Аналитика6,
	|	ТаблицаГрафикИсходный.ЦФО КАК ЦФО,
	|	ТаблицаГрафикИсходный.Проект КАК Проект
	|ИЗ
	|	ТаблицаГрафикИсходный КАК ТаблицаГрафикИсходный
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	NULL,
	|	ТаблицаОперацииВекселя.ВидДвижения,
	|	&Ссылка,
	|	ТаблицаЗадолженностьНаДатуАкцепта.Организация,
	|	ТаблицаЗадолженностьНаДатуАкцепта.Контрагент,
	|	ТаблицаЗадолженностьНаДатуАкцепта.ПредметГрафика,
	|	ТаблицаОперацииВекселя.ЭлементСтруктурыЗадолженности,
	|	ТаблицаЗадолженностьНаДатуАкцепта.Валюта,
	|	ТаблицаЗадолженностьНаДатуАкцепта.Сумма,
	|	ТаблицаОперацииВекселя.СтатьяБюджета,
	|	ТаблицаОперацииВекселя.ПриходРасход,                               
	|	ТаблицаОперацииВекселя.Операция,
	|	ТаблицаОперацииВекселя.Аналитика1,
	|	ТаблицаОперацииВекселя.Аналитика2,
	|	ТаблицаОперацииВекселя.Аналитика3,
	|	ТаблицаОперацииВекселя.Аналитика4,
	|	ТаблицаОперацииВекселя.Аналитика5,
	|	ТаблицаОперацииВекселя.Аналитика6,
	|	ТаблицаОперацииВекселя.ЦФО,
	|	ТаблицаОперацииВекселя.Проект	
	|ИЗ
	|	ТаблицаЗадолженностьНаДатуАкцепта КАК ТаблицаЗадолженностьНаДатуАкцепта
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОперацииВекселя КАК ТаблицаОперацииВекселя
	|		ПО (ТаблицаОперацииВекселя.Операция = ЗНАЧЕНИЕ(Справочник.ОперацииГрафиковДоговоров.ВексельПолученный_ПогашениеВекселя))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	втРеквизиты.ВидДоговораУХ,
	|	ТаблицаОперацииВекселя.ВидДвижения,
	|	&Ссылка,
	|	ТаблицаЗадолженностьНаДатуАкцепта.Организация,
	|	втРеквизиты.Должник,
	|	ТаблицаЗадолженностьНаДатуАкцепта.ПредметГрафика,
	|	ТаблицаОперацииВекселя.ЭлементСтруктурыЗадолженности,
	|	ТаблицаЗадолженностьНаДатуАкцепта.Валюта,
	|	ТаблицаЗадолженностьНаДатуАкцепта.Сумма,
	|	ТаблицаОперацииВекселя.СтатьяБюджета,
	|	ТаблицаОперацииВекселя.ПриходРасход,
	|	ТаблицаОперацииВекселя.Операция,
	|	ТаблицаОперацииВекселя.Аналитика1,
	|	ТаблицаОперацииВекселя.Аналитика2,
	|	ТаблицаОперацииВекселя.Аналитика3,
	|	ТаблицаОперацииВекселя.Аналитика4,
	|	ТаблицаОперацииВекселя.Аналитика5,
	|	ТаблицаОперацииВекселя.Аналитика6,
	|	ТаблицаОперацииВекселя.ЦФО,
	|	ТаблицаОперацииВекселя.Проект	
	|ИЗ
	|	ТаблицаЗадолженностьНаДатуАкцепта КАК ТаблицаЗадолженностьНаДатуАкцепта
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОперацииВекселя КАК ТаблицаОперацииВекселя
	|		ПО (ТаблицаОперацииВекселя.Операция = ЗНАЧЕНИЕ(Справочник.ОперацииГрафиковДоговоров.ВексельПолученный_ПолучениеВекселя))
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРеквизиты КАК втРеквизиты
	|		ПО (ИСТИНА)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втРеквизиты.Период,
	|	втРеквизиты.ВидДоговораУХ,
	|	ТаблицаОперацииВекселя.ВидДвижения,
	|	втРеквизиты.Ссылка,
	|	втРеквизиты.Организация,
	|	втРеквизиты.Векселедержатель,
	|	втРеквизиты.ФинансовыйИнструмент,
	|	ТаблицаОперацииВекселя.ЭлементСтруктурыЗадолженности,
	|	втРеквизиты.ВалютаДокумента,
	|	втРеквизиты.СуммаДокумента,
	|	ТаблицаОперацииВекселя.СтатьяБюджета,
	|	ТаблицаОперацииВекселя.ПриходРасход,
	|	ТаблицаОперацииВекселя.Операция,
	|	ТаблицаОперацииВекселя.Аналитика1,
	|	ТаблицаОперацииВекселя.Аналитика2,
	|	ТаблицаОперацииВекселя.Аналитика3,
	|	ТаблицаОперацииВекселя.Аналитика4,
	|	ТаблицаОперацииВекселя.Аналитика5,
	|	ТаблицаОперацииВекселя.Аналитика6,
	|	ТаблицаОперацииВекселя.ЦФО,
	|	ТаблицаОперацииВекселя.Проект	
	|ИЗ
	|	ТаблицаОперацииВекселя КАК ТаблицаОперацииВекселя
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРеквизиты КАК втРеквизиты
	|		ПО (ИСТИНА)
	|ГДЕ
	|	втРеквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАкцептПротестПереводногоВекселя.УведомлениеОПротестеПоВекселю)
	|	И ТаблицаОперацииВекселя.Операция = ЗНАЧЕНИЕ(Справочник.ОперацииГрафиковДоговоров.ВексельВыданный_ВыдачаВекселя)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втРеквизиты.Период,
	|	втРеквизиты.ВидДоговораУХ,
	|	ТаблицаОперацииВекселя.ВидДвижения,
	|	втРеквизиты.Ссылка,
	|	втРеквизиты.Организация,
	|	втРеквизиты.Векселедержатель,
	|	втРеквизиты.ФинансовыйИнструмент,
	|	ТаблицаОперацииВекселя.ЭлементСтруктурыЗадолженности,
	|	втРеквизиты.ВалютаДокумента,
	|	втРеквизиты.НачисленныйРасход,
	|	ТаблицаОперацииВекселя.СтатьяБюджета,
	|	ТаблицаОперацииВекселя.ПриходРасход,
	|	ТаблицаОперацииВекселя.Операция,
	|	ТаблицаОперацииВекселя.Аналитика1,
	|	ТаблицаОперацииВекселя.Аналитика2,
	|	ТаблицаОперацииВекселя.Аналитика3,
	|	ТаблицаОперацииВекселя.Аналитика4,
	|	ТаблицаОперацииВекселя.Аналитика5,
	|	ТаблицаОперацииВекселя.Аналитика6,
	|	ТаблицаОперацииВекселя.ЦФО,
	|	ТаблицаОперацииВекселя.Проект	
	|ИЗ
	|	ТаблицаОперацииВекселя КАК ТаблицаОперацииВекселя
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРеквизиты КАК втРеквизиты
	|		ПО (ИСТИНА)
	|ГДЕ
	|	втРеквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАкцептПротестПереводногоВекселя.УведомлениеОПротестеПоВекселю)
	|	И ТаблицаОперацииВекселя.Операция = ЗНАЧЕНИЕ(Справочник.ОперацииГрафиковДоговоров.ВексельВыданный_НачислениеРасхода)
	|	И втРеквизиты.НачисленныйРасход <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаГрафикНачислений.Дата,
	|	втРеквизиты.ВидДоговораУХ,
	|	ТаблицаОперацииВекселя.ВидДвижения,
	|	втРеквизиты.Ссылка,
	|	втРеквизиты.Организация,
	|	втРеквизиты.Векселедержатель,
	|	втРеквизиты.ФинансовыйИнструмент,
	|	ТаблицаОперацииВекселя.ЭлементСтруктурыЗадолженности,
	|	втРеквизиты.ВалютаДокумента,
	|	ТаблицаГрафикНачислений.Сумма,
	|	ТаблицаОперацииВекселя.СтатьяБюджета,
	|	ТаблицаОперацииВекселя.ПриходРасход,
	|	ТаблицаОперацииВекселя.Операция,
	|	ТаблицаОперацииВекселя.Аналитика1,
	|	ТаблицаОперацииВекселя.Аналитика2,
	|	ТаблицаОперацииВекселя.Аналитика3,
	|	ТаблицаОперацииВекселя.Аналитика4,
	|	ТаблицаОперацииВекселя.Аналитика5,
	|	ТаблицаОперацииВекселя.Аналитика6,
	|	ТаблицаОперацииВекселя.ЦФО,
	|	ТаблицаОперацииВекселя.Проект	
	|ИЗ
	|	Документ.АкцептПротестПереводногоВекселя.График КАК ТаблицаГрафикНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОперацииВекселя КАК ТаблицаОперацииВекселя
	|		ПО (ТаблицаОперацииВекселя.Операция = ЗНАЧЕНИЕ(Справочник.ОперацииГрафиковДоговоров.ВексельВыданный_НачислениеРасхода))
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРеквизиты КАК втРеквизиты
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ТаблицаГрафикНачислений.Ссылка = &Ссылка
	|	И втРеквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАкцептПротестПереводногоВекселя.УведомлениеОПротестеПоВекселю)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втРеквизиты.ДатаПлатежа,
	|	втРеквизиты.ВидДоговораУХ,
	|	ТаблицаОперацииВекселя.ВидДвижения,
	|	втРеквизиты.Ссылка,
	|	втРеквизиты.Организация,
	|	втРеквизиты.Векселедержатель,
	|	втРеквизиты.ФинансовыйИнструмент,
	|	ТаблицаОперацииВекселя.ЭлементСтруктурыЗадолженности,
	|	втРеквизиты.ВалютаДокумента,
	|	втРеквизиты.СуммаПлатежа,
	|	ТаблицаОперацииВекселя.СтатьяБюджета,
	|	ТаблицаОперацииВекселя.ПриходРасход,
	|	ТаблицаОперацииВекселя.Операция,
	|	ТаблицаОперацииВекселя.Аналитика1,
	|	ТаблицаОперацииВекселя.Аналитика2,
	|	ТаблицаОперацииВекселя.Аналитика3,
	|	ТаблицаОперацииВекселя.Аналитика4,
	|	ТаблицаОперацииВекселя.Аналитика5,
	|	ТаблицаОперацииВекселя.Аналитика6,
	|	ТаблицаОперацииВекселя.ЦФО,
	|	ТаблицаОперацииВекселя.Проект	
	|ИЗ
	|	ТаблицаОперацииВекселя КАК ТаблицаОперацииВекселя
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРеквизиты КАК втРеквизиты
	|		ПО (ИСТИНА)
	|ГДЕ
	|	втРеквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАкцептПротестПереводногоВекселя.УведомлениеОПротестеПоВекселю)
	|	И ТаблицаОперацииВекселя.Операция = ЗНАЧЕНИЕ(Справочник.ОперацииГрафиковДоговоров.ВексельВыданный_ПогашениеВекселя)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";

КонецФункции

Функция ТекстЗапросаРасчетыСКонтрагентамиФакт(НомераТаблиц, ИмяТаблицы)
	
	НомераТаблиц.Вставить(ИмяТаблицы, НомераТаблиц.Количество());
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	втРеквизиты.Период КАК Период,
	|	втРеквизиты.ВидДоговораУХ КАК ВидДоговораУХ,
	|	втРеквизиты.Организация КАК Организация,
	|	втРеквизиты.Контрагент КАК Контрагент,
	|	втРеквизиты.ВалютаДокумента КАК Валюта,
	|	втРеквизиты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	втРеквизиты.СуммаДокумента КАК Сумма,
	|	NULL КАК Операция,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ЗНАЧЕНИЕ(Перечисление.ЭлементыСтруктурыЗадолженности.ОсновнойДолг) КАК ЭлементСтруктурыЗадолженности,
	|	NULL КАК СтатьяБюджета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.ПустаяСсылка) КАК ПриходРасход
	|ИЗ
	|	втРеквизиты КАК втРеквизиты
	|ГДЕ
	|	втРеквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАкцептПротестПереводногоВекселя.УведомлениеОбАкцептеВекселя)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	втРеквизиты.ВидДоговораУХ,
	|	РасчетыСКонтрагентамиФактОстатки.Организация,
	|	РасчетыСКонтрагентамиФактОстатки.Контрагент,
	|	РасчетыСКонтрагентамиФактОстатки.Валюта,
	|	РасчетыСКонтрагентамиФактОстатки.ОбъектРасчетов,
	|	-РасчетыСКонтрагентамиФактОстатки.СуммаОстаток,
	|	NULL,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	РасчетыСКонтрагентамиФактОстатки.ЭлементСтруктурыЗадолженности,
	|	NULL,
	|	NULL
	|ИЗ
	|	РегистрНакопления.РасчетыСКонтрагентамиФакт.Остатки(
	|			&ПериодГраница,
	|			Организация = &Организация
	|				И ОбъектРасчетов В
	|					(ВЫБРАТЬ
	|						т.ФинансовыйИнструмент
	|					ИЗ
	|						втРеквизиты КАК т)) КАК РасчетыСКонтрагентамиФактОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРеквизиты КАК втРеквизиты
	|		ПО (ИСТИНА)
	|ГДЕ
	|	втРеквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАкцептПротестПереводногоВекселя.УведомлениеОбАкцептеВекселя)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	втРеквизиты.ВидДоговораУХ,
	|	РасчетыСКонтрагентамиФактОстатки.Организация,
	|	РасчетыСКонтрагентамиФактОстатки.Контрагент,
	|	РасчетыСКонтрагентамиФактОстатки.Валюта,
	|	РасчетыСКонтрагентамиФактОстатки.ОбъектРасчетов,
	|	РасчетыСКонтрагентамиФактОстатки.СуммаОстаток,
	|	NULL,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	РасчетыСКонтрагентамиФактОстатки.ЭлементСтруктурыЗадолженности,
	|	NULL,
	|	NULL
	|ИЗ
	|	РегистрНакопления.РасчетыСКонтрагентамиФакт.Остатки(
	|			&ПериодГраница,
	|			Организация = &Организация
	|				И ОбъектРасчетов В
	|					(ВЫБРАТЬ
	|						т.ФинансовыйИнструмент
	|					ИЗ
	|						втРеквизиты КАК т)) КАК РасчетыСКонтрагентамиФактОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРеквизиты КАК втРеквизиты
	|		ПО (ИСТИНА)
	|ГДЕ
	|	втРеквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАкцептПротестПереводногоВекселя.ПредъявлениеВекселяКАкцепту)
	|	И втРеквизиты.СтатусАкцепта = ЗНАЧЕНИЕ(Перечисление.СтатусыАкцептаВекселя.Акцептован)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	втРеквизиты.ВидДоговораУХ,
	|	РасчетыСКонтрагентамиФактОстатки.Организация,
	|	втРеквизиты.Контрагент,
	|	РасчетыСКонтрагентамиФактОстатки.Валюта,
	|	РасчетыСКонтрагентамиФактОстатки.ОбъектРасчетов,
	|	РасчетыСКонтрагентамиФактОстатки.СуммаОстаток,
	|	NULL,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	РасчетыСКонтрагентамиФактОстатки.ЭлементСтруктурыЗадолженности,
	|	NULL,
	|	NULL
	|ИЗ
	|	РегистрНакопления.РасчетыСКонтрагентамиФакт.Остатки(
	|			&ПериодГраница,
	|			Организация = &Организация
	|				И ОбъектРасчетов В
	|					(ВЫБРАТЬ
	|						т.ФинансовыйИнструмент
	|					ИЗ
	|						втРеквизиты КАК т)) КАК РасчетыСКонтрагентамиФактОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРеквизиты КАК втРеквизиты
	|		ПО (ИСТИНА)
	|ГДЕ
	|	втРеквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАкцептПротестПереводногоВекселя.ПредъявлениеВекселяКАкцепту)
	|	И втРеквизиты.СтатусАкцепта = ЗНАЧЕНИЕ(Перечисление.СтатусыАкцептаВекселя.Акцептован)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втРеквизиты.Период,
	|	втРеквизиты.ВидДоговораУХ,
	|	втРеквизиты.Организация,
	|	втРеквизиты.Векселедержатель,
	|	втРеквизиты.ВалютаДокумента,
	|	втРеквизиты.ФинансовыйИнструмент,
	|	втРеквизиты.НачисленныйРасход,
	|	NULL,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ЗНАЧЕНИЕ(Перечисление.ЭлементыСтруктурыЗадолженности.ОсновнойДолг),
	|	NULL,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.ПустаяСсылка)
	|ИЗ
	|	втРеквизиты КАК втРеквизиты
	|ГДЕ
	|	втРеквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАкцептПротестПереводногоВекселя.УведомлениеОПротестеПоВекселю)
	|	И втРеквизиты.НачисленныйРасход <> 0";
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаСтатусыАкцептаВекселя(НомераТаблиц, ИмяТаблицы)
	
	НомераТаблиц.Вставить(ИмяТаблицы, НомераТаблиц.Количество());
	
	Возврат 
	"ВЫБРАТЬ
	|	т.Период КАК Период,
	|	т.ФинансовыйИнструмент КАК Вексель,
	|	т.Организация КАК Организация,
	|	т.СтатусАкцепта КАК СтатусАкцепта
	|ИЗ
	|	втРеквизиты КАК т";
	
КонецФункции

Функция ТекстЗапросаПараметрыОпераций(НомераТаблиц)

	НомераТаблиц.Вставить("ВТ_ПараметрыОпераций", НомераТаблиц.Количество());
		
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПараметрыОпераций.ОперацияГрафика КАК ОперацияГрафика,
	|	ПараметрыОпераций.СтатьяБюджета КАК СтатьяБюджета,
	|	ПараметрыОпераций.Аналитика1 КАК Аналитика1,
	|	ПараметрыОпераций.Аналитика2 КАК Аналитика2,
	|	ПараметрыОпераций.Аналитика3 КАК Аналитика3,
	|	ПараметрыОпераций.Аналитика4 КАК Аналитика4,
	|	ПараметрыОпераций.Аналитика5 КАК Аналитика5,
	|	ПараметрыОпераций.Аналитика6 КАК Аналитика6
	|ПОМЕСТИТЬ ВТ_ПараметрыОпераций
	|ИЗ
	|	Документ.АкцептПротестПереводногоВекселя.ПараметрыОпераций КАК ПараметрыОпераций
	|ГДЕ
	|	ПараметрыОпераций.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;

КонецФункции
#КонецОбласти

#Область ПроводкиРСБУ

Функция ТекстЗапроса_СчетаДокумента(НомераТаблиц) Экспорт

	НомераТаблиц.Вставить("втСчетаДокумента", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	т.Ссылка.ФинансовыйИнструмент КАК ФинансовыйИнструмент,
	|	т.Ссылка КАК Ссылка,
	|	т.ВидСчетаФИ КАК ВидСчетаФИ,
	|	т.Счет КАК Счет,
	|	т.Субконто1 КАК Субконто1,
	|	т.Субконто2 КАК Субконто2,
	|	т.Субконто3 КАК Субконто3
	|ПОМЕСТИТЬ втСчетаДокумента
	|ИЗ
	|	Документ.АкцептПротестПереводногоВекселя.СчетаУчета КАК т
	|ГДЕ
	|	т.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Документ.ФинансовыйИнструмент,
	|	Документ.Ссылка,
	|	ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетВзаиморасчетовСПлательщиком),
	|	Документ.СчетВзаиморасчетовСПлательщиком,
	|	Документ.Контрагент,
	|	Документ.ДоговорАкцепта,
	|	NULL
	|ИЗ
	|	Документ.АкцептПротестПереводногоВекселя КАК Документ
	|ГДЕ
	|	Документ.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	т.Ссылка.ФинансовыйИнструмент,
	|	т.Ссылка,
	|	т.ВидСчетаФИ,
	|	т.Счет,
	|	т.Субконто1,
	|	т.Субконто2,
	|	т.Субконто3
	|ИЗ
	|	Документ.ПоступлениеВекселя.СчетаУчета КАК т
	|ГДЕ
	|	т.Ссылка В
	|			(ВЫБРАТЬ
	|				т.Регистратор
	|			ИЗ
	|				втВерсияДоДокумента КАК т)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	т.Ссылка.ФинансовыйИнструмент,
	|	т.Ссылка,
	|	ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетУчетаДисконтаПлательщика),
	|	т.Счет,
	|	ВЫБОР ТИПЗНАЧЕНИЯ(т.Субконто1)
	|		КОГДА ТИП(Справочник.Контрагенты)
	|			ТОГДА т.Ссылка.Контрагент
	|		КОГДА ТИП(Справочник.ДоговорыКонтрагентов)
	|			ТОГДА т.Ссылка.ДоговорАкцепта
	|		ИНАЧЕ т.Субконто1
	|	КОНЕЦ,
	|	ВЫБОР ТИПЗНАЧЕНИЯ(т.Субконто2)
	|		КОГДА ТИП(Справочник.Контрагенты)
	|			ТОГДА т.Ссылка.Контрагент
	|		КОГДА ТИП(Справочник.ДоговорыКонтрагентов)
	|			ТОГДА т.Ссылка.ДоговорАкцепта
	|		ИНАЧЕ т.Субконто2
	|	КОНЕЦ,
	|	ВЫБОР ТИПЗНАЧЕНИЯ(т.Субконто3)
	|		КОГДА ТИП(Справочник.Контрагенты)
	|			ТОГДА т.Ссылка.Контрагент
	|		КОГДА ТИП(Справочник.ДоговорыКонтрагентов)
	|			ТОГДА т.Ссылка.ДоговорАкцепта
	|		ИНАЧЕ т.Субконто3
	|	КОНЕЦ
	|ИЗ
	|	Документ.ПоступлениеВекселя.СчетаУчета КАК т
	|ГДЕ
	|	т.Ссылка В
	|			(ВЫБРАТЬ
	|				т.Регистратор
	|			ИЗ
	|				втВерсияДоДокумента КАК т)
	|	И т.ВидСчетаФИ = ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетУчетаДисконта)"
	
КонецФункции

Функция ТекстЗапроса_ОстаткиПоФИ(НомераТаблиц)

	НомераТаблиц.Вставить("втОстатки", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.Счет.Валютный
	|			ТОГДА ХозрасчетныйОстатки.ВалютнаяСуммаОстаток
	|		ИНАЧЕ ХозрасчетныйОстатки.СуммаОстаток
	|	КОНЕЦ КАК СуммаОстаток,
	|	ХозрасчетныйОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|	ЕстьNULL(ХозрасчетныйОстатки.Валюта, &ВалютаДокумента) КАК Валюта
	|ПОМЕСТИТЬ втОстатки
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ПериодГраница,
	|			Счет В
	|				(ВЫБРАТЬ
	|					т.Ссылка
	|				ИЗ
	|					ПланСчетов.Хозрасчетный.ВидыСубконто КАК т
	|				ГДЕ
	|					т.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ЦенныеБумаги)),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ЦенныеБумаги),
	|			Организация = &Организация
	|				И Субконто1 В (ВЫБРАТЬ т.ФинансовыйИнструмент ИЗ втРеквизиты КАК т)
	|				) КАК ХозрасчетныйОстатки";

КонецФункции

Функция ТекстЗапроса_Проводки(НомераТаблиц, ВидОперации = Неопределено) Экспорт
		
	ТекстПроводки = Новый Массив;	
	НомераТаблиц.Вставить("втТаблицаПроводок", НомераТаблиц.Количество());
	
	//УведомлениеОПротестеПоВекселю - нет проводок
	
	ТекстПроводки.Добавить(ТекстЗапроса_АкцептВекселедержателем());//ПредъявлениеВекселяКАкцепту
	ТекстПроводки.Добавить(ТекстЗапроса_АкцептВекселедержателемДисконт());//ПредъявлениеВекселяКАкцепту
	
	ТекстПроводки.Добавить(ТекстЗапроса_ПереводЗадолженностиНаПлательщика());//УведомлениеОбАкцептеВекселя
	ТекстПроводки.Добавить(ТекстЗапроса_ДисконтПремияПоВекселю());//УведомлениеОбАкцептеВекселя
	
	Разделитель = "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|";
	
	Возврат СтрСоединить(ТекстПроводки, Разделитель);

КонецФункции

#Область ПредъявлениеВекселяКАкцепту

Функция ТекстЗапроса_АкцептВекселедержателем()

	Возврат
	"ВЫБРАТЬ
	|	т.ФинансовыйИнструмент КАК ФинансовыйИнструмент,
	|	т.Дата КАК Период,
	|	т.ФинансовыйИнструмент.ВалютаНоминала КАК Валюта,
	|	1 КАК Количество,
	|	втОстатки.СуммаОстаток КАК Сумма,
	|	ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетВзаиморасчетовСПлательщиком) КАК СчетДт,
	|	ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетУчетаФИ) КАК СчетКт,
	|	""Акцепт переводного векселя"" КАК Комментарий
	|ПОМЕСТИТЬ втТаблицаПроводок
	|ИЗ
	|	Документ.АкцептПротестПереводногоВекселя КАК т
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстатки КАК втОстатки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСчетаДокумента КАК втСчетаДокумента
	|			ПО втОстатки.Счет = втСчетаДокумента.Счет
	|				И (втСчетаДокумента.ВидСчетаФИ = ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетУчетаФИ))
	|		ПО (ИСТИНА)
	|ГДЕ
	|	т.Ссылка = &Ссылка
	|	И т.СтатусАкцепта = ЗНАЧЕНИЕ(Перечисление.СтатусыАкцептаВекселя.Акцептован)
	|	И ВЫБОР
	|			КОГДА т.ФинансовыйИнструмент.Эмитент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА т.ФинансовыйИнструмент.Эмитент <> т.Контрагент
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И т.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАкцептПротестПереводногоВекселя.ПредъявлениеВекселяКАкцепту)";

КонецФункции

Функция ТекстЗапроса_АкцептВекселедержателемДисконт()

	Возврат
	"ВЫБРАТЬ
	|	т.ФинансовыйИнструмент КАК ФинансовыйИнструмент,
	|	т.Дата КАК Период,
	|	т.ФинансовыйИнструмент.ВалютаНоминала КАК Валюта,
	|	1 КАК Количество,
	|	втОстатки.СуммаОстаток КАК Сумма,
	|	ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетУчетаДисконтаПлательщика) КАК СчетДт,
	|	ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетУчетаДисконта) КАК СчетКт,
	|	""Перевод дисконта при акцепте переводного векселя"" КАК Комментарий
	|ИЗ
	|	Документ.АкцептПротестПереводногоВекселя КАК т
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстатки КАК втОстатки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСчетаДокумента КАК втСчетаДокумента
	|			ПО втОстатки.Счет = втСчетаДокумента.Счет
	|				И (втСчетаДокумента.ВидСчетаФИ = ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетУчетаДисконта))
	|				И (НЕ втОстатки.Счет В
	|						(ВЫБРАТЬ
	|							т.Счет
	|						ИЗ
	|							втСчетаДокумента КАК т
	|						ГДЕ
	|							т.ВидСчетаФИ = ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетУчетаФИ)))
	|		ПО (ИСТИНА)
	|ГДЕ
	|	т.Ссылка = &Ссылка
	|	И т.СтатусАкцепта = ЗНАЧЕНИЕ(Перечисление.СтатусыАкцептаВекселя.Акцептован)
	|	И ВЫБОР
	|			КОГДА т.ФинансовыйИнструмент.Эмитент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА т.ФинансовыйИнструмент.Эмитент <> т.Контрагент
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И т.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАкцептПротестПереводногоВекселя.ПредъявлениеВекселяКАкцепту)";

КонецФункции

#КонецОбласти

#Область УведомлениеОбАкцептеВекселя

Функция ТекстЗапроса_ПереводЗадолженностиНаПлательщика()

	Возврат
	"ВЫБРАТЬ
	|	т.ФинансовыйИнструмент КАК ФинансовыйИнструмент,
	|	т.Дата КАК Период,
	|	т.ВалютаДокумента КАК Валюта,
	|	1 КАК Количество,
	|	т.ФинансовыйИнструмент.Номинал КАК Сумма,
	|	ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетУчетаФИ) КАК СчетДт,
	|	ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетВзаиморасчетовСПлательщиком) КАК СчетКт,
	|	""Взаимозачет долга плательщика и выпущенного векселя"" КАК Комментарий
	|
	|ИЗ
	|	Документ.АкцептПротестПереводногоВекселя КАК т
	|ГДЕ
	|	т.Ссылка = &Ссылка
	|	И т.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАкцептПротестПереводногоВекселя.УведомлениеОбАкцептеВекселя)";

КонецФункции

Функция ТекстЗапроса_ДисконтПремияПоВекселю()

	Возврат
	"ВЫБРАТЬ
	|	втСобытия.ФинансовыйИнструмент КАК ФинансовыйИнструмент,
	|	втСобытия.Дата КАК Период,
	|	ЕСТЬNULL(втОстатки.Валюта, &ВалютаРегламентированногоУчета) КАК Валюта,
	|	втОстатки.КоличествоОстаток КАК Количество,
	|	ВЫБОР
	|		КОГДА втОстатки.СуммаОстаток > 0
	|			ТОГДА втОстатки.СуммаОстаток
	|		ИНАЧЕ -втОстатки.СуммаОстаток
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА втОстатки.СуммаОстаток > 0
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетНачисленияПроцентов)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетДБП)
	|	КОНЕЦ КАК СчетДт,
	|	ВЫБОР
	|		КОГДА втОстатки.СуммаОстаток > 0
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетРБП)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетНачисленияПроцентов)
	|	КОНЕЦ КАК СчетКт,
	|	ВЫБОР
	|		КОГДА втОстатки.СуммаОстаток > 0
	|			ТОГДА ""Списание остатка РБП""
	|		ИНАЧЕ ""Списание остатка ДБП""
	|	КОНЕЦ КАК Комментарий
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ПериодДоДокумента,
	|			Счет В
	|				(ВЫБРАТЬ
	|					т.Счет
	|				ИЗ
	|					втСчетаДокумента КАК т),
	|			,
	|			Организация = &Организация
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						т.Субконто1
	|					ИЗ
	|						втСчетаДокумента КАК т
	|					ГДЕ
	|						ТИПЗНАЧЕНИЯ(т.Субконто1) В (&ТипСсылкаРБП, ТИП(Справочник.ДоходыБудущихПериодов))
	|						И НЕ т.Субконто1 В (&ПустаяСсылкаРБП, ЗНАЧЕНИЕ(Справочник.ДоходыБудущихПериодов.ПустаяСсылка), НЕОПРЕДЕЛЕНО))) КАК втОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСобытия КАК втСобытия
	|		ПО (ИСТИНА)
	|ГДЕ
	|	втОстатки.СуммаОстаток <> 0
	|	И втСобытия.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАкцептПротестПереводногоВекселя.ПредъявлениеВекселяКАкцепту)";

КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти
	
#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Подразделение)";

КонецПроцедуры

#Область Отчеты

// Заполняет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	ФинансовыеИнструментыУХ.ДобавитьКомандыОтчетовФИ(КомандыОтчетов, Параметры);
КонецПроцедуры

#КонецОбласти

#Область СозданиеНаОсновании

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.АкцептПротестПереводногоВекселя.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Документы.ВыбытиеВекселей.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Документы.ПоступлениеВекселя.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	ВстраиваниеУХФинансовыеИнструменты.ДобавитьКомандыСозданияНаОсновании_ВариантыОплат(КомандыСозданияНаОсновании, Параметры);
	
КонецПроцедуры

// Добавляет команду создания документа
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Возврат СозданиеНаОсновании.ДобавитьКомандуСозданияНаОсновании(КомандыСозданияНаОсновании, ПустаяСсылка().Метаданные());
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли