
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ПриЧтенииСозданииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПриЧтенииСозданииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	ЭтотОбъектСервер = РеквизитФормыВЗначение("Объект");
	Для каждого Стр Из ЭтотОбъектСервер.ПереченьДокументов Цикл
		Вложение = Стр.Скан.Получить();
		ЕстьВложение = ?(Вложение <> Неопределено, 1, 0);
		Для каждого Стр2 Из Объект.ПереченьДокументов Цикл
			Если Стр2.ИсходныйНомерСтроки = Стр.НомерСтроки Тогда
				Стр2.Вложение = ЕстьВложение;
				Стр2.АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(Вложение, УникальныйИдентификатор);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПереченьДокументовВложениеНажатиеЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если (ВыбранныеФайлы <> Неопределено) Тогда
		ВложитьФайл(Элементы.ПереченьДокументов.ТекущаяСтрока, ВыбранныеФайлы[0]);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайл(Строка)
#Если ВебКлиент Тогда
	СохранитьФайл(Неопределено);
#Иначе
	Стр = Объект.ПереченьДокументов.НайтиПоИдентификатору(Строка);
	ВремФайл = ПолучитьИмяВременногоФайла(Стр.Расширение);
	Данные = ПолучитьИзВременногоХранилища(Стр.АдресВоВременномХранилище);
	Данные.Записать(ВремФайл);
	ЗапуститьПриложение(ВремФайл);
#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ВложитьФайл(Строка, Файл)
	Стр = Объект.ПереченьДокументов.НайтиПоИдентификатору(Строка);
	Адрес = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(Файл), УникальныйИдентификатор);
	Стр.Вложение = 1;
	Стр.АдресВоВременномХранилище = Адрес;
	РазборФайла = Новый Файл(Файл);
	ЗаполнитьЗначенияСвойств(Стр, РазборФайла);
КонецПроцедуры

&НаКлиенте
Процедура ПереченьДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле.Имя = "ПереченьДокументовВложение" Тогда
		Если Элементы.ПереченьДокументов.ТекущиеДанные.Вложение = 1 Тогда
			ОткрытьФайл(Элементы.ПереченьДокументов.ТекущаяСтрока);
		Иначе
			Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
			Диалог.МножественныйВыбор = Ложь;
			Диалог.Показать(Новый ОписаниеОповещения("ПереченьДокументовВложениеНажатиеЗавершение", ЭтотОбъект));
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Для каждого Стр Из Объект.ПереченьДокументов Цикл
		Стр2 = ТекущийОбъект.ПереченьДокументов.Найти(Стр.ИсходныйНомерСтроки, "НомерСтроки");
		Если Стр2 <> Неопределено Тогда
			Если Стр.Вложение = 0 Тогда
				Стр2.Скан = Новый ХранилищеЗначения(Неопределено);
			Иначе
				Стр2.Скан = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(Стр.АдресВоВременномХранилище));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьВложение(Команда)
	Если Элементы.ПереченьДокументов.ТекущиеДанные <> Неопределено Тогда
		Элементы.ПереченьДокументов.ТекущиеДанные.Вложение = 0;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СохранитьФайл(Команда)
	Если Элементы.ПереченьДокументов.ТекущиеДанные <> Неопределено 
				И Элементы.ПереченьДокументов.ТекущиеДанные.Вложение <> 0 Тогда
				
		Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
		Диалог.ПолноеИмяФайла = Элементы.ПереченьДокументов.ТекущиеДанные.Имя;
		Диалог.Фильтр = "*." + Элементы.ПереченьДокументов.ТекущиеДанные.Расширение + "|*." + "*." + Элементы.ПереченьДокументов.ТекущиеДанные.Расширение;
		Диалог.Показать(Новый ОписаниеОповещения("СохранитьФайлЗавершение", ЭтотОбъект, Новый Структура("Диалог", Диалог)));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СохранитьФайлЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Диалог = ДополнительныеПараметры.Диалог;
	Если (ВыбранныеФайлы <> Неопределено) Тогда
		Данные = ПолучитьИзВременногоХранилища(Элементы.ПереченьДокументов.ТекущиеДанные.АдресВоВременномХранилище);
		Данные.Записать(Диалог.ПолноеИмяФайла);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ПриЧтенииСозданииНаСервере();
КонецПроцедуры

