
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытийОбъекта

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = НачалоДня(ТекущаяДатаСеанса());
	Ответственный = ОбщегоНазначенияУХ.ТекущийПользователь();
	
	//Для каждого СтрокаВНА Из ВНА Цикл
	//	
	//	СтрокаВНА.ПервоначальнаяСтоимость = 0;
	//	СтрокаВНА.ЛиквидационнаяСтоимость = 0;
	//	
	//КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
		
	МСФОВНАВызовСервераУХ.ОбновитьПроверяемыеРеквизитыТаблицыПоРежимуЗаполнения(ПроверяемыеРеквизиты, ЭтотОбъект, "ВНА");
		
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСерверУХ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);	
	Документы.ИзменениеПараметровВНАМСФО.ПодготовитьПараметрыПроведения(ДополнительныеСвойства, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСерверУХ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	Документы.ИзменениеПараметровВНАМСФО.СформироватьДвижения(Движения, ДополнительныеСвойства, Отказ);
	ПроведениеСерверУХ.ОбработкаПроведения_ЗаписьИКонтроль(ЭтотОбъект, Отказ);	
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)


	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);	
	
	Если ДанныеЗаполнения = Неопределено Тогда 
		
		ИнициализироватьДокумент();
		
	ИначеЕсли ТипДанныхЗаполнения = Тип("Структура") Тогда 
		
		Если ДанныеЗаполнения.Свойство("ИсточникЗаполнения") 
			И ДанныеЗаполнения.ИсточникЗаполнения = "ДанныеНСБУ" Тогда
			
			ЗаполнитьИзПодсистемыНСБУ(Неопределено);
			
		ИначеЕсли ДанныеЗаполнения.Свойство("АдресТаблицы") Тогда
			
			МСФОВызовСервераУХ.ЗаполнитьПоТаблицеЗагрузки(ЭтотОбъект, ДанныеЗаполнения);
			
		ИначеЕсли ДанныеЗаполнения.Свойство("ИсточникЗаполнения")
			И ДанныеЗаполнения.ИсточникЗаполнения = "ДокументыОснования" Тогда
			
			ЗаполнитьПоОснованиям(ДокументыОснования.ВыгрузитьКолонку("ДокументОснование"));
			
		Иначе
			
			ИнициализироватьДокумент();
			
		КонецЕсли;	
		
	ИначеЕсли ТипЗнч(Ссылка) = ТипДанныхЗаполнения Тогда
		
		ИнициализироватьДокумент(ДанныеЗаполнения);
		ЗаполнитьПоОснованиям(ДанныеЗаполнения, Истина);
		
	ИначеЕсли Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
		
		ЗаполнитьПоОснованиям(ДанныеЗаполнения);
		
	Иначе
		
		ИнициализироватьДокумент();
		
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	ПроведениеСерверУХ.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	ПроведениеСерверУХ.ОбработкаУдаленияПроведения(ЭтотОбъект, Отказ);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

Процедура ИнициализироватьДокумент(ДокументИсточник = Неопределено)
	
	Если ДокументИсточник <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДокументИсточник, "Организация,Сценарий,КлассВНА");
		ЭтотОбъект.РежимЗаполнения = Перечисления.РежимЗаполненияВидовУчета.МСФО;
	КонецЕсли;
	
	МСФОУХ.ОбработкаЗаполнения(ЭтотОбъект, ДокументИсточник, РежимЗаполнения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиЗаполнения

Процедура ЗаполнитьПоОснованиям(МассивДокументыОснования, ЗаполнитьДокумент = Ложь)
	
	Если ЭтотОбъект.РежимЗаполнения = ПредопределенноеЗначение("Перечисление.РежимЗаполненияВидовУчета.МСФО") Тогда
	
		Если ЗаполнитьДокумент Тогда
			МСФОВызовСервераУХ.ЗаполнитьКонтекстПоОснованиям(ЭтотОбъект, МассивДокументыОснования);
		КонецЕсли;
		
		МСФОВызовСервераУХ.ЗаполнитьДокументИзРежимаНСБУ(
								ЭтотОбъект, 
								МассивДокументыОснования, 
								"ВНА");
								
	Иначе
		ЗаполнитьИзПодсистемыНСБУ(МассивДокументыОснования);
	КонецЕсли;	
		
КонецПроцедуры

Процедура ЗаполнитьИзПодсистемыНСБУ(ДокументыОснованияИсточник = Неопределено)
	
	ПараметрыЗаполнения = МСФОКлиентСерверУХ.ПолучитьПараметрыЗаполнения(ЭтотОбъект, ДокументыОснованияИсточник);
	ИсточникЗаполнения = МСФОВНАУХ.ПолучитьДанные_ИзменениеПараметровВНА(ПараметрыЗаполнения);
	
	ЭтотОбъект.ВНА.Загрузить(ИсточникЗаполнения.ВНА);
	ЭтотОбъект.ДокументыОснования.Загрузить(ИсточникЗаполнения.ДокументыОснования);
		
КонецПроцедуры

#КонецОбласти

#КонецЕсли