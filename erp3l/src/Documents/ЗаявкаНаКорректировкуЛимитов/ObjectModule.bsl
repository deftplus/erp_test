#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Источник = ДанныеЗаполнения;
	ТипИсточника = ТипЗнч(Источник);
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("Ответственный", Пользователи.ТекущийПользователь());
	
	Если ТипИсточника = Тип("Структура") Тогда
		Если Источник.Свойство("ЛимитыПоБюджетам") Тогда
			Для Каждого Строка Из Источник.ЛимитыПоБюджетам Цикл
				ЗаполнитьЗначенияСвойств(ЛимитыПоБюджетам.Добавить(), Строка);
			КонецЦикла;
			
			ЗаполнитьПараметрыЛимитирования();
			
		КонецЕсли;
		
	ИначеЕсли ЭтотОбъект.Метаданные().Реквизиты.Основание.Тип.СодержитТип(ТипИсточника) Тогда
		//
		ЗаполнитьПоЗаявке(Источник, ТипИсточника, ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	//
	ПроведениеСерверОПК.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	Документы.ЗаявкаНаКорректировкуЛимитов.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	ПроведениеСерверОПК.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);	
	
	//
	КонтрольЛимитовУХ.ОтразитьЛимитыПоБюджетам(ДополнительныеСвойства, Движения, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПараметрыЛимитирования()
	
	ВидБюджетаПериодЛимитирования = ЛимитыПоБюджетам.Выгрузить(, "ВидБюджета, ПериодЛимитирования");
	ВидБюджетаПериодЛимитирования.Свернуть("ВидБюджета, ПериодЛимитирования", "");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ОбщегоНазначенияОПК.ЗагрузитьТаблицуВоВременнуюТаблицуЗапроса(Запрос, "ВТ_ВидБюджетаПериод", ВидБюджетаПериодЛимитирования);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВТ_ВидБюджетаПериод.ВидБюджета КАК ВидБюджета,
	|	ВТ_ВидБюджетаПериод.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ПараметрыЛимитирования.Период КАК Период,
	|	ПараметрыЛимитирования.ПериодичностьЛимитирования КАК ПериодичностьЛимитирования,
	|	ПараметрыЛимитирования.СпособОпределенияВалютыЛимитирования КАК СпособОпределенияВалютыЛимитирования,
	|	ПараметрыЛимитирования.ИспользоватьЛимитирование КАК ИспользоватьЛимитирование
	|ИЗ
	|	ВТ_ВидБюджетаПериод КАК ВТ_ВидБюджетаПериод
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыЛимитирования КАК ПараметрыЛимитирования
	|		ПО ВТ_ВидБюджетаПериод.ВидБюджета = ПараметрыЛимитирования.ВидБюджета
	|			И (НАЧАЛОПЕРИОДА(ВТ_ВидБюджетаПериод.ПериодЛимитирования.ДатаНачала, ГОД) = ПараметрыЛимитирования.Период)";
	
	ПараметрыЛимитирования = Запрос.Выполнить().Выгрузить();
	
	СтруктураПоиска = Новый Структура("ВидБюджета, ПериодЛимитирования");
	ЗаполняемыеРеквизиты = "СпособОпределенияВалютыЛимитирования, ИспользоватьЛимитирование";
	
	Для Каждого СтрокаПараметров Из ПараметрыЛимитирования Цикл
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаПараметров);
		Строки = ЛимитыПоБюджетам.НайтиСтроки(СтруктураПоиска);
		Для Каждого Строка Из Строки Цикл
			ЗаполнитьЗначенияСвойств(Строка, СтрокаПараметров, ЗаполняемыеРеквизиты);
		КонецЦикла;
		
	КонецЦикла;
	
	
КонецПроцедуры

Процедура ЗаполнитьПоЗаявке(Источник, ТипИсточника, ДанныеЗаполнения)
	
	ДанныеЗаполнения.Вставить("Основание", Источник);
	Документы.ЗаявкаНаКорректировкуЛимитов.ЗаполнитьТаблицыПоДокументуОснованию(ЭтотОбъект, Источник)
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли 

