#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПодготовкаПараметровПроведенияДокумента

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных мханизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт	
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда		
		
		ВстраиваниеУХФинансовыеИнструменты.ПодготовитьЗапросФинансовыеИнструменты(Документ, Запрос, ТекстыЗапроса, Регистры);
		
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт	
	ВстраиваниеУХФинансовыеИнструменты.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента, Истина, Ложь);
КонецПроцедуры

#Область ТекстЗапроса_втОперации

Функция ТекстЗапроса_втОперации(Запрос, ТекстыЗапроса) Экспорт

	МассивТекстовЗапроса = Новый Массив;
	НомераТаблиц = Новый Структура;
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса_ВерсииГрафиков(НомераТаблиц));
	МассивТекстовЗапроса.Добавить(ТекстЗапроса_втАктуальныеВерсииГрафиков(НомераТаблиц));
	МассивТекстовЗапроса.Добавить(ТекстЗапроса_втПартииЦенныхБумаг(НомераТаблиц));
	МассивТекстовЗапроса.Добавить(ТекстЗапроса_втПереоценкаПартий(НомераТаблиц));
	МассивТекстовЗапроса.Добавить(ТекстЗапроса_ТаблицаОперации(НомераТаблиц));	
	
	Если ИдентификацияПродуктаУХКлиентСервер.ЭтоЕХ() Тогда
		
		Если Запрос.МенеджерВременныхТаблиц = Неопределено Тогда
			Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		КонецЕсли;
		
		//для ключей аналитик требуется таблица операций в МВТ
		ЗапросОпераций = Новый Запрос(СтрСоединить(МассивТекстовЗапроса, ПолучениеДанныхУчетнойСистемыПереопределяемыйУХ.ТекстРазделителяЗапросовПакета()));
		ЗапросОпераций.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ЗапросОпераций.Параметры, Запрос.Параметры);
 		ЗапросОпераций.Выполнить();
		
		МассивТекстовЗапроса = Новый Массив;
		НомераТаблиц = Новый Структура;
		
	КонецЕсли;
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса_ПартииЦенныхБумаг(НомераТаблиц));
	ВстраиваниеУХ.ДополнитьТекстыЗапроса(ТекстыЗапроса, НомераТаблиц, МассивТекстовЗапроса);
	
КонецФункции

Функция ТекстЗапроса_ВерсииГрафиков(НомераТаблиц)
	
	НомераТаблиц.Вставить("втАктуальныеВерсии",	НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	ВерсииРасчетовСрезПоследних.Период КАК ДатаГрафика,
	|	НАЧАЛОПЕРИОДА(ВерсииРасчетовСрезПоследних.Период, ДЕНЬ) КАК НачалоДняВерсииГрафика,
	|	ВерсииРасчетовСрезПоследних.ПредметГрафика КАК ПредметГрафика,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ВерсииРасчетовСрезПоследних.ПредметГрафика) = ТИП(Справочник.ЦенныеБумаги)
	|			ТОГДА ВЫБОР
	|					КОГДА ТИПЗНАЧЕНИЯ(ВерсииРасчетовСрезПоследних.ВерсияГрафика) В (ТИП(Документ.ПриобретениеЦеннойБумаги), ТИП(Документ.ПродажаЦеннойБумаги))
	|						ТОГДА ИСТИНА
	|					КОГДА ТИПЗНАЧЕНИЯ(ВерсииРасчетовСрезПоследних.ВерсияГрафика) В (ТИП(Документ.ПоступлениеВекселя))
	|						ТОГДА ВерсииРасчетовСрезПоследних.ВерсияГрафика.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВекселя.ПриобретениеВекселей)
	|					КОГДА ТИПЗНАЧЕНИЯ(ВерсииРасчетовСрезПоследних.ВерсияГрафика) В (ТИП(Документ.ВыбытиеВекселей))
	|						ТОГДА ВерсииРасчетовСрезПоследних.ВерсияГрафика.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВыбытиеВекселя.ПредъявлениеВекселяКПогашению), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВыбытиеВекселя.ПродажаВекселя))
	|					КОГДА ТИПЗНАЧЕНИЯ(ВерсииРасчетовСрезПоследних.ВерсияГрафика) В (ТИП(Документ.АкцептПротестПереводногоВекселя))
	|						ТОГДА ВерсииРасчетовСрезПоследних.ВерсияГрафика.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийАкцептПротестПереводногоВекселя.ПредъявлениеВекселяКАкцепту))
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ
	|		ИНАЧЕ НЕ ВерсииРасчетовСрезПоследних.ПредметГрафика.ВидДоговораУХ В ИЕРАРХИИ (ЗНАЧЕНИЕ(Справочник.ВидыДоговоровКонтрагентовУХ.ПривлечениеСредств))
	|	КОНЕЦ КАК ЭтоАктив,
	|	ВерсииРасчетовСрезПоследних.ПредметГрафика ССЫЛКА Справочник.ЦенныеБумаги
	|		И ВерсииРасчетовСрезПоследних.ПредметГрафика.ВидФинансовогоИнструмента = ЗНАЧЕНИЕ(Перечисление.ВидыФинансовыхИнструментов.Облигация) КАК ЭтоОблигация,
	|	ВерсииРасчетовСрезПоследних.ПредметГрафика ССЫЛКА Справочник.ЦенныеБумаги
	|		И ВерсииРасчетовСрезПоследних.ПредметГрафика.ВидФинансовогоИнструмента = ЗНАЧЕНИЕ(Перечисление.ВидыФинансовыхИнструментов.Вексель) КАК ЭтоВексель,
	|	ВерсииРасчетовСрезПоследних.ВерсияГрафика КАК Регистратор,
	|	ВерсииРасчетовСрезПоследних.ПредметГрафика.ВалютаВзаиморасчетов КАК Валюта
	|ПОМЕСТИТЬ втАктуальныеВерсии
	|ИЗ
	|	РегистрСведений.ВерсииРасчетов.СрезПоследних(
	|			&ДатаОкончания,
	|			Организация = &Организация
	|				И План = ЛОЖЬ) КАК ВерсииРасчетовСрезПоследних
	|{ГДЕ
	|	ВерсииРасчетовСрезПоследних.ПредметГрафика.Контрагент.* КАК Контрагент,
	|	ВерсииРасчетовСрезПоследних.ПредметГрафика.* КАК Договор,
	|	ВерсииРасчетовСрезПоследних.ПредметГрафика.Ответственный.* КАК Ответственный}
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПредметГрафика";

КонецФункции

Функция ТекстЗапроса_втАктуальныеВерсииГрафиков(НомераТаблиц)
	
	НомераТаблиц.Вставить("втАктуальныеВерсииГрафиков",	НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	втАктуальныеВерсии.ДатаГрафика КАК ДатаГрафика,
	|	втАктуальныеВерсии.НачалоДняВерсииГрафика КАК НачалоДняВерсииГрафика,
	|	втАктуальныеВерсии.ПредметГрафика КАК ПредметГрафика,
	|	втАктуальныеВерсии.ЭтоАктив КАК ЭтоАктив,
	|	втАктуальныеВерсии.ЭтоОблигация КАК ЭтоОблигация,
	|	втАктуальныеВерсии.ЭтоВексель КАК ЭтоВексель,
	|	втАктуальныеВерсии.Регистратор КАК Регистратор,
	|	втАктуальныеВерсии.Валюта КАК Валюта,
	|	ЕСТЬNULL(ВерсииРасчетовСрезПоследних.ВерсияГрафика, втАктуальныеВерсии.Регистратор) КАК ВерсияСчетаУчета
	|ПОМЕСТИТЬ втАктуальныеВерсииГрафиков
	|ИЗ
	|	втАктуальныеВерсии КАК втАктуальныеВерсии
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВерсииРасчетов.СрезПоследних(
	|				&ТекущаяДата,
	|				Организация = &Организация
	|					И План = ЛОЖЬ
	|					И ТИПЗНАЧЕНИЯ(ВерсияГрафика) В (ТИП(Документ.ВерсияСоглашенияДепозит), ТИП(Документ.ВерсияСоглашенияКредит), ТИП(Документ.ПриобретениеЦеннойБумаги), ТИП(Документ.ВыпускЦеннойБумаги), ТИП(Документ.ПоступлениеВекселя))) КАК ВерсииРасчетовСрезПоследних
	|		ПО втАктуальныеВерсии.ПредметГрафика = ВерсииРасчетовСрезПоследних.ПредметГрафика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПредметГрафика"

КонецФункции

Функция ТекстЗапроса_втПартииЦенныхБумаг(НомераТаблиц)

	НомераТаблиц.Вставить("втПартииЦенныхБумаг", НомераТаблиц.Количество());	
		
	Возврат
	"ВЫБРАТЬ
	|	ПартииЦенныхБумагОстатки.ФинансовыйИнструмент КАК ФинансовыйИнструмент,
	|	ПартииЦенныхБумагОстатки.ФинансовыйИнструмент.Котировка КАК КотировкаФИ,
	|	ПартииЦенныхБумагОстатки.Партия КАК Партия,
	|	ПартииЦенныхБумагОстатки.Договор КАК Договор,
	|	ПартииЦенныхБумагОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|	ПартииЦенныхБумагОстатки.СтоимостьОстаток КАК СтоимостьОстаток,
	|	ПартииЦенныхБумагОстатки.РыночнаяСтоимостьОстаток КАК РыночнаяСтоимостьОстаток
	|ПОМЕСТИТЬ втПартииЦенныхБумаг
	|ИЗ
	|	РегистрНакопления.ПартииЦенныхБумаг.Остатки(
	|			&ПериодДоДокумента,
	|			ФинансовыйИнструмент В
	|					(ВЫБРАТЬ
	|						т.ПредметГрафика
	|					ИЗ
	|						втАктуальныеВерсииГрафиков КАК т)
	|				И Партия.Организация = &Организация) КАК ПартииЦенныхБумагОстатки
	|ГДЕ
	|	ПартииЦенныхБумагОстатки.КоличествоОстаток <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КотировкаФИ,
	|	ФинансовыйИнструмент";

КонецФункции

Функция ТекстЗапроса_втВерсииДоДокумента(НомераТаблиц)

	НомераТаблиц.Вставить("втВерсииДоДокумента", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	Версии.ПредметГрафика КАК ФинансовыйИнструмент,
	|	Версии.ВерсияГрафика КАК Регистратор
	|ПОМЕСТИТЬ втВерсииДоДокумента
	|ИЗ
	|	РегистрСведений.ВерсииРасчетов.СрезПоследних(
	|			&ПериодДоДокумента,
	|			Организация = &Организация
	|				И ПредметГрафика В
	|					(ВЫБРАТЬ
	|						т.ФинансовыйИнструмент
	|					ИЗ
	|						втСобытия КАК т)) КАК Версии";

КонецФункции

Функция ТекстЗапроса_втПереоценкаПартий(НомераТаблиц)
	
	НомераТаблиц.Вставить("втПереоценкаПартий", НомераТаблиц.Количество());	
		
	Возврат
	"ВЫБРАТЬ
	|	втПартииЦенныхБумаг.ФинансовыйИнструмент КАК ФинансовыйИнструмент,
	|	втПартииЦенныхБумаг.ФинансовыйИнструмент.ВалютаНоминала КАК ВалютаНоминала,
	|	втПартииЦенныхБумаг.Партия КАК Партия,
	|	втПартииЦенныхБумаг.Договор КАК Договор,
	|	втПартииЦенныхБумаг.КоличествоОстаток КАК КоличествоОстаток,
	|	втПартииЦенныхБумаг.РыночнаяСтоимостьОстаток КАК СтоимостьОстаток,
	|	ЗначенияКотировокФИ.Период КАК Период,
	|	ЗначенияКотировокФИ.ВидКотировки КАК ВидКотировки,
	|	ЗначенияКотировокФИ.Значение КАК Значение,
	|	ВЫРАЗИТЬ(втПартииЦенныхБумаг.КоличествоОстаток * ЗначенияКотировокФИ.Значение - втПартииЦенныхБумаг.РыночнаяСтоимостьОстаток КАК ЧИСЛО(18, 2)) КАК Дооценка
	|ПОМЕСТИТЬ втПереоценкаПартий
	|ИЗ
	|	РегистрСведений.ЗначенияКотировокФИ.СрезПоследних(
	|			&ПериодДоДокумента,
	|			ВидКотировки В
	|				(ВЫБРАТЬ
	|					т.КотировкаФИ
	|				ИЗ
	|					втПартииЦенныхБумаг КАК т)) КАК ЗначенияКотировокФИ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПартииЦенныхБумаг КАК втПартииЦенныхБумаг
	|		ПО ЗначенияКотировокФИ.ВидКотировки = втПартииЦенныхБумаг.КотировкаФИ
	|ГДЕ
	|	(ВЫРАЗИТЬ(втПартииЦенныхБумаг.КоличествоОстаток * ЗначенияКотировокФИ.Значение - втПартииЦенныхБумаг.СтоимостьОстаток КАК ЧИСЛО(18, 2))) <> 0";

КонецФункции

Функция ТекстЗапроса_ПартииЦенныхБумаг(НомераТаблиц)
	
	НомераТаблиц.Вставить("ПартииЦенныхБумаг", НомераТаблиц.Количество());	
		
	Возврат
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	втПереоценкаПартий.ФинансовыйИнструмент КАК ФинансовыйИнструмент,
	|	втПереоценкаПартий.Партия КАК Партия,
	|	втПереоценкаПартий.Договор КАК Договор,
	|	0 КАК Количество,
	|	0 КАК Стоимость,
	|	0 КАК Номинал,
	|	втПереоценкаПартий.Дооценка КАК РыночнаяСтоимость
	|ИЗ
	|	втПереоценкаПартий КАК втПереоценкаПартий";

КонецФункции

Функция ТекстЗапроса_ТаблицаОперации(НомераТаблиц)

	НомераТаблиц.Вставить("втТаблицаПроводок", НомераТаблиц.Количество());
	
	//|ГДЕ
	//|	ГрафикиРасчетов.ЭлементСтруктурыЗадолженности В (ЗНАЧЕНИЕ(Перечисление.ЭлементыСтруктурыЗадолженности.Проценты), ЗНАЧЕНИЕ(Перечисление.ЭлементыСтруктурыЗадолженности.Комиссии), ЗНАЧЕНИЕ(Перечисление.ЭлементыСтруктурыЗадолженности.Штрафы), ЗНАЧЕНИЕ(Перечисление.ЭлементыСтруктурыЗадолженности.ПрочиеКомиссии))
		
	Возврат
	"ВЫБРАТЬ
	|	&Ссылка КАК Ссылка,
	|	Проводки.Регистратор КАК Регистратор,
	|	Проводки.Период КАК Дата,
	|	Проводки.ФинансовыйИнструмент КАК ДоговорКонтрагента,
	|	Проводки.ФинансовыйИнструмент КАК ФинансовыйИнструмент,
	|	ВЫРАЗИТЬ(Проводки.ФинансовыйИнструмент КАК Справочник.ЦенныеБумаги).Эмитент КАК Контрагент,
	|	Проводки.Валюта КАК Валюта,
	|	Проводки.Количество КАК Количество,
	|	Проводки.Сумма КАК Сумма,
	|	Проводки.Регистратор.Организация КАК Организация,
	|	Проводки.Регистратор.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Проводки.Регистратор.Подразделение КАК Подразделение,
	|	Проводки.Регистратор.НаправлениеДеятельности КАК НаправлениеДеятельности,	
	|	Проводки.СчетДт КАК СчетДт,
	|	Проводки.СчетКт КАК СчетКт,
	|	Проводки.Комментарий КАК Комментарий
	|ПОМЕСТИТЬ втОперации
	|ИЗ
	|	(ВЫБРАТЬ
	|		втАктуальныеВерсииГрафиков.ПредметГрафика КАК ФинансовыйИнструмент,
	|		втАктуальныеВерсииГрафиков.Регистратор КАК Регистратор,
	|		ГрафикиРасчетов.Период КАК Период,
	|		ГрафикиРасчетов.Валюта КАК Валюта,
	|		0 КАК Количество,
	|		ГрафикиРасчетов.Сумма КАК Сумма,
	|		ВЫБОР
	|			КОГДА втАктуальныеВерсииГрафиков.ЭтоАктив
	|				ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетУчетаФИ)
	|			ИНАЧЕ ВЫБОР ГрафикиРасчетов.ЭлементСтруктурыЗадолженности
	|					КОГДА ЗНАЧЕНИЕ(Перечисление.ЭлементыСтруктурыЗадолженности.Проценты)
	|						ТОГДА ВЫБОР
	|								КОГДА втАктуальныеВерсииГрафиков.ЭтоОблигация
	|									ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетНачисленияКупонов)
	|								ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетНачисленияПроцентов)
	|							КОНЕЦ
	|					КОГДА ЗНАЧЕНИЕ(Перечисление.ЭлементыСтруктурыЗадолженности.Комиссии)
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетНачисленияКомиссий)
	|					КОГДА ЗНАЧЕНИЕ(Перечисление.ЭлементыСтруктурыЗадолженности.Штрафы)
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетНачисленияКомиссий)
	|					КОГДА ЗНАЧЕНИЕ(Перечисление.ЭлементыСтруктурыЗадолженности.ПрочиеКомиссии)
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетНачисленияКомиссий)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетНачисленияПроцентов)
	|				КОНЕЦ
	|		КОНЕЦ КАК СчетДт,
	|		ВЫБОР
	|			КОГДА НЕ втАктуальныеВерсииГрафиков.ЭтоАктив
	|				ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетУчетаФИ)
	|			ИНАЧЕ ВЫБОР ГрафикиРасчетов.ЭлементСтруктурыЗадолженности
	|					КОГДА ЗНАЧЕНИЕ(Перечисление.ЭлементыСтруктурыЗадолженности.Проценты)
	|						ТОГДА ВЫБОР
	|								КОГДА втАктуальныеВерсииГрафиков.ЭтоОблигация
	|									ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетНачисленияКупонов)
	|								ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетНачисленияПроцентов)
	|							КОНЕЦ
	|					КОГДА ЗНАЧЕНИЕ(Перечисление.ЭлементыСтруктурыЗадолженности.Комиссии)
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетНачисленияКомиссий)
	|					КОГДА ЗНАЧЕНИЕ(Перечисление.ЭлементыСтруктурыЗадолженности.Штрафы)
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетНачисленияКомиссий)
	|					КОГДА ЗНАЧЕНИЕ(Перечисление.ЭлементыСтруктурыЗадолженности.ПрочиеКомиссии)
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетНачисленияКомиссий)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетНачисленияПроцентов)
	|				КОНЕЦ
	|		КОНЕЦ КАК СчетКт,
	|		ВЫБОР
	|			КОГДА втАктуальныеВерсииГрафиков.ЭтоОблигация
	|				ТОГДА ""Начисление купонов по облигации""
	|			ИНАЧЕ ""Начисление процентов по финансовому инструменту""
	|		КОНЕЦ КАК Комментарий
	|	ИЗ
	|		РегистрНакопления.РасчетыСКонтрагентамиГрафики КАК ГрафикиРасчетов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАктуальныеВерсииГрафиков КАК втАктуальныеВерсииГрафиков
	|			ПО ГрафикиРасчетов.Регистратор = втАктуальныеВерсииГрафиков.Регистратор
	|				И ГрафикиРасчетов.ПредметГрафика = втАктуальныеВерсииГрафиков.ПредметГрафика
	|				И (ГрафикиРасчетов.ВидБюджета = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыБюджетов.БюджетДоходовИРасходов))
	|				И (ГрафикиРасчетов.Период МЕЖДУ &ДатаНачала И &ДатаОкончания)
	|				И (ВЫБОР
	|					КОГДА НЕ втАктуальныеВерсииГрафиков.ЭтоВексель
	|						ТОГДА ИСТИНА
	|					КОГДА втАктуальныеВерсииГрафиков.ЭтоАктив
	|						ТОГДА ГрафикиРасчетов.ПриходРасход = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Приход)
	|								И ГрафикиРасчетов.Период <> втАктуальныеВерсииГрафиков.НачалоДняВерсииГрафика
	|					ИНАЧЕ ГрафикиРасчетов.ПриходРасход = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Расход)
	|							И ГрафикиРасчетов.Период <> втАктуальныеВерсииГрафиков.НачалоДняВерсииГрафика
	|				КОНЕЦ)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗначенияКотировок.ФинансовыйИнструмент,
	|		ЗначенияКотировок.Партия,
	|		&ДатаОкончания,
	|		ЗначенияКотировок.ВалютаНоминала,
	|		0,
	|		ВЫБОР
	|			КОГДА ЗначенияКотировок.Дооценка > 0
	|				ТОГДА ЗначенияКотировок.Дооценка
	|			ИНАЧЕ -ЗначенияКотировок.Дооценка
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ЗначенияКотировок.Дооценка > 0
	|				ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетУчетаФИ)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетРасходовОтПереоценки)
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ЗначенияКотировок.Дооценка > 0
	|				ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетДоходовОтПереоценки)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ВидыСчетовФИ.СчетУчетаФИ)
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ЗначенияКотировок.Дооценка > 0
	|				ТОГДА ""Дооценка ценной бумаги до рыночной стоимости по котировке""
	|			ИНАЧЕ ""Уценка ценной бумаги до рыночной стоимости по котировке""
	|		КОНЕЦ
	|	ИЗ
	|		втПереоценкаПартий КАК ЗначенияКотировок) КАК Проводки";

КонецФункции

#КонецОбласти

Функция ТекстЗапроса_СчетаДокумента(НомераТаблиц) Экспорт

	НомераТаблиц.Вставить("втСчетаДокумента", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	СчетаУчета.Ссылка.ДоговорКонтрагента КАК ФинансовыйИнструмент,
	|	СчетаУчета.Ссылка КАК Ссылка,
	|	СчетаУчета.ВидСчетаФИ КАК ВидСчетаФИ,
	|	СчетаУчета.Счет КАК Счет,
	|	СчетаУчета.Субконто1 КАК Субконто1,
	|	СчетаУчета.Субконто2 КАК Субконто2,
	|	СчетаУчета.Субконто3 КАК Субконто3
	|ПОМЕСТИТЬ втСчетаДокумента
	|ИЗ
	|	Документ.ВерсияСоглашенияДепозит.СчетаУчета КАК СчетаУчета
	|ГДЕ
	|	СчетаУчета.Ссылка В
	|			(ВЫБРАТЬ
	|				т.ВерсияСчетаУчета
	|			ИЗ
	|				втАктуальныеВерсииГрафиков КАК т)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетаУчета.Ссылка.ДоговорКонтрагента,
	|	СчетаУчета.Ссылка,
	|	СчетаУчета.ВидСчетаФИ,
	|	СчетаУчета.Счет,
	|	СчетаУчета.Субконто1,
	|	СчетаУчета.Субконто2,
	|	СчетаУчета.Субконто3
	|ИЗ
	|	Документ.ВерсияСоглашенияКредит.СчетаУчета КАК СчетаУчета
	|ГДЕ
	|	СчетаУчета.Ссылка В
	|			(ВЫБРАТЬ
	|				т.ВерсияСчетаУчета
	|			ИЗ
	|				втАктуальныеВерсииГрафиков КАК т)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетаУчета.Ссылка.ФинансовыйИнструмент,
	|	СчетаУчета.Ссылка,
	|	СчетаУчета.ВидСчетаФИ,
	|	СчетаУчета.Счет,
	|	СчетаУчета.Субконто1,
	|	СчетаУчета.Субконто2,
	|	СчетаУчета.Субконто3
	|ИЗ
	|	Документ.ПриобретениеЦеннойБумаги.СчетаУчета КАК СчетаУчета
	|ГДЕ
	|	СчетаУчета.Ссылка В
	|			(ВЫБРАТЬ
	|				т.ВерсияСчетаУчета
	|			ИЗ
	|				втАктуальныеВерсииГрафиков КАК т)
	|	И СчетаУчета.Ссылка.ФинансовыйИнструмент В
	|			(ВЫБРАТЬ
	|				т.ФинансовыйИнструмент
	|			ИЗ
	|				втПартииЦенныхБумаг КАК т)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетаУчета.Ссылка.ФинансовыйИнструмент,
	|	СчетаУчета.Ссылка,
	|	СчетаУчета.ВидСчетаФИ,
	|	СчетаУчета.Счет,
	|	СчетаУчета.Субконто1,
	|	СчетаУчета.Субконто2,
	|	СчетаУчета.Субконто3
	|ИЗ
	|	Документ.ВыпускЦеннойБумаги.СчетаУчета КАК СчетаУчета
	|ГДЕ
	|	СчетаУчета.Ссылка В
	|			(ВЫБРАТЬ
	|				т.ВерсияСчетаУчета
	|			ИЗ
	|				втАктуальныеВерсииГрафиков КАК т)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетаУчета.Ссылка.ФинансовыйИнструмент,
	|	СчетаУчета.Ссылка,
	|	СчетаУчета.ВидСчетаФИ,
	|	СчетаУчета.Счет,
	|	СчетаУчета.Субконто1,
	|	СчетаУчета.Субконто2,
	|	СчетаУчета.Субконто3
	|ИЗ
	|	Документ.ПоступлениеВекселя.СчетаУчета КАК СчетаУчета
	|ГДЕ
	|	СчетаУчета.Ссылка В
	|			(ВЫБРАТЬ
	|				т.ВерсияСчетаУчета
	|			ИЗ
	|				втАктуальныеВерсииГрафиков КАК т)";
	
КонецФункции

Функция ТекстЗапроса_Проводки(НомераТаблиц) Экспорт
	
	НомераТаблиц.Вставить("втТаблицаПроводок", НомераТаблиц.Количество());

	Возврат
	"ВЫБРАТЬ
	|	втСобытия.ФинансовыйИнструмент КАК ФинансовыйИнструмент,
	|	втСобытия.Дата КАК Период,
	|	втСобытия.Валюта КАК Валюта,
	|	втСобытия.Количество КАК Количество,
	|	втСобытия.Сумма КАК Сумма,
	|	втСобытия.СчетДт КАК СчетДт,
	|	втСобытия.СчетКт КАК СчетКт,
	|	втСобытия.Комментарий КАК Комментарий
	|ПОМЕСТИТЬ втТаблицаПроводок
	|ИЗ
	|	втОперации КАК втСобытия";

КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

#Область Отчеты

// Заполняет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ФинансовыеИнструментыУХ.ДобавитьКомандуОтчет(КомандыОтчетов, "Отчет.ГрафикиЦенныхБумаг", НСтр("ru = 'График ценной бумаги'"));
	ФинансовыеИнструментыУХ.ДобавитьКомандуОтчет(КомандыОтчетов, "Отчет.ВедомостьПоПартиямЦенныхБумаг", НСтр("ru = 'Ведомость по партиям ценной бумаги'"));
	
КонецПроцедуры

#КонецОбласти

#Область СозданиеНаОсновании

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
КонецПроцедуры

// Добавляет команду создания документа "Реализация услуг и прочих активов".
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	
	
КонецФункции

#КонецОбласти

#Область УправлениеДоступом

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти
	
#КонецЕсли
