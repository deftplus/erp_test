
&НаСервере
Процедура ДобавитьФормируемыеДокументы(ЭтапПроцесса)
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЭтапыПроцессовФормируемыеДокументы.ШаблонДокумента,
	|	ЭтапыПроцессовФормируемыеДокументы.ВидДокумента
	|ИЗ
	|	Справочник.УдалитьЭтапыПроцессов.ФормируемыеДокументы КАК ЭтапыПроцессовФормируемыеДокументы
	|ГДЕ
	|	ЭтапыПроцессовФормируемыеДокументы.Ссылка.ТипОрганизацийЭтапа = ЗНАЧЕНИЕ(Перечисление.ТипыОрганизационныхЕдиниц.Обычная)
	|	И ЭтапыПроцессовФормируемыеДокументы.Ссылка = &ЭтапПроцесса";
		
	Запрос.УстановитьПараметр("ЭтапПроцесса",ЭтапПроцесса);
	
	Результат=Запрос.Выполнить().Выбрать();
	
	Пока Результат.Следующий() Цикл
		
		СтруктураПоиска=Новый Структура("ШаблонДокумента,ВидДокумента",Результат.ШаблонДокумента,Результат.ВидДокумента);
		
		Если Объект.ФормируемыеДокументы.НайтиСтроки(СтруктураПоиска).Количество()=0 Тогда
			
			НоваяСтрока=Объект.ФормируемыеДокументы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,Результат);
			
		КонецЕсли;
		
	КонецЦикла;
				
КонецПроцедуры // ДобавитьВидыОтчетов()

&НаСервере
Процедура ОпределитьШаблонПроцесса()
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	УправлениеПериодомСценария.ВерсияОрганизационнойСтруктуры.ШаблонПроцесса КАК ШаблонПроцесса
	|ИЗ
	|	Документ.УправлениеПериодомСценария КАК УправлениеПериодомСценария
	|ГДЕ
	|	УправлениеПериодомСценария.Сценарий = &Сценарий
	|	И УправлениеПериодомСценария.ПериодСценария = &ПериодСценария";
	
	Запрос.УстановитьПараметр("Сценарий",		Объект.Сценарий);
	Запрос.УстановитьПараметр("ПериодСценария",	Объект.ПериодСценария);
	
	Результат=Запрос.Выполнить().Выбрать();
	
	Если Результат.Следующий() Тогда
		
		ШаблонПроцесса=Результат.ШаблонПроцесса;
		
	Иначе
		
		ШаблонПроцесса=Справочники.ШаблоныПроцессов.ПустаяСсылка();
		
	КонецЕсли;	
	
КонецПроцедуры // ОпределитьШаблонПроцесса() 

&НаСервере
Процедура ПриИзмененииВидаДокумента(ИдентификаторСтроки)
	
	ДанныеСтроки=Объект.ФормируемыеДокументы.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	Если ДанныеСтроки.ВидДокумента=Перечисления.ВидыДокументовЭтапа.ЭкземплярыОтчетов Тогда
		
		ДанныеСтроки.ШаблонДокумента=Справочники.ВидыОтчетов.ПустаяСсылка();
		
	ИначеЕсли ДанныеСтроки.ВидДокумента=Перечисления.ВидыДокументовЭтапа.ДокументыПараллельногоУчета Тогда
		
		ДанныеСтроки.ШаблонДокумента=Справочники.ПакетыПравилИмпортаТаблиц.ПустаяСсылка();
		
	ИначеЕсли ДанныеСтроки.ВидДокумента=Перечисления.ВидыДокументовЭтапа.ДокументыТрансляции Тогда
		
		ДанныеСтроки.ШаблонДокумента=Справочники.ШаблоныТрансляций.ПустаяСсылка();
				
	ИначеЕсли ДанныеСтроки.ВидДокумента=Перечисления.ВидыДокументовЭтапа.ТрансформационныеКорректировки Тогда
		
		ДанныеСтроки.ШаблонДокумента=Справочники.ШаблоныТрансформационныхКорректировок.ПустаяСсылка();
		
	КонецЕсли;
			
КонецПроцедуры // ПриИзмененииВидаДокумента()

&НаКлиенте
Процедура ЗаполнитьПоЭтапуПроцесса(Команда)
	
	Объект.ФормируемыеДокументы.Очистить();
	ДобавитьФормируемыеДокументы(Объект.ЭтапПроцесса);
		
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОпределитьШаблонПроцесса();
	
КонецПроцедуры

&НаКлиенте
Процедура СценарийПриИзменении(Элемент)
	
	ОпределитьШаблонПроцесса();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодОтчетаПриИзменении(Элемент)
	
	ОпределитьШаблонПроцесса();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыОтчетовВидДокументаПриИзменении(Элемент)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ФормируемыеДокументыВидДокументаПриИзменении(Элемент)
	
	ПриИзмененииВидаДокумента(Элементы.ФормируемыеДокументы.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ФормируемыеДокументыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		
		Элементы.ФормируемыеДокументы.ТекущиеДанные.ВидДокумента	= ВидДокументаЭкземпляр;
		Элементы.ФормируемыеДокументы.ТекущиеДанные.ШаблонДокумента	= ВидОтчетаСсылка;
		
	КонецЕсли;
	
КонецПроцедуры
