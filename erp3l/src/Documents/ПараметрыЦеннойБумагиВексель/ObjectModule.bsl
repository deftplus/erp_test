#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Дата = ТекущаяДатаСеанса();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ЦенныеБумаги")  Тогда
		ФинансовыйИнструмент = ДанныеЗаполнения;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДанныеФормыСтруктура") ИЛИ ТипЗнч(ДанныеЗаполнения) = Тип("СправочникОбъект.ЦенныеБумаги") Тогда
		ФинансовыйИнструмент = ДанныеЗаполнения.Ссылка;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Дата = ТекущаяДатаСеанса();
	ФинансовыйИнструмент = Неопределено;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ФормаВекселя = Перечисления.ФормаВекселя.Простой Тогда
		Должник = Векселедатель;
	КонецЕсли;
	
	Если ВариантДатыНачалаНачисленияПроцентов = Перечисления.ВариантыДатыНачалаНачисленияПроцентовПоВекселю.СДатыВозможногоПредъявления Тогда
	
		ДатаПлатежа = ДатаПредъявленияНеРанее + 86400;
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ФормаВекселя = Перечисления.ФормаВекселя.Простой Тогда 
		
		МассивНепроверяемыхРеквизитов.Добавить("Должник");
		
	КонецЕсли;
	
	Если СрокПлатежаПоВекселю <> Перечисления.СрокиПлатежаПоВекселю.НаОпределенныйДень Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДатаПлатежа");
	КонецЕсли;
	
	Если СпособУказанияДатыПредъявления <> Перечисления.СпособыУказанияДатыПредъявленияВекселя.НеРанееУказаннойДаты Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДатаПредъявленияНеРанее");
	КонецЕсли;
	
	Если Не ПредусмотренПроцентныйДоход Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ПроцентнаяСтавка");
	КонецЕсли;
	
	Если Не ПредусмотренПроцентныйДоход Или ВариантДатыНачалаНачисленияПроцентов <> Перечисления.ВариантыДатыНачалаНачисленияПроцентовПоВекселю.СУказаннойДаты Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДатаНачалаНачисленияПроцентов");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
