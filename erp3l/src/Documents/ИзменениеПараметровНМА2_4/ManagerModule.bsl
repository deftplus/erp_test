#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("НематериальныеАктивы");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	
	//++ НЕ УТ
	МеханизмыДокумента.Добавить("ОбесценениеВНА");
	//-- НЕ УТ
	
	ИзменениеПараметровНМАЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура:
//     * Ключ - Строка - Имя таблицы.
//     * Значение - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаПорядокУчетаНМА(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаНМАУУ(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПараметрыАмортизацииНМАУУ(ТекстыЗапроса, Регистры);
		
		//++ НЕ УТКА
		ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Регистры);
		//-- НЕ УТКА
		
		//++ НЕ УТ
		ТекстЗапросаТаблицаСоставЕГДС(Запрос, ТекстыЗапроса, Регистры);
		//-- НЕ УТ
		
		ИзменениеПараметровНМАЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	
	КонецЕсли;
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.КорректировкаСтоимостиИАмортизацииНМА.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	ИзменениеПараметровНМАЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

// Добавляет команду создания документа "Изменение параметров НМА".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
//	СтрокаТаблицыЗначений - добавленная команда
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт

	Если ПравоДоступа("Добавление", Метаданные.Документы.ИзменениеПараметровНМА2_4) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ИзменениеПараметровНМА2_4.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ИзменениеПараметровНМА2_4);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьВнеоборотныеАктивы2_4";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ИзменениеПараметровНМАЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	ИзменениеПараметровНМАЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Формирует таблицы движений при отложенном проведении.
//
// Параметры:
//  ДокументСсылка			 - ДокументСсылка.ИзменениеПараметровНМА2_4 - Документ, для которого формируются движения
//  МенеджерВременныхТаблиц	 - МенеджерВременныхТаблиц - Содержит вспомогательные временные таблицы, которые могут использоваться для формирования движений.
//  
// Возвращаемое значение:
//  см. ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения
//
Функция ТаблицыОтложенногоФормированияДвижений(ДокументСсылка, МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаСтоимостьНМА(ТекстыЗапроса);
	ТекстЗапросаТаблицаАмортизацияНМА(ТекстыЗапроса);
	ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса);
	
	ТаблицыДляДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос,
																							ТекстыЗапроса,
																							Неопределено);
	
	Возврат ТаблицыДляДвижений;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.ИзменениеПараметровНМА2_4";
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "";
		ВЗапросеЕстьИсточник = Ложь;
		
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровНМА) КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ОтражатьВУпрУчете КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.ОтражатьВРеглУчете КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.ПорядокУчета КАК ПорядокУчета,
	|	ДанныеДокумента.ПорядокУчетаФлаг КАК ПорядокУчетаФлаг,
	|	ДанныеДокумента.МетодНачисленияАмортизацииУУ КАК МетодНачисленияАмортизацииУУ,
	|	ДанныеДокумента.МетодНачисленияАмортизацииУУФлаг КАК МетодНачисленияАмортизацииУУФлаг,
	|	ДанныеДокумента.СрокИспользованияУУ КАК СрокИспользованияУУ,
	|	ДанныеДокумента.СрокИспользованияУУФлаг КАК СрокИспользованияУУФлаг,
	|	ДанныеДокумента.ОбъемНаработки КАК ОбъемНаработки,
	|	ДанныеДокумента.ОбъемНаработкиФлаг КАК ОбъемНаработкиФлаг,
	|	ДанныеДокумента.КоэффициентУскоренияУУ КАК КоэффициентУскоренияУУ,
	|	ДанныеДокумента.КоэффициентУскоренияУУФлаг КАК КоэффициентУскоренияУУФлаг,
	|	ДанныеДокумента.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	ДанныеДокумента.ЛиквидационнаяСтоимостьФлаг КАК ЛиквидационнаяСтоимостьФлаг,
	|	ДанныеДокумента.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ДанныеДокумента.ГруппаФинансовогоУчетаФлаг КАК ГруппаФинансовогоУчетаФлаг,
	|	ДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеДокумента.НаправлениеДеятельностиФлаг КАК НаправлениеДеятельностиФлаг,
	|	ДанныеДокумента.АмортизационныеРасходыФлаг КАК АмортизационныеРасходыФлаг,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	&ИзменениеПараметровНМА_РеквизитыДокумента,
	|	ДанныеДокумента.ЕГДСФлаг КАК ЕГДСФлаг,
	|	ДанныеДокумента.ЕГДС КАК ЕГДС
	|ИЗ
	|	Документ.ИзменениеПараметровНМА2_4 КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ИзменениеПараметровНМАЛокализация.ДобавитьВТекстЗапросаРеквизитыДокумента(Запрос.Текст, "ДанныеДокумента");
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)
	
	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ИзменениеПараметровНМА2_4"));
	ЗначенияПараметровПроведения.Вставить("СтатьяАП_НМА", ПланыВидовХарактеристик.СтатьиАктивовПассивов.НематериальныеАктивы);
	ЗначенияПараметровПроведения.Вставить("НазваниеДокумента", НСтр("ru = 'Изменение параметров НМА';
																	|en = 'Adjust intangible asset financial details'"));
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ИзменениеПараметровНМА);
	ЗначенияПараметровПроведения.Вставить("ДатаНачалаУчета", ВнеоборотныеАктивыЛокализация.ДатаНачалаУчетаВнеоборотныхАктивов2_4());
	ЗначенияПараметровПроведения.Вставить("ВедетсяРегламентированныйУчетВНА", ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА());
	
	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли;
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоНМА";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаНМА.НомерСтроки-1, 0) КАК НомерЗаписи,
	|	&Ссылка                          КАК Ссылка,
	|	ТаблицаНМА.НематериальныйАктив   КАК НематериальныйАктив,
	|	&ХозяйственнаяОперация           КАК ХозяйственнаяОперация,
	|	&Организация                     КАК Организация,
	|	НЕОПРЕДЕЛЕНО                     КАК Подразделение,
	|	&Период                          КАК Дата,
	|	&ИдентификаторМетаданных         КАК ТипСсылки,
	|	&Проведен                        КАК Проведен,
	|	&ОтражатьВУпрУчете               КАК ОтражатьВУпрУчете,
	|	&ОтражатьВРеглУчете              КАК ОтражатьВРеглУчете
	|ИЗ
	|	Документ.ИзменениеПараметровНМА2_4 КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровНМА2_4.НМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Ссылка                        КАК Ссылка,
	|	&Период                        КАК ДатаДокументаИБ,
	|	&Номер                         КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных       КАК ТипСсылки,
	|	&Организация                   КАК Организация,
	|	&ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                   КАК НаправлениеДеятельности,
	|	&Ответственный                 КАК Ответственный,
	|	&Комментарий                   КАК Комментарий,
	|	&Проведен                      КАК Проведен,
	|	&ПометкаУдаления               КАК ПометкаУдаления,
	|	ЛОЖЬ                           КАК ДополнительнаяЗапись,
	|	&Период                        КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                 КАК НомерПервичногоДокумента,
	|	ЛОЖЬ                           КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО                   КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО                   КАК ИсправляемыйДокумент,
	|	&Период                        КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО                   КАК Приоритет
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаПорядокУчетаНМА(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПорядокУчетаНМА";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаНМА(ТекстыЗапроса, "Документ.ИзменениеПараметровНМА2_4");
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	&Организация КАК Организация,
	|	
	|	ВЫБОР
	|		КОГДА &ГруппаФинансовогоУчетаФлаг
	|			ТОГДА &ГруппаФинансовогоУчета
	|		ИНАЧЕ ПорядокУчетаНМА.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|	ВЫБОР
	|		КОГДА &НаправлениеДеятельностиФлаг
	|			ТОГДА &НаправлениеДеятельности
	|		ИНАЧЕ ПорядокУчетаНМА.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА &ОбъемНаработкиФлаг
	|			ТОГДА &ОбъемНаработки
	|		ИНАЧЕ ПорядокУчетаНМА.ОбъемНаработки
	|	КОНЕЦ КАК ОбъемНаработки,
	|	
	|	&ОтражатьВУпрУчете КАК ОтражатьВУпрУчете,
	|	&ОтражатьВРеглУчете КАК ОтражатьВРеглУчете
	|ИЗ
	|	Документ.ИзменениеПараметровНМА2_4.НМА КАК ТаблицаНМА
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаНМА КАК ПорядокУчетаНМА
	|		ПО ПорядокУчетаНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	|ГДЕ
	|	ТаблицаНМА.Ссылка = &Ссылка
	|	И (&ГруппаФинансовогоУчетаФлаг
	|		ИЛИ &НаправлениеДеятельностиФлаг
	|		ИЛИ &ОбъемНаработкиФлаг)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаНМАУУ(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПорядокУчетаНМАУУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаНМАУУ(ТекстыЗапроса, "Документ.ИзменениеПараметровНМА2_4");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	&Организация КАК Организация,
	|	
	|	ПорядокУчета.Состояние КАК Состояние,
	|	ВЫБОР
	|		КОГДА &ПорядокУчетаФлаг
	|			ТОГДА &ПорядокУчета = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислятьАмортизацию)
	|		ИНАЧЕ ПорядокУчета.НачислятьАмортизациюУУ
	|	КОНЕЦ КАК НачислятьАмортизациюУУ,
	|	ВЫБОР
	|		КОГДА &АмортизационныеРасходыФлаг
	|			ТОГДА &Ссылка
	|		ИНАЧЕ ПорядокУчета.СпособОтраженияРасходов
	|	КОНЕЦ КАК СпособОтраженияРасходов,
	|	ВЫБОР
	|		КОГДА &АмортизационныеРасходыФлаг
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ПорядокУчета.СтатьяРасходов
	|	КОНЕЦ КАК СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА &АмортизационныеРасходыФлаг
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ПорядокУчета.АналитикаРасходов
	|	КОНЕЦ КАК АналитикаРасходов,
	|	ВЫБОР
	|		КОГДА &АмортизационныеРасходыФлаг
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ПорядокУчета.ПередаватьРасходыВДругуюОрганизацию
	|	КОНЕЦ КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ВЫБОР
	|		КОГДА &АмортизационныеРасходыФлаг
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		ИНАЧЕ ПорядокУчета.ОрганизацияПолучательРасходов
	|	КОНЕЦ КАК ОрганизацияПолучательРасходов
	|ИЗ
	|	Документ.ИзменениеПараметровНМА2_4.НМА КАК ТаблицаНМА
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаНМАУУ КАК ПорядокУчета
	|		ПО ПорядокУчета.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	|ГДЕ
	|	ТаблицаНМА.Ссылка = &Ссылка
	|	И &ОтражатьВУпрУчете
	|	И (&ПорядокУчетаФлаг
	|		ИЛИ &АмортизационныеРасходыФлаг)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииНМАУУ(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыАмортизацииНМАУУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыАмортизацииНМАУУ(ТекстыЗапроса, "Документ.ИзменениеПараметровНМА2_4");
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	&Организация КАК Организация,
	|	
	|	ВЫБОР
	|		КОГДА &СрокИспользованияУУФлаг
	|			ТОГДА &СрокИспользованияУУ
	|		ИНАЧЕ ЕСТЬNULL(ПараметрыАмортизацииНМАУУ.СрокИспользования, 0)
	|	КОНЕЦ КАК СрокИспользования,
	|	ВЫБОР
	|		КОГДА &КоэффициентУскоренияУУФлаг
	|			ТОГДА &КоэффициентУскоренияУУ
	|		ИНАЧЕ ЕСТЬNULL(ПараметрыАмортизацииНМАУУ.КоэффициентУскорения, 0)
	|	КОНЕЦ КАК КоэффициентУскорения,
	|	ВЫБОР
	|		КОГДА &МетодНачисленияАмортизацииУУФлаг
	|			ТОГДА &МетодНачисленияАмортизацииУУ
	|		ИНАЧЕ ПараметрыАмортизацииНМАУУ.МетодНачисленияАмортизации
	|	КОНЕЦ КАК МетодНачисленияАмортизации,
	|	ВЫБОР
	|		КОГДА &ЛиквидационнаяСтоимостьФлаг
	|			ТОГДА &ЛиквидационнаяСтоимость
	|		ИНАЧЕ ЕСТЬNULL(ПараметрыАмортизацииНМАУУ.ЛиквидационнаяСтоимость, 0)
	|	КОНЕЦ КАК ЛиквидационнаяСтоимость
	|ИЗ
	|	Документ.ИзменениеПараметровНМА2_4.НМА КАК ТаблицаНМА
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПараметрыАмортизацииНМАУУ КАК ПараметрыАмортизацииНМАУУ
	|		ПО ПараметрыАмортизацииНМАУУ.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	|ГДЕ
	|	ТаблицаНМА.Ссылка = &Ссылка
	|	И &ОтражатьВУпрУчете
	|	И (&СрокИспользованияУУФлаг
	|		ИЛИ &КоэффициентУскоренияУУФлаг
	|		ИЛИ &МетодНачисленияАмортизацииУУФлаг
	|		ИЛИ &ЛиквидационнаяСтоимостьФлаг)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаСтоимостьНМА(ТекстыЗапроса)

	ИмяРегистра = "СтоимостьНМА";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтТаблицаНМА(ТекстыЗапроса, "Документ.ИзменениеПараметровНМА2_4");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)  КАК ВидДвижения,
	|	&Период                                 КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровСтоимостиНМА) КАК ХозяйственнаяОперация,
	|	&Организация                            КАК Организация,
	|	ТаблицаНМА.НематериальныйАктив          КАК НематериальныйАктив,
	|	ТаблицаНМА.Подразделение                КАК Подразделение,
	|	ТаблицаНМА.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|
	|	СтоимостьНМА.Стоимость                  КАК Стоимость,
	|	СтоимостьНМА.СтоимостьРегл              КАК СтоимостьРегл,
	|	СтоимостьНМА.СтоимостьНУ                КАК СтоимостьНУ,
	|	СтоимостьНМА.СтоимостьПР                КАК СтоимостьПР,
	|	СтоимостьНМА.СтоимостьВР                КАК СтоимостьВР,
	|	СтоимостьНМА.СтоимостьЦФ                КАК СтоимостьЦФ,
	|	СтоимостьНМА.СтоимостьНУЦФ              КАК СтоимостьНУЦФ,
	|	СтоимостьНМА.СтоимостьПРЦФ              КАК СтоимостьПРЦФ,
	|	СтоимостьНМА.СтоимостьВРЦФ              КАК СтоимостьВРЦФ,
	|	СтоимостьНМА.ПредварительнаяСтоимость       КАК ПредварительнаяСтоимость,
	|	СтоимостьНМА.ПредварительнаяСтоимостьРегл   КАК ПредварительнаяСтоимостьРегл,
	|	СтоимостьНМА.РезервПереоценкиСтоимости      КАК РезервПереоценкиСтоимости,
	|	СтоимостьНМА.РезервПереоценкиСтоимостиРегл  КАК РезервПереоценкиСтоимостиРегл,
	|
	|	0                                       КАК ПриростСтоимости,
	|	0                                       КАК ПриростСтоимостиРегл,
	|
	|	&Организация                            КАК КорОрганизация,
	|	ТаблицаНМА.Подразделение                КАК КорПодразделение,
	|	ВЫБОР 
	|		КОГДА &НаправлениеДеятельностиФлаг
	|			ТОГДА &НаправлениеДеятельности
	|		ИНАЧЕ ТаблицаНМА.НаправлениеДеятельности
	|	КОНЕЦ                                   КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                            КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК КорАналитикаРасходов,
	|	ВЫБОР 
	|		КОГДА &ГруппаФинансовогоУчетаФлаг
	|			ТОГДА &ГруппаФинансовогоУчета
	|		ИНАЧЕ ТаблицаНМА.ГруппаФинансовогоУчета
	|	КОНЕЦ                                   КАК КорГруппаФинансовогоУчета,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки          КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровСтоимостиНМА) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втТаблицаНМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО СтоимостьНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|			И СтоимостьНМА.Ссылка = &Ссылка
	|ГДЕ
	|	&ГруппаФинансовогоУчетаФлаг
	|		ИЛИ &НаправлениеДеятельностиФлаг
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)  КАК ВидДвижения,
	|	&Период                                 КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровСтоимостиНМА) КАК ХозяйственнаяОперация,
	|	&Организация                            КАК Организация,
	|	ТаблицаНМА.НематериальныйАктив          КАК НематериальныйАктив,
	|	ТаблицаНМА.Подразделение                КАК Подразделение,
	|	ВЫБОР 
	|		КОГДА &ГруппаФинансовогоУчетаФлаг
	|			ТОГДА &ГруппаФинансовогоУчета
	|		ИНАЧЕ ТаблицаНМА.ГруппаФинансовогоУчета
	|	КОНЕЦ                                   КАК ГруппаФинансовогоУчета,
	|	ВЫБОР 
	|		КОГДА &НаправлениеДеятельностиФлаг
	|			ТОГДА &НаправлениеДеятельности
	|		ИНАЧЕ ТаблицаНМА.НаправлениеДеятельности
	|	КОНЕЦ                                   КАК НаправлениеДеятельности,
	|
	|	СтоимостьНМА.Стоимость                  КАК Стоимость,
	|	СтоимостьНМА.СтоимостьРегл              КАК СтоимостьРегл,
	|	СтоимостьНМА.СтоимостьНУ                КАК СтоимостьНУ,
	|	СтоимостьНМА.СтоимостьПР                КАК СтоимостьПР,
	|	СтоимостьНМА.СтоимостьВР                КАК СтоимостьВР,
	|	СтоимостьНМА.СтоимостьЦФ                КАК СтоимостьЦФ,
	|	СтоимостьНМА.СтоимостьНУЦФ              КАК СтоимостьНУЦФ,
	|	СтоимостьНМА.СтоимостьПРЦФ              КАК СтоимостьПРЦФ,
	|	СтоимостьНМА.СтоимостьВРЦФ              КАК СтоимостьВРЦФ,
	|	СтоимостьНМА.ПредварительнаяСтоимость       КАК ПредварительнаяСтоимость,
	|	СтоимостьНМА.ПредварительнаяСтоимостьРегл   КАК ПредварительнаяСтоимостьРегл,
	|	СтоимостьНМА.РезервПереоценкиСтоимости      КАК РезервПереоценкиСтоимости,
	|	СтоимостьНМА.РезервПереоценкиСтоимостиРегл  КАК РезервПереоценкиСтоимостиРегл,
	|
	|	0                                       КАК ПриростСтоимости,
	|	0                                       КАК ПриростСтоимостиРегл,
	|
	|	&Организация                            КАК КорОрганизация,
	|	ТаблицаНМА.Подразделение                КАК КорПодразделение,
	|	ТаблицаНМА.НаправлениеДеятельности      КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                            КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК КорАналитикаРасходов,
	|	ТаблицаНМА.ГруппаФинансовогоУчета       КАК КорГруппаФинансовогоУчета,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки          КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровСтоимостиНМА) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	втТаблицаНМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО СтоимостьНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|			И СтоимостьНМА.Ссылка = &Ссылка
	|ГДЕ
	|	&ГруппаФинансовогоУчетаФлаг
	|		ИЛИ &НаправлениеДеятельностиФлаг
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)  КАК ВидДвижения,
	|	&Период                                 КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровСтоимостиНМА) КАК ХозяйственнаяОперация,
	|	&Организация                            КАК Организация,
	|	ТаблицаНМА.НематериальныйАктив          КАК НематериальныйАктив,
	|	НЕОПРЕДЕЛЕНО                            КАК Подразделение,
	|	ТаблицаНМА.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|
	|	0                                       КАК Стоимость,
	|	0                                       КАК СтоимостьРегл,
	|	0                                       КАК СтоимостьНУ,
	|	0                                       КАК СтоимостьПР,
	|	0                                       КАК СтоимостьВР,
	|	0                                       КАК СтоимостьЦФ,
	|	0                                       КАК СтоимостьНУЦФ,
	|	0                                       КАК СтоимостьПРЦФ,
	|	0                                       КАК СтоимостьВРЦФ,
	|	0                                       КАК ПредварительнаяСтоимость,
	|	0                                       КАК ПредварительнаяСтоимостьРегл,
	|	0                                       КАК РезервПереоценкиСтоимости,
	|	0                                       КАК РезервПереоценкиСтоимостиРегл,
	|
	|	СтоимостьНМА.ПриростСтоимости           КАК ПриростСтоимости,
	|	СтоимостьНМА.ПриростСтоимостиРегл       КАК ПриростСтоимостиРегл,
	|
	|	&Организация                            КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО                            КАК КорПодразделение,
	|	ВЫБОР 
	|		КОГДА &НаправлениеДеятельностиФлаг
	|			ТОГДА &НаправлениеДеятельности
	|		ИНАЧЕ ТаблицаНМА.НаправлениеДеятельности
	|	КОНЕЦ                                   КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                            КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК КорАналитикаРасходов,
	|	ВЫБОР 
	|		КОГДА &ГруппаФинансовогоУчетаФлаг
	|			ТОГДА &ГруппаФинансовогоУчета
	|		ИНАЧЕ ТаблицаНМА.ГруппаФинансовогоУчета
	|	КОНЕЦ                                   КАК КорГруппаФинансовогоУчета,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки          КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровСтоимостиНМА) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втТаблицаНМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО СтоимостьНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|			И СтоимостьНМА.Ссылка = &Ссылка
	|ГДЕ
	|	(&ГруппаФинансовогоУчетаФлаг ИЛИ &НаправлениеДеятельностиФлаг)
	|	И (СтоимостьНМА.ПриростСтоимости <> 0 ИЛИ СтоимостьНМА.ПриростСтоимостиРегл <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)  КАК ВидДвижения,
	|	&Период                                 КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровСтоимостиНМА) КАК ХозяйственнаяОперация,
	|	&Организация                            КАК Организация,
	|	ТаблицаНМА.НематериальныйАктив          КАК НематериальныйАктив,
	|	НЕОПРЕДЕЛЕНО                            КАК Подразделение,
	|	ВЫБОР 
	|		КОГДА &ГруппаФинансовогоУчетаФлаг
	|			ТОГДА &ГруппаФинансовогоУчета
	|		ИНАЧЕ ТаблицаНМА.ГруппаФинансовогоУчета
	|	КОНЕЦ                                   КАК ГруппаФинансовогоУчета,
	|	ВЫБОР 
	|		КОГДА &НаправлениеДеятельностиФлаг
	|			ТОГДА &НаправлениеДеятельности
	|		ИНАЧЕ ТаблицаНМА.НаправлениеДеятельности
	|	КОНЕЦ                                   КАК НаправлениеДеятельности,
	|
	|	0                                       КАК Стоимость,
	|	0                                       КАК СтоимостьРегл,
	|	0                                       КАК СтоимостьНУ,
	|	0                                       КАК СтоимостьПР,
	|	0                                       КАК СтоимостьВР,
	|	0                                       КАК СтоимостьЦФ,
	|	0                                       КАК СтоимостьНУЦФ,
	|	0                                       КАК СтоимостьПРЦФ,
	|	0                                       КАК СтоимостьВРЦФ,
	|	0                                       КАК ПредварительнаяСтоимость,
	|	0                                       КАК ПредварительнаяСтоимостьРегл,
	|	0                                       КАК РезервПереоценкиСтоимости,
	|	0                                       КАК РезервПереоценкиСтоимостиРегл,
	|
	|	СтоимостьНМА.ПриростСтоимости           КАК ПриростСтоимости,
	|	СтоимостьНМА.ПриростСтоимостиРегл       КАК ПриростСтоимостиРегл,
	|
	|	&Организация                            КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО                            КАК КорПодразделение,
	|	ТаблицаНМА.НаправлениеДеятельности      КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                            КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК КорАналитикаРасходов,
	|	ТаблицаНМА.ГруппаФинансовогоУчета       КАК КорГруппаФинансовогоУчета,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки          КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровСтоимостиНМА) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	втТаблицаНМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО СтоимостьНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|			И СтоимостьНМА.Ссылка = &Ссылка
	|ГДЕ
	|	(&ГруппаФинансовогоУчетаФлаг ИЛИ &НаправлениеДеятельностиФлаг)
	|	И (СтоимостьНМА.ПриростСтоимости <> 0 ИЛИ СтоимостьНМА.ПриростСтоимостиРегл <> 0)";
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаАмортизацияНМА(ТекстыЗапроса)

	ИмяРегистра = "АмортизацияНМА";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтТаблицаНМА(ТекстыЗапроса, "Документ.ИзменениеПараметровНМА2_4");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)     КАК ВидДвижения,
	|	&Ссылка                                    КАК Регистратор,
	|	&Период                                    КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровАмортизацииНМА) КАК ХозяйственнаяОперация,
	|	&Организация                               КАК Организация,
	|	ТаблицаНМА.НематериальныйАктив             КАК НематериальныйАктив,
	|	ТаблицаНМА.Подразделение                   КАК Подразделение,
	|	ТаблицаНМА.ГруппаФинансовогоУчета          КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                               КАК Контрагент,
	|	АмортизацияНМА.Амортизация                 КАК Амортизация,
	|	АмортизацияНМА.АмортизацияРегл             КАК АмортизацияРегл,
	|	АмортизацияНМА.АмортизацияНУ               КАК АмортизацияНУ,
	|	АмортизацияНМА.АмортизацияПР               КАК АмортизацияПР,
	|	АмортизацияНМА.АмортизацияВР               КАК АмортизацияВР,
	|	АмортизацияНМА.АмортизацияЦФ               КАК АмортизацияЦФ,
	|	АмортизацияНМА.АмортизацияНУЦФ             КАК АмортизацияНУЦФ,
	|	АмортизацияНМА.АмортизацияПРЦФ             КАК АмортизацияПРЦФ,
	|	АмортизацияНМА.АмортизацияВРЦФ             КАК АмортизацияВРЦФ,
	|	АмортизацияНМА.РезервПереоценкиАмортизации КАК РезервПереоценкиАмортизации,
	|	&Организация                               КАК КорОрганизация,
	|	ТаблицаНМА.Подразделение                   КАК КорПодразделение,
	|	ВЫБОР 
	|		КОГДА &НаправлениеДеятельностиФлаг
	|			ТОГДА &НаправлениеДеятельности
	|		ИНАЧЕ ТаблицаНМА.НаправлениеДеятельности
	|	КОНЕЦ                                      КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                               КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаРасходов,
	|	ВЫБОР 
	|		КОГДА &ГруппаФинансовогоУчетаФлаг
	|			ТОГДА &ГруппаФинансовогоУчета
	|		ИНАЧЕ ТаблицаНМА.ГруппаФинансовогоУчета
	|	КОНЕЦ                    КАК КорГруппаФинансовогоУчета,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки             КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровАмортизацииНМА) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	втТаблицаНМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияНМА
	|		ПО АмортизацияНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|			И АмортизацияНМА.Ссылка = &Ссылка
	|ГДЕ
	|	&ГруппаФинансовогоУчетаФлаг
	|		ИЛИ &НаправлениеДеятельностиФлаг
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)     КАК ВидДвижения,
	|	&Ссылка                                    КАК Регистратор,
	|	&Период                                    КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровАмортизацииНМА) КАК ХозяйственнаяОперация,
	|	&Организация                               КАК Организация,
	|	ТаблицаНМА.НематериальныйАктив             КАК НематериальныйАктив,
	|	ТаблицаНМА.Подразделение                   КАК Подразделение,
	|	ВЫБОР 
	|		КОГДА &ГруппаФинансовогоУчетаФлаг
	|			ТОГДА &ГруппаФинансовогоУчета
	|		ИНАЧЕ ТаблицаНМА.ГруппаФинансовогоУчета
	|	КОНЕЦ                                      КАК ГруппаФинансовогоУчета,
	|	ВЫБОР 
	|		КОГДА &НаправлениеДеятельностиФлаг
	|			ТОГДА &НаправлениеДеятельности
	|		ИНАЧЕ ТаблицаНМА.НаправлениеДеятельности
	|	КОНЕЦ                                      КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                               КАК Контрагент,
	|	АмортизацияНМА.Амортизация                 КАК Амортизация,
	|	АмортизацияНМА.АмортизацияРегл             КАК АмортизацияРегл,
	|	АмортизацияНМА.АмортизацияНУ               КАК АмортизацияНУ,
	|	АмортизацияНМА.АмортизацияПР               КАК АмортизацияПР,
	|	АмортизацияНМА.АмортизацияВР               КАК АмортизацияВР,
	|	АмортизацияНМА.АмортизацияЦФ               КАК АмортизацияЦФ,
	|	АмортизацияНМА.АмортизацияНУЦФ             КАК АмортизацияНУЦФ,
	|	АмортизацияНМА.АмортизацияПРЦФ             КАК АмортизацияПРЦФ,
	|	АмортизацияНМА.АмортизацияВРЦФ             КАК АмортизацияВРЦФ,
	|	АмортизацияНМА.РезервПереоценкиАмортизации КАК РезервПереоценкиАмортизации,
	|	&Организация                               КАК КорОрганизация,
	|	ТаблицаНМА.Подразделение                   КАК КорПодразделение,
	|	ТаблицаНМА.НаправлениеДеятельности         КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                               КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                               КАК КорАналитикаРасходов,
	|	ТаблицаНМА.ГруппаФинансовогоУчета          КАК КорГруппаФинансовогоУчета,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки             КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ИзменениеПараметровАмортизацииНМА) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	втТаблицаНМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияНМА
	|		ПО АмортизацияНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|			И АмортизацияНМА.Ссылка = &Ссылка
	|ГДЕ
	|	&ГруппаФинансовогоУчетаФлаг
	|		ИЛИ &НаправлениеДеятельностиФлаг";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса)

	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтТаблицаНМА(ТекстыЗапроса, "Документ.ИзменениеПараметровНМА2_4");
	
	ТекстЗапроса =
	// Перемещение амортизации
	"ВЫБРАТЬ
	|	&Период                                      КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровАмортизацииНМА) КАК ХозяйственнаяОперация,
	|	&Организация                                 КАК Организация,
	|
	|	ТаблицаНМА.Подразделение                     КАК Подразделение,
	|	ТаблицаНМА.НаправлениеДеятельности           КАК НаправлениеДеятельности,
	|	&СтатьяАП_НМА                                КАК Статья,
	|	НЕОПРЕДЕЛЕНО                                 КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                 КАК АналитикаРасходов,
	|	ТаблицаНМА.НематериальныйАктив               КАК АналитикаАктивовПассивов,
	|	ТаблицаНМА.ГруппаФинансовогоУчета            КАК ГруппаФинансовогоУчета,
	|
	|	ТаблицаНМА.Подразделение                     КАК КорПодразделение,
	|	ВЫБОР 
	|		КОГДА &НаправлениеДеятельностиФлаг
	|			ТОГДА &НаправлениеДеятельности
	|		ИНАЧЕ ТаблицаНМА.НаправлениеДеятельности
	|	КОНЕЦ                                        КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_НМА                                КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                                 КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                 КАК КорАналитикаРасходов,
	|	ТаблицаНМА.НематериальныйАктив               КАК КорАналитикаАктивовПассивов,
	|	ВЫБОР 
	|		КОГДА &ГруппаФинансовогоУчетаФлаг
	|			ТОГДА &ГруппаФинансовогоУчета
	|		ИНАЧЕ ТаблицаНМА.ГруппаФинансовогоУчета
	|	КОНЕЦ                                        КАК КорГруппаФинансовогоУчета,
	|
	|	&Организация                                 КАК КорОрганизация,
	|
	|	АмортизацияНМА.Амортизация                   КАК Сумма,
	|	АмортизацияНМА.Амортизация                   КАК СуммаУпр,
	|	АмортизацияНМА.АмортизацияРегл + АмортизацияНМА.АмортизацияЦФ КАК СуммаРегл
	|ИЗ
	|	втТаблицаНМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияНМА
	|		ПО АмортизацияНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|			И АмортизацияНМА.Ссылка = &Ссылка
	|ГДЕ
	|	&ГруппаФинансовогоУчетаФлаг
	|		ИЛИ &НаправлениеДеятельностиФлаг
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Перемещение стоимости
	|ВЫБРАТЬ
	|	&Период                                      КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИзменениеПараметровСтоимостиНМА) КАК ХозяйственнаяОперация,
	|	&Организация                                 КАК Организация,
	|
	|	ТаблицаНМА.Подразделение                     КАК Подразделение,
	|	ТаблицаНМА.НаправлениеДеятельности           КАК НаправлениеДеятельности,
	|	&СтатьяАП_НМА                                КАК Статья,
	|	НЕОПРЕДЕЛЕНО                                 КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                 КАК АналитикаРасходов,
	|	ТаблицаНМА.НематериальныйАктив               КАК АналитикаАктивовПассивов,
	|	ТаблицаНМА.ГруппаФинансовогоУчета            КАК ГруппаФинансовогоУчета,
	|
	|	ТаблицаНМА.Подразделение                     КАК КорПодразделение,
	|	ВЫБОР 
	|		КОГДА &НаправлениеДеятельностиФлаг
	|			ТОГДА &НаправлениеДеятельности
	|		ИНАЧЕ ТаблицаНМА.НаправлениеДеятельности
	|	КОНЕЦ                                        КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_НМА                                КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                                 КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                                 КАК КорАналитикаРасходов,
	|	ТаблицаНМА.НематериальныйАктив               КАК КорАналитикаАктивовПассивов,
	|	ВЫБОР 
	|		КОГДА &ГруппаФинансовогоУчетаФлаг
	|			ТОГДА &ГруппаФинансовогоУчета
	|		ИНАЧЕ ТаблицаНМА.ГруппаФинансовогоУчета
	|	КОНЕЦ                                        КАК КорГруппаФинансовогоУчета,
	|
	|	&Организация                                 КАК КорОрганизация,
	|
	|	СтоимостьНМА.Стоимость                       КАК Сумма,
	|	СтоимостьНМА.Стоимость                       КАК СуммаУпр,
	|	СтоимостьНМА.СтоимостьРегл + СтоимостьНМА.СтоимостьЦФ КАК СуммаРегл
	|ИЗ
	|	втТаблицаНМА КАК ТаблицаНМА
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО СтоимостьНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|			И СтоимостьНМА.Ссылка = &Ссылка
	|ГДЕ
	|	&ГруппаФинансовогоУчетаФлаг
	|		ИЛИ &НаправлениеДеятельностиФлаг";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

//++ НЕ УТКА

Функция ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВМеждународномУчете";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                        КАК Период,
	|	&Организация                   КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)   КАК ДатаОтражения,
	|	&ХозяйственнаяОперация         КАК ХозяйственнаяОперация
	|ГДЕ
	|	&Период >= &ДатаНачалаУчета
	|	И &ГруппаФинансовогоУчетаФлаг";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

//-- НЕ УТКА

//++ НЕ УТ
Функция ТекстЗапросаТаблицаСоставЕГДС(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СоставЕГДС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтТаблицаНМА(ТекстыЗапроса, "Документ.ИзменениеПараметровНМА2_4");
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                      КАК Регистратор,
	|	&Период                      КАК Период,
	|	ВтТаблицаНМА.НематериальныйАктив КАК ВнеоборотныйАктив,
	|	&Организация                 КАК Организация,
	|	&ЕГДС                        КАК ЕГДС
	|ИЗ
	|	ВтТаблицаНМА КАК ВтТаблицаНМА
	|ГДЕ
	|	&ЕГДСФлаг = ИСТИНА";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции
//-- НЕ УТ

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат ИзменениеПараметровНМАЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат ИзменениеПараметровНМАЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Документы.ИзменениеПараметровНМА2_4.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.5.1";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("04b4b118-bd83-4058-bf59-8c25afc3cb47");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.ИзменениеПараметровНМА2_4.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Обновляет документы ""Изменение параметров НМА"":
	|- Заполняет новые реквизиты ""Отражать в БУ"", ""Отражать в НУ"".
	|- Заполняет реквизит ""Идентификатор строки"" в табличных частях.';
	|en = 'Updates the ""Adjust intangible asset financial details"" documents:
	|- Populates new ""Record in AC"", ""Record in TA"" attributes.
	|- Populates the ""Line ID"" attribute in tables.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.ИзменениеПараметровНМА2_4.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Документы.ИзменениеПараметровНМА2_4.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Документы.ИзменениеПараметровНМА2_4.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт

	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = "Документ.ИзменениеПараметровНМА2_4";
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	
	СписокЗапросов = Новый Массив;
	
	ИзменениеПараметровНМАЛокализация.ДополнитьСписокЗапросовРегистрацииДанныхКОбработкеДляПереходаНаНовуюВерсию(СписокЗапросов);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ИзменениеПараметровНМА2_4 КАК Документ
	|ГДЕ
	|	ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			Документ.ИзменениеПараметровНМА2_4.НМА КАК ТабЧасть
	|		ГДЕ
	|			ТабЧасть.Ссылка = Документ.Ссылка
	|			И ТабЧасть.ИдентификаторСтроки = """")";

	СписокЗапросов.Добавить(ТекстЗапроса);
	
	Если СписокЗапросов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	Запрос = Новый Запрос(ТекстЗапроса);
	СписокСсылок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, СписокСсылок);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт

	ПолноеИмяОбъекта = "Документ.ИзменениеПараметровНМА2_4";
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Для каждого Выборка Из ОбновляемыеДанные Цикл
	
 		НачатьТранзакцию();
		
 		Попытка
			
 			Блокировка = Новый БлокировкаДанных;
 			ЭлементБлокировки = Блокировка.Добавить("Документ.ИзменениеПараметровНМА2_4");
 			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
 			Блокировка.Заблокировать();
 			
			ДанныеОбъекта = Выборка.Ссылка.ПолучитьОбъект();
			Если ДанныеОбъекта = Неопределено Тогда
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
				ЗафиксироватьТранзакцию();
 				Продолжить;
			КонецЕсли;
			
			ИзменениеПараметровОСЛокализация.ОбработатьДанныеДляПереходаНаНовуюВерсию(ДанныеОбъекта);
			ОбщегоНазначенияУТ.ЗаполнитьИдентификаторыДокумента(ДанныеОбъекта, "НМА");
			
			Если ДанныеОбъекта.Модифицированность() Тогда
	 			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДанныеОбъекта);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
			КонецЕсли; 
	
			ЗафиксироватьТранзакцию();
			
 		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru = 'Не удалось обработать объект: %Ссылка% по причине: %Причина%';
									|en = 'Cannot process the object: %Ссылка%. Reason: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Выборка.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
									УровеньЖурналаРегистрации.Предупреждение,
									Выборка.Ссылка.Метаданные(),
									Выборка.Ссылка,
									ТекстСообщения);
									
 		КонецПопытки;
 
 	КонецЦикла;
 		
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ПараметрыВыбораСтатейИАналитик() Экспорт
	
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект.АмортизационныеРасходы";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("АмортизационныеРасходыСтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АмортизационныеРасходыАналитикаРасходов");
	
	Возврат ПараметрыВыбора;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
