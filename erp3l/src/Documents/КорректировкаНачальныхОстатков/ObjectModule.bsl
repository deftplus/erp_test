
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Перем База Экспорт;
Перем ТабНастройкиСоответствия Экспорт;
	
#Область ПрограммныйИнтерфейс

Процедура ОбновитьТаблицыДокумента() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период", Новый Граница(КонецДня(Дата), ВидГраницы.Включая));
	
	СубконтоКонтрагентДоговор = Новый СписокЗначений;
	СубконтоКонтрагентДоговор.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	СубконтоКонтрагентДоговор.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	
	Запрос.УстановитьПараметр("СубконтоКонтрагентДоговор", СубконтоКонтрагентДоговор);	
	
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВидыСубконтоКонтрагенты.Ссылка КАК Счет
	|ПОМЕСТИТЬ СчетаРасчетов
	|ИЗ
	|	ПланСчетов.Хозрасчетный.ВидыСубконто КАК ВидыСубконтоКонтрагенты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ВидыСубконтоДоговоры
	|		ПО ВидыСубконтоКонтрагенты.Ссылка = ВидыСубконтоДоговоры.Ссылка
	|ГДЕ
	|	ВидыСубконтоКонтрагенты.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты)
	|	И НЕ ВидыСубконтоКонтрагенты.Ссылка.Забалансовый
	|	И ВидыСубконтоДоговоры.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК Контрагент,
	|	СУММА(ВЫБОР
	|			КОГДА ХозрасчетныйОстатки.Счет.Валютный
	|				ТОГДА ХозрасчетныйОстатки.ВалютнаяСуммаРазвернутыйОстатокДт - ХозрасчетныйОстатки.ВалютнаяСуммаРазвернутыйОстатокКт
	|			ИНАЧЕ ХозрасчетныйОстатки.СуммаРазвернутыйОстатокДт - ХозрасчетныйОстатки.СуммаРазвернутыйОстатокКт
	|		КОНЕЦ) КАК СуммаВзаиморасчетов,
	|	ХозрасчетныйОстатки.Субконто2 КАК ДоговорКонтрагента
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&Период,
	|			Счет В
	|				(ВЫБРАТЬ
	|					СчетаРасчетов.Счет
	|				ИЗ
	|					СчетаРасчетов),
	|			&СубконтоКонтрагентДоговор,
	|			Организация В (&Организация)) КАК ХозрасчетныйОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОстатки.Субконто1,
	|	ХозрасчетныйОстатки.Субконто2";
	
	ВзаиморасчетыСКонтрагентами.Загрузить(Запрос.Выполнить().Выгрузить());
			
КонецПроцедуры // ОбновитьТаблицыДокумента() 

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();
	Дата = НачалоДня(ТекущаяДатаСеанса()) - 1;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСерверУХ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	ПараметрыПроведения = Документы.КорректировкаНачальныхОстатков.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ПараметрыПроведения", ПараметрыПроведения);
	ПроведениеСерверУХ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	Для каждого ТаблицаДвижения Из ПараметрыПроведения.ТаблицыДляДвижений Цикл
		Движения[ТаблицаДвижения.Ключ].Загрузить(ТаблицаДвижения.Значение);
		Движения[ТаблицаДвижения.Ключ].Записывать = Истина;
	КонецЦикла;
		
	ПроведениеСерверУХ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	ПроведениеСерверУХ.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);	
	ПроведениеСерверУХ.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

#КонецОбласти


#КонецЕсли