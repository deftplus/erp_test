#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт
	
	ТаблицыДляДвижений = Новый Структура;		
	ДополнительныеСвойства = Новый Структура;
	НомераТаблиц = Новый Структура;
	
	// реквизиты документа.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента();	
	Реквизиты = ПолучениеДанныхУчетнойСистемыПереопределяемыйУХ.СтрокаТаблицыЗначенийВСтруктуру(Запрос.Выполнить().Выгрузить()[0]);

	ДополнительныеСвойства.Вставить("Реквизиты", Реквизиты);
	
	Если Отказ Тогда
		Возврат ДополнительныеСвойства;
	КонецЕсли;
	
	Для Каждого КлючИЗначение Из Реквизиты Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета", ОбщегоНазначенияПовтИспУХ.ПолучитьВалютуУправленческогоУчета());
	
	ТекстРазделителя = ПолучениеДанныхУчетнойСистемыПереопределяемыйУХ.ТекстРазделителяЗапросовПакета();
	
	Запрос.Текст = 
	ТекстЗапросаВзаиморасчетыСКонтрагентами(НомераТаблиц, Реквизиты);
	
	Результат = Запрос.ВыполнитьПакет();

	Для Каждого НомерТаблицы Из НомераТаблиц Цикл
		Если СтрНачинаетсяС(НомерТаблицы.Ключ, "ВТ_") Тогда			
			Продолжить;
		КонецЕсли;
		ТаблицыДляДвижений.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
			
	ДополнительныеСвойства.Вставить("ТаблицыДляДвижений", ТаблицыДляДвижений);
	
	Возврат ДополнительныеСвойства;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц = Неопределено)

	Если НомераТаблиц <> Неопределено Тогда
		НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());	
	КонецЕсли;	
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ТолькоПоТабличнымЧастям КАК ТолькоПоТабличнымЧастям
	|ИЗ
	|	Документ.КорректировкаНачальныхОстатков КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаВзаиморасчетыСКонтрагентами(НомераТаблиц, Реквизиты)
	
	НомераТаблиц.Вставить("ВТ_ВзаиморасчетыСКонтрагентами", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("РасчетыСКонтрагентамиФакт", НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТабВзаиморасчетыСКонтрагентами.Контрагент КАК Контрагент,
	|	ТабВзаиморасчетыСКонтрагентами.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ТабВзаиморасчетыСКонтрагентами.ЭлементСтруктурыЗадолженности КАК ЭлементСтруктурыЗадолженности,
	|	ТабВзаиморасчетыСКонтрагентами.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ВЫБОР
	|		КОГДА ТабВзаиморасчетыСКонтрагентами.ОбъектРасчетов ССЫЛКА Справочник.ЦенныеБумаги
	|			ТОГДА ТабВзаиморасчетыСКонтрагентами.ОбъектРасчетов.ВалютаНоминала
	|		КОГДА ТабВзаиморасчетыСКонтрагентами.ОбъектРасчетов ССЫЛКА Справочник.ДоговорыКонтрагентов
	|			ТОГДА ТабВзаиморасчетыСКонтрагентами.ОбъектРасчетов.ВалютаВзаиморасчетов
	|		КОГДА ТабВзаиморасчетыСКонтрагентами.ОбъектРасчетов ССЫЛКА Документ.ЗаказПоставщику
	|			ТОГДА &ВалютаЗаказаПоставщику
	|	КОНЕЦ КАК ВалютаВзаиморасчетов,
	|	ВЫБОР
	|		КОГДА ТабВзаиморасчетыСКонтрагентами.ОбъектРасчетов ССЫЛКА Справочник.ЦенныеБумаги
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ТабВзаиморасчетыСКонтрагентами.ОбъектРасчетов ССЫЛКА Справочник.ДоговорыКонтрагентов
	|			ТОГДА ВЫБОР
	|					КОГДА ТабВзаиморасчетыСКонтрагентами.ОбъектРасчетов.ВидСоглашения = ЗНАЧЕНИЕ(Перечисление.ВидыСоглашений.Спецификация)
	|						ТОГДА ТабВзаиморасчетыСКонтрагентами.ОбъектРасчетов.БазовыйДоговор
	|					ИНАЧЕ ТабВзаиморасчетыСКонтрагентами.ОбъектРасчетов
	|				КОНЕЦ
	|		КОГДА ТабВзаиморасчетыСКонтрагентами.ОбъектРасчетов ССЫЛКА Документ.ЗаказПоставщику
	|			ТОГДА &ДоговорЗаказаПоставщику
	|	КОНЕЦ КАК ДоговорКонтрагента
	|ПОМЕСТИТЬ ВТ_ВзаиморасчетыСКонтрагентами
	|ИЗ
	|	Документ.КорректировкаНачальныхОстатков.ВзаиморасчетыСКонтрагентами КАК ТабВзаиморасчетыСКонтрагентами
	|ГДЕ
	|	ТабВзаиморасчетыСКонтрагентами.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА СУММА(СравнениеОстатков.СуммаВзаиморасчетовНовая) - СУММА(СравнениеОстатков.СуммаВзаиморасчетовТекущая) > 0
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	СравнениеОстатков.Контрагент КАК Контрагент,
	|	СравнениеОстатков.ОбъектРасчетов КАК ОбъектРасчетов,
	|	СравнениеОстатков.ЭлементСтруктурыЗадолженности КАК ЭлементСтруктурыЗадолженности,
	|	СравнениеОстатков.Валюта КАК Валюта,
	|	СравнениеОстатков.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВЫБОР
	|		КОГДА СУММА(СравнениеОстатков.СуммаВзаиморасчетовНовая) - СУММА(СравнениеОстатков.СуммаВзаиморасчетовТекущая) > 0
	|			ТОГДА СУММА(СравнениеОстатков.СуммаВзаиморасчетовНовая) - СУММА(СравнениеОстатков.СуммаВзаиморасчетовТекущая)
	|		ИНАЧЕ СУММА(СравнениеОстатков.СуммаВзаиморасчетовТекущая) - СУММА(СравнениеОстатков.СуммаВзаиморасчетовНовая)
	|	КОНЕЦ КАК Сумма
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ_ВзаиморасчетыСКонтрагентами.Контрагент КАК Контрагент,
	|		ВТ_ВзаиморасчетыСКонтрагентами.ОбъектРасчетов КАК ОбъектРасчетов,
	|		ВТ_ВзаиморасчетыСКонтрагентами.ЭлементСтруктурыЗадолженности КАК ЭлементСтруктурыЗадолженности,
	|		ВТ_ВзаиморасчетыСКонтрагентами.ВалютаВзаиморасчетов КАК Валюта,
	|		ВТ_ВзаиморасчетыСКонтрагентами.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|		ВТ_ВзаиморасчетыСКонтрагентами.СуммаВзаиморасчетов КАК СуммаВзаиморасчетовНовая,
	|		0 КАК СуммаВзаиморасчетовТекущая
	|	ИЗ
	|		ВТ_ВзаиморасчетыСКонтрагентами КАК ВТ_ВзаиморасчетыСКонтрагентами
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РасчетыСКонтрагентамиФактОстатки.Контрагент,
	|		РасчетыСКонтрагентамиФактОстатки.ОбъектРасчетов,
	|		РасчетыСКонтрагентамиФактОстатки.ЭлементСтруктурыЗадолженности,
	|		РасчетыСКонтрагентамиФактОстатки.Валюта,
	|		РасчетыСКонтрагентамиФактОстатки.ДоговорКонтрагента,
	|		0,
	|		РасчетыСКонтрагентамиФактОстатки.СуммаОстаток
	|	ИЗ
	|		РегистрНакопления.РасчетыСКонтрагентамиФакт.Остатки(
	|				&Период,
	|				Организация = &Организация
	|					И (НЕ &ТолькоПоТабличнымЧастям
	|						ИЛИ (ОбъектРасчетов, ЭлементСтруктурыЗадолженности, Валюта) В
	|							(ВЫБРАТЬ
	|								ВТ_ВзаиморасчетыСКонтрагентами.ОбъектРасчетов,
	|								ВТ_ВзаиморасчетыСКонтрагентами.ЭлементСтруктурыЗадолженности,
	|								ВТ_ВзаиморасчетыСКонтрагентами.ВалютаВзаиморасчетов
	|							ИЗ
	|								ВТ_ВзаиморасчетыСКонтрагентами КАК ВТ_ВзаиморасчетыСКонтрагентами))) КАК РасчетыСКонтрагентамиФактОстатки) КАК СравнениеОстатков
	|
	|СГРУППИРОВАТЬ ПО
	|	СравнениеОстатков.Контрагент,
	|	СравнениеОстатков.ОбъектРасчетов,
	|	СравнениеОстатков.ЭлементСтруктурыЗадолженности,
	|	СравнениеОстатков.Валюта,
	|	СравнениеОстатков.ДоговорКонтрагента
	|
	|ИМЕЮЩИЕ
	|	СУММА(СравнениеОстатков.СуммаВзаиморасчетовНовая) - СУММА(СравнениеОстатков.СуммаВзаиморасчетовТекущая) <> 0";
	
	РеквизитыЗаказа = ВзаиморасчетыВстраиваниеУХКлиентСервер.ИменаРеквизитовЗаказаПоставщику();
	ТекстЗапроса = СтрЗаменить(
				ТекстЗапроса, 
				"&ВалютаЗаказаПоставщику", 
				"ТабВзаиморасчетыСКонтрагентами.ОбъектРасчетов." + РеквизитыЗаказа.ВалютаДокумента);
				
	ТекстЗапроса = СтрЗаменить(
				ТекстЗапроса, 
				"&ДоговорЗаказаПоставщику", 
				"ТабВзаиморасчетыСКонтрагентами.ОбъектРасчетов." + РеквизитыЗаказа.ДоговорКонтрагента);
				
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти
	
#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти
	
#КонецЕсли