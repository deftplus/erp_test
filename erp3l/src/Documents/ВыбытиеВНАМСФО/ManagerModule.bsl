#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ПодготовкаПараметровПроведенияДокумента

Функция ПодготовитьПараметрыПроведения(ДополнительныеСвойства, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДополнительныеСвойства.ДляПроведения.Ссылка);
	
	Реквизиты = МСФОУХ.РеквизитыДокумента(Запрос, "РежимЗаполнения, АлгоритмРСБУ",, Отказ);
	Реквизиты.Вставить("ДанныеРежимаЗаполнения", УчетМСФОПовтИспУХ.ПолучитьВидыУчетаРежимаЗаполнения(Реквизиты.РежимЗаполнения));
	ДополнительныеСвойства.ДляПроведения.Вставить("Реквизиты", Реквизиты);
	Если Отказ Тогда
		Возврат ДополнительныеСвойства;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация",	Реквизиты.Организация);
	Запрос.УстановитьПараметр("Сценарий",		Реквизиты.Сценарий);	
	Запрос.УстановитьПараметр("Период",			КонецДня(Реквизиты.Период));
	Запрос.УстановитьПараметр("АлгоритмРСБУ",	Реквизиты.АлгоритмРСБУ);
	Запрос.УстановитьПараметр("ФормироватьПроводкиМСФО", 	Реквизиты.ФормироватьПроводкиМСФО);
	Запрос.УстановитьПараметр("ЕстьМСФО", 		Реквизиты.ДанныеРежимаЗаполнения.ЕстьМСФО);
	Запрос.УстановитьПараметр("ЕстьНСБУ", 		Реквизиты.ДанныеРежимаЗаполнения.ЕстьНСБУ);	
	Запрос.УстановитьПараметр("ВидыУчета", 		Перечисления.ВидыУчета.ВидыУчетаПоДаннымРежима(Реквизиты.ДанныеРежимаЗаполнения));
	Запрос.УстановитьПараметр("ВсеОрганизации", Ложь);
	
	Если Реквизиты.АлгоритмРСБУ Тогда
		Запрос.УстановитьПараметр("ВсеВНА", Ложь); //Список ВНА определяется в предыдущем запросе
	Иначе 
		Запрос.УстановитьПараметр("ВсеВНА", Истина);//список ВНА определяется остатками стоимости
		Запрос.УстановитьПараметр("ВНА", 	Неопределено);// параметр не влияет на расчет 
	КонецЕсли;
	
	Отчеты.ПараметрыВНА.ЗаполнитьПараметрыЗапроса(Запрос, Реквизиты.Период, Реквизиты.Ссылка, Реквизиты.АлгоритмРСБУ); 
	
	НомераТаблиц = Новый Структура;
	ТекстЗапроса = Новый Массив;
	
	ТекстЗапроса.Добавить(ТекстЗапроса_ТаблицаВыбытияВНА(НомераТаблиц));
	ТекстЗапроса.Добавить(МСФОВНАУХ.ТекстЗапроса_ПараметрыВНА(НомераТаблиц, "ВЫБРАТЬ т.ВНА ИЗ втТаблицаВыбытияВНА КАК т", Реквизиты.АлгоритмРСБУ));
	ДополнитьЗапросТаблицамиВыбытия(ТекстЗапроса, НомераТаблиц);
		
	Запрос.Текст = СтрСоединить(ТекстЗапроса, ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета());
	ПроведениеСерверУХ.ДополнитьТаблицамиИзПакетаЗапросов(ДополнительныеСвойства.ТаблицыДляДвижений, Запрос, НомераТаблиц);
	
КонецФункции

//процедура используется в документе ВыбытиеВНАМСФО и в лизинге полученном при выбытии(РегламентнаяОперацияПериодаПоФинансовымИнструментамМСФО)	
Процедура ДополнитьЗапросТаблицамиВыбытия(ТекстЗапроса, НомераТаблиц) Экспорт
	
	ТекстЗапроса.Добавить(ТекстЗапроса_втТаблицаВНА(НомераТаблиц));
	ТекстЗапроса.Добавить(ТекстЗапроса_ТаблицаОшибок(НомераТаблиц));
	ТекстЗапроса.Добавить(ТекстЗапроса_ТаблицаВНА(НомераТаблиц));
	ТекстЗапроса.Добавить(ТекстЗапроса_ТаблицаПроводкиВНА(НомераТаблиц));

КонецПроцедуры

Функция ТекстЗапроса_ТаблицаВыбытияВНА(НомераТаблиц)
	
	НомераТаблиц.Вставить("втТаблицаВыбытияВНА", НомераТаблиц.Количество());
	//ДатаВыбытияВНА нужна для лизинга выданного(разные даты выбытия по ВНА)
	
	Возврат
	"ВЫБРАТЬ
	|	&Период КАК ДатаВыбытияВНА,
	|	ТаблицаВНА.ВНА КАК ВНА,
	|	ТаблицаВНА.ВидОперации КАК ВидОперации,
	|	ТаблицаВНА.ЧастичноеВыбытиеНСБУ КАК ЧастичноеВыбытиеНСБУ,
	|	ТаблицаВНА.ЧастичноеВыбытиеМСФО КАК ЧастичноеВыбытиеМСФО,
	|	ТаблицаВНА.ВыбытиеСтоимостиНСБУ КАК ВыбытиеСтоимостиНСБУ,
	|	ТаблицаВНА.ВыбытиеСтоимостиМСФО КАК ВыбытиеСтоимостиМСФО,
	|	0 КАК ЧистаяИнвестицияВЛизинг,
	|	ТаблицаВНА.СчетРасходаМСФО КАК СчетРасходаМСФО,
	|	ТаблицаВНА.СчетРасходаСубконто1МСФО КАК СчетРасходаСубконто1МСФО,
	|	ТаблицаВНА.СчетРасходаСубконто2МСФО КАК СчетРасходаСубконто2МСФО,
	|	ТаблицаВНА.СчетРасходаСубконто3МСФО КАК СчетРасходаСубконто3МСФО,
	|	ТаблицаВНА.СторнироватьДанныеНСБУ КАК СторнироватьДанныеНСБУ
	|ПОМЕСТИТЬ втТаблицаВыбытияВНА
	|ИЗ
	|	Документ.ВыбытиеВНАМСФО.ВНА КАК ТаблицаВНА
	|ГДЕ
	|	ТаблицаВНА.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВНА";	

КонецФункции

Функция ТекстЗапроса_втТаблицаВНА(НомераТаблиц)

	НомераТаблиц.Вставить("втТаблицаВНА", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	т.ДатаВыбытияВНА КАК Период,
	|	т.ВНА КАК ВНА,
	|	р.ГруппаВНА КАК ГруппаВНА,
	|	р.ГруппаВНА.ПараметрыПереоценки КАК ПараметрыПереоценки,
	|	р.ГруппаВНА.ПараметрыОбесценения КАК ПараметрыОбесценения,
	|	р.ПрофильРаспределения КАК ПрофильРаспределения,
	|	р.ВидУчета КАК ВидУчета,
	|	р.Подразделение КАК ПодразделениеДт,
	|	р.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	р.Подразделение КАК ПодразделениеКт,
	|	р.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,	
	|	т.СторнироватьДанныеНСБУ КАК СторнироватьДанныеНСБУ,
	|	т.СчетРасходаМСФО КАК СчетРасходаМСФО,
	|	т.СчетРасходаСубконто1МСФО КАК СчетРасходаСубконто1МСФО,
	|	т.СчетРасходаСубконто2МСФО КАК СчетРасходаСубконто2МСФО,
	|	т.СчетРасходаСубконто3МСФО КАК СчетРасходаСубконто3МСФО,
	|	р.ГруппаВНА.ПараметрыВыбытия.СчетВыбытияВНА КАК СчетВыбытияВНА,
	|	р.ГруппаВНА.ПараметрыВыбытия.СчетВыбытияВНАСубконто1 КАК СчетВыбытияВНАСубконто1,
	|	р.ГруппаВНА.ПараметрыВыбытия.СчетВыбытияВНАСубконто2 КАК СчетВыбытияВНАСубконто2,
	|	р.ГруппаВНА.ПараметрыВыбытия.СчетВыбытияВНАСубконто3 КАК СчетВыбытияВНАСубконто3,
	|	т.ЧистаяИнвестицияВЛизинг КАК ЧистаяИнвестицияВЛизинг,
	|	ЕСТЬNULL(р.ГруппаВНА.ПараметрыВыбытия.СчетВыбытияВНА, ЗНАЧЕНИЕ(Справочник.СчетаБД.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.СчетаБД.ПустаяСсылка)
	|		И ЕСТЬNULL(р.ГруппаВНА.ПараметрыВыбытия.СчетВыбытияВНА, НЕОПРЕДЕЛЕНО) <> р.ГруппаВНА.СчетУчетаПервоначальнойСтоимости КАК ИспользоватьСчетВыбытияОстаточнойСтоимости,
	|	ВЫБОР
	|		КОГДА р.ПервоначальнаяСтоимость = 0
	|			ТОГДА 0
	|		КОГДА т.ЧастичноеВыбытиеМСФО
	|				И р.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.МСФО)
	|			ТОГДА т.ВыбытиеСтоимостиМСФО
	|		КОГДА т.ЧастичноеВыбытиеНСБУ
	|				И р.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.НСБУ)
	|			ТОГДА т.ВыбытиеСтоимостиНСБУ
	|		ИНАЧЕ р.ПервоначальнаяСтоимость
	|	КОНЕЦ КАК ПервоначальнаяСтоимость,
	|	ВЫБОР
	|		КОГДА р.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.НСБУ)
	|			ТОГДА р.Амортизация
	|		КОГДА &АлгоритмРСБУ
	|				И т.ЧастичноеВыбытиеМСФО
	|			ТОГДА р.Амортизация * т.ВыбытиеСтоимостиМСФО / р.ПервоначальнаяСтоимость
	|		КОГДА р.Амортизация + р.АмортизацияРасчет = 0
	|			ТОГДА 0
	|		КОГДА т.ЧастичноеВыбытиеМСФО
	|			ТОГДА (р.Амортизация + р.АмортизацияРасчет) * т.ВыбытиеСтоимостиМСФО / р.ПервоначальнаяСтоимость
	|		ИНАЧЕ р.Амортизация + р.АмортизацияРасчет
	|	КОНЕЦ КАК НакопленнаяАмортизация,
	|	р.Переоценка + р.РезервПереоценкиРасчет КАК Переоценка,
	|	р.РезервПереоценки,
	|	р.Обесценение КАК Обесценение,
	|	ВЫБОР
	|		КОГДА т.ЧастичноеВыбытиеМСФО
	|				И &АлгоритмРСБУ
	|			ТОГДА 0
	|		ИНАЧЕ р.АмортизацияРасчет
	|	КОНЕЦ КАК АмортизацияРасчет,
	|	ВЫБОР
	|		КОГДА т.ЧастичноеВыбытиеМСФО
	|				И &АлгоритмРСБУ
	|			ТОГДА 0
	|		ИНАЧЕ р.РезервПереоценкиРасчет
	|	КОНЕЦ КАК РезервПереоценкиРасчет,	
	|	р.КоэффициентУскорения КАК КоэффициентУскорения,
	|	ВЫБОР
	|		КОГДА р.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.МСФО)
	|			ТОГДА р.ЛиквидационнаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЛиквидационнаяСтоимость,
	|	р.НачислятьАмортизацию КАК НачислятьАмортизацию,
	|	ВЫБОР
	|		КОГДА р.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.МСФО)
	|			ТОГДА р.ОбъемПродукцииРабот
	|		ИНАЧЕ р.ОбъемПродукцииРабот
	|	КОНЕЦ КАК ОбъемПродукцииРабот,
	|	р.ПараметрВыработки КАК ПараметрВыработки,	
	|	р.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
	|	р.ПрофильРаспределения КАК СпособОтраженияРасходовПоАмортизации,
	|	ВЫБОР
	|		КОГДА р.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.НСБУ)
	|			ТОГДА р.СПИ
	|		ИНАЧЕ р.СПИ
	|	КОНЕЦ КАК СрокПолезногоИспользования,
	|	ВЫБОР
	|		КОГДА р.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.НСБУ)
	|			ТОГДА р.СПИ - РАЗНОСТЬДАТ(р.ДатаВвода, &Период, МЕСЯЦ)
	|		КОГДА &АлгоритмРСБУ
	|			ТОГДА р.СПИ - РАЗНОСТЬДАТ(р.ДатаВвода, &Период, МЕСЯЦ)
	|		ИНАЧЕ ВЫРАЗИТЬ(р.СПИ - РАЗНОСТЬДАТ(р.ДатаВвода, &Период, ДЕНЬ) * 12 / 365 КАК ЧИСЛО(18, 3))
	|	КОНЕЦ КАК СрокИспользованияДляВычисленияАмортизации,
	|	ВЫБОР
	|		КОГДА р.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.НСБУ)
	|			ТОГДА р.ПервоначальнаяСтоимость - р.Амортизация - р.АмортизацияРасчет - т.ВыбытиеСтоимостиМСФО
	|		КОГДА &АлгоритмРСБУ
	|			ТОГДА р.ПервоначальнаяСтоимость + р.Переоценка + р.Обесценение - р.ЛиквидационнаяСтоимость - р.Амортизация - т.ВыбытиеСтоимостиМСФО
	|		ИНАЧЕ р.ПервоначальнаяСтоимость + р.Переоценка + р.Обесценение - р.ЛиквидационнаяСтоимость - р.Амортизация - р.АмортизацияРасчет - т.ВыбытиеСтоимостиМСФО
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизации,
	|	р.СчетУчетаПервоначальнойСтоимости КАК СчетУчетаПервоначальнойСтоимости,
	|	р.СчетУчетаПервоначальнойСтоимостиСубконто1 КАК СчетУчетаПервоначальнойСтоимостиСубконто1,
	|	р.СчетУчетаПервоначальнойСтоимостиСубконто2 КАК СчетУчетаПервоначальнойСтоимостиСубконто2,
	|	р.СчетУчетаПервоначальнойСтоимостиСубконто3 КАК СчетУчетаПервоначальнойСтоимостиСубконто3,
	|	р.СчетНакопленнойАмортизации КАК СчетНакопленнойАмортизации,
	|	р.СчетНакопленнойАмортизацииСубконто1 КАК СчетНакопленнойАмортизацииСубконто1,
	|	р.СчетНакопленнойАмортизацииСубконто2 КАК СчетНакопленнойАмортизацииСубконто2,
	|	р.СчетНакопленнойАмортизацииСубконто3 КАК СчетНакопленнойАмортизацииСубконто3
	|ПОМЕСТИТЬ втТаблицаВНА
	|ИЗ
	|	втТаблицаВыбытияВНА КАК т
	|		ЛЕВОЕ СОЕДИНЕНИЕ втТаблицаРасчетВНА КАК р
	|		ПО т.ВНА = р.ВНА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИспользоватьСчетВыбытияОстаточнойСтоимости";

КонецФункции

Функция ТекстЗапроса_ТаблицаВНА(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаВНА", НомераТаблиц.Количество());	
	Возврат
	
	"ВЫБРАТЬ
	|	втТаблицаВНА.Период КАК Период,
	|	втТаблицаВНА.ВНА КАК ВНА,
	|	втТаблицаВНА.ГруппаВНА КАК ГруппаВНА,
	|	втТаблицаВНА.ВидУчета КАК ВидУчета,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций.НачислениеАмортизации) КАК ВидОперации,
	|	втТаблицаВНА.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	втТаблицаВНА.КоэффициентУскорения КАК КоэффициентУскорения,
	|	втТаблицаВНА.НачислятьАмортизацию КАК НачислятьАмортизацию,
	|	втТаблицаВНА.ОбъемПродукцииРабот КАК ОбъемПродукцииРабот,
	|	втТаблицаВНА.ПараметрВыработки КАК ПараметрВыработки,
	|	втТаблицаВНА.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
	|	втТаблицаВНА.ПрофильРаспределения КАК СпособОтраженияРасходовПоАмортизации,
	|	втТаблицаВНА.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	втТаблицаВНА.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	втТаблицаВНА.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользованияДляВычисленияАмортизации,
	|	0 КАК СуммаВВалютеУчета,
	|	втТаблицаВНА.АмортизацияРасчет КАК Амортизация,
	|	0 КАК Переоценка,
	|	0 КАК Обесценение,
	|	-втТаблицаВНА.РезервПереоценкиРасчет КАК РезервПереоценки
	|ИЗ
	|	втТаблицаВНА КАК втТаблицаВНА
	|ГДЕ
	|	втТаблицаВНА.ПервоначальнаяСтоимость <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втТаблицаВНА.Период,
	|	втТаблицаВНА.ВНА,
	|	втТаблицаВНА.ГруппаВНА,
	|	втТаблицаВНА.ВидУчета,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ВыбытиеВНА),
	|	втТаблицаВНА.ЛиквидационнаяСтоимость,
	|	втТаблицаВНА.КоэффициентУскорения,
	|	ЛОЖЬ,
	|	втТаблицаВНА.ОбъемПродукцииРабот,
	|	втТаблицаВНА.ПараметрВыработки,
	|	втТаблицаВНА.СпособНачисленияАмортизации,
	|	втТаблицаВНА.ПрофильРаспределения,
	|	втТаблицаВНА.СтоимостьДляВычисленияАмортизации,
	|	втТаблицаВНА.СрокПолезногоИспользования,
	|	втТаблицаВНА.СрокИспользованияДляВычисленияАмортизации,
	|	-втТаблицаВНА.ПервоначальнаяСтоимость,
	|	-втТаблицаВНА.НакопленнаяАмортизация,
	|	-втТаблицаВНА.Переоценка,
	|	-втТаблицаВНА.Обесценение,
	|	-втТаблицаВНА.РезервПереоценки
	|ИЗ
	|	втТаблицаВНА КАК втТаблицаВНА
	|ГДЕ
	|	втТаблицаВНА.ПервоначальнаяСтоимость <> 0";
	
КонецФункции

Функция ТекстЗапроса_ТаблицаОшибок(НомераТаблиц)

	ТекстЗапроса = Новый Массив;
	ТекстЗапроса.Добавить(МСФОВНАУХ.ТекстЗапроса_ПроверкаНаБалансе(НомераТаблиц));
	ТекстЗапроса.Добавить(МСФОВНАУХ.ТекстЗапроса_ПроверкаДублейВНА());	
	
	Возврат СтрСоединить(ТекстЗапроса, " ОБЪЕДИНИТЬ ВСЕ ");

КонецФункции

#Область ПроводкиЗапрос

Функция ТекстЗапроса_ТаблицаПроводкиВНА(НомераТаблиц)
	
	НомераТаблиц.Вставить("ТаблицаПроводкиВНА", НомераТаблиц.Количество());
	
	Возврат 
	МСФОВНАУХ.ТекстЗапроса_НачислениеАмортизацииМСФО(, "втТаблицаВНА.Период") 
	+ " ОБЪЕДИНИТЬ ВСЕ " 
	+ МСФОВНАУХ.ТекстЗапроса_НачислениеРезерваПереоценкиМСФО("втТаблицаВНА.Период")
	+ " ОБЪЕДИНИТЬ ВСЕ " 
	+ ТекстЗапроса_ВыбытиеСписаниеПервоначальнойСтоимости()
	+ " ОБЪЕДИНИТЬ ВСЕ " 
	+ ТекстЗапроса_ВыбытиеСписаниеНакопленнойАмортизации()
	+ " ОБЪЕДИНИТЬ ВСЕ "
	+ ТекстЗапроса_ВыбытиеПереоценки()
	+ " ОБЪЕДИНИТЬ ВСЕ "
	+ ТекстЗапроса_ВыбытиеОбесценения()
	+ " ОБЪЕДИНИТЬ ВСЕ "	
	+ ТекстЗапроса_ВыбытиеСписаниеОстаточнойСтоимости()
	+ " ОБЪЕДИНИТЬ ВСЕ " 
	+ ТекстЗапроса_ВыбытиеРезерваПереоценки()	
	;

КонецФункции

Функция ТекстЗапроса_ВыбытиеСписаниеПервоначальнойСтоимости()
	
	Возврат
	"ВЫБРАТЬ
	|	втТаблицаВНА.Период КАК Период,
	|	втТаблицаВНА.ВНА КАК ВНА,
	|	втТаблицаВНА.ГруппаВНА КАК ГруппаВНА,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ВыбытиеВНА) КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.НСБУ)
	|			ТОГДА втТаблицаВНА.ПервоначальнаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаОперацииНСБУ,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.МСФО)
	|			ТОГДА втТаблицаВНА.ПервоначальнаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаОперацииМСФО,
	|	втТаблицаВНА.ПодразделениеДт КАК ПодразделениеДт, 
	|	втТаблицаВНА.НаправлениеДеятельностиДт КАК НаправлениеДеятельностиДт, 
	|	втТаблицаВНА.ПодразделениеКт КАК ПодразделениеКт, 
	|	втТаблицаВНА.НаправлениеДеятельностиКт КАК НаправлениеДеятельностиКт,	
	|	втТаблицаВНА.СчетВыбытияВНА КАК СчетДт,
	|	втТаблицаВНА.СчетВыбытияВНА.СчетСсылка КАК СчетДтМСФО,
	|	втТаблицаВНА.СчетВыбытияВНАСубконто1 КАК СубконтоДт1,
	|	втТаблицаВНА.СчетВыбытияВНАСубконто2 КАК СубконтоДт2,
	|	втТаблицаВНА.СчетВыбытияВНАСубконто3 КАК СубконтоДт3,
	|	втТаблицаВНА.СчетУчетаПервоначальнойСтоимости КАК СчетКт,
	|	втТаблицаВНА.СчетУчетаПервоначальнойСтоимости.СчетСсылка КАК СчетКтМСФО,
	|	втТаблицаВНА.СчетУчетаПервоначальнойСтоимостиСубконто1 КАК СубконтоКт1,
	|	втТаблицаВНА.СчетУчетаПервоначальнойСтоимостиСубконто2 КАК СубконтоКт2,
	|	втТаблицаВНА.СчетУчетаПервоначальнойСтоимостиСубконто3 КАК СубконтоКт3
	|ИЗ
	|	втТаблицаВНА КАК втТаблицаВНА
	|ГДЕ
	|	втТаблицаВНА.ПервоначальнаяСтоимость <> 0
	|	И втТаблицаВНА.ИспользоватьСчетВыбытияОстаточнойСтоимости
	|	И ВЫБОР
	|			КОГДА &ЕстьНСБУ
	|					И &ЕстьМСФО
	|				ТОГДА втТаблицаВНА.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.МСФО)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ";

КонецФункции

Функция ТекстЗапроса_ВыбытиеСписаниеНакопленнойАмортизации()
	
	Возврат
	"ВЫБРАТЬ
	|	втТаблицаВНА.Период КАК Период,
	|	втТаблицаВНА.ВНА КАК ВНА,
	|	втТаблицаВНА.ГруппаВНА КАК ГруппаВНА,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ВыбытиеАмортизации) КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.НСБУ)
	|				И НЕ &ФормироватьПроводкиМСФО
	|				И втТаблицаВНА.СторнироватьДанныеНСБУ
	|			ТОГДА втТаблицаВНА.НакопленнаяАмортизация
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаОперацииНСБУ,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.МСФО)
	|			ТОГДА втТаблицаВНА.НакопленнаяАмортизация
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаОперацииМСФО,
	|	втТаблицаВНА.ПодразделениеДт КАК ПодразделениеДт, 
	|	втТаблицаВНА.НаправлениеДеятельностиДт КАК НаправлениеДеятельностиДт, 
	|	втТаблицаВНА.ПодразделениеКт КАК ПодразделениеКт, 
	|	втТаблицаВНА.НаправлениеДеятельностиКт КАК НаправлениеДеятельностиКт,		
	|	втТаблицаВНА.СчетНакопленнойАмортизации КАК СчетДт,
	|	втТаблицаВНА.СчетНакопленнойАмортизации.СчетСсылка КАК СчетДтМСФО,
	|	втТаблицаВНА.СчетНакопленнойАмортизацииСубконто1 КАК СубконтоДт1,
	|	втТаблицаВНА.СчетНакопленнойАмортизацииСубконто2 КАК СубконтоДт2,
	|	втТаблицаВНА.СчетНакопленнойАмортизацииСубконто3 КАК СубконтоДт3,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ИспользоватьСчетВыбытияОстаточнойСтоимости
	|			ТОГДА втТаблицаВНА.СчетВыбытияВНА
	|		ИНАЧЕ втТаблицаВНА.СчетУчетаПервоначальнойСтоимости
	|	КОНЕЦ КАК СчетКт,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ИспользоватьСчетВыбытияОстаточнойСтоимости
	|			ТОГДА втТаблицаВНА.СчетВыбытияВНА.СчетСсылка
	|		ИНАЧЕ втТаблицаВНА.СчетУчетаПервоначальнойСтоимости.СчетСсылка
	|	КОНЕЦ КАК СчетКтМСФО,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ИспользоватьСчетВыбытияОстаточнойСтоимости
	|			ТОГДА втТаблицаВНА.СчетВыбытияВНАСубконто1
	|		ИНАЧЕ втТаблицаВНА.СчетУчетаПервоначальнойСтоимостиСубконто1
	|	КОНЕЦ КАК СубконтоКт1,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ИспользоватьСчетВыбытияОстаточнойСтоимости
	|			ТОГДА втТаблицаВНА.СчетВыбытияВНАСубконто2
	|		ИНАЧЕ втТаблицаВНА.СчетУчетаПервоначальнойСтоимостиСубконто2
	|	КОНЕЦ КАК СубконтоКт2,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ИспользоватьСчетВыбытияОстаточнойСтоимости
	|			ТОГДА втТаблицаВНА.СчетВыбытияВНАСубконто3
	|		ИНАЧЕ втТаблицаВНА.СчетУчетаПервоначальнойСтоимостиСубконто3
	|	КОНЕЦ КАК СубконтоКт3
	|ИЗ
	|	втТаблицаВНА КАК втТаблицаВНА
	|ГДЕ
	|	втТаблицаВНА.НакопленнаяАмортизация <> 0
	|	И ВЫБОР
	|			КОГДА &ЕстьНСБУ
	|					И &ЕстьМСФО
	|				ТОГДА втТаблицаВНА.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.МСФО)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ";

КонецФункции

Функция ТекстЗапроса_ВыбытиеПереоценки()
	
	Возврат
	"ВЫБРАТЬ
	|	втТаблицаВНА.Период КАК Период,
	|	втТаблицаВНА.ВНА КАК ВНА,
	|	втТаблицаВНА.ГруппаВНА КАК ГруппаВНА,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ВыбытиеВНА) КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.НСБУ)
	|			ТОГДА втТаблицаВНА.Переоценка
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаОперацииНСБУ,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.МСФО)
	|			ТОГДА втТаблицаВНА.Переоценка
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаОперацииМСФО,
	
	|	втТаблицаВНА.ПодразделениеДт КАК ПодразделениеДт, 
	|	втТаблицаВНА.НаправлениеДеятельностиДт КАК НаправлениеДеятельностиДт, 
	|	втТаблицаВНА.ПодразделениеКт КАК ПодразделениеКт, 
	|	втТаблицаВНА.НаправлениеДеятельностиКт КАК НаправлениеДеятельностиКт,		
		
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ИспользоватьСчетВыбытияОстаточнойСтоимости
	|			ТОГДА втТаблицаВНА.СчетВыбытияВНА
	|		ИНАЧЕ втТаблицаВНА.СчетУчетаПервоначальнойСтоимости
	|	КОНЕЦ КАК СчетДт,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ИспользоватьСчетВыбытияОстаточнойСтоимости
	|			ТОГДА втТаблицаВНА.СчетВыбытияВНА.СчетСсылка
	|		ИНАЧЕ втТаблицаВНА.СчетУчетаПервоначальнойСтоимости.СчетСсылка
	|	КОНЕЦ КАК СчетДтМСФО,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ИспользоватьСчетВыбытияОстаточнойСтоимости
	|			ТОГДА втТаблицаВНА.СчетВыбытияВНАСубконто1
	|		ИНАЧЕ втТаблицаВНА.СчетУчетаПервоначальнойСтоимостиСубконто1
	|	КОНЕЦ КАК СубконтоДт1,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ИспользоватьСчетВыбытияОстаточнойСтоимости
	|			ТОГДА втТаблицаВНА.СчетВыбытияВНАСубконто2
	|		ИНАЧЕ втТаблицаВНА.СчетУчетаПервоначальнойСтоимостиСубконто2
	|	КОНЕЦ КАК СубконтоДт2,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ИспользоватьСчетВыбытияОстаточнойСтоимости
	|			ТОГДА втТаблицаВНА.СчетВыбытияВНАСубконто3
	|		ИНАЧЕ втТаблицаВНА.СчетУчетаПервоначальнойСтоимостиСубконто3
	|	КОНЕЦ КАК СубконтоДт3,
	|	втТаблицаВНА.ПараметрыПереоценки.СчетНакопленнойПереоценки КАК СчетКт,
	|	втТаблицаВНА.ПараметрыПереоценки.СчетНакопленнойПереоценки.СчетСсылка КАК СчетКтМСФО,
	|	втТаблицаВНА.ПараметрыПереоценки.СчетНакопленнойПереоценкиСубконто1 КАК СубконтоКт1,
	|	втТаблицаВНА.ПараметрыПереоценки.СчетНакопленнойПереоценкиСубконто2 КАК СубконтоКт2,
	|	втТаблицаВНА.ПараметрыПереоценки.СчетНакопленнойПереоценкиСубконто3 КАК СубконтоКт3
	|ИЗ
	|	втТаблицаВНА КАК втТаблицаВНА
	|ГДЕ
	|	втТаблицаВНА.Переоценка <> 0
	|	И втТаблицаВНА.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.МСФО)
	|	И ВЫБОР
	|			КОГДА втТаблицаВНА.ИспользоватьСчетВыбытияОстаточнойСтоимости
	|				ТОГДА втТаблицаВНА.СчетВыбытияВНА
	|			ИНАЧЕ втТаблицаВНА.СчетУчетаПервоначальнойСтоимости
	|		КОНЕЦ <> втТаблицаВНА.ПараметрыПереоценки.СчетНакопленнойПереоценки";

КонецФункции

Функция ТекстЗапроса_ВыбытиеОбесценения()
	
	Возврат
	"ВЫБРАТЬ
	|	втТаблицаВНА.Период КАК Период,
	|	втТаблицаВНА.ВНА КАК ВНА,
	|	втТаблицаВНА.ГруппаВНА КАК ГруппаВНА,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ВыбытиеВНА) КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.НСБУ)
	|			ТОГДА втТаблицаВНА.Обесценение
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаОперацииНСБУ,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.МСФО)
	|			ТОГДА втТаблицаВНА.Обесценение
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаОперацииМСФО,
	|	втТаблицаВНА.ПодразделениеДт КАК ПодразделениеДт,
	|	втТаблицаВНА.НаправлениеДеятельностиДт КАК НаправлениеДеятельностиДт,
	|	втТаблицаВНА.ПодразделениеКт КАК ПодразделениеКт,
	|	втТаблицаВНА.НаправлениеДеятельностиКт КАК НаправлениеДеятельностиКт,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ИспользоватьСчетВыбытияОстаточнойСтоимости
	|			ТОГДА втТаблицаВНА.СчетВыбытияВНА
	|		ИНАЧЕ втТаблицаВНА.СчетУчетаПервоначальнойСтоимости
	|	КОНЕЦ КАК СчетДт,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ИспользоватьСчетВыбытияОстаточнойСтоимости
	|			ТОГДА втТаблицаВНА.СчетВыбытияВНА.СчетСсылка
	|		ИНАЧЕ втТаблицаВНА.СчетУчетаПервоначальнойСтоимости.СчетСсылка
	|	КОНЕЦ КАК СчетДтМСФО,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ИспользоватьСчетВыбытияОстаточнойСтоимости
	|			ТОГДА втТаблицаВНА.СчетВыбытияВНАСубконто1
	|		ИНАЧЕ втТаблицаВНА.СчетУчетаПервоначальнойСтоимостиСубконто1
	|	КОНЕЦ КАК СубконтоДт1,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ИспользоватьСчетВыбытияОстаточнойСтоимости
	|			ТОГДА втТаблицаВНА.СчетВыбытияВНАСубконто2
	|		ИНАЧЕ втТаблицаВНА.СчетУчетаПервоначальнойСтоимостиСубконто2
	|	КОНЕЦ КАК СубконтоДт2,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ИспользоватьСчетВыбытияОстаточнойСтоимости
	|			ТОГДА втТаблицаВНА.СчетВыбытияВНАСубконто3
	|		ИНАЧЕ втТаблицаВНА.СчетУчетаПервоначальнойСтоимостиСубконто3
	|	КОНЕЦ КАК СубконтоДт3,
	|	втТаблицаВНА.ПараметрыОбесценения.СчетНакопленногоОбесценения КАК СчетКт,
	|	втТаблицаВНА.ПараметрыОбесценения.СчетНакопленногоОбесценения.СчетСсылка КАК СчетКтМСФО,
	|	втТаблицаВНА.ПараметрыОбесценения.СчетНакопленногоОбесцененияСубконто1 КАК СубконтоКт1,
	|	втТаблицаВНА.ПараметрыОбесценения.СчетНакопленногоОбесцененияСубконто2 КАК СубконтоКт2,
	|	втТаблицаВНА.ПараметрыОбесценения.СчетНакопленногоОбесцененияСубконто3 КАК СубконтоКт3
	|ИЗ
	|	втТаблицаВНА КАК втТаблицаВНА
	|ГДЕ
	|	втТаблицаВНА.Обесценение <> 0
	|	И втТаблицаВНА.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.МСФО)
	|	И ВЫБОР
	|			КОГДА втТаблицаВНА.ИспользоватьСчетВыбытияОстаточнойСтоимости
	|				ТОГДА втТаблицаВНА.СчетВыбытияВНА
	|			ИНАЧЕ втТаблицаВНА.СчетУчетаПервоначальнойСтоимости
	|		КОНЕЦ <> втТаблицаВНА.ПараметрыОбесценения.СчетНакопленногоОбесценения";

КонецФункции

Функция ТекстЗапроса_ВыбытиеСписаниеОстаточнойСтоимости()
	
	Возврат
	"ВЫБРАТЬ
	|	втТаблицаВНА.Период КАК Период,
	|	втТаблицаВНА.ВНА КАК ВНА,
	|	втТаблицаВНА.ГруппаВНА КАК ГруппаВНА,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ВыбытиеВНА) КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.НСБУ)
	|			ТОГДА втТаблицаВНА.ПервоначальнаяСтоимость + втТаблицаВНА.Переоценка + втТаблицаВНА.Обесценение - втТаблицаВНА.НакопленнаяАмортизация
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаОперацииНСБУ,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.МСФО)
	|			ТОГДА втТаблицаВНА.ПервоначальнаяСтоимость + втТаблицаВНА.Переоценка + втТаблицаВНА.Обесценение - втТаблицаВНА.НакопленнаяАмортизация - втТаблицаВНА.ЧистаяИнвестицияВЛизинг
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаОперацииМСФО,
	
	|	втТаблицаВНА.ПодразделениеДт КАК ПодразделениеДт, 
	|	втТаблицаВНА.НаправлениеДеятельностиДт КАК НаправлениеДеятельностиДт, 
	|	втТаблицаВНА.ПодразделениеКт КАК ПодразделениеКт, 
	|	втТаблицаВНА.НаправлениеДеятельностиКт КАК НаправлениеДеятельностиКт,		
		
	|	втТаблицаВНА.СчетРасходаМСФО КАК СчетДт,
	|	втТаблицаВНА.СчетРасходаМСФО.СчетСсылка КАК СчетДтМСФО,
	|	втТаблицаВНА.СчетРасходаСубконто1МСФО КАК СубконтоДт1,
	|	втТаблицаВНА.СчетРасходаСубконто2МСФО КАК СубконтоДт2,
	|	втТаблицаВНА.СчетРасходаСубконто3МСФО КАК СубконтоДт3,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ИспользоватьСчетВыбытияОстаточнойСтоимости
	|			ТОГДА втТаблицаВНА.СчетВыбытияВНА
	|		ИНАЧЕ втТаблицаВНА.СчетУчетаПервоначальнойСтоимости
	|	КОНЕЦ КАК СчетКт,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ИспользоватьСчетВыбытияОстаточнойСтоимости
	|			ТОГДА втТаблицаВНА.СчетВыбытияВНА.СчетСсылка
	|		ИНАЧЕ втТаблицаВНА.СчетУчетаПервоначальнойСтоимости.СчетСсылка
	|	КОНЕЦ КАК СчетКтМСФО,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ИспользоватьСчетВыбытияОстаточнойСтоимости
	|			ТОГДА втТаблицаВНА.СчетВыбытияВНАСубконто1
	|		ИНАЧЕ втТаблицаВНА.СчетУчетаПервоначальнойСтоимостиСубконто1
	|	КОНЕЦ КАК СубконтоКт1,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ИспользоватьСчетВыбытияОстаточнойСтоимости
	|			ТОГДА втТаблицаВНА.СчетВыбытияВНАСубконто2
	|		ИНАЧЕ втТаблицаВНА.СчетУчетаПервоначальнойСтоимостиСубконто2
	|	КОНЕЦ КАК СубконтоКт2,
	|	ВЫБОР
	|		КОГДА втТаблицаВНА.ИспользоватьСчетВыбытияОстаточнойСтоимости
	|			ТОГДА втТаблицаВНА.СчетВыбытияВНАСубконто3
	|		ИНАЧЕ втТаблицаВНА.СчетУчетаПервоначальнойСтоимостиСубконто3
	|	КОНЕЦ КАК СубконтоКт3
	|ИЗ
	|	втТаблицаВНА КАК втТаблицаВНА
	|ГДЕ
	|	втТаблицаВНА.ПервоначальнаяСтоимость - втТаблицаВНА.НакопленнаяАмортизация <> 0
	|	И ВЫБОР
	|			КОГДА &ЕстьНСБУ
	|					И &ЕстьМСФО
	|				ТОГДА втТаблицаВНА.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.МСФО)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ";

КонецФункции

Функция ТекстЗапроса_ВыбытиеРезерваПереоценки()
	
	//Дт Счет резерва переоценки Кт НРП - Переоценка
	
	Возврат
	"ВЫБРАТЬ
	|	втТаблицаВНА.Период КАК Период,
	|	втТаблицаВНА.ВНА КАК ВНА,
	|	втТаблицаВНА.ГруппаВНА КАК ГруппаВНА,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПереоценкаВыбытиеПереоценки) КАК ВидОперации,
	|	0 КАК СуммаОперацииНСБУ,
	|	втТаблицаВНА.РезервПереоценки + втТаблицаВНА.РезервПереоценкиРасчет КАК СуммаОперацииМСФО,
	
	|	втТаблицаВНА.ПодразделениеДт КАК ПодразделениеДт, 
	|	втТаблицаВНА.НаправлениеДеятельностиДт КАК НаправлениеДеятельностиДт, 
	|	втТаблицаВНА.ПодразделениеКт КАК ПодразделениеКт, 
	|	втТаблицаВНА.НаправлениеДеятельностиКт КАК НаправлениеДеятельностиКт,		
		
	|	втТаблицаВНА.ПараметрыПереоценки.СчетУчетаРезерва КАК СчетДт,
	|	втТаблицаВНА.ПараметрыПереоценки.СчетУчетаРезерва.СчетСсылка КАК СчетДтМСФО,
	|	втТаблицаВНА.ПараметрыПереоценки.СчетУчетаРезерваСубконто1 КАК СубконтоДт1,
	|	втТаблицаВНА.ПараметрыПереоценки.СчетУчетаРезерваСубконто2 КАК СубконтоДт2,
	|	втТаблицаВНА.ПараметрыПереоценки.СчетУчетаРезерваСубконто3 КАК СубконтоДт3,
	|	ФиксированныеСчетаУчетаБД.Счет КАК СчетКт,
	|	ФиксированныеСчетаУчетаБД.Счет.СчетСсылка КАК СчетКтМСФО,
	|	ФиксированныеСчетаУчетаБД.Субконто1 КАК СубконтоКт1,
	|	ФиксированныеСчетаУчетаБД.Субконто2 КАК СубконтоКт2,
	|	ФиксированныеСчетаУчетаБД.Субконто3 КАК СубконтоКт3
	|ИЗ
	|	втТаблицаВНА КАК втТаблицаВНА
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФиксированныеСчетаУчетаБД КАК ФиксированныеСчетаУчетаБД
	|		ПО (ФиксированныеСчетаУчетаБД.Ссылка = ЗНАЧЕНИЕ(Справочник.ФиксированныеСчетаУчетаБД.НераспределеннаяПрибыльНепокрытыйУбытокОтчетногоПериода))
	|ГДЕ
	|	втТаблицаВНА.РезервПереоценки > 0
	|	И втТаблицаВНА.ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.МСФО)";

КонецФункции

#КонецОбласти

Процедура СформироватьДвижения(Движения, ДополнительныеСвойства, Отказ) Экспорт

	Реквизиты 				= ДополнительныеСвойства.ДляПроведения.Реквизиты;
	ТаблицаВНА 				= ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаВНА;
	ТаблицаПроводкиВНА 		= ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаПроводкиВНА;
	
	Если МСФОВНАУХ.ЕстьОшибкиПроведения(ДополнительныеСвойства, Отказ) Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиОтражения = МСФОВНАУХ.ПолучитьНастройкиОтражения();	
	НастройкиОтражения.Вставить("СтоимостьВНАМСФО", Истина);
	НастройкиОтражения.Вставить("ПараметрыАмортизацииВНАМСФО", Истина);//для частичного выбытия
	
	МСФОВНАУХ.ОтразитьДвиженияПараметровУчетаВНА(Движения, Реквизиты, ТаблицаВНА, НастройкиОтражения);
	МСФОВНАУХ.ОтразитьПроводкиВНА(Движения, Реквизиты, ТаблицаПроводкиВНА, Отказ, Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеДокумента

//КоллекцияСтрок - ТабличнаяЧасть,ТаблицаЗначений,ДанныеФормыКоллекция, СтрокаТабличнойЧасти
//Источник - ДокументОбъект, ДанныеФормыСтруктура, УправляемаяФорма
//ПараметрыЗаполнения - Структура(ЗаполнитьРеквизиты = Ложь,  ЗаполнитьНСБУ = Ложь, ЗаполнитьМСФО = Ложь)
Процедура ЗаполнитьСтроки(КоллекцияСтрок, Источник, ПараметрыЗаполненияТЧ = Неопределено) Экспорт

	ЗаполняемыеРеквизитыТаблицыФормы = МСФОУХ.ПолучитьЗаполняемыеРеквизитыТаблицыФормы(Источник);
	ОбъектИсточник = ?(ТипЗнч(Источник)= Тип("УправляемаяФорма"), Источник.Объект, Источник);
	Граница = МСФОВНАВызовСервераУХ.ПолучитьГраницуДокумента(ОбъектИсточник);
	
	ОбъектыВНА = Новый Массив;
	Для каждого ТекущаяСтрока Из КоллекцияСтрок Цикл
		ОбъектыВНА.Добавить(ТекущаяСтрока.ВНА);
	КонецЦикла;
	
	ПоляВыбора =  
	"ГруппаВНА,
	|СчетУчетаПервоначальнойСтоимости,
	|СчетУчетаПервоначальнойСтоимостиСубконто1,
	|СчетУчетаПервоначальнойСтоимостиСубконто2,
	|СчетУчетаПервоначальнойСтоимостиСубконто3";
	ВидУчетаМСФО = ПредопределенноеЗначение("Перечисление.ВидыУчета.МСФО");
	РезультатРеквизитыВНА = МСФОВНАВызовСервераУХ.ПолучитьРеквизитыВНА(Граница, ОбъектыВНА, 
								ОбъектИсточник.Организация, , ВидУчетаМСФО, Новый Структура(ПоляВыбора));
	
	Для каждого ТекущаяСтрока Из КоллекцияСтрок Цикл
		
		Если ТекущаяСтрока.ВидОперации.Пустая() Тогда
			ТекущаяСтрока.ВидОперации = ПредопределенноеЗначение("Справочник.ВидыОпераций.ВыбытиеВНА");
		КонецЕсли;
		
		РеквизитыВНА = РезультатРеквизитыВНА.МСФО.Получить(ТекущаяСтрока.ВНА);		
		
		Если ТекущаяСтрока.СчетРасходаМСФО.Пустая() И (РеквизитыВНА <> Неопределено) И РеквизитыВНА.Свойство("ГруппаВНА") Тогда
			
			ПараметрыВыбытия = РеквизитыВНА.ГруппаВНА.ПараметрыВыбытия;
			
			ТекущаяСтрока.СчетРасходаМСФО 			= ПараметрыВыбытия.СчетСписанияПриВыбытии;
			ТекущаяСтрока.СчетРасходаСубконто1МСФО 	= ПараметрыВыбытия.СчетСписанияПриВыбытииСубконто1;
			ТекущаяСтрока.СчетРасходаСубконто2МСФО 	= ПараметрыВыбытия.СчетСписанияПриВыбытииСубконто2;
			ТекущаяСтрока.СчетРасходаСубконто3МСФО 	= ПараметрыВыбытия.СчетСписанияПриВыбытииСубконто3;
			
		КонецЕсли;
		
		Если ЗаполняемыеРеквизитыТаблицыФормы <> Неопределено Тогда
			МСФОКлиентСерверУХ.ЗаполнитьРасчетныеРеквизитыСтроки(ТекущаяСтрока, ЗаполняемыеРеквизитыТаблицыФормы);			
		КонецЕсли;
	
	КонецЦикла;

КонецПроцедуры 

Процедура ЗаполнитьЗависимостиРеквизитовДокумента(ФормаДокумента) Экспорт 
	ФормаДокумента.КэшируемыеЗначения.Вставить("ЗависимостиРеквизитов", Новый Массив);	
КонецПроцедуры

Функция ПолучитьИменаСубконто() Экспорт 

	Результат = Новый Структура;
	
	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетРасходаСубконто1НСБУ");
	Субконто.Вставить(2, "СчетРасходаСубконто2НСБУ");
	Субконто.Вставить(3, "СчетРасходаСубконто3НСБУ");
	
	Результат.Вставить("СчетРасходаНСБУ", Субконто);	
	
	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетРасходаСубконто1МСФО");
	Субконто.Вставить(2, "СчетРасходаСубконто2МСФО");
	Субконто.Вставить(3, "СчетРасходаСубконто3МСФО");
	
	Результат.Вставить("СчетРасходаМСФО", Субконто);	
	
	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетВыбытияВНАСубконто1НСБУ");
	Субконто.Вставить(2, "СчетВыбытияВНАСубконто2НСБУ");
	Субконто.Вставить(3, "СчетВыбытияВНАСубконто3НСБУ");
	
	Результат.Вставить("СчетВыбытияВНАНСБУ", Субконто);
	
	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетВыбытияВНАСубконто1МСФО");
	Субконто.Вставить(2, "СчетВыбытияВНАСубконто2МСФО");
	Субконто.Вставить(3, "СчетВыбытияВНАСубконто3МСФО");
	
	Результат.Вставить("СчетВыбытияВНАМСФО", Субконто);	
	
	Результат.Вставить("ИсключенияСкрытияСубконто", Новый Структура("СчетРасходаНСБУ,СчетРасходаМСФО"));
	
	Возврат Результат;	
	
КонецФункции

#КонецОбласти

#КонецЕсли