#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ДанныеЗаполнения <> Неопределено И ТипДанныхЗаполнения <> Тип("Структура")
		И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
		
		ЗаполнитьПоДокументуОснованию(ДанныеЗаполнения);
		
	Иначе
		
		Если ДанныеЗаполнения <> Неопределено И ТипДанныхЗаполнения = Тип("Структура") И
			 ДанныеЗаполнения.Свойство("Владелец") 
			 И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Владелец.Метаданные()) Тогда
		
			ЗаполнитьПоДокументуОснованию(ДанныеЗаполнения);
		
		КонецЕсли;
	КонецЕсли;
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата              = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный     = Пользователи.ТекущийПользователь();
	ДокументОснование = Неопределено;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоДокументуОснованию(Основание)

	// Заполним реквизиты из стандартного набора по документу основанию.
	ВстраиваниеУХ.ЗаполнитьПоОснованию(ЭтотОбъект, Основание);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Основание, , "Номер, Дата, Ответственный, ДокументОснование");
	
	ДокументОснование = Основание;
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.СтрокаПланаЗакупок") Тогда
		Организация = Основание.ОрганизаторЗакупки;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	//при закупке у единственного поставщика группа реквизитов для ЕП обязательны к заполнению
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		ТекСпособЗакупки = ДокументОснование.СпособВыбораПоставщика;
		Если ТекСпособЗакупки = Перечисления.СпособыВыбораПоставщика.ЗакупкаУЕдинственногоПоставщика Тогда
			ПроверяемыеРеквизиты.Добавить("ОбоснованиеСпособаЗакупкиЕП");
			ПроверяемыеРеквизиты.Добавить("ОбоснованиеВыбораПоставщикаЗакупкаЕП");
			ПроверяемыеРеквизиты.Добавить("ЕдинственныйПоставщик");
		КонецЕсли;
	Иначе
		// Документ основание не заполнен. Способ закупки определить нельзя, ограничения не накладываем.
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли