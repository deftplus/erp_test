Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	РаботаСДоговорамиКонтрагентовУХКлиентСервер.ОбработкаПолученияПредставленияВерсияСоглашения(Данные, Представление, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	РаботаСДоговорамиКонтрагентовУХКлиентСервер.ОбработкаПолученияПолейПредставленияВерсияСоглашения(Поля, СтандартнаяОбработка);
	
КонецПроцедуры

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ЗаполнитьГрафикРасчетов(Объект) Экспорт
	
	Объект.ГрафикРасчетов.Очистить(); 
	
	ОперацииГрафика = РаботаСДоговорамиКонтрагентовУХ.ОперацииГрафика(,Объект);
		
	// Передача 1-ой валюты контрагенту в дату начала
	// - БДС
	Операция = Справочники.ОперацииГрафиковДоговоров.ВалютныйСвоп_ПлатежКонтрагенту;
	ОписаниеОперации = ОперацииГрафика[Операция];
	ОписаниеОперации.Валюта = Объект.ПерваяВалюта;
	ОписаниеОперации.СчетОрганизации = Объект.СчетОрганизацииВПервойВалюте;
	ОписаниеОперации.СчетКонтрагента = Объект.СчетКонтрагентаВПервойВалюте;
	РаботаСДоговорамиКонтрагентовУХ.ДобавитьПозициюВГрафик(Объект.ГрафикРасчетов, Объект.СуммаПервоначальногоПлатежаВПервойВалюте, Объект.ДатаПервоначальногоПлатежа,,ОписаниеОперации);
	
	// - БДР
	Операция = Справочники.ОперацииГрафиковДоговоров.ВалютныйСвоп_НачислениеРасхода;
	ОписаниеОперации = ОперацииГрафика[Операция];
	ОписаниеОперации.Валюта = Объект.ПерваяВалюта;
	ОписаниеОперации.СчетОрганизации = Объект.СчетОрганизацииВПервойВалюте;
	ОписаниеОперации.СчетКонтрагента = Объект.СчетКонтрагентаВПервойВалюте;
	РаботаСДоговорамиКонтрагентовУХ.ДобавитьПозициюВГрафик(Объект.ГрафикРасчетов, Объект.СуммаПервоначальногоПлатежаВПервойВалюте, Объект.ДатаПервоначальногоПлатежа,,ОписаниеОперации);
	
	// Поступление 2-ой валюты от контрагента в дату начала
	// + БДС
	Операция = Справочники.ОперацииГрафиковДоговоров.ВалютныйСвоп_ПоступлениеОтКонтрагента;
	ОписаниеОперации = ОперацииГрафика[Операция];
	ОписаниеОперации.Валюта = Объект.ВтораяВалюта;
	ОписаниеОперации.СчетОрганизации = Объект.СчетОрганизацииВоВторойВалюте;
	ОписаниеОперации.СчетКонтрагента = Объект.СчетКонтрагентаВоВторойВалюте;
	РаботаСДоговорамиКонтрагентовУХ.ДобавитьПозициюВГрафик(Объект.ГрафикРасчетов, Объект.СуммаПервоначальногоПлатежаВоВторойВалюте, Объект.ДатаПервоначальногоПлатежа,,ОписаниеОперации);
	
	// + БДР
	Операция = Справочники.ОперацииГрафиковДоговоров.ВалютныйСвоп_НачислениеДохода;
	ОписаниеОперации = ОперацииГрафика[Операция];
	ОписаниеОперации.Валюта = Объект.ВтораяВалюта;
	ОписаниеОперации.СчетОрганизации = Объект.СчетОрганизацииВоВторойВалюте;
	ОписаниеОперации.СчетКонтрагента = Объект.СчетКонтрагентаВоВторойВалюте;
	РаботаСДоговорамиКонтрагентовУХ.ДобавитьПозициюВГрафик(Объект.ГрафикРасчетов, Объект.СуммаПервоначальногоПлатежаВоВторойВалюте, Объект.ДатаПервоначальногоПлатежа,,ОписаниеОперации);
	
	// Возврат контрагентом 1-ой валюты в дату окончания
	// + БДС
	Операция = Справочники.ОперацииГрафиковДоговоров.ВалютныйСвоп_ПоступлениеОтКонтрагента;
	ОписаниеОперации = ОперацииГрафика[Операция];
	ОписаниеОперации.Валюта = Объект.ПерваяВалюта;
	ОписаниеОперации.СчетОрганизации = Объект.СчетОрганизацииВПервойВалюте;
	ОписаниеОперации.СчетКонтрагента = Объект.СчетКонтрагентаВПервойВалюте;	
	РаботаСДоговорамиКонтрагентовУХ.ДобавитьПозициюВГрафик(Объект.ГрафикРасчетов, Объект.СуммаОкончательногоПлатежаВПервойВалюте, Объект.ДатаОкончательногоПлатежа,,ОписаниеОперации);
	
	// + БДР
	Операция = Справочники.ОперацииГрафиковДоговоров.ВалютныйСвоп_НачислениеДохода;
	ОписаниеОперации = ОперацииГрафика[Операция];
	ОписаниеОперации.Валюта = Объект.ПерваяВалюта;
	ОписаниеОперации.СчетОрганизации = Объект.СчетОрганизацииВПервойВалюте;
	ОписаниеОперации.СчетКонтрагента = Объект.СчетКонтрагентаВПервойВалюте;	
	РаботаСДоговорамиКонтрагентовУХ.ДобавитьПозициюВГрафик(Объект.ГрафикРасчетов, Объект.СуммаОкончательногоПлатежаВПервойВалюте, Объект.ДатаОкончательногоПлатежа,,ОписаниеОперации);
	
	// Возврат контрагенту 2-ой валюты в дату окончания
	// - БДС
	Операция = Справочники.ОперацииГрафиковДоговоров.ВалютныйСвоп_ПлатежКонтрагенту;
	ОписаниеОперации = ОперацииГрафика[Операция];
	ОписаниеОперации.Валюта = Объект.ВтораяВалюта;
	ОписаниеОперации.СчетОрганизации = Объект.СчетОрганизацииВоВторойВалюте;
	ОписаниеОперации.СчетКонтрагента = Объект.СчетКонтрагентаВоВторойВалюте;	
	РаботаСДоговорамиКонтрагентовУХ.ДобавитьПозициюВГрафик(Объект.ГрафикРасчетов, Объект.СуммаОкончательногоПлатежаВоВторойВалюте, Объект.ДатаОкончательногоПлатежа,,ОписаниеОперации);
	
	// - БДР
	Операция = Справочники.ОперацииГрафиковДоговоров.ВалютныйСвоп_НачислениеРасхода;
	ОписаниеОперации = ОперацииГрафика[Операция];
	ОписаниеОперации.Валюта = Объект.ВтораяВалюта;
	ОписаниеОперации.СчетОрганизации = Объект.СчетОрганизацииВоВторойВалюте;
	ОписаниеОперации.СчетКонтрагента = Объект.СчетКонтрагентаВоВторойВалюте;	
	РаботаСДоговорамиКонтрагентовУХ.ДобавитьПозициюВГрафик(Объект.ГрафикРасчетов, Объект.СуммаОкончательногоПлатежаВоВторойВалюте, Объект.ДатаОкончательногоПлатежа,,ОписаниеОперации);
	
КонецПроцедуры

Процедура ДополнитьПереченьОперацийГрафиков(ОперацииГрафика, ОписаниеГрафика, ДокументОбъект) Экспорт
	РаботаСДоговорамиКонтрагентовУХ.ДополнитьПереченьОперацийГрафиков(ОперацииГрафика, ОписаниеГрафика, ДокументОбъект);				
КонецПроцедуры

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	// реквизиты документа.
	Запрос = Новый Запрос;
	РаботаСДоговорамиКонтрагентовУХ.ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	Параметры = Запрос.Параметры;
	ВыполнятьБюджетирование = РаботаСДоговорамиКонтрагентовУХ.ВыполнятьБюджетирование(Параметры.РежимИспользованияГрафика);
	
	// Если в справочнике периоды нет требуемых элементов, то прекращаем собирать данные
	Если ВыполнятьБюджетирование Тогда
		Если НЕ КонтрольЛимитовУХ.ПодготовитьТаблицуДатИПериодов(Запрос, ДокументСсылка, ДополнительныеСвойства) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ТекстыЗапроса = Новый СписокЗначений;
	РаботаСДоговорамиКонтрагентовУХ.ДобавитьТекстЗапросаВерсииРасчетов(ТекстыЗапроса, ИмяТаблицыДокумента());
	РаботаСДоговорамиКонтрагентовУХ.ДобавитьТекстЗапросаРасчетыСКонтрагентамиГрафики(ТекстыЗапроса, ИмяТаблицыДокумента());
	
	Если ВыполнятьБюджетирование Тогда
		РаботаСДоговорамиКонтрагентовУХ.ДобавитьТекстЗапросаОперативныйПланПоБюджетам(ТекстыЗапроса, ИмяТаблицыДокумента());
		РаботаСДоговорамиКонтрагентовУХ.ДобавитьТекстЗапросаВТ_ТаблицаПлановССуммамиЛимитирования(ТекстыЗапроса);
	КонецЕсли;
	
	ПроведениеСерверУХ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
	Если ВыполнятьБюджетирование Тогда
		РаботаСДоговорамиКонтрагентовУХ.ПодготовитьТаблицуЛимитовПоБюджетам(Запрос, ДополнительныеСвойства.ТаблицыДляДвижений);
	КонецЕсли;	
	
КонецПроцедуры

Функция ВычисляемыеРеквизитыДоговора(ВерсияСоглашения) Экспорт
	
	Результат = Новый Структура;
	
	РаботаСДоговорамиКонтрагентовУХ.ЗаполнитьВычисляемыеРеквизитыДоговора(Результат, ВерсияСоглашения);
	
	Если ВерсияСоглашения.ВидСделки = ПредопределенноеЗначение("Перечисление.ВидыВалютныхСвопов.КупилПродал") Тогда
		
		Результат.Вставить("ВалютаВзаиморасчетов", ВерсияСоглашения.ВтораяВалюта);
		Результат.Вставить("Сумма", ВерсияСоглашения.СуммаПервоначальногоПлатежаВоВторойВалюте);
	Иначе
		
		Результат.Вставить("ВалютаВзаиморасчетов", ВерсияСоглашения.ПерваяВалюта);
		Результат.Вставить("Сумма", ВерсияСоглашения.СуммаПервоначальногоПлатежаВПервойВалюте);
		
	КонецЕсли;
	
	Результат.Вставить("ДатаНачалаДействия", ВерсияСоглашения.ДатаПервоначальногоПлатежа);
	Результат.Вставить("ЗапретПлатежаВПрочихВалютах", Истина);
	Результат.Вставить("Обеспеченность", Перечисления.ОбеспеченностьДоговоров.НеОбеспечен);
	Результат.Вставить("СпособФормированияПлатежей", Перечисления.СпособыФормированияПлатежейПоДоговору.ПоГрафикуПлатежей);
	
	ДоговорыКонтрагентовВстраиваниеУХ.ЗаполнитьВычисляемыеРеквизитыДоговора(Результат, ВерсияСоглашения);
	
	Возврат Результат;
	
КонецФункции

Функция ОписаниеТабличныхЧастейДокумента() Экспорт
	
	ОписаниеТабличныхЧастей = РаботаСДоговорамиКонтрагентовУХ.НовыйОписаниеТабличныхЧастейДокумента();
	РаботаСДоговорамиКонтрагентовУХ.ДобавитьОписаниеТабличнойЧасти(ОписаниеТабличныхЧастей, "ГрафикРасчетов", Ложь);
	РаботаСДоговорамиКонтрагентовУХ.ДобавитьОписаниеТабличнойЧасти(ОписаниеТабличныхЧастей, "Проценты", Ложь);
	РаботаСДоговорамиКонтрагентовУХ.ДобавитьОписаниеТабличнойЧасти(ОписаниеТабличныхЧастей, "ПроизводственныеКалендари", Истина);
	РаботаСДоговорамиКонтрагентовУХ.ДобавитьОписаниеТабличнойЧасти(ОписаниеТабличныхЧастей, "ПараметрыОпераций", Истина);
	
	Возврат ОписаниеТабличныхЧастей;
КонецФункции

Функция ИменаКлючевыхРеквизитов() Экспорт
	
	Результат = РаботаСДоговорамиКонтрагентовУХКлиентСервер.НовыйИменаКлючевыхРеквизитов();
	Результат.ДатаНачалаДействия = "ДатаПервоначальногоПлатежа";
	Результат.ДатаОкончанияДействия = "ДатаОкончательногоПлатежа";
	Результат.Сумма = "СуммаПервоначальногоПлатежаВоВторойВалюте";
	
	Возврат Результат;
	
КонецФункции


#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Партнер)
	|	И ЗначениеРазрешено(Подразделение)";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИмяТаблицыДокумента()
	Возврат "Документ.ВерсияСоглашенияВалютныйСвоп";
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область КонтрольНаличияПериодов
	
// Функция возвращает текст запроса, который формирует временную таблицу запроса ВТ_ДатыОпераций с колонками
//   Дата 		- Дата - Дата операции по документу
//	 ВидБюджета - ПеречислениеСсылка.ПредназначенияЭлементовСтруктурыОтчета - Вид бюджета, по которому будет операция
// Текст запроса уникален для каждого вида документа
//
Функция ТексЗапроса_ДатыОперацийДокумента() экспорт
	
	Возврат РаботаСДоговорамиКонтрагентовУХ.ТекстЗапросаДатыОперацийДокумента(ИмяТаблицыДокумента());
	
КонецФункции

#КонецОбласти 

#Область ИсточникиДляКонтроляДокумента

Функция ИсточникиДокумента(Объект, ДопПараметры = неопределено) Экспорт
	
	Источники = Новый Соответствие;
	
	// 1. Источник для контроля бюджетных лимитов и резервов
	КонтрольУХ.ДобавитьИсточник(
		Источники, 
		КонтрольУХБюджетныеЛимиты, 
		Источник_БюджетныеЛимитыРезервы(Объект, ДопПараметры));
		
	//// 2. Планы по взаиморасчетам с контрагентом
	//КонтрольУХ.ДобавитьИсточник(
	//	Источники, 
	//	КонтрольУХВзаиморасчетыПоКонтрагенту, 
	//	Источник_ВзаиморасчетыПоКонтрагенту(Объект));
		
	Возврат Источники;
	
КонецФункции

Функция Источник_БюджетныеЛимитыРезервы(Объект, ДопПараметры) Экспорт
	
	// Доп.параметры
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Дата", 				Объект.Дата);
	ДопПараметры.Вставить("Ссылка", 			Объект.Ссылка);
	ДопПараметры.Вставить("ЭтоНовый", 			НЕ ЗначениеЗаполнено(Объект.Ссылка));
	ДопПараметры.Вставить("ДатаКонтроляЛимита", Объект.Дата);
	
	Если Объект.РежимИспользованияГрафика = Перечисления.РежимыИспользованияГрафика.КонтрольЛимитовИСозданиеПозиций Тогда
		ДанныеДляПроверки = ПланыДокумента(Объект);
	Иначе
		ДанныеДляПроверки = КонтрольЛимитовУХ.Новый_ТаблицаПланыДокумента();
	КонецЕсли;
	
	//
	ДанныеДляПроверки.Колонки.Сумма.Имя = "Заявлено";
	ДанныеДляПроверки.Колонки.Добавить("Лимит", РаботаСКурсамиВалют.ОписаниеТипаДенежногоПоля());
	ДанныеДляПроверки.Колонки.Добавить("Зарезервировано", РаботаСКурсамиВалют.ОписаниеТипаДенежногоПоля());
	ДанныеДляПроверки.Колонки.Добавить("Исполнено", РаботаСКурсамиВалют.ОписаниеТипаДенежногоПоля());
	ДанныеДляПроверки.ЗаполнитьЗначения(Объект.Дата, "Период");
	
	//
	Источник = Новый Структура;
	Источник.Вставить("Параметры",		ДопПараметры);
	Источник.Вставить("ПланыДокумента", ДанныеДляПроверки);
	
	Возврат Источник;
	
КонецФункции

Функция Источник_ВзаиморасчетыПоКонтрагенту(Объект)
	
	// Если незаполнен контрагент, то контроль не выполняется
	Если Не ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Возврат неопределено;
	КонецЕсли;
	
	// Если роль определить не удалось, то контроль не выполняется
	РольКонтрагента = РольПоВидуДоговораУХ()[Объект.ВидДоговораУХ];
	Если РольКонтрагента = неопределено Тогда
		Возврат неопределено;
	КонецЕсли;
	
	//
	Результат = Новый Структура;
	Результат.Вставить("Заявка", Объект.Ссылка);
	Результат.Вставить("Контрагент", Объект.Контрагент);
	Результат.Вставить("СуммаПлатежа", Объект.Сумма);
	Результат.Вставить("Валюта", Объект.ВалютаВзаиморасчетов);
	Результат.Вставить("РольКонтрагента", РольКонтрагента);
	
	Возврат Результат;
	
КонецФункции

Функция РольПоВидуДоговораУХ()
	
	Результат = Новый Соответствие;
	
	Результат.Вставить(Справочники.ВидыДоговоровКонтрагентовУХ.СПокупателем, Перечисления.РолиКонтрагентов.Покупатели);
	Результат.Вставить(Справочники.ВидыДоговоровКонтрагентовУХ.СПоставщиком, Перечисления.РолиКонтрагентов.Поставщики);
	Результат.Вставить(Справочники.ВидыДоговоровКонтрагентовУХ.ВвозИзЕАЭС, Перечисления.РолиКонтрагентов.Поставщики);
	Результат.Вставить(Справочники.ВидыДоговоровКонтрагентовУХ.Импорт, Перечисления.РолиКонтрагентов.Поставщики);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти 

#Область ПланыДокумента

// Процедура возвращает таблицу планов документа
//
// Параметры:
//  Источник  - ДокументСсылка, ДокументОбъект, Форма.Объект - Документ планирования
//
// Возвращаемое значение:
//   ТаблицаЗначений   - Таблица планов документа
//
Функция ПланыДокумента(Источник) Экспорт
	
	//
	ТаблицаПланов = КонтрольЛимитовУХ.Новый_ТаблицаПланыДокумента();
	Если ТипЗнч(Источник) = ТипЗнч(ПустаяСсылка()) Тогда
		ДанныеОбъекта = ДанныеОбъектаПоСсылке(Источник);
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ВерсияСоглашенияВалютныйСвоп") Тогда
		ДанныеОбъекта = Источник;
	ИначеЕсли ТипЗнч(Источник) = Тип("ДанныеФормыСтруктура") И Источник.Свойство("Ссылка") И ТипЗнч(Источник.Ссылка) = ТипЗнч(ПустаяСсылка()) Тогда
		ДанныеОбъекта = Источник;
	Иначе
		Возврат ТаблицаПланов;
	КонецЕсли;
	
	//
	ПредопределенныеДанные = Новый Структура(
		"Период, Организация, Контрагент, ДоговорКонтрагента", 
		ДанныеОбъекта.Дата, ДанныеОбъекта.Организация, ДанныеОбъекта.Контрагент, ДанныеОбъекта.ДоговорКонтрагента
	);
	
	//
	РеквизитыСтроки = "СтатьяБюджета, Валюта, ЦФО, Проект,"+АналитикиСтатейБюджетовУХКлиентСервер.РеквизитыАналитикИзШаблона("Аналитика%1");
	Для Каждого Строка Из ДанныеОбъекта.ГрафикРасчетов Цикл
		
		Если Строка.Сумма = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Предназначение = ПланыВидовХарактеристик.ВидыБюджетов.Предназначение(Строка.ВидБюджета);
		ПараметрыОперПланирования = ОперативноеПланированиеПовтИспУХ.ПолучитьПараметрыОперПланирования(Предназначение, Строка.Дата);
		
		СтрокаПлана = ТаблицаПланов.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПлана, ПредопределенныеДанные);
		ЗаполнитьЗначенияСвойств(СтрокаПлана, Строка, РеквизитыСтроки);
		
		СтрокаПлана.Сумма = Строка.Сумма;
		СтрокаПлана.ПериодОтчета =  ОбщегоНазначенияУХ.глОтносительныйПериодПоДате(Строка.Дата, ПараметрыОперПланирования.ПериодичностьОперативногоПланирования,0);
		СтрокаПлана.ВидБюджета = Строка.ВидБюджета;
		СтрокаПлана.Предназначение = Предназначение;
		СтрокаПлана.ПриходРасход = Строка.НаправлениеДвижения;
		
		Если НЕ ЗначениеЗаполнено(Строка.ЦФО) Тогда
			СтрокаПлана.ЦФО = ДанныеОбъекта.ОсновнойЦФО;
			СтрокаПлана.Проект = ДанныеОбъекта.ОсновнойПроект;
		КонецЕсли;
		
		Если Строка.ВидБюджета = ПланыВидовХарактеристик.ВидыБюджетов.БюджетДвиженияДенежныхСредств Тогда
			СтрокаПлана.ДокументРезервирования = ДанныеОбъекта.ИсточникЛимитовБДДС;
		Иначе
			СтрокаПлана.ДокументРезервирования = ДанныеОбъекта.ИсточникЛимитовИсполнение;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаПланов;
	
КонецФункции

Функция ДанныеОбъектаПоСсылке(Источник)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Источник);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВерсияСоглашенияКоммерческийДоговор.Ссылка КАК Ссылка,
	|	ВерсияСоглашенияКоммерческийДоговор.Дата КАК Дата,
	|	ВерсияСоглашенияКоммерческийДоговор.Организация КАК Организация,
	|	ВерсияСоглашенияКоммерческийДоговор.Контрагент КАК Контрагент,
	|	ВерсияСоглашенияКоммерческийДоговор.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВерсияСоглашенияКоммерческийДоговор.ОсновнойЦФО КАК ОсновнойЦФО,
	|	ВерсияСоглашенияКоммерческийДоговор.ОсновнойПроект КАК ОсновнойПроект,
	|	ВерсияСоглашенияКоммерческийДоговор.ИсточникЛимитовБДДС КАК ИсточникЛимитовБДДС,
	|	ВерсияСоглашенияКоммерческийДоговор.ИсточникЛимитовИсполнение КАК ИсточникЛимитовИсполнение,
	|	ВерсияСоглашенияКоммерческийДоговор.ГрафикРасчетов.(
	|		Дата КАК Дата,
	|		Валюта КАК Валюта,
	|		ВидБюджета КАК ВидБюджета,
	|		СтатьяБюджета КАК СтатьяБюджета,
	|		Аналитика1 КАК Аналитика1,
	|		Аналитика2 КАК Аналитика2,
	|		Аналитика3 КАК Аналитика3,
	|		Аналитика4 КАК Аналитика4,
	|		Аналитика5 КАК Аналитика5,
	|		Аналитика6 КАК Аналитика6,
	|		ЦФО КАК ЦФО,
	|		Проект КАК Проект,
	|		Сумма КАК Сумма,
	|		НаправлениеДвижения КАК НаправлениеДвижения
	|	) КАК ГрафикРасчетов
	|ИЗ
	|	Документ.ВерсияСоглашенияКоммерческийДоговор КАК ВерсияСоглашенияКоммерческийДоговор
	|ГДЕ
	|	ВерсияСоглашенияКоммерческийДоговор.Ссылка = &Ссылка";
	РезультатЗапроса = Запрос.Выполнить();
	
	Результат = Новый Структура;
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		Результат.Вставить(Колонка.Имя);
	КонецЦикла;
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка,, "ГрафикРасчетов");
		Результат.ГрафикРасчетов = Выборка.ГрафикРасчетов.Выгрузить();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти 

#КонецОбласти

#КонецЕсли