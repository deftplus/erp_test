#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//
	ЭлементыРазрядность = Новый Массив;
	ЭлементыРазрядность.Добавить(Элементы.СуммаРазрядность);
	ЭлементыРазрядность.Добавить(Элементы.КолвоРазрядность);
	ЭлементыРазрядность.Добавить(Элементы.СуммаРазрядностьВвод);
	ЭлементыРазрядность.Добавить(Элементы.КолвоРазрядностьВвод);
	СформироватьСписокВыбораРазрядности(ЭлементыРазрядность);
	
	//
	ДобавляемыеРеквизиты = Новый массив;
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ОписаниеПараметров", Новый ОписаниеТипов(неопределено)));
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	ЭтаФорма.ОписаниеПараметров = ЭтаФорма.Параметры.ОписаниеПараметров;
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, ЭтаФорма.Параметры, ЭтаФорма.ОписаниеПараметров.Вход);
	
	Для каждого Строка Из ЭтаФорма.Параметры.НастройкаАналитик Цикл
		ЗаполнитьЗначенияСвойств(НастройкаАналитик.Добавить(), Строка);
	КонецЦикла;  
	
	//
	Справочники.ВидыОперативныхПланов.УстановитьУсловноеОформлениеНастройкиАналитик(ЭтаФорма, Предназначение, Истина);
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	//
	УстановитьМаксимальноеКоличествоЗнаковПослеЗапятой(Элементы.СуммаПослеЗапятой,     	"Сумма",      СуммаРазрядность,     СуммаПослеЗапятой);
	УстановитьМаксимальноеКоличествоЗнаковПослеЗапятой(Элементы.КолвоПослеЗапятой,    	"Количество", КолвоРазрядность,     КолвоПослеЗапятой);
	
	УстановитьМаксимальноеКоличествоЗнаковПослеЗапятой(Элементы.СуммаПослеЗапятойВвод,	"Сумма",      СуммаРазрядностьВвод,	СуммаПослеЗапятойВвод);
	УстановитьМаксимальноеКоличествоЗнаковПослеЗапятой(Элементы.КолвоПослеЗапятойВвод,	"Количество", КолвоРазрядностьВвод,	КолвоПослеЗапятойВвод);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КнопкаЗаписать(Команда)
	
	Реквизиты = ЭтаФорма.ОписаниеПараметров.Выход +","+ЭтаФорма.ОписаниеПараметров.Спец;
	Результат = Новый Структура(Реквизиты);
	ЗаполнитьЗначенияСвойств(Результат, ЭтаФорма, Реквизиты);
	
	РезультатВыбора = Новый Структура;
	РезультатВыбора.Вставить("Значение", КодВозвратаДиалога.ОК);
	РезультатВыбора.Вставить("Параметры", Результат);
	РезультатВыбора.Вставить("ОписаниеПараметров", ЭтаФорма.ОписаниеПараметров);
	
	Закрыть(РезультатВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаРазрядностьПриИзменении(Элемент)
	УстановитьМаксимальноеКоличествоЗнаковПослеЗапятой(Элементы.СуммаПослеЗапятой, "Сумма", СуммаРазрядность, СуммаПослеЗапятой);
КонецПроцедуры

&НаКлиенте
Процедура КолвоРазрядностьПриИзменении(Элемент)
	УстановитьМаксимальноеКоличествоЗнаковПослеЗапятой(Элементы.КолвоПослеЗапятой, "Количество", КолвоРазрядность, КолвоПослеЗапятой);
КонецПроцедуры

&НаКлиенте
Процедура КолвоРазрядностьВводПриИзменении(Элемент)
	УстановитьМаксимальноеКоличествоЗнаковПослеЗапятой(Элементы.КолвоПослеЗапятойВвод, "Количество", КолвоРазрядностьВвод, КолвоПослеЗапятойВвод);
КонецПроцедуры

&НаКлиенте
Процедура СуммаРазрядностьВводПриИзменении(Элемент)
	УстановитьМаксимальноеКоличествоЗнаковПослеЗапятой(Элементы.СуммаПослеЗапятойВвод, "Сумма", СуммаРазрядностьВвод, СуммаПослеЗапятойВвод);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеФормой()
	
	ВидимостьКоличества = Предназначение = Перечисления.ПредназначенияЭлементовСтруктурыОтчета.БюджетДвиженияРесурсов;
	Элементы.ГруппаКоличество.Видимость = ВидимостьКоличества;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьСписокВыбораРазрядности(ЭлементыРазрядность)
	
	МассивВыбора = Новый Массив;
	МассивВыбора.Добавить(НСтр("ru ='Единицы'"));
	МассивВыбора.Добавить(НСтр("ru ='Тысячи'"));
	МассивВыбора.Добавить(НСтр("ru ='Миллионы'"));
	
	Для Каждого Элемент Из ЭлементыРазрядность Цикл
		Элемент.СписокВыбора.ЗагрузитьЗначения(МассивВыбора);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьМаксимальноеКоличествоЗнаковПослеЗапятой(Элемент, ИмяРесурса, Единица, ПослеЗапятой)
	
	Если ИмяРесурса = "Количество" Тогда
		Макс = 3;
	Иначе
		Макс = 2;
	КонецЕсли;
	
	Если Единица = "Единицы" Тогда
		Элемент.МаксимальноеЗначение = Макс;
	ИначеЕсли Единица = "Тысячи" Тогда
		Элемент.МаксимальноеЗначение = Макс + 3;
	ИначеЕсли Единица = "Миллионы" Тогда
		Элемент.МаксимальноеЗначение = Макс + 6;
	ИначеЕсли Единица = "Миллиарды" Тогда
		Элемент.МаксимальноеЗначение = Макс + 9;
	Иначе
		Единица = "Единицы";
		Элемент.МаксимальноеЗначение = Макс;
		ПослеЗапятой = Макс;
	КонецЕсли;
	
	Если ПослеЗапятой > Элемент.МаксимальноеЗначение Тогда
		ПослеЗапятой = Элемент.МаксимальноеЗначение;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 