
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ПодготовкаПараметровПроведенияДокумента

Функция ПодготовитьПараметрыПроведения(ДополнительныеСвойства, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДополнительныеСвойства.ДляПроведения.Ссылка);
	ИменаДопРеквизитов = "ПериодОтчета, ДатаНачала, ДатаОкончания, ВидОтчета";
	Реквизиты = МСФОУХ.РеквизитыДокумента(Запрос, ИменаДопРеквизитов, "ПериодОтчета", Отказ);
	Реквизиты.Вставить("ГраницыПериода", 		МСФОВНАВызовСервераУХ.ПолучитьГраницыПериодаДокумента(Реквизиты));
	Реквизиты.Вставить("ГраницыГодаДляКраткосрочнойЧасти",	МСФОКлиентСерверУХ.ПолучитьГраницыГодаДляКраткосрочнойЧасти(Реквизиты.ГраницыПериода.ДатаОкончания));
	
	ДополнительныеСвойства.ДляПроведения.Вставить("Реквизиты", Реквизиты);
	
	Если Отказ Тогда
		Возврат ДополнительныеСвойства;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация",				Реквизиты.Организация);
	Запрос.УстановитьПараметр("Сценарий",					Реквизиты.Сценарий);
	Запрос.УстановитьПараметр("ПериодОтчета", 				Реквизиты.ПериодОтчета);
	Запрос.УстановитьПараметр("ФункциональнаяВалюта",		Реквизиты.ФункциональнаяВалюта);
	Запрос.УстановитьПараметр("ВалютаПредставления",		Реквизиты.ВалютаПредставления);
	Запрос.УстановитьПараметр("ФормироватьПроводкиМСФО", 	Реквизиты.ФормироватьПроводкиМСФО);
	
	Запрос.УстановитьПараметр("Период",						Реквизиты.Период);
	Запрос.УстановитьПараметр("ДатаГраница",				Новый Граница(Реквизиты.Период, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ГраницаДоДокумента",			МСФОВызовСервераУХ.ПолучитьГраницуДоДокумента(Реквизиты.Период, Реквизиты.Ссылка));
	Запрос.УстановитьПараметр("ГраницаКонецПериода",		КонецМесяца(Реквизиты.Период));
	Запрос.УстановитьПараметр("ДатаНачалаПериода",			Реквизиты.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончанияПериода",		Реквизиты.ДатаОкончания);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаТаблицаПроводок(НомераТаблиц);
	ПроведениеСерверУХ.ДополнитьТаблицамиИзПакетаЗапросов(ДополнительныеСвойства.ТаблицыДляДвижений, Запрос, НомераТаблиц);
	
КонецФункции

Функция ТекстЗапросаТаблицаПроводок(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаПроводок", НомераТаблиц.Количество());
		
	Возврат
	"ВЫБРАТЬ
	|	ОперацииНСБУ.ФИ КАК ФИ,
	|	СведенияОФИ.Контрагент КАК Контрагент,
	|	ОперацииНСБУ.ДатаОперации КАК Период,
	|	ОперацииНСБУ.СчетДтМСФО КАК СчетДт,
	|	ОперацииНСБУ.СчетКтМСФО КАК СчетКт,
	|	ОперацииНСБУ.СчетДтСубконто1МСФО КАК СубконтоДт1,
	|	ОперацииНСБУ.СчетДтСубконто2МСФО КАК СубконтоДт2,
	|	ОперацииНСБУ.СчетДтСубконто3МСФО КАК СубконтоДт3,
	|	ОперацииНСБУ.СчетКтСубконто1МСФО КАК СубконтоКт1,
	|	ОперацииНСБУ.СчетКтСубконто2МСФО КАК СубконтоКт2,
	|	ОперацииНСБУ.СчетКтСубконто3МСФО КАК СубконтоКт3,
	|	ВЫБОР
	|		КОГДА ОперацииНСБУ.ВалютаДт <> ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|			ТОГДА ОперацииНСБУ.ВалютаДт
	|		КОГДА ОперацииНСБУ.ВалютаКт <> ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|			ТОГДА ОперацииНСБУ.ВалютаКт
	|		ИНАЧЕ &ФункциональнаяВалюта
	|	КОНЕЦ КАК Валюта,
	|	ОперацииНСБУ.ВалютаДт КАК ВалютаДт,
	|	ОперацииНСБУ.ВалютаКт КАК ВалютаКт,
	|	ОперацииНСБУ.ВидОперации КАК ВидОперации,
	|	ОперацииНСБУ.ВидОперации КАК Событие,
	|	ОперацииНСБУ.КоличествоДт КАК КоличествоДт,
	|	ОперацииНСБУ.КоличествоКт КАК КоличествоКт,
	|	ОперацииНСБУ.Сумма КАК СуммаВВалютеУчета,
	|	ВЫБОР
	|		КОГДА ОперацииНСБУ.ВалютаДт <> ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|			ТОГДА ОперацииНСБУ.СуммаДт
	|		КОГДА ОперацииНСБУ.ВалютаКт <> ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|			ТОГДА ОперацииНСБУ.СуммаКт
	|		ИНАЧЕ ОперацииНСБУ.Сумма
	|	КОНЕЦ КАК СуммаВВалюте,
	|	ОперацииНСБУ.Сумма КАК СуммаВФункциональнойВалюте,
	|	ОперацииНСБУ.Сумма * КурсФВ.Курс * КурсыВалютОтчетности.Кратность / (КурсыВалютОтчетности.Курс * КурсФВ.Кратность) КАК СуммаВВалютеОтчетности,
	|	ОперацииНСБУ.СуммаДт КАК СуммаДт,
	|	ОперацииНСБУ.СуммаКт КАК СуммаКт,
	|	ОперацииНСБУ.ПодразделениеДт КАК ПодразделениеДт,
	|	ОперацииНСБУ.НаправлениеДеятельностиДт КАК НаправлениеДеятельностиДт,
	|	ОперацииНСБУ.ПодразделениеКт КАК ПодразделениеКт,
	|	ОперацииНСБУ.НаправлениеДеятельностиКт КАК НаправлениеДеятельностиКт
	|ИЗ
	|	Документ.ПризнаниеРасходовФинансовыхИнструментов.Сторно КАК ОперацииНСБУ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОФИ.СрезПоследних(
	|				&ГраницаДоДокумента,
	|				Организация = &Организация
	|					И Сценарий = &Сценарий) КАК СведенияОФИ
	|		ПО ОперацииНСБУ.ФИ = СведенияОФИ.ФИ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсФВ
	|		ПО ОперацииНСБУ.ДатаОперации = КурсФВ.Период
	|			И (КурсФВ.Валюта = &ФункциональнаяВалюта)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалютОтчетности
	|		ПО ОперацииНСБУ.ДатаОперации = КурсыВалютОтчетности.Период
	|			И (КурсыВалютОтчетности.Валюта = &ВалютаПредставления)
	|ГДЕ
	|	ОперацииНСБУ.Ссылка = &Ссылка";
		
КонецФункции

Функция СформироватьДвижения(Движения, ДополнительныеСвойства, Отказ) Экспорт 

	Реквизиты 		= ДополнительныеСвойства.ДляПроведения.Реквизиты;	
	ТаблицаПроводок = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаПроводок;		
		
	МСФОВызовСервераУХ.ОтразитьДвиженияПоТаблицеПроводок(Движения, ТаблицаПроводок, Реквизиты, Отказ);	
	МСФОВызовСервераУХ.ЗаписатьДвижения(Движения);// ТК должна видеть движения

	Если Не Реквизиты.ФормироватьПроводкиМСФО Тогда
		МодульТК = ОбщегоНазначения.ОбщийМодуль("ТрансформационныеКорректировкиУХ");
		МодульТК.СформироватьКорректировку(Реквизиты, Отказ, ТаблицаПроводок);	
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ЗаполнениеДокумента

Функция ПолучитьИменаСубконто() Экспорт

	Результат = Новый Структура;
	
	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетДтСубконто1Источник");
	Субконто.Вставить(2, "СчетДтСубконто2Источник");
	Субконто.Вставить(3, "СчетДтСубконто3Источник");
	
	Результат.Вставить("СчетДтИсточник", Субконто);

	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетКтСубконто1Источник");
	Субконто.Вставить(2, "СчетКтСубконто2Источник");
	Субконто.Вставить(3, "СчетКтСубконто3Источник");
	
	Результат.Вставить("СчетКтИсточник", Субконто);

	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетДтСубконто1МСФО");
	Субконто.Вставить(2, "СчетДтСубконто2МСФО");
	Субконто.Вставить(3, "СчетДтСубконто3МСФО");
	Субконто.Вставить("Подразделение", "ПодразделениеДт");
	Субконто.Вставить("НаправлениеДеятельности", "НаправлениеДеятельностиДт");
	
	Результат.Вставить("СчетДтМСФО", Субконто);

	Субконто = Новый Соответствие;
	Субконто.Вставить(1, "СчетКтСубконто1МСФО");
	Субконто.Вставить(2, "СчетКтСубконто2МСФО");
	Субконто.Вставить(3, "СчетКтСубконто3МСФО");
	Субконто.Вставить("Подразделение", "ПодразделениеКт");
	Субконто.Вставить("НаправлениеДеятельности", "НаправлениеДеятельностиКт");
	
	Результат.Вставить("СчетКтМСФО", Субконто);

	Возврат Результат;
	
КонецФункции

Процедура Заполнить(ДокументОбъект) Экспорт
	
	//начисления процентов(Сторно), 
	//оплата процентов/задолженности (перевод)
	//реализация/расход(сторно)-  для ФИ с НДС
	//НДС(Перевод) -  для ФИ с НДС
	//ФИ с НДС - Лизинг, ДКЗ	
	ДатаОкончания = КонецДня(ДокументОбъект.ПериодОтчета.ДатаОкончания);
	РеквизитыУП = МСФОВызовСервераУХ.РеквизитыДляФормыУП(ДокументОбъект.Организация, ДатаОкончания, ДокументОбъект.Сценарий);

	НомераТаблиц = Новый Структура;
	
	ТекстЗапроса = Новый Массив;
	ТекстЗапроса.Добавить(ТекстЗапроса_втФИ(НомераТаблиц));
	ТекстЗапроса.Добавить(ТекстЗапроса_втДоговоры(НомераТаблиц));
	ТекстЗапроса.Добавить(ТекстЗапроса_втНомераСтрокПроводок(НомераТаблиц));
	ТекстЗапроса.Добавить(ТекстЗапроса_втПроводкиБезНДС(НомераТаблиц));
	ТекстЗапроса.Добавить(ТекстЗапроса_ПроводкиИсточники(НомераТаблиц));
	ТекстЗапроса.Добавить(ТекстЗапроса_ТаблицаСторно(НомераТаблиц));
	
	Запрос = Новый Запрос(СтрСоединить(ТекстЗапроса, ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета()));
	
	Запрос.УстановитьПараметр("Организация", 	ДокументОбъект.Организация);
	Запрос.УстановитьПараметр("Сценарий", 		ДокументОбъект.Сценарий);
	Запрос.УстановитьПараметр("Регистратор", 	ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДатаНачала", 	ДокументОбъект.ПериодОтчета.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", 	ДатаОкончания);
	Запрос.УстановитьПараметр("ПланСчетовМСФО", РеквизитыУП.ПланСчетовМСФО);
	
	ДокументОбъект.Сторно.Загрузить(Запрос.Выполнить().Выгрузить());
		
КонецПроцедуры

Функция ТекстЗапроса_втФИ(НомераТаблиц)

	НомераТаблиц.Вставить("втФИ", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	втФИ.ФИ КАК ФИ,
	|	втФИ.Регистратор КАК Регистратор,
	|	втФИ.ПараметрыУчетаФИ КАК ПараметрыУчетаФИ,
	|	втФИ.СчетУчетаПриемник КАК СчетУчетаПриемник,
	|	СчетаУчета.Счет КАК Счет,
	|	ВЫБОР ЕСТЬNULL(СчетаБДВидыСубконто1.ВидСубконтоСсылка, НЕОПРЕДЕЛЕНО)
	|		КОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоКорпоративные.СправочникКонтрагенты)
	|			ТОГДА втФИ.Контрагент
	|		КОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоКорпоративные.СправочникДоговораКонтрагентов)
	|			ТОГДА втФИ.ФИ
	|		КОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоКорпоративные.ЦенныеБумаги)
	|			ТОГДА втФИ.ФИ
	|		ИНАЧЕ СчетаУчета.Субконто1
	|	КОНЕЦ КАК Субконто1,
	|	ВЫБОР ЕСТЬNULL(СчетаБДВидыСубконто2.ВидСубконтоСсылка, НЕОПРЕДЕЛЕНО)
	|		КОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоКорпоративные.СправочникКонтрагенты)
	|			ТОГДА втФИ.Контрагент
	|		КОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоКорпоративные.СправочникДоговораКонтрагентов)
	|			ТОГДА втФИ.ФИ
	|		КОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоКорпоративные.ЦенныеБумаги)
	|			ТОГДА втФИ.ФИ
	|		ИНАЧЕ СчетаУчета.Субконто2
	|	КОНЕЦ КАК Субконто2,
	|	ВЫБОР ЕСТЬNULL(СчетаБДВидыСубконто3.ВидСубконтоСсылка, НЕОПРЕДЕЛЕНО)
	|		КОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоКорпоративные.СправочникКонтрагенты)
	|			ТОГДА втФИ.Контрагент
	|		КОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоКорпоративные.СправочникДоговораКонтрагентов)
	|			ТОГДА втФИ.ФИ
	|		КОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоКорпоративные.ЦенныеБумаги)
	|			ТОГДА втФИ.ФИ
	|		ИНАЧЕ СчетаУчета.Субконто3
	|	КОНЕЦ КАК Субконто3,
	|	СчетАвансаПоНДС.Счет КАК СчетАвансаПоНДС,
	|	ВЫБОР ЕСТЬNULL(СчетАвансаПоНДСВидыСубконто1.ВидСубконтоСсылка, НЕОПРЕДЕЛЕНО)
	|		КОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоКорпоративные.СправочникКонтрагенты)
	|			ТОГДА втФИ.Контрагент
	|		КОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоКорпоративные.СправочникДоговораКонтрагентов)
	|			ТОГДА втФИ.ФИ
	|		КОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоКорпоративные.ЦенныеБумаги)
	|			ТОГДА втФИ.ФИ
	|		ИНАЧЕ СчетАвансаПоНДС.Субконто1
	|	КОНЕЦ КАК СчетАвансаПоНДССубконто1,
	|	ВЫБОР ЕСТЬNULL(СчетАвансаПоНДСВидыСубконто2.ВидСубконтоСсылка, НЕОПРЕДЕЛЕНО)
	|		КОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоКорпоративные.СправочникКонтрагенты)
	|			ТОГДА втФИ.Контрагент
	|		КОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоКорпоративные.СправочникДоговораКонтрагентов)
	|			ТОГДА втФИ.ФИ
	|		КОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоКорпоративные.ЦенныеБумаги)
	|			ТОГДА втФИ.ФИ
	|		ИНАЧЕ СчетАвансаПоНДС.Субконто2
	|	КОНЕЦ КАК СчетАвансаПоНДССубконто2,
	|	ВЫБОР ЕСТЬNULL(СчетАвансаПоНДСВидыСубконто3.ВидСубконтоСсылка, НЕОПРЕДЕЛЕНО)
	|		КОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоКорпоративные.СправочникКонтрагенты)
	|			ТОГДА втФИ.Контрагент
	|		КОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоКорпоративные.СправочникДоговораКонтрагентов)
	|			ТОГДА втФИ.ФИ
	|		КОГДА ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоКорпоративные.ЦенныеБумаги)
	|			ТОГДА втФИ.ФИ
	|		ИНАЧЕ СчетАвансаПоНДС.Субконто3
	|	КОНЕЦ КАК СчетАвансаПоНДССубконто3,
	|	втФИ.Контрагент КАК Контрагент,
	|	втФИ.Валюта КАК Валюта
	|ПОМЕСТИТЬ втФИ
	|ИЗ
	|	(ВЫБРАТЬ
	|		т.ФИ КАК ФИ,
	|		т.Контрагент КАК Контрагент,
	|		т.Регистратор КАК Регистратор,
	|		т.ПараметрыУчетаФИ КАК ПараметрыУчетаФИ,
	|		ВЫБОР
	|			КОГДА т.ПараметрыУчетаФИ.ВидОбъектаФинансовогоХарактера.Лизинг
	|				ТОГДА ЗНАЧЕНИЕ(Справочник.СчетаУчетаФИ.СчетНачисленияПлатежа)
	|			КОГДА т.ПараметрыУчетаФИ.ВидОбъектаФинансовогоХарактера.Задолженность
	|				ТОГДА ВЫБОР
	|						КОГДА т.ПараметрыУчетаФИ.ВидОбъектаФинансовогоХарактера.АктивОбязательство = ЗНАЧЕНИЕ(Перечисление.АктивОбязательствоОбъектовФинансовогоХарактера.Актив)
	|							ТОГДА ЗНАЧЕНИЕ(Справочник.СчетаУчетаФИ.СчетАктиваДолгосрочный)
	|						ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СчетаУчетаФИ.СчетОбязательстваДолгосрочный)
	|					КОНЕЦ
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СчетаУчетаФИ.СчетКраткосрочныхПроцентов)
	|		КОНЕЦ КАК СчетУчетаПриемник,
	|		т.Валюта КАК Валюта
	|	ИЗ
	|		РегистрСведений.СведенияОФИ.СрезПоследних(
	|				&ДатаОкончания,
	|				Организация = &Организация
	|					И Сценарий = &Сценарий) КАК т
	|	ГДЕ
	|		т.ДатаОкончания > &ДатаНачала) КАК втФИ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПараметрыУчетаФинансовыхИнструментовМСФО.СчетаУчета КАК СчетаУчета
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СчетаБД.ВидыСубконто КАК СчетаБДВидыСубконто1
	|			ПО СчетаУчета.Счет = СчетаБДВидыСубконто1.Ссылка
	|				И (СчетаБДВидыСубконто1.Номер = 1)
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СчетаБД.ВидыСубконто КАК СчетаБДВидыСубконто2
	|			ПО СчетаУчета.Счет = СчетаБДВидыСубконто2.Ссылка
	|				И (СчетаБДВидыСубконто2.Номер = 2)
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СчетаБД.ВидыСубконто КАК СчетаБДВидыСубконто3
	|			ПО СчетаУчета.Счет = СчетаБДВидыСубконто3.Ссылка
	|				И (СчетаБДВидыСубконто3.Номер = 3)
	|		ПО втФИ.ПараметрыУчетаФИ = СчетаУчета.Ссылка
	|			И втФИ.СчетУчетаПриемник = СчетаУчета.СчетУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПараметрыУчетаФинансовыхИнструментовМСФО.СчетаУчета КАК СчетАвансаПоНДС
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СчетаБД.ВидыСубконто КАК СчетАвансаПоНДСВидыСубконто1
	|			ПО СчетАвансаПоНДС.Счет = СчетАвансаПоНДСВидыСубконто1.Ссылка
	|				И (СчетАвансаПоНДСВидыСубконто1.Номер = 1)
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СчетаБД.ВидыСубконто КАК СчетАвансаПоНДСВидыСубконто2
	|			ПО СчетАвансаПоНДС.Счет = СчетАвансаПоНДСВидыСубконто2.Ссылка
	|				И (СчетАвансаПоНДСВидыСубконто2.Номер = 2)
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СчетаБД.ВидыСубконто КАК СчетАвансаПоНДСВидыСубконто3
	|			ПО СчетАвансаПоНДС.Счет = СчетАвансаПоНДСВидыСубконто3.Ссылка
	|				И (СчетАвансаПоНДСВидыСубконто3.Номер = 3)
	|		ПО втФИ.ПараметрыУчетаФИ = СчетАвансаПоНДС.Ссылка
	|			И (СчетАвансаПоНДС.СчетУчета = ЗНАЧЕНИЕ(Справочник.СчетаУчетаФИ.СчетНачисленияАванса))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	втФИ.ФИ,
	|	втФИ.Регистратор";
	
КонецФункции

Функция ТекстЗапроса_втДоговоры(НомераТаблиц)

	НомераТаблиц.Вставить("втДоговоры", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	т.ФИ КАК ФИ,
	|	т.ФИ КАК Договор
	|ПОМЕСТИТЬ втДоговоры
	|ИЗ
	|	втФИ КАК т
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	т.ФИ,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(т.СчетРасходаСубконто1МСФО) В (ТИП(Справочник.ДоговорыКонтрагентов), ТИП(Справочник.ЦенныеБумаги))
	|			ТОГДА т.СчетРасходаСубконто1МСФО
	|		КОГДА ТИПЗНАЧЕНИЯ(т.СчетРасходаСубконто2МСФО) В (ТИП(Справочник.ДоговорыКонтрагентов), ТИП(Справочник.ЦенныеБумаги))
	|			ТОГДА т.СчетРасходаСубконто2МСФО	
	|		КОГДА ТИПЗНАЧЕНИЯ(т.СчетРасходаСубконто3МСФО) В (ТИП(Справочник.ДоговорыКонтрагентов), ТИП(Справочник.ЦенныеБумаги))
	|			ТОГДА т.СчетРасходаСубконто3МСФО
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|ИЗ
	|	Документ.ВводСведенийОФинансовыхИнструментах.ДополнительныеРасходы КАК т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втФИ КАК втФИ
	|		ПО т.Ссылка = втФИ.Регистратор
	|			И т.ФИ = втФИ.ФИ";
	
КонецФункции

Функция ТекстЗапроса_втНомераСтрокПроводок(НомераТаблиц)

	НомераТаблиц.Вставить("втНомераСтрокПроводок", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	МАКСИМУМ(втДоговоры.ФИ) КАК ФИ,
	|	МСФОСубконто.Регистратор КАК Регистратор,
	|	МСФОСубконто.НомерСтроки КАК НомерСтроки,
	|	МАКСИМУМ(МСФОСубконто.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Дебет)) КАК СчетИсточникДебет
	|ПОМЕСТИТЬ втНомераСтрокПроводок
	|ИЗ
	|	РегистрБухгалтерии.МСФО.Субконто КАК МСФОСубконто
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДоговоры КАК втДоговоры
	|		ПО МСФОСубконто.Значение = втДоговоры.Договор
	|ГДЕ
	|	МСФОСубконто.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И НЕ ТИПЗНАЧЕНИЯ(МСФОСубконто.Регистратор) В (ТИП(Документ.РегламентнаяОперацияПериодаПоФинансовымИнструментамМСФО), ТИП(Документ.ПризнаниеРасходовФинансовыхИнструментов))
	|
	|СГРУППИРОВАТЬ ПО
	|	МСФОСубконто.Регистратор,
	|	МСФОСубконто.НомерСтроки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	НомерСтроки";

КонецФункции

Функция ТекстЗапроса_втПроводкиБезНДС(НомераТаблиц)

	НомераТаблиц.Вставить("втПроводкиБезНДС", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	втНомераСтрокПроводок.ФИ КАК ФИ,
	|	втНомераСтрокПроводок.СчетИсточникДебет КАК СчетИсточникДебет,
	|	Проводки.Период КАК Период,
	|	Проводки.Регистратор КАК Регистратор,
	|	Проводки.НомерСтроки КАК НомерСтроки,
	|	Проводки.СчетДт КАК СчетДт,
	|	Проводки.СубконтоДт1 КАК СубконтоДт1,
	|	Проводки.СубконтоДт2 КАК СубконтоДт2,
	|	Проводки.СубконтоДт3 КАК СубконтоДт3,
	|	Проводки.СчетКт КАК СчетКт,
	|	Проводки.СубконтоКт1 КАК СубконтоКт1,
	|	Проводки.СубконтоКт2 КАК СубконтоКт2,
	|	Проводки.СубконтоКт3 КАК СубконтоКт3,
	|	Проводки.ВалютаДт КАК ВалютаДт,
	|	Проводки.ВалютаКт КАК ВалютаКт,
	|	Проводки.СуммаВВалютеДт КАК СуммаВВалютеДт,
	|	Проводки.СуммаВВалютеКт КАК СуммаВВалютеКт,
	|	Проводки.КоличествоДт КАК КоличествоДт,
	|	Проводки.КоличествоКт КАК КоличествоКт,
	|	Проводки.ПодразделениеДт КАК ПодразделениеДт,
	|	Проводки.ПодразделениеКт КАК ПодразделениеКт,
	|	Проводки.НаправлениеДеятельностиДт КАК НаправлениеДеятельностиДт,
	|	Проводки.НаправлениеДеятельностиКт КАК НаправлениеДеятельностиКт,
	|	Проводки.СуммаВВалютеУчета КАК СуммаВВалютеУчета
	|ПОМЕСТИТЬ втПроводкиБезНДС
	|ИЗ
	|	РегистрБухгалтерии.МСФО.ДвиженияССубконто(
	|			,
	|			,
	|			Организация = &Организация
	|				И Сценарий = &Сценарий
	|				И ВидОперации = ЗНАЧЕНИЕ(Справочник.ВидыОпераций.Трансляция)
	|				И Активность,
	|			,
	|			) КАК Проводки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втНомераСтрокПроводок КАК втНомераСтрокПроводок
	|		ПО Проводки.Регистратор = втНомераСтрокПроводок.Регистратор
	|			И Проводки.НомерСтроки = втНомераСтрокПроводок.НомерСтроки";
	
КонецФункции

Функция ТекстЗапроса_ПроводкиИсточники(НомераТаблиц)

	НомераТаблиц.Вставить("втПроводки", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	Проводки.ФИ КАК ФИ,
	|	ВЫБОР
	|		КОГДА Проводки.СчетДт.РазделПланаСчетов В (ЗНАЧЕНИЕ(Справочник.РазделыПланаСчетов.ДенежныеСредства), ЗНАЧЕНИЕ(Справочник.РазделыПланаСчетов.Налоги))
	|				ИЛИ Проводки.СчетКт.РазделПланаСчетов В (ЗНАЧЕНИЕ(Справочник.РазделыПланаСчетов.ДенежныеСредства), ЗНАЧЕНИЕ(Справочник.РазделыПланаСчетов.Налоги))
	|			ТОГДА Проводки.СчетИсточникДебет
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СчетИсточникДебет,
	|	ВЫБОР
	|		КОГДА Проводки.СчетДт.РазделПланаСчетов В (ЗНАЧЕНИЕ(Справочник.РазделыПланаСчетов.ДенежныеСредства))
	|				ИЛИ Проводки.СчетКт.РазделПланаСчетов В (ЗНАЧЕНИЕ(Справочник.РазделыПланаСчетов.ДенежныеСредства))
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПереносОплаты)
	|		КОГДА Проводки.СчетДт.РазделПланаСчетов В (ЗНАЧЕНИЕ(Справочник.РазделыПланаСчетов.Налоги))
	|				ИЛИ Проводки.СчетКт.РазделПланаСчетов В (ЗНАЧЕНИЕ(Справочник.РазделыПланаСчетов.Налоги))
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПереносНДС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ВидыОпераций.СторноПроцентов)
	|	КОНЕЦ КАК ВидОперации,
	|	Проводки.Период КАК Период,
	|	Проводки.Регистратор КАК Регистратор,
	|	Проводки.НомерСтроки КАК НомерСтроки,
	|	Проводки.СчетДт КАК СчетДт,
	|	Проводки.СубконтоДт1 КАК СубконтоДт1,
	|	Проводки.СубконтоДт2 КАК СубконтоДт2,
	|	Проводки.СубконтоДт3 КАК СубконтоДт3,
	|	Проводки.СчетКт КАК СчетКт,
	|	Проводки.СубконтоКт1 КАК СубконтоКт1,
	|	Проводки.СубконтоКт2 КАК СубконтоКт2,
	|	Проводки.СубконтоКт3 КАК СубконтоКт3,
	|	Проводки.ВалютаДт КАК ВалютаДт,
	|	Проводки.ВалютаКт КАК ВалютаКт,
	|	Проводки.СуммаВВалютеДт КАК СуммаВВалютеДт,
	|	Проводки.СуммаВВалютеКт КАК СуммаВВалютеКт,
	|	Проводки.КоличествоДт КАК КоличествоДт,
	|	Проводки.КоличествоКт КАК КоличествоКт,
	|	Проводки.ПодразделениеДт КАК ПодразделениеДт,
	|	Проводки.ПодразделениеКт КАК ПодразделениеКт,
	|	Проводки.НаправлениеДеятельностиДт КАК НаправлениеДеятельностиДт,
	|	Проводки.НаправлениеДеятельностиКт КАК НаправлениеДеятельностиКт,
	|	Проводки.СуммаВВалютеУчета КАК СуммаВВалютеУчета
	|ПОМЕСТИТЬ втПроводки
	|ИЗ
	|	втПроводкиБезНДС КАК Проводки";

КонецФункции

Функция ТекстЗапроса_ТаблицаСторно(НомераТаблиц)

	//Если в проводке используется счет налогов или денежных средств, то с коррсчета переводим на счет учета оплат,налогов
	//например:
	//проводка источник: Дт СчетДенежныхСредств Кт СчетЗадолженностиРСБУ Сумма
	//проводка МСФО: Дт СчетЗадолженностиРСБУ Кт СчетЗадолженностиМСФО Сумма
	
	//Если в проводке не используются счета налогов и денежных средств(Проводка начисления процентов), то такую проводку сторнируем
	//например:
	//проводка источник: Дт НачисленияПроцентов Кт СчетЗадолженностиРСБУ Сумма	
	//проводка МСФО: Дт НачисленияПроцентов Кт СчетЗадолженностиРСБУ -Сумма
	
	НомераТаблиц.Вставить("ТаблицаСторно", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(Проводки.СчетДтМСФО КАК Справочник.СчетаБД).ПризнакиУчета ПОДОБНО ""%Количественый%""
	|			ТОГДА ЕСТЬNULL(Проводки.КоличествоДт, Проводки.КоличествоКт)
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК КоличествоДт,
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(Проводки.СчетКтМСФО КАК Справочник.СчетаБД).ПризнакиУчета ПОДОБНО ""%Количественый%""
	|			ТОГДА ЕСТЬNULL(Проводки.КоличествоДт, Проводки.КоличествоКт)
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК КоличествоКт,
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(Проводки.СчетДтМСФО КАК Справочник.СчетаБД).Валютный
	|			ТОГДА ЕСТЬNULL(Проводки.ВалютаДт, Проводки.ВалютаКт)
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК ВалютаДт,
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(Проводки.СчетКтМСФО КАК Справочник.СчетаБД).Валютный
	|			ТОГДА ЕСТЬNULL(Проводки.ВалютаДт, Проводки.ВалютаКт)
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК ВалютаКт,
	|	Проводки.ПодразделениеДт КАК ПодразделениеДт,
	|	Проводки.ПодразделениеКт КАК ПодразделениеКт,
	|	Проводки.НаправлениеДеятельностиДт КАК НаправлениеДеятельностиДт,
	|	Проводки.НаправлениеДеятельностиКт КАК НаправлениеДеятельностиКт,
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(Проводки.СчетДтМСФО КАК Справочник.СчетаБД).Валютный
	|			ТОГДА ЕСТЬNULL(Проводки.СуммаДт, Проводки.СуммаКт)
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК СуммаДт,
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(Проводки.СчетКтМСФО КАК Справочник.СчетаБД).Валютный
	|			ТОГДА ЕСТЬNULL(Проводки.СуммаДт, Проводки.СуммаКт)
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК СуммаКт,
	|	Проводки.ВидОперации КАК ВидОперации,
	|	Проводки.ДатаОперации КАК ДатаОперации,
	|	Проводки.Сумма КАК Сумма,
	|	Проводки.СчетДтМСФО КАК СчетДтМСФО,
	|	Проводки.СчетДтСубконто1МСФО КАК СчетДтСубконто1МСФО,
	|	Проводки.СчетДтСубконто2МСФО КАК СчетДтСубконто2МСФО,
	|	Проводки.СчетДтСубконто3МСФО КАК СчетДтСубконто3МСФО,
	|	Проводки.СчетКтМСФО КАК СчетКтМСФО,
	|	Проводки.СчетКтСубконто1МСФО КАК СчетКтСубконто1МСФО,
	|	Проводки.СчетКтСубконто2МСФО КАК СчетКтСубконто2МСФО,
	|	Проводки.СчетКтСубконто3МСФО КАК СчетКтСубконто3МСФО,
	|	Проводки.СчетДтИсточник КАК СчетДтИсточник,
	|	Проводки.СубконтоДт1Источник КАК СубконтоДт1Источник,
	|	Проводки.СубконтоДт2Источник КАК СубконтоДт2Источник,
	|	Проводки.СубконтоДт3Источник КАК СубконтоДт3Источник,
	|	Проводки.СчетКтИсточник КАК СчетКтИсточник,
	|	Проводки.СубконтоКт1Источник КАК СубконтоКт1Источник,
	|	Проводки.СубконтоКт2Источник КАК СубконтоКт2Источник,
	|	Проводки.СубконтоКт3Источник КАК СубконтоКт3Источник,
	|	Проводки.ФИ КАК ФИ,
	|	Проводки.СчетИсточникДебет КАК СчетИсточникДебет,
	|	Проводки.Регистратор КАК Регистратор,
	|	Проводки.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	(ВЫБРАТЬ
	|		Проводки.ВалютаДт КАК ВалютаДт,
	|		Проводки.ВалютаКт КАК ВалютаКт,
	|		Проводки.ВидОперации КАК ВидОперации,
	|		Проводки.Период КАК ДатаОперации,
	|		Проводки.КоличествоДт КАК КоличествоДт,
	|		Проводки.КоличествоКт КАК КоличествоКт,
	|		Проводки.ПодразделениеДт КАК ПодразделениеДт,
	|		Проводки.ПодразделениеКт КАК ПодразделениеКт,
	|		Проводки.НаправлениеДеятельностиДт КАК НаправлениеДеятельностиДт,
	|		Проводки.НаправлениеДеятельностиКт КАК НаправлениеДеятельностиКт,
	|		ВЫБОР
	|			КОГДА Проводки.ВидОперации = ЗНАЧЕНИЕ(Справочник.ВидыОпераций.СторноПроцентов)
	|				ТОГДА -Проводки.СуммаВВалютеУчета
	|			ИНАЧЕ Проводки.СуммаВВалютеУчета
	|		КОНЕЦ КАК Сумма,
	|		ВЫБОР
	|			КОГДА Проводки.ВидОперации = ЗНАЧЕНИЕ(Справочник.ВидыОпераций.СторноПроцентов)
	|				ТОГДА -Проводки.СуммаВВалютеДт
	|			ИНАЧЕ Проводки.СуммаВВалютеДт
	|		КОНЕЦ КАК СуммаДт,
	|		ВЫБОР
	|			КОГДА Проводки.ВидОперации = ЗНАЧЕНИЕ(Справочник.ВидыОпераций.СторноПроцентов)
	|				ТОГДА -Проводки.СуммаВВалютеКт
	|			ИНАЧЕ Проводки.СуммаВВалютеКт
	|		КОНЕЦ КАК СуммаКт,
	|		ВЫБОР Проводки.СчетИсточникДебет
	|			КОГДА ИСТИНА
	|				ТОГДА ВЫБОР
	|						КОГДА Проводки.ВидОперации = ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПереносНДС)
	|							ТОГДА втФИ.СчетАвансаПоНДС
	|						ИНАЧЕ втФИ.Счет
	|					КОНЕЦ
	|			КОГДА ЛОЖЬ
	|				ТОГДА тСчетКт.Ссылка
	|			ИНАЧЕ тСчетДт.Ссылка
	|		КОНЕЦ КАК СчетДтМСФО,
	|		ВЫБОР Проводки.СчетИсточникДебет
	|			КОГДА ИСТИНА
	|				ТОГДА ВЫБОР
	|						КОГДА Проводки.ВидОперации = ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПереносНДС)
	|							ТОГДА втФИ.СчетАвансаПоНДССубконто1
	|						ИНАЧЕ втФИ.Субконто1
	|					КОНЕЦ
	|			КОГДА ЛОЖЬ
	|				ТОГДА Проводки.СубконтоКт1
	|			ИНАЧЕ Проводки.СубконтоДт1
	|		КОНЕЦ КАК СчетДтСубконто1МСФО,
	|		ВЫБОР Проводки.СчетИсточникДебет
	|			КОГДА ИСТИНА
	|				ТОГДА ВЫБОР
	|						КОГДА Проводки.ВидОперации = ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПереносНДС)
	|							ТОГДА втФИ.СчетАвансаПоНДССубконто2
	|						ИНАЧЕ втФИ.Субконто2
	|					КОНЕЦ
	|			КОГДА ЛОЖЬ
	|				ТОГДА Проводки.СубконтоКт2
	|			ИНАЧЕ Проводки.СубконтоДт2
	|		КОНЕЦ КАК СчетДтСубконто2МСФО,
	|		ВЫБОР Проводки.СчетИсточникДебет
	|			КОГДА ИСТИНА
	|				ТОГДА ВЫБОР
	|						КОГДА Проводки.ВидОперации = ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПереносНДС)
	|							ТОГДА втФИ.СчетАвансаПоНДССубконто3
	|						ИНАЧЕ втФИ.Субконто3
	|					КОНЕЦ
	|			КОГДА ЛОЖЬ
	|				ТОГДА Проводки.СубконтоКт3
	|			ИНАЧЕ Проводки.СубконтоДт3
	|		КОНЕЦ КАК СчетДтСубконто3МСФО,
	|		ВЫБОР Проводки.СчетИсточникДебет
	|			КОГДА ИСТИНА
	|				ТОГДА тСчетДт.Ссылка
	|			КОГДА ЛОЖЬ
	|				ТОГДА ВЫБОР
	|						КОГДА Проводки.ВидОперации = ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПереносНДС)
	|							ТОГДА втФИ.СчетАвансаПоНДС
	|						ИНАЧЕ втФИ.Счет
	|					КОНЕЦ
	|			ИНАЧЕ тСчетКт.Ссылка
	|		КОНЕЦ КАК СчетКтМСФО,
	|		ВЫБОР Проводки.СчетИсточникДебет
	|			КОГДА ИСТИНА
	|				ТОГДА Проводки.СубконтоДт1
	|			КОГДА ЛОЖЬ
	|				ТОГДА ВЫБОР
	|						КОГДА Проводки.ВидОперации = ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПереносНДС)
	|							ТОГДА втФИ.СчетАвансаПоНДССубконто1
	|						ИНАЧЕ втФИ.Субконто1
	|					КОНЕЦ
	|			ИНАЧЕ Проводки.СубконтоКт1
	|		КОНЕЦ КАК СчетКтСубконто1МСФО,
	|		ВЫБОР Проводки.СчетИсточникДебет
	|			КОГДА ИСТИНА
	|				ТОГДА Проводки.СубконтоДт2
	|			КОГДА ЛОЖЬ
	|				ТОГДА ВЫБОР
	|						КОГДА Проводки.ВидОперации = ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПереносНДС)
	|							ТОГДА втФИ.СчетАвансаПоНДССубконто2
	|						ИНАЧЕ втФИ.Субконто2
	|					КОНЕЦ
	|			ИНАЧЕ Проводки.СубконтоКт2
	|		КОНЕЦ КАК СчетКтСубконто2МСФО,
	|		ВЫБОР Проводки.СчетИсточникДебет
	|			КОГДА ИСТИНА
	|				ТОГДА Проводки.СубконтоДт3
	|			КОГДА ЛОЖЬ
	|				ТОГДА ВЫБОР
	|						КОГДА Проводки.ВидОперации = ЗНАЧЕНИЕ(Справочник.ВидыОпераций.ПереносНДС)
	|							ТОГДА втФИ.СчетАвансаПоНДССубконто3
	|						ИНАЧЕ втФИ.Субконто3
	|					КОНЕЦ
	|			ИНАЧЕ Проводки.СубконтоКт3
	|		КОНЕЦ КАК СчетКтСубконто3МСФО,
	|		Проводки.СчетДт КАК СчетДтИсточник,
	|		Проводки.СубконтоДт1 КАК СубконтоДт1Источник,
	|		Проводки.СубконтоДт2 КАК СубконтоДт2Источник,
	|		Проводки.СубконтоДт3 КАК СубконтоДт3Источник,
	|		Проводки.СчетКт КАК СчетКтИсточник,
	|		Проводки.СубконтоКт1 КАК СубконтоКт1Источник,
	|		Проводки.СубконтоКт2 КАК СубконтоКт2Источник,
	|		Проводки.СубконтоКт3 КАК СубконтоКт3Источник,
	|		Проводки.ФИ КАК ФИ,
	|		Проводки.СчетИсточникДебет КАК СчетИсточникДебет,
	|		Проводки.Регистратор КАК Регистратор,
	|		Проводки.НомерСтроки КАК НомерСтроки
	|	ИЗ
	|		втПроводки КАК Проводки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втФИ КАК втФИ
	|			ПО Проводки.ФИ = втФИ.ФИ
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СчетаБД КАК тСчетДт
	|			ПО Проводки.СчетДт = тСчетДт.СчетСсылка
	|				И (тСчетДт.Владелец = &ПланСчетовМСФО)
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СчетаБД КАК тСчетКт
	|			ПО Проводки.СчетКт = тСчетКт.СчетСсылка
	|				И (тСчетКт.Владелец = &ПланСчетовМСФО)) КАК Проводки";

КонецФункции

#КонецОбласти

#Область ОбновлениеИБ

Процедура ЗаполнитьРеквизитыСторно() Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПризнаниеРасходовФинансовыхИнструментовСторно.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПризнаниеРасходовФинансовыхИнструментов.Сторно КАК ПризнаниеРасходовФинансовыхИнструментовСторно
	|ГДЕ
	|	ПризнаниеРасходовФинансовыхИнструментовСторно.Удалить_СуммаОперацииВВалютеДоговораНСБУ <> 0
	|	И ПризнаниеРасходовФинансовыхИнструментовСторно.СуммаДт = 0
	|	И ПризнаниеРасходовФинансовыхИнструментовСторно.СуммаКт = 0");
	
	Выборка = Запрос.Выполнить().Выбрать();	
	Пока Выборка.Следующий() Цикл
		
		ЕстьИзменения = Ложь;
		
		ТекущийОбъект = Выборка.Ссылка.ПолучитьОбъект();
				
		Для каждого СтрокаТаб Из ТекущийОбъект.Сторно Цикл
		
			Если ЗначениеЗаполнено(СтрокаТаб.Удалить_СуммаОперацииВВалютеДоговораНСБУ) Тогда
			
				Если (СтрокаТаб.СуммаДт = 0) И (СтрокаТаб.СуммаКт = 0) Тогда
				
					Если СтрокаТаб.СчетДтМСФО.Валютный = Истина Тогда
						
						СтрокаТаб.ВалютаДт = СтрокаТаб.Удалить_Валюта;
						СтрокаТаб.СуммаДт = СтрокаТаб.Удалить_СуммаОперацииВВалютеДоговораНСБУ;
						
						ЕстьИзменения = Истина;
						
					КонецЕсли;					
					
					Если СтрокаТаб.СчетКтМСФО.Валютный = Истина Тогда
						
						СтрокаТаб.ВалютаКт = СтрокаТаб.Удалить_Валюта;
						СтрокаТаб.СуммаКт = СтрокаТаб.Удалить_СуммаОперацииВВалютеДоговораНСБУ;
						
						ЕстьИзменения = Истина;
						
					КонецЕсли;					
					
				КонецЕсли;
			
			КонецЕсли;
		
		КонецЦикла;
		
		Если Не ЕстьИзменения Тогда
			Продолжить;
		КонецЕсли;
		
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ТекущийОбъект);
		
	КонецЦикла; 

КонецПроцедуры

#КонецОбласти

#КонецЕсли