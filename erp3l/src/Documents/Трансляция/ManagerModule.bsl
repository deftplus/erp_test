#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

// Возвращает ссылку на документ Трансляция по указанным реквизитам. Когда документ не найден -
// возвращает пустую ссылку.
Функция ПолучитьЭкземплярТрансляции(ПериодВход, СценарийВход, ОрганизацияВход, ШаблонКорректировкиВход, ОсновнаяВалютаВход = Неопределено) Экспорт
	РезультатФункции = Документы.Трансляция.ПустаяСсылка();
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Трансляция.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.Трансляция КАК Трансляция
		|ГДЕ
		|	НЕ Трансляция.ПометкаУдаления
		|	И Трансляция.ПериодОтчета = &ПериодОтчета
		|	И Трансляция.Сценарий = &Сценарий
		|	И ВЫБОР
		|			КОГДА &ОсновнаяВалюта <> НЕОПРЕДЕЛЕНО
		|				ТОГДА Трансляция.ОсновнаяВалюта = &ОсновнаяВалюта
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|	И Трансляция.Организация = &Организация
		|	И Трансляция.ШаблонКорректировки = &ШаблонКорректировки";
	Запрос.УстановитьПараметр("Организация", ОрганизацияВход);
	Запрос.УстановитьПараметр("ОсновнаяВалюта", ОсновнаяВалютаВход);
	Запрос.УстановитьПараметр("ПериодОтчета", ПериодВход);
	Запрос.УстановитьПараметр("Сценарий", СценарийВход);
	Запрос.УстановитьПараметр("ШаблонКорректировки", ШаблонКорректировкиВход);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		РезультатФункции = ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла;
	Возврат РезультатФункции;
КонецФункции		 // ПолучитьЭкземплярТрансляции()

#КонецЕсли