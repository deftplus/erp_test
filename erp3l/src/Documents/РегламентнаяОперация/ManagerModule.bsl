#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		РеглУчетПроведениеСервер.ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
	
КонецПроцедуры

// Добавляет команду создания документа "Регламентная операция".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.РегламентнаяОперация) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РегламентнаяОперация.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.РегламентнаяОперация);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьРеглУчет";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
	

		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ЗаполнениеПустыхВременныхТаблицДляОтраженияВРеглУчете

Функция ТекстЗапросаПустаяВременнаяТаблицаРеклассификацияРасчетыПоФинИнструментам() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК Договор,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК Валюта,
	|	ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка) КАК ГруппаФинансовогоУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ПустаяСсылка) КАК ТипДоговора,
	|	ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.ПустаяСсылка) КАК ХарактерДоговора,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ПустаяСсылка) КАК ТипСуммы,
	|	ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаКонтрагентов.ПустаяСсылка) КАК БанковскийСчетКонтрагента,
	|	0 КАК СуммаРегл,
	|	0 КАК СуммаУпр,
	|	0 КАК СуммаЗадолженности	
	|ПОМЕСТИТЬ вт_РеклассификацияРасчетыПоФинИнструментам
	|ГДЕ
	|	ЛОЖЬ";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПустаяВременнаяТаблицаРеклассификацияРасчетыСКлиентами() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК Валюта,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК Договор,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка) КАК ГруппаФинансовогоУчета,
	|	ЛОЖЬ КАК ЭтоВозврат,
	|	0 КАК ПредоплатаРегл,
	|	0 КАК ПредоплатаУпр,
	|	0 КАК ДолгРегл,
	|	0 КАК ДолгУпр,
	|	0 КАК Долг,
	|	0 КАК Предоплата
	|ПОМЕСТИТЬ вт_РеклассификацияРасчетыСКлиентами
	|ГДЕ
	|	ЛОЖЬ";
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаПустаяВременнаяТаблицаРеклассификацияРасчетыСПоставщиками() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК Валюта,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК Договор,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка) КАК ГруппаФинансовогоУчета,
	|	ЛОЖЬ КАК ЭтоВозврат,
	|	0 КАК ПредоплатаРегл,
	|	0 КАК ПредоплатаУпр,
	|	0 КАК ДолгРегл,
	|	0 КАК ДолгУпр,
	|	0 КАК Долг,
	|	0 КАК Предоплата
	|ПОМЕСТИТЬ вт_РеклассификацияРасчетыСПоставщиками
	|ГДЕ
	|	ЛОЖЬ";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаПустаяВременнаяТаблицаРеклассификацияРасчетыПоАренде() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК Валюта,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК Договор,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка) КАК ГруппаФинансовогоУчета,
	|	ЛОЖЬ КАК ЭтоВозврат,
	|	0 КАК Сумма,
	|	0 КАК СуммаРегл,
	|	0 КАК СуммаУпр
	|ПОМЕСТИТЬ вт_РеклассификацияРасчетыПоАренде
	|ГДЕ
	|	ЛОЖЬ";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаПустаяВременнаяТаблицаРеклассификацияРасчетыПоДисконтированию() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК Валюта,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК Договор,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка) КАК ГруппаФинансовогоУчета,
	|	0 КАК Долг,
	|	0 КАК ДолгРегл,
	|	0 КАК ДолгУпр
	|ПОМЕСТИТЬ вт_РеклассификацияРасчетыПоДисконтированию
	|ГДЕ
	|	ЛОЖЬ";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаПустаяВременнаяТаблицаКосвенныхРасходов() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НЕОПРЕДЕЛЕНО КАК Регистратор,
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	0 КАК СуммаРеглОСНО,
	|	0 КАК СуммаРеглЕНВД,
	|	0 КАК СуммаУпрОСНО,
	|	0 КАК СуммаУпрЕНВД,
	|	
	|	0 КАК ПостояннаяРазницаОСНО,
	|	0 КАК ПостояннаяРазницаЕНВД,
	|	
	|	0 КАК ВременнаяРазницаОСНО,
	|	0 КАК ВременнаяРазницаЕНВД
	|
	|ПОМЕСТИТЬ КосвенныеРасходы
	|ГДЕ
	|	ЛОЖЬ";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиЭтаповЗакрытияМесяца
//++ НЕ УТ
#Область РасчетКурсовыхРазниц

// Добавляет этап в таблицу этапов закрытия месяца.
// Элементы данной таблицы являются элементами второго уровня в дереве этапов в форме закрытия месяца.
//
// Параметры:
// 	ТаблицаЭтапов - (См. Обработки.ОперацииЗакрытияМесяца.ЗаполнитьОписаниеЭтаповЗакрытияМесяца)
// 	ТекущийРодитель - Строка - идентификатор группы.
Процедура ДобавитьЭтап_РасчетКурсовыхРазниц(ТаблицаЭтапов,ТекущийРодитель) Экспорт
	
	НоваяСтрока = ЗакрытиеМесяцаСервер.ДобавитьЭтапВТаблицу(ТаблицаЭтапов, ТекущийРодитель,
		Перечисления.ОперацииЗакрытияМесяца.РасчетКурсовыхРазниц,
		Ложь, Истина, Ложь,
		Перечисления.ОперацииЗакрытияМесяца.РеклассификацияДолгосрочныхАктивовОбязательств);
	НоваяСтрока.ПредшествующиеЭтапы.Добавить(Перечисления.ОперацииЗакрытияМесяца.РасчетПартийИСебестоимости);
	НоваяСтрока.ПредшествующиеЭтапы.Добавить(Перечисления.ОперацииЗакрытияМесяца.ОтражениеДокументовВРегламентированномУчете);
	НоваяСтрока.ПредшествующиеЭтапы.Добавить(Перечисления.ОперацииЗакрытияМесяца.ФормированиеРезервовПоСомнительнымДолгам);
	НоваяСтрока.ТекстВыполнить = НСтр("ru = 'Рассчитать';
										|en = 'Calculate'");
	НоваяСтрока.ДействиеИспользование = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"Документы.РегламентнаяОперация.Использование_РасчетКурсовыхРазниц");
	НоваяСтрока.ДействиеВыполнить  = ЗакрытиеМесяцаСервер.ОписаниеДействия_ВыполнитьРасчет(
		"Документы.РегламентнаяОперация.Выполнить_РасчетКурсовыхРазниц");
	НоваяСтрока.ДействиеПодробнее = ЗакрытиеМесяцаЛокализация.ОписаниеДействия_ОткрытьСписокДокументовРегламентнаяОперация();
	НоваяСтрока.ТипыРегламентныхОпераций.Добавить(Перечисления.ТипыРегламентныхОпераций.РасчетКурсовыхРазниц);
	
КонецПроцедуры

// Обработчики этапа.

Процедура Использование_РасчетКурсовыхРазниц(ПараметрыОбработчика) Экспорт
	
	ЗакрытиеМесяцаСервер.ПроверитьИспользованиеРегламентированногоУчета(ПараметрыОбработчика);
	
	Если ЗакрытиеМесяцаСервер.РасчетЭтапаНеТребуется(ПараметрыОбработчика.ДанныеЭтапа) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(Запрос, ПараметрыОбработчика);
	
	Запрос.УстановитьПараметр("ВестиУУНаПланеСчетов", 
		ПолучитьФункциональнуюОпцию("ВестиУУНаПланеСчетовХозрасчетный")
		И НачалоМесяца(ПараметрыОбработчика.ПараметрыРасчета.Период) >= Константы.ДатаНачалаУУНаПланеСчетовХозрасчетный.Получить());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка,
	|	Хозрасчетный.Валютный,
	|	Хозрасчетный.НалоговыйУчет,
	|	Хозрасчетный.ИсключитьСуммуБУИзПереоценкиПоПлануСчетов,
	|	Хозрасчетный.ИсключитьСуммуУУИзПереоценкиПоПлануСчетов
	|ПОМЕСТИТЬ втСчета
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Валютный
	|	И НЕ (Хозрасчетный.ИсключитьСуммуБУИзПереоценкиПоПлануСчетов
	|			ИЛИ &ВестиУУНаПланеСчетов И Хозрасчетный.ИсключитьСуммуУУИзПереоценкиПоПлануСчетов)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	Т.Счет                    КАК Счет,
	|	Т.Организация             КАК Организация,
	|	Т.Подразделение           КАК Подразделение,
	|	Т.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Т.Субконто1               КАК Субконто1,
	|	Т.Субконто2               КАК Субконто2,
	|	Т.Субконто3               КАК Субконто3,
	|	Т.Валюта                  КАК Валюта,
	|	Т.ВалютнаяСуммаОстаток    КАК ВалютнаяСуммаОстаток,
	|	Т.СуммаОстаток            КАК СуммаБУОстаток,
	|	Т.СуммаУУОстаток          КАК СуммаУУОстаток
	|ПОМЕСТИТЬ втОстатки
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ГраницаКонецПериода,
	|			Счет В (ВЫБРАТЬ Т.Ссылка ИЗ втСчета КАК Т),
	|			,
	|			Организация В (&МассивОрганизаций)) КАК Т
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Организация
	|ИЗ
	|	втОстатки КАК Т";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ЗакрытиеМесяцаСервер.УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыОбработчика, РезультатЗапроса.Выгрузить().Количество());
	
	Если РезультатЗапроса.Пустой() Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеТребуется(
			ПараметрыОбработчика,
			НСтр("ru = 'Нет остатков по переоцениваемым валютным счетам в регистре бухгалтерии ""Хозрасчетный"".';
				|en = 'No balance for revalued currency accounts in the Self-financing accounting register.'", ОбщегоНазначения.КодОсновногоЯзыка()));
		Возврат;
		
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.Период,
	|	Т.Валюта,
	|	Т.БазоваяВалюта,
	|	Т.КурсЧислитель,
	|	Т.КурсЗнаменатель
	|ПОМЕСТИТЬ втКурсы
	|ИЗ
	|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&КонецПериода, ) КАК Т
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Период,
	|	Т.Валюта,
	|	Т.БазоваяВалюта,
	|	Т.КурсЧислитель,
	|	Т.КурсЗнаменатель
	|ПОМЕСТИТЬ втКурсУУ
	|ИЗ
	|	втКурсы КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО Т.Валюта = Константы.ВалютаУправленческогоУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Данные.Счет                      КАК Счет,
	|	Данные.Организация               КАК Организация,
	|	Данные.Подразделение             КАК Подразделение,
	|	Данные.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|	Данные.Субконто1                 КАК Субконто1,
	|	Данные.Субконто2                 КАК Субконто2,
	|	Данные.Субконто3                 КАК Субконто3,
	|	Данные.Валюта                    КАК Валюта,
	|	Данные.ОстатокВалюты             КАК ОстатокВалюты,
	|	
	|	СУММА(Данные.ОстатокРегл)        КАК ОстатокРегл,
	|	СУММА(Данные.ОстатокПоКурсу)     КАК ОстатокПоКурсу,
	|	СУММА(Данные.АбсолютнаяРазница)  КАК АбсолютнаяРазница,
	|	СУММА(Данные.КурсоваяРазница)    КАК КурсоваяРазница,
	|	
	|	СУММА(Данные.ОстатокПоКурсуУУ)    КАК ОстатокПоКурсуУУ,
	|	СУММА(Данные.АбсолютнаяРазницаУУ) КАК АбсолютнаяРазницаУУ,
	|	СУММА(Данные.КурсоваяРазницаУУ)   КАК КурсоваяРазницаУУ
	|ПОМЕСТИТЬ ДанныеДляПереоценки
	|ИЗ
	|	(ВЫБРАТЬ
	|		Остатки.Счет,
	|		Остатки.Организация,
	|		Остатки.Подразделение,
	|		Остатки.НаправлениеДеятельности,
	|		Остатки.Субконто1,
	|		Остатки.Субконто2,
	|		Остатки.Субконто3,
	|		Остатки.Валюта,
	|		Остатки.ВалютнаяСуммаОстаток КАК ОстатокВалюты,
	|		
	|		Остатки.СуммаБУОстаток КАК ОстатокРегл,
	|		Остатки.ВалютнаяСуммаОстаток * Курсы.КурсЧислитель / Курсы.КурсЗнаменатель КАК ОстатокПоКурсу,
	|		Остатки.ВалютнаяСуммаОстаток * Курсы.КурсЧислитель / Курсы.КурсЗнаменатель - Остатки.СуммаБУОстаток КАК АбсолютнаяРазница,
	|		ВЫРАЗИТЬ(Остатки.ВалютнаяСуммаОстаток * Курсы.КурсЧислитель / Курсы.КурсЗнаменатель КАК ЧИСЛО(31,2)) - Остатки.СуммаБУОстаток КАК КурсоваяРазница,
	|		
	|		0 КАК ОстатокПоКурсуУУ,
	|		0 КАК АбсолютнаяРазницаУУ,
	|		0 КАК КурсоваяРазницаУУ
	|	
	|	ИЗ
	|		ВтОстатки КАК Остатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсы КАК Курсы
	|			ПО Остатки.Валюта = Курсы.Валюта
	|				И Остатки.Организация.ВалютаРегламентированногоУчета = Курсы.БазоваяВалюта
	|	ГДЕ
	|		НЕ Остатки.Счет.ИсключитьСуммуБУИзПереоценкиПоПлануСчетов
	|		И ВЫРАЗИТЬ(Остатки.ВалютнаяСуммаОстаток * Курсы.КурсЧислитель / Курсы.КурсЗнаменатель КАК ЧИСЛО(31,2)) <> Остатки.СуммаБУОстаток
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Остатки.Счет,
	|		Остатки.Организация,
	|		Остатки.Подразделение,
	|		Остатки.НаправлениеДеятельности,
	|		Остатки.Субконто1,
	|		Остатки.Субконто2,
	|		Остатки.Субконто3,
	|		Остатки.Валюта,
	|		Остатки.ВалютнаяСуммаОстаток КАК ОстатокВалюты,
	|		
	|		0 КАК ОстатокРегл,
	|		0 КАК ОстатокПоКурсу,
	|		0 КАК АбсолютнаяРазница,
	|		0 КАК КурсоваяРазница,
	|		
	|		Остатки.ВалютнаяСуммаОстаток * Курсы.КурсЧислитель / Курсы.КурсЗнаменатель / КурсУУ.КурсЧислитель * КурсУУ.КурсЗнаменатель КАК ОстатокПоКурсуУУ,
	|		Остатки.ВалютнаяСуммаОстаток * Курсы.КурсЧислитель / Курсы.КурсЗнаменатель / КурсУУ.КурсЧислитель * КурсУУ.КурсЗнаменатель - Остатки.СуммаУУОстаток КАК АбсолютнаяРазницаУУ,
	|		ВЫРАЗИТЬ(Остатки.ВалютнаяСуммаОстаток * Курсы.КурсЧислитель / Курсы.КурсЗнаменатель / КурсУУ.КурсЧислитель * КурсУУ.КурсЗнаменатель КАК ЧИСЛО(31,2)) - Остатки.СуммаУУОстаток КАК КурсоваяРазницаУУ
	|	
	|	ИЗ
	|		ВтОстатки КАК Остатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсы КАК Курсы
	|			ПО Остатки.Валюта = Курсы.Валюта
	|				И Остатки.Организация.ВалютаРегламентированногоУчета = Курсы.БазоваяВалюта
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсУУ КАК КурсУУ
	|			ПО Остатки.Организация.ВалютаРегламентированногоУчета = КурсУУ.БазоваяВалюта
	|	ГДЕ
	|		&ВестиУУНаПланеСчетов
	|		И НЕ Остатки.Счет.ИсключитьСуммуУУИзПереоценкиПоПлануСчетов
	|		И ВЫРАЗИТЬ(Остатки.ВалютнаяСуммаОстаток * Курсы.КурсЧислитель / Курсы.КурсЗнаменатель / КурсУУ.КурсЧислитель * КурсУУ.КурсЗнаменатель КАК ЧИСЛО(31,2)) <> Остатки.СуммаУУОстаток
	|	) КАК Данные
	|
	|СГРУППИРОВАТЬ ПО
	|	Данные.Счет,
	|	Данные.Организация,
	|	Данные.Подразделение,
	|	Данные.НаправлениеДеятельности,
	|	Данные.Субконто1,
	|	Данные.Субконто2,
	|	Данные.Субконто3,
	|	Данные.Валюта,
	|	Данные.ОстатокВалюты
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Организация
	|ИЗ
	|	ДанныеДляПереоценки КАК Т
	|
	|УПОРЯДОЧИТЬ ПО
	|	Т.Организация";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ЗакрытиеМесяцаСервер.УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыОбработчика, Выборка.Количество());
	
	Пока Выборка.Следующий() Цикл
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеВыполнен(
			ПараметрыОбработчика,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'По организации ""%1"" не выполнен расчет курсовых разниц.';
					|en = 'Exchange rate difference is not calculated for the ""%1"" company.'", ОбщегоНазначения.КодОсновногоЯзыка()),
				Выборка.Организация));
		
	КонецЦикла;
	
КонецПроцедуры

Процедура Выполнить_РасчетКурсовыхРазниц(ПараметрыОбработчика) Экспорт
	
	ЗакрытиеМесяцаЛокализация.СформироватьДокументРегламентнаяОперация(ПараметрыОбработчика, ПараметрыОбработчика.ПараметрыРасчета.МассивОрганизаций);
	
КонецПроцедуры

#КонецОбласти

#Область РасчетТорговогоСбора

// Добавляет этап в таблицу этапов закрытия месяца.
// Элементы данной таблицы являются элементами второго уровня в дереве этапов в форме закрытия месяца.
//
// Параметры:
// 	ТаблицаЭтапов - (См. Обработки.ОперацииЗакрытияМесяца.ЗаполнитьОписаниеЭтаповЗакрытияМесяца)
// 	ТекущийРодитель - Строка - идентификатор группы.
Процедура ДобавитьЭтап_РасчетТорговогоСбора(ТаблицаЭтапов,ТекущийРодитель) Экспорт
	НоваяСтрока = ЗакрытиеМесяцаСервер.ДобавитьЭтапВТаблицу(ТаблицаЭтапов, ТекущийРодитель,
		Перечисления.ОперацииЗакрытияМесяца.РасчетТорговогоСбора,,,,
		Перечисления.ОперацииЗакрытияМесяца.РасчетЗемельногоНалога);
	НоваяСтрока.ТекстВыполнить = НСтр("ru = 'Рассчитать';
										|en = 'Calculate'");
	НоваяСтрока.ДействиеИспользование = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"Документы.РегламентнаяОперация.Использование_РасчетТорговогоСбора");
	НоваяСтрока.ДействиеВыполнить  = ЗакрытиеМесяцаСервер.ОписаниеДействия_ВыполнитьРасчет(
		"Документы.РегламентнаяОперация.Выполнить_РасчетТорговогоСбора");
	НоваяСтрока.ДействиеПодробнее = ЗакрытиеМесяцаЛокализация.ОписаниеДействия_ОткрытьСписокДокументовРегламентнаяОперация();
	НоваяСтрока.ТипыРегламентныхОпераций.Добавить(Перечисления.ТипыРегламентныхОпераций.РасчетТорговогоСбора);
КонецПроцедуры

// Обработчики этапа.

Процедура Использование_РасчетТорговогоСбора(ПараметрыОбработчика) Экспорт
	
	ЗакрытиеМесяцаСервер.УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыОбработчика, 1);
	
	Если НЕ ПолучитьФункциональнуюОпцию("УплачиваетсяТорговыйСбор") Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеОтключено(
			ПараметрыОбработчика,
			НСтр("ru = 'Учет торгового сбора не ведется.';
				|en = 'Sales charge accounting is not kept.'", ОбщегоНазначения.КодОсновногоЯзыка()));
			
		Возврат;
		
	КонецЕсли;
	
	ЗакрытиеМесяцаСервер.ПроверитьИспользованиеРегламентированногоУчета(ПараметрыОбработчика);
	
	Если ЗакрытиеМесяцаСервер.РасчетЭтапаНеТребуется(ПараметрыОбработчика.ДанныеЭтапа) Тогда
		Возврат;
	КонецЕсли;
	
	МесяцРасчета = Месяц(ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации);
	Если МесяцРасчета % 3 <> 0 Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеТребуется(
			ПараметрыОбработчика,
			НСтр("ru = 'Торговый сбор рассчитывается в последнем месяце квартала.';
				|en = 'Sales charge is calculated in the last month of the quarter.'", ОбщегоНазначения.КодОсновногоЯзыка()));
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(Запрос, ПараметрыОбработчика);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Организация КАК Ссылка
	|ПОМЕСТИТЬ ВТОрганизацииДляРасчета
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ПараметрыТорговыхТочекСрезПоследних.Организация КАК Организация
	|	ИЗ
	|		РегистрСведений.ПараметрыТорговыхТочек.СрезПоследних(&КонецПериода, Организация В (&МассивОрганизаций)) КАК ПараметрыТорговыхТочекСрезПоследних
	|	ГДЕ
	|		ПараметрыТорговыхТочекСрезПоследних.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийТорговыеТочки.СнятиеСУчета)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ПараметрыТорговыхТочек.Организация
	|	ИЗ
	|		РегистрСведений.ПараметрыТорговыхТочек КАК ПараметрыТорговыхТочек
	|	ГДЕ
	|		ПараметрыТорговыхТочек.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И ПараметрыТорговыхТочек.Организация В(&МассивОрганизаций)) КАК Т";
	
	Запрос.Выполнить();
	
	Если ЗакрытиеМесяцаСервер.РазмерВременнойТаблицы(Запрос, "ВТОрганизацииДляРасчета", ПараметрыОбработчика) = 0 Тогда
			
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеТребуется(
			ПараметрыОбработчика,
			НСтр("ru = 'Нет торговых точек для расчета налога.';
				|en = 'No sales outlets to calculate taxes.'", ОбщегоНазначения.КодОсновногоЯзыка()));
		Возврат;
		
	КонецЕсли;
	
	ВведеныРегламентныеДокументы = ЗакрытиеМесяцаСервер.ПроверитьНаличиеДокументаРегламентнаяОперация(ПараметрыОбработчика,, "ВТОрганизацииДляРасчета", Ложь);
	
КонецПроцедуры

Процедура Выполнить_РасчетТорговогоСбора(ПараметрыОбработчика) Экспорт
	
	ЗакрытиеМесяцаЛокализация.СформироватьДокументРегламентнаяОперация(ПараметрыОбработчика, ПараметрыОбработчика.ПараметрыРасчета.МассивОрганизаций);
	
КонецПроцедуры

#КонецОбласти
//-- НЕ УТ
#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроводкиРеглУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	ТекстЗапроса - Строка - Текст запроса отражения в регл. учете.
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	ТекстыОтражения = Новый Массив;
	
#Область СписаниеКосвенныхРасходовОСНО
	ТекстЗапроса = "
	|ВЫБРАТЬ 
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	КосвенныеРасходы.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	КосвенныеРасходы.СуммаРеглОСНО КАК Сумма,
	|	КосвенныеРасходы.СуммаУпрОСНО КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СписаниеРасходовОСНО) КАК ВидСчетаДт,
	|	КосвенныеРасходы.СтатьяРасходов КАК АналитикаУчетаДт,
	|	КосвенныеРасходы.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	КосвенныеРасходы.Подразделение КАК ПодразделениеДт,
	|	КосвенныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	КосвенныеРасходы.СтатьяРасходов КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	КосвенныеРасходы.СуммаРеглОСНО - КосвенныеРасходы.ПостояннаяРазницаОСНО - КосвенныеРасходы.ВременнаяРазницаОСНО КАК СуммаНУДт,
	|	КосвенныеРасходы.ПостояннаяРазницаОСНО КАК СуммаПРДт,
	|	КосвенныеРасходы.ВременнаяРазницаОСНО КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	КосвенныеРасходы.СтатьяРасходов КАК АналитикаУчетаКт,
	|	КосвенныеРасходы.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	КосвенныеРасходы.Подразделение КАК ПодразделениеКт,
	|	КосвенныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	КосвенныеРасходы.СтатьяРасходов КАК СубконтоКт1,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	КосвенныеРасходы.СуммаРеглОСНО - КосвенныеРасходы.ПостояннаяРазницаОСНО - КосвенныеРасходы.ВременнаяРазницаОСНО КАК СуммаНУКт,
	|	КосвенныеРасходы.ПостояннаяРазницаОСНО КАК СуммаПРКт,
	|	КосвенныеРасходы.ВременнаяРазницаОСНО КАК СуммаВРКт,
	|	""Списание косвенных расходов (ОСНО)"" КАК Содержание
	|
	|ИЗ
	|	Документ.РегламентнаяОперация КАК Операция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		КосвенныеРасходы КАК КосвенныеРасходы
	|	ПО
	|		Операция.Ссылка = КосвенныеРасходы.Регистратор
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.СписаниеКосвенныхРасходов)
	|	И (КосвенныеРасходы.СуммаРеглОСНО <> 0 
	|		ИЛИ КосвенныеРасходы.ПостояннаяРазницаОСНО <> 0
	|		ИЛИ КосвенныеРасходы.ВременнаяРазницаОСНО <> 0)
	|";
	ТекстыОтражения.Добавить(ТекстЗапроса);
#КонецОбласти
	
#Область СписаниеКосвенныхРасходовЕНВД
	ТекстЗапроса = "
	|ВЫБРАТЬ 
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	КосвенныеРасходы.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	КосвенныеРасходы.СуммаРеглЕНВД КАК Сумма,
	|	КосвенныеРасходы.СуммаУпрЕНВД КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СписаниеРасходовЕНВД) КАК ВидСчетаДт,
	|	КосвенныеРасходы.СтатьяРасходов КАК АналитикаУчетаДт,
	|	КосвенныеРасходы.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	КосвенныеРасходы.Подразделение КАК ПодразделениеДт,
	|	КосвенныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	КосвенныеРасходы.СтатьяРасходов КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	КосвенныеРасходы.СуммаРеглЕНВД - КосвенныеРасходы.ПостояннаяРазницаЕНВД - КосвенныеРасходы.ВременнаяРазницаЕНВД КАК СуммаНУДт,
	|	КосвенныеРасходы.ПостояннаяРазницаЕНВД КАК СуммаПРДт,
	|	КосвенныеРасходы.ВременнаяРазницаЕНВД КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	КосвенныеРасходы.СтатьяРасходов КАК АналитикаУчетаКт,
	|	КосвенныеРасходы.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	КосвенныеРасходы.Подразделение КАК ПодразделениеКт,
	|	КосвенныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	КосвенныеРасходы.СтатьяРасходов КАК СубконтоКт1,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	КосвенныеРасходы.СуммаРеглЕНВД - КосвенныеРасходы.ПостояннаяРазницаЕНВД - КосвенныеРасходы.ВременнаяРазницаЕНВД КАК СуммаНУКт,
	|	КосвенныеРасходы.ПостояннаяРазницаЕНВД КАК СуммаПРКт,
	|	КосвенныеРасходы.ВременнаяРазницаЕНВД КАК СуммаВРКт,
	|	""Списание косвенных расходов (ЕНВД)"" КАК Содержание
	|
	|ИЗ
	|	Документ.РегламентнаяОперация КАК Операция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		КосвенныеРасходы КАК КосвенныеРасходы
	|	ПО
	|		Операция.Ссылка = КосвенныеРасходы.Регистратор
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.СписаниеКосвенныхРасходов)
	|	И (КосвенныеРасходы.СуммаРеглЕНВД <> 0 
	|		ИЛИ КосвенныеРасходы.ПостояннаяРазницаЕНВД <> 0
	|		ИЛИ КосвенныеРасходы.ВременнаяРазницаЕНВД <> 0)
	|";
	ТекстыОтражения.Добавить(ТекстЗапроса);
#КонецОбласти

#Область НачислениеИмущественныхНалогов
	ТекстЗапроса = "
	|ВЫБРАТЬ 
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	РасходыПоНалогу.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	РасходыПоНалогу.СуммаРегл КАК Сумма,
	|	РасходыПоНалогу.СуммаУпр КАК СуммаУУ,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	РасходыПоНалогу.СтатьяРасходов КАК АналитикаУчетаДт,
	|	РасходыПоНалогу.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	РасходыПоНалогу.Подразделение КАК ПодразделениеДт,
	|	РасходыПоНалогу.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	РасходыПоНалогу.СтатьяРасходов КАК СубконтоДт1,
	|	РасходыПоНалогу.АналитикаРасходов КАК СубконтоДт2,
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL
	|		ТОГДА 
	|				ВЫБОР КОГДА ОбъектыСтроительства.СпособСтроительства <> ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка) 
	|						ТОГДА ОбъектыСтроительства.СпособСтроительства
	|					ИНАЧЕ 
	|						ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Хозспособ) 
	|				КОНЕЦ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	РасходыПоНалогу.СуммаРегл - РасходыПоНалогу.ПостояннаяРазница - РасходыПоНалогу.ВременнаяРазница КАК СуммаНУДт,
	|	РасходыПоНалогу.ПостояннаяРазница КАК СуммаПРДт,
	|	РасходыПоНалогу.ВременнаяРазница КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	&ВалютаРеглУчета КАК ВалютаКт,
	|	РасходыПоНалогу.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ВЫБОР КОГДА Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РасчетНалогаНаИмущество) ТОГДА
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НалогНаИмущество)
	|	КОГДА Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РасчетТранспортногоНалога) ТОГДА
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТранспортныйНалог)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ЗемельныйНалог)
	|	КОНЕЦ КАК СчетКт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.Налог) КАК СубконтоКт1,
	|	РасходыПоНалогу.РегистрацияВНалоговомОргане КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	РасходыПоНалогу.СуммаРегл КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	РасходыПоНалогу.СуммаРегл - РасходыПоНалогу.ПостояннаяРазница - РасходыПоНалогу.ВременнаяРазница КАК СуммаНУКт,
	|	РасходыПоНалогу.ПостояннаяРазница КАК СуммаПРКт,
	|	РасходыПоНалогу.ВременнаяРазница КАК СуммаВРКт,
	|	""Начисление имущественных налогов"" КАК Содержание
	|
	|ИЗ
	|	Документ.РегламентнаяОперация КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы КАК РасходыПоНалогу
	|		ПО Операция.Ссылка = РасходыПоНалогу.Регистратор
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО РасходыПоНалогу.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|	ПО ОбъектыСтроительства.Ссылка = РасходыПоНалогу.АналитикаРасходов
	|
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РасчетНалогаНаИмущество),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РасчетТранспортногоНалога),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РасчетЗемельногоНалога))
	|	И РасходыПоНалогу.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРБП)
	|";
	ТекстыОтражения.Добавить(ТекстЗапроса);
#КонецОбласти
	
#Область ТекстРасходыНаПлатон
	ТекстЗапроса = " // Поступление услуг (Дт <25, 26, 44, 20, 23, 08.3, 20> :: Кт <97.21> )
	|ВЫБРАТЬ 
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	РасходыПоНалогу.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	РасходыПоНалогу.СуммаРегл КАК Сумма,
	|	РасходыПоНалогу.СуммаУпр КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	РасходыПоНалогу.СтатьяРасходов КАК АналитикаУчетаДт,
	|	РасходыПоНалогу.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	РасходыПоНалогу.Подразделение КАК ПодразделениеДт,
	|	РасходыПоНалогу.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	РасходыПоНалогу.СтатьяРасходов КАК СубконтоДт1,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоДт2,
	|	РасходыПоНалогу.АналитикаРасходов КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	РасходыПоНалогу.СуммаРегл - РасходыПоНалогу.ПостояннаяРазница - РасходыПоНалогу.ВременнаяРазница КАК СуммаНУДт,
	|	РасходыПоНалогу.ПостояннаяРазница КАК СуммаПРДт,
	|	РасходыПоНалогу.ВременнаяРазница КАК СуммаВРДт,
	|
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.РасходыНаПлатон) КАК АналитикаУчетаКт,
	|	РасходыПоНалогу.Подразделение КАК МестоУчетаКт,
	|
	|	&ВалютаРеглУчета КАК ВалютаКт,
	|	РасходыПоНалогу.Подразделение КАК ПодразделениеКт,
	|	РасходыПоНалогу.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.РасходыНаПлатон) КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	РасходыПоНалогу.СуммаРегл КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	РасходыПоНалогу.СуммаРегл - РасходыПоНалогу.ВременнаяРазница КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	РасходыПоНалогу.ВременнаяРазница КАК СуммаВРКт,
	|	""Отражение в расходах платы по системе """"Платон"""""" КАК Содержание
	|
	|ИЗ
	|	Документ.РегламентнаяОперация КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы КАК РасходыПоНалогу
	|		ПО Операция.Ссылка = РасходыПоНалогу.Регистратор
	|			И РасходыПоНалогу.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РасчетТранспортногоНалога)
	|	И РасходыПоНалогу.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРБП)
	|";
	ТекстыОтражения.Добавить(ТекстЗапроса);
#КонецОбласти
	
#Область НачислениеТорговогоСбора
	ТекстЗапроса = "
	|ВЫБРАТЬ 
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	КосвенныеРасходы.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	КосвенныеРасходы.СуммаРегл КАК Сумма,
	|	КосвенныеРасходы.СуммаУпр КАК СуммаУУ,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	КосвенныеРасходы.СтатьяРасходов КАК АналитикаУчетаДт,
	|	КосвенныеРасходы.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	КосвенныеРасходы.Подразделение КАК ПодразделениеДт,
	|	КосвенныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	КосвенныеРасходы.СтатьяРасходов КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	КосвенныеРасходы.СуммаРегл - КосвенныеРасходы.ПостояннаяРазница - КосвенныеРасходы.ВременнаяРазница КАК СуммаНУДт,
	|	КосвенныеРасходы.ПостояннаяРазница КАК СуммаПРДт,
	|	КосвенныеРасходы.ВременнаяРазница КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	&ВалютаРеглУчета КАК ВалютаКт,
	|	КосвенныеРасходы.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТорговыйСбор) КАК СчетКт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.Налог) КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	КосвенныеРасходы.СуммаРегл КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	КосвенныеРасходы.СуммаРегл - КосвенныеРасходы.ПостояннаяРазница - КосвенныеРасходы.ВременнаяРазница КАК СуммаНУКт,
	|	КосвенныеРасходы.ПостояннаяРазница КАК СуммаПРКт,
	|	КосвенныеРасходы.ВременнаяРазница КАК СуммаВРКт,
	|	""Начисление торгового сбора"" КАК Содержание
	|
	|ИЗ
	|	Документ.РегламентнаяОперация КАК Операция
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы КАК КосвенныеРасходы
	|		ПО Операция.Ссылка = КосвенныеРасходы.Регистратор
	|
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РасчетТорговогоСбора)
	|";
	ТекстыОтражения.Добавить(ТекстЗапроса);
#КонецОбласти
	
#Область РасходыПоАренднымПлатежамНУ
	ТекстЗапроса = "
	|ВЫБРАТЬ // Расходы по арендным платежам НУ (Дт <25, 26, 44, 20, 23> :: Кт <01.К>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Строки.ПодразделениеЗатрат КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.ПодразделениеЗатрат КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельностиЗатрат КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.РасходыЛизинговыеПлатежи) КАК СубконтоДт1,
	|	Строки.СтатьяРасходов КАК СубконтоДт2, 
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Хозспособ) 
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СуммаПлатежаНУ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	-Строки.СуммаПлатежаНУ КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КорректировкаСтоимостиАрендованногоИмущества) КАК СчетКт,
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СуммаПлатежаНУ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	-Строки.СуммаПлатежаНУ КАК СуммаВРКт,
	|	""Услуга по аренде за вычетом амортизации признана в расходах налогового учета"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РегламентнаяОперация КАК Операция
	|		ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРасходыПоАренднымПлатежам КАК Строки
	|		ПО ИСТИНА
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|		ПО Строки.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|			И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|	
	|ГДЕ
	|	Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.ПризнаниеВНУАрендныхПлатежей)
	|	И Строки.СуммаПлатежаНУ <> 0
	|
	|";
	ТекстыОтражения.Добавить(ТекстЗапроса);
#КонецОбласти

#Область КорректировкаАмортизацииПоАренднымПлатежамНУ
	ТекстЗапроса = "
	|ВЫБРАТЬ // Коррректировка расходов по амортизации (Дт <25, 26, 44, 20, 23> :: Кт <01.К>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Строки.ПодразделениеЗатрат КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.ПодразделениеЗатрат КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельностиЗатрат КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.СтатьяРасходов КАК СубконтоДт1,
	|	Строки.АналитикаРасходов КАК СубконтоДт2, 
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА 
	|				ВЫБОР КОГДА ОбъектыСтроительства.СпособСтроительства <> ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка) 
	|						ТОГДА ОбъектыСтроительства.СпособСтроительства
	|					ИНАЧЕ 
	|						ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный) 
	|				КОНЕЦ
	|		ИНАЧЕ 
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) 
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.КорректировкаАмортизацииНУ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	-Строки.КорректировкаАмортизацииНУ КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КорректировкаСтоимостиАрендованногоИмущества) КАК СчетКт,
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.КорректировкаАмортизацииНУ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	-Строки.КорректировкаАмортизацииНУ КАК СуммаВРКт,
	|	""Коррректировка расходов по амортизации на величину превышения над арендными платежами"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РегламентнаяОперация КАК Операция
	|		ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтКорректировкаАмортизации КАК Строки
	|		ПО ИСТИНА
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|		ПО Строки.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|			И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|		ПО ОбъектыСтроительства.Ссылка = Строки.АналитикаРасходов
	|ГДЕ
	|	Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.ПризнаниеВНУАрендныхПлатежей)
	|	И Строки.КорректировкаАмортизацииНУ <> 0
	|
	|";
	ТекстыОтражения.Добавить(ТекстЗапроса);
#КонецОбласти
	
// Кредиты, депозиты, займы
#Область Реклассификация_КредитыИЗаймы // (Дт <66, 67, 67, 76.09> :: Кт <66, 67, 67, 76.09>)
	ТекстЗапроса = "
	|ВЫБРАТЬ // Реклассификация долгосрочных активов и обязательств (Дт <66, 67, 67, 76.09> :: Кт <66, 67, 67, 76.09>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Строки.СуммаРегл КАК Сумма,
	|	Строки.СуммаУпр КАК СуммаУУ,
	|
	|	ВЫБОР
	|		КОГДА Строки.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКредиторамиОсновнойДолг)
	|		КОГДА Строки.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКредиторамиПроценты)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКредиторамиКомиссия) КОНЕЦ КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Строки.Валюта КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.Контрагент КАК СубконтоДт1,
	|	Строки.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	Строки.СуммаЗадолженности КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СуммаРегл КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ВЫБОР
	|		КОГДА Строки.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКредиторамиОсновнойДолг)
	|		КОГДА Строки.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКредиторамиПроценты)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКредиторамиКомиссия) КОНЕЦ КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Строки.Валюта КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	Строки.Контрагент КАК СубконтоКт1,
	|	Строки.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	Строки.СуммаЗадолженности КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СуммаРегл КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|
	|	""Реклассификация долгосрочных активов и обязательств"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РегламентнаяОперация КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		вт_РеклассификацияРасчетыПоФинИнструментам КАК Строки
	|	ПО
	|		ИСТИНА
	|ГДЕ
	|	Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств)
	|	И Строки.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|";
	
	ТекстыОтражения.Добавить(ТекстЗапроса);
	
#КонецОбласти

#Область Реклассификация_Депозиты // (Дт <55.03, 58.03, 76.09> :: Кт <55.03, 58.03, 76.09>)
	ТекстЗапроса = "
	|ВЫБРАТЬ // Реклассификация долгосрочных активов и обязательств (Дт <55.03, 58.03, 76.09> :: Кт <55.03, 58.03, 76.09>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Строки.СуммаРегл КАК Сумма,
	|	Строки.СуммаУпр КАК СуммаУУ,
	|
	|	ВЫБОР
	|		КОГДА Строки.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг)
	|			ИЛИ (Строки.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты)
	|					И Строки.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ДепозитВБанкеСКапитализацией))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСДебиторамиОсновнойДолг)
	|		КОГДА Строки.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСДебиторамиПроценты)
	|		ИНАЧЕ 
	|			ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСДебиторамиКомиссия)
	|	КОНЕЦ КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Строки.Валюта КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.БанковскийСчетКонтрагента КАК СубконтоДт1,
	|	Строки.Контрагент КАК СубконтоДт2,
	|	Строки.Договор КАК СубконтоДт3,
	|	
	|	Строки.СуммаЗадолженности КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СуммаРегл КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ВЫБОР
	|		КОГДА Строки.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг)
	|			ИЛИ (Строки.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты)
	|					И Строки.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговораКредитовИДепозитов.ДепозитВБанкеСКапитализацией))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСДебиторамиОсновнойДолг)
	|		КОГДА Строки.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСДебиторамиПроценты)
	|		ИНАЧЕ 
	|			ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСДебиторамиКомиссия)
	|	КОНЕЦ КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Строки.Валюта КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	Строки.БанковскийСчетКонтрагента КАК СубконтоКт1,
	|	Строки.Контрагент КАК СубконтоКт2,
	|	Строки.Договор КАК СубконтоКт3,
	|
	|	Строки.СуммаЗадолженности КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СуммаРегл КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Реклассификация долгосрочных активов и обязательств"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РегламентнаяОперация КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		вт_РеклассификацияРасчетыПоФинИнструментам КАК Строки
	|	ПО
	|		ИСТИНА
	|ГДЕ
	|	Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств)
	|	И Строки.ХарактерДоговора <> ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|";
	
	ТекстыОтражения.Добавить(ТекстЗапроса);
#КонецОбласти

// Аренда
#Область Реклассификация_Аренда // (Дт <76.07.Х> :: Кт <76.07.Х>)
	ТекстЗапроса = "
	|ВЫБРАТЬ // Реклассификация долгосрочных активов и обязательств (Дт <76.07.Х> :: Кт <76.07.Х>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Строки.СуммаРегл КАК Сумма,
	|	Строки.СуммаУпр КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АрендныеОбязательства) КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Строки.Валюта КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|	Строки.Контрагент КАК СубконтоДт1,
	|	Строки.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	Строки.Сумма КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СуммаРегл КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АрендныеОбязательства) КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Строки.Валюта КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	Строки.Контрагент КАК СубконтоКт1,
	|	Строки.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	Строки.Сумма КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СуммаРегл КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Реклассификация долгосрочных активов и обязательств"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РегламентнаяОперация КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		вт_РеклассификацияРасчетыПоАренде КАК Строки
	|	ПО
	|		ИСТИНА
	|ГДЕ
	|	Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств)
	|";
	
	ТекстыОтражения.Добавить(ТекстЗапроса);
	
#КонецОбласти

// Расчеты с клиентами
#Область Реклассификация_РасчетыСКлиентами
	ТекстЗапроса = "
	|ВЫБРАТЬ // Реклассификация долгосрочных активов и обязательств (Дт <62.х> :: Кт <62.х>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Строки.ДолгРегл КАК Сумма,
	|	0 КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКлиентами) КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Строки.Валюта КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.Контрагент КАК СубконтоДт1,
	|	Строки.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	Строки.Долг КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.ДолгРегл КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКлиентами) КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Строки.Валюта КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Строки.Контрагент КАК СубконтоКт1,
	|	Строки.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	Строки.Долг КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.ДолгРегл КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Реклассификация долгосрочных расчетов с клиентами"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РегламентнаяОперация КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		вт_РеклассификацияРасчетыСКлиентами КАК Строки
	|	ПО
	|		ИСТИНА
	|ГДЕ
	|	Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств)
	|	И Строки.ДолгРегл > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Реклассификация долгосрочных активов и обязательств  (Дт Кт <62.х> :: Кт <62.х>) УПР
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	0 КАК Сумма,
	|	Строки.ДолгУпр КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКлиентами) КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Строки.Валюта  КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.Контрагент КАК СубконтоДт1,
	|	Строки.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	Строки.Долг КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКлиентами) КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Строки.Валюта КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Строки.Контрагент КАК СубконтоКт1,
	|	Строки.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	Строки.Долг КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Реклассификация долгосрочных расчетов с клиентами"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РегламентнаяОперация КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		вт_РеклассификацияРасчетыСКлиентами КАК Строки
	|	ПО
	|		ИСТИНА
	|ГДЕ
	|	Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств)
	|	И Строки.ДолгУпр > 0
	|";
	ТекстыОтражения.Добавить(ТекстЗапроса);
	
#КонецОбласти

// Ошибки округления, возврат авансов
#Область Реклассификация_РасчетыСКлиентами_Предоплата
	ТекстЗапроса = "
	|ВЫБРАТЬ // Реклассификация долгосрочных активов и обязательств (Дт Кт <62.х> :: Кт <62.х>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Строки.ПредоплатаРегл КАК Сумма,
	|	0 КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АвансыПолученные) КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Строки.Валюта КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.Контрагент КАК СубконтоДт1,
	|	Строки.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	Строки.Предоплата КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.ПредоплатаРегл КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АвансыПолученные) КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Строки.Валюта КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Строки.Контрагент КАК СубконтоКт1,
	|	Строки.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	Строки.Предоплата КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.ПредоплатаРегл КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Реклассификация долгосрочных расчетов с клиентами"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РегламентнаяОперация КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		вт_РеклассификацияРасчетыСКлиентами КАК Строки
	|	ПО
	|		ИСТИНА
	|ГДЕ
	|	Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств)
	|	И Строки.ПредоплатаРегл > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Реклассификация долгосрочных активов и обязательств (Дт Кт <62.х> :: Кт <62.х>) УПР
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	0 КАК Сумма,
	|	ВЫБОР КОГДА Строки.ПредоплатаУпр > 0
	|		ТОГДА Строки.ПредоплатаУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АвансыПолученные) КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Строки.Валюта КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.Контрагент КАК СубконтоДт1,
	|	Строки.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	Строки.Предоплата КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АвансыПолученные) КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Строки.Валюта КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Строки.Контрагент КАК СубконтоКт1,
	|	Строки.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	Строки.Предоплата КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Реклассификация долгосрочных расчетов с клиентами"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РегламентнаяОперация КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		вт_РеклассификацияРасчетыСКлиентами КАК Строки
	|	ПО
	|		ИСТИНА
	|ГДЕ
	|	Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств)
	|	И Строки.ПредоплатаУпр > 0
	|";
	
	ТекстыОтражения.Добавить(ТекстЗапроса);
	
#КонецОбласти

// Расчеты с поставщиками
#Область Реклассификация_РасчетыСПоставщиками
	ТекстЗапроса = "
	|ВЫБРАТЬ // Реклассификация долгосрочных активов и обязательств (Дт <60.х> :: Кт <60.х>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Строки.ДолгРегл КАК Сумма,
	|	0 КАК СуммаУУ,
	|
	|	ВЫБОР КОГДА Строки.ЭтоВозврат
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыПоПретензиям)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками)
	|	КОНЕЦ КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Строки.Валюта КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.Контрагент КАК СубконтоДт1,
	|	Строки.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	Строки.Долг КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.ДолгРегл КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ВЫБОР КОГДА Строки.ЭтоВозврат
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыПоПретензиям)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками)
	|	КОНЕЦ КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Строки.Валюта КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Строки.Контрагент КАК СубконтоКт1,
	|	Строки.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	Строки.Долг КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.ДолгРегл КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Реклассификация долгосрочных расчетов с поставщиками"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РегламентнаяОперация КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		вт_РеклассификацияРасчетыСПоставщиками КАК Строки
	|	ПО
	|		ИСТИНА
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств)
	|	И Строки.ДолгРегл > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Реклассификация долгосрочных активов и обязательств (Дт Кт <60.х> :: Кт <60.х>) УПР
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	0 КАК Сумма,
	|	Строки.ДолгУпр КАК СуммаУУ,
	|
	|	ВЫБОР КОГДА Строки.ЭтоВозврат
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыПоПретензиям)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками)
	|	КОНЕЦ КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Строки.Валюта КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.Контрагент КАК СубконтоДт1,
	|	Строки.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	Строки.Долг КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ВЫБОР КОГДА Строки.ЭтоВозврат
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыПоПретензиям)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками)
	|	КОНЕЦ КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Строки.Валюта КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Строки.Контрагент КАК СубконтоКт1,
	|	Строки.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	Строки.Долг КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Реклассификация долгосрочных расчетов с поставщиками"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РегламентнаяОперация КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		вт_РеклассификацияРасчетыСПоставщиками КАК Строки
	|	ПО
	|		ИСТИНА
	|ГДЕ
	|	Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств)
	|	И Строки.ДолгУпр > 0
	|";
	
	ТекстыОтражения.Добавить(ТекстЗапроса);
	
#КонецОбласти

// Ошибки округления, возврат авансов
#Область Реклассификация_РасчетыСПоставщиками_Предоплата
	ТекстЗапроса = "
	|ВЫБРАТЬ // Реклассификация долгосрочных активов и обязательств (Дт Кт <60.х> :: Кт <60.х>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	-Строки.ПредоплатаРегл КАК Сумма,
	|	0 КАК СуммаУУ,
	|
	|	ВЫБОР КОГДА Строки.ЭтоВозврат
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыПоПретензиям)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АвансыВыданные)
	|	КОНЕЦ КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Строки.Валюта КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.Контрагент КАК СубконтоДт1,
	|	Строки.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	-Строки.Предоплата КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	-Строки.ПредоплатаРегл КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ВЫБОР КОГДА Строки.ЭтоВозврат
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыПоПретензиям)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АвансыВыданные)
	|	КОНЕЦ КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Строки.Валюта КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Строки.Контрагент КАК СубконтоКт1,
	|	Строки.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	-Строки.Предоплата КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	-Строки.ПредоплатаРегл КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Реклассификация долгосрочных расчетов с поставщиками"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РегламентнаяОперация КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		вт_РеклассификацияРасчетыСПоставщиками КАК Строки
	|	ПО
	|		ИСТИНА
	|ГДЕ
	|	Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств)
	|	И Строки.ПредоплатаРегл < 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Реклассификация долгосрочных активов и обязательств (Дт Кт <60.х>:: Кт <60.х>) УПР
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	0 КАК Сумма,
	|	-Строки.ПредоплатаУпр КАК СуммаУУ,
	|
	|	ВЫБОР КОГДА Строки.ЭтоВозврат
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыПоПретензиям)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АвансыВыданные)
	|	КОНЕЦ КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Строки.Валюта КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.Контрагент КАК СубконтоДт1,
	|	Строки.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	-Строки.Предоплата КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ВЫБОР КОГДА Строки.ЭтоВозврат
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыПоПретензиям)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АвансыВыданные)
	|	КОНЕЦ КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Строки.Валюта КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Строки.Контрагент КАК СубконтоКт1,
	|	Строки.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	-Строки.Предоплата КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Реклассификация долгосрочных расчетов с поставщиками"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РегламентнаяОперация КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		вт_РеклассификацияРасчетыСПоставщиками КАК Строки
	|	ПО
	|		ИСТИНА
	|ГДЕ
	|	Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств)
	|	И Строки.ПредоплатаУпр < 0
	|";
	
	ТекстыОтражения.Добавить(ТекстЗапроса);
	
#КонецОбласти
	
// Расчеты по дисконтированию
#Область Реклассификация_РасчетыПоДисконтированию
	ТекстЗапроса = "
	|ВЫБРАТЬ // Реклассификация долгосрочных активов и обязательств (Дт Кт <76.хх> :: Кт <76.хх>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ВЫБОР КОГДА Строки.ДолгРегл < 0 ТОГДА -1 ИНАЧЕ 0 КОНЕЦ * Строки.ДолгРегл КАК Сумма,
	|	ВЫБОР КОГДА Строки.ДолгУпр < 0 ТОГДА -1 ИНАЧЕ 0 КОНЕЦ * Строки.ДолгУпр КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПроцентыПоДисконтированию) КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Строки.Валюта КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.Контрагент КАК СубконтоДт1,
	|	Строки.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	ВЫБОР КОГДА Строки.Долг < 0 ТОГДА -1 ИНАЧЕ 0 КОНЕЦ * Строки.Долг КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	ВЫБОР КОГДА Строки.ДолгРегл < 0 ТОГДА -1 ИНАЧЕ 0 КОНЕЦ * Строки.ДолгРегл КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПроцентыПоДисконтированию) КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Строки.Валюта КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Строки.Контрагент КАК СубконтоКт1,
	|	Строки.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	ВЫБОР КОГДА Строки.Долг < 0 ТОГДА -1 ИНАЧЕ 0 КОНЕЦ * Строки.Долг КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	ВЫБОР КОГДА Строки.ДолгРегл < 0 ТОГДА -1 ИНАЧЕ 0 КОНЕЦ * Строки.ДолгРегл КАК СуммаВРКт,
	|	""Реклассификация долгосрочных расчетов по дисконтированию"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РегламентнаяОперация КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		вт_РеклассификацияРасчетыПоДисконтированию КАК Строки
	|	ПО
	|		ИСТИНА
	|ГДЕ
	|	Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств)
	|	И Строки.ДолгРегл + Строки.ДолгУпр <> 0
	|";
	
	ТекстыОтражения.Добавить(ТекстЗапроса);
	
#КонецОбласти
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса создания временных таблиц.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	МассивТекстовЗапроса = Новый Массив;
	
	#Область КосвенныеРасходы
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Операция.Ссылка КАК Регистратор,
	|	КосвенныеРасходы.Организация КАК Организация,
	|	КосвенныеРасходы.Подразделение КАК Подразделение,
	|	КосвенныеРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	КосвенныеРасходы.АналитикаРасходов КАК АналитикаРасходов,
	|	КосвенныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВЫБОР 
	|		КОГДА НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.ПрименяетсяЕНВД, ЛОЖЬ) 
	|			  ИЛИ СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|			ТОГДА КосвенныеРасходы.СуммаРегл
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|			ТОГДА 0
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|			ТОГДА КосвенныеРасходы.СуммаРегл - ВЫРАЗИТЬ(КосвенныеРасходы.СуммаРегл * Доли.ДоляЕНВД КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаРеглОСНО,
	|	ВЫБОР 
	|		КОГДА НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.ПрименяетсяЕНВД, ЛОЖЬ) 
	|			  ИЛИ СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|			ТОГДА 0
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|			ТОГДА КосвенныеРасходы.СуммаРегл
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|			ТОГДА ВЫРАЗИТЬ(КосвенныеРасходы.СуммаРегл * Доли.ДоляЕНВД КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаРеглЕНВД,
	|	ВЫБОР 
	|		КОГДА НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.ПрименяетсяЕНВД, ЛОЖЬ) 
	|			  ИЛИ СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|			ТОГДА КосвенныеРасходы.СуммаУпр
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|			ТОГДА 0
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|			ТОГДА КосвенныеРасходы.СуммаУпр - ВЫРАЗИТЬ(КосвенныеРасходы.СуммаУпр * Доли.ДоляЕНВД / КурсВалютыУпрУчета.Курс КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаУпрОСНО,
	|	ВЫБОР 
	|		КОГДА НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.ПрименяетсяЕНВД, ЛОЖЬ) 
	|			  ИЛИ СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|			ТОГДА 0
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|			ТОГДА КосвенныеРасходы.СуммаУпр
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|			ТОГДА ВЫРАЗИТЬ(КосвенныеРасходы.СуммаУпр * Доли.ДоляЕНВД / КурсВалютыУпрУчета.Курс КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаУпрЕНВД,
	|	
	|	ВЫБОР 
	|		КОГДА НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.ПрименяетсяЕНВД, ЛОЖЬ)
	|			  ИЛИ СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|			ТОГДА КосвенныеРасходы.ПостояннаяРазница
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|			ТОГДА 0
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|			ТОГДА КосвенныеРасходы.ПостояннаяРазница - ВЫРАЗИТЬ(КосвенныеРасходы.ПостояннаяРазница * Доли.ДоляЕНВД КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК ПостояннаяРазницаОСНО,
	|	ВЫБОР 
	|		КОГДА НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.ПрименяетсяЕНВД, ЛОЖЬ) 
	|			  ИЛИ СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|			ТОГДА 0
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|			ТОГДА КосвенныеРасходы.ПостояннаяРазница
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|			ТОГДА ВЫРАЗИТЬ(КосвенныеРасходы.ПостояннаяРазница * Доли.ДоляЕНВД КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК ПостояннаяРазницаЕНВД,
	|	
	|	ВЫБОР 
	|		КОГДА НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.ПрименяетсяЕНВД, ЛОЖЬ)
	|			  ИЛИ СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|			ТОГДА КосвенныеРасходы.ВременнаяРазница
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|			ТОГДА 0
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|			ТОГДА КосвенныеРасходы.ВременнаяРазница - ВЫРАЗИТЬ(КосвенныеРасходы.ВременнаяРазница * Доли.ДоляЕНВД КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК ВременнаяРазницаОСНО,
	|	ВЫБОР 
	|		КОГДА НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.ПрименяетсяЕНВД, ЛОЖЬ) 
	|			  ИЛИ СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|			ТОГДА 0
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|			ТОГДА КосвенныеРасходы.ВременнаяРазница
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|			ТОГДА ВЫРАЗИТЬ(КосвенныеРасходы.ВременнаяРазница * Доли.ДоляЕНВД КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК ВременнаяРазницаЕНВД
	|
	|ПОМЕСТИТЬ КосвенныеРасходы 
	|ИЗ
	|	Документ.РегламентнаяОперация КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ПрочиеРасходы КАК КосвенныеРасходы
	|	ПО
	|		Операция.Ссылка = КосвенныеРасходы.Регистратор
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|	ПО
	|		КосвенныеРасходы.СтатьяРасходов = СтатьиРасходов.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|	ПО
	|		Операция.Ссылка = УчетнаяПолитикаОрганизаций.Ссылка
	|		И Операция.Организация = УчетнаяПолитикаОрганизаций.Организация
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ДолиСписанияКосвенныхРасходов КАК Доли
	|	ПО
	|		Операция.Ссылка = Доли.Регистратор
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = Доли.ПериодРасчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		ПорядокОтраженияНаСчетахУчетаПереопределяемый КАК ПорядокОтраженияНаСчетахУчетаПереопределяемый
	|			ПО ПорядокОтраженияНаСчетахУчетаПереопределяемый.ВидСчета = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы)
	|				И ПорядокОтраженияНаСчетахУчетаПереопределяемый.Организация = КосвенныеРасходы.Организация
	|				И ПорядокОтраженияНаСчетахУчетаПереопределяемый.АналитикаУчета = СтатьиРасходов.ГруппаФинансовогоУчетаРегл
	|				И ПорядокОтраженияНаСчетахУчетаПереопределяемый.МестоУчета = КосвенныеРасходы.Подразделение
	|		
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.СписаниеКосвенныхРасходов)
	|	И НЕ ПорядокОтраженияНаСчетахУчетаПереопределяемый.СчетУчета В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеДоходыИРасходы), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Продажи))";
	
	РегистрыСведений.ПорядокОтраженияНаСчетахУчета.ПереопределитьВТекстеЗапросаПорядокОтраженияСчетаУчета(ТекстЗапроса);
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	МассивТекстовЗапроса.Добавить(ТекстЗапросаПустаяВременнаяТаблицаРеклассификацияРасчетыПоФинИнструментам());
	МассивТекстовЗапроса.Добавить(ТекстЗапросаПустаяВременнаяТаблицаРеклассификацияРасчетыСКлиентами());
	МассивТекстовЗапроса.Добавить(ТекстЗапросаПустаяВременнаяТаблицаРеклассификацияРасчетыСПоставщиками());
	МассивТекстовЗапроса.Добавить(ТекстЗапросаПустаяВременнаяТаблицаРеклассификацияРасчетыПоАренде());
	МассивТекстовЗапроса.Добавить(ТекстЗапросаПустаяВременнаяТаблицаРеклассификацияРасчетыПоДисконтированию());
	
	МассивТекстовЗапроса.Добавить(ВнеоборотныеАктивыЛокализация.ТекстЗапросаПустаяВременнаяТаблицаКорректировкаАмортизации());
	МассивТекстовЗапроса.Добавить(ВнеоборотныеАктивыЛокализация.ТекстЗапросаПустаяВременнаяТаблицаРасходыПоАренднымПлатежам());
	
	// Добавим пустой запрос, для того чтобы последний запрос тоже заканчивался на разделитель пакета запросов:
	МассивТекстовЗапроса.Добавить("");
	
	Возврат СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
КонецФункции

// Создает и проводит документы "Регламентная операция" за указанный месяц.
//
//	Параметры:
//		Период - Дата - Начало месяца, в котором необходимо создать документы.
//		МассивОпераций - Массив - Содержит массив типов регламентных операций (значений перечисления "ТипыРегламентныхОпераций")
//		Организация - Массив, Неопределено - Список организаций по которым формируются документы. Если список пустой,
//												то документы формируются по всем организациям.
//		Отказ - Булево - Используется при вызове из формы закрытия месяца. При установке в "Истина" - дальнейшие операции
//							выполняться не будут.
//		ПроверятьНеобходимостьРаспределения - Булево - Признак того, что операцию выполнять только в случае если требуется. В противном случае выполнятся безусловно.
//		ПроводитьДокументы - Булево - Признак того, что созданные документы необходимо провести.
//
// Возвращаемое значение:
//		РезультатРасчета - Структура - структура, содержащая:
//			* МассивДокументов - Массив - массив ссылок на созданные/измененные документы
//			* ТекстОшибки - Строка - текст описание ошибки; заполнен при Отказ = Истина
//			* ТипОперации - ПеречислениеСсылка.ТипыРегламентныхОпераций - операция, завершившаяся с ошибкой
//			* Организация - СправочникСсылка.Организации - организация, по которой возникла ошибка.
//
Функция РассчитатьРегламентныеОперации(Период, МассивОпераций, Организация = Неопределено, Отказ = Ложь,
	ПроверятьНеобходимостьРаспределения = Ложь,	ПроводитьДокументы = Истина) Экспорт
	
	РезультатРасчета = Новый Структура;
	РезультатРасчета.Вставить("МассивДокументов", Новый Массив);
	РезультатРасчета.Вставить("СписокОшибок", Новый Массив);
	РезультатРасчета.Вставить("ТекстОшибки", "");
	РезультатРасчета.Вставить("ДокументСОшибкой");
	РезультатРасчета.Вставить("КоличествоДанных", 0);
	РезультатРасчета.Вставить("ТипОперации");
	РезультатРасчета.Вставить("Организация");
	
	ОперацииРеглУчета = Новый Массив;
	ОперацииРеглУчета.Добавить(Перечисления.ТипыРегламентныхОпераций.РасчетДолейСписанияКосвенныхРасходов);
	ОперацииРеглУчета.Добавить(Перечисления.ТипыРегламентныхОпераций.РасчетКурсовыхРазниц);
	ОперацииРеглУчета.Добавить(Перечисления.ТипыРегламентныхОпераций.СписаниеКосвенныхРасходов);
	ОперацииРеглУчета.Добавить(Перечисления.ТипыРегламентныхОпераций.ФормированиеФинансовогоРезультата);
	ОперацииРеглУчета.Добавить(Перечисления.ТипыРегламентныхОпераций.СписаниеУбытковПрошлыхЛет);
	ОперацииРеглУчета.Добавить(Перечисления.ТипыРегламентныхОпераций.РасчетНалогаНаПрибыль);
	ОперацииРеглУчета.Добавить(Перечисления.ТипыРегламентныхОпераций.РасчетОтложенногоНалога);
	ОперацииРеглУчета.Добавить(Перечисления.ТипыРегламентныхОпераций.ПереоценкаСуммыВВалютеФинОтчетности);
	ОперацииРеглУчета.Добавить(Перечисления.ТипыРегламентныхОпераций.ЗакрытиеГода);
	
	Если ПроверятьНеобходимостьРаспределения Тогда
		Состояние = СостояниеРасчетДолейСписанияКосвенныхРасходов(Организация, Период);
		Если Состояние = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется Тогда
			Возврат РезультатРасчета;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Организация) = Тип("Массив") Тогда
		МассивОрганизаций = Организация;
	ИначеЕсли ТипЗнч(Организация) = Тип("СписокЗначений") Тогда
		МассивОрганизаций = Организация.ВыгрузитьЗначения();
	Иначе
		МассивОрганизаций = Новый Массив;
		Если ЗначениеЗаполнено(Организация) Тогда
			МассивОрганизаций.Добавить(Организация);
		КонецЕсли;
	КонецЕсли;
	
	ТаблицаОпераций = Новый ТаблицаЗначений;
	ТаблицаОпераций.Колонки.Добавить("ТипОперации", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыРегламентныхОпераций"));
	ТаблицаОпераций.Колонки.Добавить("Порядок", ОбщегоНазначения.ОписаниеТипаЧисло(1));
	Для Сч = 1 По МассивОпераций.Количество() Цикл
		СтрокаОперации = ТаблицаОпераций.Добавить();
		СтрокаОперации.ТипОперации = МассивОпераций[Сч-1];
		СтрокаОперации.Порядок = Сч;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаОпераций.Порядок      КАК Порядок,
	|	ТаблицаОпераций.ТипОперации  КАК ТипОперации
	|ПОМЕСТИТЬ ВтТипыОпераций
	|ИЗ &ТаблицаОпераций КАК ТаблицаОпераций
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеСправочника.Ссылка КАК Ссылка,
	|	ДанныеСправочника.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ДанныеСправочника.ОбособленноеПодразделение КАК ОбособленноеПодразделение
	|ПОМЕСТИТЬ ВтОрганизации
	|ИЗ
	|	Справочник.Организации КАК ДанныеСправочника
	|ГДЕ
	|	(ДанныеСправочника.Ссылка В (&МассивОрганизаций)
	|			ИЛИ &ПоВсемОрганизациям)
	|	И (ДанныеСправочника.Ссылка <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|			ИЛИ &УчитыватьУпрОрганизацию)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Организации.Ссылка                    КАК Организация,
	|	Организации.ГоловнаяОрганизация       КАК ГоловнаяОрганизация,
	|	Организации.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
	|	ТипыРегламентныхОпераций.ТипОперации  КАК ТипОперации,
	|	ТипыРегламентныхОпераций.Порядок      КАК Порядок
	|ПОМЕСТИТЬ ВтТипыОперацийПоОрганизациям
	|ИЗ
	|	ВтОрганизации КАК Организации,
	|	ВтТипыОпераций КАК ТипыРегламентныхОпераций
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТипыОпераций.Организация,
	|	ТипыОпераций.ОбособленноеПодразделение,
	|	ТипыОпераций.ТипОперации,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НастройкиСистемыНалогообложения.СистемаНалогообложения, &ОСНО) = &ОСНО
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрганизацияНаОСНО,
	|	ЕСТЬNULL(НастройкиУчетаУСН.ПрименяетсяУСНДоходыМинусРасходы, ЛОЖЬ) КАК ПрименяетсяУСНДоходыМинусРасходы,
	|	ЕСТЬNULL(НастройкиУчетаНалогаНаПрибыль.ВключатьВСоставНалоговыхРасходовАрендныеПлатежи, ЛОЖЬ) КАК ВключатьВСоставНалоговыхРасходовАрендныеПлатежи,
	|	ЕСТЬNULL(УчетнаяПолитикаФинансовогоУчета.ИспользоватьВыделениеДолгосрочныхАктивовОбязательств, ЛОЖЬ) КАК ИспользуетсяВыделениеДолгосрочныхАктивовОбязательств,
	|	МАКСИМУМ(ЕСТЬNULL(РегламентнаяОперацияПроведена.Ссылка, РегламентнаяОперацияЗаписана.Ссылка)) КАК Ссылка
	|ИЗ
	|	ВтТипыОперацийПоОрганизациям КАК ТипыОпераций
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.РегламентнаяОперация КАК РегламентнаяОперацияПроведена
	|	ПО
	|		ТипыОпераций.Организация = РегламентнаяОперацияПроведена.Организация
	|		И ТипыОпераций.ТипОперации = РегламентнаяОперацияПроведена.ТипОперации
	|		И РегламентнаяОперацияПроведена.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|		И РегламентнаяОперацияПроведена.Проведен
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.РегламентнаяОперация КАК РегламентнаяОперацияЗаписана
	|	ПО
	|		ТипыОпераций.Организация = РегламентнаяОперацияЗаписана.Организация
	|		И ТипыОпераций.ТипОперации = РегламентнаяОперацияЗаписана.ТипОперации
	|		И РегламентнаяОперацияЗаписана.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|		И НЕ РегламентнаяОперацияЗаписана.ПометкаУдаления
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаНалогаНаПрибыль.СрезПоследних(
	|				&ДатаОкончания,
	|				Организация В
	|					(ВЫБРАТЬ
	|						Т.ГоловнаяОрганизация
	|					ИЗ
	|						ВтОрганизации КАК Т)) КАК НастройкиУчетаНалогаНаПрибыль
	|		ПО ТипыОпераций.ГоловнаяОрганизация = НастройкиУчетаНалогаНаПрибыль.Организация
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(
	|				&ДатаОкончания,
	|				Организация В
	|					(ВЫБРАТЬ
	|						Т.ГоловнаяОрганизация
	|					ИЗ
	|						ВтОрганизации КАК Т)) КАК НастройкиСистемыНалогообложения
	|		ПО ТипыОпераций.ГоловнаяОрганизация = НастройкиСистемыНалогообложения.Организация
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаУСН.СрезПоследних(
	|				&ДатаОкончания,
	|				Организация В
	|					(ВЫБРАТЬ
	|						Т.ГоловнаяОрганизация
	|					ИЗ
	|						ВтОрганизации КАК Т)) КАК НастройкиУчетаУСН
	|		ПО ТипыОпераций.ГоловнаяОрганизация = НастройкиУчетаУСН.Организация
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(
	|				&ДатаОкончания,
	|				Организация В
	|					(ВЫБРАТЬ
	|						Т.ГоловнаяОрганизация
	|					ИЗ
	|						ВтОрганизации КАК Т)) КАК УчетнаяПолитикаФинансовогоУчета
	|		ПО ТипыОпераций.ГоловнаяОрганизация = УчетнаяПолитикаФинансовогоУчета.Организация
	|СГРУППИРОВАТЬ ПО
	|	ТипыОпераций.Организация,
	|	ТипыОпераций.ОбособленноеПодразделение,
	|	ТипыОпераций.ТипОперации,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НастройкиСистемыНалогообложения.СистемаНалогообложения, &ОСНО) = &ОСНО
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ЕСТЬNULL(НастройкиУчетаУСН.ПрименяетсяУСНДоходыМинусРасходы, ЛОЖЬ),
	|	ЕСТЬNULL(НастройкиУчетаНалогаНаПрибыль.ВключатьВСоставНалоговыхРасходовАрендныеПлатежи, ЛОЖЬ),
	|	ЕСТЬNULL(УчетнаяПолитикаФинансовогоУчета.ИспользоватьВыделениеДолгосрочныхАктивовОбязательств, ЛОЖЬ),
	|	ТипыОпераций.Порядок
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТипыОпераций.Порядок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка КАК Ссылка,
	|	РегламентнаяОперация.Организация КАК Организация
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтОрганизации КАК Организации
	|	ПО
	|		РегламентнаяОперация.Организация = Организации.Ссылка 
	|ГДЕ
	|	РегламентнаяОперация.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И РегламентнаяОперация.Проведен
	|	И РегламентнаяОперация.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.ЗакрытиеГода)
	|
	|";

	Запрос.УстановитьПараметр("ДатаНачала", 			 НачалоМесяца(Период));
	Запрос.УстановитьПараметр("ДатаОкончания", 			 КонецМесяца(Период));
	Запрос.УстановитьПараметр("УчитыватьУпрОрганизацию", ПолучитьФункциональнуюОпцию("ИспользоватьУправленческуюОрганизацию"));
	Запрос.УстановитьПараметр("ОСНО", 					 Перечисления.СистемыНалогообложения.Общая);
	Запрос.УстановитьПараметр("ТаблицаОпераций", 		 ТаблицаОпераций);
	Запрос.УстановитьПараметр("МассивОрганизаций", 		 МассивОрганизаций);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", 	 НЕ ЗначениеЗаполнено(МассивОрганизаций));
	
	Результат = Запрос.ВыполнитьПакет();

	ТаблицаОпераций     = Результат[3].Выгрузить();
	ТаблицаЗакрытияГода = Результат[4].Выгрузить();
	ИмяСобытия		= НСтр("ru = 'РегламентнаяОперация';
							|en = 'РегламентнаяОперация'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	
	КоличествоОбработанныхДанных = 0;
	
	Для Каждого Строка Из ТаблицаОпераций Цикл
		
		Если Строка.Организация = Справочники.Организации.УправленческаяОрганизация
				И (Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.ФормированиеФинансовогоРезультата
				ИЛИ Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.СписаниеКосвенныхРасходов
				ИЛИ Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.СписаниеУбытковПрошлыхЛет
				ИЛИ Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетНалогаНаПрибыль
				ИЛИ Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетОтложенногоНалога
				ИЛИ Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.ЗакрытиеГода) Тогда
			Продолжить;
		ИначеЕсли Строка.ОбособленноеПодразделение
				И НЕ ОперацияИспользуетсяДляОбособленногоПодразделения(Строка.ТипОперации) Тогда
			Продолжить; // Эти операции выполняются только для головной организации
		ИначеЕсли НЕ Строка.ОрганизацияНаОСНО 
				И (Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетНалогаНаПрибыль
				ИЛИ Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетОтложенногоНалога
				ИЛИ Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.СписаниеКосвенныхРасходов
				ИЛИ Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.СписаниеУбытковПрошлыхЛет) Тогда   
			Продолжить; // Расчет налога на прибыль выполняем только для организация на ОСНО
		ИначеЕсли Строка.ОрганизацияНаОСНО 
				И (Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.ПризнаниеРасходовПриУСН
				ИЛИ Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетНалогаУСН) Тогда   
			Продолжить; // Эти операции выполняются только для организаций не на ОСНО
		ИначеЕсли НЕ Строка.ПрименяетсяУСНДоходыМинусРасходы 
				И Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.ПризнаниеРасходовПриУСН Тогда   
				   Продолжить; // Эта операция выполняются только для организаций на УСН "Доходы минус расходы"
		ИначеЕсли Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.ПризнаниеВНУАрендныхПлатежей
			И НЕ Строка.ВключатьВСоставНалоговыхРасходовАрендныеПлатежи Тогда
			Продолжить;
		ИначеЕсли ОперацииРеглУчета.Найти(Строка.ТипОперации) <> Неопределено
			И (Не ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет") ИЛИ Период < Константы.ДатаНачалаВеденияРеглУчета.Получить()) Тогда
			Продолжить; // Эта операция выполняется только при включенной опции регламентированного учета
		ИначеЕсли НЕ РеглУчетСервер.НастройкиУчета().ДополнительноВедетсяУчетВВалютеФинОтчетности
			И Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.ПереоценкаСуммыВВалютеФинОтчетности Тогда   
			Продолжить; // Эта операция выполняются только при ведении управленческого учета на плане счетов в валюте ФО
		ИначеЕсли НЕ Строка.ИспользуетсяВыделениеДолгосрочныхАктивовОбязательств
				И Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.РеклассификацияДолгосрочныхАктивовОбязательств Тогда
					Продолжить; // Эта операция выполняются только для организаций с выделением долгосрочных активов и обязательств
		КонецЕсли;
			
		Если Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.СписаниеКосвенныхРасходов
			ИЛИ Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.ФормированиеФинансовогоРезультата
			ИЛИ Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.СписаниеУбытковПрошлыхЛет
			ИЛИ Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетНалогаНаПрибыль
			ИЛИ Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетОтложенногоНалога Тогда
			
			// Отменим проведение закрытия года, т.к. они оперируют одними данными
			СтруктураПоиска = Новый Структура("Организация", Строка.Организация);
			ЗакрытияГода = ТаблицаЗакрытияГода.НайтиСтроки(СтруктураПоиска);
			Для каждого ЗакрытиеГода Из ЗакрытияГода Цикл
				ЗакрытиеГодаДокумент = ЗакрытиеГода.Ссылка.ПолучитьОбъект();
				РезультатРасчета.МассивДокументов.Добавить(ЗакрытиеГода.Ссылка);
				Попытка 
					// Проверим, что документ находится в доступном для изменения периоде.
					ОписаниеОшибкиДаты = "";
					Если ДатыЗапретаИзменения.ИзменениеЗапрещено(ЗакрытиеГодаДокумент, , ОписаниеОшибкиДаты) Тогда
						ОписаниеОшибкиДаты = РасчетСебестоимостиПротоколРасчета.ПредставлениеМногострочногоТекста(ОписаниеОшибкиДаты);
						ВызватьИсключение ОписаниеОшибкиДаты;
					КонецЕсли;
					ЗакрытиеГодаДокумент.ДополнительныеСвойства.Вставить("ОчисткаДляПоследующегоПроведения",Истина);
					ЗакрытиеГодаДокумент.Записать(РежимЗаписиДокумента.ОтменаПроведения);
					
				Исключение
					Отказ = Истина;
					ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					РезультатРасчета.ТекстОшибки = СокрЛП(РезультатРасчета.ТекстОшибки + Символы.ПС + ТекстОшибки);
					РезультатРасчета.ТипОперации = Строка.ТипОперации;
					РезультатРасчета.Организация = Строка.Организация;
					РезультатРасчета.ДокументСОшибкой = ЗакрытиеГода.Ссылка;
					
					ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
					Возврат РезультатРасчета;
				КонецПопытки;
			КонецЦикла;
		КонецЕсли;
		
		Если Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетНалогаНаИмущество Тогда
			
			УплачиваютсяАвансы = РасчетИмущественныхНалоговУП.УплачиваютсяАвансыПоНалогуНаИмущество(
									Строка.Организация, Период);
			
			НеобходимРасчетНалогаНаИмущество = 
				Месяц(Период) = 12 
				ИЛИ (Месяц(Период)%3 = 0 И УплачиваютсяАвансы);
			
			Если Не НеобходимРасчетНалогаНаИмущество Тогда
				Продолжить;
			КонецЕсли;
			
		ИначеЕсли Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетТранспортногоНалога Тогда
			
			УплачиваютсяАвансы = РасчетИмущественныхНалоговУП.УплачиваютсяАвансыПоТранспортномуНалогу(
									Строка.Организация, Период);
			
			НеобходимРасчетТранспортногоНалога = 
				Месяц(Период) = 12 
				ИЛИ (Месяц(Период)%3 = 0 И УплачиваютсяАвансы);
			
			Если Не НеобходимРасчетТранспортногоНалога Тогда
				Продолжить;
			КонецЕсли;
			
		ИначеЕсли Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетЗемельногоНалога Тогда
			
			УплачиваютсяАвансы = РасчетИмущественныхНалоговУП.УплачиваютсяАвансыПоЗемельномуНалогу(
									Строка.Организация, Период);
			
			НеобходимРасчетЗемельногоНалога = 
				Месяц(Период) = 12 
				ИЛИ (Месяц(Период)%3 = 0 И УплачиваютсяАвансы);
			
			Если Не НеобходимРасчетЗемельногоНалога Тогда
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Строка.Ссылка) Тогда
			РеглОперация = Документы.РегламентнаяОперация.СоздатьДокумент();
			РеглОперация.Дата = КонецМесяца(Период);
			РеглОперация.ТипОперации = Строка.ТипОперации;
			РеглОперация.Организация = Строка.Организация;
		Иначе
			РеглОперация = Строка.Ссылка.ПолучитьОбъект();
		КонецЕсли;
		
		// Проверим, что документ находится в доступном для изменения периоде.
		ОписаниеОшибкиДаты = "";
		
		Если ДатыЗапретаИзменения.ИзменениеЗапрещено(РеглОперация, , ОписаниеОшибкиДаты) Тогда
			Отказ = Истина;
			ТекстОшибки = РасчетСебестоимостиПротоколРасчета.ПредставлениеМногострочногоТекста(ОписаниеОшибкиДаты);
			РезультатРасчета.ТекстОшибки = СокрЛП(РезультатРасчета.ТекстОшибки + Символы.ПС + ТекстОшибки);
			РезультатРасчета.ТипОперации = Строка.ТипОперации;
			РезультатРасчета.Организация = Строка.Организация;
			РезультатРасчета.ДокументСОшибкой = Строка.Ссылка;
			
			ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
			Возврат РезультатРасчета;
		КонецЕсли;
		
		Если РеглОперация.Проведен Тогда
			РеглОперация.ДополнительныеСвойства.Вставить("ОчисткаДляПоследующегоПроведения",Истина);
			Попытка 
				РеглОперация.Записать(РежимЗаписиДокумента.ОтменаПроведения)
			Исключение
				Отказ = Истина;
				ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				РезультатРасчета.ТекстОшибки = СокрЛП(РезультатРасчета.ТекстОшибки + Символы.ПС + ТекстОшибки);
				РезультатРасчета.ТипОперации = Строка.ТипОперации;
				РезультатРасчета.Организация = Строка.Организация;
				РезультатРасчета.ДокументСОшибкой = РеглОперация.Ссылка;
				
				ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
				Возврат РезультатРасчета;
			КонецПопытки;
		КонецЕсли;
		
		РеглОперация.ДополнительныеСвойства.Вставить("ВыводитьСообщенияВЖурналРегистрации", Истина);
		РеглОперация.Записать(РежимЗаписиДокумента.Запись);
		РезультатРасчета.МассивДокументов.Добавить(РеглОперация.Ссылка);
		
		Если ПроводитьДокументы Тогда
			
			СообщенияПроверки = РасчетСебестоимостиПрикладныеАлгоритмы.ПроверитьЗаполнениеОбъектаСПерехватомСообщений(РеглОперация);
			
			Если ЗначениеЗаполнено(СообщенияПроверки) Тогда
				
				Отказ = Истина;
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'При проверке документа ""%1"" были обнаружены ошибки:
						|%2';
						|en = 'Errors were detected while checking the ""%1"" document:
						|%2'"),
					СокрЛП(РеглОперация.Ссылка),
					РасчетСебестоимостиПротоколРасчета.ПредставлениеМногострочногоТекста(СообщенияПроверки, " - "));
				
				РезультатРасчета.ТекстОшибки = СокрЛП(РезультатРасчета.ТекстОшибки + Символы.ПС + ТекстОшибки);
				РезультатРасчета.ТипОперации = Строка.ТипОперации;
				РезультатРасчета.Организация = Строка.Организация;
				РезультатРасчета.ДокументСОшибкой = РеглОперация.Ссылка;
				
				ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
				
				Возврат РезультатРасчета;
				
			КонецЕсли;
				
			Попытка
				
				РеглОперация.Записать(РежимЗаписиДокумента.Проведение);
				
				Если РеглОперация.ДополнительныеСвойства.Свойство("КоличествоОбработанныхДанных") Тогда
					КоличествоОбработанныхДанных = КоличествоОбработанныхДанных + РеглОперация.ДополнительныеСвойства.КоличествоОбработанныхДанных;
				КонецЕсли; 
				
				Если РеглОперация.ДополнительныеСвойства.Свойство("СписокОшибок") Тогда
					ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
						РезультатРасчета.СписокОшибок, 
						РеглОперация.ДополнительныеСвойства.СписокОшибок);
				КонецЕсли;
				
			Исключение
				
				Отказ = Истина;
				
				РезультатРасчета.ТипОперации = Строка.ТипОперации;
				РезультатРасчета.Организация = Строка.Организация;
				РезультатРасчета.ДокументСОшибкой = РеглОперация.Ссылка;
				
				Если РеглОперация.ДополнительныеСвойства.Свойство("СписокОшибок")
					И РеглОперация.ДополнительныеСвойства.СписокОшибок.Количество() <> 0 Тогда
					
					ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
						РезультатРасчета.СписокОшибок, 
						РеглОперация.ДополнительныеСвойства.СписокОшибок);
					
				Иначе
					
					ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					РезультатРасчета.ТекстОшибки = СокрЛП(РезультатРасчета.ТекстОшибки + Символы.ПС + ТекстОшибки);
					ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
					
				КонецЕсли; 
				
				Возврат РезультатРасчета;
				
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;
	
	РезультатРасчета.Вставить("КоличествоДанных", КоличествоОбработанныхДанных);
	
	Возврат РезультатРасчета;
	
КонецФункции

// Возвращает текст запроса определения необходимости расчета долей списания косвенных расходов.
//
// Возвращаемое значение:
// 	ТекстЗапроса - Строка - Текст запроса.
//
Функция ТекстРасчетаДолейСписанияКосвенныхРасходов()
	Возврат "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Ссылка,
	|	ДД.ГоловнаяОрганизация
	|ПОМЕСТИТЬ ОрганизацииИОбособленныеПодразделения
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЕСТЬNULL(ОбособПодр.Ссылка, ДД.Ссылка) КАК Ссылка,
	|		ДД.Ссылка КАК ГоловнаяОрганизация
	|	ИЗ
	|		Справочник.Организации КАК ДД
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОбособПодр
	|		ПО 
	|			ДД.Ссылка = ОбособПодр.ГоловнаяОрганизация
	|			И ДД.Ссылка <> ОбособПодр.Ссылка
	|	
	|	ГДЕ
	|		НЕ ДД.ОбособленноеПодразделение
	|		И ЕСТЬNULL(ОбособПодр.ОбособленноеПодразделение, ИСТИНА)
	|		И ДД.Ссылка В (&СписокОрганизаций)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ДД.Ссылка КАК Ссылка, // Добавим ссылки на сами головные организации
	|		ДД.Ссылка КАК ГоловнаяОрганизация
	|	ИЗ
	|		Справочник.Организации КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОбособПодр
	|		ПО ДД.Ссылка = ОбособПодр.ГоловнаяОрганизация
	|	ГДЕ
	|		НЕ ДД.ОбособленноеПодразделение
	|		И ОбособПодр.ОбособленноеПодразделение
	|		И ДД.Ссылка В (&СписокОрганизаций)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ДД.Ссылка КАК Ссылка, // Добавим ссылки на филиалы
	|		ДД.ГоловнаяОрганизация КАК ГоловнаяОрганизация
	|	ИЗ
	|		Справочник.Организации КАК ДД
	|	ГДЕ
	|		ДД.ОбособленноеПодразделение
	|		И ДД.Ссылка В (&СписокОрганизаций)
	|	) КАК ДД
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СпрОрганизации.ГоловнаяОрганизация КАК Организация,
	|	СпрОрганизации.Ссылка КАК ОбособленноеПодразделение
	|ПОМЕСТИТЬ
	|	ВтГоловныеОрганизации
	|ИЗ
	|	Справочник.Организации КАК СпрОрганизации
	|ГДЕ
	|	СпрОрганизации.Ссылка В (&СписокОрганизаций)
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация;
	|
	|ВЫБРАТЬ
	|	ГоловныеОрганизации.ОбособленноеПодразделение КАК Ссылка
	|ПОМЕСТИТЬ ВтОрганизацииФормированияРезервовПоСомнительнымДолгам
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаБухУчета.СрезПоследних(&ДатаНачала, Организация В
	|			(ВЫБРАТЬ
	|				ГоловныеОрганизации.Организация
	|			ИЗ
	|				ВтГоловныеОрганизации КАК ГоловныеОрганизации)) КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтГоловныеОрганизации КАК ГоловныеОрганизации 
	|	ПО ГоловныеОрганизации.Организация = Т.Организация
	|ГДЕ
	|	Т.ВариантУчетаОтложенногоНалога <> ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОтложенногоНалога.Нет)
	|	
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ГоловныеОрганизации.ОбособленноеПодразделение КАК Ссылка
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаБухУчета.СрезПоследних(&ДатаНачала, Организация В
	|			(ВЫБРАТЬ
	|				ГоловныеОрганизации.Организация
	|			ИЗ
	|				ВтГоловныеОрганизации КАК ГоловныеОрганизации)) КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтГоловныеОрганизации КАК ГоловныеОрганизации 
	|	ПО ГоловныеОрганизации.Организация = Т.Организация
	|ГДЕ
	|	Т.ФормироватьРезервыПоСомнительнымДолгамБУ
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ГоловныеОрганизации.ОбособленноеПодразделение КАК Ссылка
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНалогаНаПрибыль.СрезПоследних(&ДатаНачала, Организация В
	|			(ВЫБРАТЬ
	|				ГоловныеОрганизации.Организация
	|			ИЗ
	|				ВтГоловныеОрганизации КАК ГоловныеОрганизации)) КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтГоловныеОрганизации КАК ГоловныеОрганизации 
	|	ПО ГоловныеОрганизации.Организация = Т.Организация
	|ГДЕ
	|	Т.ФормироватьРезервыПоСомнительнымДолгамНУ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатьиРасходов.Ссылка
	|ПОМЕСТИТЬ ВТКосвенныеРасходы
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|ГДЕ
	|	СтатьиРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
	|	ИЛИ СтатьиРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|	ИЛИ СтатьиРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
	|	ИЛИ СтатьиРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация КАК Ссылка
	|ПОМЕСТИТЬ ВТОрганизацииСРасходами
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы.ОстаткиИОбороты(
	|			&ДатаНачала,
	|			&ДатаОкончания,,,
	|			Организация В (
	|						ВЫБРАТЬ РАЗЛИЧНЫЕ
	|							ОрганизацииИОбособленныеПодразделения.Ссылка КАК Ссылка 
	|						ИЗ
	|							ОрганизацииИОбособленныеПодразделения КАК ОрганизацииИОбособленныеПодразделения)
	|	) КАК ДанныеРегистра
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКосвенныеРасходы КАК ВТКосвенныеРасходы
	|		ПО ДанныеРегистра.СтатьяРасходов = ВТКосвенныеРасходы.Ссылка
	|ГДЕ
	|	(ДанныеРегистра.СуммаРеглНачальныйОстаток 
	|		- ДанныеРегистра.ПостояннаяРазницаНачальныйОстаток
	|		- ДанныеРегистра.ВременнаяРазницаНачальныйОстаток) <> 0
	|	ИЛИ
	|	(ДанныеРегистра.СуммаРеглПриход
	|		- ДанныеРегистра.ПостояннаяРазницаПриход
	|		- ДанныеРегистра.ВременнаяРазницаПриход) <> 0
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Организации.Ссылка КАК Ссылка
	|ИЗ 
	|	ВТОрганизацииСРасходами КАК Организации 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Организации.Ссылка КАК Ссылка
	|ИЗ
	|	ВтОрганизацииФормированияРезервовПоСомнительнымДолгам КАК Организации
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Организации.Ссылка.ГоловнаяОрганизация
	|ИЗ
	|	ВТОрганизацииСРасходами КАК Организации
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДолиСписанияКосвенныхРасходов КАК ДанныеРегистра
	|		ПО Организации.Ссылка.ГоловнаяОрганизация = ДанныеРегистра.Организация 
	|		И ДанныеРегистра.ПериодРасчета МЕЖДУ &ДатаНачала И &ДатаОкончания
	|ГДЕ
	|	ДанныеРегистра.Организация ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Организации.Ссылка.ГоловнаяОрганизация
	|ИЗ
	|	ВтОрганизацииФормированияРезервовПоСомнительнымДолгам КАК Организации
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДолиСписанияКосвенныхРасходов КАК ДанныеРегистра
	|		ПО Организации.Ссылка.ГоловнаяОрганизация = ДанныеРегистра.Организация 
	|		И ДанныеРегистра.ПериодРасчета МЕЖДУ &ДатаНачала И &ДатаОкончания
	|ГДЕ
	|	ДанныеРегистра.Организация ЕСТЬ NULL
	|";
	
КонецФункции

// Метод рассчитывает доли списания косвенных расходов.
//
// Параметры:
//  СписокОрганизаций - Массив - Массив организаций к расчету.
//  Период - Дата - Месяц, в котором производится расчет.
//
// Возвращаемое значение:
//	Перечисления.СостоянияОперацийЗакрытияМесяца - состояние расчета долей.
//
Функция СостояниеРасчетДолейСписанияКосвенныхРасходов(СписокОрганизаций, Период)
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстРасчетаДолейСписанияКосвенныхРасходов();
	
	ТипыСубконто = Новый Массив;
	ТипыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	ТипыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	ТипыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами);
	
	МассивОрганизаций = ?(ЗначениеЗаполнено(СписокОрганизаций), СписокОрганизаций, Справочники.Организации.ДоступныеОрганизации());
	
	Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(Период));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(Период));
	Запрос.УстановитьПараметр("СписокОрганизаций", МассивОрганизаций);
	Запрос.УстановитьПараметр("Граница", Новый Граница(КонецМесяца(Период), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ТипыСубконто", ТипыСубконто);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Если Результат[4].Пустой() Тогда
		Состояние = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется;
	ИначеЕсли НЕ Результат[5].Пустой() Тогда
		Состояние = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено;
	Иначе
		Состояние = Перечисления.СостоянияОперацийЗакрытияМесяца.ВыполненоУспешно;
	КонецЕсли;
	
	Возврат Состояние;
	
КонецФункции

// Возвращает признак необходимости выполнения указанной операции для обособленного подразделения.
//
Функция ОперацияИспользуетсяДляОбособленногоПодразделения(ТипОперации) Экспорт
	
	Если ТипОперации = Перечисления.ТипыРегламентныхОпераций.ФормированиеФинансовогоРезультата
	 ИЛИ ТипОперации = Перечисления.ТипыРегламентныхОпераций.СписаниеУбытковПрошлыхЛет
	 ИЛИ ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетНалогаНаПрибыль
	 ИЛИ ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетОтложенногоНалога
	 ИЛИ ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетДолейСписанияКосвенныхРасходов Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Дата                    КАК Период,
	|	ДанныеДокумента.Организация             КАК Организация
	|ИЗ
	|	Документ.РегламентнаяОперация КАК ДанныеДокумента
	|	
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("НачПериода", НачалоМесяца(Реквизиты.Период));
	Запрос.УстановитьПараметр("КонПериода", Реквизиты.Период);
	Запрос.УстановитьПараметр("КонГраница", Новый Граница(Реквизиты.Период, ВидГраницы.Включая));
	
КонецПроцедуры

// Подготовка параметров выполнения операций документа

Функция ТекстЗапросаРеквизиты()

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка КАК Регистратор,
	|	РегламентнаяОперация.Дата КАК Период,
	|	РегламентнаяОперация.Организация КАК Организация
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|ГДЕ
	|	РегламентнаяОперация.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Расчет долей списания косвенных расходов

Функция ПодготовитьДанныеРасчетаДолейСписанияКосвенныхРасходов(СтруктураШапки, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", СтруктураШапки.Ссылка);
	
	Запрос.Текст = ТекстЗапросаРеквизиты();
	Результат    = Запрос.ВыполнитьПакет();
	НомераТаблиц = Новый Структура;
	
	НомераТаблиц.Вставить("ТаблицаРеквизитыРасчетДолей", НомераТаблиц.Количество());
	
	Возврат ТаблицыПроведения(НомераТаблиц, Результат);
	
КонецФункции

// Списание косвенных расходов

Функция ТекстЗапросаСписаниеКосвенныхРасходов(НомераТаблиц)
	
	// Временные таблицы
	НомераТаблиц.Вставить("ВТ_СтатьиРасходов", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_СписаниеРасходов", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаПрочиеРасходы", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтатьиРасходов.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ СтатьиРасходов
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|
	|ГДЕ
	|	СтатьиРасходов.ВидРасходов В (&ВидыРасходовНормируемые)
	|	И НЕ СтатьиРасходов.ВариантРаспределенияРасходовРегл В (
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаРасходыБудущихПериодов), 
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров),
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НеРаспределять),
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&КонДата                               КАК Период,
	|	&ХозяйственнаяОперация                 КАК ХозяйственнаяОперация,
	|	ПрочиеРасходы.Организация              КАК Организация,
	|	ПрочиеРасходы.Подразделение            КАК Подразделение,
	|	ПрочиеРасходы.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	ПрочиеРасходы.СтатьяРасходов           КАК СтатьяРасходов,
	|	ПрочиеРасходы.АналитикаРасходов        КАК АналитикаРасходов,
	|	ПрочиеРасходы.СуммаРеглОстаток         КАК СуммаРегл,
	|	ПрочиеРасходы.ПостояннаяРазницаОстаток КАК ПостояннаяРазница,
	|	ПрочиеРасходы.ВременнаяРазницаОстаток  КАК ВременнаяРазница
	|ПОМЕСТИТЬ СписаниеРасходов
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы.Остатки(
	|			&Граница,
	|			Организация = &Организация
	|				И СтатьяРасходов В (ВЫБРАТЬ Т.Ссылка ИЗ СтатьиРасходов КАК Т)) КАК ПрочиеРасходы
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписаниеРасходов.Период                   КАК Период,
	|	СписаниеРасходов.ХозяйственнаяОперация    КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)    КАК ВидДвижения,
	|	СписаниеРасходов.Организация              КАК Организация,
	|	СписаниеРасходов.Подразделение            КАК Подразделение,
	|	СписаниеРасходов.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	СписаниеРасходов.СтатьяРасходов           КАК СтатьяРасходов,
	|	СписаниеРасходов.АналитикаРасходов        КАК АналитикаРасходов,
	|	СписаниеРасходов.СуммаРегл                КАК СуммаРегл,
	|	СписаниеРасходов.ПостояннаяРазница        КАК ПостояннаяРазница,
	|	СписаниеРасходов.ВременнаяРазница         КАК ВременнаяРазница,
	|	НастройкиХозяйственныхОпераций.Ссылка     КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	СписаниеРасходов КАК СписаниеРасходов
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиХозяйственныхОпераций КАК НастройкиХозяйственныхОпераций
	|		ПО СписаниеРасходов.ХозяйственнаяОперация = НастройкиХозяйственныхОпераций.ХозяйственнаяОперация
	|ГДЕ
	|	СписаниеРасходов.СуммаРегл <> 0
	|	ИЛИ СписаниеРасходов.ПостояннаяРазница <> 0
	|	ИЛИ СписаниеРасходов.ВременнаяРазница <> 0
	|";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

// Формирование финансового результата

Функция ПодготовитьПараметрыЗакрытиеСчетов90_91(СтруктураШапки, Отказ) Экспорт
	
	ПараметрыПроведения = Новый Структура;
	
	Запрос     = Новый Запрос;
	Содержание = НСтр("ru = 'Определение финансовых результатов';
						|en = 'Determine financial results'");
	Запрос.УстановитьПараметр("Ссылка",          СтруктураШапки.Ссылка);
	Запрос.УстановитьПараметр("КоэффициентЕНВД", НалоговыйУчет.КоэффициентРаспределенияРасходовПоВидамДеятельности(СтруктураШапки.Организация, СтруктураШапки.КонДата));
	Запрос.УстановитьПараметр("Содержание",      Содержание);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаЗакрытиеСчетов90_91(НомераТаблиц);
	Результат    = Запрос.ВыполнитьПакет();

	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции // ПодготовитьПараметрыЗакрытиеСчетов90_91()

Функция ТекстЗапросаЗакрытиеСчетов90_91(НомераТаблиц)
	
	// Временные таблицы
	НомераТаблиц.Вставить("ТаблицаРеквизитыЗакрытие90_91", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка КАК Регистратор,
	|	РегламентнаяОперация.Организация КАК Организация,
	|	РегламентнаяОперация.Дата КАК Период,
	|	НАЧАЛОПЕРИОДА(РегламентнаяОперация.Дата, МЕСЯЦ) КАК НачДата,
	|	КОНЕЦПЕРИОДА(РегламентнаяОперация.Дата, МЕСЯЦ) КАК КонДата,
	|	&КоэффициентЕНВД КАК КоэффициентЕНВД,
	|	&Содержание КАК Содержание
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|ГДЕ
	|	РегламентнаяОперация.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции // ТекстЗапросаЗакрытиеСчетов90_91()

Функция ТекстЗапросаОкруглениеНДС(НомераТаблиц)
	
	// Временные таблицы
	НомераТаблиц.Вставить("ТаблицаРеквизитыОкруглениеНДС", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка КАК Регистратор,
	|	РегламентнаяОперация.Организация КАК Организация,
	|	РегламентнаяОперация.Дата КАК Период,
	|	НАЧАЛОПЕРИОДА(РегламентнаяОперация.Дата, КВАРТАЛ) КАК НачДата,
	|	КОНЕЦПЕРИОДА(РегламентнаяОперация.Дата, КВАРТАЛ) КАК КонДата,
	|	&Содержание КАК Содержание
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|ГДЕ
	|	РегламентнаяОперация.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции // ТекстЗапросаОкруглениеНДС()

Функция ПодготовитьПараметрыОкругленияНДС(СтруктураШапки) Экспорт
	
	ПараметрыПроведения = Новый Структура;
	
	Запрос     = Новый Запрос;
	Содержание = НСтр("ru = 'Отклонение при округлениии до рублей';
						|en = 'Variance on rounding up to rubles'");
	Запрос.УстановитьПараметр("Ссылка",          СтруктураШапки.Ссылка);
	Запрос.УстановитьПараметр("Содержание",      Содержание);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаОкруглениеНДС(НомераТаблиц);
	Результат    = Запрос.ВыполнитьПакет();

	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции // ПодготовитьПараметрыОкругленияНДС()

// Списание убытков прошлых лет

Функция ПодготовитьПараметрыСписаниеУбытков(СтруктураШапки, Отказ) Экспорт
	
	ПараметрыПроведения = Новый Структура;
	
	Запрос     = Новый Запрос;
	Содержание = НСтр("ru = 'Списание убытков прошлых лет';
						|en = 'Debiting of previous years losses'");
	Запрос.УстановитьПараметр("Ссылка",     СтруктураШапки.Ссылка);
	Запрос.УстановитьПараметр("Содержание", Содержание);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаСписаниеУбытковПрошлыхЛет(НомераТаблиц);
	Результат    = Запрос.ВыполнитьПакет();

	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции // ПодготовитьПараметрыСписаниеУбытковПрошлыхЛет()

Функция ТекстЗапросаСписаниеУбытковПрошлыхЛет(НомераТаблиц)
	
	// Временная таблица
	НомераТаблиц.Вставить("ТаблицаРеквизитыСписаниеУбытков", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка КАК Регистратор,
	|	РегламентнаяОперация.Организация КАК Организация,
	|	РегламентнаяОперация.Дата КАК Период,
	|	НАЧАЛОПЕРИОДА(РегламентнаяОперация.Дата, МЕСЯЦ) КАК НачДата,
	|	КОНЕЦПЕРИОДА(РегламентнаяОперация.Дата, МЕСЯЦ) КАК КонДата,
	|	&Содержание КАК Содержание
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|ГДЕ
	|	РегламентнаяОперация.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции // ТекстЗапросаСписаниеУбытковПрошлыхЛет()

// Расчет налога на прибыль

// Подготовка данных для регл. операции "Расчет налога на прибыль"
// 
// Параметры:
// 	СтруктураШапки - Структура - структура шапки документа "Регламентная операция"
// 	Отказ - Булево -
//
// Возвращаемое значение:
// 	Структура - Описание:
// 		* ТаблицаРеквизитыРасчетНалога - ТаблицаЗначений - содержит:
// 			** Регистратор - ДокументСсылка.РегламентнаяОперация -
//			** Организация - СправочникСсылка.Организации -
//			** Дата        - Дата -
//			** Период      - Дата -
//			** НачалоГода  - Дата -
//			** НачДата     - Дата -
//			** КонДата     - Дата -
//			** РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане -
//			** КоэффициентЕНВД             - Число -
//			** СтавкаНалогаНаПрибыль       - Число -
//			** Содержание                  - Строка
//
Функция ПодготовитьДанныеРасчетаНалогаНаПрибыль(СтруктураШапки, Отказ) Экспорт
	
#Область ДопПараметрыШапки // Для передачи "наверх!" - в ПроведениеПересчетаОНАиОНОКаждыйМесяц()
	СтруктураШапки.Вставить(
		"ПоддержкаПБУ18",
		УчетнаяПолитика.ПоддержкаПБУ18(СтруктураШапки.Организация, СтруктураШапки.КонДата));
	СтруктураШапки.Вставить(
		"СтавкаНалогаНаПрибыль",
		БухгалтерскийУчетПереопределяемый.ПолучитьСтавкуНалогаНаПрибыль(СтруктураШапки));
#Конецобласти
	
	Запрос = Новый Запрос;
	
	РегистрацияВНалоговомОргане = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		СтруктураШапки.Организация,
		"РегистрацияВНалоговомОргане").РегистрацияВНалоговомОргане;
	КоэффициентЕНВД = НалоговыйУчет.КоэффициентРаспределенияРасходовПоВидамДеятельности(
		СтруктураШапки.Организация,
		СтруктураШапки.КонДата);
	
	Запрос.УстановитьПараметр("Ссылка",                      СтруктураШапки.Ссылка);
	Запрос.УстановитьПараметр("СтавкаНалогаНаПрибыль",       СтруктураШапки.СтавкаНалогаНаПрибыль);
	Запрос.УстановитьПараметр("КоэффициентЕНВД",             КоэффициентЕНВД);
	Запрос.УстановитьПараметр("Содержание",                  НСтр("ru = 'Налог на прибыль';
																	|en = 'Profit tax'"));
	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаРасчетНалога(НомераТаблиц);
	Результат    = Запрос.ВыполнитьПакет();
	
	Возврат ТаблицыПроведения(НомераТаблиц, Результат);
	
КонецФункции

Функция ТекстЗапросаРасчетНалога(НомераТаблиц)
	
	// Временные таблицы
	НомераТаблиц.Вставить("ТаблицаРеквизитыРасчетНалога", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка КАК Регистратор,
	|	РегламентнаяОперация.Организация КАК Организация,
	|	РегламентнаяОперация.Дата КАК Дата,
	|	РегламентнаяОперация.Дата КАК Период,
	|	НАЧАЛОПЕРИОДА(РегламентнаяОперация.Дата, ГОД) КАК НачалоГода,
	|	НАЧАЛОПЕРИОДА(РегламентнаяОперация.Дата, МЕСЯЦ) КАК НачДата,
	|	КОНЕЦПЕРИОДА(РегламентнаяОперация.Дата, МЕСЯЦ) КАК КонДата,
	|	&РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	&КоэффициентЕНВД КАК КоэффициентЕНВД,
	|	&СтавкаНалогаНаПрибыль КАК СтавкаНалогаНаПрибыль,
	|	&Содержание КАК Содержание
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|ГДЕ
	|	РегламентнаяОперация.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

// Закрытие года

Функция ПодготовитьДанныеЗакрытияГода(СтруктураШапки, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	
	СодержаниеРеформация   = НСтр("ru = 'Реформация баланса';
									|en = 'Balance sheet closing'");;
	СодержаниеЗакрытиеГода = НСтр("ru = 'Закрытие года';
									|en = 'Year-end closing'");
	
	Период                 = СтруктураШапки.Дата;
	СтруктураШапки.Дата    = КонецМесяца(СтруктураШапки.Дата) + 1;
	НоваяСтавка            = БухгалтерскийУчетПереопределяемый.ПолучитьСтавкуНалогаНаПрибыль(СтруктураШапки) * 100;
	
	СтруктураШапки.Дата    = Период;
	СтараяСтавка           = БухгалтерскийУчетПереопределяемый.ПолучитьСтавкуНалогаНаПрибыль(СтруктураШапки) * 100;
	
	Запрос.УстановитьПараметр("Ссылка",                  СтруктураШапки.Ссылка);
	Запрос.УстановитьПараметр("НоваяСтавка",             НоваяСтавка);
	Запрос.УстановитьПараметр("СтараяСтавка",            СтараяСтавка);
	Запрос.УстановитьПараметр("Предприниматель",         СтруктураШапки.Предприниматель);
	Запрос.УстановитьПараметр("СодержаниеРеформация",    СодержаниеРеформация);
	Запрос.УстановитьПараметр("СодержаниеЗакрытиеГода",  СодержаниеЗакрытиеГода);
	Запрос.УстановитьПараметр("ЗакрытиеГода",            Истина);
	Запрос.УстановитьПараметр("ДоляСписанияТР",          0);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация",   Перечисления.ХозяйственныеОперации.ЗакрытиеМесяца);
	Запрос.УстановитьПараметр("НачДата",                 СтруктураШапки.НачДата);
	Запрос.УстановитьПараметр("КонДата",                 СтруктураШапки.КонДата);
	Запрос.УстановитьПараметр("Граница",                 СтруктураШапки.КонГраница);
	Запрос.УстановитьПараметр("Организация",             СтруктураШапки.Организация);
	Запрос.УстановитьПараметр("ВидыРасходовНормируемые", Перечисления.ВидыРасходовНУ.НормируемыеРасходы());
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаЗакрытиеГода(НомераТаблиц)
	             + ТекстЗапросаСписаниеКосвенныхРасходов(НомераТаблиц);
	Результат    = Запрос.ВыполнитьПакет();
	
	Возврат ТаблицыПроведения(НомераТаблиц, Результат);
	
КонецФункции

Функция ТекстЗапросаЗакрытиеГода(НомераТаблиц)
	
	// Временная таблица
	НомераТаблиц.Вставить("ТаблицаРеквизитыЗакрытиеГода", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка КАК Регистратор,
	|	РегламентнаяОперация.Организация КАК Организация,
	|	РегламентнаяОперация.Дата КАК Период,
	|	НАЧАЛОПЕРИОДА(РегламентнаяОперация.Дата, ГОД) КАК НачалоГода,
	|	&НачДата КАК НачДата,
	|	&КонДата КАК КонДата,
	|	&НоваяСтавка КАК НоваяСтавка,
	|	&СтараяСтавка КАК СтараяСтавка,
	|	&Предприниматель КАК Предприниматель,
	|	&СодержаниеРеформация КАК СодержаниеРеформация,
	|	&СодержаниеЗакрытиеГода КАК СодержаниеЗакрытиеГода
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|ГДЕ
	|	РегламентнаяОперация.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

// Прочее

Функция ТаблицыПроведения(НомераТаблиц, Результат)
	
	ТаблицыПроведения = Новый Структура;
	
	Для Каждого НомерТаблицы Из НомераТаблиц Цикл
		Если НРег(Лев(НомерТаблицы.Ключ, 7)) = "таблица" Тогда
			ТаблицыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицыПроведения;
	
КонецФункции

// Расчет налога на имущество

Функция ПодготовитьПараметрыРасчетИмущественныхНалогов(СтруктураШапки, Отказ) Экспорт
	
	ПараметрыПроведения = Новый Структура;

	Если СтруктураШапки.ВидНалога = Перечисления.ВидыИмущественныхНалогов.НалогНаИмущество Тогда
		
		НачалоПериода = НачалоГода(СтруктураШапки.НачДата);
		ИмяРегистраРасчетНалогов = "РасчетНалогаНаИмущество";
		
	Иначе
		НачалоПериода = ?(КонецГода(СтруктураШапки.КонДата) = КонецКвартала(СтруктураШапки.КонДата),
						НачалоГода(СтруктураШапки.НачДата),
						НачалоКвартала(СтруктураШапки.НачДата));
						
		Если СтруктураШапки.ВидНалога = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог Тогда
			ИмяРегистраРасчетНалогов = "РасчетТранспортногоНалога";	
		ИначеЕсли СтруктураШапки.ВидНалога = Перечисления.ВидыИмущественныхНалогов.ЗемельныйНалог Тогда
			ИмяРегистраРасчетНалогов = "РасчетЗемельногоНалога";	
		КонецЕсли;
			
	КонецЕсли;
	
	Содержание  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Начислен %1 за %2';
			|en = '%1 accrued for %2'"),
		Строка(СтруктураШапки.ВидНалога),
		ПредставлениеПериода(НачалоПериода, СтруктураШапки.КонДата, "ФП = Истина"));
	
	Запрос = Новый Запрос;
		
	Запрос.УстановитьПараметр("Ссылка", СтруктураШапки.Ссылка);
	Запрос.УстановитьПараметр("Содержание", Содержание);
	Запрос.УстановитьПараметр("ВидНалога", СтруктураШапки.ВидНалога);
	Запрос.УстановитьПараметр("ИмяРегистраРасчетНалогов", ИмяРегистраРасчетНалогов);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаРасчетИмущественныхНалогов(НомераТаблиц);
	Результат = Запрос.ВыполнитьПакет();
	
	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции

Функция ТекстЗапросаРасчетИмущественныхНалогов(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаРеквизитыРасчетИмущественныхНалогов", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка КАК Регистратор,
	|	РегламентнаяОперация.Организация КАК Организация,
	|	РегламентнаяОперация.Дата КАК Период,
	|	&ВидНалога КАК ВидНалога,
	|	&Содержание КАК Содержание,
	|	&ИмяРегистраРасчетНалогов КАК ИмяРегистраРасчетНалогов
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|ГДЕ
	|	РегламентнаяОперация.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

// Расчет курсовых разниц

Процедура РасчетКурсовыхРазниц(ПараметрыРасчета, Отказ) Экспорт
	
	Период = КонецМесяца(ПараметрыРасчета.Дата);
	Запрос = Новый Запрос(ТекстЗапросаРасчетКурсовыхРазниц());
	Запрос.УстановитьПараметр("Организация", ПараметрыРасчета.Организация);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(ПараметрыРасчета.Организация));
	Запрос.УстановитьПараметр("ГраницаОстатков", Новый Граница(Период, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("НаДату", Период);
	
	НастройкиРеглУчета = РеглУчетСервер.НастройкиУчета(); 
	УправленческийУчетНаПланеСчетовРегл = НастройкиРеглУчета.ДополнительноВедетсяУправленческийУчет;
	ДатаНачалаВеденияУУ = НастройкиРеглУчета.ДатаНачалаВеденияУправленческогоУчета;
	Запрос.УстановитьПараметр("ВестиУУНаПланеСчетов", УправленческийУчетНаПланеСчетовРегл И Период >= ДатаНачалаВеденияУУ);
	Запрос.УстановитьПараметр("ВалютаУпр", Константы.ВалютаУправленческогоУчета.Получить());
	
	ПроводкиДокумента = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
	ПроводкиДокумента.Отбор.Регистратор.Установить(ПараметрыРасчета.Ссылка);
	
	СтатьяДДС = Новый Структура("ВидСубконто, Прибыль, Убыток");
	СтатьяДДС.ВидСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиДвиженияДенежныхСредств;
	СтатьяДДС.Прибыль = Справочники.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыПрибыль;
	СтатьяДДС.Убыток = Справочники.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыУбыток;
	
	КурсоваяРазница = Новый Структура("Ресурс, Сумма");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Проводка = ПроводкиДокумента.Добавить();
		Проводка.Активность = Истина;
		Проводка.Период = Период;
		Проводка.Организация = ПараметрыРасчета.Организация;
		
		КурсоваяРазница.Ресурс = "Сумма";
		КурсоваяРазница.Сумма = Выборка.КурсоваяРазница;
		ЗаполнитьПроводку(Проводка, Выборка, КурсоваяРазница, СтатьяДДС);
		
		Если УправленческийУчетНаПланеСчетовРегл Тогда
			
			КурсоваяРазница.Ресурс = "СуммаУУ";
			КурсоваяРазница.Сумма = Выборка.КурсоваяРазницаУУ;
			
			Если Выборка.РазныйЗнакКусовыхРазниц Тогда
				// Добавим новую проводку для отражение курсовой разницы по УУ
				Проводка = ПроводкиДокумента.Добавить();
				Проводка.Активность = Истина;
				Проводка.Период = Период;
				Проводка.Организация = ПараметрыРасчета.Организация;
				
				ЗаполнитьПроводку(Проводка, Выборка, КурсоваяРазница, СтатьяДДС);
			Иначе
				// Заполним данные в текущей проводке
				ЗаполнитьПроводку(Проводка, Выборка, КурсоваяРазница, СтатьяДДС);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;// по валютным остаткам
	
	ПроводкиДокумента.Записать();
	
КонецПроцедуры

Процедура ЗаполнитьПроводку(Проводка, Выборка, КурсоваяРазница, ПараметрыСтатьиДДС = Неопределено)
	
	Перем КорСчет, КорСубконто, КорВидСубконто;
	
	СуммаРазницы = Макс(КурсоваяРазница.Сумма, -КурсоваяРазница.Сумма);
	
	Если КурсоваяРазница.Сумма > 0 Тогда // отразим прибыль
		Счет = "Дт";
		Кор = "Кт";
		КорУчетПоПодразделениям = Выборка.СчетДоходовУчетПоПодразделениям;
		КорУчетПоНаправлениям = Выборка.СчетДоходовУчетПоНаправлениямДеятельности;
		Если Не Выборка.СчетЗабалансовый Тогда
			КорСчет = Выборка.СчетДоходов;
			КорСубконто = Выборка.СтатьяДоходов;
			КорВидСубконто = Выборка.ВидСубконтоДоходыРасходы;
		КонецЕсли;
		СтатьяДДС = ?(ПараметрыСтатьиДДС = Неопределено, Неопределено, ПараметрыСтатьиДДС.Прибыль);
		
	ИначеЕсли КурсоваяРазница.Сумма < 0 Тогда // отразим убытки
		Счет = "Кт";
		Кор = "Дт";
		КорУчетПоПодразделениям = Выборка.СчетРасходовУчетПоПодразделениям;
		КорУчетПоНаправлениям = Выборка.СчетРасходовУчетПоНаправлениямДеятельности;
		Если Не Выборка.СчетЗабалансовый Тогда
			КорСчет = Выборка.СчетРасходов;
			КорСубконто = Выборка.СтатьяРасходов;
			КорВидСубконто = Выборка.ВидСубконтоДоходыРасходы;
		КонецЕсли;
		СтатьяДДС = ?(ПараметрыСтатьиДДС = Неопределено, Неопределено, ПараметрыСтатьиДДС.Убыток);
	Иначе // проводку не заполняем
		Возврат;
	КонецЕсли;
	
	// заполним счет и аналитику возникновения курсовой разницы
	Проводка["Счет"+Счет] = Выборка.Счет;
	Проводка["Валюта"+Счет] = Выборка.Валюта;
	Проводка["Подразделение"+Счет] = Выборка.Подразделение;
	Проводка["НаправлениеДеятельности"+Счет] = Выборка.НаправлениеДеятельности;
	ВидыСубконтоСчета = Выборка.ВидыСубконтоСчета.Выгрузить();
	Для Каждого СтрокаТаблицы Из ВидыСубконтоСчета Цикл
		Если Счет = "Дт" Тогда
			Проводка.СубконтоДт[СтрокаТаблицы.ВидСубконто] = Выборка["Субконто"+СтрокаТаблицы.НомерСтроки];
		Иначе
			Проводка.СубконтоКт[СтрокаТаблицы.ВидСубконто] = Выборка["Субконто"+СтрокаТаблицы.НомерСтроки];
		КонецЕсли;
		Если СтатьяДДС <> Неопределено 
			И СтрокаТаблицы.ВидСубконто = ПараметрыСтатьиДДС.ВидСубконто 
			И СтрокаТаблицы.ТолькоОбороты Тогда
			
			Если Счет = "Дт" Тогда
				Проводка.СубконтоДт[СтрокаТаблицы.ВидСубконто] = СтатьяДДС;
			Иначе
				Проводка.СубконтоКт[СтрокаТаблицы.ВидСубконто] = СтатьяДДС;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	// заполним счет учета прибыли\убытка от курсовой разницы
	Проводка["Счет" + Кор] = КорСчет;
	Если КорУчетПоПодразделениям Тогда
		Проводка["Подразделение"+Кор] = Выборка.Подразделение;
	КонецЕсли;
	Если КорУчетПоНаправлениям Тогда
		Проводка["НаправлениеДеятельности"+Кор] = Выборка.НаправлениеДеятельности;
	КонецЕсли;
	Если ЗначениеЗаполнено(КорВидСубконто) И  КорСчет.ВидыСубконто.Найти(КорВидСубконто, "ВидСубконто") <> Неопределено Тогда
		Если Кор = "Дт" Тогда
			Проводка.СубконтоДт[КорВидСубконто] = КорСубконто;
		Иначе
			Проводка.СубконтоКт[КорВидСубконто] = КорСубконто;
		КонецЕсли;
	КонецЕсли;
	
	Проводка[КурсоваяРазница.Ресурс] = СуммаРазницы;
	Если КурсоваяРазница.Ресурс = "Сумма" Тогда
		Если Выборка.НалоговыйУчет Тогда
			Проводка["СуммаНУ"+Счет] = СуммаРазницы;
		КонецЕсли;
		Если Не Выборка.СчетЗабалансовый Тогда
			Проводка["СуммаНУ"+Кор] = СуммаРазницы;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстЗапросаРасчетКурсовыхРазниц()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Период,
	|	КурсыВалютСрезПоследних.Валюта,
	|	КурсыВалютСрезПоследних.КурсЧислитель,
	|	КурсыВалютСрезПоследних.КурсЗнаменатель
	|ПОМЕСТИТЬ втКурсУУ
	|ИЗ
	|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&НаДату, Валюта = &ВалютаУпр И БазоваяВалюта = &ВалютаРегламентированногоУчета) КАК КурсыВалютСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Период,
	|	КурсыВалютСрезПоследних.Валюта,
	|	КурсыВалютСрезПоследних.КурсЧислитель,
	|	КурсыВалютСрезПоследних.КурсЗнаменатель
	|ПОМЕСТИТЬ втКурсы
	|ИЗ
	|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&НаДату, БазоваяВалюта = &ВалютаРегламентированногоУчета) КАК КурсыВалютСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Хозрасчетный.Ссылка,
	|	Хозрасчетный.Валютный,
	|	Хозрасчетный.НалоговыйУчет,
	|	Хозрасчетный.ИсключитьСуммуБУИзПереоценкиПоПлануСчетов,
	|	Хозрасчетный.ИсключитьСуммуУУИзПереоценкиПоПлануСчетов
	|ПОМЕСТИТЬ втСчета
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Валютный
	|	И НЕ (Хозрасчетный.ИсключитьСуммуБУИзПереоценкиПоПлануСчетов
	|			ИЛИ &ВестиУУНаПланеСчетов И Хозрасчетный.ИсключитьСуммуУУИзПереоценкиПоПлануСчетов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Остатки.Счет                    КАК Счет,
	|	Остатки.Организация             КАК Организация,
	|	Остатки.Подразделение           КАК Подразделение,
	|	Остатки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Остатки.Субконто1               КАК Субконто1,
	|	Остатки.Субконто2               КАК Субконто2,
	|	Остатки.Субконто3               КАК Субконто3,
	|	Остатки.Валюта                  КАК Валюта,
	|	Остатки.ВалютнаяСуммаОстаток    КАК ВалютнаяСуммаОстаток,
	|	Остатки.СуммаОстаток            КАК ОстатокБУОстаток,
	|	Остатки.СуммаУУОстаток          КАК СуммаУУОстаток
	|ПОМЕСТИТЬ ВтОстатки
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ГраницаОстатков,
	|			Счет В (ВЫБРАТЬ Т.Ссылка ИЗ втСчета КАК Т),
	|			,
	|			Организация = &Организация) КАК Остатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Данные.Счет                      КАК Счет,
	|	Данные.Организация               КАК Организация,
	|	Данные.Подразделение             КАК Подразделение,
	|	Данные.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|	Данные.Субконто1                 КАК Субконто1,
	|	Данные.Субконто2                 КАК Субконто2,
	|	Данные.Субконто3                 КАК Субконто3,
	|	Данные.Валюта                    КАК Валюта,
	|	Данные.ОстатокВалюты             КАК ОстатокВалюты,
	|	
	|	СУММА(Данные.ОстатокРегл)        КАК ОстатокРегл,
	|	СУММА(Данные.ОстатокПоКурсу)     КАК ОстатокПоКурсу,
	|	СУММА(Данные.АбсолютнаяРазница)  КАК АбсолютнаяРазница,
	|	СУММА(Данные.КурсоваяРазница)    КАК КурсоваяРазница,
	|	
	|	СУММА(Данные.ОстатокПоКурсуУУ)    КАК ОстатокПоКурсуУУ,
	|	СУММА(Данные.АбсолютнаяРазницаУУ) КАК АбсолютнаяРазницаУУ,
	|	СУММА(Данные.КурсоваяРазницаУУ)   КАК КурсоваяРазницаУУ
	|ПОМЕСТИТЬ втКурсовыеРазницы
	|ИЗ
	|	(ВЫБРАТЬ
	|		Остатки.Счет,
	|		Остатки.Организация,
	|		Остатки.Подразделение,
	|		Остатки.НаправлениеДеятельности,
	|		Остатки.Субконто1,
	|		Остатки.Субконто2,
	|		Остатки.Субконто3,
	|		Остатки.Валюта,
	|		Остатки.ВалютнаяСуммаОстаток КАК ОстатокВалюты,
	|		
	|		Остатки.ОстатокБУОстаток КАК ОстатокРегл,
	|		Остатки.ВалютнаяСуммаОстаток * Курсы.КурсЧислитель / Курсы.КурсЗнаменатель КАК ОстатокПоКурсу,
	|		Остатки.ВалютнаяСуммаОстаток * Курсы.КурсЧислитель / Курсы.КурсЗнаменатель - Остатки.ОстатокБУОстаток КАК АбсолютнаяРазница,
	|		ВЫРАЗИТЬ(Остатки.ВалютнаяСуммаОстаток * Курсы.КурсЧислитель / Курсы.КурсЗнаменатель КАК ЧИСЛО(31,2)) - Остатки.ОстатокБУОстаток КАК КурсоваяРазница,
	|		
	|		0 КАК ОстатокПоКурсуУУ,
	|		0 КАК АбсолютнаяРазницаУУ,
	|		0 КАК КурсоваяРазницаУУ
	|	
	|	ИЗ
	|		ВтОстатки КАК Остатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсы КАК Курсы
	|			ПО Остатки.Валюта = Курсы.Валюта
	|	ГДЕ
	|		НЕ Остатки.Счет.ИсключитьСуммуБУИзПереоценкиПоПлануСчетов
	|		И ВЫРАЗИТЬ(Остатки.ВалютнаяСуммаОстаток * Курсы.КурсЧислитель / Курсы.КурсЗнаменатель КАК ЧИСЛО(31,2)) <> Остатки.ОстатокБУОстаток
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Остатки.Счет,
	|		Остатки.Организация,
	|		Остатки.Подразделение,
	|		Остатки.НаправлениеДеятельности,
	|		Остатки.Субконто1,
	|		Остатки.Субконто2,
	|		Остатки.Субконто3,
	|		Остатки.Валюта,
	|		Остатки.ВалютнаяСуммаОстаток КАК ОстатокВалюты,
	|		
	|		0 КАК ОстатокРегл,
	|		0 КАК ОстатокПоКурсу,
	|		0 КАК АбсолютнаяРазница,
	|		0 КАК КурсоваяРазница,
	|		
	|		Остатки.ВалютнаяСуммаОстаток * Курсы.КурсЧислитель / Курсы.КурсЗнаменатель / КурсУУ.КурсЧислитель * КурсУУ.КурсЗнаменатель КАК ОстатокПоКурсуУУ,
	|		Остатки.ВалютнаяСуммаОстаток * Курсы.КурсЧислитель / Курсы.КурсЗнаменатель / КурсУУ.КурсЧислитель * КурсУУ.КурсЗнаменатель - Остатки.СуммаУУОстаток КАК АбсолютнаяРазницаУУ,
	|		ВЫРАЗИТЬ(Остатки.ВалютнаяСуммаОстаток * Курсы.КурсЧислитель / Курсы.КурсЗнаменатель / КурсУУ.КурсЧислитель * КурсУУ.КурсЗнаменатель КАК ЧИСЛО(31,2)) - Остатки.СуммаУУОстаток КАК КурсоваяРазницаУУ
	|	
	|	ИЗ
	|		ВтОстатки КАК Остатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсы КАК Курсы
	|			ПО Остатки.Валюта = Курсы.Валюта
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсУУ КАК КурсУУ
	|			ПО ИСТИНА
	|	ГДЕ
	|		&ВестиУУНаПланеСчетов
	|		И НЕ Остатки.Счет.ИсключитьСуммуУУИзПереоценкиПоПлануСчетов
	|		И ВЫРАЗИТЬ(Остатки.ВалютнаяСуммаОстаток * Курсы.КурсЧислитель / Курсы.КурсЗнаменатель / КурсУУ.КурсЧислитель * КурсУУ.КурсЗнаменатель КАК ЧИСЛО(31,2)) <> Остатки.СуммаУУОстаток
	|	) КАК Данные
	|
	|СГРУППИРОВАТЬ ПО
	|	Данные.Счет,
	|	Данные.Организация,
	|	Данные.Подразделение,
	|	Данные.НаправлениеДеятельности,
	|	Данные.Субконто1,
	|	Данные.Субконто2,
	|	Данные.Субконто3,
	|	Данные.Валюта,
	|	Данные.ОстатокВалюты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Данные.Счет                      КАК Счет,
	|	Данные.Организация               КАК Организация,
	|	Данные.Подразделение             КАК Подразделение,
	|	Данные.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|	Данные.Субконто1                 КАК Субконто1,
	|	Данные.Субконто2                 КАК Субконто2,
	|	Данные.Субконто3                 КАК Субконто3,
	|	Данные.Валюта                    КАК Валюта,
	|	Данные.ОстатокВалюты             КАК ОстатокВалюты,
	|	
	|	Данные.ОстатокРегл               КАК ОстатокРегл,
	|	Данные.ОстатокПоКурсу            КАК ОстатокПоКурсу,
	|	Данные.АбсолютнаяРазница         КАК АбсолютнаяРазница,
	|	Данные.КурсоваяРазница           КАК КурсоваяРазница,
	|	
	|	Данные.ОстатокПоКурсуУУ          КАК ОстатокПоКурсуУУ,
	|	Данные.АбсолютнаяРазницаУУ       КАК АбсолютнаяРазницаУУ,
	|	Данные.КурсоваяРазницаУУ         КАК КурсоваяРазницаУУ,
	|	
	|	ВЫБОР 
	|		КОГДА Данные.КурсоваяРазница < 0 И Данные.КурсоваяРазницаУУ > 0 
	|			ТОГДА ИСТИНА
	|		КОГДА Данные.КурсоваяРазница > 0 И Данные.КурсоваяРазницаУУ < 0 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                            КАК РазныйЗнакКусовыхРазниц, 
	|	
	|	ЕСТЬNULL(НастройкиПоАналитикеДоходов.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеДоходы)) КАК СчетДоходов,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы) КАК СтатьяДоходов,
	|	
	|	ЕСТЬNULL(НастройкиПоАналитикеРасходов.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасходы)) КАК СчетРасходов,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы) КАК СтатьяРасходов,
	|	
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИрасходы) КАК ВидСубконтоДоходыРасходы
	|
	|ПОМЕСТИТЬ втДанные
	|ИЗ
	|	втКурсовыеРазницы КАК Данные
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК КурсовыеРазницыРасходов
	|	ПО
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы) = КурсовыеРазницыРасходов.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиДоходов КАК КурсовыеРазницыДоходов
	|	ПО
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы) = КурсовыеРазницыДоходов.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК НастройкиПоАналитикеРасходов
	|	ПО 
	|		НастройкиПоАналитикеРасходов.ВидСчета = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы)
	|		И НастройкиПоАналитикеРасходов.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		И НастройкиПоАналитикеРасходов.АналитикаУчета = КурсовыеРазницыРасходов.ГруппаФинансовогоУчетаРегл
	|		И НастройкиПоАналитикеРасходов.МестоУчета = НЕОПРЕДЕЛЕНО
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК НастройкиПоАналитикеДоходов
	|	ПО 
	|		НастройкиПоАналитикеДоходов.ВидСчета = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы)
	|		И НастройкиПоАналитикеДоходов.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		И НастройкиПоАналитикеДоходов.АналитикаУчета = КурсовыеРазницыДоходов.ГруппаФинансовогоУчета
	|		И НастройкиПоАналитикеДоходов.МестоУчета = НЕОПРЕДЕЛЕНО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Данные.Счет                      КАК Счет,
	|	Данные.Счет.ВидыСубконто         КАК ВидыСубконтоСчета,
	|	Данные.Счет.Валютный             КАК СчетВалютный,
	|	Данные.Счет.Забалансовый         КАК СчетЗабалансовый,
	|	Данные.Счет.НалоговыйУчет        КАК НалоговыйУчет,
	|	Данные.Организация               КАК Организация,
	|	Данные.Подразделение             КАК Подразделение,
	|	Данные.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|	Данные.Субконто1                 КАК Субконто1,
	|	Данные.Субконто2                 КАК Субконто2,
	|	Данные.Субконто3                 КАК Субконто3,
	|	Данные.Валюта                    КАК Валюта,
	|	Данные.ОстатокВалюты             КАК ОстатокВалюты,
	|	
	|	Данные.ОстатокРегл               КАК ОстатокРегл,
	|	Данные.ОстатокПоКурсу            КАК ОстатокПоКурсу,
	|	Данные.АбсолютнаяРазница         КАК АбсолютнаяРазница,
	|	Данные.КурсоваяРазница           КАК КурсоваяРазница,
	|	
	|	Данные.ОстатокПоКурсуУУ          КАК ОстатокПоКурсуУУ,
	|	Данные.АбсолютнаяРазницаУУ       КАК АбсолютнаяРазницаУУ,
	|	Данные.КурсоваяРазницаУУ         КАК КурсоваяРазницаУУ,
	|	
	|	Данные.РазныйЗнакКусовыхРазниц   КАК РазныйЗнакКусовыхРазниц, 
	|	
	|	Данные.СчетДоходов КАК СчетДоходов,
	|	Данные.СчетДоходов.УчетПоПодразделениям КАК СчетДоходовУчетПоПодразделениям,
	|	Данные.СчетДоходов.УчетПоНаправлениямДеятельности КАК СчетДоходовУчетПоНаправлениямДеятельности,
	|	Данные.СтатьяДоходов КАК СтатьяДоходов,
	|	
	|	Данные.СчетРасходов КАК СчетРасходов,
	|	Данные.СчетРасходов.УчетПоПодразделениям КАК СчетРасходовУчетПоПодразделениям,
	|	Данные.СчетРасходов.УчетПоНаправлениямДеятельности КАК СчетРасходовУчетПоНаправлениямДеятельности,
	|	Данные.СтатьяРасходов КАК СтатьяРасходов,
	|	
	|	Данные.ВидСубконтоДоходыРасходы КАК ВидСубконтоДоходыРасходы
	|
	|ИЗ
	|	втДанные КАК Данные
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура РасчетКурсовыхРазницФО(ПараметрыРасчета, Отказ) Экспорт
	
	НастройкиРеглУчета = РеглУчетСервер.НастройкиУчета();
	
	Если НЕ НастройкиРеглУчета.ДополнительноВедетсяУчетВВалютеФинОтчетности Тогда
		Возврат;
	КонецЕсли;
	
	ВалютаРегламентированногоУчета = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(ПараметрыРасчета.Организация);
	Если Константы.ИсточникСуммыДляПересчетаВВалютуФинОтчетности.Получить() = Перечисления.ИсточникиСуммыДляПересчетаВВалютуФинОтчетности.БУ Тогда
		ТекущаяВалюта = ВалютаРегламентированногоУчета;
		ПолеИсточника = "Остатки.СуммаОстаток";
	ИначеЕсли Константы.ИсточникСуммыДляПересчетаВВалютуФинОтчетности.Получить() = Перечисления.ИсточникиСуммыДляПересчетаВВалютуФинОтчетности.УУ Тогда
		ТекущаяВалюта = Константы.ВалютаУправленческогоУчета.Получить();
		ПолеИсточника = "Остатки.СуммаУУОстаток";
	Иначе
		Возврат;
	КонецЕсли;
	НоваяВалюта = НастройкиРеглУчета.ВалютаФинОтчетности;
	Период = КонецМесяца(ПараметрыРасчета.Дата);
	КоэффициентПересчетаВалютыФО = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентПересчетаИзВалютыВВалюту(
										ТекущаяВалюта, НоваяВалюта, Период, ВалютаРегламентированногоУчета);
	
	ТекстЗапроса = ТекстЗапросаРасчетКурсовыхРазницФО();
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИсточникДляПересчета", ПолеИсточника);
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Организация", ПараметрыРасчета.Организация);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
	Запрос.УстановитьПараметр("ГраницаОстатков", Новый Граница(Период, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КоэффициентПересчета", КоэффициентПересчетаВалютыФО);
	
	ПроводкиДокумента = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
	ПроводкиДокумента.Отбор.Регистратор.Установить(ПараметрыРасчета.Ссылка);
	
	КурсоваяРазница = Новый Структура("Ресурс, Сумма");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Проводка = ПроводкиДокумента.Добавить();
		Проводка.Активность = Истина;
		Проводка.Период = Период;
		Проводка.Организация = ПараметрыРасчета.Организация;
		
		КурсоваяРазница.Ресурс = "СуммаФО";
		КурсоваяРазница.Сумма = Выборка.КурсоваяРазница;
		ЗаполнитьПроводку(Проводка, Выборка, КурсоваяРазница);
		ДебетКредит = ?(Проводка.СчетДт = ПланыСчетов.Хозрасчетный.ПрибылиИУбыткиНеЕНВД Или Проводка.СчетДт.Пустая(), "Дт", "Кт");
		Проводка["Подразделение"+ДебетКредит] = Неопределено;
		Проводка["НаправлениеДеятельности"+ДебетКредит] = Неопределено;
		
	КонецЦикла;// по валютным остаткам
	
	ПроводкиДокумента.Записать();
	
КонецПроцедуры

Функция ТекстЗапросаРасчетКурсовыхРазницФО()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Остатки.Счет,
	|	Остатки.Организация,
	|	Остатки.Валюта,
	|	Остатки.Подразделение,
	|	Остатки.НаправлениеДеятельности,
	|	Остатки.Счет.ВидыСубконто КАК ВидыСубконтоСчета,
	|	Остатки.Счет.Валютный КАК СчетВалютный,
	|	Остатки.Счет.Забалансовый КАК СчетЗабалансовый,
	|	Остатки.Счет.НалоговыйУчет КАК НалоговыйУчет,
	|	Остатки.Субконто1,
	|	Остатки.Субконто2,
	|	Остатки.Субконто3,
	|
	|	Остатки.ВалютнаяСуммаОстаток КАК ОстатокВалюты,
	|	Остатки.СуммаОстаток КАК ОстатокРегл,
	|	Остатки.СуммаУУОстаток КАК ОстатокУпр,
	|	Остатки.СуммаФООстаток КАК ОстатокФО,
	|	&ИсточникДляПересчета * &КоэффициентПересчета КАК ОстатокПоКурсу,
	|	&ИсточникДляПересчета * &КоэффициентПересчета - Остатки.СуммаФООстаток КАК АбсолютнаяРазница,
	|	(ВЫРАЗИТЬ(&ИсточникДляПересчета * &КоэффициентПересчета КАК ЧИСЛО(31,2))) - Остатки.СуммаФООстаток КАК КурсоваяРазница,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрибылиИУбыткиНеЕНВД) КАК СчетДоходов,
	|	ЛОЖЬ КАК СчетДоходовУчетПоПодразделениям,
	|	ЛОЖЬ КАК СчетДоходовУчетПоНаправлениямДеятельности,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрибылиИУбыткиНеЕНВД) КАК СчетРасходов,
	|	ЛОЖЬ КАК СчетРасходовУчетПоПодразделениям,
	|	ЛОЖЬ КАК СчетРасходовУчетПоНаправлениямДеятельности,
	|	ЗНАЧЕНИЕ(Перечисление.ПрибылиИУбытки.КурсовыеРазницыОтПересчетаВВалютуФО) КАК СтатьяДоходов,
	|	ЗНАЧЕНИЕ(Перечисление.ПрибылиИУбытки.КурсовыеРазницыОтПересчетаВВалютуФО) КАК СтатьяРасходов,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрибылиИУбытки) КАК ВидСубконтоДоходыРасходы
	|	
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&ГраницаОстатков,
	|		НЕ Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрибылиИУбытки)),
	|		,
	|		Организация = &Организация) КАК Остатки
	|ГДЕ
	|	(ВЫРАЗИТЬ(&ИсточникДляПересчета * &КоэффициентПересчета КАК ЧИСЛО(31,2))) <> Остатки.СуммаФООстаток";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Расчет торгового сбора

Функция ПодготовитьПараметрыРасчетТорговогоСбора(СтруктураШапки, Отказ) Экспорт
	
	ПараметрыПроведения = Новый Структура;
	
	НачалоПериода = НачалоКвартала(СтруктураШапки.НачДата);
	КонецПериода  = КонецКвартала(СтруктураШапки.КонДата);
	
	Содержание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Начислен торговый сбор за %1';
			|en = 'Sales charge is accrued for %1'"),
		ПредставлениеПериода(НачалоПериода, КонецПериода, "ФП = Истина"));
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", СтруктураШапки.Ссылка);
	Запрос.УстановитьПараметр("Содержание", Содержание);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаРасчетТорговогоСбора(НомераТаблиц);
	Результат    = Запрос.ВыполнитьПакет();
	
	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции

Функция ТекстЗапросаРасчетТорговогоСбора(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаРеквизитыРасчетТорговогоСбора", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка                       КАК Регистратор,
	|	РегламентнаяОперация.Организация                  КАК Организация,
	|	РегламентнаяОперация.Дата                         КАК Период,
	|	НАЧАЛОПЕРИОДА(РегламентнаяОперация.Дата, КВАРТАЛ) КАК НачДата,
	|	КОНЕЦПЕРИОДА(РегламентнаяОперация.Дата, КВАРТАЛ)  КАК КонДата,
	|	&Содержание                                       КАК Содержание
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|ГДЕ
	|	РегламентнаяОперация.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

// Начисление налога УСН

Функция ПодготовитьПараметрыНачисленияНалогаУСН(СтруктураШапки, Отказ) Экспорт

	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", СтруктураШапки.Ссылка);
	
	Запрос.УстановитьПараметр("ПрименяетсяУСН",
		УчетнаяПолитика.ПрименяетсяУСН(СтруктураШапки.Организация, СтруктураШапки.КонДата));
	Запрос.УстановитьПараметр("ПрименяетсяУСНДоходы",
		УчетнаяПолитика.ПрименяетсяУСНДоходы(СтруктураШапки.Организация, СтруктураШапки.КонДата));
	Запрос.УстановитьПараметр("ПрименяетсяУСНДоходыМинусРасходы",
		УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(СтруктураШапки.Организация, СтруктураШапки.КонДата));
	Запрос.УстановитьПараметр("СтавкаНалогаУСН",
		УчетнаяПолитика.СтавкаНалогаУСН(СтруктураШапки.Организация, СтруктураШапки.КонДата));
		
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаНачислениеНалогаУСН(НомераТаблиц);
	Результат = Запрос.ВыполнитьПакет();
	
	Возврат ТаблицыПроведения(НомераТаблиц, Результат);

КонецФункции

Функция ТекстЗапросаНачислениеНалогаУСН(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаРеквизитыНалогУСН", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка КАК Регистратор,
	|	РегламентнаяОперация.Организация КАК Организация,
	|	РегламентнаяОперация.Дата КАК Период,
	|	НАЧАЛОПЕРИОДА(РегламентнаяОперация.Дата, ГОД) КАК НачалоГода,
	|	КОНЕЦПЕРИОДА(РегламентнаяОперация.Дата, КВАРТАЛ) КАК КонДата,
	|	&ПрименяетсяУСН КАК ПрименяетсяУСН,
	|	&ПрименяетсяУСНДоходы КАК ПрименяетсяУСНДоходы,
	|	&ПрименяетсяУСНДоходыМинусРасходы КАК ПрименяетсяУСНДоходыМинусРасходы,
	|	&СтавкаНалогаУСН,
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(РегламентнаяОперация.Дата, ГОД) = НАЧАЛОПЕРИОДА(РегламентнаяОперация.Дата, КВАРТАЛ)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоНачалоГода,
	|	ВЫБОР
	|		КОГДА КОНЕЦПЕРИОДА(РегламентнаяОперация.Дата, ГОД) = КОНЕЦПЕРИОДА(РегламентнаяОперация.Дата, КВАРТАЛ)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоКонецГода
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|ГДЕ
	|	РегламентнаяОперация.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

#Область БлокировкаПриОбновленииИБ

Процедура ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ПредставлениеОперации, ТипОперации) Экспорт
	
	ВходящиеДанные = Новый Соответствие;
	
	ВходящиеДанные.Вставить(Метаданные.Документы.РегламентнаяОперация);
	ВходящиеДанные.Вставить(Метаданные.РегистрыБухгалтерии.Хозрасчетный);
	
	Если ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетДолейСписанияКосвенныхРасходов Тогда
		
		ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ВыручкаИСебестоимостьПродаж);
		ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ДвиженияКонтрагентДоходыРасходы);
		ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПрочиеРасходы);
		ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.СебестоимостьТоваров);
		ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ТрудозатратыНезавершенногоПроизводства);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.АналитикаУчетаНоменклатуры);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.АналитикаУчетаПоПартнерам);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.ДолиСписанияКосвенныхРасходов);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.ПорядокОтраженияНаСчетахУчета);
		
	ИначеЕсли ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетНалогаНаИмущество
	 ИЛИ ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетТранспортногоНалога
	 ИЛИ ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетЗемельногоНалога Тогда
		
		ВходящиеДанные.Вставить(Метаданные.Документы.ИзменениеПараметровОС);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.МестонахождениеОС);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.ПервоначальныеСведенияОС);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.ПорядокУплатыНалоговНаМестах);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.ПорядокУчетаОС);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.ПорядокУчетаОСБУ);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.РегистрацияЗемельныхУчастков);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.РегистрацияТранспортныхСредств);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.СпособыОтраженияРасходовПоИмущественнымНалогам);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.СтавкиНалогаНаИмущество);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам);
		
	ИначеЕсли ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетКурсовыхРазниц Тогда
		
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.ОтносительныеКурсыВалют);
		
	ИначеЕсли ТипОперации = Перечисления.ТипыРегламентныхОпераций.ФормированиеФинансовогоРезультата Тогда
		
		ВходящиеДанные.Вставить(Метаданные.Документы.СчетФактураНалоговыйАгент);
		
	ИначеЕсли ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетНалогаНаПрибыль
		ИЛИ ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетОтложенногоНалога Тогда
		
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.ПервоначальныеСведенияОС);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.РасчетДолейБазыНалогаНаПрибыль);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.РасчетСтавкиНалогаНаПрибыльЗаМесяц);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.РегистрацииВНалоговомОргане);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.СтавкиНалогаНаПрибыльВБюджетСубъектовРФ);
		
	ИначеЕсли ТипОперации = Перечисления.ТипыРегламентныхОпераций.ЗакрытиеГода Тогда
		
		ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПрочиеРасходы);
		ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.ДолиСписанияКосвенныхРасходов);
		
	КонецЕсли;
	
	ЗакрытиеМесяцаСервер.ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ВходящиеДанные, ПредставлениеОперации);
	
КонецПроцедуры

#КонецОбласти


#КонецОбласти

#КонецЕсли
