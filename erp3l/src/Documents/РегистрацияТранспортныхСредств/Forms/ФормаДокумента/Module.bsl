
#Область ОписаниеПеременных

&НаКлиенте
Перем СтруктураСохраняемыхРеквизитов;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
	ПараметрыВыбораСтатейИАналитик = Документы.РегистрацияТранспортныхСредств.ПараметрыВыбораСтатейИАналитик();
	ДоходыИРасходыСервер.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
	#Область СтандартныеПодсистемы
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ИнтеграцияС1СДокументооборот.ПриСозданииНаСервере(ЭтотОбъект);
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	#КонецОбласти
	
	// СтандартныеПодсистемы.РаботаСФайлами
	ГиперссылкаФайлов = РаботаСФайлами.ГиперссылкаФайлов();
	ГиперссылкаФайлов.Размещение = "КоманднаяПанель";
	РаботаСФайлами.ПриСозданииНаСервере(ЭтотОбъект, ГиперссылкаФайлов);
	// Конец СтандартныеПодсистемы.РаботаСФайлами

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// Обработчик механизма "ДатыЗапретаИзменения"
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	ПриЧтенииСозданииНаСервере();
	
	ПараметрыВыбораСтатейИАналитик = Документы.РегистрацияТранспортныхСредств.ПараметрыВыбораСтатейИАналитик();
	ДоходыИРасходыСервер.ПриЧтенииНаСервере(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	СтруктураСохраняемыхРеквизитов = Новый Структура;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайламиКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	// Конец СтандартныеПодсистемы.РаботаСФайлами

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Объект.СтавкаОпределяетсяАвтоматически = Булево(СтавкаОпределяетсяАвтоматически);
	
	СобытияФормКлиент.ПередЗаписью(ЭтотОбъект, Отказ, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ВыполнитьИнициализацию();
	
	ДоходыИРасходыСервер.ПослеЗаписиНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_РегистрацияТранспортныхСредств", ПараметрыЗаписи, Объект.Ссылка);
	ОбщегоНазначенияУТКлиент.ОповеститьОЗаписиДокументаВЖурнал();
	ВнеоборотныеАктивыКлиент.ОповеститьОЗаписиДокументаВЖурналОС();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ОбъектыЭксплуатации"
		ИЛИ ИмяСобытия = "ЗагруженыСтавкиТранспортногоНалога"
		ИЛИ ИмяСобытия = "Запись_СтавкиТранспортногоНалога" Тогда
		ЗаполнитьПредставлениеНалоговойСтавки(ЭтотОбъект);
	КонецЕсли;
	
	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайламиКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия);
	// Конец СтандартныеПодсистемы.РаботаСФайлами

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	ДатаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КодВидаТранспортногоСредстваПриИзменении(Элемент)
	
	СтруктураКодов = КодыВидовТС.Получить(Объект.КодВидаТранспортногоСредства);
	
	Если СтруктураКодов <> Неопределено Тогда
		
		КодЕдиницыИзмерения = Элементы.ЕдиницаИзмеренияНалоговойБазы.СписокВыбора.НайтиПоЗначению(СтруктураКодов.КодЕдиницыИзмерения);
		Если КодЕдиницыИзмерения <> Неопределено Тогда
			Объект.ЕдиницаИзмеренияНалоговойБазы = КодЕдиницыИзмерения.Значение;
		Иначе
			Объект.ЕдиницаИзмеренияНалоговойБазы = "";
		КонецЕсли;
			
		УстановитьЗаголовокНалоговойБазы(ЭтотОбъект);
	КонецЕсли;
	
	ЗаполнитьНаименованиеВидаТС(ЭтотОбъект);
	ЗаполнитьПредставлениеНалоговойСтавки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КодВидаТранспортногоСредстваНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если СписокВыбораКодовТС.Количество() = 0 Тогда
	
		СписокВыбораКодовТС = Новый СписокЗначений;
		СписокВыбораКодовТС.Добавить("51000", НСтр("ru = '51000 Автомобили легковые';
													|en = '51000 Passenger cars'"));
		СписокВыбораКодовТС.Добавить("51004", НСтр("ru = '51004 - прочие автомобили легковые (кроме включенных по кодам 56600, 56700)';
													|en = '51004 - other cars (except for included by 56600, 56700 codes)'"));
		СписокВыбораКодовТС.Добавить("52001", НСтр("ru = '52001 Автомобили грузовые (кроме включенных по коду 57000)';
													|en = '52001 Trucks (except for included by 57000 code)'"));
		
	КонецЕсли;
	
	Если СписокВыбораКодовТС.НайтиПоЗначению("ФормаВыбораКодаТС") = Неопределено Тогда
		СписокВыбораКодовТС.Добавить("ФормаВыбораКодаТС", НСтр("ru = 'Другие коды видов ТС...';
																|en = 'Other codes of vehicle kinds...'"));
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ВыбратьИзСпискаКодВидаТранспортногоСредстваЗавершение", ЭтотОбъект, Элемент);
	ПоказатьВыборИзСписка(Оповещение, СписокВыбораКодовТС, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияНалоговойБазыПриИзменении(Элемент)
	
	УстановитьЗаголовокНалоговойБазы(ЭтотОбъект);
	ЗаполнитьПредставлениеНалоговойСтавки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭкологическийКлассНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипОбъекта", "РегистрСведений");
	ПараметрыФормы.Вставить("НазваниеОбъекта", "РегистрацияТранспортныхСредств");
	ПараметрыФормы.Вставить("НазваниеМакета", "КодыЭкологическихКлассов");
	ПараметрыФормы.Вставить("ТекущийПериод", Объект.Дата);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ЭкологическийКлассНачалоВыбораЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораКода", ПараметрыФормы,,,,,ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура ПостановкаНаУчетВНалоговомОрганеПриИзменении(Элемент)
	
	УстановитьДоступностьНалоговогоОргана(ЭтотОбъект);
	
	Если Объект.ПостановкаНаУчетВНалоговомОргане = ПредопределенноеЗначение("Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане") Тогда
		ВосстановитьРеквизиты(Объект, "НалоговыйОрган, КодПоОКТМО, КодПоОКАТО", СтруктураСохраняемыхРеквизитов);
	Иначе
		СохранитьРеквизиты(Объект, "НалоговыйОрган, КодПоОКТМО, КодПоОКАТО", СтруктураСохраняемыхРеквизитов);
	КонецЕсли;
	
	УстановитьЗаголовокСвернутогоОтображенияПостановкаНаУчетВНО();
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговыйОрганПриИзменении(Элемент)
	
	НалоговыйОрганПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговаяЛьготаПриИзменении(Элемент)
	
	// Сохраним значения
	Если ТекущаяНалоговаяЛьгота = 
		ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения") Тогда
		
		СохранитьРеквизиты(ЭтотОбъект,
			"КодНалоговойЛьготыОсвобождениеОтНалогообложения, РегиональныйКодЛьготыОсвобождениеОтНалогообложения",
			СтруктураСохраняемыхРеквизитов);
			
		КодНалоговойЛьготыОсвобождениеОтНалогообложения = "";
		РегиональныйКодЛьготыОсвобождениеОтНалогообложения = "";
			
	ИначеЕсли ТекущаяНалоговаяЛьгота = 
		ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки") Тогда
		
		СохранитьРеквизиты(ЭтотОбъект, "РегиональныйКодЛьготыСнижениеНалоговойСтавки", СтруктураСохраняемыхРеквизитов);
		СохранитьРеквизиты(Объект, "ЛьготнаяСтавка", СтруктураСохраняемыхРеквизитов);
		
		КодНалоговойЛьготыСнижениеНалоговойСтавки = "";
		РегиональныйКодЛьготыСнижениеНалоговойСтавки = "";
		Объект.ЛьготнаяСтавка = 0;
		
	ИначеЕсли ТекущаяНалоговаяЛьгота = 
		ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах") Тогда
		
		СохранитьРеквизиты(ЭтотОбъект, "РегиональныйКодЛьготыУменьшениеСуммыНалогаВПроцентах", СтруктураСохраняемыхРеквизитов);
		СохранитьРеквизиты(Объект, "ПроцентУменьшения", СтруктураСохраняемыхРеквизитов);
		
		КодНалоговойЛьготыУменьшениеСуммыНалогаВПроцентах = "";
		РегиональныйКодЛьготыУменьшениеСуммыНалогаВПроцентах = "";
		Объект.ПроцентУменьшения = 0;
		
	ИначеЕсли ТекущаяНалоговаяЛьгота = 
		ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму") Тогда
		
		СохранитьРеквизиты(ЭтотОбъект, "КодНалоговойЛьготыУменьшениеСуммыНалогаНаСумму", СтруктураСохраняемыхРеквизитов);
		СохранитьРеквизиты(Объект, "СуммаУменьшения", СтруктураСохраняемыхРеквизитов);
		
		КодНалоговойЛьготыУменьшениеСуммыНалогаНаСумму = "";
		Объект.СуммаУменьшения = 0;
		
	КонецЕсли;
	
	Объект.КодНалоговойЛьготы = "";
	Объект.РегиональныйКодЛьготы = "";
	
	// Восстановим значения
	Если Объект.НалоговаяЛьгота = 
		ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения") Тогда
		
		ВосстановитьРеквизиты(ЭтотОбъект,
			"КодНалоговойЛьготыОсвобождениеОтНалогообложения, РегиональныйКодЛьготыОсвобождениеОтНалогообложения",
			СтруктураСохраняемыхРеквизитов);
			
		Объект.КодНалоговойЛьготы = КодНалоговойЛьготыОсвобождениеОтНалогообложения;
		Объект.РегиональныйКодЛьготы = РегиональныйКодЛьготыОсвобождениеОтНалогообложения;
			
	ИначеЕсли Объект.НалоговаяЛьгота = 
		ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки") Тогда
		
		КодНалоговойЛьготыСнижениеНалоговойСтавки = "20230";
		ВосстановитьРеквизиты(ЭтотОбъект, "РегиональныйКодЛьготыСнижениеНалоговойСтавки", СтруктураСохраняемыхРеквизитов);
		ВосстановитьРеквизиты(Объект, "ЛьготнаяСтавка", СтруктураСохраняемыхРеквизитов);
		
		Объект.КодНалоговойЛьготы = КодНалоговойЛьготыСнижениеНалоговойСтавки;
		Объект.РегиональныйКодЛьготы = РегиональныйКодЛьготыСнижениеНалоговойСтавки;
		
	ИначеЕсли Объект.НалоговаяЛьгота = 
		ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах") Тогда
		
		КодНалоговойЛьготыУменьшениеСуммыНалогаВПроцентах = "20220";
		ВосстановитьРеквизиты(ЭтотОбъект, "РегиональныйКодЛьготыУменьшениеСуммыНалогаВПроцентах", СтруктураСохраняемыхРеквизитов);
		ВосстановитьРеквизиты(Объект, "ПроцентУменьшения", СтруктураСохраняемыхРеквизитов);
		
		Объект.КодНалоговойЛьготы = КодНалоговойЛьготыУменьшениеСуммыНалогаВПроцентах;
		Объект.РегиональныйКодЛьготы = РегиональныйКодЛьготыУменьшениеСуммыНалогаВПроцентах;
		
	ИначеЕсли Объект.НалоговаяЛьгота = 
		ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму") Тогда
		
		ВосстановитьРеквизиты(ЭтотОбъект, "КодНалоговойЛьготыУменьшениеСуммыНалогаНаСумму", СтруктураСохраняемыхРеквизитов);
		ВосстановитьРеквизиты(Объект, "СуммаУменьшения", СтруктураСохраняемыхРеквизитов);
		
		Объект.КодНалоговойЛьготы = КодНалоговойЛьготыУменьшениеСуммыНалогаНаСумму;
		Объект.РегиональныйКодЛьготы = "";
		
	КонецЕсли;
	
	ТекущаяНалоговаяЛьгота = Объект.НалоговаяЛьгота;
	УстановитьДоступностьНалоговыхЛьгот(ЭтотОбъект);
	УстановитьЗаголовокСвернутогоОтображенияНалоговойЛьготы(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура КодПоОКТМОПриИзменении(Элемент)
	
	УстановитьЗаголовокСвернутогоОтображенияПостановкаНаУчетВНО();
	ЗаполнитьПредставлениеНалоговойСтавки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготнаяСтавкаПриИзменении(Элемент)
	
	УстановитьЗаголовокСвернутогоОтображенияНалоговойЛьготы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцентУменьшенияПриИзменении(Элемент)
	
	УстановитьЗаголовокСвернутогоОтображенияНалоговойЛьготы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаУменьшенияПриИзменении(Элемент)
	
	УстановитьЗаголовокСвернутогоОтображенияНалоговойЛьготы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КодЛьготыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипОбъекта", "РегистрСведений");
	ПараметрыФормы.Вставить("НазваниеОбъекта", "РегистрацияТранспортныхСредств");
	ПараметрыФормы.Вставить("НазваниеМакета", "ЛьготыПоТранспортномуНалогу");
	Если Элемент.Имя = "КодЛьготыУменьшениеСуммыНалогаНаСумму" Тогда
		ПараметрыФормы.Вставить("ТекущийПериод", '20070101');
	Иначе
		ПараметрыФормы.Вставить("ТекущийПериод", Объект.Дата);
	КонецЕсли;

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Элемент", Элемент);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("КодНалоговойЛьготыНачалоВыбораЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораКода", ПараметрыФормы,,,,,ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура КодЛьготыОчистка(Элемент, СтандартнаяОбработка)
	
	Объект.КодНалоговойЛьготы = "";
	УстановитьЗаголовокСвернутогоОтображенияНалоговойЛьготы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура РегиональныйКодЛьготыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОснованиеЛьготы", Объект.РегиональныйКодЛьготы);
	
	ДополнительныеПараметры = Новый Структура("Льгота", Элемент.Имя);
	Оповещение = Новый ОписаниеОповещения("ОткрытьФормуРегиональныйКодЛьготыЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму(
		"Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ФормаВводаОснованияЛьготы", 
		ПараметрыФормы, 
		ЭтотОбъект,,,,Оповещение,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура РегиональныйКодЛьготыОчистка(Элемент, СтандартнаяОбработка)
	
	Объект.РегиональныйКодЛьготы = "";
	УстановитьЗаголовокСвернутогоОтображенияНалоговойЛьготы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговаяБазаПриИзменении(Элемент)
	
	ЗаполнитьПредставлениеНалоговойСтавки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаОпределяетсяАвтоматическиПриИзменении(Элемент)
	
	Объект.НалоговаяСтавка = 0;
	Объект.НалоговаяСтавкаЗависитОтГодаВыпускаТС = Ложь;
	ЗаполнитьПредставлениеНалоговойСтавки(ЭтотОбъект);
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "СтавкаОпределяетсяАвтоматически");
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаУказанаВручнуюПриИзменении(Элемент)
	
	ЗаполнитьНалоговуюСтавку(ЭтотОбъект);
	Объект.НалоговаяСтавка = НалоговаяСтавка;
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "СтавкаОпределяетсяАвтоматически");
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговаяСтавкаПредставлениеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	
	Если ПараметрыСтавки <> Неопределено Тогда
	
		ОтборНабораЗаписей = Новый Структура("Период,ОКТМО");
		ЗаполнитьЗначенияСвойств(ОтборНабораЗаписей, ПараметрыСтавки);
		
		ПараметрыФормы.Вставить("Отбор", ОтборНабораЗаписей);
		
		ПараметрыСтавкиФормыСписка = Новый Структура(
			"Период,
			|ОКТМО,
			|НаименованиеОбъектаНалогообложения,
			|МинимальноеЗначениеМощности,
			|МаксимальноеЗначениеМощности,
			|МинимальноеКоличествоЛетСГодаВыпускаТС,
			|МаксимальноеКоличествоЛетСГодаВыпускаТС");
		
		ЗаполнитьЗначенияСвойств(ПараметрыСтавкиФормыСписка, ПараметрыСтавки);
		ПараметрыФормы.Вставить("ПараметрыСтавки", ПараметрыСтавкиФормыСписка);
		
	КонецЕсли;	
			
	ОткрытьФорму("РегистрСведений.СтавкиТранспортногоНалога.ФормаСписка", ПараметрыФормы);
	Оповестить("ПоказатьПараметрыСтавки", ПараметрыФормы);
	
КонецПроцедуры

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраНажатие(Элемент, СтандартнаяОбработка)

	РаботаСФайламиКлиент.ПолеПредпросмотраНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)

	РаботаСФайламиКлиент.ПолеПредпросмотраПроверкаПеретаскивания(ЭтотОбъект, Элемент,
				ПараметрыПеретаскивания, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)

	РаботаСФайламиКлиент.ПолеПредпросмотраПеретаскивание(ЭтотОбъект, Элемент,
				ПараметрыПеретаскивания, СтандартнаяОбработка);

КонецПроцедуры
// Конец СтандартныеПодсистемы.РаботаСФайлами

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОС

&НаКлиенте
Процедура ОСПриИзменении(Элемент)
	
	ЗаполнитьПредставлениеНалоговойСтавки(ЭтотОбъект);
	ЗаполнитьКодыВидовТС();
	
КонецПроцедуры

&НаКлиенте
Процедура ОСПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	УстановитьВидимость(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОСОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ВнеоборотныеАктивыКлиентСервер.ОбработкаВыбораЭлемента(Объект.ОС, "ОсновноеСредство", ВыбранноеЗначение);
	УстановитьВидимость(ЭтотОбъект);
	
	ЗаполнитьПредставлениеНалоговойСтавки(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтражениеРасходов

&НаКлиенте
Процедура ОтражениеРасходовСтатьяРасходовПриИзменении(Элемент)
	
	ДоходыИРасходыКлиентСервер.СтатьяПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтражениеРасходовСтатьяРасходовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.НачалоВыбораСтатьи(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтражениеРасходовАналитикаРасходовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.НачалоВыбораАналитикиРасходов(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтражениеРасходовАналитикаРасходовАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.АвтоПодборАналитикиРасходов(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтражениеРасходовАналитикаРасходовОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.ОкончаниеВводаТекстаАналитикиРасходов(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры


&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	
	ОбщегоНазначенияУТКлиент.ПровестиИЗакрыть(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Записать(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Провести(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДатуРегистрации(Команда)
	
	Если Элементы.ОС.ВыделенныеСтроки.Количество() = 0 Тогда
		ТекстПредупреждения = НСтр("ru = 'Необходимо выбрать строки, в которых необходимо заполнить дату регистрации';
									|en = 'Select lines to fill in a registration date'");
		ПоказатьПредупреждение(,ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ЗаполнитьДатуРегистрацииЗавершение", ЭтотОбъект, Элементы.ОС.ВыделенныеСтроки);
	ПоказатьВводДаты(Оповещение, Объект.Дата, НСтр("ru = 'Введите дату регистрации';
													|en = 'Enter registration date'"), ЧастиДаты.Дата);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСтавку(Команда)

	Ошибки = Новый Массив;
	
	ЗаполнитьНалоговуюСтавку(ЭтотОбъект, Ошибки);
	
	Объект.НалоговаяСтавка = НалоговаяСтавка;
	
	Для Каждого Ошибка Из Ошибки Цикл
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Ошибка.ТекстСообщения, , Ошибка.Поле, "");
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Подобрать(Команда)
	
	ПараметрыПодбора = ВнеоборотныеАктивыКлиентСервер.ПараметрыПодбора(Элементы.ОСОсновноеСредство, ЭтотОбъект);
	ОткрытьФорму("Справочник.ОбъектыЭксплуатации.ФормаВыбора", 
					ПараметрыПодбора, Элементы.ОС,,,,, 
					РежимОткрытияОкнаФормы.БлокироватьОкноВладельца)
	
КонецПроцедуры

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_КомандаПанелиПрисоединенныхФайлов(Команда)

	РаботаСФайламиКлиент.КомандаУправленияПрисоединеннымиФайлами(ЭтотОбъект, Команда);

КонецПроцедуры
// Конец СтандартныеПодсистемы.РаботаСФайлами

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	#Область ОбщаяСобственность
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.ОСДоляВПравеОбщейСобственностиЧислитель.Имя);
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.ОСДоляВПравеОбщейСобственностиЗнаменатель.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ОС.ОбщаяСобственность");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не требуется>';
																|en = '<not required>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	#КонецОбласти
	
	#Область ОСДатаРегистрации
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.ОСДатаРегистрации.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ОС.ДатаРегистрации");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<совпадает с датой документа>';
																|en = '<matches the document date>'"));
	
	#КонецОбласти
	
	#Область ОтражениеРасходовПодразделение
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОтражениеРасходовПодразделение.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ОтражениеРасходов.Подразделение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Местонахождение ОС>';
																|en = '<FA location>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	
	#КонецОбласти
	
	#Область ОтражениеРасходовНаправлениеДеятельности
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОтражениеРасходовНаправлениеДеятельности.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ОтражениеРасходов.НаправлениеДеятельности");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Направление деятельности ОС>';
																|en = '<FA line of business>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	
	#КонецОбласти
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	ПараметрыСтавки = Неопределено;
	
	ВыполнитьИнициализацию();
	
	ЗаполнитьДанныеОНалоговомОргане();
	УстановитьВидимость(ЭтотОбъект);
	УстановитьДоступностьНалоговогоОргана(ЭтотОбъект);
	УстановитьЗаголовокНалоговойБазы(ЭтотОбъект);
	ЗаполнитьНаименованиеВидаТС(ЭтотОбъект);
	УстановитьЗаголовокСвернутогоОтображенияПостановкаНаУчетВНО();
	УстановитьЗаголовокСвернутогоОтображенияНалоговойЛьготы(ЭтотОбъект);
	УстановитьДоступностьНалоговыхЛьгот(ЭтотОбъект);
	УстановитьГоловнуюОрганизацию(ЭтотОбъект);
	
	СтавкаОпределяетсяАвтоматически = Число(Объект.СтавкаОпределяетсяАвтоматически);
	ЗаполнитьПредставлениеНалоговойСтавки(ЭтотОбъект);
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормы(Форма, ИзмененныеРеквизитыИлиЭлемент = "")

	Если ТипЗнч(ИзмененныеРеквизитыИлиЭлемент) = Тип("Строка") Тогда
		ИзмененныеРеквизиты = ИзмененныеРеквизитыИлиЭлемент;
	Иначе
		ИзмененныеРеквизиты = ИзмененныеРеквизитыИлиЭлемент.Имя;
	КонецЕсли;
	
	Элементы = Форма.Элементы;
	
	СтруктураИзмененныхРеквизитов = Новый Структура(ИзмененныеРеквизиты);
	ОбновитьВсе = СтруктураИзмененныхРеквизитов.Количество() = 0;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("СтавкаОпределяетсяАвтоматически")
		ИЛИ ОбновитьВсе Тогда
		
		Элементы.НалоговаяСтавкаПредставление.Видимость = Форма.СтавкаОпределяетсяАвтоматически;
		Элементы.НалоговаяСтавка.ТолькоПросмотр = Форма.СтавкаОпределяетсяАвтоматически;
		Элементы.ЗаполнитьСтавку.Доступность = НЕ Форма.СтавкаОпределяетсяАвтоматически;
		Элементы.НалоговаяСтавкаЗависитОтГодаВыпускаТС.ТолькоПросмотр = Форма.СтавкаОпределяетсяАвтоматически;
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ВыполнитьИнициализацию()
	
	Если ИнициализацияВыполнена Тогда
		Возврат;
	КонецЕсли;
	
	ИнициализацияВыполнена = Истина;
	
	СписокКодовЕдиницИзмеренияТС = КодыЕдиницИзмеренияТС();
	Для Каждого ЕдиницаИзмеренияТС Из СписокКодовЕдиницИзмеренияТС Цикл
		Элементы.ЕдиницаИзмеренияНалоговойБазы.СписокВыбора.Добавить(ЕдиницаИзмеренияТС.Значение, ЕдиницаИзмеренияТС.Представление);
	КонецЦикла;
	
	ЗаполнитьСписокРегионов();
	ЗаполнитьКодыВидовТС();
	
	Если Объект.НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения Тогда
		КодНалоговойЛьготыОсвобождениеОтНалогообложения = Объект.КодНалоговойЛьготы;
		РегиональныйКодЛьготыОсвобождениеОтНалогообложения = Объект.РегиональныйКодЛьготы;
	ИначеЕсли Объект.НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки Тогда
		КодНалоговойЛьготыСнижениеНалоговойСтавки = Объект.КодНалоговойЛьготы;
		РегиональныйКодЛьготыСнижениеНалоговойСтавки = Объект.РегиональныйКодЛьготы;
	ИначеЕсли Объект.НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах Тогда
		КодНалоговойЛьготыУменьшениеСуммыНалогаВПроцентах = Объект.КодНалоговойЛьготы;
		РегиональныйКодЛьготыУменьшениеСуммыНалогаВПроцентах = Объект.РегиональныйКодЛьготы;
	ИначеЕсли Объект.НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму Тогда
		КодНалоговойЛьготыУменьшениеСуммыНалогаНаСумму = Объект.КодНалоговойЛьготы;
	КонецЕсли;
	
	// Кэшируем цвета гиперссылки в форме, чтобы не обращаться потом на сервер
	ЦветаГиперссылкиСтавкиНалога = Новый Структура;
	ЦветаГиперссылкиСтавкиНалога.Вставить("Заполнена", ЦветаСтиля.ГиперссылкаЦвет);
	ЦветаГиперссылкиСтавкиНалога.Вставить("НеЗаполнена", ЦветаСтиля.НезаполненныйРеквизит);
	
	ВалютаРегламентированногоУчетаПредставление = Строка(ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Объект.Организация));
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КодыЕдиницИзмеренияТС()
	
	Список = Новый СписокЗначений;
	
	Список.Добавить("251", НСтр("ru = 'л. с.';
								|en = 'r. r.'"));
	Список.Добавить("999", НСтр("ru = 'Кгс';
								|en = 'Kgf'"));
	Список.Добавить("181", НСтр("ru = 'БРТ';
								|en = 'GROSS'"));
	Список.Добавить("796", НСтр("ru = 'шт';
								|en = 'pcs.'"));
	
	Возврат Список;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьКодыВидовТС()
	
	ДатаРегистрации = ДатаРегистрации(Объект);
	
	Если ДатаРегистрации = '000101010000' Тогда
		ТекущийПоказательГод = Год(ТекущаяДатаСеанса());
	Иначе
		ТекущийПоказательГод = Год(ДатаРегистрации);
	КонецЕсли;
	
	Если ИмяОбластиМакетаВидыТранспортныхСредств(СтарыйГод) <> ИмяОбластиМакетаВидыТранспортныхСредств(ТекущийПоказательГод)
		ИЛИ СтарыйГод = 0 Тогда 
		
		ТаблицаКодовВидовТС = ПолучитьКодыВидовТС(ТекущийПоказательГод);
		СтарыйГод = ТекущийПоказательГод;

		СтруктураКода = Новый Структура("Наименование, КодЕдиницыИзмерения");
		
		СоответствиеКодов = Новый Соответствие;
		Для каждого СтрокаКода Из ТаблицаКодовВидовТС Цикл
			ЗаполнитьЗначенияСвойств(СтруктураКода, СтрокаКода);
			СоответствиеКодов.Вставить(СтрокаКода.Код, Новый ФиксированнаяСтруктура(СтруктураКода));
		КонецЦикла;
		КодыВидовТС = Новый ФиксированноеСоответствие(СоответствиеКодов);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКодыВидовТС(Год)

	КодыВидовТС = Новый ТаблицаЗначений();
	КодыВидовТС.Колонки.Добавить("Код");
	КодыВидовТС.Колонки.Добавить("Наименование");
	КодыВидовТС.Колонки.Добавить("КодЕдиницыИзмерения");
	
	Макет = РегистрыСведений.РегистрацияТранспортныхСредств.ПолучитьМакет("ВидыТранспортныхСредств");
	ТекущаяОбласть = Макет.Области.Найти(ИмяОбластиМакетаВидыТранспортныхСредств(Год));

	Если НЕ (ТекущаяОбласть = Неопределено) Тогда
	
		Для НомерСтр = ТекущаяОбласть.Верх По ТекущаяОбласть.Низ Цикл
			
			// Перебираем строки макета.
			КодПоказателя       = СокрП(Макет.Область(НомерСтр, 1).Текст);
			Название            = СокрП(Макет.Область(НомерСтр, 2).Текст);
			КодЕдиницыИзмерения = СокрП(Макет.Область(НомерСтр, 3).Текст);
			
			Если КодПоказателя = "###" Тогда
				Прервать;
			ИначеЕсли ПустаяСтрока(КодПоказателя) Тогда
				Продолжить;
			Иначе
				НоваяСтрока = КодыВидовТС.Добавить();
				НоваяСтрока.Код                 = КодПоказателя;
				НоваяСтрока.Наименование        = Название;
				НоваяСтрока.КодЕдиницыИзмерения = КодЕдиницыИзмерения;
			КонецЕсли;

		КонецЦикла;
		
	КонецЕсли;
	
	Возврат КодыВидовТС;

КонецФункции

&НаСервереБезКонтекста
Функция ИмяОбластиМакетаВидыТранспортныхСредств(Год)
	
	Если Год < 2011 Тогда
		Возврат "Область2007";
	ИначеЕсли Год < 2012 Тогда
		Возврат "Область2011";
	ИначеЕсли Год < 2013 Тогда
		Возврат "Область2012";
	ИначеЕсли Год < 2019 Тогда
		Возврат "Область2013";
	Иначе
		Возврат "Область2019";
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимость(Форма)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ДатаРегистрации = ВнеоборотныеАктивыКлиентСервер.МинимальнаяДатаРегистрации(Объект);
	
	Элементы.КодПоОКАТО.Видимость = (ДатаРегистрации <> '000101010000' И Год(ДатаРегистрации) < 2014);
	
	ВидимостьЛьготы = 
		(ДатаРегистрации <> '000101010000' И Год(ДатаРегистрации) < 2012) 
		ИЛИ Объект.НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму");
	Элементы.НалоговаяЛьготаУменьшениеСуммыНалогаНаСумму.Видимость = ВидимостьЛьготы;
	Элементы.СуммаУменьшения.Видимость = ВидимостьЛьготы;
	Элементы.КодЛьготыУменьшениеСуммыНалогаНаСумму.Видимость = ВидимостьЛьготы;
	
КонецПроцедуры
 
&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьНалоговогоОргана(Форма)
	
	Форма.Элементы.ГруппаНалоговыйОрган.Доступность = 
		Форма.Объект.ПостановкаНаУчетВНалоговомОргане = ПредопределенноеЗначение("Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьНалоговыхЛьгот(Форма)
	
	НалоговаяЛьгота = Форма.Объект.НалоговаяЛьгота;
	Элементы = Форма.Элементы;
	
	Элементы.ГруппаКодЛьготыОсвобождениеОтНалогообложения.Доступность = 
		НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения");
		
	СнижениеНалоговойСтавки = 
		НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки");
	Элементы.ЛьготнаяСтавка.Доступность = СнижениеНалоговойСтавки;
	Элементы.ГруппаКодЛьготыСнижениеНалоговойСтавки.Доступность = СнижениеНалоговойСтавки;
	
	УменьшениеСуммыНалогаВПроцентах = 
		НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах");
	Элементы.ПроцентУменьшения.Доступность = УменьшениеСуммыНалогаВПроцентах;
	Элементы.ГруппаКодЛьготыУменьшениеСуммыНалогаВПроцентах.Доступность = УменьшениеСуммыНалогаВПроцентах;
		
	УменьшениеСуммыНалогаНаСумму = 
		НалоговаяЛьгота = ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму");
	Элементы.СуммаУменьшения.Доступность = УменьшениеСуммыНалогаНаСумму;
	Элементы.КодЛьготыУменьшениеСуммыНалогаНаСумму.Доступность = УменьшениеСуммыНалогаНаСумму;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокНалоговойБазы(Форма)
	
	Форма.Элементы.НалоговаяБаза.Заголовок = ПредставлениеНалоговойБазы(Форма.Объект.ЕдиницаИзмеренияНалоговойБазы);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеНалоговойБазы(ЕдиницаИзмеренияНалоговойБазы)
	
	Если ПустаяСтрока(ЕдиницаИзмеренияНалоговойБазы) Тогда
		ПредставлениеНалоговойБазы = НСтр("ru = 'Налоговая база';
											|en = 'Tax base'");
	ИначеЕсли ЕдиницаИзмеренияНалоговойБазы = "251" Тогда
		ПредставлениеНалоговойБазы = НСтр("ru = 'Мощность двигателя';
											|en = 'Engine capacity '");
	ИначеЕсли ЕдиницаИзмеренияНалоговойБазы = "999" Тогда
		ПредставлениеНалоговойБазы = НСтр("ru = 'Сила тяги';
											|en = 'Tractive force'");
	ИначеЕсли ЕдиницаИзмеренияНалоговойБазы = "181" Тогда
		ПредставлениеНалоговойБазы = НСтр("ru = 'Валовая вместимость';
											|en = 'Gross tonnage'");
	ИначеЕсли ЕдиницаИзмеренияНалоговойБазы = "796" Тогда
		ПредставлениеНалоговойБазы = НСтр("ru = 'Налоговая база';
											|en = 'Tax base'");
	Иначе
		ПредставлениеНалоговойБазы = НСтр("ru = 'Налоговая база';
											|en = 'Tax base'");
	КонецЕсли;
	
	Возврат ПредставлениеНалоговойБазы;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьНаименованиеВидаТС(Форма)
	
	Если ПустаяСтрока(Форма.Объект.КодВидаТранспортногоСредства) Тогда
		Форма.НаименованиеВидаТС = "";
	Иначе
		СтруктураКодов = Форма.КодыВидовТС.Получить(Форма.Объект.КодВидаТранспортногоСредства);
		Если СтруктураКодов = Неопределено Тогда
			Форма.НаименованиеВидаТС = "";
		Иначе
			Форма.НаименованиеВидаТС = СтруктураКодов.Наименование;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокСвернутогоОтображенияПостановкаНаУчетВНО()
	
	ТекстПостановкаНаУчетВНалоговомОргане = НСтр("ru = 'Постановка на учет в налоговом органе';
												|en = 'Registration with a tax authority'");
	
	Если Объект.ПостановкаНаУчетВНалоговомОргане = 
		ПредопределенноеЗначение("Перечисление.ПостановкаНаУчетВНалоговомОргане.ПоМестуНахожденияОрганизации") Тогда
		
		ТекстПостановкаНаУчетВНалоговомОргане = НСтр("ru = 'Постановка на учет в налоговом органе: по месту нахождения организации';
													|en = 'Registration with a tax authority: according to company location'");
		
	ИначеЕсли Объект.ПостановкаНаУчетВНалоговомОргане = 
		ПредопределенноеЗначение("Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане") Тогда
		
		Если ЗначениеЗаполнено(Объект.НалоговыйОрган) Тогда
			КодНО = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.НалоговыйОрган, "Код");
		Иначе
			КодНО = "<...>";
		КонецЕсли;
		
		ШаблонТекста = НСтр("ru = 'Постановка на учет в налоговом органе: ИФНС %1, код по ОКТМО %2';
							|en = 'Registration with a tax authority: IFTS %1, RNCMT code %2'");
		ТекстПостановкаНаУчетВНалоговомОргане = 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонТекста,
				КодНО,
				?(ЗначениеЗаполнено(Объект.КодПоОКТМО), Объект.КодПоОКТМО, "<...>"));
		
	КонецЕсли;
	
	УстановитьЗаголовокГруппы(ЭтотОбъект, "ГруппаПостановкаНаУчетВНалоговомОргане", ТекстПостановкаНаУчетВНалоговомОргане);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокСвернутогоОтображенияНалоговойЛьготы(Форма)
	
	Объект = Форма.Объект;
	
	Если Объект.НалоговаяЛьгота = 
		ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.НеПрименяется") Тогда
		
		ТекстНалоговойЛьготы = НСтр("ru = 'Налоговая льгота: Не применяется';
									|en = 'Tax relief: Not applied'");
		
	ИначеЕсли Объект.НалоговаяЛьгота = 
		ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения") Тогда
		
		ТекстНалоговойЛьготы = НСтр("ru = 'Освобождение от налогообложения';
									|en = 'Taxation exemption '");
		
	ИначеЕсли Объект.НалоговаяЛьгота = 
		ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки") Тогда
		
		ШаблонТекста = НСтр("ru = 'Пониженная налоговая ставка: %1';
							|en = 'Reduced tax rate: %1'");
		ТекстНалоговойЛьготы = 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Объект.ЛьготнаяСтавка);
		
	ИначеЕсли Объект.НалоговаяЛьгота = 
		ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах") Тогда
		
		ШаблонТекста = НСтр("ru = 'Уменьшение суммы налога на: %1 %2';
							|en = 'Tax amount reduction in the amount of: %1 %2'");
		ТекстНалоговойЛьготы = 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Объект.ПроцентУменьшения, "%");
		
	ИначеЕсли Объект.НалоговаяЛьгота = 
		ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму") Тогда
		
		ШаблонТекста = НСтр("ru = 'Уменьшение суммы налога на %1';
							|en = 'Tax amount reduction in the amount of %1'");
		ТекстНалоговойЛьготы = 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, Объект.СуммаУменьшения);
		
	КонецЕсли;
	
	Если Объект.НалоговаяЛьгота <> ПредопределенноеЗначение("Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.НеПрименяется") Тогда
		
		Если Объект.КодНалоговойЛьготы = "30200" Тогда
			
			ШаблонТекста = НСтр("ru = '%1, код льготы %2';
								|en = '%1, benefit code %2 '");
				ТекстНалоговойЛьготы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста,
				ТекстНалоговойЛьготы,
				Объект.КодНалоговойЛьготы);
			
		Иначе
			
			ШаблонТекста = НСтр("ru = '%1, код льготы %2/%3';
								|en = '%1, benefit code %2/%3'");
			ТекстНалоговойЛьготы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста,
				ТекстНалоговойЛьготы,
				?(ЗначениеЗаполнено(Объект.КодНалоговойЛьготы), Объект.КодНалоговойЛьготы, "<...>"),
				?(ЗначениеЗаполнено(Объект.РегиональныйКодЛьготы), Объект.РегиональныйКодЛьготы, "<...>"));
			
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьЗаголовокГруппы(Форма, "ГруппаНалоговаяЛьгота", ТекстНалоговойЛьготы);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокГруппы(Форма, НазваниеГруппы, ЗаголовокТекст)
	
	Форма.Элементы[НазваниеГруппы].ЗаголовокСвернутогоОтображения = ЗаголовокТекст;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьГоловнуюОрганизацию(Форма)
	
	Форма.ГоловнаяОрганизация = ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(Форма.Объект.Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИзСпискаКодВидаТранспортногоСредстваЗавершение(Результат, Элемент) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		Если Результат.Значение <> "ФормаВыбораКодаТС" Тогда
			
			Объект.КодВидаТранспортногоСредства = Результат.Значение;
			Модифицированность = Истина;
			КодВидаТранспортногоСредстваПриИзменении(Элемент);
			
		Иначе
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ТекущийКод", Элемент.ТекстРедактирования);
			ПараметрыФормы.Вставить("Дата", ДатаРегистрации(Объект));
			
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("Элемент", Элемент);
			
			ОповещениеОЗакрытии = Новый ОписаниеОповещения(
				"КодВидаТранспортногоСредстваНачалоВыбораЗавершение",
				ЭтотОбъект,
				ДополнительныеПараметры);
			
			ОткрытьФорму(
				"Документ.РегистрацияТранспортныхСредств.Форма.ФормаВыбораКодаВидаТранспортногоСредства",
				ПараметрыФормы,
				ЭтотОбъект,,,,
				ОповещениеОЗакрытии);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КодВидаТранспортногоСредстваНачалоВыбораЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Элемент = ДополнительныеПараметры.Элемент;
	
	ВыбранноеЗначение = РезультатЗакрытия;
	
	Если ВыбранноеЗначение <> Неопределено Тогда
		
		Модифицированность = Истина;
		
		Объект.КодВидаТранспортногоСредства = ВыбранноеЗначение.КодВидаТС;
		
		Если СписокВыбораКодовТС.НайтиПоЗначению(ВыбранноеЗначение.КодВидаТС) = Неопределено Тогда
			
			СписокВыбораКодовТС.Вставить(0, ВыбранноеЗначение.КодВидаТС, ВыбранноеЗначение.КодВидаТС + " " + ВыбранноеЗначение.Наименование); 
			
			ЗначениеДляОткрытияФормыВыбора = СписокВыбораКодовТС.НайтиПоЗначению("ФормаВыбораКодаТС");
			Если ЗначениеДляОткрытияФормыВыбора <> Неопределено Тогда
				СписокВыбораКодовТС.Удалить(ЗначениеДляОткрытияФормыВыбора);
			КонецЕсли;
			
			МаксимальноеКоличество = 6;
			КоличествоЗначений     = СписокВыбораКодовТС.Количество();
			
			Если КоличествоЗначений > МаксимальноеКоличество Тогда
				Для ОбратныйИндекс = 1 По КоличествоЗначений Цикл
					ЗначениеСписка = СписокВыбораКодовТС[КоличествоЗначений - ОбратныйИндекс];
					Если СписокВыбораКодовТС.Индекс(ЗначениеСписка) > (МаксимальноеКоличество - 1) Тогда
						СписокВыбораКодовТС.Удалить(ЗначениеСписка);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
		КодВидаТранспортногоСредстваПриИзменении(Элемент);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЭкологическийКлассНачалоВыбораЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	КодЭкологическогоКласса = РезультатЗакрытия;
	
	Если КодЭкологическогоКласса <> Неопределено Тогда
		Объект.ЭкологическийКласс = КодЭкологическогоКласса;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СохранитьРеквизиты(Объект, СписокРеквизитов, СтруктураСохраняемыхРеквизитов)
	
	ВременнаяСтруктура = Новый Структура(СписокРеквизитов);
	Для каждого КлючИЗначение Из ВременнаяСтруктура Цикл
		СтруктураСохраняемыхРеквизитов.Вставить(КлючИЗначение.Ключ, Объект[КлючИЗначение.Ключ]);
		Объект[КлючИЗначение.Ключ] = ОбщегоНазначенияБПКлиентСервер.ПустоеЗначениеТипа(ТипЗнч(Объект[КлючИЗначение.Ключ]));
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВосстановитьРеквизиты(Объект, СписокРеквизитов, СтруктураСохраняемыхРеквизитов)
	
	ВременнаяСтруктура = Новый Структура(СписокРеквизитов);
	Для каждого КлючИЗначение Из ВременнаяСтруктура Цикл
		Если СтруктураСохраняемыхРеквизитов.Свойство(КлючИЗначение.Ключ) Тогда
			Объект[КлючИЗначение.Ключ] = СтруктураСохраняемыхРеквизитов[КлючИЗначение.Ключ]
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НалоговыйОрганПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.НалоговыйОрган) Тогда 
		Объект.КодПоОКТМО = Объект.НалоговыйОрган.КодПоОКТМО;
		Объект.КодПоОКАТО = Объект.НалоговыйОрган.КодПоОКАТО;
	КонецЕсли;
	
	УстановитьЗаголовокСвернутогоОтображенияПостановкаНаУчетВНО();
	ЗаполнитьПредставлениеНалоговойСтавки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КодНалоговойЛьготыНачалоВыбораЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Элемент = ДополнительныеПараметры.Элемент;
	
	КодЛьготы = РезультатЗакрытия;
	
	Если КодЛьготы <> Неопределено Тогда
		Элемент.ВыделенныйТекст = КодЛьготы;
		Объект.КодНалоговойЛьготы = КодЛьготы;
	КонецЕсли;
	
	УстановитьЗаголовокСвернутогоОтображенияНалоговойЛьготы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРегиональныйКодЛьготыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ЭтотОбъект[ДополнительныеПараметры.Льгота] = Результат;
		Объект.РегиональныйКодЛьготы = Результат;
		Модифицированность = Истина;
	КонецЕсли;
	
	УстановитьЗаголовокСвернутогоОтображенияНалоговойЛьготы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДатуРегистрацииЗавершение(ВыбраннаяДата, МассивВыделенныхСтрок) Экспорт
	
	Если ВыбраннаяДата <> Неопределено Тогда
		Для Каждого ИдентификаторСтроки Из МассивВыделенныхСтрок Цикл
			СтрокаТаблицы = Объект.ОС.НайтиПоИдентификатору(ИдентификаторСтроки);
			СтрокаТаблицы.ДатаРегистрации = ВыбраннаяДата;
		КонецЦикла;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПредставлениеНалоговойСтавки(Форма)

	Если НЕ Форма.СтавкаОпределяетсяАвтоматически Тогда
		Возврат;
	КонецЕсли;
	
	Форма.НалоговаяСтавкаПредставление = Новый ФорматированнаяСтрока(НСтр("ru = '<не определена>';
																			|en = '<not defined>'"));
	Форма.ПараметрыСтавки = Неопределено;
	
	ЗаполнитьНалоговуюСтавку(Форма);

	Если Форма.ПараметрыСтавки <> Неопределено Тогда

		СтавкаТекст = СтрШаблон(НСтр("ru = '%1 %2';
									|en = '%1 %2'"), Формат(Форма.НалоговаяСтавка, "ЧДЦ=2; ЧН="), Форма.ВалютаРегламентированногоУчетаПредставление);
		ЦветГиперссылки = Форма.ЦветаГиперссылкиСтавкиНалога.Заполнена;
		
		ПредставлениеЕдиницыБазы = Форма.Элементы.ЕдиницаИзмеренияНалоговойБазы.СписокВыбора.НайтиПоЗначению(Форма.Объект.ЕдиницаИзмеренияНалоговойБазы);
		ПредставлениеЕдиницыБазы = ?(ПредставлениеЕдиницыБазы <> Неопределено, ПредставлениеЕдиницыБазы.Представление, "");
		
		СведенияОРегионе = Форма.СписокРегионов.НайтиПоЗначению(Форма.ПараметрыСтавки.ОКТМО);
		РегионПредставление = ?(СведенияОРегионе <> Неопределено, СведенияОРегионе.Представление, НСтр("ru = 'Не удалось определить регион';
																										|en = 'Cannot determine region'"));
		
		Если Форма.ПараметрыСтавки.НалоговаяСтавкаЗависитОтГодаВыпускаТС Тогда
			
			СтавкаПодсказка = НСтр("ru = 'Ставка определяется по данным классификатора ""Ставки транспортного налога"".
	                                   |Используются следующие параметры: 
	                                   |- Регион по коду ОКТМО: %1
	                                   |- Категория по коду вида ТС: %2
	                                   |- %3: %4 %5
	                                   |- Количество лет с года выпуска: %6';
	                                   |en = 'The rate is determined according to the ""Vehicle tax rates"" classifier data.
	                                   |The following parameters are in use: 
	                                   |- Region as per municipal territory classifier code: %1
	                                   |- Category as per vehicle type code: %2
	                                   |- %3: %4 %5
	                                   |- Years after production year: %6'");
			
			СтавкаПодсказка = СтрШаблон(СтавкаПодсказка,
				РегионПредставление,
				Строка(Форма.ПараметрыСтавки.НаименованиеОбъектаНалогообложения),
				ПредставлениеНалоговойБазы(Форма.Объект.ЕдиницаИзмеренияНалоговойБазы),
				Формат(Форма.Объект.НалоговаяБаза, "ЧДЦ=2"),
				ПредставлениеЕдиницыБазы,
				Форма.ПараметрыСтавки.КоличествоЛетСГодаВыпускаТС);
				
		Иначе
			
			СтавкаПодсказка = НСтр("ru = 'Ставка определяется по данным классификатора ""Ставки транспортного налога"".
	                                   |Используются следующие параметры: 
	                                   |- Регион по коду ОКТМО: %1
	                                   |- Категория по коду вида ТС: %2
	                                   |- %3: %4 %5';
	                                   |en = 'The rate is determined according to the ""Vehicle tax rates"" classifier data.
	                                   |The following parameters are in use: 
	                                   |- Region as per municipal territory classifier code: %1
	                                   |- Category as per vehicle type code: %2
	                                   |- %3: %4 %5'");
			
			СтавкаПодсказка = СтрШаблон(СтавкаПодсказка,
				РегионПредставление,
				Строка(Форма.ПараметрыСтавки.НаименованиеОбъектаНалогообложения),
				ПредставлениеНалоговойБазы(Форма.Объект.ЕдиницаИзмеренияНалоговойБазы),
				Формат(Форма.Объект.НалоговаяБаза, "ЧДЦ=2"),
				ПредставлениеЕдиницыБазы);
				
		КонецЕсли;
		
	Иначе
		
		СтавкаТекст = НСтр("ru = '<не определена>';
							|en = '<not defined>'");
		ЦветГиперссылки = Форма.ЦветаГиперссылкиСтавкиНалога.НеЗаполнена;
		
		СтавкаПодсказка = НСтр("ru = 'Не удалось определить ставку.
                                |Ставка определяется по данным классификатора ""Ставки транспортного налога"".
                                |Возможно, не указаны параметры: Код по ОКТМО, Код вида ТС, %1,
                                |дата выпуска (если ставка зависит от года выпуска) или нет данных в классификаторе.';
                                |en = 'Cannot determine rate.
                                |The rate is determined according to the ""Vehicle tax rates"" classifier data.
                                |The following parameters may be blank: Municipal territory classifier code, Vehicle type code, %1,
                                |production date (if the rate depends on the production year) or classifier contains no data.'");
		СтавкаПодсказка = СтрШаблон(СтавкаПодсказка, ПредставлениеНалоговойБазы(Форма.Объект.ЕдиницаИзмеренияНалоговойБазы));
		
	КонецЕсли;
	
	Форма.НалоговаяСтавкаПредставление = Новый ФорматированнаяСтрока(СтавкаТекст,, ЦветГиперссылки,, "#ОткрытьСписок");
	Форма.Элементы.НалоговаяСтавкаПредставление.РасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(СтавкаПодсказка);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьНалоговуюСтавку(Форма, Ошибки = Неопределено)
	
	Объект = Форма.Объект;
	
	Если Ошибки = Неопределено Тогда
		Ошибки = Новый Массив;
	КонецЕсли;
	
	Отказ = Ложь;

	Если НЕ ЗначениеЗаполнено(Объект.Дата) Тогда
		Отказ = Истина;
		
		Ошибка = Новый Структура("ТекстСообщения, Поле",
			ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'Дата';
																		|en = 'Date'")), "Объект.Период");
		Ошибки.Добавить(Ошибка);
	КонецЕсли;

	Если Объект.ПостановкаНаУчетВНалоговомОргане = ПредопределенноеЗначение("Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане") Тогда

		ОКТМОДляЗаполнения = Объект.КодПоОКТМО;

		Если НЕ ЗначениеЗаполнено(Объект.КодПоОКТМО) Тогда
			Отказ = Истина;
			
			Ошибка = Новый Структура("ТекстСообщения, Поле",
				ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'ОКТМО';
																			|en = 'RNCMT'")), "Объект.КодПоОКТМО");
			Ошибки.Добавить(Ошибка);
		КонецЕсли;

	Иначе

		ОКТМОДляЗаполнения = Форма.ОКТМО;

		Если НЕ ЗначениеЗаполнено(Форма.ОКТМО) Тогда
			Отказ = Истина;
			
			Ошибка = Новый Структура("ТекстСообщения, Поле",
				ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'ОКТМО';
																			|en = 'RNCMT'")), "ОКТМОСсылка");
			Ошибки.Добавить(Ошибка);
		КонецЕсли;

	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Объект.КодВидаТранспортногоСредства) Тогда
		Отказ = Истина;
		
		Ошибка = Новый Структура("ТекстСообщения, Поле",
			ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, НСтр("ru = 'Код вида ТС';
																		|en = 'Vehicle kind code'")), "Объект.КодВидаТранспортногоСредства");
		Ошибки.Добавить(Ошибка);
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Объект.НалоговаяБаза) Тогда
		Отказ = Истина;
		
		Ошибка = Новый Структура("ТекстСообщения, Поле",
			ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(,, Форма.Элементы.НалоговаяБаза.Заголовок), "Объект.НалоговаяБаза");
		Ошибки.Добавить(Ошибка);
	КонецЕсли;

	Если Отказ Тогда
		Возврат;
	КонецЕсли;
		
	ДанныеДляЗаполнения = Новый Структура("
		|Период,
		|ОсновноеСредство,
		|Организация,
		|ОКТМО,
		|КодВидаТранспортногоСредства,
		|НалоговаяБаза");
		
	ЗаполнитьЗначенияСвойств(ДанныеДляЗаполнения, Объект);
	ДанныеДляЗаполнения.Период = Объект.Дата;
	ДанныеДляЗаполнения.ОКТМО = ОКТМОДляЗаполнения;
	
	Если Объект.ОС.Количество() <> 0 Тогда
		ДанныеДляЗаполнения.ОсновноеСредство = Объект.ОС[0].ОсновноеСредство;
	КонецЕсли; 
	
	ЗаполнитьСтавкуНаСервере(
		ДанныеДляЗаполнения,
		Форма.НалоговаяСтавка,
		Форма.ПараметрыСтавки,
		Ошибки);
		
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьСтавкуНаСервере(ДанныеДляЗаполнения, НалоговаяСтавка, ПараметрыСтавки, Ошибки)
	
	// Для кодов автономных округов в составе края (области), являющихся субъектами РФ,
	// значимыми для идентификации субъекта являются 1-й, 2-й и 3-й знаки.
	КодПоОКТМО = Лев(ДанныеДляЗаполнения.ОКТМО, 3);

	// Для остальных субъектов РФ значимые для идентификации знаки 1-й и 2-й.
	Если НЕ (КодПоОКТМО = "118" ИЛИ КодПоОКТМО = "718" ИЛИ КодПоОКТМО = "719") Тогда
		КодПоОКТМО = Лев(КодПоОКТМО, 2);
	КонецЕсли;

	Макет = Документы.РегистрацияТранспортныхСредств.ПолучитьМакет("КодыВидовИКатегорииТС");
	КодыВидовИКатегорииТС = ОбщегоНазначения.ПрочитатьXMLВТаблицу(Макет.ПолучитьТекст()).Данные;
	
	НайденнаяСтрока = КодыВидовИКатегорииТС.Найти(ДанныеДляЗаполнения.КодВидаТранспортногоСредства, "КодВида");
	Если НайденнаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КатегорияТС = НайденнаяСтрока.Категория;
	Год = Год(ДанныеДляЗаполнения.Период);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СтавкиТранспортногоНалогаСрезПоследних.Период КАК Период,
	|	СтавкиТранспортногоНалогаСрезПоследних.ОКТМО КАК ОКТМО,
	|	СтавкиТранспортногоНалогаСрезПоследних.НаименованиеОбъектаНалогообложения КАК НаименованиеОбъектаНалогообложения,
	|	СтавкиТранспортногоНалогаСрезПоследних.МинимальноеЗначениеМощности КАК МинимальноеЗначениеМощности,
	|	СтавкиТранспортногоНалогаСрезПоследних.МаксимальноеЗначениеМощности КАК МаксимальноеЗначениеМощности,
	|	СтавкиТранспортногоНалогаСрезПоследних.МинимальноеКоличествоЛетСГодаВыпускаТС КАК МинимальноеКоличествоЛетСГодаВыпускаТС,
	|	СтавкиТранспортногоНалогаСрезПоследних.МаксимальноеКоличествоЛетСГодаВыпускаТС КАК МаксимальноеКоличествоЛетСГодаВыпускаТС,
	|	СтавкиТранспортногоНалогаСрезПоследних.НалоговаяСтавка КАК НалоговаяСтавка
	|ПОМЕСТИТЬ ВТВсеПодходящиеЗаписи
	|ИЗ
	|	РегистрСведений.СтавкиТранспортногоНалога.СрезПоследних(
	|			&Период,
	|			ОКТМО = &ОКТМО
	|				И НаименованиеОбъектаНалогообложения = &НаименованиеОбъектаНалогообложения
	|				И МинимальноеЗначениеМощности <= &НалоговаяБаза
	|				И (МаксимальноеЗначениеМощности = 0
	|					ИЛИ &НалоговаяБаза <= МаксимальноеЗначениеМощности)) КАК СтавкиТранспортногоНалогаСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТВсеПодходящиеЗаписи.Период) КАК Период
	|ПОМЕСТИТЬ ВТПериодПоследнейЗаписи
	|ИЗ
	|	ВТВсеПодходящиеЗаписи КАК ВТВсеПодходящиеЗаписи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВсеПодходящиеЗаписи.Период КАК Период,
	|	ВТВсеПодходящиеЗаписи.ОКТМО КАК ОКТМО,
	|	ВТВсеПодходящиеЗаписи.НаименованиеОбъектаНалогообложения КАК НаименованиеОбъектаНалогообложения,
	|	ВТВсеПодходящиеЗаписи.МинимальноеЗначениеМощности КАК МинимальноеЗначениеМощности,
	|	ВТВсеПодходящиеЗаписи.МаксимальноеЗначениеМощности КАК МаксимальноеЗначениеМощности,
	|	ВТВсеПодходящиеЗаписи.МинимальноеКоличествоЛетСГодаВыпускаТС КАК МинимальноеКоличествоЛетСГодаВыпускаТС,
	|	ВТВсеПодходящиеЗаписи.МаксимальноеКоличествоЛетСГодаВыпускаТС КАК МаксимальноеКоличествоЛетСГодаВыпускаТС,
	|	ВЫБОР
	|		КОГДА ВТВсеПодходящиеЗаписи.МинимальноеКоличествоЛетСГодаВыпускаТС > 0
	|				ИЛИ ВТВсеПодходящиеЗаписи.МаксимальноеКоличествоЛетСГодаВыпускаТС > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НалоговаяСтавкаЗависитОтГодаВыпускаТС,
	|	ВТВсеПодходящиеЗаписи.НалоговаяСтавка КАК НалоговаяСтавка
	|ИЗ
	|	ВТВсеПодходящиеЗаписи КАК ВТВсеПодходящиеЗаписи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПериодПоследнейЗаписи КАК ВТПериодПоследнейЗаписи
	|		ПО ВТВсеПодходящиеЗаписи.Период = ВТПериодПоследнейЗаписи.Период";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Период", ?(Год > 2014, ДанныеДляЗаполнения.Период, Дата('20150101')));
	Запрос.УстановитьПараметр("ОКТМО", ?(СтрДлина(КодПоОКТМО) = 3, КодПоОКТМО + "00000", КодПоОКТМО + "000000"));
	Запрос.УстановитьПараметр("НаименованиеОбъектаНалогообложения", Перечисления.КатегорииТранспортныхСредств[КатегорияТС]);
	Запрос.УстановитьПараметр("НалоговаяБаза", ДанныеДляЗаполнения.НалоговаяБаза);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		
		Ошибка = Новый Структура("ТекстСообщения, Поле",
			НСтр("ru = 'Не найдена подходящая ставка. Проверьте ставки транспортного налога';
				|en = 'No suitable rate found. Check vehicle tax rates'"),
			"НалоговаяСтавкаСсылка");
		
		Ошибки.Добавить(Ошибка);
		
		НалоговаяСтавка = 0;
		
	Иначе	
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НалоговаяСтавкаЗависитОтГодаВыпускаТС = Выборка.НалоговаяСтавкаЗависитОтГодаВыпускаТС;
			
			Если НалоговаяСтавкаЗависитОтГодаВыпускаТС
				И ЗначениеЗаполнено(ДанныеДляЗаполнения.ОсновноеСредство) Тогда
				
				ДатаВыпуска = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеДляЗаполнения.ОсновноеСредство, "ДатаВыпуска");
					
				Если ЗначениеЗаполнено(ДатаВыпуска) Тогда
						
					КоличествоЛетСГодаВыпускаТС = Год - Год(ДатаВыпуска);
					Если Выборка.МинимальноеКоличествоЛетСГодаВыпускаТС <= КоличествоЛетСГодаВыпускаТС
						И (НЕ ЗначениеЗаполнено(Выборка.МаксимальноеКоличествоЛетСГодаВыпускаТС)
						   ИЛИ КоличествоЛетСГодаВыпускаТС <= Выборка.МаксимальноеКоличествоЛетСГодаВыпускаТС) Тогда
						   
						ПараметрыСтавки = Новый Структура(
							"Период,
							|ОКТМО,
							|НаименованиеОбъектаНалогообложения,
							|МинимальноеЗначениеМощности,
							|МаксимальноеЗначениеМощности,
							|МинимальноеКоличествоЛетСГодаВыпускаТС,
							|МаксимальноеКоличествоЛетСГодаВыпускаТС,
							|НалоговаяСтавкаЗависитОтГодаВыпускаТС,
							|КоличествоЛетСГодаВыпускаТС");
						   
						ЗаполнитьЗначенияСвойств(ПараметрыСтавки, Выборка);
						ПараметрыСтавки.КоличествоЛетСГодаВыпускаТС = КоличествоЛетСГодаВыпускаТС;
						НалоговаяСтавка = Выборка.НалоговаяСтавка;
						Возврат;
						
					КонецЕсли;
						
				Иначе
					
					Ошибка = Новый Структура("ТекстСообщения, Поле",
						НСтр("ru = 'Ставка налога установлена с учетом количества лет, прошедших с года выпуска транспортного средства.
						|Укажите дату выпуска основного средства.';
						|en = 'Tax rate is set considering the number of years passed since the vehicle manufacture year. 
						|Specify the issue date of the fixed asset.'"),
						"Запись.ОсновноеСредство");
					
					Ошибки.Добавить(Ошибка);
					
					НалоговаяСтавка = 0;
					Возврат;
					
				КонецЕсли;
			Иначе
				
				ПараметрыСтавки = Новый Структура(
					"Период,
					|ОКТМО,
					|НаименованиеОбъектаНалогообложения,
					|МинимальноеЗначениеМощности,
					|МаксимальноеЗначениеМощности,
					|МинимальноеКоличествоЛетСГодаВыпускаТС,
					|МаксимальноеКоличествоЛетСГодаВыпускаТС,
					|НалоговаяСтавкаЗависитОтГодаВыпускаТС,
					|КоличествоЛетСГодаВыпускаТС");
				
				ЗаполнитьЗначенияСвойств(ПараметрыСтавки, Выборка);
				НалоговаяСтавка = Выборка.НалоговаяСтавка;
				Возврат;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()

	ЗаполнитьДанныеОНалоговомОргане();
	УстановитьГоловнуюОрганизацию(ЭтотОбъект);
	ЗаполнитьПредставлениеНалоговойСтавки(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеОНалоговомОргане()
	
	ОКТМО = "";
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
	
		НалоговыйОрган = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Организация, "РегистрацияВНалоговомОргане");
		
		Если ЗначениеЗаполнено(НалоговыйОрган) Тогда
			ОКТМО = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НалоговыйОрган, "КодПоОКТМО");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииНаСервере()

	ЗаполнитьПредставлениеНалоговойСтавки(ЭтотОбъект);
	ЗаполнитьКодыВидовТС();
	УстановитьВидимость(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ДатаРегистрации(Объект)
	
	ДатаРегистрации = Объект.Дата;
	
	Для каждого ДанныеСтроки Из Объект.ОС Цикл
		Если ЗначениеЗаполнено(ДанныеСтроки.ДатаРегистрации) Тогда
			ДатаРегистрации = ДанныеСтроки.ДатаРегистрации;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДатаРегистрации;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокРегионов()
	
	Макет = РегистрыСведений.СтавкиТранспортногоНалога.ПолучитьМакет("КодыОКТМОСубъектовРФ");
	КодыОКТМОСубъектовРФ = ОбщегоНазначения.ПрочитатьXMLВТаблицу(Макет.ПолучитьТекст()).Данные;
	
	ТаблицаРегионов = АдресныйКлассификатор.СубъектыРФ();
	
	ШаблонПредставления = НСтр("ru = '%1 %2';
								|en = '%1 %2'"); //например: "Москва г"
	Для Каждого Регион Из ТаблицаРегионов Цикл
		
		СтрокаСоответствия = КодыОКТМОСубъектовРФ.Найти(Строка(Регион.КодСубъектаРФ), "КодСубъектаРФ");
		
		Если СтрокаСоответствия <> Неопределено Тогда
		
			Представление = СтрШаблон(ШаблонПредставления, Регион.Наименование, Регион.Сокращение);
			СписокРегионов.Добавить(СтрокаСоответствия.КодОКТМО, Представление);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
