#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РегистрацияОстатковДЗООстаткиНоменклатуры.Номенклатура КАК Номенклатура,
		|	РегистрацияОстатковДЗООстаткиНоменклатуры.Характеристика КАК Характеристика,
		|	МИНИМУМ(РегистрацияОстатковДЗООстаткиНоменклатуры.Коэффициент) КАК Коэффициент,
		|	РегистрацияОстатковДЗООстаткиНоменклатуры.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	СУММА(РегистрацияОстатковДЗООстаткиНоменклатуры.Количество) КАК Количество,
		|	МАКСИМУМ(РегистрацияОстатковДЗООстаткиНоменклатуры.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияБазовая,
		|	РегистрацияОстатковДЗООстаткиНоменклатуры.Ссылка.Организация КАК Организация,
		|	РегистрацияОстатковДЗООстаткиНоменклатуры.Ссылка.Склад КАК Склад,
		|	РегистрацияОстатковДЗООстаткиНоменклатуры.Сумма КАК Сумма
		|ПОМЕСТИТЬ НоменклатураДокумента
		|ИЗ
		|	Документ.УдалитьРегистрацияОстатковДЗО.ОстаткиНоменклатуры КАК РегистрацияОстатковДЗООстаткиНоменклатуры
		|ГДЕ
		|	РегистрацияОстатковДЗООстаткиНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РегистрацияОстатковДЗООстаткиНоменклатуры.Номенклатура,
		|	РегистрацияОстатковДЗООстаткиНоменклатуры.ЕдиницаИзмерения,
		|	РегистрацияОстатковДЗООстаткиНоменклатуры.Ссылка.Склад,
		|	РегистрацияОстатковДЗООстаткиНоменклатуры.Ссылка.Организация,
		|	РегистрацияОстатковДЗООстаткиНоменклатуры.Сумма,
		|	РегистрацияОстатковДЗООстаткиНоменклатуры.Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НоменклатураДокумента.Номенклатура КАК Номенклатура,
		|	НоменклатураДокумента.Количество * ВЫБОР
		|		КОГДА НастройкаЗаменыНоменклатуры.Коэффициент ЕСТЬ NULL
		|			ТОГДА НоменклатураДокумента.Коэффициент
		|		ИНАЧЕ НастройкаЗаменыНоменклатуры.Коэффициент
		|	КОНЕЦ КАК Количество,
		|	НоменклатураДокумента.Склад КАК Склад,
		|	НоменклатураДокумента.Организация КАК Организация,
		|	НоменклатураДокумента.Сумма * ВЫБОР
		|		КОГДА НастройкаЗаменыНоменклатуры.Коэффициент ЕСТЬ NULL
		|			ТОГДА НоменклатураДокумента.Коэффициент
		|		ИНАЧЕ НастройкаЗаменыНоменклатуры.Коэффициент
		|	КОНЕЦ КАК Сумма
		|ИЗ
		|	НоменклатураДокумента КАК НоменклатураДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаЗаменыНоменклатуры КАК НастройкаЗаменыНоменклатуры
		|		ПО (НастройкаЗаменыНоменклатуры.СпособЗамены = &СпособЗамены)
		|			И НоменклатураДокумента.Номенклатура = НастройкаЗаменыНоменклатуры.НоменклатураИсточник
		|			И НоменклатураДокумента.Номенклатура = НастройкаЗаменыНоменклатуры.НоменклатураПриемник
		|			И НоменклатураДокумента.ЕдиницаИзмеренияБазовая = НастройкаЗаменыНоменклатуры.ЕдиницаИзмеренияИсточник
		|			И НоменклатураДокумента.ЕдиницаИзмерения = НастройкаЗаменыНоменклатуры.НоменклатураПриемник
		|			И НоменклатураДокумента.Характеристика = НастройкаЗаменыНоменклатуры.ХарактеристикаИсточник
		|			И НоменклатураДокумента.Характеристика = НастройкаЗаменыНоменклатуры.ХарактеристикаПриемник";
	
	Запрос.УстановитьПараметр("СпособЗамены", Перечисления.СпособыЗаменыНоменклатуры.ПересчетЕдиницыИзмерения);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	// регистр ТоварыНаСкладахДЗО
	Движения.НоменклатураНаСкладахДЗО.Записывать = Истина;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = Движения.НоменклатураНаСкладахДЗО.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
		Движение.Период = Дата;
	КонецЦикла;

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если НЕ ЗначениеЗаполнено(ТипЦен) Тогда
		ТипЦен = Константы.ТипЦенДляРасценкиЗаявокНаПотребность.Получить();
	КонецЕсли;
КонецПроцедуры

#КонецЕсли
