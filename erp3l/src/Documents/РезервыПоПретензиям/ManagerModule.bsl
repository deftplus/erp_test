
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПодготовкаПараметровПроведенияДокумента

Функция ПодготовитьПараметрыПроведения(ДополнительныеСвойства, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДополнительныеСвойства.ДляПроведения.Ссылка);
	
	Реквизиты = МСФОУХ.РеквизитыДокумента(Запрос, "ПериодОтчета, ДатаНачала, ВидОтчета", "ПериодОтчета", Отказ);
	Реквизиты.Вставить("ГраницыПериода", 		МСФОВНАВызовСервераУХ.ПолучитьГраницыПериодаДокумента(Реквизиты,,Истина));
	Реквизиты.Вставить("Период", 				КонецДня(Реквизиты.ГраницыПериода.ДатаОкончания));
	Реквизиты.Вставить("ИсточникКорректировки",	"РасчетныеДанныеЗапасы");
			
	ДополнительныеСвойства.ДляПроведения.Вставить("Реквизиты", Реквизиты);
	
	Если Отказ Тогда
		Возврат ДополнительныеСвойства;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация",			Реквизиты.Организация);
	Запрос.УстановитьПараметр("Сценарий",				Реквизиты.Сценарий);
	Запрос.УстановитьПараметр("ФункциональнаяВалюта",	Реквизиты.ФункциональнаяВалюта);	
	Запрос.УстановитьПараметр("Период",					КонецДня(Реквизиты.ГраницыПериода.ДатаОкончания));
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаТаблицаДляПроводок(НомераТаблиц);
	
	ПроведениеСерверУХ.ДополнитьТаблицамиИзПакетаЗапросов(ДополнительныеСвойства.ТаблицыДляДвижений, Запрос, НомераТаблиц);
	
КонецФункции

Функция ТекстЗапросаТаблицаДляПроводок(НомераТаблиц)

	НомераТаблиц.Вставить("ПроводкиРезервы",		НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПроводкиСторноРезервов",	НомераТаблиц.Количество());
	НомераТаблиц.Вставить("РезервыПоПретензиям",	НомераТаблиц.Количество());
	
    Возврат	
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	""Резерв по претензиям"" КАК Комментарий,
	|	СчетДт.Счет КАК СчетДт,
	|	СчетДт.Субконто1 КАК СубконтоДт1,
	|	СчетДт.Субконто2 КАК СубконтоДт2,
	|	СчетДт.Субконто3 КАК СубконтоДт3,
	|	СчетКт.Счет КАК СчетКт,
	|	СчетКт.Субконто1 КАК СубконтоКт1,
	|	СчетКт.Субконто2 КАК СубконтоКт2,
	|	СчетКт.Субконто3 КАК СубконтоКт3,
	|	СУММА(Резервы.Сумма) КАК Значение,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций.РезервПоПретензиям) КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА СчетДт.Счет.Валютный
	|				И Резервы.Валюта <> &ФункциональнаяВалюта
	|			ТОГДА Резервы.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаДт
	|ИЗ
	|	Документ.РезервыПоПретензиям.Резервы КАК Резервы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФиксированныеСчетаУчетаБД КАК СчетДт
	|		ПО (СчетДт.Ссылка = ЗНАЧЕНИЕ(Справочник.ФиксированныеСчетаУчетаБД.СчетРасходаПоПретензиям))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФиксированныеСчетаУчетаБД КАК СчетКт
	|		ПО (СчетКт.Ссылка = ЗНАЧЕНИЕ(Справочник.ФиксированныеСчетаУчетаБД.СчетРезерваПоПретензиям))
	|ГДЕ
	|	Резервы.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетДт.Субконто1,
	|	СчетДт.Субконто2,
	|	СчетКт.Счет,
	|	СчетДт.Счет,
	|	СчетДт.Субконто3,
	|	СчетКт.Субконто1,
	|	Резервы.Валюта,
	|	СчетКт.Субконто3,
	|	СчетКт.Субконто2,
	|	ВЫБОР
	|		КОГДА СчетДт.Счет.Валютный
	|			ТОГДА Резервы.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СчетДт.Счет.Валютный
	|				И Резервы.Валюта <> &ФункциональнаяВалюта
	|			ТОГДА Резервы.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	""Сторно резерва по претензиям"" КАК Комментарий,
	|	СчетДт.Счет КАК СчетДт,
	|	СчетДт.Субконто1 КАК СубконтоДт1,
	|	СчетДт.Субконто2 КАК СубконтоДт2,
	|	СчетДт.Субконто3 КАК СубконтоДт3,
	|	СчетКт.Счет КАК СчетКт,
	|	СчетКт.Субконто1 КАК СубконтоКт1,
	|	СчетКт.Субконто2 КАК СубконтоКт2,
	|	СчетКт.Субконто3 КАК СубконтоКт3,
	|	-СУММА(Резервы.Сумма) КАК Значение,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОпераций.СторноРезерваПоПретензиям) КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА СчетДт.Счет.Валютный
	|				И Резервы.Валюта <> &ФункциональнаяВалюта
	|			ТОГДА Резервы.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаДт
	|ИЗ
	|	Документ.РезервыПоПретензиям.СторноРезервов КАК Резервы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФиксированныеСчетаУчетаБД КАК СчетДт
	|		ПО (СчетДт.Ссылка = ЗНАЧЕНИЕ(Справочник.ФиксированныеСчетаУчетаБД.СчетРасходаПоПретензиям))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФиксированныеСчетаУчетаБД КАК СчетКт
	|		ПО (СчетКт.Ссылка = ЗНАЧЕНИЕ(Справочник.ФиксированныеСчетаУчетаБД.СчетРезерваПоПретензиям))
	|ГДЕ
	|	Резервы.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетДт.Счет,
	|	СчетДт.Субконто1,
	|	СчетКт.Счет,
	|	СчетДт.Субконто3,
	|	СчетДт.Субконто2,
	|	СчетКт.Субконто1,
	|	СчетКт.Субконто2,
	|	СчетКт.Субконто3,
	|	Резервы.Валюта,
	|	ВЫБОР
	|		КОГДА СчетДт.Счет.Валютный
	|			ТОГДА Резервы.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СчетДт.Счет.Валютный
	|				И Резервы.Валюта <> &ФункциональнаяВалюта
	|			ТОГДА Резервы.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	&Сценарий КАК Сценарий,
	|	Свод.Контрагент,
	|	Свод.Валюта,
	|	Свод.Претензия,
	|	СУММА(Свод.Сумма) КАК Сумма
	|ИЗ
	|	(ВЫБРАТЬ
	|		Резервы.Контрагент КАК Контрагент,
	|		Резервы.Валюта КАК Валюта,
	|		Резервы.Претензия КАК Претензия,
	|		Резервы.Сумма КАК Сумма
	|	ИЗ
	|		Документ.РезервыПоПретензиям.Резервы КАК Резервы
	|	ГДЕ
	|		Резервы.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Резервы.Контрагент,
	|		Резервы.Валюта,
	|		Резервы.Претензия,
	|		-Резервы.Сумма
	|	ИЗ
	|		Документ.РезервыПоПретензиям.СторноРезервов КАК Резервы
	|	ГДЕ
	|		Резервы.Ссылка = &Ссылка) КАК Свод
	|
	|СГРУППИРОВАТЬ ПО
	|	Свод.Претензия,
	|	Свод.Контрагент,
	|	Свод.Валюта";

КонецФункции
     	
#КонецОбласти

#Область ЗаполнениеДокумента

Функция ПолучитьТекстЗапросаЗаполнение(НомераТаблиц) Экспорт

	НомераТаблиц.Вставить("втПретензииПериода",	НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Резервы",			НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СторноРезервов",		НомераТаблиц.Количество());
		
	Возврат 
	"ВЫБРАТЬ
	|	ХодПретензионноИсковойРаботы.Претензия.Контрагент КАК Контрагент,
	|	ХодПретензионноИсковойРаботы.Претензия КАК Претензия,
	|	ВЫБОР
	|		КОГДА ХодПретензионноИсковойРаботы.Валюта = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|			ТОГДА &ФункциональнаяВалюта
	|		ИНАЧЕ ХодПретензионноИсковойРаботы.Валюта
	|	КОНЕЦ КАК Валюта,
	|	ХодПретензионноИсковойРаботы.Сумма КАК Сумма
	|ПОМЕСТИТЬ втПретензииПериода
	|ИЗ
	|	РегистрСведений.ХодПретензионноИсковойРаботы.СрезПоследних(&КонецПериода, Претензия.Организация = &Организация) КАК ХодПретензионноИсковойРаботы
	|ГДЕ
	|	ХодПретензионноИсковойРаботы.ЭтапРаботы <> ЗНАЧЕНИЕ(Справочник.ЭтапыПретензионноИсковойРаботы.Закрыта)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРезервы.Контрагент КАК Контрагент,
	|	ТаблицаРезервы.Претензия КАК Претензия,
	|	ТаблицаРезервы.Валюта КАК Валюта,
	|	СУММА(ТаблицаРезервы.Сумма) КАК Сумма
	|ИЗ
	|	(ВЫБРАТЬ
	|		втПретензииПериода.Контрагент КАК Контрагент,
	|		втПретензииПериода.Претензия КАК Претензия,
	|		втПретензииПериода.Валюта КАК Валюта,
	|		втПретензииПериода.Сумма КАК Сумма
	|	ИЗ
	|		втПретензииПериода КАК втПретензииПериода
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОстаткиПоПретензиямНаНачалоПериода.Претензия.Контрагент,
	|		ОстаткиПоПретензиямНаНачалоПериода.Претензия,
	|		ОстаткиПоПретензиямНаНачалоПериода.Валюта,
	|		-ОстаткиПоПретензиямНаНачалоПериода.Сумма
	|	ИЗ
	|		РегистрСведений.ХодПретензионноИсковойРаботы.СрезПоследних(
	|				&КонецПредыдущегоПериода,
	|				Претензия В
	|					(ВЫБРАТЬ
	|						т.Претензия
	|					ИЗ
	|						втПретензииПериода КАК т)) КАК ОстаткиПоПретензиямНаНачалоПериода) КАК ТаблицаРезервы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРезервы.Контрагент,
	|	ТаблицаРезервы.Претензия,
	|	ТаблицаРезервы.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСторноРезервы.Контрагент КАК Контрагент,
	|	ТаблицаСторноРезервы.Претензия КАК Претензия,
	|	ТаблицаСторноРезервы.Валюта КАК Валюта,
	|	ТаблицаСторноРезервы.СуммаОстаток КАК Сумма
	|ИЗ
	|	РегистрНакопления.РезервыПоПретензиям.Остатки(
	|			&КонецПериода,
	|			Организация = &Организация
	|				И Сценарий = &Сценарий
	|				И Претензия В
	|					(ВЫБРАТЬ
	|						ЗакрытыеПретензии.Претензия
	|					ИЗ
	|						РегистрСведений.ХодПретензионноИсковойРаботы.СрезПоследних(&КонецПериода, Претензия.Организация = &Организация) КАК ЗакрытыеПретензии
	|					ГДЕ
	|						ЗакрытыеПретензии.ЭтапРаботы = ЗНАЧЕНИЕ(Справочник.ЭтапыПретензионноИсковойРаботы.Закрыта))) КАК ТаблицаСторноРезервы";
	
КонецФункции

#КонецОбласти

#Область Движения

Процедура СформироватьДвижения(Движения, ДополнительныеСвойства, Отказ) Экспорт

	Реквизиты 		= ДополнительныеСвойства.ДляПроведения.Реквизиты;	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	ДобавитьКолонкиПроводок(ТаблицыДляДвижений.ПроводкиРезервы);
	ДобавитьКолонкиПроводок(ТаблицыДляДвижений.ПроводкиСторноРезервов);
		
	Реквизиты.Вставить("ВидОперации", Справочники.ВидыОпераций.РезервПоПретензиям);	
	ТрансформационныеКорректировкиУХ.СформироватьКорректировку(Реквизиты, Отказ, ТаблицыДляДвижений.ПроводкиРезервы);
	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;	
	
	Реквизиты.Вставить("ВидОперации", Справочники.ВидыОпераций.СторноРезерваПоПретензиям);	
	ТрансформационныеКорректировкиУХ.СформироватьКорректировку(Реквизиты, Отказ, ТаблицыДляДвижений.ПроводкиСторноРезервов);
	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Движения.РезервыПоПретензиям.Загрузить(ТаблицыДляДвижений.РезервыПоПретензиям);
	Движения.РезервыПоПретензиям.Записывать = Истина;
	
КонецПроцедуры

Процедура ДобавитьКолонкиПроводок(ТаблицаПроводок)
	
	ТаблицаПроводок.Колонки.Добавить("РесурсРегистра");	
	ТаблицаПроводок.ЗаполнитьЗначения("СуммаВВалютеУчета", "РесурсРегистра");

КонецПроцедуры

#КонецОбласти

#КонецЕсли
