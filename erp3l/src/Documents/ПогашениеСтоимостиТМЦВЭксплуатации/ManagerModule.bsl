#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	МеханизмыДокумента.Добавить("СебестоимостьИПартионныйУчет");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
	
	КонецЕсли;
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// Метод создает документы погашения стоимости ТМЦ в эксплуатации для указанного месяца.
//
// Параметры:
//  Месяц		 - Дата	- Месяц, в котором необходимо создать документы амортизации.
//  Организация	 - СправочникСсылка.Организации	 - Список организаций по которым формируются документы. Если список пустой, то документы формируются по всем организациям.
//  Отказ		 - Булево	- Используется при вызове из формы закрытия месяца. При установке в "Истина" - дальнейшие операции выполняться не будут.
//
Процедура СоздатьДокументы(Месяц, Организация, Отказ = Ложь) Экспорт
	
	ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(НСтр("ru = 'Погашение стоимости ТМЦ в эксплуатации';
															|en = 'Repayment for inventory in operation'"));
	
	ИмяСобытияЖР = 
		НСтр("ru = 'Закрытие месяца';
			|en = 'Month-end closing'", ОбщегоНазначения.КодОсновногоЯзыка())
		+ "."
		+ НСтр("ru = 'Погашение стоимости ТМЦ в эксплуатации';
				|en = 'Repayment for inventory in operation'", ОбщегоНазначения.КодОсновногоЯзыка());
			
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Задания.Организация КАК Организация,
	|	Задания.НомерПакета КАК НомерПакета
	|ПОМЕСТИТЬ ПакетыКРасчету
	|ИЗ
	|	РегистрСведений.ЗаданияКПогашениюСтоимостиТМЦВЭксплуатации КАК Задания
	|ГДЕ
	|	(Задания.Организация В (&СписокОрганизаций)
	|		ИЛИ &ПоВсемОрганизациям)
	|	И Задания.Месяц МЕЖДУ &НачДата И &КонДата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегламентныйДокумент.Ссылка
	|ИЗ
	|	Документ.ПогашениеСтоимостиТМЦВЭксплуатации КАК РегламентныйДокумент
	|ГДЕ
	|	(РегламентныйДокумент.Организация В (&СписокОрганизаций)
	|		ИЛИ &ПоВсемОрганизациям)
	|	И РегламентныйДокумент.Дата МЕЖДУ &НачДата И &КонДата
	|	И РегламентныйДокумент.НомерПакета = 0
	|	И НЕ РегламентныйДокумент.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПакетыКРасчету.Организация,
	|	ПакетыКРасчету.НомерПакета,
	|
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(РегламентныйДокумент.Проведен, ЛОЖЬ) = ИСТИНА
	|				ТОГДА РегламентныйДокумент.Ссылка
	|			ИНАЧЕ ЗНАЧЕНИЕ(Документ.ПогашениеСтоимостиТМЦВЭксплуатации.ПустаяСсылка)
	|		КОНЕЦ) КАК СсылкаПроведен,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(РегламентныйДокумент.Проведен, НЕОПРЕДЕЛЕНО) = ЛОЖЬ
	|				ТОГДА РегламентныйДокумент.Ссылка
	|			ИНАЧЕ ЗНАЧЕНИЕ(Документ.ПогашениеСтоимостиТМЦВЭксплуатации.ПустаяСсылка)
	|		КОНЕЦ) КАК СсылкаНеПроведен,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(РегламентныйДокумент.ПометкаУдаления, ЛОЖЬ) = ИСТИНА
	|				ТОГДА РегламентныйДокумент.Ссылка
	|			ИНАЧЕ ЗНАЧЕНИЕ(Документ.ПогашениеСтоимостиТМЦВЭксплуатации.ПустаяСсылка)
	|		КОНЕЦ) КАК СсылкаУдален
	|ИЗ
	|	ПакетыКРасчету КАК ПакетыКРасчету
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПогашениеСтоимостиТМЦВЭксплуатации КАК РегламентныйДокумент
	|		ПО ПакетыКРасчету.Организация = РегламентныйДокумент.Организация
	|			И ПакетыКРасчету.НомерПакета = РегламентныйДокумент.НомерПакета
	|			И (РегламентныйДокумент.Дата >= &НачДата)
	|			И (РегламентныйДокумент.Дата <= &КонДата)
	|ГДЕ
	|	ПакетыКРасчету.НомерПакета <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ПакетыКРасчету.Организация,
	|	ПакетыКРасчету.НомерПакета";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НачДата", НачалоМесяца(Месяц));
	Запрос.УстановитьПараметр("КонДата", КонецМесяца(Месяц));
	Запрос.УстановитьПараметр("СписокОрганизаций", Организация);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", НЕ ЗначениеЗаполнено(Организация));
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Выборка = РезультатЗапроса[РезультатЗапроса.ВГраница()-1].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ДокументОбъект.ДополнительныеСвойства.Вставить("ОчисткаДляПоследующегоПроведения",Истина);
		
		Попытка
			
			ДокументОбъект.УстановитьПометкуУдаления(Истина);
			
		Исключение
			
			Отказ = Истина;
			
			ЗаписьЖурналаРегистрации(
				ИмяСобытияЖР,
				УровеньЖурналаРегистрации.Ошибка,,,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
		КонецПопытки;
		
	КонецЦикла;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса[РезультатЗапроса.ВГраница()].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.СсылкаПроведен.Пустая()
			И Выборка.СсылкаНеПроведен.Пустая()
			И Выборка.СсылкаУдален.Пустая() Тогда
			
			ДокументОбъект = Документы.ПогашениеСтоимостиТМЦВЭксплуатации.СоздатьДокумент();
			
		ИначеЕсли НЕ Выборка.СсылкаПроведен.Пустая() Тогда
			
			ДокументОбъект = Выборка.СсылкаПроведен.ПолучитьОбъект();
			
		ИначеЕсли НЕ Выборка.СсылкаНеПроведен.Пустая() Тогда
			
			ДокументОбъект = Выборка.СсылкаНеПроведен.ПолучитьОбъект();
			ДокументОбъект.ПометкаУдаления = Ложь;
			
		Иначе
			
			ДокументОбъект = Выборка.СсылкаУдален.ПолучитьОбъект();
			ДокументОбъект.ПометкаУдаления = Ложь;
			
		КонецЕсли; 
		
		ДокументОбъект.Организация = Выборка.Организация;
		ДокументОбъект.НомерПакета = Выборка.НомерПакета;
		ДокументОбъект.Дата = КонецМесяца(Месяц);
		ДокументОбъект.Ответственный = Пользователи.ТекущийПользователь();
		
		Попытка
			
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			
		Исключение
			
			Отказ = Истина;
			
			ЗаписьЖурналаРегистрации(
				ИмяСобытияЖР,
				УровеньЖурналаРегистрации.Ошибка,,,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
		КонецПопытки;
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#Область Команды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
	
КонецПроцедуры

// Добавляет команду создания документа "Погашение стоимости ТМЦ в эксплуатации".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.ПогашениеСтоимостиТМЦВЭксплуатации) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ПогашениеСтоимостиТМЦВЭксплуатации.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ПогашениеСтоимостиТМЦВЭксплуатации);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
	

		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	Возврат ИсточникиДанных;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Организация КАК Организация
	|ИЗ
	|	Документ.ПогашениеСтоимостиТМЦВЭксплуатации КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("ИдентификаторНеиспользуемойФинЗаписи", ПроведениеДокументов.ИдентификаторНеиспользуемойФинЗаписи());
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	Строки.Подразделение КАК Подразделение,
	|	Строки.СтатьяРасходов КАК СтатьяРасходов,
	|	Строки.АналитикаРасходов КАК АналитикаРасходов,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаБезНДСУпр,
	|	Строки.СуммаБУ КАК СуммаСНДСРегл,
	|	Строки.СуммаБУ КАК СуммаБезНДСРегл,
	|	Строки.СуммаПР КАК ПостояннаяРазница,
	|	Строки.СуммаВР КАК ВременнаяРазница,
	|	
	|	Неопределено КАК ХозяйственнаяОперация,
	|	Неопределено КАК АналитикаУчетаНоменклатуры,
	|	
	|	&ИдентификаторНеиспользуемойФинЗаписи КАК ИдентификаторФинЗаписи,
	|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации
	|
	|ПОМЕСТИТЬ ВтИсходныеПрочиеРасходы
	|ИЗ
	|	Документ.ПогашениеСтоимостиТМЦВЭксплуатации.Расходы КАК Строки
	|ГДЕ
	|	Строки.Ссылка = &Ссылка
	|	И НЕ (Строки.СуммаБУ=0 И Строки.СуммаНУ=0 И Строки.СуммаПР=0 И Строки.СуммаВР=0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	Строки.Подразделение КАК Подразделение,
	|	Строки.СтатьяРасходов КАК СтатьяРасходов,
	|	Строки.АналитикаРасходов КАК АналитикаРасходов,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаБезНДСУпр,
	|	Строки.СуммаБУ КАК СуммаСНДСРегл,
	|	Строки.СуммаБУ КАК СуммаБезНДСРегл,
	|	Строки.СуммаПР КАК ПостояннаяРазница,
	|	Строки.СуммаВР КАК ВременнаяРазница,
	|	
	|	Неопределено КАК ХозяйственнаяОперация,
	|	Неопределено КАК АналитикаУчетаНоменклатуры,
	|	
	|	&ИдентификаторНеиспользуемойФинЗаписи КАК ИдентификаторФинЗаписи,
	|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	Документ.ПогашениеСтоимостиТМЦВЭксплуатации.Списания КАК Строки
	|ГДЕ
	|	Строки.Ссылка = &Ссылка
	|	И НЕ (Строки.СуммаБУ=0 И Строки.СуммаНУ=0 И Строки.СуммаПР=0 И Строки.СуммаВР=0)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПартииПрочихРасходов";
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	Таблица.Подразделение КАК Подразделение,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	Таблица.СтатьяРасходов КАК СтатьяРасходов,
	|	Таблица.АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
	|	Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьБезНДС,
	|	0 КАК НДСУпр,
	|	Таблица.СуммаБУ КАК СтоимостьРегл,
	|	0 КАК НДСРегл,
	|	Таблица.СуммаПР КАК ПостояннаяРазница,
	|	Таблица.СуммаВР КАК ВременнаяРазница,
	|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
	|	
	|	&ИдентификаторНеиспользуемойФинЗаписи КАК ИдентификаторФинЗаписи,
	|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации
	|
	|ПОМЕСТИТЬ ВтИсходныеПартииПрочихРасходов
	|ИЗ
	|	Документ.ПогашениеСтоимостиТМЦВЭксплуатации.Расходы КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &Ссылка
	|	И НЕ (Таблица.СуммаБУ=0 И Таблица.СуммаНУ=0 И Таблица.СуммаПР=0 И Таблица.СуммаВР=0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	Таблица.Подразделение КАК Подразделение,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	Таблица.СтатьяРасходов КАК СтатьяРасходов,
	|	Таблица.АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
	|	Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|	
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьБезНДС,
	|	0 КАК НДСУпр,
	|	Таблица.СуммаБУ КАК СтоимостьРегл,
	|	0 КАК НДСРегл,
	|	Таблица.СуммаПР КАК ПостояннаяРазница,
	|	Таблица.СуммаВР КАК ВременнаяРазница,
	|	НЕОПРЕДЕЛЕНО КАК ХозяйственнаяОперация,
	|	
	|	&ИдентификаторНеиспользуемойФинЗаписи КАК ИдентификаторФинЗаписи,
	|	НЕОПРЕДЕЛЕНО КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	Документ.ПогашениеСтоимостиТМЦВЭксплуатации.Списания КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &Ссылка
	|	И НЕ (Таблица.СуммаБУ=0 И Таблица.СуммаНУ=0 И Таблица.СуммаПР=0 И Таблица.СуммаВР=0)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтПартииПрочихРасходов";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаВтПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПартииПрочихРасходов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаПартииПрочихРасходов();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеАктивыПассивы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеАктивыПассивы.ТекстЗапросаТаблицаПрочиеАктивыПассивы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период                      КАК Период,
	|	&Организация                 КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|";
	ТекстЗапроса = ТекстЗапроса + "ОБЪЕДИНИТЬ ВСЕ"
		+ ДоходыИРасходыСервер.ДополнитьТекстЗапросаТаблицаОтражениеДокументовВРеглУчете();
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстОтраженияВРеглУчете() Экспорт
	
	ТекстыОтражения = Новый Массив;
	ТекстыОтражения.Добавить(ТекстРасходыПогашенияСтоимости());
	ТекстыОтражения.Добавить(ТекстСписание());
	ТекстыОтражения.Добавить(ТекстСписаниеЗабаланс());
	ТекстыОтражения.Добавить(ТекстПеремещение());
	ТекстыОтражения.Добавить(ТекстПеремещениеЗабаланс());
	ТекстыОтражения.Добавить(ТекстВозвратИзДоходов());
	ТекстыОтражения.Добавить(ТекстВозвратИзЭксплуатации());
	ТекстыОтражения.Добавить(ТекстСписанияФСБУ5());
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат "";
	
КонецФункции

Функция ТекстРасходыПогашенияСтоимости()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////
	|// Погашение стоимости ТМЦ в эксплуатации (Дт СчетРасходов :: Кт 10.11.Х)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.СтатьяРасходов КАК СубконтоДт1,
	|	Строки.АналитикаРасходов КАК СубконтоДт2,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СуммаНУ КАК СуммаНУДт,
	|	Строки.СуммаПР КАК СуммаПРДт,
	|	Строки.СуммаВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТМЦВЭксплуатации) КАК ВидСчетаКт,
	|	Строки.Партия.КатегорияЭксплуатации КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	
	|	Строки.Номенклатура КАК СубконтоКт1,
	|	Строки.Партия КАК СубконтоКт2,
	|	Строки.ФизическоеЛицо КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	Строки.Количество КАК КоличествоКт,
	|	Строки.СуммаНУ КАК СуммаНУКт,
	|	Строки.СуммаПР КАК СуммаПРКт,
	|	Строки.СуммаВР КАК СуммаВРКт,
	|	""Погашение стоимости ТМЦ в эксплуатации"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПогашениеСтоимостиТМЦВЭксплуатации КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПогашениеСтоимостиТМЦВЭксплуатации.Расходы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|";
	
КонецФункции

Функция ТекстСписание()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////
	|// Списание стоимости ТМЦ из эксплуатации (Дт СчетРасходов :: Кт 10.11.Х)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.СтатьяРасходов КАК СубконтоДт1,
	|	Строки.АналитикаРасходов КАК СубконтоДт2,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СуммаНУ КАК СуммаНУДт,
	|	Строки.СуммаПР КАК СуммаПРДт,
	|	Строки.СуммаВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТМЦВЭксплуатации) КАК ВидСчетаКт,
	|	Строки.Партия.КатегорияЭксплуатации КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	
	|	Строки.Номенклатура КАК СубконтоКт1,
	|	Строки.Партия КАК СубконтоКт2,
	|	Строки.ФизическоеЛицо КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	Строки.Количество КАК КоличествоКт,
	|	Строки.СуммаНУ КАК СуммаНУКт,
	|	Строки.СуммаПР КАК СуммаПРКт,
	|	Строки.СуммаВР КАК СуммаВРКт,
	|	""Списание стоимости ТМЦ из эксплуатации"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПогашениеСтоимостиТМЦВЭксплуатации КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПогашениеСтоимостиТМЦВЭксплуатации.Списания КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|";
	
КонецФункции

Функция ТекстСписаниеЗабаланс()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Списание из эксплуатации, забалансовый учет (Дт --- :: Кт МЦ.0Х)
	|ВЫБРАТЬ //
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.Сумма КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТМЦВЭксплуатацииЗаБалансом) КАК ВидСчетаКт,
	|	Строки.Партия.КатегорияЭксплуатации КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Строки.Номенклатура КАК СубконтоКт1,
	|	Строки.Партия КАК СубконтоКт2,
	|	Строки.ФизическоеЛицо КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	Строки.Количество КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Списание из эксплуатации, забалансовый учет"" КАК Содержание
	|	
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПогашениеСтоимостиТМЦВЭксплуатации КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПогашениеСтоимостиТМЦВЭксплуатации.СписанияЗабалансовыйУчет КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|";
	
КонецФункции

Функция ТекстПеремещение()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////
	|// Перемещение ТМЦ эксплуатации (Дт 10.11.Х :: Кт 10.11.Х)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТМЦВЭксплуатации) КАК ВидСчетаДт,
	|	Строки.Партия.КатегорияЭксплуатации КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.ПодразделениеПолучатель КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельностиПолучатель КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.Номенклатура КАК СубконтоДт1,
	|	Строки.Партия КАК СубконтоДт2,
	|	Строки.ФизическоеЛицоПолучатель КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	Строки.Количество КАК КоличествоДт,
	|	Строки.СуммаНУ КАК СуммаНУДт,
	|	Строки.СуммаПР КАК СуммаПРДт,
	|	Строки.СуммаВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТМЦВЭксплуатации) КАК ВидСчетаКт,
	|	Строки.Партия.КатегорияЭксплуатации КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельностиОтправитель КАК НаправлениеДеятельностиКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	Строки.Номенклатура КАК СубконтоКт1,
	|	Строки.Партия КАК СубконтоКт2,
	|	Строки.ФизическоеЛицо КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	Строки.Количество КАК КоличествоКт,
	|	Строки.СуммаНУ КАК СуммаНУКт,
	|	Строки.СуммаПР КАК СуммаПРКт,
	|	Строки.СуммаВР КАК СуммаВРКт,
	|	""Перемещение ТМЦ эксплуатации"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПогашениеСтоимостиТМЦВЭксплуатации КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПогашениеСтоимостиТМЦВЭксплуатации.Перемещения КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|";
	
КонецФункции

Функция ТекстПеремещениеЗабаланс()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Перемещение в эксплуатации, забалансовый учет (Дт МЦ.0Х :: Кт МЦ.0Х)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.Сумма КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТМЦВЭксплуатацииЗаБалансом) КАК ВидСчетаДт,
	|	Строки.Партия.КатегорияЭксплуатации КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.ПодразделениеПолучатель КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.Номенклатура КАК СубконтоДт1,
	|	Строки.Партия КАК СубконтоДт2,
	|	Строки.ФизическоеЛицоПолучатель КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	Строки.Количество КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТМЦВЭксплуатацииЗаБалансом) КАК ВидСчетаКт,
	|	Строки.Партия.КатегорияЭксплуатации КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Строки.Номенклатура КАК СубконтоКт1,
	|	Строки.Партия КАК СубконтоКт2,
	|	Строки.ФизическоеЛицо КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	Строки.Количество КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Перемещение в эксплуатации, забалансовый учет"" КАК Содержание
	|	
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПогашениеСтоимостиТМЦВЭксплуатации КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПогашениеСтоимостиТМЦВЭксплуатации.ПеремещенияЗабалансовыйУчет КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|";
	
КонецФункции

Функция ТекстВозвратИзДоходов()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Возврат из доходов (Дт 10.10 :: Кт 91.01)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаБУ КАК Сумма,
	|	Строки.СуммаУУ КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе) КАК ВидСчетаДт,
	|	ВЫБОР
	|		КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|				И Строки.ВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
	|			ТОГДА Строки.ВидЗапасов.ГруппаФинансовогоУчета
	|		ИНАЧЕ Строки.Номенклатура.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК АналитикаУчетаДт,
	|	Строки.Склад КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Склад.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.Номенклатура КАК СубконтоДт1,
	|	Строки.Склад КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	Строки.Количество КАК КоличествоДт,
	|	Строки.СуммаНУ КАК СуммаНУДт,
	|	Строки.СуммаПР КАК СуммаПРДт,
	|	Строки.СуммаВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	Строки.СтатьяДоходов КАК АналитикаУчетаКт,
	|	Строки.Подразделение КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Строки.СтатьяДоходов КАК СубконтоКт1,
	|	Строки.Номенклатура КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	Строки.Количество КАК КоличествоКт,
	|	Строки.СуммаНУ КАК СуммаНУКт,
	|	Строки.СуммаПР КАК СуммаПРКт,
	|	Строки.СуммаВР КАК СуммаВРКт,
	|	""Возврат из доходов"" КАК Содержание
	|	
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПогашениеСтоимостиТМЦВЭксплуатации КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПогашениеСтоимостиТМЦВЭксплуатации.Доходы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|";
	
КонецФункции

Функция ТекстВозвратИзЭксплуатации()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Возврат из эксплуатации (Дт 10.10 :: Кт 10.11)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе) КАК ВидСчетаДт,
	|	ВЫБОР
	|		КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|				И Строки.ВидЗапасов <> ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
	|			ТОГДА Строки.ВидЗапасов.ГруппаФинансовогоУчета
	|		ИНАЧЕ Строки.Номенклатура.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК АналитикаУчетаДт,
	|	Строки.Склад КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Склад.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.Номенклатура КАК СубконтоДт1,
	|	Строки.Склад КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	Строки.Количество КАК КоличествоДт,
	|	Строки.СуммаНУ КАК СуммаНУДт,
	|	Строки.СуммаПР КАК СуммаПРДт,
	|	Строки.СуммаВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТМЦВЭксплуатации) КАК ВидСчетаКт,
	|	Строки.Партия.КатегорияЭксплуатации КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	Строки.Номенклатура КАК СубконтоКт1,
	|	Строки.Партия КАК СубконтоКт2,
	|	Строки.ФизическоеЛицо КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	Строки.Количество КАК КоличествоКт,
	|	Строки.СуммаНУ КАК СуммаНУКт,
	|	Строки.СуммаПР КАК СуммаПРКт,
	|	Строки.СуммаВР КАК СуммаВРКт,
	|	""Возврат из эксплуатации"" КАК Содержание
	|	
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПогашениеСтоимостиТМЦВЭксплуатации КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПогашениеСтоимостиТМЦВЭксплуатации.Возвраты КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|";
	
КонецФункции

Функция ТекстСписанияФСБУ5()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////
	|// Списание стоимости ТМЦ из эксплуатации (Дт 84.02 :: Кт 10.11.Х)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаБУ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.УбытокПодлежащийПокрытию) КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СуммаНУ КАК СуммаНУДт,
	|	Строки.СуммаПР КАК СуммаПРДт,
	|	Строки.СуммаВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ТМЦВЭксплуатации) КАК ВидСчетаКт,
	|	Строки.Партия.КатегорияЭксплуатации КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	
	|	Строки.Номенклатура КАК СубконтоКт1,
	|	Строки.Партия КАК СубконтоКт2,
	|	Строки.ФизическоеЛицо КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	Строки.Количество КАК КоличествоКт,
	|	Строки.СуммаНУ КАК СуммаНУКт,
	|	Строки.СуммаПР КАК СуммаПРКт,
	|	Строки.СуммаВР КАК СуммаВРКт,
	|	""Списание ТМЦ из эксплуатации"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПогашениеСтоимостиТМЦВЭксплуатации КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПогашениеСтоимостиТМЦВЭксплуатации.СписанияФСБУ5 КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|";
	
КонецФункции

#КонецОбласти

#Область БлокировкаПриОбновленииИБ

Процедура ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ПредставлениеОперации)
	
	ВходящиеДанные = Новый Соответствие;
	
	ВходящиеДанные.Вставить(Метаданные.Документы.ПеремещениеВЭксплуатации);
	ВходящиеДанные.Вставить(Метаданные.Документы.ПогашениеСтоимостиТМЦВЭксплуатации);
	ВходящиеДанные.Вставить(Метаданные.Документы.ПрочееОприходованиеТоваров);
	ВходящиеДанные.Вставить(Метаданные.Документы.СписаниеИзЭксплуатации);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ТМЦВЭксплуатации);
	ВходящиеДанные.Вставить(Метаданные.РегистрыСведений.СуммыДокументовВВалютахУчета);
	ВходящиеДанные.Вставить(Метаданные.Справочники.КатегорииЭксплуатации);
	ВходящиеДанные.Вставить(Метаданные.Справочники.ПартииТМЦВЭксплуатации);
	
	ЗакрытиеМесяцаСервер.ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ВходящиеДанные, ПредставлениеОперации);
	
КонецПроцедуры

#КонецОбласти

#Область ЗаданияКЗакрытиюМесяца

// Определяет необходимость погашения стоимости ТМЦ в эксплуатации в указанном периоде.
//
// Параметры:
//  Организация	 - СправочникСсылка.Организации - Организация для которой требуется определить необходимость погашения стоимости ТМЦ в эксплуатации.
//  Период		 - Дата - Период в котором нужно проверить необходимость расчета.
//
// Возвращаемое значение:
//  Булево - Истина, если требуется погашение стоимости ТМЦ в эксплуатации.
//
Функция ТребуетсяПогашениеСтоимостиТМЦВЭксплуатации(Организация, Период) Экспорт

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПартииТМЦ.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ПартииТМЦ
	|ИЗ
	|	Справочник.ПартииТМЦВЭксплуатации КАК ПартииТМЦ
	|ГДЕ
	|	ПартииТМЦ.ДатаНачалаЭксплуатации < &НачалоПримененияФСБУ5
	|	И (ПартииТМЦ.ДатаЗавершенияЭксплуатации >= &НачалоМесяца
	|		ИЛИ ПартииТМЦ.ДатаЗавершенияЭксплуатации = ДАТАВРЕМЯ(1, 1, 1))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТМЦВЭксплуатации.КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.ТМЦВЭксплуатации.Остатки(
	|			&НачалоМесяца,
	|			(Организация В (&Организация)
	|				ИЛИ &ПоВсемОрганизациям)
	|			И Партия В (ВЫБРАТЬ ПартииТМЦ.Ссылка ИЗ ПартииТМЦ КАК ПартииТМЦ)
	|	) КАК ТМЦВЭксплуатации
	|ГДЕ
	|	ТМЦВЭксплуатации.КоличествоОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВозвратИзЭксплуатации.Ссылка
	|ИЗ
	|	Документ.ПрочееОприходованиеТоваров КАК ВозвратИзЭксплуатации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрочееОприходованиеТоваров.Товары КАК ВозвратИзЭксплуатацииТовары
	|		ПО ВозвратИзЭксплуатации.Ссылка = ВозвратИзЭксплуатацииТовары.Ссылка
	|ГДЕ
	|	(ВозвратИзЭксплуатации.Организация В(&Организация)
	|		ИЛИ &ПоВсемОрганизациям)
	|	И ВозвратИзЭксплуатации.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратИзЭксплуатации))
	|	И ВозвратИзЭксплуатации.Проведен
	|	И НЕ ВозвратИзЭксплуатации.ПометкаУдаления
	|	И ВозвратИзЭксплуатацииТовары.Партия.ДатаНачалаЭксплуатации < &НачалоПримененияФСБУ5
	|	И ВозвратИзЭксплуатации.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца 
	|	И НАЧАЛОПЕРИОДА(ВозвратИзЭксплуатации.Дата, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ВозвратИзЭксплуатацииТовары.Партия.ДатаНачалаЭксплуатации, МЕСЯЦ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВозвратИзЭксплуатации.Ссылка
	|ИЗ
	|	Документ.ПрочееОприходованиеТоваров КАК ВозвратИзЭксплуатации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрочееОприходованиеТоваров.Товары КАК ВозвратИзЭксплуатацииТовары
	|		ПО ВозвратИзЭксплуатации.Ссылка = ВозвратИзЭксплуатацииТовары.Ссылка
	|ГДЕ
	|	(ВозвратИзЭксплуатации.Организация В(&Организация)
	|		ИЛИ &ПоВсемОрганизациям)
	|	И ВозвратИзЭксплуатации.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратИзЭксплуатации))
	|	И ВозвратИзЭксплуатации.Проведен
	|	И НЕ ВозвратИзЭксплуатации.ПометкаУдаления
	|	И ВозвратИзЭксплуатацииТовары.Партия.ДатаНачалаЭксплуатации < &НачалоПримененияФСБУ5
	|	И ВозвратИзЭксплуатации.Дата МЕЖДУ &НачалоПрошлогоМесяца И &НачалоМесяца
	|	И НАЧАЛОПЕРИОДА(ВозвратИзЭксплуатации.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ВозвратИзЭксплуатацииТовары.Партия.ДатаНачалаЭксплуатации, МЕСЯЦ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПеремещениеВЭксплуатации.Ссылка
	|ИЗ
	|	Документ.ПеремещениеВЭксплуатации КАК ПеремещениеВЭксплуатации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПеремещениеВЭксплуатации.Товары КАК ПеремещениеВЭксплуатацииТовары
	|		ПО ПеремещениеВЭксплуатации.Ссылка = ПеремещениеВЭксплуатацииТовары.Ссылка
	|ГДЕ
	|	(ПеремещениеВЭксплуатации.Организация В(&Организация)
	|		ИЛИ &ПоВсемОрганизациям)
	|	И ПеремещениеВЭксплуатации.Проведен
	|	И НЕ ПеремещениеВЭксплуатации.ПометкаУдаления
	|	И ПеремещениеВЭксплуатацииТовары.Партия.ДатаНачалаЭксплуатации < &НачалоПримененияФСБУ5
	|	И ПеремещениеВЭксплуатации.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца 
	|	И НАЧАЛОПЕРИОДА(ПеремещениеВЭксплуатации.Дата, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ПеремещениеВЭксплуатацииТовары.Партия.ДатаНачалаЭксплуатации, МЕСЯЦ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПеремещениеВЭксплуатации.Ссылка
	|ИЗ
	|	Документ.ПеремещениеВЭксплуатации КАК ПеремещениеВЭксплуатации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПеремещениеВЭксплуатации.Товары КАК ПеремещениеВЭксплуатацииТовары
	|		ПО ПеремещениеВЭксплуатации.Ссылка = ПеремещениеВЭксплуатацииТовары.Ссылка
	|ГДЕ
	|	(ПеремещениеВЭксплуатации.Организация В(&Организация)
	|		ИЛИ &ПоВсемОрганизациям)
	|	И ПеремещениеВЭксплуатации.Проведен
	|	И НЕ ПеремещениеВЭксплуатации.ПометкаУдаления
	|	И ПеремещениеВЭксплуатацииТовары.Партия.ДатаНачалаЭксплуатации < &НачалоПримененияФСБУ5
	|	И ПеремещениеВЭксплуатации.Дата МЕЖДУ &НачалоПрошлогоМесяца И &НачалоМесяца
	|	И НАЧАЛОПЕРИОДА(ПеремещениеВЭксплуатации.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ПеремещениеВЭксплуатацииТовары.Партия.ДатаНачалаЭксплуатации, МЕСЯЦ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	СписаниеИзЭксплуатации.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.СписаниеИзЭксплуатации КАК СписаниеИзЭксплуатации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеИзЭксплуатации.Товары КАК СписаниеИзЭксплуатацииТовары
	|		ПО СписаниеИзЭксплуатации.Ссылка = СписаниеИзЭксплуатацииТовары.Ссылка
	|ГДЕ
	|	(СписаниеИзЭксплуатации.Организация В(&Организация)
	|		ИЛИ &ПоВсемОрганизациям)
	|	И СписаниеИзЭксплуатации.Проведен
	|	И НЕ СписаниеИзЭксплуатации.ПометкаУдаления
	|	И СписаниеИзЭксплуатацииТовары.Партия.ДатаНачалаЭксплуатации < &НачалоПримененияФСБУ5
	|	И СписаниеИзЭксплуатации.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца 
	|	И НАЧАЛОПЕРИОДА(СписаниеИзЭксплуатации.Дата, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(СписаниеИзЭксплуатацииТовары.Партия.ДатаНачалаЭксплуатации, МЕСЯЦ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	СписаниеИзЭксплуатации.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.СписаниеИзЭксплуатации КАК СписаниеИзЭксплуатации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеИзЭксплуатации.Товары КАК СписаниеИзЭксплуатацииТовары
	|		ПО СписаниеИзЭксплуатации.Ссылка = СписаниеИзЭксплуатацииТовары.Ссылка
	|ГДЕ
	|	(СписаниеИзЭксплуатации.Организация В(&Организация)
	|		ИЛИ &ПоВсемОрганизациям)
	|	И СписаниеИзЭксплуатации.Проведен
	|	И НЕ СписаниеИзЭксплуатации.ПометкаУдаления
	|	И СписаниеИзЭксплуатацииТовары.Партия.ДатаНачалаЭксплуатации < &НачалоПримененияФСБУ5
	|	И СписаниеИзЭксплуатации.Дата МЕЖДУ &НачалоПрошлогоМесяца И &НачалоМесяца
	|	И НАЧАЛОПЕРИОДА(СписаниеИзЭксплуатации.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(СписаниеИзЭксплуатацииТовары.Партия.ДатаНачалаЭксплуатации, МЕСЯЦ)
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НачалоПрошлогоМесяца", НачалоМесяца(НачалоМесяца(Период)-1));
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Период));
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(Период));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", НЕ ЗначениеЗаполнено(Организация));
	Запрос.УстановитьПараметр("НачалоПримененияФСБУ5", РеглУчетКлиентСервер.НачалоПримененияФСБУ5_2019());
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат НЕ Результат.Пустой();

КонецФункции

#КонецОбласти

#Область Прочее

Процедура СоздатьПакетыПогашенияСтоимостиТМЦ(Месяц, Организация, Отказ) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();

	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПакетыПогашенияСтоимостиТМЦ");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
		ИсточникБлокировки = Новый ТаблицаЗначений;
		ИсточникБлокировки.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
		
		Если ТипЗнч(Организация) = Тип("Массив") Тогда
			КоличествоСтрок = Организация.Количество();
			Если КоличествоСтрок <> 0 Тогда
				Счетчик = 0;
				Пока Счетчик < КоличествоСтрок Цикл
					ИсточникБлокировки.Добавить();
					Счетчик = Счетчик + 1;
				КонецЦикла;
				ИсточникБлокировки.ЗагрузитьКолонку(Организация, "Организация");
				ЭлементБлокировки.ИсточникДанных = ИсточникБлокировки;
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Организация", "Организация");
			КонецЕсли;
			// если массив пустой, то блокировка ставится по всем организациям.
		ИначеЕсли ТипЗнч(Организация) = Тип("СправочникСсылка.Организации")
			И ЗначениеЗаполнено(Организация) Тогда
			СтрокаБлокировки = ИсточникБлокировки.Добавить();
			СтрокаБлокировки.Организация = Организация;
			ЭлементБлокировки.ИсточникДанных = ИсточникБлокировки;
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Организация", "Организация");
		КонецЕсли;
		
		Блокировка.Заблокировать(); 
	
		РассчитатьНомераПакетовДляПартийТМЦ(Месяц, Организация);
		УдалитьЗаданияНаНулевойПакет(Месяц, Организация);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
	
		ОтменитьТранзакцию();
		
		Отказ = Истина;
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Закрытие месяца.Погашение стоимости ТМЦ в эксплуатации';
				|en = 'Month-end closing. Repayment for inventory in operation'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Документы.ПогашениеСтоимостиТМЦВЭксплуатации,
			,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки; 
	
КонецПроцедуры

Процедура РассчитатьНомераПакетовДляПартийТМЦ(Месяц, Организация)
	
	МаксимальныйОбъемПакета = 20000;
	ТекущиеНомераПакетов = Новый Соответствие;
	
	ИзмененныеПакеты = Новый ТаблицаЗначений;
	ИзмененныеПакеты.Колонки.Добавить("Организация");
	ИзмененныеПакеты.Колонки.Добавить("НомерПакета");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПартииТМЦ.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ПартииТМЦ
	|ИЗ
	|	Справочник.ПартииТМЦВЭксплуатации КАК ПартииТМЦ
	|ГДЕ
	|	(ПартииТМЦ.ДатаЗавершенияЭксплуатации > &НачДата
	|		ИЛИ ПартииТМЦ.ДатаЗавершенияЭксплуатации = ДАТАВРЕМЯ(1, 1, 1))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.Партия,
	|	ВложенныйЗапрос.НомерПакета
	|ПОМЕСТИТЬ ПакетыПогашенияСтоимостиТМЦ
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТМЦВЭксплуатации.Организация КАК Организация,
	|		ТМЦВЭксплуатации.Партия КАК Партия,
	|		ЕСТЬNULL(ПакетыПогашенияСтоимостиТМЦ.НомерПакета, 0) КАК НомерПакета
	|	ИЗ
	|		РегистрНакопления.ТМЦВЭксплуатации.Остатки(
	|				&НачДата,
	|				(Организация В (&СписокОрганизаций)
	|					ИЛИ &ПоВсемОрганизациям)
	|				И Партия В (ВЫБРАТЬ ПартииТМЦ.Ссылка ИЗ ПартииТМЦ КАК ПартииТМЦ)
	|		) КАК ТМЦВЭксплуатации
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыПогашенияСтоимостиТМЦ КАК ПакетыПогашенияСтоимостиТМЦ
	|			ПО ТМЦВЭксплуатации.Партия = ПакетыПогашенияСтоимостиТМЦ.Партия
	|				И ТМЦВЭксплуатации.Организация = ПакетыПогашенияСтоимостиТМЦ.Организация
	|	ГДЕ
	|		ТМЦВЭксплуатации.КоличествоОстаток > 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВозвратИзЭксплуатации.Организация,
	|		ВозвратИзЭксплуатацииТовары.Партия,
	|		ЕСТЬNULL(ПакетыПогашенияСтоимостиТМЦ.НомерПакета, 0)
	|	ИЗ
	|		Документ.ПрочееОприходованиеТоваров КАК ВозвратИзЭксплуатации
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрочееОприходованиеТоваров.Товары КАК ВозвратИзЭксплуатацииТовары
	|			ПО ВозвратИзЭксплуатации.Ссылка = ВозвратИзЭксплуатацииТовары.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|			ПО (ВозвратИзЭксплуатацииТовары.ИдентификаторСтроки = СуммыДокументовВВалютахУчета.ИдентификаторСтроки)
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыПогашенияСтоимостиТМЦ КАК ПакетыПогашенияСтоимостиТМЦ
	|			ПО (ВозвратИзЭксплуатацииТовары.Партия = ПакетыПогашенияСтоимостиТМЦ.Партия)
	|				И ВозвратИзЭксплуатации.Организация = ПакетыПогашенияСтоимостиТМЦ.Организация
	|	ГДЕ
	|		(ВозвратИзЭксплуатации.Организация В (&СписокОрганизаций)
	|				ИЛИ &ПоВсемОрганизациям)
	|		И ВозвратИзЭксплуатации.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратИзЭксплуатации))
	|		И ВозвратИзЭксплуатации.Проведен
	|		И НЕ ВозвратИзЭксплуатации.ПометкаУдаления
	|		И ВозвратИзЭксплуатации.Дата МЕЖДУ &НачДата И &КонДата
	|		И НАЧАЛОПЕРИОДА(ВозвратИзЭксплуатации.Дата, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ВозвратИзЭксплуатацииТовары.Партия.ДатаНачалаЭксплуатации, МЕСЯЦ)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВозвратИзЭксплуатации.Организация,
	|		ВозвратИзЭксплуатацииТовары.Партия,
	|		ЕСТЬNULL(ПакетыПогашенияСтоимостиТМЦ.НомерПакета, 0)
	|	ИЗ
	|		Документ.ПрочееОприходованиеТоваров КАК ВозвратИзЭксплуатации
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрочееОприходованиеТоваров.Товары КАК ВозвратИзЭксплуатацииТовары
	|			ПО ВозвратИзЭксплуатации.Ссылка = ВозвратИзЭксплуатацииТовары.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|			ПО (ВозвратИзЭксплуатацииТовары.ИдентификаторСтроки = СуммыДокументовВВалютахУчета.ИдентификаторСтроки)
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыПогашенияСтоимостиТМЦ КАК ПакетыПогашенияСтоимостиТМЦ
	|			ПО (ВозвратИзЭксплуатацииТовары.Партия = ПакетыПогашенияСтоимостиТМЦ.Партия)
	|				И ВозвратИзЭксплуатации.Организация = ПакетыПогашенияСтоимостиТМЦ.Организация
	|	ГДЕ
	|		(ВозвратИзЭксплуатации.Организация В (&СписокОрганизаций)
	|				ИЛИ &ПоВсемОрганизациям)
	|		И ВозвратИзЭксплуатации.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратИзЭксплуатации))
	|		И ВозвратИзЭксплуатации.Проведен
	|		И НЕ ВозвратИзЭксплуатации.ПометкаУдаления
	|		И ВозвратИзЭксплуатации.Дата МЕЖДУ &НачДатаПрошлогоМесяца И &НачДата
	|		И НАЧАЛОПЕРИОДА(ВозвратИзЭксплуатации.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ВозвратИзЭксплуатацииТовары.Партия.ДатаНачалаЭксплуатации, МЕСЯЦ)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПеремещениеВЭксплуатации.Организация,
	|		ПеремещениеВЭксплуатацииТовары.Партия,
	|		ЕСТЬNULL(ПакетыПогашенияСтоимостиТМЦ.НомерПакета, 0)
	|	ИЗ
	|		Документ.ПеремещениеВЭксплуатации КАК ПеремещениеВЭксплуатации
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПеремещениеВЭксплуатации.Товары КАК ПеремещениеВЭксплуатацииТовары
	|			ПО ПеремещениеВЭксплуатации.Ссылка = ПеремещениеВЭксплуатацииТовары.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыПогашенияСтоимостиТМЦ КАК ПакетыПогашенияСтоимостиТМЦ
	|			ПО (ПеремещениеВЭксплуатацииТовары.Партия = ПакетыПогашенияСтоимостиТМЦ.Партия)
	|				И ПеремещениеВЭксплуатации.Организация = ПакетыПогашенияСтоимостиТМЦ.Организация
	|	ГДЕ
	|		(ПеремещениеВЭксплуатации.Организация В (&СписокОрганизаций)
	|				ИЛИ &ПоВсемОрганизациям)
	|		И ПеремещениеВЭксплуатации.Проведен
	|		И НЕ ПеремещениеВЭксплуатации.ПометкаУдаления
	|		И ПеремещениеВЭксплуатации.Дата МЕЖДУ &НачДата И &КонДата
	|		И НАЧАЛОПЕРИОДА(ПеремещениеВЭксплуатации.Дата, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ПеремещениеВЭксплуатацииТовары.Партия.ДатаНачалаЭксплуатации, МЕСЯЦ)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПеремещениеВЭксплуатации.Организация,
	|		ПеремещениеВЭксплуатацииТовары.Партия,
	|		ЕСТЬNULL(ПакетыПогашенияСтоимостиТМЦ.НомерПакета, 0)
	|	ИЗ
	|		Документ.ПеремещениеВЭксплуатации КАК ПеремещениеВЭксплуатации
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПеремещениеВЭксплуатации.Товары КАК ПеремещениеВЭксплуатацииТовары
	|			ПО ПеремещениеВЭксплуатации.Ссылка = ПеремещениеВЭксплуатацииТовары.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыПогашенияСтоимостиТМЦ КАК ПакетыПогашенияСтоимостиТМЦ
	|			ПО (ПеремещениеВЭксплуатацииТовары.Партия = ПакетыПогашенияСтоимостиТМЦ.Партия)
	|				И ПеремещениеВЭксплуатации.Организация = ПакетыПогашенияСтоимостиТМЦ.Организация
	|	ГДЕ
	|		(ПеремещениеВЭксплуатации.Организация В (&СписокОрганизаций)
	|				ИЛИ &ПоВсемОрганизациям)
	|		И ПеремещениеВЭксплуатации.Проведен
	|		И НЕ ПеремещениеВЭксплуатации.ПометкаУдаления
	|		И ПеремещениеВЭксплуатации.Дата МЕЖДУ &НачДатаПрошлогоМесяца И &НачДата
	|		И НАЧАЛОПЕРИОДА(ПеремещениеВЭксплуатации.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ПеремещениеВЭксплуатацииТовары.Партия.ДатаНачалаЭксплуатации, МЕСЯЦ)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписаниеИзЭксплуатации.Организация,
	|		СписаниеИзЭксплуатацииТовары.Партия,
	|		ЕСТЬNULL(ПакетыПогашенияСтоимостиТМЦ.НомерПакета, 0)
	|	ИЗ
	|		Документ.СписаниеИзЭксплуатации КАК СписаниеИзЭксплуатации
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеИзЭксплуатации.Товары КАК СписаниеИзЭксплуатацииТовары
	|			ПО СписаниеИзЭксплуатации.Ссылка = СписаниеИзЭксплуатацииТовары.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыПогашенияСтоимостиТМЦ КАК ПакетыПогашенияСтоимостиТМЦ
	|			ПО (СписаниеИзЭксплуатацииТовары.Партия = ПакетыПогашенияСтоимостиТМЦ.Партия)
	|				И СписаниеИзЭксплуатации.Организация = ПакетыПогашенияСтоимостиТМЦ.Организация
	|	ГДЕ
	|		(СписаниеИзЭксплуатации.Организация В (&СписокОрганизаций)
	|				ИЛИ &ПоВсемОрганизациям)
	|		И СписаниеИзЭксплуатации.Проведен
	|		И НЕ СписаниеИзЭксплуатации.ПометкаУдаления
	|		И СписаниеИзЭксплуатации.Дата МЕЖДУ &НачДата И &КонДата
	|		И НАЧАЛОПЕРИОДА(СписаниеИзЭксплуатации.Дата, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(СписаниеИзЭксплуатацииТовары.Партия.ДатаНачалаЭксплуатации, МЕСЯЦ)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписаниеИзЭксплуатации.Организация,
	|		СписаниеИзЭксплуатацииТовары.Партия,
	|		ЕСТЬNULL(ПакетыПогашенияСтоимостиТМЦ.НомерПакета, 0)
	|	ИЗ
	|		Документ.СписаниеИзЭксплуатации КАК СписаниеИзЭксплуатации
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеИзЭксплуатации.Товары КАК СписаниеИзЭксплуатацииТовары
	|			ПО СписаниеИзЭксплуатации.Ссылка = СписаниеИзЭксплуатацииТовары.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыПогашенияСтоимостиТМЦ КАК ПакетыПогашенияСтоимостиТМЦ
	|			ПО (СписаниеИзЭксплуатацииТовары.Партия = ПакетыПогашенияСтоимостиТМЦ.Партия)
	|				И СписаниеИзЭксплуатации.Организация = ПакетыПогашенияСтоимостиТМЦ.Организация
	|	ГДЕ
	|		(СписаниеИзЭксплуатации.Организация В (&СписокОрганизаций)
	|				ИЛИ &ПоВсемОрганизациям)
	|		И СписаниеИзЭксплуатации.Проведен
	|		И НЕ СписаниеИзЭксплуатации.ПометкаУдаления
	|		И СписаниеИзЭксплуатации.Дата МЕЖДУ &НачДатаПрошлогоМесяца И &НачДата
	|		И НАЧАЛОПЕРИОДА(СписаниеИзЭксплуатации.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(СписаниеИзЭксплуатацииТовары.Партия.ДатаНачалаЭксплуатации, МЕСЯЦ)
	|	) КАК ВложенныйЗапрос
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПакетыПогашенияСтоимостиТМЦ.Организация,
	|	ПакетыПогашенияСтоимостиТМЦ.НомерПакета,
	|	КОЛИЧЕСТВО(ПакетыПогашенияСтоимостиТМЦ.Партия) КАК ОбъемПакета
	|ИЗ
	|	ПакетыПогашенияСтоимостиТМЦ КАК ПакетыПогашенияСтоимостиТМЦ
	|ГДЕ
	|	ПакетыПогашенияСтоимостиТМЦ.НомерПакета <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ПакетыПогашенияСтоимостиТМЦ.Организация,
	|	ПакетыПогашенияСтоимостиТМЦ.НомерПакета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПакетыПогашенияСтоимостиТМЦ.Организация,
	|	ПакетыПогашенияСтоимостиТМЦ.Партия
	|ИЗ
	|	ПакетыПогашенияСтоимостиТМЦ КАК ПакетыПогашенияСтоимостиТМЦ
	|ГДЕ
	|	ПакетыПогашенияСтоимостиТМЦ.НомерПакета = 0";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НачДата", НачалоМесяца(Месяц));
	Запрос.УстановитьПараметр("КонДата", КонецМесяца(Месяц));
	Запрос.УстановитьПараметр("НачДатаПрошлогоМесяца", НачалоМесяца(НачалоМесяца(Месяц)-1));
	Запрос.УстановитьПараметр("СписокОрганизаций", Организация);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", НЕ ЗначениеЗаполнено(Организация));
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ОбъемПакетов = РезультатЗапроса[РезультатЗапроса.ВГраница()-1].Выгрузить();
	
	Выборка = РезультатЗапроса[РезультатЗапроса.ВГраница()].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НомерИОбъемПакета = ТекущиеНомераПакетов.Получить(Выборка.Организация);
		Если НомерИОбъемПакета = Неопределено 
			ИЛИ НомерИОбъемПакета.ОбъемПакета >= МаксимальныйОбъемПакета Тогда
			
			СледующийНомерПакета = ?(НомерИОбъемПакета = Неопределено, 0, НомерИОбъемПакета.НомерПакета);
			НомерИОбъемПакета = НомерИОбъемПакета(Выборка.Организация, СледующийНомерПакета, ОбъемПакетов, МаксимальныйОбъемПакета);
			
			ТекущиеНомераПакетов.Вставить(Выборка.Организация, НомерИОбъемПакета);
			
			ИзмененныйПакет = ИзмененныеПакеты.Добавить();
			ИзмененныйПакет.Организация = Выборка.Организация;
			ИзмененныйПакет.НомерПакета = НомерИОбъемПакета.НомерПакета;
			
		КонецЕсли; 
		
		НовыйПакетЗапись = РегистрыСведений.ПакетыПогашенияСтоимостиТМЦ.СоздатьМенеджерЗаписи();
		НовыйПакетЗапись.Организация = Выборка.Организация;
		НовыйПакетЗапись.Партия = Выборка.Партия;
		НовыйПакетЗапись.НомерПакета = НомерИОбъемПакета.НомерПакета;
		НовыйПакетЗапись.Записать();
		
		НомерИОбъемПакета.ОбъемПакета = НомерИОбъемПакета.ОбъемПакета + 1;
		
	КонецЦикла;
	
	Если ПланыОбмена.ГлавныйУзел() = Неопределено Тогда
		// В РИБ данный регистр обрабатывается только в главном узле.
		
		НомерЗадания = РегистрыСведений.ЗаданияКПогашениюСтоимостиТМЦВЭксплуатации.ПолучитьНомерЗадания();
		
		// Требуется сформировать задания, т.к. у партий появился номер пакета.
		// Если это не сделать, то потеряется информация о том, что для этих партий нужен пересчет.
		Для каждого ИзмененныйПакет Из ИзмененныеПакеты Цикл
			НовоеЗаданиеЗапись = РегистрыСведений.ЗаданияКПогашениюСтоимостиТМЦВЭксплуатации.СоздатьМенеджерЗаписи();
			НовоеЗаданиеЗапись.Месяц        = НачалоМесяца(Месяц);
			НовоеЗаданиеЗапись.НомерПакета  = ИзмененныйПакет.НомерПакета;
			НовоеЗаданиеЗапись.Организация  = ИзмененныйПакет.Организация;
			НовоеЗаданиеЗапись.НомерЗадания = НомерЗадания;
			НовоеЗаданиеЗапись.Записать();
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

Процедура УдалитьЗаданияНаНулевойПакет(Месяц, Организация)

	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		// В РИБ данный регистр обрабатывается только в главном узле.
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачДата", НачалоМесяца(Месяц));
	Запрос.УстановитьПараметр("КонДата", КонецМесяца(Месяц));
	Запрос.УстановитьПараметр("СписокОрганизаций", Организация);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", НЕ ЗначениеЗаполнено(Организация));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Задания.Организация,
	|	Задания.НомерЗадания,
	|	Задания.Месяц
	|ИЗ
	|	РегистрСведений.ЗаданияКПогашениюСтоимостиТМЦВЭксплуатации КАК Задания
	|ГДЕ
	|	(Задания.Организация В (&СписокОрганизаций)
	|			ИЛИ &ПоВсемОрганизациям)
	|	И Задания.Месяц МЕЖДУ &НачДата И &КонДата
	|	И Задания.НомерПакета = 0";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		НаборЗаписей = РегистрыСведений.ЗаданияКПогашениюСтоимостиТМЦВЭксплуатации.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Месяц.Установить(Выборка.Месяц);
		НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
		НаборЗаписей.Отбор.НомерЗадания.Установить(Выборка.НомерЗадания);
		НаборЗаписей.Отбор.НомерПакета.Установить(0);
		НаборЗаписей.Записать();
	КонецЦикла; 
	
КонецПроцедуры

Функция НомерИОбъемПакета(Организация, Знач НомерПакета, ОбъемПакетов, МаксимальныйОбъем)
	
	ОбъемПакета = МаксимальныйОбъем;
	Пока ОбъемПакета >= МаксимальныйОбъем Цикл
		НомерПакета = НомерПакета + 1;
		СтруктураПоиска = Новый Структура("Организация,НомерПакета", Организация, НомерПакета);
		СписокСтрок = ОбъемПакетов.НайтиСтроки(СтруктураПоиска);
		Если СписокСтрок.Количество() <> 0 Тогда
			ОбъемПакета = СписокСтрок[0].ОбъемПакета;
		Иначе
			ОбъемПакета = 0;
		КонецЕсли;
	КонецЦикла;

	Возврат Новый Структура("ОбъемПакета,НомерПакета", ОбъемПакета,НомерПакета);
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Документы.ПогашениеСтоимостиТМЦВЭксплуатации.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.7.273";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("fec2ffd9-1240-4de9-8032-7031fda105ba");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.ПогашениеСтоимостиТМЦВЭксплуатации.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Обновляет документы ""Погашение стоимости ТМЦ в эксплуатации"":
	|- заполняет новый реквизит ""Направление деятельности"" в табличных частях
	|- заполняет реквизит ""Партия"" новой партией';
	|en = 'Updates the ""Repayment for inventory in operation"" documents:
	|- fills in the new ""Line of business"" attribute in tables
	|- fills in the ""Lot reference"" attribute with a new lot'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.ПогашениеСтоимостиТМЦВЭксплуатации.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ПартииТМЦВЭксплуатации.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Документы.ПогашениеСтоимостиТМЦВЭксплуатации.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Документы.ПогашениеСтоимостиТМЦВЭксплуатации.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ПартииТМЦВЭксплуатации.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

КонецПроцедуры

// Регистрирует данные к обработке при переходе на новую версию.
// 
// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = "Документ.ПогашениеСтоимостиТМЦВЭксплуатации";
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Ссылка.Дата УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Ссылка");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка КАК Ссылка
	|ИЗ
	|	(ВЫБРАТЬ 
	|		ТабличнаяЧасть.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.ПогашениеСтоимостиТМЦВЭксплуатации.Расходы КАК ТабличнаяЧасть
	|	ГДЕ
	|		ТабличнаяЧасть.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			И ТабличнаяЧасть.УдалитьПартияТМЦВЭксплуатации.НаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|
	|		ИЛИ НЕ ЕСТЬNULL(ТабличнаяЧасть.Партия.Партия258, ЛОЖЬ)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ 
	|		ТабличнаяЧасть.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.ПогашениеСтоимостиТМЦВЭксплуатации.Списания КАК ТабличнаяЧасть
	|	ГДЕ
	|		ТабличнаяЧасть.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			И ТабличнаяЧасть.УдалитьПартияТМЦВЭксплуатации.НаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|
	|		ИЛИ НЕ ЕСТЬNULL(ТабличнаяЧасть.Партия.Партия258, ЛОЖЬ)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ 
	|		ТабличнаяЧасть.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.ПогашениеСтоимостиТМЦВЭксплуатации.Перемещения КАК ТабличнаяЧасть
	|	ГДЕ
	|		ТабличнаяЧасть.НаправлениеДеятельностиОтправитель = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			И ТабличнаяЧасть.УдалитьПартияТМЦВЭксплуатации.НаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|
	|		ИЛИ НЕ ЕСТЬNULL(ТабличнаяЧасть.Партия.Партия258, ЛОЖЬ)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ 
	|		ТабличнаяЧасть.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.ПогашениеСтоимостиТМЦВЭксплуатации.СписанияЗабалансовыйУчет КАК ТабличнаяЧасть
	|	ГДЕ
	|		ТабличнаяЧасть.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			И ТабличнаяЧасть.УдалитьПартияТМЦВЭксплуатации.НаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|
	|		ИЛИ НЕ ЕСТЬNULL(ТабличнаяЧасть.Партия.Партия258, ЛОЖЬ)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ 
	|		ТабличнаяЧасть.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.ПогашениеСтоимостиТМЦВЭксплуатации.ПеремещенияЗабалансовыйУчет КАК ТабличнаяЧасть
	|	ГДЕ
	|		ТабличнаяЧасть.НаправлениеДеятельностиОтправитель = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			И ТабличнаяЧасть.УдалитьПартияТМЦВЭксплуатации.НаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|
	|		ИЛИ НЕ ЕСТЬNULL(ТабличнаяЧасть.Партия.Партия258, ЛОЖЬ)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ 
	|		ТабличнаяЧасть.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.ПогашениеСтоимостиТМЦВЭксплуатации.Доходы КАК ТабличнаяЧасть
	|	ГДЕ
	|		НЕ ЕСТЬNULL(ТабличнаяЧасть.Партия.Партия258, ЛОЖЬ)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ 
	|		ТабличнаяЧасть.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.ПогашениеСтоимостиТМЦВЭксплуатации.Возвраты КАК ТабличнаяЧасть
	|	ГДЕ
	|		НЕ ЕСТЬNULL(ТабличнаяЧасть.Партия.Партия258, ЛОЖЬ)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ 
	|		ТабличнаяЧасть.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.ПогашениеСтоимостиТМЦВЭксплуатации.СписанияФСБУ5 КАК ТабличнаяЧасть
	|	ГДЕ
	|		ТабличнаяЧасть.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			И ТабличнаяЧасть.УдалитьПартияТМЦВЭксплуатации.НаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|
	|		ИЛИ НЕ ЕСТЬNULL(ТабличнаяЧасть.Партия.Партия258, ЛОЖЬ)
	|
	|	) КАК ДанныеДокумента";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "Документ.ПогашениеСтоимостиТМЦВЭксплуатации";
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;

	ЧитаемыеДанные = Новый Массив;
	ЧитаемыеДанные.Добавить("Справочник.ПартииТМЦВЭксплуатации");
	ДополнительныеПараметрыПроцедуры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДополнительныеПараметрыПроцедуры.ИмяВременнойТаблицы = "ВТЗаблокированныеДанные";
	ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияСсылок(
			Параметры.Очередь, 
			ЧитаемыеДанные, 
			МенеджерВременныхТаблиц, 
			ДополнительныеПараметрыПроцедуры);

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаДанных.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТДляОбработки
	|ИЗ
	|	&ТаблицаОбновляемыхДанных КАК ТаблицаДанных
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|
	|	ВТДляОбработки.Ссылка КАК Ссылка
	|
	|ПОМЕСТИТЬ ВТЗаблокированныеСсылки
	|
	|ИЗ
	|
	|	(ВЫБРАТЬ
	|		ВТДляОбработки.Ссылка КАК Ссылка
	|	ИЗ
	|		ВТДляОбработки КАК ВТДляОбработки
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПогашениеСтоимостиТМЦВЭксплуатации.Расходы КАК ТабличнаяЧасть
	|			ПО (ТабличнаяЧасть.Ссылка = ВТДляОбработки.Ссылка)
	|
	|		ГДЕ
	|			ТабличнаяЧасть.УдалитьПартияТМЦВЭксплуатации В
	|				(ВЫБРАТЬ
	|					ВТЗаблокированныеДанные.Ссылка
	|				ИЗ
	|					ВТЗаблокированныеДанные КАК ВТЗаблокированныеДанные)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ВТДляОбработки.Ссылка КАК Ссылка
	|	ИЗ
	|		ВТДляОбработки КАК ВТДляОбработки
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПогашениеСтоимостиТМЦВЭксплуатации.Списания КАК ТабличнаяЧасть
	|			ПО (ТабличнаяЧасть.Ссылка = ВТДляОбработки.Ссылка)
	|
	|		ГДЕ
	|			ТабличнаяЧасть.УдалитьПартияТМЦВЭксплуатации В
	|				(ВЫБРАТЬ
	|					ВТЗаблокированныеДанные.Ссылка
	|				ИЗ
	|					ВТЗаблокированныеДанные КАК ВТЗаблокированныеДанные)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ВТДляОбработки.Ссылка КАК Ссылка
	|	ИЗ
	|		ВТДляОбработки КАК ВТДляОбработки
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПогашениеСтоимостиТМЦВЭксплуатации.Перемещения КАК ТабличнаяЧасть
	|			ПО (ТабличнаяЧасть.Ссылка = ВТДляОбработки.Ссылка)
	|
	|		ГДЕ
	|			ТабличнаяЧасть.УдалитьПартияТМЦВЭксплуатации В
	|				(ВЫБРАТЬ
	|					ВТЗаблокированныеДанные.Ссылка
	|				ИЗ
	|					ВТЗаблокированныеДанные КАК ВТЗаблокированныеДанные)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ВТДляОбработки.Ссылка КАК Ссылка
	|	ИЗ
	|		ВТДляОбработки КАК ВТДляОбработки
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПогашениеСтоимостиТМЦВЭксплуатации.СписанияЗабалансовыйУчет КАК ТабличнаяЧасть
	|			ПО (ТабличнаяЧасть.Ссылка = ВТДляОбработки.Ссылка)
	|
	|		ГДЕ
	|			ТабличнаяЧасть.УдалитьПартияТМЦВЭксплуатации В
	|				(ВЫБРАТЬ
	|					ВТЗаблокированныеДанные.Ссылка
	|				ИЗ
	|					ВТЗаблокированныеДанные КАК ВТЗаблокированныеДанные)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ВТДляОбработки.Ссылка КАК Ссылка
	|	ИЗ
	|		ВТДляОбработки КАК ВТДляОбработки
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПогашениеСтоимостиТМЦВЭксплуатации.ПеремещенияЗабалансовыйУчет КАК ТабличнаяЧасть
	|			ПО (ТабличнаяЧасть.Ссылка = ВТДляОбработки.Ссылка)
	|
	|		ГДЕ
	|			ТабличнаяЧасть.УдалитьПартияТМЦВЭксплуатации В
	|				(ВЫБРАТЬ
	|					ВТЗаблокированныеДанные.Ссылка
	|				ИЗ
	|					ВТЗаблокированныеДанные КАК ВТЗаблокированныеДанные)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ВТДляОбработки.Ссылка КАК Ссылка
	|	ИЗ
	|		ВТДляОбработки КАК ВТДляОбработки
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПогашениеСтоимостиТМЦВЭксплуатации.Доходы КАК ТабличнаяЧасть
	|			ПО (ТабличнаяЧасть.Ссылка = ВТДляОбработки.Ссылка)
	|
	|		ГДЕ
	|			ТабличнаяЧасть.УдалитьПартияТМЦВЭксплуатации В
	|				(ВЫБРАТЬ
	|					ВТЗаблокированныеДанные.Ссылка
	|				ИЗ
	|					ВТЗаблокированныеДанные КАК ВТЗаблокированныеДанные)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ВТДляОбработки.Ссылка КАК Ссылка
	|	ИЗ
	|		ВТДляОбработки КАК ВТДляОбработки
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПогашениеСтоимостиТМЦВЭксплуатации.Возвраты КАК ТабличнаяЧасть
	|			ПО (ТабличнаяЧасть.Ссылка = ВТДляОбработки.Ссылка)
	|
	|		ГДЕ
	|			ТабличнаяЧасть.УдалитьПартияТМЦВЭксплуатации В
	|				(ВЫБРАТЬ
	|					ВТЗаблокированныеДанные.Ссылка
	|				ИЗ
	|					ВТЗаблокированныеДанные КАК ВТЗаблокированныеДанные)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ВТДляОбработки.Ссылка КАК Ссылка
	|	ИЗ
	|		ВТДляОбработки КАК ВТДляОбработки
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПогашениеСтоимостиТМЦВЭксплуатации.СписанияФСБУ5 КАК ТабличнаяЧасть
	|			ПО (ТабличнаяЧасть.Ссылка = ВТДляОбработки.Ссылка)
	|
	|		ГДЕ
	|			ТабличнаяЧасть.УдалитьПартияТМЦВЭксплуатации В
	|				(ВЫБРАТЬ
	|					ВТЗаблокированныеДанные.Ссылка
	|				ИЗ
	|					ВТЗаблокированныеДанные КАК ВТЗаблокированныеДанные)
	|
	|	) КАК ВТДляОбработки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДляОбработки.Ссылка КАК Ссылка
	|ИЗ
	|	ВТДляОбработки КАК ВТДляОбработки
	|ГДЕ
	|	НЕ ВТДляОбработки.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТЗаблокированныеСсылки.Ссылка
	|				ИЗ
	|					ВТЗаблокированныеСсылки КАК ВТЗаблокированныеСсылки)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаОбновляемыхДанных", ОбновляемыеДанные);
	Результат = Запрос.Выполнить();
	
	ПроблемныхОбъектов = 0;
	ОбъектовОбработано = 0;
	
	СписокТЧ = Новый Массив;
	СписокТЧ.Добавить("Расходы");
	СписокТЧ.Добавить("Списания");
	СписокТЧ.Добавить("Перемещения");
	СписокТЧ.Добавить("СписанияЗабалансовыйУчет");
	СписокТЧ.Добавить("ПеремещенияЗабалансовыйУчет");
	СписокТЧ.Добавить("Доходы");
	СписокТЧ.Добавить("Возвраты");
	СписокТЧ.Добавить("СписанияФСБУ5");
	
	Выборка = Результат.Выбрать();
 	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект(); // ДокументОбъект.ПогашениеСтоимостиТМЦВЭксплуатации
			Если ДокументОбъект = Неопределено Тогда
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
				ЗафиксироватьТранзакцию();
 				Продолжить;
			КонецЕсли;
			
			СписокПартий = Новый Массив;
			Для Каждого ИмяТЧ Из СписокТЧ Цикл
				Для Каждого ДанныеСтроки Из ДокументОбъект[ИмяТЧ] Цикл
					Если ЗначениеЗаполнено(ДанныеСтроки.УдалитьПартияТМЦВЭксплуатации) Тогда
						СписокПартий.Добавить(ДанныеСтроки.УдалитьПартияТМЦВЭксплуатации);
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			
			Если СписокПартий.Количество() <> 0 Тогда
				
				РеквизитыПартий = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
					СписокПартий, "НаправлениеДеятельности,НоваяПартия,Партия258");
				
				Для Каждого ИмяТЧ Из СписокТЧ Цикл
					Для Каждого ДанныеСтроки Из ДокументОбъект[ИмяТЧ] Цикл
						
						Если НЕ ЗначениеЗаполнено(ДанныеСтроки.УдалитьПартияТМЦВЭксплуатации) Тогда
							Продолжить
						КонецЕсли;
						
						СвойстваПартии = РеквизитыПартий.Получить(ДанныеСтроки.УдалитьПартияТМЦВЭксплуатации);
						
						Если НЕ СвойстваПартии.Партия258
							И ЗначениеЗаполнено(СвойстваПартии.НоваяПартия) Тогда
							ДанныеСтроки.Партия = СвойстваПартии.НоваяПартия;
						ИначеЕсли СвойстваПартии.Партия258 Тогда
							ДанныеСтроки.Партия = ДанныеСтроки.УдалитьПартияТМЦВЭксплуатации;
						КонецЕсли;
						
						Если (ИмяТЧ = "Перемещения" ИЛИ ИмяТЧ = "ПеремещенияЗабалансовыйУчет")
							И НЕ ЗначениеЗаполнено(ДанныеСтроки.НаправлениеДеятельностиОтправитель) Тогда 
						
							ДанныеСтроки.НаправлениеДеятельностиОтправитель = СвойстваПартии.НаправлениеДеятельности;
							ДанныеСтроки.НаправлениеДеятельностиПолучатель = СвойстваПартии.НаправлениеДеятельности;
							
						ИначеЕсли ИмяТЧ <> "Доходы"
							И ИмяТЧ <> "Возвраты"
							И ИмяТЧ <> "Перемещения"
							И ИмяТЧ <> "ПеремещенияЗабалансовыйУчет"
							И НЕ ЗначениеЗаполнено(ДанныеСтроки.НаправлениеДеятельности) Тогда
								
							ДанныеСтроки.НаправлениеДеятельности = СвойстваПартии.НаправлениеДеятельности;
						КонецЕсли;
						
					КонецЦикла;
				КонецЦикла;
				
			КонецЕсли;
			
			Если ДокументОбъект.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ДокументОбъект.Ссылка);
			КонецЕсли;
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;

			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Выборка.Ссылка);
			
		КонецПопытки;
		
	КонецЦикла;
	
 	ВнеоборотныеАктивыСлужебный.ПроверитьВыполнениеОбработчика(
 		ПроблемныхОбъектов, 
 		ОбъектовОбработано, 
 		ПолноеИмяОбъекта);
 		
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры
 
#КонецОбласти

#КонецОбласти

#КонецЕсли
