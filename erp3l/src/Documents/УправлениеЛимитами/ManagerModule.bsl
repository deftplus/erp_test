#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс


#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция СформироватьДокумент(ДанныеЗаполнения) Экспорт
	
	Если ТипЗнч(ДанныеЗаполнения.Данные) <> Тип("ТаблицаЗначений") ИЛИ ДанныеЗаполнения.Данные.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка 
		Док = СоздатьДокумент();
		Док.Заполнить(ДанныеЗаполнения);
		Док.ДополнительныеСвойства.Вставить("ИнициализироватьДанные", Истина);
		Док.Записать(РежимЗаписиДокумента.Запись);
		ЗаполнитьЗначенияПоказателейИПараметрыЛимитирования(Док.Ссылка);
		Док.Записать(РежимЗаписиДокумента.Проведение);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Создание документа установки лимитов'", ОбщегоНазначения.КодОсновногоЯзыка()),
        	УровеньЖурналаРегистрации.Ошибка,
        	,
        	,
        	ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ВызватьИсключение;
			
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

Процедура ЗаполнитьЗначенияПоказателейИПараметрыЛимитирования(ДокументСсылка) Экспорт
	
	//
	Инфо = ПолучитьДанныеДляУстановкиЛимитов(ДокументСсылка);
	
	//
	НаборЗаписей = РегистрыСведений.УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(ДокументСсылка);
	ЗначенияПоказателей = НаборЗаписей.ВыгрузитьКолонки();
	
	//
	ПоискПоказателей = Новый Структура("Предназначение, ВидОтчета, Область");
	ПоискПериодов = Новый Структура("ДокументБюджетирования");
	
	//
	РаскрываемыеАналитики = Новый Структура;
	Для Инд = 1 По ВстраиваниеОПКПереопределяемый.ПараметрСеанса_ЧислоДопАналитик() Цикл
		РаскрываемыеАналитики.Вставить("Аналитика"+Инд);
	КонецЦикла;	
	РаскрываемыеАналитики.Вставить("АналитикаВалюта");
	
	//
	СтруктураПараметров = Новый Структура("Организация, Проект, Сценарий, ВидОтчета");
	Для Каждого СтрокаДокумент Из Инфо.ДокументыБюджетирования Цикл
		
		ЗаполнитьЗначенияСвойств(ПоискПоказателей, СтрокаДокумент);
		ЗаполнитьЗначенияСвойств(ПоискПериодов, СтрокаДокумент);
		
		ПоказателиДокумента = Инфо.Показатели.Скопировать(ПоискПоказателей, "Показатель, ПоказательПометкаУдаления, ЕстьЗначениеВВалютеРаскрытия, СтатьяБюджета");
		Показатели = ПоказателиДокумента.ВыгрузитьКолонку("Показатель");
		Периоды = Инфо.ПериодыПланирования.Скопировать(ПоискПериодов, "ПериодПланирования").ВыгрузитьКолонку("ПериодПланирования");
		Если Показатели.Количество() = 0 ИЛИ Периоды.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		//
		ЗаполнитьЗначенияСвойств(СтруктураПараметров, СтрокаДокумент);
		СтруктураПараметров.Вставить("ПоказательОтчета", Показатели);
		СтруктураПараметров.Вставить("ПериодОтчета", Периоды);
		
		ТаблицаПоказателей = ВстраиваниеОПКПереопределяемый.ПолучитьЗначениеПоказателей(СтруктураПараметров, РаскрываемыеАналитики);
		ТаблицаПоказателей.Колонки.ПоказательОтчета.Имя = "Показатель";
		
		ТаблицаПоказателей = СкорректироватьТаблицуПоказателей(СтрокаДокумент, ТаблицаПоказателей);
		
		ДобавитьВТаблицу(ЗначенияПоказателей, СтрокаДокумент, ПоказателиДокумента, ТаблицаПоказателей);
		
	КонецЦикла;
	
	ЗначенияПоказателей.ЗаполнитьЗначения(ДокументСсылка, "Документ");
	
	НаборЗаписей.Загрузить(ЗначенияПоказателей);
	НаборЗаписей.Записать(Истина);
	
	// запись параметров лимитирования
	ПараметрыЛимитирования = ПолучитьПараметрыЛимитирования(ДокументСсылка);
	ПараметрыЛимитированияСтатей = ПолучитьПараметрыЛимитированияСтатей(ДокументСсылка);
	
	// Параметры лимитирования
	НаборЗаписей = РегистрыСведений.УстановкаЛимитовПоДаннымБюджетированияПараметрыЛимитирования.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(ДокументСсылка);
	НаборЗаписей.Загрузить(ПараметрыЛимитирования);
	НаборЗаписей.Записать(Истина);
	
	// Статьи
	НаборЗаписей = РегистрыСведений.УстановкаЛимитовПоДаннымБюджетированияПараметрыЛимитированияСтатейБюджетов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(ДокументСсылка);
	НаборЗаписей.Загрузить(ПараметрыЛимитированияСтатей);
	НаборЗаписей.Записать(Истина);
	
	// Параметры бюджетирования
	ПараметрыБюджетирования = ПолучитьПараметрыБюджетирования(ДокументСсылка);
	НаборЗаписей = РегистрыСведений.УстановкаЛимитовПоДаннымБюджетированияПараметрыБюджетирования.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(ДокументСсылка);
	НаборЗаписей.Загрузить(ПараметрыБюджетирования);
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	СтруктураРеквизиты = ОбщегоНазначения.СкопироватьРекурсивно(Запрос.Параметры);
	ДополнительныеСвойства.Вставить("ПараметрыПроведения", Новый Структура("Реквизиты", СтруктураРеквизиты));
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	// Текст запроса, формирующего таблицы движений
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаЛимитыПоБюджетам(Запрос, ТекстыЗапроса);
	ТекстЗапросаСостояния(Запрос, ТекстыЗапроса);
	ТекстЗапросаСостояниеПериодовЛимитирования(Запрос, ТекстыЗапроса);
	ТекстЗапросаАктуальность(Запрос, ТекстыЗапроса);
	
	// Выполнение запроса и выгрузка полученных таблиц для формирования движений
	ПроведениеСерверОПК.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ОтразитьЛимитыПоБюджетам(ДополнительныеСвойства, Движения, Отказ) Экспорт
	
	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЛимитыПоБюджетам;
	
	Если Отказ Или Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Движения.ЛимитыПоБюджетам.Записывать = Истина;
	Движения.ЛимитыПоБюджетам.Загрузить(Таблица);
	
КонецПроцедуры

Процедура ОтразитьСостояниеУстановки(ДополнительныеСвойства, Движения, Отказ) Экспорт
	
	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаУстановкаЛимитовПоДаннымБюджетированияСостояния;
	
	Если Отказ Или Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Движения.УстановкаЛимитовПоДаннымБюджетированияСостояния.Записывать = Истина;
	Движения.УстановкаЛимитовПоДаннымБюджетированияСостояния.Загрузить(Таблица);
	
КонецПроцедуры

Процедура ОтразитьСостояниеПериодовЛимитирования(ДополнительныеСвойства, Движения, Отказ) Экспорт
	
	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаСостояниеПериодовЛимитирования;
	
	Если Отказ Или Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Движения.СостояниеПериодовЛимитирования.Записывать = Истина;
	Движения.СостояниеПериодовЛимитирования.Загрузить(Таблица);
	
КонецПроцедуры

Функция ПоследующиеДокументы(ДокументСсылка) Экспорт
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УстановкаЛимитовПоДаннымБюджетированияСостояния.Период КАК Период,
	|	УстановкаЛимитовПоДаннымБюджетированияСостояния.Регистратор КАК Регистратор,
	|	УстановкаЛимитовПоДаннымБюджетированияСостояния.НомерСтроки КАК НомерСтроки,
	|	УстановкаЛимитовПоДаннымБюджетированияСостояния.Активность КАК Активность,
	|	УстановкаЛимитовПоДаннымБюджетированияСостояния.ВидБюджета КАК ВидБюджета,
	|	УстановкаЛимитовПоДаннымБюджетированияСостояния.ЦФО КАК ЦФО,
	|	УстановкаЛимитовПоДаннымБюджетированияСостояния.Проект КАК Проект,
	|	УстановкаЛимитовПоДаннымБюджетированияСостояния.ПериодЛимитирования КАК ПериодЛимитирования,
	|	УстановкаЛимитовПоДаннымБюджетированияСостояния.Состояние КАК Состояние,
	|	УстановкаЛимитовПоДаннымБюджетированияСостояния.УОП КАК УОП,
	|	УстановкаЛимитовПоДаннымБюджетированияСостояния.Сценарий КАК Сценарий,
	|	УстановкаЛимитовПоДаннымБюджетированияСостояния.ВидОперации КАК ВидОперации,
	|	УстановкаЛимитовПоДаннымБюджетированияСостояния.МоментВремени КАК МоментВремени
	|ПОМЕСТИТЬ ВТ_ТекущийДокумент
	|ИЗ
	|	РегистрСведений.УстановкаЛимитовПоДаннымБюджетированияСостояния КАК УстановкаЛимитовПоДаннымБюджетированияСостояния
	|ГДЕ
	|	УстановкаЛимитовПоДаннымБюджетированияСостояния.Регистратор = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	УстановкаЛимитовПоДаннымБюджетированияСостояния.Регистратор КАК Регистратор,
	|	УстановкаЛимитовПоДаннымБюджетированияСостояния.МоментВремени КАК МоментВремени
	|ИЗ
	|	ВТ_ТекущийДокумент КАК ВТ_ТекущийДокумент
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УстановкаЛимитовПоДаннымБюджетированияСостояния КАК УстановкаЛимитовПоДаннымБюджетированияСостояния
	|		ПО ВТ_ТекущийДокумент.ВидБюджета = УстановкаЛимитовПоДаннымБюджетированияСостояния.ВидБюджета
	|			И ВТ_ТекущийДокумент.ЦФО = УстановкаЛимитовПоДаннымБюджетированияСостояния.ЦФО
	|			И ВТ_ТекущийДокумент.Проект = УстановкаЛимитовПоДаннымБюджетированияСостояния.Проект
	|			И ВТ_ТекущийДокумент.ПериодЛимитирования = УстановкаЛимитовПоДаннымБюджетированияСостояния.ПериодЛимитирования
	|ГДЕ
	|	УстановкаЛимитовПоДаннымБюджетированияСостояния.МоментВремени > ВТ_ТекущийДокумент.МоментВремени
	|
	|УПОРЯДОЧИТЬ ПО
	|	МоментВремени";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДанныеДляУстановкиЛимитов(ДокументСсылка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	УправлениеЛимитамиДанные.ВидБюджета КАК ВидБюджета,
	|	УправлениеЛимитамиДанные.ЦФО КАК ЦФО,
	|	УправлениеЛимитамиДанные.Проект КАК Проект,
	|	УправлениеЛимитамиДанные.ПериодЛимитирования КАК ПериодЛимитирования,
	|	УправлениеЛимитамиДанные.УОП КАК УОП
	|ПОМЕСТИТЬ ВТ_ДанныеДокумента
	|ИЗ
	|	Документ.УправлениеЛимитами.Данные КАК УправлениеЛимитамиДанные
	|ГДЕ
	|	УправлениеЛимитамиДанные.Ссылка = &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.Периодичность.Год) КАК ПериодичностьЛимитирования,
	|	ЗНАЧЕНИЕ(Перечисление.Периодичность.Год) КАК ПериодичностьПланирования
	|ПОМЕСТИТЬ ВТ_ДопустимаяПериодичностьЛимитирования
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.Периодичность.Год),
	|	ЗНАЧЕНИЕ(Перечисление.Периодичность.Полугодие)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.Периодичность.Год),
	|	ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.Периодичность.Год),
	|	ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.Периодичность.Полугодие),
	|	ЗНАЧЕНИЕ(Перечисление.Периодичность.Полугодие)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.Периодичность.Полугодие),
	|	ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.Периодичность.Полугодие),
	|	ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал),
	|	ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал),
	|	ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц),
	|	ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДокумента.ВидБюджета КАК ВидБюджета,
	|	ВЫРАЗИТЬ(ВТ_ДанныеДокумента.ВидБюджета КАК ПланВидовХарактеристик.ВидыБюджетов).Предназначение КАК Предназначение,
	|	ВТ_ДанныеДокумента.ЦФО КАК ЦФО,
	|	ВТ_ДанныеДокумента.Проект КАК Проект,
	|	ВТ_ДанныеДокумента.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ВТ_ДанныеДокумента.ПериодЛимитирования.ДатаНачала КАК ПериодЛимитированияДатаНачала,
	|	ВТ_ДанныеДокумента.ПериодЛимитирования.ДатаОкончания КАК ПериодЛимитированияДатаОкончания,
	|	ВТ_ДанныеДокумента.ПериодЛимитирования.Периодичность КАК ПериодичностьЛимитирования,
	|	ВТ_ДопустимаяПериодичностьЛимитирования.ПериодичностьПланирования КАК ПериодичностьПланирования,
	|	ВТ_ДанныеДокумента.УОП КАК УОП
	|ПОМЕСТИТЬ ВТ_ДанныеКУстановкеСПериодичностьюПланирования
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДопустимаяПериодичностьЛимитирования КАК ВТ_ДопустимаяПериодичностьЛимитирования
	|		ПО ВТ_ДанныеДокумента.ПериодЛимитирования.Периодичность = ВТ_ДопустимаяПериодичностьЛимитирования.ПериодичностьЛимитирования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УправлениеПериодомСценарияВидыОтчетовДляУстановкиЛимитов.Ссылка КАК УОП,
	|	УправлениеПериодомСценарияВидыОтчетовДляУстановкиЛимитов.Ссылка.Сценарий КАК Сценарий,
	|	УправлениеПериодомСценарияВидыОтчетовДляУстановкиЛимитов.Ссылка.ТекущаяПериодичность КАК Периодичность,
	|	УправлениеПериодомСценарияВидыОтчетовДляУстановкиЛимитов.Ссылка.ПериодСценария.ДатаНачала КАК УОПДатаНачала,
	|	УправлениеПериодомСценарияВидыОтчетовДляУстановкиЛимитов.Ссылка.ПериодСценарияОкончание.ДатаОкончания КАК УОПДатаОкончания,
	|	УправлениеПериодомСценарияВидыОтчетовДляУстановкиЛимитов.ВидОтчета КАК ВидОтчета,
	|	УправлениеПериодомСценарияВидыОтчетовДляУстановкиЛимитов.ВидОтчета.ОбъектДляСогласованияЗначений КАК ОбъектДляСогласованияЗначений,
	|	ВидыОтчетовПараметрыУстановкиЛимитов.Предназначение КАК Предназначение,
	|	ВидыОтчетовПараметрыУстановкиЛимитов.КолонкаОтчета КАК КолонкаОтчета
	|ПОМЕСТИТЬ ВТ_УОПВидыОтчета
	|ИЗ
	|	Документ.УправлениеПериодомСценария.ВидыОтчетовДляУстановкиЛимитов КАК УправлениеПериодомСценарияВидыОтчетовДляУстановкиЛимитов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыОтчетов.ПараметрыУстановкиЛимитов КАК ВидыОтчетовПараметрыУстановкиЛимитов
	|		ПО УправлениеПериодомСценарияВидыОтчетовДляУстановкиЛимитов.ВидОтчета = ВидыОтчетовПараметрыУстановкиЛимитов.Ссылка
	|ГДЕ
	|	УправлениеПериодомСценарияВидыОтчетовДляУстановкиЛимитов.Ссылка.УстанавливатьЛимиты = ИСТИНА
	|	И УправлениеПериодомСценарияВидыОтчетовДляУстановкиЛимитов.Ссылка В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ВТ_ДанныеДокумента.УОП КАК УОП
	|			ИЗ
	|				ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеКУстановкеСПериодичностьюПланирования.ВидБюджета КАК ВидБюджета,
	|	ВТ_УОПВидыОтчета.Предназначение КАК Предназначение,
	|	ВТ_ДанныеКУстановкеСПериодичностьюПланирования.ЦФО КАК ЦФО,
	|	ВТ_ДанныеКУстановкеСПериодичностьюПланирования.Проект КАК Проект,
	|	ВТ_ДанныеКУстановкеСПериодичностьюПланирования.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ВТ_ДанныеКУстановкеСПериодичностьюПланирования.ПериодЛимитированияДатаНачала КАК ПериодЛимитированияДатаНачала,
	|	ВТ_ДанныеКУстановкеСПериодичностьюПланирования.ПериодЛимитированияДатаОкончания КАК ПериодЛимитированияДатаОкончания,
	|	ВТ_ДанныеКУстановкеСПериодичностьюПланирования.ПериодичностьЛимитирования КАК ПериодичностьЛимитирования,
	|	ВТ_ДанныеКУстановкеСПериодичностьюПланирования.ПериодичностьПланирования КАК ПериодичностьПланирования,
	|	ВТ_ДанныеКУстановкеСПериодичностьюПланирования.УОП КАК УОП,
	|	ВТ_УОПВидыОтчета.Сценарий КАК Сценарий,
	|	ВТ_УОПВидыОтчета.ВидОтчета КАК ВидОтчета,
	|	ВТ_УОПВидыОтчета.УОПДатаНачала КАК УОПДатаНачала,
	|	ВТ_УОПВидыОтчета.УОПДатаОкончания КАК УОПДатаОкончания,
	|	ВТ_УОПВидыОтчета.КолонкаОтчета КАК КолонкаДляУстановкиЛимита,
	|	ВТ_УОПВидыОтчета.ОбъектДляСогласованияЗначений КАК ОбъектДляСогласованияЗначений
	|ПОМЕСТИТЬ ВТ_ДанныеКУстановкеДаннымиУОПВидаОтчета
	|ИЗ
	|	ВТ_ДанныеКУстановкеСПериодичностьюПланирования КАК ВТ_ДанныеКУстановкеСПериодичностьюПланирования
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_УОПВидыОтчета КАК ВТ_УОПВидыОтчета
	|		ПО ВТ_ДанныеКУстановкеСПериодичностьюПланирования.УОП = ВТ_УОПВидыОтчета.УОП
	|			И ВТ_ДанныеКУстановкеСПериодичностьюПланирования.ПериодичностьПланирования = ВТ_УОПВидыОтчета.Периодичность
	|			И ВТ_ДанныеКУстановкеСПериодичностьюПланирования.Предназначение = ВТ_УОПВидыОтчета.Предназначение
	|			И (ВТ_ДанныеКУстановкеСПериодичностьюПланирования.ПериодЛимитированияДатаНачала МЕЖДУ ВТ_УОПВидыОтчета.УОПДатаНачала И ВТ_УОПВидыОтчета.УОПДатаОкончания)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеКУстановкеДаннымиУОПВидаОтчета.ВидБюджета КАК ВидБюджета,
	|	ВТ_ДанныеКУстановкеДаннымиУОПВидаОтчета.Предназначение КАК Предназначение,
	|	ВТ_ДанныеКУстановкеДаннымиУОПВидаОтчета.ЦФО КАК ЦФО,
	|	ВТ_ДанныеКУстановкеДаннымиУОПВидаОтчета.Проект КАК Проект,
	|	ВТ_ДанныеКУстановкеДаннымиУОПВидаОтчета.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ВТ_ДанныеКУстановкеДаннымиУОПВидаОтчета.УОП КАК УОП,
	|	ВТ_ДанныеКУстановкеДаннымиУОПВидаОтчета.Сценарий КАК Сценарий,
	|	ВТ_ДанныеКУстановкеДаннымиУОПВидаОтчета.УОПДатаНачала КАК УОПДатаНачала,
	|	ВТ_ДанныеКУстановкеДаннымиУОПВидаОтчета.УОПДатаОкончания КАК УОПДатаОкончания,
	|	ВТ_ДанныеКУстановкеДаннымиУОПВидаОтчета.ВидОтчета КАК ВидОтчета,
	|	Периоды.Ссылка КАК ПериодПланирования,
	|	ВТ_ДанныеКУстановкеДаннымиУОПВидаОтчета.ОбъектДляСогласованияЗначений КАК ОбъектДляСогласованияЗначений,
	|	ВТ_ДанныеКУстановкеДаннымиУОПВидаОтчета.КолонкаДляУстановкиЛимита КАК КолонкаДляУстановкиЛимита
	|ПОМЕСТИТЬ ВТ_ДанныеКУстановкеСПериодомПланирования
	|ИЗ
	|	ВТ_ДанныеКУстановкеДаннымиУОПВидаОтчета КАК ВТ_ДанныеКУстановкеДаннымиУОПВидаОтчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Периоды КАК Периоды
	|		ПО ВТ_ДанныеКУстановкеДаннымиУОПВидаОтчета.ПериодичностьПланирования = Периоды.Периодичность
	|			И (Периоды.Произвольный = ЛОЖЬ)
	|			И (Периоды.ДатаНачала МЕЖДУ ВТ_ДанныеКУстановкеДаннымиУОПВидаОтчета.ПериодЛимитированияДатаНачала И ВТ_ДанныеКУстановкеДаннымиУОПВидаОтчета.ПериодЛимитированияДатаОкончания)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеКУстановкеСПериодомПланирования.ВидБюджета КАК ВидБюджета,
	|	ВТ_ДанныеКУстановкеСПериодомПланирования.Предназначение КАК Предназначение,
	|	ВТ_ДанныеКУстановкеСПериодомПланирования.ЦФО КАК ЦФО,
	|	ВТ_ДанныеКУстановкеСПериодомПланирования.Проект КАК Проект,
	|	ВТ_ДанныеКУстановкеСПериодомПланирования.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ВТ_ДанныеКУстановкеСПериодомПланирования.УОП КАК УОП,
	|	ВТ_ДанныеКУстановкеСПериодомПланирования.Сценарий КАК Сценарий,
	|	ВТ_ДанныеКУстановкеСПериодомПланирования.ВидОтчета КАК ВидОтчета,
	|	ЗНАЧЕНИЕ(Справочник.ОбластиДанныхВидовОтчетов.ПустаяСсылка) КАК Область,
	|	ВТ_ДанныеКУстановкеСПериодомПланирования.ПериодПланирования КАК ПериодПланирования,
	|	НастраиваемыйОтчет.Ссылка КАК ДокументБюджетирования,
	|	ВТ_ДанныеКУстановкеСПериодомПланирования.КолонкаДляУстановкиЛимита КАК КолонкаДляУстановкиЛимита,
	|	ВТ_ДанныеКУстановкеСПериодомПланирования.ОбъектДляСогласованияЗначений КАК ОбъектДляСогласованияЗначений
	|ПОМЕСТИТЬ ВТ_Данные
	|ИЗ
	|	ВТ_ДанныеКУстановкеСПериодомПланирования КАК ВТ_ДанныеКУстановкеСПериодомПланирования
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НастраиваемыйОтчет КАК НастраиваемыйОтчет
	|		ПО ВТ_ДанныеКУстановкеСПериодомПланирования.УОП = НастраиваемыйОтчет.УправлениеПериодом
	|			И (ВТ_ДанныеКУстановкеСПериодомПланирования.ОбъектДляСогласованияЗначений = ЗНАЧЕНИЕ(Перечисление.ОбъектыСогласованияЗначенийПоказателейОтчетов.ЭкземплярОтчета))
	|			И (НастраиваемыйОтчет.ПометкаУдаления = ЛОЖЬ)
	|			И ВТ_ДанныеКУстановкеСПериодомПланирования.ЦФО = НастраиваемыйОтчет.Организация
	|			И ВТ_ДанныеКУстановкеСПериодомПланирования.Проект = НастраиваемыйОтчет.Проект
	|			И ВТ_ДанныеКУстановкеСПериодомПланирования.ВидОтчета = НастраиваемыйОтчет.ВидОтчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ДанныеКУстановкеСПериодомПланирования.ВидБюджета,
	|	ВТ_ДанныеКУстановкеСПериодомПланирования.Предназначение,
	|	ВТ_ДанныеКУстановкеСПериодомПланирования.ЦФО,
	|	ВТ_ДанныеКУстановкеСПериодомПланирования.Проект,
	|	ВТ_ДанныеКУстановкеСПериодомПланирования.ПериодЛимитирования,
	|	ВТ_ДанныеКУстановкеСПериодомПланирования.УОП,
	|	ВТ_ДанныеКУстановкеСПериодомПланирования.Сценарий,
	|	ВТ_ДанныеКУстановкеСПериодомПланирования.ВидОтчета,
	|	РакурсДанных.Область,
	|	ВТ_ДанныеКУстановкеСПериодомПланирования.ПериодПланирования,
	|	РакурсДанных.Ссылка,
	|	ВТ_ДанныеКУстановкеСПериодомПланирования.КолонкаДляУстановкиЛимита,
	|	ВТ_ДанныеКУстановкеСПериодомПланирования.ОбъектДляСогласованияЗначений
	|ИЗ
	|	ВТ_ДанныеКУстановкеСПериодомПланирования КАК ВТ_ДанныеКУстановкеСПериодомПланирования
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РакурсДанных КАК РакурсДанных
	|		ПО ВТ_ДанныеКУстановкеСПериодомПланирования.ВидОтчета = РакурсДанных.Область.Владелец
	|			И (РакурсДанных.ПометкаУдаления = ЛОЖЬ)
	|			И ВТ_ДанныеКУстановкеСПериодомПланирования.УОПДатаНачала = РакурсДанных.ПериодНачалоДата
	|			И ВТ_ДанныеКУстановкеСПериодомПланирования.УОПДатаОкончания = РакурсДанных.ПериодОкончаниеДата
	|			И ВТ_ДанныеКУстановкеСПериодомПланирования.ЦФО = РакурсДанных.Организация
	|			И ВТ_ДанныеКУстановкеСПериодомПланирования.Проект = РакурсДанных.Проект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_Данные.ВидБюджета КАК ВидБюджета,
	|	ВТ_Данные.Предназначение КАК Предназначение,
	|	ВТ_Данные.ВидОтчета КАК ВидОтчета,
	|	ВТ_Данные.Область КАК Область,
	|	ВТ_Данные.КолонкаДляУстановкиЛимита КАК КолонкаДляУстановкиЛимита,
	|	ВТ_Данные.ОбъектДляСогласованияЗначений КАК ОбъектДляСогласованияЗначений
	|ПОМЕСТИТЬ ВТ_ВидыОтчетовВидыБюджетов
	|ИЗ
	|	ВТ_Данные КАК ВТ_Данные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВидыОтчетовВидыБюджетов.ВидБюджета КАК ВидБюджета,
	|	ВТ_ВидыОтчетовВидыБюджетов.Предназначение КАК Предназначение,
	|	ВТ_ВидыОтчетовВидыБюджетов.ВидОтчета КАК ВидОтчета,
	|	ВТ_ВидыОтчетовВидыБюджетов.Область КАК Область,
	|	ВТ_ВидыОтчетовВидыБюджетов.КолонкаДляУстановкиЛимита КАК КолонкаДляУстановкиЛимита,
	|	ВТ_ВидыОтчетовВидыБюджетов.ОбъектДляСогласованияЗначений КАК ОбъектДляСогласованияЗначений,
	|	СтрокиОтчетов.Ссылка КАК СтрокаОтчета,
	|	ВЫБОР СтрокиОтчетов.Предназначение
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ПредназначенияЭлементовСтруктурыОтчета.БюджетДвиженияДенежныхСредств)
	|			ТОГДА СтрокиОтчетов.СтатьяДвиженияДенежныхСредств
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ПредназначенияЭлементовСтруктурыОтчета.БюджетДоходовИРасходов)
	|			ТОГДА СтрокиОтчетов.СтатьяДоходовИРасходов
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ПредназначенияЭлементовСтруктурыОтчета.БюджетДвиженияРесурсов)
	|			ТОГДА СтрокиОтчетов.СтатьяДвиженияРесурсов
	|	КОНЕЦ КАК СтатьяБюджета
	|ПОМЕСТИТЬ ВТ_СтрокаОтчетов
	|ИЗ
	|	ВТ_ВидыОтчетовВидыБюджетов КАК ВТ_ВидыОтчетовВидыБюджетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтрокиОтчетов КАК СтрокиОтчетов
	|		ПО ВТ_ВидыОтчетовВидыБюджетов.ВидОтчета = СтрокиОтчетов.Владелец
	|			И ВТ_ВидыОтчетовВидыБюджетов.Предназначение = СтрокиОтчетов.Предназначение
	|ГДЕ
	|	ВТ_ВидыОтчетовВидыБюджетов.ОбъектДляСогласованияЗначений = ЗНАЧЕНИЕ(Перечисление.ОбъектыСогласованияЗначенийПоказателейОтчетов.ЭкземплярОтчета)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ВидыОтчетовВидыБюджетов.ВидБюджета,
	|	ВТ_ВидыОтчетовВидыБюджетов.Предназначение,
	|	ВТ_ВидыОтчетовВидыБюджетов.ВидОтчета,
	|	ВТ_ВидыОтчетовВидыБюджетов.Область,
	|	ВТ_ВидыОтчетовВидыБюджетов.КолонкаДляУстановкиЛимита,
	|	ВТ_ВидыОтчетовВидыБюджетов.ОбъектДляСогласованияЗначений,
	|	СтрокиОтчетов.Ссылка,
	|	ВЫБОР СтрокиОтчетов.Предназначение
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ПредназначенияЭлементовСтруктурыОтчета.БюджетДвиженияДенежныхСредств)
	|			ТОГДА СтрокиОтчетов.СтатьяДвиженияДенежныхСредств
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ПредназначенияЭлементовСтруктурыОтчета.БюджетДоходовИРасходов)
	|			ТОГДА СтрокиОтчетов.СтатьяДоходовИРасходов
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ПредназначенияЭлементовСтруктурыОтчета.БюджетДвиженияРесурсов)
	|			ТОГДА СтрокиОтчетов.СтатьяДвиженияРесурсов
	|	КОНЕЦ
	|ИЗ
	|	ВТ_ВидыОтчетовВидыБюджетов КАК ВТ_ВидыОтчетовВидыБюджетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбластиДанныхВидовОтчетов КАК ОбластиДанныхВидовОтчетов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтрокиОтчетов КАК СтрокиОтчетов
	|			ПО ОбластиДанныхВидовОтчетов.Владелец = СтрокиОтчетов.Владелец
	|		ПО ВТ_ВидыОтчетовВидыБюджетов.ВидОтчета = ОбластиДанныхВидовОтчетов.Владелец
	|			И ВТ_ВидыОтчетовВидыБюджетов.Область = ОбластиДанныхВидовОтчетов.Ссылка
	|			И (ОбластиДанныхВидовОтчетов.ВключатьВсеПоказателиВидаОтчета = ИСТИНА)
	|ГДЕ
	|	ВТ_ВидыОтчетовВидыБюджетов.ОбъектДляСогласованияЗначений = ЗНАЧЕНИЕ(Перечисление.ОбъектыСогласованияЗначенийПоказателейОтчетов.Ракурс)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ВидыОтчетовВидыБюджетов.ВидБюджета,
	|	ВТ_ВидыОтчетовВидыБюджетов.Предназначение,
	|	ВТ_ВидыОтчетовВидыБюджетов.ВидОтчета,
	|	ВТ_ВидыОтчетовВидыБюджетов.Область,
	|	ВТ_ВидыОтчетовВидыБюджетов.КолонкаДляУстановкиЛимита,
	|	ВТ_ВидыОтчетовВидыБюджетов.ОбъектДляСогласованияЗначений,
	|	НастройкиСоставаОбластейВидовОтчетов.СтрокаОтчета,
	|	ВЫБОР НастройкиСоставаОбластейВидовОтчетов.СтрокаОтчета.Предназначение
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ПредназначенияЭлементовСтруктурыОтчета.БюджетДвиженияДенежныхСредств)
	|			ТОГДА НастройкиСоставаОбластейВидовОтчетов.СтрокаОтчета.СтатьяДвиженияДенежныхСредств
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ПредназначенияЭлементовСтруктурыОтчета.БюджетДоходовИРасходов)
	|			ТОГДА НастройкиСоставаОбластейВидовОтчетов.СтрокаОтчета.СтатьяДоходовИРасходов
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ПредназначенияЭлементовСтруктурыОтчета.БюджетДвиженияРесурсов)
	|			ТОГДА НастройкиСоставаОбластейВидовОтчетов.СтрокаОтчета.СтатьяДвиженияРесурсов
	|	КОНЕЦ
	|ИЗ
	|	ВТ_ВидыОтчетовВидыБюджетов КАК ВТ_ВидыОтчетовВидыБюджетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСоставаОбластейВидовОтчетов КАК НастройкиСоставаОбластейВидовОтчетов
	|		ПО ВТ_ВидыОтчетовВидыБюджетов.Область = НастройкиСоставаОбластейВидовОтчетов.КлючОбласти
	|			И (ВТ_ВидыОтчетовВидыБюджетов.Область.ВключатьВсеПоказателиВидаОтчета = ЛОЖЬ)
	|			И (НастройкиСоставаОбластейВидовОтчетов.КлючОбласти.Владелец ССЫЛКА Справочник.ВидыОтчетов)
	|			И (ВТ_ВидыОтчетовВидыБюджетов.ВидОтчета = (ВЫРАЗИТЬ(НастройкиСоставаОбластейВидовОтчетов.КлючОбласти.Владелец КАК Справочник.ВидыОтчетов)))
	|ГДЕ
	|	ВТ_ВидыОтчетовВидыБюджетов.ОбъектДляСогласованияЗначений = ЗНАЧЕНИЕ(Перечисление.ОбъектыСогласованияЗначенийПоказателейОтчетов.Ракурс)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_Данные.ВидБюджета КАК ВидБюджета,
	|	ВТ_Данные.Предназначение КАК Предназначение,
	|	ВТ_Данные.ЦФО КАК Организация,
	|	ВТ_Данные.Проект КАК Проект,
	|	ВТ_Данные.УОП КАК УОП,
	|	ВТ_Данные.Сценарий КАК Сценарий,
	|	ВТ_Данные.ВидОтчета КАК ВидОтчета,
	|	ВТ_Данные.Область КАК Область,
	|	ВТ_Данные.ДокументБюджетирования КАК ДокументБюджетирования,
	|	ВЫБОР
	|		КОГДА ВТ_Данные.ДокументБюджетирования ССЫЛКА Документ.НастраиваемыйОтчет
	|			ТОГДА ВТ_Данные.ВидОтчета.ВидАналитики1
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоКорпоративные.ПустаяСсылка)
	|	КОНЕЦ КАК ВидАналитики1,
	|	ВЫБОР
	|		КОГДА ВТ_Данные.ДокументБюджетирования ССЫЛКА Документ.НастраиваемыйОтчет
	|			ТОГДА ВТ_Данные.ВидОтчета.ВидАналитики2
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоКорпоративные.ПустаяСсылка)
	|	КОНЕЦ КАК ВидАналитики2,
	|	ВЫБОР
	|		КОГДА ВТ_Данные.ДокументБюджетирования ССЫЛКА Документ.НастраиваемыйОтчет
	|			ТОГДА ВТ_Данные.ВидОтчета.ВидАналитики3
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоКорпоративные.ПустаяСсылка)
	|	КОНЕЦ КАК ВидАналитики3,
	|	ВЫБОР
	|		КОГДА ВТ_Данные.ДокументБюджетирования ССЫЛКА Документ.НастраиваемыйОтчет
	|			ТОГДА ВТ_Данные.ВидОтчета.ВидАналитики4
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоКорпоративные.ПустаяСсылка)
	|	КОНЕЦ КАК ВидАналитики4,
	|	ВЫБОР
	|		КОГДА ВТ_Данные.ДокументБюджетирования ССЫЛКА Документ.НастраиваемыйОтчет
	|			ТОГДА ВТ_Данные.ВидОтчета.ВидАналитики5
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоКорпоративные.ПустаяСсылка)
	|	КОНЕЦ КАК ВидАналитики5,
	|	ВЫБОР
	|		КОГДА ВТ_Данные.ДокументБюджетирования ССЫЛКА Документ.НастраиваемыйОтчет
	|			ТОГДА ВТ_Данные.ВидОтчета.ВидАналитики6
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоКорпоративные.ПустаяСсылка)
	|	КОНЕЦ КАК ВидАналитики6,
	|	ВЫБОР
	|		КОГДА ВТ_Данные.ДокументБюджетирования ССЫЛКА Документ.НастраиваемыйОтчет
	|			ТОГДА ВЫРАЗИТЬ(ВТ_Данные.ДокументБюджетирования КАК Документ.НастраиваемыйОтчет).Аналитика1
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Аналитика1,
	|	ВЫБОР
	|		КОГДА ВТ_Данные.ДокументБюджетирования ССЫЛКА Документ.НастраиваемыйОтчет
	|			ТОГДА ВЫРАЗИТЬ(ВТ_Данные.ДокументБюджетирования КАК Документ.НастраиваемыйОтчет).Аналитика2
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Аналитика2,
	|	ВЫБОР
	|		КОГДА ВТ_Данные.ДокументБюджетирования ССЫЛКА Документ.НастраиваемыйОтчет
	|			ТОГДА ВЫРАЗИТЬ(ВТ_Данные.ДокументБюджетирования КАК Документ.НастраиваемыйОтчет).Аналитика3
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Аналитика3,
	|	ВЫБОР
	|		КОГДА ВТ_Данные.ДокументБюджетирования ССЫЛКА Документ.НастраиваемыйОтчет
	|			ТОГДА ВЫРАЗИТЬ(ВТ_Данные.ДокументБюджетирования КАК Документ.НастраиваемыйОтчет).Аналитика4
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Аналитика4,
	|	ВЫБОР
	|		КОГДА ВТ_Данные.ДокументБюджетирования ССЫЛКА Документ.НастраиваемыйОтчет
	|			ТОГДА ВЫРАЗИТЬ(ВТ_Данные.ДокументБюджетирования КАК Документ.НастраиваемыйОтчет).Аналитика5
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Аналитика5,
	|	ВЫБОР
	|		КОГДА ВТ_Данные.ДокументБюджетирования ССЫЛКА Документ.НастраиваемыйОтчет
	|			ТОГДА ВЫРАЗИТЬ(ВТ_Данные.ДокументБюджетирования КАК Документ.НастраиваемыйОтчет).Аналитика6
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Аналитика6
	|ИЗ
	|	ВТ_Данные КАК ВТ_Данные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Данные.ДокументБюджетирования КАК ДокументБюджетирования,
	|	ВТ_Данные.ПериодПланирования КАК ПериодПланирования,
	|	ВТ_Данные.ПериодЛимитирования КАК ПериодЛимитирования
	|ИЗ
	|	ВТ_Данные КАК ВТ_Данные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СтрокаОтчетов.ВидБюджета КАК ВидБюджета,
	|	ВТ_СтрокаОтчетов.Предназначение КАК Предназначение,
	|	ВТ_СтрокаОтчетов.ВидОтчета КАК ВидОтчета,
	|	ВТ_СтрокаОтчетов.Область КАК Область,
	|	ВТ_СтрокаОтчетов.КолонкаДляУстановкиЛимита КАК КолонкаДляУстановкиЛимита,
	|	ВТ_СтрокаОтчетов.ОбъектДляСогласованияЗначений КАК ОбъектДляСогласованияЗначений,
	|	ВТ_СтрокаОтчетов.СтрокаОтчета КАК СтрокаОтчета,
	|	ВТ_СтрокаОтчетов.СтатьяБюджета КАК СтатьяБюджета,
	|	ПоказателиОтчетов.Ссылка КАК Показатель,
	|	ПоказателиОтчетов.ПометкаУдаления КАК ПоказательПометкаУдаления,
	|	ПоказателиОтчетов.ПересчитыватьВалютнуюСумму КАК ЕстьЗначениеВВалютеРаскрытия
	|ИЗ
	|	ВТ_СтрокаОтчетов КАК ВТ_СтрокаОтчетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПоказателиОтчетов КАК ПоказателиОтчетов
	|		ПО ВТ_СтрокаОтчетов.СтрокаОтчета = ПоказателиОтчетов.Строка
	|			И ВТ_СтрокаОтчетов.КолонкаДляУстановкиЛимита = ПоказателиОтчетов.Колонка";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Всего = РезультатЗапроса.Количество();
	
	Результат = Новый Структура("ДокументыБюджетирования, ПериодыПланирования, Показатели");
	Результат.Вставить("ДокументыБюджетирования", РезультатЗапроса[Всего-1-2].Выгрузить());
	Результат.Вставить("ПериодыПланирования", РезультатЗапроса[Всего-1-1].Выгрузить());
	Результат.Вставить("Показатели", РезультатЗапроса[Всего-1-0].Выгрузить());
	
	// Индексы, для ускорения поиска
	Результат.ПериодыПланирования.Индексы.Добавить("ДокументБюджетирования");
	Результат.Показатели.Индексы.Добавить("Предназначение, ВидОтчета, Область");
	
	Возврат Результат;
	
КонецФункции

Процедура ДобавитьВТаблицу(Получатель, СтрокаДокумент, ПоказателиДокумента, КДобавлению)
	
	ИзСтрокиДокумента = "ВидБюджета, Предназначение, ВидОтчета, Область, ДокументБюджетирования, УОП";
	
	ИзПоказателяДокумента = "ПоказательПометкаУдаления, ЕстьЗначениеВВалютеРаскрытия, СтатьяБюджета";
	
	Показатели = Новый Соответствие;
	Для Каждого СтрокаПоказатель Из ПоказателиДокумента Цикл
		ДанныеПоказателя = Новый Структура(ИзПоказателяДокумента);
		ЗаполнитьЗначенияСвойств(ДанныеПоказателя, СтрокаПоказатель);
		Показатели.Вставить(СтрокаПоказатель.Показатель, ДанныеПоказателя);
	КонецЦикла;
	
	Для Каждого Строка Из КДобавлению Цикл
		СтрокаПолучателя = Получатель.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПолучателя, Строка);
		ЗаполнитьЗначенияСвойств(СтрокаПолучателя, СтрокаДокумент, ИзСтрокиДокумента);
		
		ДанныеПоказателя = Показатели[СтрокаПолучателя.Показатель];
		ЗаполнитьЗначенияСвойств(СтрокаПолучателя, ДанныеПоказателя, ИзПоказателяДокумента);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьПараметрыЛимитирования(ДокументСсылка)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документ", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.Документ КАК Документ,
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.ВидБюджета КАК ВидБюджета,
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.Организация КАК ЦФО,
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.ПериодОтчета КАК ПериодОтчета,
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.ПериодОтчета.ДатаНачала КАК ПериодОтчетаДатаНачала
	|ПОМЕСТИТЬ ВТ_Данные
	|ИЗ
	|	РегистрСведений.УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей КАК УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей
	|ГДЕ
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.Документ = &Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПараметрыЛимитирования.Период КАК Период,
	|	ПараметрыЛимитирования.ВидБюджета КАК ВидБюджета,
	|	ПараметрыЛимитирования.ПериодичностьЛимитирования КАК ПериодичностьЛимитирования,
	|	ПараметрыЛимитирования.ИспользоватьЛимитирование КАК ИспользоватьЛимитирование,
	|	ПараметрыЛимитирования.СпособОпределенияВалютыЛимитирования КАК СпособОпределенияВалютыЛимитирования
	|ПОМЕСТИТЬ ВТ_ПараметрыЛимитированияРегистр
	|ИЗ
	|	РегистрСведений.ПараметрыЛимитирования КАК ПараметрыЛимитирования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Данные.ВидБюджета КАК ВидБюджета,
	|	ВТ_Данные.ПериодОтчетаДатаНачала КАК ПериодОтчетаДатаНачала,
	|	МАКСИМУМ(ВТ_ПараметрыЛимитированияРегистр.Период) КАК Период
	|ПОМЕСТИТЬ ВТ_ПараметрыЛимитированияСвязьПериодов
	|ИЗ
	|	ВТ_Данные КАК ВТ_Данные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПараметрыЛимитированияРегистр КАК ВТ_ПараметрыЛимитированияРегистр
	|		ПО ВТ_Данные.ВидБюджета = ВТ_ПараметрыЛимитированияРегистр.ВидБюджета
	|			И ВТ_Данные.ПериодОтчетаДатаНачала >= ВТ_ПараметрыЛимитированияРегистр.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Данные.ВидБюджета,
	|	ВТ_Данные.ПериодОтчетаДатаНачала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Данные.Документ КАК Документ,
	|	ВТ_Данные.ВидБюджета КАК ВидБюджета,
	|	ВТ_Данные.ЦФО КАК ЦФО,
	|	ВТ_Данные.ПериодОтчета КАК ПериодОтчета,
	|	ВТ_Данные.ПериодОтчетаДатаНачала КАК ПериодОтчетаДатаНачала,
	|	ВТ_ПараметрыЛимитированияСвязьПериодов.Период КАК Период,
	|	ВТ_ПараметрыЛимитированияРегистр.ИспользоватьЛимитирование КАК ИспользоватьЛимитирование,
	|	ВТ_ПараметрыЛимитированияРегистр.ПериодичностьЛимитирования КАК ПериодичностьЛимитирования,
	|	ВТ_ПараметрыЛимитированияРегистр.СпособОпределенияВалютыЛимитирования КАК СпособОпределенияВалютыЛимитирования
	|ПОМЕСТИТЬ ВТ_ДанныеБезПериодовЛимитирования
	|ИЗ
	|	ВТ_Данные КАК ВТ_Данные
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПараметрыЛимитированияСвязьПериодов КАК ВТ_ПараметрыЛимитированияСвязьПериодов
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПараметрыЛимитированияРегистр КАК ВТ_ПараметрыЛимитированияРегистр
	|			ПО ВТ_ПараметрыЛимитированияСвязьПериодов.ВидБюджета = ВТ_ПараметрыЛимитированияРегистр.ВидБюджета
	|				И ВТ_ПараметрыЛимитированияСвязьПериодов.Период = ВТ_ПараметрыЛимитированияРегистр.Период
	|		ПО ВТ_Данные.ВидБюджета = ВТ_ПараметрыЛимитированияСвязьПериодов.ВидБюджета
	|			И ВТ_Данные.ПериодОтчетаДатаНачала = ВТ_ПараметрыЛимитированияСвязьПериодов.ПериодОтчетаДатаНачала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеБезПериодовЛимитирования.Документ КАК Документ,
	|	ВТ_ДанныеБезПериодовЛимитирования.ВидБюджета КАК ВидБюджета,
	|	ВТ_ДанныеБезПериодовЛимитирования.ЦФО КАК ЦФО,
	|	ВТ_ДанныеБезПериодовЛимитирования.ПериодОтчета КАК ПериодОтчета,
	|	ВТ_ДанныеБезПериодовЛимитирования.Период КАК Период,
	|	ВТ_ДанныеБезПериодовЛимитирования.ИспользоватьЛимитирование КАК ИспользоватьЛимитирование,
	|	ВТ_ДанныеБезПериодовЛимитирования.ПериодичностьЛимитирования КАК ПериодичностьЛимитирования,
	|	ВТ_ДанныеБезПериодовЛимитирования.СпособОпределенияВалютыЛимитирования КАК СпособОпределенияВалютыЛимитирования,
	|	Периоды.Ссылка КАК ПериодЛимитирования,
	|	ВЫБОР ВТ_ДанныеБезПериодовЛимитирования.СпособОпределенияВалютыЛимитирования
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.СпособыОпределенияВалютыЛимитирования.ВалютаЛимитированияЦФО)
	|			ТОГДА ЕСТЬNULL(ПараметрыЛимитированияЦФО.ВалютаЛимитирования, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка))
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.СпособыОпределенияВалютыЛимитирования.ВалютаУпрУчета)
	|			ТОГДА ВалютаУправленческогоУчета.Значение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|	КОНЕЦ КАК ВалютаЛимитирования,
	|	ЕСТЬNULL(СостояниеПериодовЛимитирования.Закрыт, ЛОЖЬ) КАК ПериодЛимитированияЗакрыт
	|ИЗ
	|	ВТ_ДанныеБезПериодовЛимитирования КАК ВТ_ДанныеБезПериодовЛимитирования
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыЛимитированияЦФО КАК ПараметрыЛимитированияЦФО
	|		ПО ПараметрыЛимитированияЦФО.ЦФО = ВТ_ДанныеБезПериодовЛимитирования.ЦФО
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Периоды КАК Периоды
	|		ПО ВТ_ДанныеБезПериодовЛимитирования.ПериодичностьЛимитирования = Периоды.Периодичность
	|			И (Периоды.Произвольный = ЛОЖЬ)
	|			И (ВТ_ДанныеБезПериодовЛимитирования.ПериодОтчетаДатаНачала МЕЖДУ Периоды.ДатаНачала И Периоды.ДатаОкончания)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеПериодовЛимитирования.СрезПоследних(,) КАК СостояниеПериодовЛимитирования
	|		ПО ВТ_ДанныеБезПериодовЛимитирования.ВидБюджета = СостояниеПериодовЛимитирования.ВидБюджета
	|			И ВТ_ДанныеБезПериодовЛимитирования.ЦФО = СостояниеПериодовЛимитирования.ЦФО
	|			И (Периоды.Ссылка = СостояниеПериодовЛимитирования.ПериодЛимитирования),
	|	Константа.ВалютаУправленческогоУчета КАК ВалютаУправленческогоУчета";
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Функция ПолучитьПараметрыЛимитированияСтатей(ДокументСсылка)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документ", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.Документ КАК Документ,
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.ВидБюджета КАК ВидБюджета,
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.Предназначение КАК Предназначение,
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.ПериодОтчета КАК ПериодОтчета,
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.СтатьяБюджета КАК СтатьяБюджета,
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.ПериодОтчета.ДатаНачала КАК ПериодОтчетаДатаНачала
	|ПОМЕСТИТЬ ВТ_Данные
	|ИЗ
	|	РегистрСведений.УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей КАК УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей
	|ГДЕ
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.Документ = &Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.Период КАК Период,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.ВидБюджета КАК ВидБюджета,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.СтатьяБюджета КАК СтатьяБюджета,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.КонтролироватьСтатью КАК КонтролироватьСтатью,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.КонтролироватьАналитику1 КАК КонтролироватьАналитику1,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.КонтролироватьАналитику2 КАК КонтролироватьАналитику2,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.КонтролироватьАналитику3 КАК КонтролироватьАналитику3,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.КонтролироватьАналитику4 КАК КонтролироватьАналитику4,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.КонтролироватьАналитику5 КАК КонтролироватьАналитику5,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.КонтролироватьАналитику6 КАК КонтролироватьАналитику6
	|ПОМЕСТИТЬ ВТ_ПараметрыКонтроляЛимитаСтатейБюджетовРегистр
	|ИЗ
	|	РегистрСведений.ПараметрыКонтроляЛимитаСтатейБюджетов КАК ПараметрыКонтроляЛимитаСтатейБюджетов
	|ГДЕ
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.ВидБюджета В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ВТ_Данные.ВидБюджета КАК ВидБюджета
	|			ИЗ
	|				ВТ_Данные КАК ВТ_Данные)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_Данные.ВидБюджета КАК ВидБюджета,
	|	ВТ_Данные.СтатьяБюджета КАК СтатьяБюджета,
	|	ВТ_Данные.ПериодОтчетаДатаНачала КАК ПериодОтчетаДатаНачала
	|ПОМЕСТИТЬ ВТ_ДатыПланированияПоСтатьям
	|ИЗ
	|	ВТ_Данные КАК ВТ_Данные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДатыПланированияПоСтатьям.ВидБюджета КАК ВидБюджета,
	|	ВТ_ДатыПланированияПоСтатьям.СтатьяБюджета КАК СтатьяБюджета,
	|	ВТ_ДатыПланированияПоСтатьям.ПериодОтчетаДатаНачала КАК ПериодОтчетаДатаНачала,
	|	МАКСИМУМ(ВТ_ПараметрыКонтроляЛимитаСтатейБюджетовРегистр.Период) КАК Период
	|ПОМЕСТИТЬ ВТ_ПараметрыЛимитированияСтатейСвязьПериодов
	|ИЗ
	|	ВТ_ДатыПланированияПоСтатьям КАК ВТ_ДатыПланированияПоСтатьям
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПараметрыКонтроляЛимитаСтатейБюджетовРегистр КАК ВТ_ПараметрыКонтроляЛимитаСтатейБюджетовРегистр
	|		ПО ВТ_ДатыПланированияПоСтатьям.ВидБюджета = ВТ_ПараметрыКонтроляЛимитаСтатейБюджетовРегистр.ВидБюджета
	|			И ВТ_ДатыПланированияПоСтатьям.СтатьяБюджета = ВТ_ПараметрыКонтроляЛимитаСтатейБюджетовРегистр.СтатьяБюджета
	|			И ВТ_ДатыПланированияПоСтатьям.ПериодОтчетаДатаНачала >= ВТ_ПараметрыКонтроляЛимитаСтатейБюджетовРегистр.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДатыПланированияПоСтатьям.ВидБюджета,
	|	ВТ_ДатыПланированияПоСтатьям.СтатьяБюджета,
	|	ВТ_ДатыПланированияПоСтатьям.ПериодОтчетаДатаНачала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Данные.Документ КАК Документ,
	|	ВТ_Данные.ВидБюджета КАК ВидБюджета,
	|	ВТ_Данные.Предназначение КАК Предназначение,
	|	ВТ_Данные.ПериодОтчета КАК ПериодОтчета,
	|	ВТ_Данные.СтатьяБюджета КАК СтатьяБюджета,
	|	ВТ_Данные.ПериодОтчетаДатаНачала КАК ПериодОтчетаДатаНачала,
	|	ЕСТЬNULL(ВТ_ПараметрыКонтроляЛимитаСтатейБюджетовРегистр.КонтролироватьСтатью, ЛОЖЬ) КАК КонтролироватьСтатью,
	|	ЕСТЬNULL(ВТ_ПараметрыКонтроляЛимитаСтатейБюджетовРегистр.КонтролироватьАналитику1, ЛОЖЬ) КАК КонтролироватьАналитику1,
	|	ЕСТЬNULL(ВТ_ПараметрыКонтроляЛимитаСтатейБюджетовРегистр.КонтролироватьАналитику2, ЛОЖЬ) КАК КонтролироватьАналитику2,
	|	ЕСТЬNULL(ВТ_ПараметрыКонтроляЛимитаСтатейБюджетовРегистр.КонтролироватьАналитику3, ЛОЖЬ) КАК КонтролироватьАналитику3,
	|	ЕСТЬNULL(ВТ_ПараметрыКонтроляЛимитаСтатейБюджетовРегистр.КонтролироватьАналитику4, ЛОЖЬ) КАК КонтролироватьАналитику4,
	|	ЕСТЬNULL(ВТ_ПараметрыКонтроляЛимитаСтатейБюджетовРегистр.КонтролироватьАналитику5, ЛОЖЬ) КАК КонтролироватьАналитику5,
	|	ЕСТЬNULL(ВТ_ПараметрыКонтроляЛимитаСтатейБюджетовРегистр.КонтролироватьАналитику6, ЛОЖЬ) КАК КонтролироватьАналитику6
	|ИЗ
	|	ВТ_Данные КАК ВТ_Данные
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПараметрыЛимитированияСтатейСвязьПериодов КАК ВТ_ПараметрыЛимитированияСтатейСвязьПериодов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПараметрыКонтроляЛимитаСтатейБюджетовРегистр КАК ВТ_ПараметрыКонтроляЛимитаСтатейБюджетовРегистр
	|			ПО ВТ_ПараметрыЛимитированияСтатейСвязьПериодов.ВидБюджета = ВТ_ПараметрыКонтроляЛимитаСтатейБюджетовРегистр.ВидБюджета
	|				И ВТ_ПараметрыЛимитированияСтатейСвязьПериодов.СтатьяБюджета = ВТ_ПараметрыКонтроляЛимитаСтатейБюджетовРегистр.СтатьяБюджета
	|				И ВТ_ПараметрыЛимитированияСтатейСвязьПериодов.Период = ВТ_ПараметрыКонтроляЛимитаСтатейБюджетовРегистр.Период
	|		ПО ВТ_Данные.ВидБюджета = ВТ_ПараметрыЛимитированияСтатейСвязьПериодов.ВидБюджета
	|			И ВТ_Данные.СтатьяБюджета = ВТ_ПараметрыЛимитированияСтатейСвязьПериодов.СтатьяБюджета
	|			И ВТ_Данные.ПериодОтчетаДатаНачала = ВТ_ПараметрыЛимитированияСтатейСвязьПериодов.ПериодОтчетаДатаНачала";
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Функция ПолучитьПараметрыБюджетирования(ДокументСсылка)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документ", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.Документ КАК Документ,
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.ВидБюджета КАК ВидБюджета,
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.Организация КАК Организация,
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.Проект КАК Проект,
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.ПериодОтчета КАК ПериодОтчета,
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.ДокументБюджетирования КАК ДокументБюджетирования,
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.УОП КАК УОП,
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.УОП.ВерсияОрганизационнойСтруктуры КАК Регламент
	|ПОМЕСТИТЬ ВТ_ДокументыБюджетирования
	|ИЗ
	|	РегистрСведений.УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей КАК УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей
	|ГДЕ
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.Документ = &Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	УстановкаЛимитовПоДаннымБюджетированияПараметрыЛимитирования.Документ КАК Документ,
	|	УстановкаЛимитовПоДаннымБюджетированияПараметрыЛимитирования.ВидБюджета КАК ВидБюджета,
	|	УстановкаЛимитовПоДаннымБюджетированияПараметрыЛимитирования.ЦФО КАК ЦФО,
	|	УстановкаЛимитовПоДаннымБюджетированияПараметрыЛимитирования.ПериодОтчета КАК ПериодОтчета,
	|	УстановкаЛимитовПоДаннымБюджетированияПараметрыЛимитирования.ПериодЛимитирования КАК ПериодЛимитирования
	|ПОМЕСТИТЬ ВТ_Периоды
	|ИЗ
	|	РегистрСведений.УстановкаЛимитовПоДаннымБюджетированияПараметрыЛимитирования КАК УстановкаЛимитовПоДаннымБюджетированияПараметрыЛимитирования
	|ГДЕ
	|	УстановкаЛимитовПоДаннымБюджетированияПараметрыЛимитирования.Документ = &Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВерсииРегламентовПодготовкиОтчетностиОрганизационныеЕдиницы.Ссылка КАК Регламент,
	|	ВерсииРегламентовПодготовкиОтчетностиОрганизационныеЕдиницы.ОрганизационнаяЕдиница КАК ОрганизационнаяЕдиница,
	|	ВерсииРегламентовПодготовкиОтчетностиОрганизационныеЕдиницы.ОсновнаяВалюта КАК ОсновнаяВалюта
	|ПОМЕСТИТЬ ВТ_Регламенты
	|ИЗ
	|	Справочник.ВерсииРегламентовПодготовкиОтчетности.ОрганизационныеЕдиницы КАК ВерсииРегламентовПодготовкиОтчетностиОрганизационныеЕдиницы
	|ГДЕ
	|	ВерсииРегламентовПодготовкиОтчетностиОрганизационныеЕдиницы.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ_ДокументыБюджетирования.УОП.ВерсияОрганизационнойСтруктуры КАК УОПВерсияОрганизационнойСтруктуры
	|			ИЗ
	|				ВТ_ДокументыБюджетирования КАК ВТ_ДокументыБюджетирования)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ДокументыБюджетирования.Документ КАК Документ,
	|	ВТ_ДокументыБюджетирования.ВидБюджета КАК ВидБюджета,
	|	ВТ_ДокументыБюджетирования.Организация КАК ЦФО,
	|	ВТ_ДокументыБюджетирования.Проект КАК Проект,
	|	ВТ_Периоды.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ВТ_ДокументыБюджетирования.ДокументБюджетирования КАК ДокументБюджетирования,
	|	ВТ_ДокументыБюджетирования.УОП КАК УОП,
	|	ЕСТЬNULL(РегистрСостоянийОбъектовСрезПоследних.СостояниеОбъекта, ЗНАЧЕНИЕ(Перечисление.СостоянияОтчетов.Выполняется)) КАК СостояниеСогласования,
	|	ВТ_ДокументыБюджетирования.Регламент КАК Регламент,
	|	ВТ_Регламенты.ОсновнаяВалюта КАК ОсновнаяВалюта
	|ИЗ
	|	ВТ_ДокументыБюджетирования КАК ВТ_ДокументыБюджетирования
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Периоды КАК ВТ_Периоды
	|		ПО ВТ_ДокументыБюджетирования.Документ = ВТ_Периоды.Документ
	|			И ВТ_ДокументыБюджетирования.ПериодОтчета = ВТ_Периоды.ПериодОтчета
	|			И ВТ_ДокументыБюджетирования.Организация = ВТ_Периоды.ЦФО
	|			И ВТ_ДокументыБюджетирования.ВидБюджета = ВТ_Периоды.ВидБюджета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрСостоянийОбъектов.СрезПоследних КАК РегистрСостоянийОбъектовСрезПоследних
	|		ПО ВТ_ДокументыБюджетирования.ДокументБюджетирования = РегистрСостоянийОбъектовСрезПоследних.Объект
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Регламенты КАК ВТ_Регламенты
	|		ПО ВТ_ДокументыБюджетирования.Регламент = ВТ_Регламенты.Регламент
	|			И ВТ_ДокументыБюджетирования.Организация = ВТ_Регламенты.ОрганизационнаяЕдиница";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УстановкаЛимитов.Дата КАК Период,
	|	УстановкаЛимитов.ВидОперации КАК ВидОперации
	|ИЗ
	|	Документ.УправлениеЛимитами КАК УстановкаЛимитов
	|ГДЕ
	|	УстановкаЛимитов.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.ТекущийПользователь());
	
КонецПроцедуры

Процедура ТекстЗапросаЛимитыПоБюджетам(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ЛимитыПоБюджетам";
	
	// Данные документа
	Если НЕ ПроведениеСерверОПК.ЕстьТаблицаЗапроса("ВТ_Данные", ТекстыЗапроса) Тогда
		ТекстЗапросаВТ_Данные(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	// ВТ_ЛимитыКУстановке
	Если НЕ ПроведениеСерверОПК.ЕстьТаблицаЗапроса("ВТ_ЛимитыКУстановке", ТекстыЗапроса) Тогда
		ТекстЗапросаВТ_ЛимитыКУстановке(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	// ВТ_ЛимитыКСторнированию
	Если НЕ ПроведениеСерверОПК.ЕстьТаблицаЗапроса("ВТ_ЛимитыКСторнированию", ТекстыЗапроса) Тогда
		ТекстЗапросаВТ_ЛимитыКСторнированию(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	Запрос.Предназначение КАК Предназначение,
	|	Запрос.ПериодЛимитирования КАК ПериодЛимитирования,
	|	Запрос.ЦФО КАК ЦФО,
	|	Запрос.Проект КАК Проект,
	|	Запрос.СтатьяБюджета КАК СтатьяБюджета,
	|	Запрос.Аналитика1 КАК Аналитика1,
	|	Запрос.Аналитика2 КАК Аналитика2,
	|	Запрос.Аналитика3 КАК Аналитика3,
	|	Запрос.Аналитика4 КАК Аналитика4,
	|	Запрос.Аналитика5 КАК Аналитика5,
	|	Запрос.Аналитика6 КАК Аналитика6,
	|	Запрос.Валюта КАК Валюта,
	|	СУММА(Запрос.Лимит) КАК Лимит,
	|	СУММА(Запрос.Корректировка) КАК Корректировка,
	|	СУММА(Запрос.КОбеспечению) КАК КОбеспечению,
	|	Запрос.ДокументРезервирования КАК ДокументРезервирования,
	|	Запрос.ДокументПланирования КАК ДокументПланирования
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ_ЛимитыКУстановке.Предназначение КАК Предназначение,
	|		ВТ_ЛимитыКУстановке.ПериодЛимитирования КАК ПериодЛимитирования,
	|		ВТ_ЛимитыКУстановке.Организация КАК ЦФО,
	|		ВТ_ЛимитыКУстановке.Проект КАК Проект,
	|		ВТ_ЛимитыКУстановке.СтатьяБюджета КАК СтатьяБюджета,
	|		ВТ_ЛимитыКУстановке.Аналитика1 КАК Аналитика1,
	|		ВТ_ЛимитыКУстановке.Аналитика2 КАК Аналитика2,
	|		ВТ_ЛимитыКУстановке.Аналитика3 КАК Аналитика3,
	|		ВТ_ЛимитыКУстановке.Аналитика4 КАК Аналитика4,
	|		ВТ_ЛимитыКУстановке.Аналитика5 КАК Аналитика5,
	|		ВТ_ЛимитыКУстановке.Аналитика6 КАК Аналитика6,
	|		ВТ_ЛимитыКУстановке.Валюта КАК Валюта,
	|		ВТ_ЛимитыКУстановке.Лимит КАК Лимит,
	|		ВТ_ЛимитыКУстановке.Корректировка КАК Корректировка,
	|		ВТ_ЛимитыКУстановке.КОбеспечению КАК КОбеспечению,
	|		ЗНАЧЕНИЕ(Документ.ОперативныйПлан.ПустаяСсылка) КАК ДокументРезервирования,
	|		НЕОПРЕДЕЛЕНО КАК ДокументПланирования
	|	ИЗ
	|		ВТ_ЛимитыКУстановке КАК ВТ_ЛимитыКУстановке
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_ЛимитыКСторнированию.Предназначение,
	|		ВТ_ЛимитыКСторнированию.ПериодЛимитирования,
	|		ВТ_ЛимитыКСторнированию.ЦФО,
	|		ВТ_ЛимитыКСторнированию.Проект,
	|		ВТ_ЛимитыКСторнированию.СтатьяБюджета,
	|		ВТ_ЛимитыКСторнированию.Аналитика1,
	|		ВТ_ЛимитыКСторнированию.Аналитика2,
	|		ВТ_ЛимитыКСторнированию.Аналитика3,
	|		ВТ_ЛимитыКСторнированию.Аналитика4,
	|		ВТ_ЛимитыКСторнированию.Аналитика5,
	|		ВТ_ЛимитыКСторнированию.Аналитика6,
	|		ВТ_ЛимитыКСторнированию.Валюта,
	|		ВТ_ЛимитыКСторнированию.Лимит,
	|		ВТ_ЛимитыКСторнированию.Корректировка,
	|		ВТ_ЛимитыКСторнированию.КОбеспечению,
	|		ВТ_ЛимитыКСторнированию.ДокументРезервирования,
	|		ВТ_ЛимитыКСторнированию.ДокументПланирования
	|	ИЗ
	|		ВТ_ЛимитыКСторнированию КАК ВТ_ЛимитыКСторнированию) КАК Запрос
	|
	|СГРУППИРОВАТЬ ПО
	|	Запрос.Проект,
	|	Запрос.Аналитика2,
	|	Запрос.Аналитика5,
	|	Запрос.Валюта,
	|	Запрос.ДокументРезервирования,
	|	Запрос.ДокументПланирования,
	|	Запрос.Аналитика3,
	|	Запрос.Аналитика4,
	|	Запрос.ПериодЛимитирования,
	|	Запрос.СтатьяБюджета,
	|	Запрос.Предназначение,
	|	Запрос.ЦФО,
	|	Запрос.Аналитика1,
	|	Запрос.Аналитика6
	|
	|ИМЕЮЩИЕ
	|	СУММА(Запрос.Лимит) <> 0 ИЛИ СУММА(Запрос.Корректировка) <> 0 ИЛИ СУММА(Запрос.КОбеспечению) <> 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаСостояния(Запрос, ТекстыЗапроса)
	
	// ВТ_СостоянияПериодовЛимитирования
	Если НЕ ПроведениеСерверОПК.ЕстьТаблицаЗапроса("ВТ_СостоянияПериодовЛимитирования", ТекстыЗапроса) Тогда
		ТекстЗапросаВТ_СостоянияПериодовЛимитирования(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	// ВТ_ПериодыЛимитирования
	Если НЕ ПроведениеСерверОПК.ЕстьТаблицаЗапроса("ВТ_ПериодыЛимитирования", ТекстыЗапроса) Тогда
		ТекстЗапросаВТ_ПериодыЛимитирования(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	// ВТ_СостоянияСогласования
	Если НЕ ПроведениеСерверОПК.ЕстьТаблицаЗапроса("ВТ_СостоянияСогласования", ТекстыЗапроса) Тогда
		ТекстЗапросаВТ_СостоянияСогласования(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ИмяРегистра = "УстановкаЛимитовПоДаннымБюджетированияСостояния";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ВТ_ПериодыЛимитирования.ВидБюджета КАК ВидБюджета,
	|	ВТ_ПериодыЛимитирования.ЦФО КАК ЦФО,
	|	ВТ_ПериодыЛимитирования.Проект КАК Проект,
	|	ВТ_ПериодыЛимитирования.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ВТ_ПериодыЛимитирования.УОП КАК УОП,
	|	ВТ_ПериодыЛимитирования.УОП.Сценарий КАК Сценарий,
	|	&ВидОперации КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУправлениеЛимитами.ОтменаУстановкиЛимитов)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияУстановкиЛимитов.НеУстановлен)
	|		КОГДА ВТ_СостоянияСогласования.СостояниеСогласования = ЗНАЧЕНИЕ(Перечисление.СостоянияОтчетов.Утвержден)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияУстановкиЛимитов.Установлен)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияУстановкиЛимитов.УстановленПоНеутвержденнымДанным)
	|	КОНЕЦ КАК Состояние
	|ИЗ
	|	ВТ_ПериодыЛимитирования КАК ВТ_ПериодыЛимитирования
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СостоянияСогласования КАК ВТ_СостоянияСогласования
	|		ПО ВТ_ПериодыЛимитирования.ВидБюджета = ВТ_СостоянияСогласования.ВидБюджета
	|			И ВТ_ПериодыЛимитирования.ЦФО = ВТ_СостоянияСогласования.ЦФО
	|			И ВТ_ПериодыЛимитирования.Проект = ВТ_СостоянияСогласования.Проект
	|			И ВТ_ПериодыЛимитирования.ПериодЛимитирования = ВТ_СостоянияСогласования.ПериодЛимитирования
	|			И ВТ_ПериодыЛимитирования.ПериодЛимитирования = ВТ_СостоянияСогласования.ПериодЛимитирования
	|ГДЕ
	|	ВТ_ПериодыЛимитирования.ПериодЛимитированияЗакрыт = ЛОЖЬ
	|	И (&ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУправлениеЛимитами.УстановкаЛимитовПоИсточнику)
	|	ИЛИ &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУправлениеЛимитами.ОтменаУстановкиЛимитов))";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаВТ_Данные(Запрос, ТекстыЗапроса)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗначенияПоказателей.Предназначение КАК Предназначение,
	|	ПараметрыЛимитирования.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ЗначенияПоказателей.Организация КАК Организация,
	|	ЗначенияПоказателей.Проект КАК Проект,
	|	ЗначенияПоказателей.СтатьяБюджета КАК СтатьяБюджета,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПараметрыЛимитированияСтатейБюджетов.КонтролироватьАналитику1, ЛОЖЬ) = ИСТИНА
	|			ТОГДА ЗначенияПоказателей.Аналитика1
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Аналитика1,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПараметрыЛимитированияСтатейБюджетов.КонтролироватьАналитику2, ЛОЖЬ) = ИСТИНА
	|			ТОГДА ЗначенияПоказателей.Аналитика2
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Аналитика2,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПараметрыЛимитированияСтатейБюджетов.КонтролироватьАналитику3, ЛОЖЬ) = ИСТИНА
	|			ТОГДА ЗначенияПоказателей.Аналитика3
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Аналитика3,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПараметрыЛимитированияСтатейБюджетов.КонтролироватьАналитику4, ЛОЖЬ) = ИСТИНА
	|			ТОГДА ЗначенияПоказателей.Аналитика4
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Аналитика4,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПараметрыЛимитированияСтатейБюджетов.КонтролироватьАналитику5, ЛОЖЬ) = ИСТИНА
	|			ТОГДА ЗначенияПоказателей.Аналитика5
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Аналитика5,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПараметрыЛимитированияСтатейБюджетов.КонтролироватьАналитику6, ЛОЖЬ) = ИСТИНА
	|			ТОГДА ЗначенияПоказателей.Аналитика6
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Аналитика6,
	|	ПараметрыБюджетирования.ОсновнаяВалюта КАК ОсновнаяВалюта,
	|	ПараметрыЛимитирования.СпособОпределенияВалютыЛимитирования КАК СпособОпределенияВалютыЛимитирования,
	|	ПараметрыЛимитирования.ВалютаЛимитирования КАК ВалютаЛимитирования,
	|	ЗначенияПоказателей.Валюта КАК Валюта,
	|	ЗначенияПоказателей.АналитикаВалюта КАК АналитикаВалюта,
	|	ЗначенияПоказателей.Значение КАК Значение,
	|	ЗначенияПоказателей.ЗначениеВалюта КАК ЗначениеВалюта,
	|	ЗначенияПоказателей.ЕстьЗначениеВВалютеРаскрытия КАК ЕстьЗначениеВВалютеРаскрытия
	|ПОМЕСТИТЬ ВТ_Данные
	|ИЗ
	|	РегистрСведений.УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей КАК ЗначенияПоказателей
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УстановкаЛимитовПоДаннымБюджетированияПараметрыЛимитирования КАК ПараметрыЛимитирования
	|		ПО ЗначенияПоказателей.Документ = ПараметрыЛимитирования.Документ
	|			И ЗначенияПоказателей.ВидБюджета = ПараметрыЛимитирования.ВидБюджета
	|			И ЗначенияПоказателей.ПериодОтчета = ПараметрыЛимитирования.ПериодОтчета
	|			И ЗначенияПоказателей.Организация = ПараметрыЛимитирования.ЦФО
	|			И (ПараметрыЛимитирования.ИспользоватьЛимитирование = ИСТИНА)
	|			И (ПараметрыЛимитирования.ПериодЛимитированияЗакрыт = ЛОЖЬ)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УстановкаЛимитовПоДаннымБюджетированияПараметрыЛимитированияСтатейБюджетов КАК ПараметрыЛимитированияСтатейБюджетов
	|		ПО ЗначенияПоказателей.Документ = ПараметрыЛимитированияСтатейБюджетов.Документ
	|			И ЗначенияПоказателей.ВидБюджета = ПараметрыЛимитированияСтатейБюджетов.ВидБюджета
	|			И ЗначенияПоказателей.ПериодОтчета = ПараметрыЛимитированияСтатейБюджетов.ПериодОтчета
	|			И ЗначенияПоказателей.СтатьяБюджета = ПараметрыЛимитированияСтатейБюджетов.СтатьяБюджета
	|			И (ПараметрыЛимитированияСтатейБюджетов.КонтролироватьСтатью = ИСТИНА)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УстановкаЛимитовПоДаннымБюджетированияПараметрыБюджетирования КАК ПараметрыБюджетирования
	|		ПО ЗначенияПоказателей.Документ = ПараметрыБюджетирования.Документ
	|			И ЗначенияПоказателей.ВидБюджета = ПараметрыБюджетирования.ВидБюджета
	|			И ЗначенияПоказателей.Организация = ПараметрыБюджетирования.ЦФО
	|			И ЗначенияПоказателей.Проект = ПараметрыБюджетирования.Проект
	|			И ПараметрыЛимитирования.ПериодЛимитирования = ПараметрыБюджетирования.ПериодЛимитирования
	|			И ЗначенияПоказателей.ДокументБюджетирования = ПараметрыБюджетирования.ДокументБюджетирования
	|ГДЕ
	|	ЗначенияПоказателей.Документ = &Ссылка
	|	И &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУправлениеЛимитами.УстановкаЛимитовПоИсточнику)
	|	И ЗначенияПоказателей.СтатьяБюджета.ЭтоГруппа = ЛОЖЬ";
	
	ИмяРегистра = "ВТ_Данные";
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры 

Процедура ТекстЗапросаВТ_ЛимитыКУстановке(Запрос, ТекстыЗапроса)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВТ_Данные.Предназначение КАК Предназначение,
	|	ВТ_Данные.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ВТ_Данные.Организация КАК Организация,
	|	ВТ_Данные.Проект КАК Проект,
	|	ВТ_Данные.СтатьяБюджета КАК СтатьяБюджета,
	|	ВТ_Данные.Аналитика1 КАК Аналитика1,
	|	ВТ_Данные.Аналитика2 КАК Аналитика2,
	|	ВТ_Данные.Аналитика3 КАК Аналитика3,
	|	ВТ_Данные.Аналитика4 КАК Аналитика4,
	|	ВТ_Данные.Аналитика5 КАК Аналитика5,
	|	ВТ_Данные.Аналитика6 КАК Аналитика6,
	|	ВЫБОР
	|		КОГДА ВТ_Данные.ЕстьЗначениеВВалютеРаскрытия
	|			ТОГДА ВТ_Данные.АналитикаВалюта
	|		ИНАЧЕ ВТ_Данные.Валюта
	|	КОНЕЦ КАК Валюта,
	|	ВЫБОР
	|		КОГДА ВТ_Данные.ЕстьЗначениеВВалютеРаскрытия
	|			ТОГДА ВТ_Данные.ЗначениеВалюта
	|		ИНАЧЕ ВТ_Данные.Значение
	|	КОНЕЦ КАК Лимит,
	|	0 КАК Корректировка,
	|	0 КАК КОбеспечению
	|ПОМЕСТИТЬ ВТ_ЛимитыКУстановке
	|ИЗ
	|	ВТ_Данные КАК ВТ_Данные
	|ГДЕ
	|	ВТ_Данные.СпособОпределенияВалютыЛимитирования = ЗНАЧЕНИЕ(Перечисление.СпособыОпределенияВалютыЛимитирования.ВалютаОперации)
	|	И ВТ_Данные.Валюта = ВТ_Данные.ОсновнаяВалюта
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_Данные.Предназначение,
	|	ВТ_Данные.ПериодЛимитирования,
	|	ВТ_Данные.Организация,
	|	ВТ_Данные.Проект,
	|	ВТ_Данные.СтатьяБюджета,
	|	ВТ_Данные.Аналитика1,
	|	ВТ_Данные.Аналитика2,
	|	ВТ_Данные.Аналитика3,
	|	ВТ_Данные.Аналитика4,
	|	ВТ_Данные.Аналитика5,
	|	ВТ_Данные.Аналитика6,
	|	ВТ_Данные.Валюта,
	|	ВТ_Данные.Значение,
	|	0,
	|	0
	|ИЗ
	|	ВТ_Данные КАК ВТ_Данные
	|ГДЕ
	|	ВТ_Данные.СпособОпределенияВалютыЛимитирования <> ЗНАЧЕНИЕ(Перечисление.СпособыОпределенияВалютыЛимитирования.ВалютаОперации)
	|	И ВТ_Данные.Валюта = ВТ_Данные.ВалютаЛимитирования
	|	И &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУправлениеЛимитами.УстановкаЛимитовПоИсточнику)";
	
	ИмяРегистра = "ВТ_ЛимитыКУстановке";
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры 

Процедура ТекстЗапросаВТ_ЛимитыКСторнированию(Запрос, ТекстыЗапроса)
	
	// ВТ_СостоянияПериодовЛимитирования
	Если НЕ ПроведениеСерверОПК.ЕстьТаблицаЗапроса("ВТ_СостоянияПериодовЛимитирования", ТекстыЗапроса) Тогда
		ТекстЗапросаВТ_СостоянияПериодовЛимитирования(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	// ВТ_ПериодыЛимитирования
	Если НЕ ПроведениеСерверОПК.ЕстьТаблицаЗапроса("ВТ_ПериодыЛимитирования", ТекстыЗапроса) Тогда
		ТекстЗапросаВТ_ПериодыЛимитирования(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЛимитыПоБюджетамОбороты.Предназначение КАК Предназначение,
	|	ЛимитыПоБюджетамОбороты.ДокументРезервирования КАК ДокументРезервирования,
	|	ЛимитыПоБюджетамОбороты.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ЛимитыПоБюджетамОбороты.ЦФО КАК ЦФО,
	|	ЛимитыПоБюджетамОбороты.Проект КАК Проект,
	|	ЛимитыПоБюджетамОбороты.СтатьяБюджета КАК СтатьяБюджета,
	|	ЛимитыПоБюджетамОбороты.Аналитика1 КАК Аналитика1,
	|	ЛимитыПоБюджетамОбороты.Аналитика2 КАК Аналитика2,
	|	ЛимитыПоБюджетамОбороты.Аналитика3 КАК Аналитика3,
	|	ЛимитыПоБюджетамОбороты.Аналитика4 КАК Аналитика4,
	|	ЛимитыПоБюджетамОбороты.Аналитика5 КАК Аналитика5,
	|	ЛимитыПоБюджетамОбороты.Аналитика6 КАК Аналитика6,
	|	ЛимитыПоБюджетамОбороты.Валюта КАК Валюта,
	|	ЛимитыПоБюджетамОбороты.ДокументПланирования КАК ДокументПланирования,
	|	-ЛимитыПоБюджетамОбороты.ЛимитОборот КАК Лимит,
	|	-ЛимитыПоБюджетамОбороты.КорректировкаОборот КАК Корректировка,
	|	-ЛимитыПоБюджетамОбороты.КОбеспечениюОборот КАК КОбеспечению
	|ПОМЕСТИТЬ ВТ_ЛимитыКСторнированию
	|ИЗ
	|	РегистрНакопления.ЛимитыПоБюджетам.Обороты(
	|			,
	|			,
	|			,
	|			(Предназначение, ЦФО, Проект, ПериодЛимитирования) В
	|				(ВЫБРАТЬ
	|					ВТ_ПериодыЛимитирования.ВидБюджета.Предназначение КАК Предназначение,
	|					ВТ_ПериодыЛимитирования.ЦФО КАК ЦФО,
	|					ВТ_ПериодыЛимитирования.Проект КАК Проект,
	|					ВТ_ПериодыЛимитирования.ПериодЛимитирования КАК ПериодЛимитирования
	|				ИЗ
	|					ВТ_ПериодыЛимитирования КАК ВТ_ПериодыЛимитирования
	|				ГДЕ
	|					ВТ_ПериодыЛимитирования.ПериодЛимитированияЗакрыт = ЛОЖЬ)) КАК ЛимитыПоБюджетамОбороты
	|ГДЕ
	|	&ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУправлениеЛимитами.УстановкаЛимитовПоИсточнику)
	|	ИЛИ &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУправлениеЛимитами.ОтменаУстановкиЛимитов)";
	
	ИмяРегистра = "ВТ_ЛимитыКСторнированию";
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры 

Процедура ТекстЗапросаВТ_СостоянияПериодовЛимитирования(Запрос, ТекстыЗапроса)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	УстановкаЛимитовПоДаннымБюджетированияПараметрыЛимитирования.ВидБюджета КАК ВидБюджета,
	|	УстановкаЛимитовПоДаннымБюджетированияПараметрыЛимитирования.ЦФО КАК ЦФО,
	|	УстановкаЛимитовПоДаннымБюджетированияПараметрыЛимитирования.ПериодЛимитирования КАК ПериодЛимитирования,
	|	МАКСИМУМ(УстановкаЛимитовПоДаннымБюджетированияПараметрыЛимитирования.ПериодЛимитированияЗакрыт) КАК ПериодЛимитированияЗакрыт
	|ПОМЕСТИТЬ ВТ_СостоянияПериодовЛимитирования
	|ИЗ
	|	РегистрСведений.УстановкаЛимитовПоДаннымБюджетированияПараметрыЛимитирования КАК УстановкаЛимитовПоДаннымБюджетированияПараметрыЛимитирования
	|ГДЕ
	|	УстановкаЛимитовПоДаннымБюджетированияПараметрыЛимитирования.Документ = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	УстановкаЛимитовПоДаннымБюджетированияПараметрыЛимитирования.ВидБюджета,
	|	УстановкаЛимитовПоДаннымБюджетированияПараметрыЛимитирования.ЦФО,
	|	УстановкаЛимитовПоДаннымБюджетированияПараметрыЛимитирования.ПериодЛимитирования";
	
	ИмяРегистра = "ВТ_СостоянияПериодовЛимитирования";
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры 

Процедура ТекстЗапросаВТ_ПериодыЛимитирования(Запрос, ТекстыЗапроса)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	УправлениеЛимитамиДанные.ВидБюджета КАК ВидБюджета,
	|	УправлениеЛимитамиДанные.ЦФО КАК ЦФО,
	|	УправлениеЛимитамиДанные.Проект КАК Проект,
	|	УправлениеЛимитамиДанные.ПериодЛимитирования КАК ПериодЛимитирования,
	|	УправлениеЛимитамиДанные.УОП КАК УОП,
	|	ЕСТЬNULL(ВТ_СостоянияПериодовЛимитирования.ПериодЛимитированияЗакрыт, ЛОЖЬ) КАК ПериодЛимитированияЗакрыт
	|ПОМЕСТИТЬ ВТ_ПериодыЛимитирования
	|ИЗ
	|	Документ.УправлениеЛимитами.Данные КАК УправлениеЛимитамиДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СостоянияПериодовЛимитирования КАК ВТ_СостоянияПериодовЛимитирования
	|		ПО УправлениеЛимитамиДанные.ВидБюджета = ВТ_СостоянияПериодовЛимитирования.ВидБюджета
	|			И УправлениеЛимитамиДанные.ЦФО = ВТ_СостоянияПериодовЛимитирования.ЦФО
	|			И УправлениеЛимитамиДанные.ПериодЛимитирования = ВТ_СостоянияПериодовЛимитирования.ПериодЛимитирования
	|ГДЕ
	|	УправлениеЛимитамиДанные.Ссылка = &Ссылка";
	
	ИмяРегистра = "ВТ_ПериодыЛимитирования";
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры 

Процедура ТекстЗапросаВТ_СостоянияСогласования(Запрос, ТекстыЗапроса)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	УстановкаЛимитовПоДаннымБюджетированияПараметрыБюджетирования.Документ КАК Документ,
	|	УстановкаЛимитовПоДаннымБюджетированияПараметрыБюджетирования.ВидБюджета КАК ВидБюджета,
	|	УстановкаЛимитовПоДаннымБюджетированияПараметрыБюджетирования.ЦФО КАК ЦФО,
	|	УстановкаЛимитовПоДаннымБюджетированияПараметрыБюджетирования.Проект КАК Проект,
	|	УстановкаЛимитовПоДаннымБюджетированияПараметрыБюджетирования.ПериодЛимитирования КАК ПериодЛимитирования,
	|	МИНИМУМ(УстановкаЛимитовПоДаннымБюджетированияПараметрыБюджетирования.СостояниеСогласования) КАК СостояниеСогласования
	|ПОМЕСТИТЬ ВТ_СостоянияСогласования
	|ИЗ
	|	РегистрСведений.УстановкаЛимитовПоДаннымБюджетированияПараметрыБюджетирования КАК УстановкаЛимитовПоДаннымБюджетированияПараметрыБюджетирования
	|ГДЕ
	|	УстановкаЛимитовПоДаннымБюджетированияПараметрыБюджетирования.Документ = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	УстановкаЛимитовПоДаннымБюджетированияПараметрыБюджетирования.Документ,
	|	УстановкаЛимитовПоДаннымБюджетированияПараметрыБюджетирования.ВидБюджета,
	|	УстановкаЛимитовПоДаннымБюджетированияПараметрыБюджетирования.ЦФО,
	|	УстановкаЛимитовПоДаннымБюджетированияПараметрыБюджетирования.Проект,
	|	УстановкаЛимитовПоДаннымБюджетированияПараметрыБюджетирования.ПериодЛимитирования";
	
	ИмяРегистра = "ВТ_СостоянияСогласования";
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры 

Процедура ТекстЗапросаСостояниеПериодовЛимитирования(Запрос, ТекстыЗапроса)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	УправлениеЛимитамиДанные.ВидБюджета КАК ВидБюджета,
	|	УправлениеЛимитамиДанные.ЦФО КАК ЦФО,
	|	УправлениеЛимитамиДанные.Проект КАК Проект,
	|	УправлениеЛимитамиДанные.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ВЫБОР
	|		КОГДА &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУправлениеЛимитами.ЗакрытиеПериодовЛимитирования)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Закрыт
	|ИЗ
	|	Документ.УправлениеЛимитами.Данные КАК УправлениеЛимитамиДанные
	|ГДЕ
	|	УправлениеЛимитамиДанные.Ссылка = &Ссылка
	|	И (&ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУправлениеЛимитами.ЗакрытиеПериодовЛимитирования)
	|			ИЛИ &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУправлениеЛимитами.ОткрытиеПериодовЛимитирования))";
	
	ИмяРегистра = "СостояниеПериодовЛимитирования";
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаАктуальность(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "АктуальностьУстановленныхЛимитов";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Период КАК Период,
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.Организация КАК Организация,
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.Проект КАК Проект,
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.ВидОтчета КАК ВидОтчета,
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.Область КАК Область,
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.УОП КАК УОП,
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.Предназначение КАК Предназначение,
	|	ИСТИНА КАК Актуальность,
	|	&Ссылка КАК Документ,
	|	&ТекущийПользователь КАК Пользователь
	|ИЗ
	|	РегистрСведений.УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей КАК УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей
	|ГДЕ
	|	УстановкаЛимитовПоДаннымБюджетированияЗначенияПоказателей.Документ = &Ссылка
	|	И &ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийУправлениеЛимитами.УстановкаЛимитовПоИсточнику)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция СкорректироватьТаблицуПоказателей(СтрокаДокумент, ТаблицаПоказателей)
	
	ШаблонАналитики = "Аналитика%1";
	ШаблонВидАналитики = "ВидАналитики%1";
	
	СтруктураПоиска = Новый Структура;
	Для НомерАналитики = 1 По ПараметрыСеанса.ЧислоДопАналитик Цикл
		
		ИмяАналитики = СтрШаблон(ШаблонАналитики, НомерАналитики);
		ИмяВидАналитики = СтрШаблон(ШаблонВидАналитики, НомерАналитики);
		
		Если НЕ ЗначениеЗаполнено(СтрокаДокумент[ИмяВидАналитики]) Тогда
			Прервать;
		КонецЕсли;
		СтруктураПоиска.Вставить(ИмяАналитики, СтрокаДокумент[ИмяАналитики]);
	КонецЦикла;
	
	Если СтруктураПоиска.Количество() = 0 Тогда
		Возврат ТаблицаПоказателей;
	КонецЕсли;
	
	Возврат ТаблицаПоказателей.Скопировать(СтруктураПоиска);
	
КонецФункции

#КонецОбласти

#КонецЕсли
