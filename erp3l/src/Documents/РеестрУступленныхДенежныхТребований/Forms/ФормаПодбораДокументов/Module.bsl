#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОтборОрганизация = Параметры.Организация;
	
	//
	ПараметрыДинамическогоСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("НаДату", КонецДня(ТекущаяДатаСеанса()));
	ПараметрыЗапроса.Вставить("Организация", ОтборОрганизация);
	
	//
	ТекстЗапроса = ПретензииУХ.ТекстЗапросаДебиторскаяЗадолженностьФормаПодбора(ПараметрыЗапроса);
	ПараметрыДинамическогоСписка.Вставить("ТекстЗапроса", ТекстЗапроса);
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Список, ПараметрыДинамическогоСписка);
	
	//
	Список.Параметры.УстановитьЗначениеПараметра("НаДату", ПараметрыЗапроса.НаДату);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список,
		"Организация", ПараметрыЗапроса.Организация, ЗначениеЗаполнено(ОтборОрганизация));
	
	Элементы.ОтборОрганизация.ТолькоПросмотр = ЗначениеЗаполнено(ОтборОрганизация);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Организация", ОтборОрганизация, ЗначениеЗаполнено(ОтборОрганизация));

КонецПроцедуры

&НаКлиенте
Процедура ОтборКонтрагентПриИзменении(Элемент)
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Контрагент", ОтборКонтрагент, ЗначениеЗаполнено(ОтборКонтрагент));
КонецПроцедуры

&НаКлиенте
Процедура ОтборДоговорПриИзменении(Элемент)
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Договор", ОтборДоговор, ЗначениеЗаполнено(ОтборДоговор));
КонецПроцедуры

&НаКлиенте
Процедура ОтборВалютаПриИзменении(Элемент)
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Валюта", ОтборВалюта, ЗначениеЗаполнено(ОтборВалюта));
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Выбрать(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	ТД = Элементы.Список.ТекущиеДанные;
	Если ТД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ЗначениеЗаполнено(ТД.РасчетныйДокумент) Тогда
		СтруктураВозврата = Новый Структура();
		СтруктураВозврата.Вставить("Организация", ТД.Организация);
		СтруктураВозврата.Вставить("Контрагент", ТД.Контрагент);
		СтруктураВозврата.Вставить("ДоговорКонтрагента", ТД.Договор);
		СтруктураВозврата.Вставить("ОбъектРасчетов", ТД.ОбъектРасчетов);
		СтруктураВозврата.Вставить("РасчетныйДокумент", ТД.РасчетныйДокумент);
		СтруктураВозврата.Вставить("СрокПогашения", ТД.ДатаПлановогоПогашения);
		СтруктураВозврата.Вставить("Сумма", ТД.СуммаВзаиморасчетов);
		СтруктураВозврата.Вставить("Валюта", ТД.Валюта);
		ОповеститьОВыборе(СтруктураВозврата);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти

