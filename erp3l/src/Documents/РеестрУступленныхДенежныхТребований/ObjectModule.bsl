#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
	
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СуммаОбязательств = УступленныеТребования.Итог("СуммаОбязательства");
	СуммаПоступления = ГрафикРасчета.Итог("СуммаПоступления");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеестрУступленныхДенежныхТребованийУступленныеТребования.Ссылка.Организация КАК Организация,
		|	РеестрУступленныхДенежныхТребованийУступленныеТребования.Ссылка.Контрагент КАК Контрагент,
		|	РеестрУступленныхДенежныхТребованийУступленныеТребования.Контрагент КАК Дебитор,
		|	РеестрУступленныхДенежныхТребованийУступленныеТребования.Ссылка.ДатаФинансирования КАК Период,
		|	РеестрУступленныхДенежныхТребованийУступленныеТребования.Ссылка КАК Регистратор,
		|	РеестрУступленныхДенежныхТребованийУступленныеТребования.СуммаОбязательства КАК УступленнаяЗадолженность,
		|	0 КАК СуммаПоступления,
		|	0 КАК Комиссия,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиДоходовИРасходов.ПустаяСсылка) КАК СтатьяБюджета,
		|	ЗНАЧЕНИЕ(Перечисление.ЭлементыСтруктурыЗадолженности.ОсновнойДолг) КАК ЭлементСтруктурыЗадолженности
		|ПОМЕСТИТЬ Расчет
		|ИЗ
		|	Документ.РеестрУступленныхДенежныхТребований.УступленныеТребования КАК РеестрУступленныхДенежныхТребованийУступленныеТребования
		|ГДЕ
		|	РеестрУступленныхДенежныхТребованийУступленныеТребования.Ссылка = &Ссылка
		|	И НЕ РеестрУступленныхДенежныхТребованийУступленныеТребования.Отклонено
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РеестрУступленныхДенежныхТребованийГрафикРасчета.Ссылка.Организация,
		|	РеестрУступленныхДенежныхТребованийГрафикРасчета.Ссылка.Контрагент,
		|	РеестрУступленныхДенежныхТребованийГрафикРасчета.Контрагент,
		|	РеестрУступленныхДенежныхТребованийГрафикРасчета.Дата,
		|	РеестрУступленныхДенежныхТребованийГрафикРасчета.Ссылка,
		|	0,
		|	РеестрУступленныхДенежныхТребованийГрафикРасчета.СуммаПоступления,
		|	РеестрУступленныхДенежныхТребованийГрафикРасчета.Комиссия,
		|	РеестрУступленныхДенежныхТребованийГрафикРасчета.СтатьяДвиженияДенежныхСредств,
		|	ЗНАЧЕНИЕ(Перечисление.ЭлементыСтруктурыЗадолженности.ОсновнойДолг)
		|ИЗ
		|	Документ.РеестрУступленныхДенежныхТребований.ГрафикРасчета КАК РеестрУступленныхДенежныхТребованийГрафикРасчета
		|ГДЕ
		|	РеестрУступленныхДенежныхТребованийГрафикРасчета.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Расчет.Организация КАК Организация,
		|	Расчет.Контрагент КАК Контрагент,
		|	Расчет.Дебитор КАК Дебитор,
		|	Расчет.Период КАК Период,
		|	Расчет.Регистратор КАК Регистратор,
		|	СУММА(Расчет.УступленнаяЗадолженность) КАК УступленнаяЗадолженность,
		|	СУММА(Расчет.СуммаПоступления) КАК Получено,
		|	СУММА(Расчет.Комиссия) КАК Комиссия
		|ИЗ
		|	Расчет КАК Расчет
		|
		|СГРУППИРОВАТЬ ПО
		|	Расчет.Организация,
		|	Расчет.Контрагент,
		|	Расчет.Дебитор,
		|	Расчет.Период,
		|	Расчет.Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&Ссылка КАК ПредметГрафика,
		|	&Ссылка КАК ВерсияГрафика,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	Расчет.Период КАК Период,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиДоходовИРасходов.ПустаяСсылка) КАК СтатьяБюджета,
		|	СУММА(Расчет.УступленнаяЗадолженность) КАК Сумма,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Расход) КАК ПриходРасход,
		|	Расчет.Организация КАК Организация,
		|	Расчет.Контрагент КАК Контрагент,
		|	Расчет.ЭлементСтруктурыЗадолженности КАК ЭлементСтруктурыЗадолженности
		|ИЗ
		|	Расчет КАК Расчет
		|ГДЕ
		|	Расчет.УступленнаяЗадолженность > 0
		|
		|СГРУППИРОВАТЬ ПО
		|	Расчет.Период,
		|	Расчет.Организация,
		|	Расчет.Контрагент,
		|	Расчет.ЭлементСтруктурыЗадолженности
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Ссылка,
		|	&Ссылка,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
		|	Расчет.Период,
		|	Расчет.СтатьяБюджета,
		|	СУММА(Расчет.СуммаПоступления),
		|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Приход),
		|	Расчет.Организация,
		|	Расчет.Контрагент,
		|	Расчет.ЭлементСтруктурыЗадолженности
		|ИЗ
		|	Расчет КАК Расчет
		|ГДЕ
		|	Расчет.СуммаПоступления > 0
		|
		|СГРУППИРОВАТЬ ПО
		|	Расчет.Период,
		|	Расчет.СтатьяБюджета,
		|	Расчет.Организация,
		|	Расчет.Контрагент,
		|	Расчет.ЭлементСтруктурыЗадолженности
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Ссылка,
		|	&Ссылка,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
		|	Расчет.Период,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиДоходовИРасходов.ПустаяСсылка),
		|	СУММА(Расчет.Комиссия),
		|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Расход),
		|	Расчет.Организация,
		|	Расчет.Контрагент,
		|	Расчет.ЭлементСтруктурыЗадолженности
		|ИЗ
		|	Расчет КАК Расчет
		|ГДЕ
		|	Расчет.Комиссия > 0
		|
		|СГРУППИРОВАТЬ ПО
		|	Расчет.Период,
		|	Расчет.Организация,
		|	Расчет.Контрагент,
		|	Расчет.ЭлементСтруктурыЗадолженности";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	// регистр РасчетыСКонтрагентамиГрафики
	Движения.РасчетыСКонтрагентамиГрафики.Записывать = Истина;
	Движения.РасчетыСКонтрагентамиГрафики.Загрузить(РезультатыЗапроса[2].Выгрузить());

	// регистр ВерсииРасчетов
	Движения.ВерсииРасчетов.Записывать = Истина;
	Движение = Движения.ВерсииРасчетов.Добавить();
	Движение.Период = Дата;
	Движение.ВерсияГрафика = Ссылка;
	Движение.ПредметГрафика = Ссылка;
	Движение.Организация = Организация;
	
	// регистр ЭффективностьФакторинга
	Движения.ЭффективностьФакторинга.Записывать = Истина;
	
	ВыборкаДетальныеЗаписи = РезультатыЗапроса[1].Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = Движения.ЭффективностьФакторинга.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
	КонецЦикла;
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
		
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Ответственный = Пользователи.ТекущийПользователь();
	Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	ПроведениеСерверУХ.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли
