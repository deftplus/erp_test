#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область ОбработчикиСобытийДокумента


Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	ПроверитьСообщитьДокументНеУникальный(Отказ);
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если ДанныеЗаполнения = Неопределено Тогда
		ПериодРегистрации = ОбщегоНазначенияУХ.ОпределитьПериодПоДате(
			ТекущаяДатаСеанса(), Перечисления.Периодичность.Месяц);
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
	ЗаполнитьСуммыДокумента();
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Ответственный = Пользователи.ТекущийПользователь();
	УИД_ЕИС = "";
КонецПроцедуры


#КонецОбласти


#Область ПрограммныйИнтерфейс


Процедура ЗаполнитьСуммыДокумента() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	1 КАК Номер,
		|	ЕСТЬNULL(СУММА(РасчетыСКонтрагентамиФактОбороты.СуммаПриход), 0) КАК Сумма,
		|	ЕСТЬNULL(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РасчетыСКонтрагентамиФактОбороты.ДоговорКонтрагента), 0) КАК Количество
		|ИЗ
		|	РегистрНакопления.РасчетыСКонтрагентамиФакт.Обороты(&ДатаНачала, &ДатаКонца, Период, Организация = &Организация) КАК РасчетыСКонтрагентамиФактОбороты
		|ГДЕ
		|	&КодТипаОтчета = 1
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	2,
		|	ЕСТЬNULL(СУММА(СтатистикаДоговоровДляЕИС.СуммаДоговораВВалютеРеглУчета), 0),
		|	ЕСТЬNULL(КОЛИЧЕСТВО(СтатистикаДоговоровДляЕИС.Договор), 0)
		|ИЗ
		|	РегистрСведений.СтатистикаДоговоровДляЕИС КАК СтатистикаДоговоровДляЕИС
		|ГДЕ
		|	СтатистикаДоговоровДляЕИС.Организация = &Организация
		|	И СтатистикаДоговоровДляЕИС.ДатаПодписанияДоговора МЕЖДУ &ДатаНачала И &ДатаКонца
		|	И СтатистикаДоговоровДляЕИС.СпособВыбораПоставщика = ЗНАЧЕНИЕ(Перечисление.СпособыВыбораПоставщика.ЗакупкаУЕдинственногоПоставщика)
		|	И НЕ СтатистикаДоговоровДляЕИС.НеВключенВПланЗакупкиЧасть15Ст4ФЗ223
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	3,
		|	ЕСТЬNULL(СУММА(СтатистикаДоговоровДляЕИС.СуммаДоговораВВалютеРеглУчета), 0),
		|	ЕСТЬNULL(КОЛИЧЕСТВО(СтатистикаДоговоровДляЕИС.Договор), 0)
		|ИЗ
		|	РегистрСведений.СтатистикаДоговоровДляЕИС КАК СтатистикаДоговоровДляЕИС
		|ГДЕ
		|	СтатистикаДоговоровДляЕИС.Организация = &Организация
		|	И СтатистикаДоговоровДляЕИС.ДатаПодписанияДоговора МЕЖДУ &ДатаНачала И &ДатаКонца
		|	И НЕ СтатистикаДоговоровДляЕИС.НеВключенВПланЗакупкиЧасть15Ст4ФЗ223
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	4,
		|	ЕСТЬNULL(СУММА(СтатистикаДоговоровДляЕИС.СуммаДоговораВВалютеРеглУчета), 0),
		|	ЕСТЬNULL(КОЛИЧЕСТВО(СтатистикаДоговоровДляЕИС.Договор), 0)
		|ИЗ
		|	РегистрСведений.СтатистикаДоговоровДляЕИС КАК СтатистикаДоговоровДляЕИС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмерыКонтрагентовДляЕИС.СрезПоследних(&ДатаКонца, Размер <> ЗНАЧЕНИЕ(Перечисление.РазмерыКонтрагентовДляЕИС.Крупный)) КАК РазмерыКонтрагентовДляЕИССрезПоследних
		|		ПО (СтатистикаДоговоровДляЕИС.Организация = &Организация)
		|			И (РазмерыКонтрагентовДляЕИССрезПоследних.Контрагент = СтатистикаДоговоровДляЕИС.Договор.Контрагент)
		|			И (СтатистикаДоговоровДляЕИС.ДатаПодписанияДоговора МЕЖДУ &ДатаНачала И &ДатаКонца)
		|ГДЕ
		|	&КодТипаОтчета = 4";
	Запрос.УстановитьПараметр("ДатаНачала", ПериодРегистрации.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаКонца", ПериодРегистрации.ДатаОкончания);
	Запрос.УстановитьПараметр("КодТипаОтчета", ТипОтчетностиОДоговорах.Код);
	Запрос.УстановитьПараметр("Организация", ОрганизацияЗаказчик);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Сумма = ВыборкаДетальныеЗаписи.Сумма;
		Количество = ВыборкаДетальныеЗаписи.Количество;
	Иначе
		Сумма = 0;
		Количество = 0;
	КонецЕсли;
КонецПроцедуры


#КонецОбласти


#Область ВнутреннийПрограммныйИнтерфейс


Процедура ПроверитьСообщитьДокументНеУникальный(Отказ)
	мДокументов = ЦентрализованныеЗакупкиУХ.ПолучитьДокументыПоОтбору(
		"РегистрацияОтчетаОЗаключенныхДоговорах",
		Новый Структура(
			"ОрганизацияЗаказчик, ПериодРегистрации, ТипОтчетностиОДоговорах, Проведен",
			 ОрганизацияЗаказчик, ПериодРегистрации, ТипОтчетностиОДоговорах, Истина),
		Ссылка);
	Если мДокументов.Количество() > 0 Тогда
		ОбщегоНазначенияУХ.СообщитьОбОшибке(
			СтрШаблон(НСтр(
				"ru='Для организации ""%1"" на период ""%2"" уже зарегистрирован отчет"
				 + " о заключенных договорах с типом ""%3"" документом ""%4!""'"),
		 		ОрганизацияЗаказчик,
				ПериодРегистрации,
				ТипОтчетностиОДоговорах,
				мДокументов[0]),
			Отказ,
			,
			СтатусСообщения.Важное);
	КонецЕсли;
КонецПроцедуры


#КонецОбласти


#КонецЕсли