#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	ПриИзмененииКлючевыхРеквизитов();
	УстановитьУсловноеОформление();
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапки
&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	ПриИзмененииКлючевыхРеквизитов();
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаПриИзменении(Элемент)
	ПриИзмененииКлючевыхРеквизитов();
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ПриИзмененииКлючевыхРеквизитов();
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	ПриИзмененииКлючевыхРеквизитов();
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
КонецПроцедуры
#КонецОбласти

#Область ОбработкаСобытийТаблицыКовенантов
&НаКлиенте
Процедура КовенантыВейверНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)			
	РаботаСФайламиКлиент.ОткрытьФормуВыбораФайлов(Объект.Ссылка, Элемент, СтандартнаяОбработка);	
КонецПроцедуры

&НаКлиенте
Процедура КовенантыКовенантНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	СтруктураОтбора = Новый Структура("Ссылка", КовенантыПоДоговору);
	ПараметрыФормы.Вставить("Отбор", СтруктураОтбора);
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ОткрытьФорму("Справочник.Ковенанты.ФормаВыбора", ПараметрыФормы, Элемент,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры
#КонецОбласти

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры
&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ПриИзмененииКлючевыхРеквизитов()
	ОбновитьСписокВыбораПлановыхДат();
	КовенантыПоДоговору.ЗагрузитьЗначения(КовенантыПоДоговору(Объект.ДоговорКонтрагента, Объект.Дата));
КонецПроцедуры	

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Поле: Реакция банка на нарушение
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, Элементы.КовенантыРеакцияБанкаНаНарушение.Имя);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.Ковенанты.СтатусИсполнения",
		ВидСравненияКомпоновкиДанных.НеРавно,
		Перечисления.СтатусыИсполненияКовентантов.Нарушен,
		, 	   // Представление
		Истина // Использование
	);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", Нстр("ru = '<не требуется>'"));
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WindowsЦвета.ТекстНедоступный);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	// Поле: Вейвер
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, Элементы.КовенантыВейвер.Имя);
	ГруппаОтборИЛИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
			ЭлементУО.Отбор.Элементы, 
			"ГруппаВейвер", 
			ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
			ГруппаОтборИЛИ,
			"Объект.Ковенанты.СтатусИсполнения",
			ВидСравненияКомпоновкиДанных.НеРавно,
			Перечисления.СтатусыИсполненияКовентантов.Нарушен);
			
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
			ГруппаОтборИЛИ,
			"Объект.Ковенанты.РеакцияБанкаНаНарушение",
			ВидСравненияКомпоновкиДанных.НеРавно,
			Перечисления.РеакцииБанковНаНарушенияКовенантов.Вейвер);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", Нстр("ru = '<не требуется>'"));
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WindowsЦвета.ТекстНедоступный);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокВыбораПлановыхДат()
	
	МассивДат =	РаботаСДоговорамиКонтрагентовУХ.ПлановыеДатыКовенантовПоДоговору(Объект.ДоговорКонтрагента, Объект.Дата);
	СписокВыбора = Элементы.ПлановаяДата.СписокВыбора;
	СписокВыбора.Очистить();
	Для каждого ПлановаяДата Из МассивДат Цикл
		СписокВыбора.Добавить(ПлановаяДата, Формат(ПлановаяДата, "ДЛФ=D"));
	КонецЦикла;
		
	Если ЗначениеЗаполнено(Объект.ПлановаяДата)
		И СписокВыбора.НайтиПоЗначению(Объект.ПлановаяДата) = Неопределено Тогда
		Объект.ПлановаяДата = Дата(1, 1, 1);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КовенантыПоДоговору(ДоговорКонтрагента, Дата)
	
	ВерсияСоглашения = РегистрыСведений.ВерсииРасчетов.ПолучитьАктуальнуюВерсиюФинансовогоИнструмента(ДоговорКонтрагента, Дата);
	Если ЗначениеЗаполнено(ВерсияСоглашения) Тогда
		Возврат Документы.ВерсияСоглашенияКредит.КовенантыДокумента(ВерсияСоглашения).ВыгрузитьКолонку("Ковенант");
	Иначе
		Возврат Новый Массив;
	КонецЕсли;
	
КонецФункции	
#КонецОбласти