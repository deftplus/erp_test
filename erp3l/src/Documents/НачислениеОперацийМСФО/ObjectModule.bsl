
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСерверУХ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	Документы.НачислениеОперацийМСФО.ПодготовитьПараметрыПроведения(ДополнительныеСвойства, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСерверУХ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	МСФОВызовСервераУХ.СформироватьДвижения(Движения, ДополнительныеСвойства, Отказ);
	ПроведениеСерверУХ.ОбработкаПроведения_ЗаписьИКонтроль(ЭтотОбъект, Отказ);	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ПроведениеСерверУХ.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	ЭтотОбъект.Сумма = ДвиженияНачислений.Итог("Значение");
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = НачалоДня(ТекущаяДатаСеанса());
	Ответственный = ОбщегоНазначенияУХ.ТекущийПользователь();

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
		
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);	
	
	Если ДанныеЗаполнения = Неопределено Тогда
		
		МСФОУХ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда 
		
		Если ДанныеЗаполнения.Свойство("АдресТаблицы") Тогда
			МСФОВызовСервераУХ.ЗаполнитьПоТаблицеЗагрузки(ЭтотОбъект, ДанныеЗаполнения);
		Иначе
			МСФОУХ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения);
		КонецЕсли;
		
	ИначеЕсли ИдентификацияПродуктаУХКлиентСервер.ЭтоУправлениеХолдингом() Тогда
				
		Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка." + "ОперативныйПлан")
			Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка." + "ЗаявкаНаРасход")
			Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка." + "ПланируемыйДоход")
			Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка." + "ЗаявкаНаРасходованиеДенежныхСредств")
			Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка." + "ОжидаемоеПоступлениеДенежныхСредств") Тогда
			
			МСФОУХ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения);
			
			ДокументОснование = ДанныеЗаполнения;
			
			Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка." + "ОперативныйПлан") Тогда
				ЗаполнитьПоДокументамОснованиям(ДанныеЗаполнения);
			Иначе
				
				МодульЗаявкиНаОперации = ОбщегоНазначения.ОбщийМодуль("ЗаявкиНаОперации");//для БМ, НМ
				Инфо = МодульЗаявкиНаОперации.РеквизитыДокументаЗаявка(ТипЗнч(ДанныеЗаполнения));
				Если Инфо.СтатьяБюджета.ЕстьРеквизит И Инфо.СтатьяБюджета.РазмещенВТЧ Тогда
					Для Каждого СтрокаБюджета Из ДанныеЗаполнения[Инфо.СтатьяБюджета.ИмяТЧ] Цикл
						НоваяСтрока = ДвиженияНачислений.Добавить();
						НоваяСтрока.Значение = СтрокаБюджета.Сумма;
					КонецЦикла;
				Иначе
					НоваяСтрока = ДвиженияНачислений.Добавить();
					НоваяСтрока.Значение = ДанныеЗаполнения.СуммаДокумента;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	ПроведениеСерверУХ.ОбработкаУдаленияПроведения(ЭтотОбъект, Отказ);
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	РеквизитыУП = МСФОВызовСервераУХ.ОсновныеРеквизитыУП(Организация, Дата, Сценарий); 

	Если РеквизитыУП.ФормироватьПроводкиМСФО Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ПериодСценария");	
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

Процедура ЗаполнитьПоДокументамОснованиям(ДанныеЗаполнения)

	ПериодСценария = ДанныеЗаполнения.ПериодОтчета;
	ОсновнаяВалюта = ДанныеЗаполнения.ВалютаДокумента;
	Комментарий = НСтр("ru = 'Введен на основании '") + Строка(ДанныеЗаполнения);
	
	Для Каждого СтрокаБюджета Из ДанныеЗаполнения.ДвиженияОперации Цикл
		НоваяСтрока = ДвиженияНачислений.Добавить();
		НоваяСтрока.Значение = СтрокаБюджета.Сумма;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
