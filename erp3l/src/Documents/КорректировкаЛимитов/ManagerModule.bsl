#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс
	
#Область СтандартныеПодсистемыКоманды

// Добавляет команду создания объекта на основании.
// 
// Параметры:
// 	КомандыСозданияНаОсновании - ТаблицаЗначений - перечень команд
// Возвращаемое значение:
// 	Неопределено, СтрокаТаблицыЗначений - Добавленная Команда.
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	МетаданныеДокумента = ПустаяСсылка().Метаданные();
	
	Если ПравоДоступа("Добавление", МетаданныеДокумента) Тогда
		
		КомандаСоздатьНаОсновании						= КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер				= МетаданныеДокумента.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление			= ОбщегоНазначения.ПредставлениеОбъекта(МетаданныеДокумента);
		КомандаСоздатьНаОсновании.РежимЗаписи			= "Записывать";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Заполняет список команд создания на основании.
// 
// Параметры:
// 	КомандыСозданияНаОсновании - ТаблицаЗначений - перечень команд
// 	Параметры - Структура - параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаполнитьПоДокументуОснованию(Объект, Источник) Экспорт
	
	Объект.Требуется.Очистить();
	Объект.ОбеспечитьЗаСчет.Очистить();
	Если ЗначениеЗаполнено(Источник) Тогда
		ТипИсточника = ТипЗнч(Источник);
		Если ТипИсточника = Тип("ДокументСсылка.ЗаявкаНаКорректировкуЛимитов") Тогда
			Объект.Основание = Источник;
			Объект.ДокументДляОбеспечения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник, "Основание");
			ЗаполнитьПоЗаявкеНаКорректировку(Объект);
		Иначе
			
			Если Источник.Проведен = ИСТИНА Тогда
				ТекстСообщения = НСтр("ru = 'Документ основание должен быть не проведенным'");
				ВызватьИсключение ТекстСообщения;
			КонецЕсли;
			
			Объект.Основание = Источник;
			Объект.ДокументДляОбеспечения = Источник;
			ЗаполнитьПоДокументуОбеспечения(Объект);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТребуетсяПоОснованию(Объект) Экспорт
	
	Объект.Требуется.Очистить();
	
	ТипИсточника = ТипЗнч(Объект.Основание);
	Если ТипИсточника = Тип("ДокументСсылка.ЗаявкаНаКорректировкуЛимитов") Тогда
		ЗаполнитьТребуетсяПоДаннымРегистраКОбеспечениюПоВидуБюджетаВалютеГоду(Объект);
	ИначеЕсли ЗначениеЗаполнено(Объект.Основание) Тогда
		ТребуемыеЛимиты = ПолучитьТребуемыеЛимитыПоПланамДокумента(Объект.Основание, Объект.Дата);
		ЗаполнитьПоТребуемымЛимитамПоВидуБюджетаВалютеГоду(Объект, ТребуемыеЛимиты);
	КонецЕсли;
	
КонецПроцедуры

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаДвиженияЛимитовПоБюджетам(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСерверОПК.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Функция ПолучитьВидБюджетаВалютуГодПоОснованию(Основание, Регистратор) Экспорт
	
	ТипИсточника = ТипЗнч(Основание);
	Если ТипИсточника = Тип("ДокументСсылка.ЗаявкаНаКорректировкуЛимитов") Тогда
		Результат = ПолучитьВидБюджетаВалютуГодПоРегистру(Основание, Регистратор);
	ИначеЕсли ЗначениеЗаполнено(Основание) Тогда
		ТребуемыеЛимиты = ПолучитьТребуемыеЛимитыПоПланамДокумента(Основание, ТекущаяДатаСеанса());
		Результат = ПолучитьВидБюджетаВалютуГодПоТребуемымЛимитам(ТребуемыеЛимиты);
	Иначе
		Результат = Новый ТаблицаЗначений;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыЛимитирования(Объект) Экспорт
	
	ПараметрыЛимитирования = ПолучитьПараметрыЛимитирования(Объект.ВидБюджета, Объект.ГодЛимитирования);
	
	ЗаполнитьЗначенияСвойств(Объект, ПараметрыЛимитирования, "ИспользоватьЛимитирование, ПериодичностьЛимитирования, СпособОпределенияВалютыЛимитирования");
	
	Если Объект.СпособОпределенияВалютыЛимитирования = Перечисления.СпособыОпределенияВалютыЛимитирования.ВалютаУпрУчета Тогда
		Объект.Валюта = ВстраиваниеОПКПереопределяемый.КонстантаВалютаУправленческогоУчета();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТребуемыеЛимитыПоПланамДокумента(Основание, ДатаСовершенияОперации)
	
	Если НЕ ЗначениеЗаполнено(Основание) Тогда
		Возврат неопределено;
	КонецЕсли;
	
	//
	Менеджер = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Основание);
	Попытка
		ТаблицаПланов = Менеджер.ПланыДокумента(Основание);
	Исключение
	    Возврат неопределено;
	КонецПопытки;
	
	//
	ОбеспечиваетсяДоговор = ВстраиваниеОПКПереопределяемый.ЭтоВерсияСоглашения(Основание);
	
	//
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", Основание);
	Запрос.УстановитьПараметр("ОбеспечиваетсяДоговор", ОбеспечиваетсяДоговор);
	
	Если ТаблицаПланов.Колонки.Найти("Период") = неопределено Тогда
		ТаблицаПланов.Колонки.Добавить("Период", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
		ТаблицаПланов.ЗаполнитьЗначения(ДатаСовершенияОперации, "Период");
	КонецЕсли;
	
	// Формируем колонку ДатаОперации
	Если ТаблицаПланов.Колонки.Найти("ДатаОперации") = неопределено Тогда
		ТаблицаПланов.Колонки.Добавить("ДатаОперации", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	КонецЕсли;
	ТаблицаПланов.ЗагрузитьКолонку(ТаблицаПланов.ВыгрузитьКолонку("Период"), "ДатаОперации");
	
	// Формируем колонки ресурсы
	ТаблицаПланов.Колонки.Добавить("Лимит", ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	ТаблицаПланов.Колонки.Добавить("Зарезервировано", ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	ТаблицаПланов.Колонки.Сумма.Имя = "Заявлено";
	ТаблицаПланов.Колонки.Добавить("Исполнено", ОбщегоНазначения.ОписаниеТипаЧисло(15,2));
	
	ОбщегоНазначенияОПК.ЗагрузитьТаблицуВоВременнуюТаблицуЗапроса(Запрос, "ВТ_ТаблицаПлановССуммамиЛимитирования", ТаблицаПланов);
	КонтрольЛимитовУХ.ПолучитьТаблицуЛимитов(Запрос, ДатаСовершенияОперации, Истина);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЛимитыПоБюджетамОбороты.Предназначение КАК Предназначение,
	|	ЛимитыПоБюджетамОбороты.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ЛимитыПоБюджетамОбороты.ЦФО КАК ЦФО,
	|	ЛимитыПоБюджетамОбороты.Проект КАК Проект,
	|	ЛимитыПоБюджетамОбороты.СтатьяБюджета КАК СтатьяБюджета,
	|	ЛимитыПоБюджетамОбороты.Аналитика1 КАК Аналитика1,
	|	ЛимитыПоБюджетамОбороты.Аналитика2 КАК Аналитика2,
	|	ЛимитыПоБюджетамОбороты.Аналитика3 КАК Аналитика3,
	|	ЛимитыПоБюджетамОбороты.Аналитика4 КАК Аналитика4,
	|	ЛимитыПоБюджетамОбороты.Аналитика5 КАК Аналитика5,
	|	ЛимитыПоБюджетамОбороты.Аналитика6 КАК Аналитика6,
	|	ЛимитыПоБюджетамОбороты.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА ЛимитыПоБюджетамОбороты.ЛимитОборот + ЛимитыПоБюджетамОбороты.КорректировкаОборот - ЛимитыПоБюджетамОбороты.ЗарезервированоОборот - ЛимитыПоБюджетамОбороты.ЗаявленоОборот - ЛимитыПоБюджетамОбороты.ИсполненоОборот < 0
	|			ТОГДА 0
	|		ИНАЧЕ ЛимитыПоБюджетамОбороты.ЛимитОборот + ЛимитыПоБюджетамОбороты.КорректировкаОборот - ЛимитыПоБюджетамОбороты.ЗарезервированоОборот - ЛимитыПоБюджетамОбороты.ЗаявленоОборот - ЛимитыПоБюджетамОбороты.ИсполненоОборот
	|	КОНЕЦ КАК Свободно
	|ПОМЕСТИТЬ ВТ_Свободно
	|ИЗ
	|	РегистрНакопления.ЛимитыПоБюджетам.Обороты(
	|			,
	|			,
	|			,
	|			(Предназначение, ПериодЛимитирования, Валюта, ЦФО, Проект, СтатьяБюджета, Аналитика1, Аналитика2, Аналитика3, Аналитика4, Аналитика5, Аналитика6) В
	|				(ВЫБРАТЬ
	|					Данные.Предназначение,
	|					Данные.ПериодЛимитирования,
	|					Данные.Валюта,
	|					Данные.ЦФО,
	|					Данные.Проект,
	|					Данные.СтатьяБюджета,
	|					Данные.Аналитика1,
	|					Данные.Аналитика2,
	|					Данные.Аналитика3,
	|					Данные.Аналитика4,
	|					Данные.Аналитика5,
	|					Данные.Аналитика6
	|				ИЗ
	|					ВТ_ДвиженияЛимитыПоБюджетам КАК Данные)) КАК ЛимитыПоБюджетамОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЛимитыПоБюджетамОбороты.Предназначение КАК Предназначение,
	|	ЛимитыПоБюджетамОбороты.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ЛимитыПоБюджетамОбороты.ЦФО КАК ЦФО,
	|	ЛимитыПоБюджетамОбороты.Проект КАК Проект,
	|	ЛимитыПоБюджетамОбороты.СтатьяБюджета КАК СтатьяБюджета,
	|	ЛимитыПоБюджетамОбороты.Аналитика1 КАК Аналитика1,
	|	ЛимитыПоБюджетамОбороты.Аналитика2 КАК Аналитика2,
	|	ЛимитыПоБюджетамОбороты.Аналитика3 КАК Аналитика3,
	|	ЛимитыПоБюджетамОбороты.Аналитика4 КАК Аналитика4,
	|	ЛимитыПоБюджетамОбороты.Аналитика5 КАК Аналитика5,
	|	ЛимитыПоБюджетамОбороты.Аналитика6 КАК Аналитика6,
	|	ЛимитыПоБюджетамОбороты.Валюта КАК Валюта,
	|	ЛимитыПоБюджетамОбороты.ЗаявленоОборот + ЛимитыПоБюджетамОбороты.КОбеспечениюОборот КАК НаОбеспечении
	|ПОМЕСТИТЬ ВТ_ОбеспеченоНаОбеспечении
	|ИЗ
	|	РегистрНакопления.ЛимитыПоБюджетам.Обороты(, , , ДокументПланирования = &ССылка) КАК ЛимитыПоБюджетамОбороты
	|ГДЕ
	|	&ОбеспечиваетсяДоговор = ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЛимитыПоБюджетамОбороты.Предназначение,
	|	ЛимитыПоБюджетамОбороты.ПериодЛимитирования,
	|	ЛимитыПоБюджетамОбороты.ЦФО,
	|	ЛимитыПоБюджетамОбороты.Проект,
	|	ЛимитыПоБюджетамОбороты.СтатьяБюджета,
	|	ЛимитыПоБюджетамОбороты.Аналитика1,
	|	ЛимитыПоБюджетамОбороты.Аналитика2,
	|	ЛимитыПоБюджетамОбороты.Аналитика3,
	|	ЛимитыПоБюджетамОбороты.Аналитика4,
	|	ЛимитыПоБюджетамОбороты.Аналитика5,
	|	ЛимитыПоБюджетамОбороты.Аналитика6,
	|	ЛимитыПоБюджетамОбороты.Валюта,
	|	ЛимитыПоБюджетамОбороты.КОбеспечениюОборот
	|ИЗ
	|	РегистрНакопления.ЛимитыПоБюджетам.Обороты(, , , ДокументПланирования = &ССылка) КАК ЛимитыПоБюджетамОбороты
	|ГДЕ
	|	&ОбеспечиваетсяДоговор = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыБюджетов.Ссылка КАК ВидБюджета,
	|	ВТ_Лимиты.Предназначение КАК Предназначение,
	|	НАЧАЛОПЕРИОДА(ВТ_Лимиты.ПериодЛимитирования.ДатаНачала, ГОД) КАК ГодЛимитирования,
	|	ВТ_Лимиты.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ВТ_Лимиты.ЦФО КАК ЦФО,
	|	ВТ_Лимиты.Проект КАК Проект,
	|	ВТ_Лимиты.СтатьяБюджета КАК СтатьяБюджета,
	|	ВТ_Лимиты.Аналитика1 КАК Аналитика1,
	|	ВТ_Лимиты.Аналитика2 КАК Аналитика2,
	|	ВТ_Лимиты.Аналитика3 КАК Аналитика3,
	|	ВТ_Лимиты.Аналитика4 КАК Аналитика4,
	|	ВТ_Лимиты.Аналитика5 КАК Аналитика5,
	|	ВТ_Лимиты.Аналитика6 КАК Аналитика6,
	|	ВТ_Лимиты.Валюта КАК Валюта,
	|	ВТ_Лимиты.Заявлено КАК Сумма,
	|	ЕСТЬNULL(ВТ_Свободно.Свободно, 0) КАК Свободно,
	|	ЕСТЬNULL(ВТ_ОбеспеченоНаОбеспечении.НаОбеспечении, 0) КАК НаОбеспечении,
	|	ВЫБОР
	|		КОГДА ВТ_Лимиты.Заявлено - ЕСТЬNULL(ВТ_ОбеспеченоНаОбеспечении.НаОбеспечении, 0) > ЕСТЬNULL(ВТ_Свободно.Свободно, 0)
	|			ТОГДА ВТ_Лимиты.Заявлено - ЕСТЬNULL(ВТ_ОбеспеченоНаОбеспечении.НаОбеспечении, 0) - ЕСТЬNULL(ВТ_Свободно.Свободно, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КОбеспечению
	|ИЗ
	|	ВТ_ДвиженияЛимитыПоБюджетам КАК ВТ_Лимиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ВидыБюджетов КАК ВидыБюджетов
	|		ПО ВТ_Лимиты.Предназначение = ВидыБюджетов.Предназначение
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Свободно КАК ВТ_Свободно
	|		ПО ВТ_Лимиты.Предназначение = ВТ_Свободно.Предназначение
	|			И ВТ_Лимиты.ПериодЛимитирования = ВТ_Свободно.ПериодЛимитирования
	|			И ВТ_Лимиты.Валюта = ВТ_Свободно.Валюта
	|			И ВТ_Лимиты.ЦФО = ВТ_Свободно.ЦФО
	|			И ВТ_Лимиты.Проект = ВТ_Свободно.Проект
	|			И ВТ_Лимиты.СтатьяБюджета = ВТ_Свободно.СтатьяБюджета
	|			И ВТ_Лимиты.Аналитика1 = ВТ_Свободно.Аналитика1
	|			И ВТ_Лимиты.Аналитика2 = ВТ_Свободно.Аналитика2
	|			И ВТ_Лимиты.Аналитика3 = ВТ_Свободно.Аналитика3
	|			И ВТ_Лимиты.Аналитика4 = ВТ_Свободно.Аналитика4
	|			И ВТ_Лимиты.Аналитика5 = ВТ_Свободно.Аналитика5
	|			И ВТ_Лимиты.Аналитика6 = ВТ_Свободно.Аналитика6
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОбеспеченоНаОбеспечении КАК ВТ_ОбеспеченоНаОбеспечении
	|		ПО ВТ_Лимиты.Предназначение = ВТ_ОбеспеченоНаОбеспечении.Предназначение
	|			И ВТ_Лимиты.ПериодЛимитирования = ВТ_ОбеспеченоНаОбеспечении.ПериодЛимитирования
	|			И ВТ_Лимиты.Валюта = ВТ_ОбеспеченоНаОбеспечении.Валюта
	|			И ВТ_Лимиты.ЦФО = ВТ_ОбеспеченоНаОбеспечении.ЦФО
	|			И ВТ_Лимиты.Проект = ВТ_ОбеспеченоНаОбеспечении.Проект
	|			И ВТ_Лимиты.СтатьяБюджета = ВТ_ОбеспеченоНаОбеспечении.СтатьяБюджета
	|			И ВТ_Лимиты.Аналитика1 = ВТ_ОбеспеченоНаОбеспечении.Аналитика1
	|			И ВТ_Лимиты.Аналитика2 = ВТ_ОбеспеченоНаОбеспечении.Аналитика2
	|			И ВТ_Лимиты.Аналитика3 = ВТ_ОбеспеченоНаОбеспечении.Аналитика3
	|			И ВТ_Лимиты.Аналитика4 = ВТ_ОбеспеченоНаОбеспечении.Аналитика4
	|			И ВТ_Лимиты.Аналитика5 = ВТ_ОбеспеченоНаОбеспечении.Аналитика5
	|			И ВТ_Лимиты.Аналитика6 = ВТ_ОбеспеченоНаОбеспечении.Аналитика6
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ВТ_Лимиты.Заявлено - ЕСТЬNULL(ВТ_ОбеспеченоНаОбеспечении.НаОбеспечении, 0) > ЕСТЬNULL(ВТ_Свободно.Свободно, 0)
	|				ТОГДА ВТ_Лимиты.Заявлено - ЕСТЬNULL(ВТ_ОбеспеченоНаОбеспечении.НаОбеспечении, 0) - ЕСТЬNULL(ВТ_Свободно.Свободно, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ > 0";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Дата КАК Дата,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.Валюта КАК Валюта,
	|	ДанныеДокумента.Основание КАК Основание,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.СуммаДокумента КАК СуммаДокумента,
	|	ДанныеДокумента.Обеспечено КАК Обеспечено,
	|	ДанныеДокумента.ВидБюджета КАК ВидБюджета,
	|	ДанныеДокумента.ДокументДляОбеспечения КАК ДокументДляОбеспечения,
	|	ДанныеДокумента.ДокументДляОбеспечения КАК ДокументПланирования,
	|	ДанныеДокумента.ДокументДляОбеспечения.ДокументПланирования КАК ДокументРезервирования,
	|	ДанныеДокумента.ВидБюджета.Предназначение КАК Предназначение
	|ИЗ
	|	Документ.КорректировкаЛимитов КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТаблицаРеквизитов = Запрос.Выполнить().Выгрузить();
	Реквизиты = ТаблицаРеквизитов[0];
	
	Для Каждого Колонка Из ТаблицаРеквизитов.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ЕстьОснование", ЗначениеЗаполнено(Запрос.Параметры.Основание));
	Запрос.УстановитьПараметр("ЕстьДокументДляОбеспечения", ЗначениеЗаполнено(Запрос.Параметры.ДокументДляОбеспечения));
	Запрос.УстановитьПараметр("ОснованиеЗаявкаНаКорректировку", ЗначениеЗаполнено(Запрос.Параметры.Основание) И ТипЗнч(Запрос.Параметры.Основание) = Тип("ДокументСсылка.ЗаявкаНаКорректировкуЛимитов"));
	Запрос.УстановитьПараметр("ОбеспечиваетсяДоговор", ТипЗнч(Запрос.Параметры.ДокументДляОбеспечения) = Тип("ДокументСсылка.ВерсияСоглашенияКоммерческийДоговор"));
	
КонецПроцедуры

Процедура ТекстЗапросаДвиженияЛимитовПоБюджетам(Запрос, ТекстыЗапроса, Регистры)
	
	ТекстЗапроса1 = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Предназначение КАК Предназначение,
	|	КорректировкаЛимитовТребуется.ПериодЛимитирования КАК ПериодЛимитирования,
	|	&ДокументРезервирования КАК ДокументРезервирования,
	|	&ДокументПланирования КАК ДокументПланирования,
	|	КорректировкаЛимитовТребуется.Ссылка.Валюта КАК Валюта,
	|	КорректировкаЛимитовТребуется.ЦФО КАК ЦФО,
	|	КорректировкаЛимитовТребуется.Проект КАК Проект,
	|	КорректировкаЛимитовТребуется.СтатьяБюджета КАК СтатьяБюджета,
	|	КорректировкаЛимитовТребуется.Аналитика1 КАК Аналитика1,
	|	КорректировкаЛимитовТребуется.Аналитика2 КАК Аналитика2,
	|	КорректировкаЛимитовТребуется.Аналитика3 КАК Аналитика3,
	|	КорректировкаЛимитовТребуется.Аналитика4 КАК Аналитика4,
	|	КорректировкаЛимитовТребуется.Аналитика5 КАК Аналитика5,
	|	КорректировкаЛимитовТребуется.Аналитика6 КАК Аналитика6,
	|	0 КАК Корректировка,
	|	КорректировкаЛимитовТребуется.КОбеспечению КАК Заявлено,
	|	0 КАК КОбеспечению
	|ПОМЕСТИТЬ ВТ_ЛимитыПоБюджетамПредварительные
	|ИЗ
	|	Документ.КорректировкаЛимитов.Требуется КАК КорректировкаЛимитовТребуется
	|ГДЕ
	|	КорректировкаЛимитовТребуется.Ссылка = &Ссылка
	|	И &ЕстьДокументДляОбеспечения = ИСТИНА
	|	И &ОбеспечиваетсяДоговор = ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	&Предназначение,
	|	КорректировкаЛимитовТребуется.ПериодЛимитирования,
	|	&ДокументРезервирования,
	|	&Основание,
	|	КорректировкаЛимитовТребуется.Ссылка.Валюта,
	|	КорректировкаЛимитовТребуется.ЦФО,
	|	КорректировкаЛимитовТребуется.Проект,
	|	КорректировкаЛимитовТребуется.СтатьяБюджета,
	|	КорректировкаЛимитовТребуется.Аналитика1,
	|	КорректировкаЛимитовТребуется.Аналитика2,
	|	КорректировкаЛимитовТребуется.Аналитика3,
	|	КорректировкаЛимитовТребуется.Аналитика4,
	|	КорректировкаЛимитовТребуется.Аналитика5,
	|	КорректировкаЛимитовТребуется.Аналитика6,
	|	0,
	|	0,
	|	-КорректировкаЛимитовТребуется.КОбеспечению
	|ИЗ
	|	Документ.КорректировкаЛимитов.Требуется КАК КорректировкаЛимитовТребуется
	|ГДЕ
	|	КорректировкаЛимитовТребуется.Ссылка = &Ссылка
	|	И &ОснованиеЗаявкаНаКорректировку = ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	&Предназначение,
	|	КорректировкаЛимитовТребуется.ПериодЛимитирования,
	|	ЗНАЧЕНИЕ(Документ.ОперативныйПлан.ПустаяСсылка),
	|	НЕОПРЕДЕЛЕНО,
	|	КорректировкаЛимитовТребуется.Ссылка.Валюта,
	|	КорректировкаЛимитовТребуется.ЦФО,
	|	КорректировкаЛимитовТребуется.Проект,
	|	КорректировкаЛимитовТребуется.СтатьяБюджета,
	|	КорректировкаЛимитовТребуется.Аналитика1,
	|	КорректировкаЛимитовТребуется.Аналитика2,
	|	КорректировкаЛимитовТребуется.Аналитика3,
	|	КорректировкаЛимитовТребуется.Аналитика4,
	|	КорректировкаЛимитовТребуется.Аналитика5,
	|	КорректировкаЛимитовТребуется.Аналитика6,
	|	КорректировкаЛимитовТребуется.КОбеспечению,
	|	0,
	|	0
	|ИЗ
	|	Документ.КорректировкаЛимитов.Требуется КАК КорректировкаЛимитовТребуется
	|ГДЕ
	|	КорректировкаЛимитовТребуется.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	&Предназначение,
	|	КорректировкаЛимитовОбеспечитьЗаСчет.ПериодЛимитирования,
	|	ЗНАЧЕНИЕ(Документ.ОперативныйПлан.ПустаяСсылка),
	|	НЕОПРЕДЕЛЕНО,
	|	КорректировкаЛимитовОбеспечитьЗаСчет.Ссылка.Валюта,
	|	КорректировкаЛимитовОбеспечитьЗаСчет.ЦФО,
	|	КорректировкаЛимитовОбеспечитьЗаСчет.Проект,
	|	КорректировкаЛимитовОбеспечитьЗаСчет.СтатьяБюджета,
	|	КорректировкаЛимитовОбеспечитьЗаСчет.Аналитика1,
	|	КорректировкаЛимитовОбеспечитьЗаСчет.Аналитика2,
	|	КорректировкаЛимитовОбеспечитьЗаСчет.Аналитика3,
	|	КорректировкаЛимитовОбеспечитьЗаСчет.Аналитика4,
	|	КорректировкаЛимитовОбеспечитьЗаСчет.Аналитика5,
	|	КорректировкаЛимитовОбеспечитьЗаСчет.Аналитика6,
	|	-КорректировкаЛимитовОбеспечитьЗаСчет.Сумма,
	|	0,
	|	0
	|ИЗ
	|	Документ.КорректировкаЛимитов.ОбеспечитьЗаСчет КАК КорректировкаЛимитовОбеспечитьЗаСчет
	|ГДЕ
	|	КорректировкаЛимитовОбеспечитьЗаСчет.Ссылка = &Ссылка
	|	И КорректировкаЛимитовОбеспечитьЗаСчет.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировкаЛимитов.Перенос)";
	
	ТекстЗапроса2 = 
	"ВЫБРАТЬ
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.Период КАК Период,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.ВидБюджета КАК ВидБюджета,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.ВидБюджета.Предназначение КАК Предназначение,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.СтатьяБюджета КАК СтатьяБюджета,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.КонтролироватьСтатью КАК КонтролироватьСтатью,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.КонтролироватьАналитику1 КАК КонтролироватьАналитику1,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.КонтролироватьАналитику2 КАК КонтролироватьАналитику2,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.КонтролироватьАналитику3 КАК КонтролироватьАналитику3,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.КонтролироватьАналитику4 КАК КонтролироватьАналитику4,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.КонтролироватьАналитику5 КАК КонтролироватьАналитику5,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.КонтролироватьАналитику6 КАК КонтролироватьАналитику6
	|ПОМЕСТИТЬ ВТ_ПараметрыКонтроляСтатейРегистр
	|ИЗ
	|	РегистрСведений.ПараметрыКонтроляЛимитаСтатейБюджетов КАК ПараметрыКонтроляЛимитаСтатейБюджетов
	|ГДЕ
	|	(ПараметрыКонтроляЛимитаСтатейБюджетов.ВидБюджета.Предназначение, ПараметрыКонтроляЛимитаСтатейБюджетов.СтатьяБюджета) В
	|			(ВЫБРАТЬ
	|				ВТ_ЛимитыПоБюджетамПредварительные.Предназначение КАК Предназначение,
	|				ВТ_ЛимитыПоБюджетамПредварительные.СтатьяБюджета КАК СтатьяБюджета
	|			ИЗ
	|				ВТ_ЛимитыПоБюджетамПредварительные КАК ВТ_ЛимитыПоБюджетамПредварительные)";
	
	ТекстЗапроса3 = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ЛимитыПоБюджетамПредварительные.Предназначение КАК Предназначение,
	|	ВТ_ЛимитыПоБюджетамПредварительные.СтатьяБюджета КАК СтатьяБюджета,
	|	ВТ_ЛимитыПоБюджетамПредварительные.ПериодЛимитирования КАК ПериодЛимитирования,
	|	НАЧАЛОПЕРИОДА(ВТ_ЛимитыПоБюджетамПредварительные.ПериодЛимитирования.ДатаНачала, ГОД) КАК ПараметрыГодДействия
	|ПОМЕСТИТЬ ВТ_ТребуемыеВидБюджетаСтатьяПериод
	|ИЗ
	|	ВТ_ЛимитыПоБюджетамПредварительные КАК ВТ_ЛимитыПоБюджетамПредварительные";
	
	ТекстЗапроса4 = 
	"ВЫБРАТЬ
	|	ВТ_ТребуемыеВидБюджетаСтатьяПериод.Предназначение КАК Предназначение,
	|	ВТ_ТребуемыеВидБюджетаСтатьяПериод.СтатьяБюджета КАК СтатьяБюджета,
	|	ВТ_ТребуемыеВидБюджетаСтатьяПериод.ПериодЛимитирования КАК ПериодЛимитирования,
	|	МАКСИМУМ(ВТ_ПараметрыКонтроляСтатейРегистр.Период) КАК Период
	|ПОМЕСТИТЬ ВТ_ПараметрыКонтроляПериодыДействия
	|ИЗ
	|	ВТ_ТребуемыеВидБюджетаСтатьяПериод КАК ВТ_ТребуемыеВидБюджетаСтатьяПериод
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПараметрыКонтроляСтатейРегистр КАК ВТ_ПараметрыКонтроляСтатейРегистр
	|		ПО ВТ_ТребуемыеВидБюджетаСтатьяПериод.Предназначение = ВТ_ПараметрыКонтроляСтатейРегистр.Предназначение
	|			И ВТ_ТребуемыеВидБюджетаСтатьяПериод.СтатьяБюджета = ВТ_ПараметрыКонтроляСтатейРегистр.СтатьяБюджета
	|			И ВТ_ТребуемыеВидБюджетаСтатьяПериод.ПараметрыГодДействия >= ВТ_ПараметрыКонтроляСтатейРегистр.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТребуемыеВидБюджетаСтатьяПериод.Предназначение,
	|	ВТ_ТребуемыеВидБюджетаСтатьяПериод.СтатьяБюджета,
	|	ВТ_ТребуемыеВидБюджетаСтатьяПериод.ПериодЛимитирования";
	
	ТекстЗапроса5 = 
	"ВЫБРАТЬ
	|	ПараметрыЛимитирования.ВидБюджета.Предназначение КАК Предназначение,
	|	ПараметрыЛимитирования.Период КАК Период,
	|	ПараметрыЛимитирования.ИспользоватьЛимитирование КАК ИспользоватьЛимитирование,
	|	ВТ_ПараметрыКонтроляСтатейРегистр.СтатьяБюджета КАК СтатьяБюджета,
	|	ВТ_ПараметрыКонтроляСтатейРегистр.КонтролироватьСтатью КАК КонтролироватьСтатью,
	|	ВТ_ПараметрыКонтроляСтатейРегистр.КонтролироватьАналитику1 КАК КонтролироватьАналитику1,
	|	ВТ_ПараметрыКонтроляСтатейРегистр.КонтролироватьАналитику2 КАК КонтролироватьАналитику2,
	|	ВТ_ПараметрыКонтроляСтатейРегистр.КонтролироватьАналитику3 КАК КонтролироватьАналитику3,
	|	ВТ_ПараметрыКонтроляСтатейРегистр.КонтролироватьАналитику4 КАК КонтролироватьАналитику4,
	|	ВТ_ПараметрыКонтроляСтатейРегистр.КонтролироватьАналитику5 КАК КонтролироватьАналитику5,
	|	ВТ_ПараметрыКонтроляСтатейРегистр.КонтролироватьАналитику6 КАК КонтролироватьАналитику6,
	|	ВТ_ПараметрыКонтроляПериодыДействия.ПериодЛимитирования КАК ПериодЛимитирования
	|ПОМЕСТИТЬ ВТ_ПараметрыЛимитированияСтатейБюджетов
	|ИЗ
	|	РегистрСведений.ПараметрыЛимитирования КАК ПараметрыЛимитирования
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПараметрыКонтроляПериодыДействия КАК ВТ_ПараметрыКонтроляПериодыДействия
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПараметрыКонтроляСтатейРегистр КАК ВТ_ПараметрыКонтроляСтатейРегистр
	|			ПО ВТ_ПараметрыКонтроляПериодыДействия.Предназначение = ВТ_ПараметрыКонтроляСтатейРегистр.Предназначение
	|				И ВТ_ПараметрыКонтроляПериодыДействия.СтатьяБюджета = ВТ_ПараметрыКонтроляСтатейРегистр.СтатьяБюджета
	|				И ВТ_ПараметрыКонтроляПериодыДействия.Период = ВТ_ПараметрыКонтроляСтатейРегистр.Период
	|				И (ВТ_ПараметрыКонтроляСтатейРегистр.КонтролироватьСтатью = ИСТИНА)
	|		ПО ПараметрыЛимитирования.ВидБюджета.Предназначение = ВТ_ПараметрыКонтроляПериодыДействия.Предназначение
	|			И ПараметрыЛимитирования.Период = ВТ_ПараметрыКонтроляПериодыДействия.Период
	|			И (ПараметрыЛимитирования.ИспользоватьЛимитирование = ИСТИНА)";
	
	ТекстЗапроса6 = 
	"ВЫБРАТЬ
	|	ВТ_ЛимитыПоБюджетамПредварительные.Период КАК Период,
	|	ВТ_ЛимитыПоБюджетамПредварительные.Предназначение КАК Предназначение,
	|	ВТ_ЛимитыПоБюджетамПредварительные.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ВТ_ЛимитыПоБюджетамПредварительные.ДокументРезервирования КАК ДокументРезервирования,
	|	ВТ_ЛимитыПоБюджетамПредварительные.ДокументПланирования КАК ДокументПланирования,
	|	ВТ_ЛимитыПоБюджетамПредварительные.Валюта КАК Валюта,
	|	ВТ_ЛимитыПоБюджетамПредварительные.ЦФО КАК ЦФО,
	|	ВТ_ЛимитыПоБюджетамПредварительные.Проект КАК Проект,
	|	ВТ_ЛимитыПоБюджетамПредварительные.СтатьяБюджета КАК СтатьяБюджета,
	|	ВЫБОР
	|		КОГДА ВТ_ПараметрыЛимитированияСтатейБюджетов.КонтролироватьАналитику1 = ЛОЖЬ
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВТ_ЛимитыПоБюджетамПредварительные.Аналитика1
	|	КОНЕЦ КАК Аналитика1,
	|	ВЫБОР
	|		КОГДА ВТ_ПараметрыЛимитированияСтатейБюджетов.КонтролироватьАналитику2 = ЛОЖЬ
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВТ_ЛимитыПоБюджетамПредварительные.Аналитика2
	|	КОНЕЦ КАК Аналитика2,
	|	ВЫБОР
	|		КОГДА ВТ_ПараметрыЛимитированияСтатейБюджетов.КонтролироватьАналитику3 = ЛОЖЬ
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВТ_ЛимитыПоБюджетамПредварительные.Аналитика3
	|	КОНЕЦ КАК Аналитика3,
	|	ВЫБОР
	|		КОГДА ВТ_ПараметрыЛимитированияСтатейБюджетов.КонтролироватьАналитику4 = ЛОЖЬ
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВТ_ЛимитыПоБюджетамПредварительные.Аналитика4
	|	КОНЕЦ КАК Аналитика4,
	|	ВЫБОР
	|		КОГДА ВТ_ПараметрыЛимитированияСтатейБюджетов.КонтролироватьАналитику5 = ЛОЖЬ
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВТ_ЛимитыПоБюджетамПредварительные.Аналитика5
	|	КОНЕЦ КАК Аналитика5,
	|	ВЫБОР
	|		КОГДА ВТ_ПараметрыЛимитированияСтатейБюджетов.КонтролироватьАналитику6 = ЛОЖЬ
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВТ_ЛимитыПоБюджетамПредварительные.Аналитика6
	|	КОНЕЦ КАК Аналитика6,
	|	ВТ_ЛимитыПоБюджетамПредварительные.Корректировка КАК Корректировка,
	|	ВТ_ЛимитыПоБюджетамПредварительные.Заявлено КАК Заявлено,
	|	ВТ_ЛимитыПоБюджетамПредварительные.КОбеспечению КАК КОбеспечению
	|ИЗ
	|	ВТ_ПараметрыЛимитированияСтатейБюджетов КАК ВТ_ПараметрыЛимитированияСтатейБюджетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЛимитыПоБюджетамПредварительные КАК ВТ_ЛимитыПоБюджетамПредварительные
	|		ПО ВТ_ПараметрыЛимитированияСтатейБюджетов.Предназначение = ВТ_ЛимитыПоБюджетамПредварительные.Предназначение
	|			И ВТ_ПараметрыЛимитированияСтатейБюджетов.ПериодЛимитирования = ВТ_ЛимитыПоБюджетамПредварительные.ПериодЛимитирования
	|			И ВТ_ПараметрыЛимитированияСтатейБюджетов.СтатьяБюджета = ВТ_ЛимитыПоБюджетамПредварительные.СтатьяБюджета";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса1, "ВТ_ЛимитыПоБюджетамПредварительные");
	ТекстыЗапроса.Добавить(ТекстЗапроса2, "ВТ_ПараметрыКонтроляСтатейРегистр");
	ТекстыЗапроса.Добавить(ТекстЗапроса3, "ВТ_ТребуемыеВидБюджетаСтатьяПериод");
	ТекстыЗапроса.Добавить(ТекстЗапроса4, "ВТ_ПараметрыКонтроляПериодыДействия");
	ТекстыЗапроса.Добавить(ТекстЗапроса5, "ВТ_ПараметрыЛимитированияСтатейБюджетов");
	ТекстыЗапроса.Добавить(ТекстЗапроса6, "ЛимитыПоБюджетам");
	
КонецПроцедуры

Процедура ЗаполнитьПоЗаявкеНаКорректировку(Объект)
	
	Если НЕ ЗначениеЗаполнено(Объект.Основание) Тогда
		Возврат;
	КонецЕсли;
	
	//
	ВидБюджетаВалютаГод = ПолучитьВидБюджетаВалютуГодПоРегистру(Объект.Основание, Объект.Ссылка);
	Если ВидБюджетаВалютаГод.Количество() > 0 Тогда
		Объект.ВидБюджета = ВидБюджетаВалютаГод[0].ВидБюджета;
		Объект.Валюта = ВидБюджетаВалютаГод[0].Валюта;
		Объект.ГодЛимитирования = ВидБюджетаВалютаГод[0].ГодЛимитирования;
		ЗаполнитьТребуетсяПоДаннымРегистраКОбеспечениюПоВидуБюджетаВалютеГоду(Объект);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоДокументуОбеспечения(Объект)

	Если НЕ ЗначениеЗаполнено(Объект.Основание) Тогда
		Возврат;
	КонецЕсли;
	
	// 
	ТребуемыеЛимиты = ПолучитьТребуемыеЛимитыПоПланамДокумента(Объект.Основание, ТекущаяДатаСеанса());
	ВидБюджетаВалютаГод = ПолучитьВидБюджетаВалютуГодПоТребуемымЛимитам(ТребуемыеЛимиты);
	Если ВидБюджетаВалютаГод.Количество() > 0 Тогда
		Объект.ВидБюджета = ВидБюджетаВалютаГод[0].ВидБюджета;
		Объект.Валюта = ВидБюджетаВалютаГод[0].Валюта;
		Объект.ГодЛимитирования = ВидБюджетаВалютаГод[0].ГодЛимитирования;
		ЗаполнитьПоТребуемымЛимитамПоВидуБюджетаВалютеГоду(Объект, ТребуемыеЛимиты);
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьВидБюджетаВалютуГодПоТребуемымЛимитам(ТребуемыеЛимиты)
	
	// Сворачиваем
	Реквизиты = "ВидБюджета, Валюта, ГодЛимитирования";
	ВидыБюджетовВалютыГод = ТребуемыеЛимиты.Скопировать(, Реквизиты+", КОбеспечению");
	ВидыБюджетовВалютыГод.Свернуть(Реквизиты, "КОбеспечению");
	ВидыБюджетовВалютыГод.Сортировать(Реквизиты);
	
	Возврат ВидыБюджетовВалютыГод;
	
КонецФункции

Функция ПолучитьВидБюджетаВалютуГодПоРегистру(Основание, Регистратор)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Основание", Основание);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Запрос.ВидБюджета КАК ВидБюджета,
	|	Запрос.Валюта КАК Валюта,
	|	Запрос.ГодЛимитирования КАК ГодЛимитирования,
	|	СУММА(Запрос.КОбеспечению) КАК КОбеспечению
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЕСТЬNULL(ВидыБюджетов.Ссылка, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыБюджетов.ПустаяСсылка)) КАК ВидБюджета,
	|		ЛимитыПоБюджетамОбороты.Валюта КАК Валюта,
	|		ЛимитыПоБюджетамОбороты.КОбеспечениюОборот КАК КОбеспечению,
	|		НАЧАЛОПЕРИОДА(ЛимитыПоБюджетамОбороты.ПериодЛимитирования.ДатаНачала, ГОД) КАК ГодЛимитирования
	|	ИЗ
	|		РегистрНакопления.ЛимитыПоБюджетам.Обороты(, , , ДокументПланирования = &Основание) КАК ЛимитыПоБюджетамОбороты
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ВидыБюджетов КАК ВидыБюджетов
	|			ПО ЛимитыПоБюджетамОбороты.Предназначение = ВидыБюджетов.Предназначение
	|	ГДЕ
	|		ЛимитыПоБюджетамОбороты.КОбеспечениюОборот <> 0
	|		И ЛимитыПоБюджетамОбороты.Предназначение <> ЗНАЧЕНИЕ(Перечисление.ПредназначенияЭлементовСтруктурыОтчета.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЕСТЬNULL(ВидыБюджетов.Ссылка, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыБюджетов.ПустаяСсылка)),
	|		ЛимитыПоБюджетам.Валюта,
	|		-ЛимитыПоБюджетам.КОбеспечению,
	|		НАЧАЛОПЕРИОДА(ЛимитыПоБюджетам.ПериодЛимитирования.ДатаНачала, ГОД)
	|	ИЗ
	|		РегистрНакопления.ЛимитыПоБюджетам КАК ЛимитыПоБюджетам
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ВидыБюджетов КАК ВидыБюджетов
	|			ПО ЛимитыПоБюджетам.Предназначение = ВидыБюджетов.Предназначение
	|	ГДЕ
	|		ЛимитыПоБюджетам.Регистратор = &Регистратор) КАК Запрос
	|
	|СГРУППИРОВАТЬ ПО
	|	Запрос.ВидБюджета,
	|	Запрос.Валюта,
	|	Запрос.ГодЛимитирования
	|
	|ИМЕЮЩИЕ
	|	СУММА(Запрос.КОбеспечению) <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВидБюджета,
	|	Валюта,
	|	ГодЛимитирования";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура ЗаполнитьПоТребуемымЛимитамПоВидуБюджетаВалютеГоду(Объект, ТребуемыеЛимиты)
	
	ЗаполнитьПараметрыЛимитирования(Объект);
	
	Объект.Требуется.Очистить();
	
	Реквизиты = "ВидБюджета, Валюта, ГодЛимитирования";
	СтруктураПоиска = Новый Структура(Реквизиты);
	ЗаполнитьЗначенияСвойств(СтруктураПоиска, Объект, Реквизиты);
	
	ТребуетсяДляЗагрузки = ТребуемыеЛимиты.Скопировать(СтруктураПоиска);
	
	Объект.Требуется.Загрузить(ТребуетсяДляЗагрузки);
	
КонецПроцедуры

Процедура ЗаполнитьТребуетсяПоДаннымРегистраКОбеспечениюПоВидуБюджетаВалютеГоду(Объект)
	
	ЗаполнитьПараметрыЛимитирования(Объект);
	
	Объект.Требуется.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Основание", Объект.Основание);
	Запрос.УстановитьПараметр("ДокументОбеспечения", Объект.Ссылка);
	Запрос.УстановитьПараметр("ВидБюджета", Объект.ВидБюджета);
	Запрос.УстановитьПараметр("Валюта", Объект.Валюта);
	Запрос.УстановитьПараметр("ГодЛимитирования", Объект.ГодЛимитирования);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Запрос.ВидБюджета КАК ВидБюджета,
	|	Запрос.Предназначение КАК Предназначение,
	|	Запрос.ДокументРезервирования КАК ДокументРезервирования,
	|	Запрос.ПериодЛимитирования КАК ПериодЛимитирования,
	|	Запрос.ЦФО КАК ЦФО,
	|	Запрос.Проект КАК Проект,
	|	Запрос.СтатьяБюджета КАК СтатьяБюджета,
	|	Запрос.Аналитика1 КАК Аналитика1,
	|	Запрос.Аналитика2 КАК Аналитика2,
	|	Запрос.Аналитика3 КАК Аналитика3,
	|	Запрос.Аналитика4 КАК Аналитика4,
	|	Запрос.Аналитика5 КАК Аналитика5,
	|	Запрос.Аналитика6 КАК Аналитика6,
	|	Запрос.Валюта КАК Валюта,
	|	Запрос.ДокументПланирования КАК ДокументПланирования,
	|	СУММА(Запрос.КОбеспечению) КАК КОбеспечению
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВидыБюджетов.Ссылка КАК ВидБюджета,
	|		ЛимитыПоБюджетамОбороты.Предназначение КАК Предназначение,
	|		ЛимитыПоБюджетамОбороты.ДокументРезервирования КАК ДокументРезервирования,
	|		ЛимитыПоБюджетамОбороты.ПериодЛимитирования КАК ПериодЛимитирования,
	|		ЛимитыПоБюджетамОбороты.ЦФО КАК ЦФО,
	|		ЛимитыПоБюджетамОбороты.Проект КАК Проект,
	|		ЛимитыПоБюджетамОбороты.СтатьяБюджета КАК СтатьяБюджета,
	|		ЛимитыПоБюджетамОбороты.Аналитика1 КАК Аналитика1,
	|		ЛимитыПоБюджетамОбороты.Аналитика2 КАК Аналитика2,
	|		ЛимитыПоБюджетамОбороты.Аналитика3 КАК Аналитика3,
	|		ЛимитыПоБюджетамОбороты.Аналитика4 КАК Аналитика4,
	|		ЛимитыПоБюджетамОбороты.Аналитика5 КАК Аналитика5,
	|		ЛимитыПоБюджетамОбороты.Аналитика6 КАК Аналитика6,
	|		ЛимитыПоБюджетамОбороты.Валюта КАК Валюта,
	|		ЛимитыПоБюджетамОбороты.ДокументПланирования КАК ДокументПланирования,
	|		ЛимитыПоБюджетамОбороты.КОбеспечениюОборот КАК КОбеспечению
	|	ИЗ
	|		РегистрНакопления.ЛимитыПоБюджетам.Обороты(
	|				,
	|				,
	|				,
	|				ДокументПланирования = &Основание
	|					И Валюта = &Валюта
	|					И НАЧАЛОПЕРИОДА(ПериодЛимитирования.ДатаНачала, ГОД) = &ГодЛимитирования) КАК ЛимитыПоБюджетамОбороты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ВидыБюджетов КАК ВидыБюджетов
	|			ПО ЛимитыПоБюджетамОбороты.Предназначение = ВидыБюджетов.Предназначение
	|				И (ВидыБюджетов.Ссылка = &ВидБюджета)
	|	ГДЕ
	|		ВидыБюджетов.Ссылка = &ВидБюджета
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВидыБюджетов.Ссылка,
	|		ЛимитыПоБюджетам.Предназначение,
	|		ЛимитыПоБюджетам.ДокументРезервирования,
	|		ЛимитыПоБюджетам.ПериодЛимитирования,
	|		ЛимитыПоБюджетам.ЦФО,
	|		ЛимитыПоБюджетам.Проект,
	|		ЛимитыПоБюджетам.СтатьяБюджета,
	|		ЛимитыПоБюджетам.Аналитика1,
	|		ЛимитыПоБюджетам.Аналитика2,
	|		ЛимитыПоБюджетам.Аналитика3,
	|		ЛимитыПоБюджетам.Аналитика4,
	|		ЛимитыПоБюджетам.Аналитика5,
	|		ЛимитыПоБюджетам.Аналитика6,
	|		ЛимитыПоБюджетам.Валюта,
	|		ЛимитыПоБюджетам.ДокументПланирования,
	|		-ЛимитыПоБюджетам.КОбеспечению
	|	ИЗ
	|		РегистрНакопления.ЛимитыПоБюджетам КАК ЛимитыПоБюджетам
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ВидыБюджетов КАК ВидыБюджетов
	|			ПО ЛимитыПоБюджетам.Предназначение = ВидыБюджетов.Предназначение
	|				И (ВидыБюджетов.Ссылка = &ВидБюджета)
	|	ГДЕ
	|		ЛимитыПоБюджетам.Регистратор = &ДокументОбеспечения
	|		И ЛимитыПоБюджетам.Валюта = &Валюта
	|		И ЛимитыПоБюджетам.КОбеспечению <> 0
	|		И НАЧАЛОПЕРИОДА(ЛимитыПоБюджетам.ПериодЛимитирования.ДатаНачала, ГОД) = &ГодЛимитирования) КАК Запрос
	|
	|СГРУППИРОВАТЬ ПО
	|	Запрос.ЦФО,
	|	Запрос.ДокументРезервирования,
	|	Запрос.Аналитика5,
	|	Запрос.Проект,
	|	Запрос.Аналитика3,
	|	Запрос.Предназначение,
	|	Запрос.ПериодЛимитирования,
	|	Запрос.ДокументПланирования,
	|	Запрос.Аналитика2,
	|	Запрос.СтатьяБюджета,
	|	Запрос.Аналитика4,
	|	Запрос.ВидБюджета,
	|	Запрос.Валюта,
	|	Запрос.Аналитика6,
	|	Запрос.Аналитика1
	|
	|ИМЕЮЩИЕ
	|	СУММА(Запрос.КОбеспечению) <> 0";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Объект.Требуется.Очистить();
		Возврат;
	КонецЕсли;
	
	Объект.Требуется.Загрузить(Результат.Выгрузить());
	
КонецПроцедуры

Функция ПолучитьПараметрыЛимитирования(ВидБюджета, НаДату)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидБюджета", ВидБюджета);
	Запрос.УстановитьПараметр("НаДату", НачалоГода(НаДату));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПараметрыЛимитированияСрезПоследних.ВидБюджета КАК ВидБюджета,
	|	ПараметрыЛимитированияСрезПоследних.ПериодичностьЛимитирования КАК ПериодичностьЛимитирования,
	|	ПараметрыЛимитированияСрезПоследних.ИспользоватьЛимитирование КАК ИспользоватьЛимитирование,
	|	ПараметрыЛимитированияСрезПоследних.СпособОпределенияВалютыЛимитирования КАК СпособОпределенияВалютыЛимитирования
	|ИЗ
	|	РегистрСведений.ПараметрыЛимитирования.СрезПоследних(&НаДату, ВидБюджета = &ВидБюджета) КАК ПараметрыЛимитированияСрезПоследних";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Результат = Новый Структура("ВидБюджета, ИспользоватьЛимитирование, ПериодичностьЛимитирования, СпособОпределенияВалютыЛимитирования");
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
