
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПериодЛимитированияНачало = НачалоГода(Параметры.ГодЛимитирования);
	ПериодЛимитированияОкончание = НачалоДня(КонецГода(ПериодЛимитированияНачало));
	Валюта = Параметры.Валюта;
	ВидБюджета = Параметры.ВидБюджета;
	
	//
	ПараметрыЛимитирования = ПолучитьПараметрыЛимитирования(ВидБюджета, ПериодЛимитированияНачало);
	Периодичность = ПараметрыЛимитирования.Периодичность;
	Предназначение = ПараметрыЛимитирования.Предназначение;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ВидБюджета", ВидБюджета);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Валюта", Валюта);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ДатаНачала", ПериодЛимитированияНачало);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ДатаОкончания", ПериодЛимитированияОкончание);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Предназначение", Предназначение);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Периодичность", Периодичность);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	//  ПараметрыВыбора для периода лимитирования
	Элемент = Элементы.КомпоновщикНастроекПользовательскиеНастройкиЭлемент2Значение;
	
	ПараметрыВыбора = Новый Массив();
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Произвольный", Ложь));
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Периодичность", Периодичность));
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.ТолькоПериодыГода", ПериодЛимитированияНачало));
	Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
	
&НаКлиенте
Процедура ВидБюджетаНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПериодичностьЛимитированияНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ВалютаНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПериодЛимитированияНачалоНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПериодЛимитированияОкончаниеНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	ВыполнитьВыбор(Элемент.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ВыполнитьВыбор(Элемент.ТекущиеДанные);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьПараметрыЛимитирования(ВидБюджета, ПериодЛимитированияНачало)
	
	Результат = Новый Структура("ВидБюджета, Предназначение, Периодичность", 
		ВидБюджета, Перечисления.ПредназначенияЭлементовСтруктурыОтчета.ПустаяСсылка(), Перечисления.Периодичность.ПустаяСсылка());
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидБюджета", ВидБюджета);
	Запрос.УстановитьПараметр("НаДату", ПериодЛимитированияНачало);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПараметрыЛимитированияСрезПоследних.ВидБюджета КАК ВидБюджета,
	|	ПараметрыЛимитированияСрезПоследних.ВидБюджета.Предназначение КАК Предназначение,
	|	ПараметрыЛимитированияСрезПоследних.ПериодичностьЛимитирования КАК Периодичность
	|ИЗ
	|	РегистрСведений.ПараметрыЛимитирования.СрезПоследних(&НаДату, ВидБюджета = &ВидБюджета) КАК ПараметрыЛимитированияСрезПоследних";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Результат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьВыбор(ДанныеВыбора)
	Результат = Новый Структура;
	Результат.Вставить("ВидБюджета");
	Результат.Вставить("Предназначение");
	Результат.Вставить("ПериодЛимитирования");
	Результат.Вставить("Валюта");
	Результат.Вставить("ЦФО");
	Результат.Вставить("Проект");
	Результат.Вставить("СтатьяБюджета");
	Результат.Вставить("Аналитика1");
	Результат.Вставить("Аналитика2");
	Результат.Вставить("Аналитика3");
	Результат.Вставить("Аналитика4");
	Результат.Вставить("Аналитика5");
	Результат.Вставить("Аналитика6");
	Результат.Вставить("Свободно");
	ЗаполнитьЗначенияСвойств(Результат, ДанныеВыбора);
	Закрыть(Результат);
КонецПроцедуры

#КонецОбласти

