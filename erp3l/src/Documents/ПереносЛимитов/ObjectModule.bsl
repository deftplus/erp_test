#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипИсточника = ТипЗнч(ДанныеЗаполнения);
	Если ТипИсточника <> Тип("Структура") Тогда
		ДанныеИсточник = ДанныеЗаполнения;
		ДанныеЗаполнения = Новый Структура;
	КонецЕсли;
	
	ДанныеЗаполнения.Вставить("Ответственный", Пользователи.ТекущийПользователь());
	
	Если ДанныеЗаполнения.Свойство("Источник") Тогда
		
		ДанныеЗаполнения.Вставить("ИспользоватьЛимитирование");
		ДанныеЗаполнения.Вставить("ПериодичностьЛимитирования");
		ДанныеЗаполнения.Вставить("СпособОпределенияВалютыЛимитирования");
		Документы.ПереносЛимитов.ЗаполнитьПараметрыЛимитирования(ДанныеЗаполнения);
		
		Для Каждого Строка Из ДанныеЗаполнения.Источник Цикл
			ЗаполнитьЗначенияСвойств(Источник.Добавить(), Строка);
		КонецЦикла;
	ИначеЕсли ДанныеЗаполнения.Свойство("ПеренестиНеиспользованныеЛимиты") Тогда
		ВыполнитьЗаполнениеПоДаннымПереносаЛимитов(ДанныеЗаполнения);
	КонецЕсли;
	
	Если НЕ ДанныеЗаполнения.Свойство("ВидБюджета") Тогда
		ДанныеЗаполнения.Вставить("ВидБюджета", ПланыВидовХарактеристик.ВидыБюджетов.БюджетДвиженияДенежныхСредств);
	КонецЕсли;
	
	Если НЕ ДанныеЗаполнения.Свойство("ГодЛимитирования") Тогда
		ДанныеЗаполнения.Вставить("ГодЛимитирования", НачалоГода(ТекущаяДатаСеанса()));
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("ВидБюджета") И ДанныеЗаполнения.Свойство("ГодЛимитирования") Тогда
		
		ДанныеЗаполнения.Вставить("ИспользоватьЛимитирование");
		ДанныеЗаполнения.Вставить("ПериодичностьЛимитирования");
		ДанныеЗаполнения.Вставить("СпособОпределенияВалютыЛимитирования");
		Если НЕ ДанныеЗаполнения.Свойство("Валюта") Тогда
			ДанныеЗаполнения.Вставить("Валюта");
		КонецЕсли;
		
		Документы.ПереносЛимитов.ЗаполнитьПараметрыЛимитирования(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	//
	СуммаДокумента = Источник.Итог("Сумма");
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// Соответствие сумм источника и получателя
	ИтогоПолучатель = Получатель.Итог("Сумма");
	ИтогоИсточник = Источник.Итог("Сумма");
	Если ВидОперации = Перечисления.ВидыОперацийПереносЛимитов.ПереносПроизвольный И ИтогоПолучатель <> ИтогоИсточник Тогда
		Шаблон = НСтр("ru = 'Итого по строкам источника лимитов бюджетов (%1) не равно итого по строкам получателя лимитов (%2). Проведение невозможно.'");
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(Шаблон, ИтогоИсточник, ИтогоПолучатель),,,,Отказ);
	КонецЕсли;
	
	//
	МассивНепроверяемыхРеквизитов = Новый Массив();
	Если ВидОперации = Перечисления.ВидыОперацийПереносЛимитов.ПереносАналитикиНеУказаны
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПереносЛимитов.ПереносПроизвольный
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПереносЛимитов.ПереносПроект Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ЦФО");
		МассивНепроверяемыхРеквизитов.Добавить("ПериодЛимитирования");
		МассивНепроверяемыхРеквизитов.Добавить("СтатьяБюджета");
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПереносЛимитов.ПереносПериод
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПереносЛимитов.ПереносПериодПроект Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ЦФО");
		МассивНепроверяемыхРеквизитов.Добавить("СтатьяБюджета");
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПереносЛимитов.ПереносПериодСтатья
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПереносЛимитов.ПереносПериодСтатьяПроект Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ЦФО");
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПереносЛимитов.ПереносПериодСтатьяЦФО Тогда
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПереносЛимитов.ПереносПериодЦФО
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПереносЛимитов.ПереносПериодЦФОПроект Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СтатьяБюджета");
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПереносЛимитов.ПереносСтатья
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПереносЛимитов.ПереносСтатьяПроект Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ЦФО");
		МассивНепроверяемыхРеквизитов.Добавить("ПериодЛимитирования");
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПереносЛимитов.ПереносСтатьяЦФО
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПереносЛимитов.ПереносСтатьяЦФОПроект Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ПериодЛимитирования");
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПереносЛимитов.ПереносЦФО
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПереносЛимитов.ПереносЦФОПроект Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ПериодЛимитирования");
		МассивНепроверяемыхРеквизитов.Добавить("СтатьяБюджета");
	КонецЕсли;
	
	// Удаляем
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ВидОперации = Перечисления.ВидыОперацийПереносЛимитов.ПереносАналитикиНеУказаны Тогда
		ТекстСообщения = НСтр("ru = 'На закладке ""Получатель лимитов"" не указаны целевые аналитики для переноса лимитов. Проведение невозможно.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		Возврат;
	КонецЕсли;
	
	//
	ПроведениеСерверОПК.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	Документы.ПереносЛимитов.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	ПроведениеСерверОПК.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);	
	
	//
	КонтрольЛимитовУХ.ОтразитьЛимитыПоБюджетам(ДополнительныеСвойства, Движения, Отказ);
	
	//
	СформироватьСписокРегистровДляКонтроля();
	ПроведениеСерверОПК.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ПроведениеСерверОПК.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	
	ПроведениеСерверОПК.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьСписокРегистровДляКонтроля()
	
	Массив = Новый Массив;
	Массив.Добавить(Движения.ЛимитыПоБюджетам);
	ДополнительныеСвойства.ДляПроведения.Вставить("РегистрыДляКонтроля", Массив);
	
КонецПроцедуры

Процедура ВыполнитьЗаполнениеПоДаннымПереносаЛимитов(ДанныеЗаполнения)
	
	ВидБюджета = ДанныеЗаполнения.ВидБюджета;
	Валюта = ДанныеЗаполнения.Валюта;
	ПериодЛимитирования = ДанныеЗаполнения.ПеренестиНеиспользованныеЛимиты.ПериодЛимитированияПолучатель; 
	ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПереносЛимитов.ПереносПериод");
	
	ДатаЗаполнения = ПериодЛимитирования.ДатаНачала;
	
	ПараметрыЛимитирования = Документы.ПереносЛимитов.ПолучитьПараметрыЛимитирования(ВидБюджета,ДатаЗаполнения);
	ИспользоватьЛимитирование = ПараметрыЛимитирования.ИспользоватьЛимитирование;
	ПериодичностьЛимитирования = ПараметрыЛимитирования.ПериодичностьЛимитирования;
	СпособОпределенияВалютыЛимитирования = ПараметрыЛимитирования.СпособОпределенияВалютыЛимитирования;
	ГодЛимитирования = НачалоГода(ДатаЗаполнения);
	
	ТаблицаОтбора = Новый ТаблицаЗначений;
	ТаблицаОтбора.Колонки.Добавить("ЦФО",Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаОтбора.Колонки.Добавить("Проект",Новый ОписаниеТипов("СправочникСсылка.Проекты"));	
	Для Каждого СтрокаОтбора Из ДанныеЗаполнения.ПеренестиНеиспользованныеЛимиты.Отбор Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаОтбора.Добавить(), СтрокаОтбора);
	КонецЦикла;	
	ТаблицаОтбора.Свернуть("ЦФО,Проект");
	ЦФООтбор = ДанныеЗаполнения.ПеренестиНеиспользованныеЛимиты.Отбор[0].ЦФО;
	ПроектОтбор = ДанныеЗаполнения.ПеренестиНеиспользованныеЛимиты.Отбор[0].Проект;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ТаблицаОтбора",	ТаблицаОтбора);
	Запрос.УстановитьПараметр("ДатаНачала", 	НачалоГода(ДатаЗаполнения));
	Запрос.УстановитьПараметр("ДатаОкончания",	ДатаЗаполнения - 1);
	Запрос.УстановитьПараметр("Периодичность",	ПериодичностьЛимитирования);
	Запрос.УстановитьПараметр("ВидБюджета",		ВидБюджета);
	Запрос.УстановитьПараметр("Валюта",			Валюта);
	Запрос.УстановитьПараметр("Проект",			ПроектОтбор);
	Запрос.УстановитьПараметр("ЦФО",			ЦФООтбор);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Периоды.Ссылка КАК ПериодЛимитирования
	|ПОМЕСТИТЬ ВТ_ПериодыЛимитирования
	|ИЗ
	|	Справочник.Периоды КАК Периоды
	|ГДЕ
	|	Периоды.Периодичность = &Периодичность
	|	И Периоды.ДатаНачала >= &ДатаНачала
	|	И Периоды.ДатаОкончания <= &ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОтбора.ЦФО КАК ЦФО,
	|	ТаблицаОтбора.Проект КАК Проект
	|ПОМЕСТИТЬ ТаблицаОтбора
	|ИЗ
	|	&ТаблицаОтбора КАК ТаблицаОтбора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЛимитыПоБюджетамОбороты.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ЛимитыПоБюджетамОбороты.ЦФО КАК ЦФО,
	|	ЛимитыПоБюджетамОбороты.Проект КАК Проект,
	|	ЛимитыПоБюджетамОбороты.СтатьяБюджета КАК СтатьяБюджета,
	|	ЛимитыПоБюджетамОбороты.Аналитика1 КАК Аналитика1,
	|	ЛимитыПоБюджетамОбороты.Аналитика2 КАК Аналитика2,
	|	ЛимитыПоБюджетамОбороты.Аналитика3 КАК Аналитика3,
	|	ЛимитыПоБюджетамОбороты.Аналитика4 КАК Аналитика4,
	|	ЛимитыПоБюджетамОбороты.Аналитика5 КАК Аналитика5,
	|	ЛимитыПоБюджетамОбороты.Аналитика6 КАК Аналитика6,
	|	ЛимитыПоБюджетамОбороты.Валюта КАК Валюта,
	|	ЛимитыПоБюджетамОбороты.ЛимитОборот КАК Лимит,
	|	ЛимитыПоБюджетамОбороты.КорректировкаОборот КАК Корректировка,
	|	ЛимитыПоБюджетамОбороты.ЛимитОборот + ЛимитыПоБюджетамОбороты.КорректировкаОборот - ЛимитыПоБюджетамОбороты.ЗарезервированоОборот - ЛимитыПоБюджетамОбороты.ЗаявленоОборот - ЛимитыПоБюджетамОбороты.ИсполненоОборот КАК Сумма
	|ИЗ
	|	РегистрНакопления.ЛимитыПоБюджетам.Обороты(
	|			,
	|			,
	|			,
	|			Предназначение В
	|					(ВЫБРАТЬ
	|						ПВХ.Предназначение
	|					ИЗ
	|						ПланВидовХарактеристик.ВидыБюджетов КАК ПВХ
	|					ГДЕ
	|						ПВХ.Ссылка = &ВидБюджета)
	|				И Валюта = &Валюта
	|				И ПериодЛимитирования В
	|					(ВЫБРАТЬ
	|						ВТ_ПериодыЛимитирования.ПериодЛимитирования КАК ПериодЛимитирования
	|					ИЗ
	|						ВТ_ПериодыЛимитирования КАК ВТ_ПериодыЛимитирования)
	|				И (ЦФО, Проект) В
	|					(ВЫБРАТЬ
	|						ТаблицаОтбора.ЦФО,
	|						ТаблицаОтбора.Проект
	|					ИЗ
	|						ТаблицаОтбора)) КАК ЛимитыПоБюджетамОбороты
	|ГДЕ
	|	ЛимитыПоБюджетамОбороты.ЛимитОборот + ЛимитыПоБюджетамОбороты.КорректировкаОборот - ЛимитыПоБюджетамОбороты.ЗарезервированоОборот - ЛимитыПоБюджетамОбороты.ЗаявленоОборот - ЛимитыПоБюджетамОбороты.ИсполненоОборот > 0";
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Источник.Загрузить(Результат.Выгрузить());
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли 
