
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность	
	#Область УХ_ВНА
	ПриСозданииНаСервереУХ(Отказ, СтандартнаяОбработка);
	#КонецОбласти
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	#Область УХ_ВНА
	Справочники.ГруппыВНАМСФО.СохранитьПараметрыПоУмолчаниюВНА(ТекущийОбъект.Ссылка, ПараметрыУчетаМСФО);
	#КонецОбласти
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект)
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьСервер.ПередЗаписьюНаСервере(ТекущийОбъект);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормыУХ

&НаКлиенте
Процедура ВариантУчетаВНАПриИзмененииУХ(Элемент)
	Если ВариантУчетаТрансляция(ВариантУчетаВНА) Тогда //трансляция
		ПараметрыУчетаМСФО = Неопределено;
	КонецЕсли;
	
	УправлениеФормойУХ(ЭтаФорма);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииУХ

&НаСервере
Процедура ПриСозданииНаСервереУХ(Отказ, СтандартнаяОбработка)
	ДатаСведений = ТекущаяДата();
	МСФОВНАВызовСервераУХ.ОбновитьПараметрыМСФО(ЭтаФорма, ДатаСведений);
	УправлениеФормойУХ(ЭтаФорма);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормойУХ(Форма)

	Элементы = Форма.Элементы;
	
	ЕстьМСФО = ЗначениеЗаполнено(Форма.ПринятоКУчетуМСФО);
	
	ВариантУчетаТрансляция = ВариантУчетаТрансляция(Форма.ВариантУчетаВНА);
	
	Элементы.ПараметрыУчетаВНА.Видимость 		= Не ВариантУчетаТрансляция;
	
	Элементы.ВариантУчетаВНА.ТолькоПросмотр 	= ЕстьМСФО;
	Элементы.ПараметрыУчетаВНА.ТолькоПросмотр 	= ЕстьМСФО;
	Элементы.ПараметрыУчетаВНА.Заголовок 		= ?(ЕстьМСФО, "Параметры учета", "Параметры по умолчанию");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ВариантУчетаТрансляция(ВариантУчетаВНА)
	Возврат (ВариантУчетаВНА = 0);
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура Подключаемый_Открытие(Элемент, СтандартнаяОбработка)
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьКлиент.ПриОткрытии(ЭтотОбъект, Объект, Элемент, СтандартнаяОбработка);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
КонецПроцедуры

#КонецОбласти