#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		
		ПриЧтенииСозданииНаСервере();
		
	КонецЕсли;
	
	Если Объект.Ссылка = ПланыВидовХарактеристик.ВидыКонтроляДокументов.КонтрольБюджетныхЛимитов
		ИЛИ Объект.Ссылка = ПланыВидовХарактеристик.ВидыКонтроляДокументов.КонтрольБюджетныхРезервов Тогда
		
		ТекстРежимКонтроля = НСтр("ru = 'Настройка режима контроля производится в параметрах лимитирования'");
		Элементы.ТекстРежимКонтроля.Видимость = Истина;
		Элементы.РежимКонтроля.Видимость = Ложь;
		
	Иначе
		Элементы.ТекстРежимКонтроля.Видимость = Ложь;
		Элементы.РежимКонтроля.Видимость = Истина;
	КонецЕсли;
	
	Элементы.РегистрироватьДефицитРезерва.Видимость = Объект.Ссылка = ПланыВидовХарактеристик.ВидыКонтроляДокументов.КонтрольБюджетныхРезервов;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ПриЧтенииСозданииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
	
&НаКлиенте
Процедура ТекстРежимКонтроляНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("РегистрСведений.ПараметрыЛимитирования.ФормаСписка", , ЭтотОбъект, , , , ,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	Если Объект.Ссылка = ПланыВидовХарактеристик.ВидыКонтроляДокументов.КонтрольБюджетныхРезервов Тогда
		РегистрироватьДефицитРезерва = Константы.РегистрироватьДефицитРезерва.Получить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Константы.РегистрироватьДефицитРезерва.Установить(ЭтотОбъект.РегистрироватьДефицитРезерва);
КонецПроцедуры

#КонецОбласти


