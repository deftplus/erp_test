#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиОбновления

Процедура ПервоначальноеЗаполнениеПВХПараметрыЗакупки() экспорт
	
	//
	Параметры223ФЗ = Новый Массив;
	Параметры223ФЗ.Добавить(ПланыВидовХарактеристик.ПараметрыЗакупки.ЗакупкаИВПиЛС);
	Параметры223ФЗ.Добавить(ПланыВидовХарактеристик.ПараметрыЗакупки.ГосТайна);
	Параметры223ФЗ.Добавить(ПланыВидовХарактеристик.ПараметрыЗакупки.ПриоритетСМП);
	Параметры223ФЗ.Добавить(ПланыВидовХарактеристик.ПараметрыЗакупки.СубподрядчикиСМП);
	
	//
	ПараметрыСтроки = Новый Массив;
	ПараметрыСтроки.Добавить(ПланыВидовХарактеристик.ПараметрыЗакупки.Обоснование);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Параметры223ФЗ", Параметры223ФЗ);
	Запрос.УстановитьПараметр("ПараметрыСтроки", ПараметрыСтроки);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПараметрыЗакупки.Ссылка КАК Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.ПараметрыЗакупки КАК ПараметрыЗакупки
	|ГДЕ
	|	ПараметрыЗакупки.Ссылка В(&Параметры223ФЗ)
	|	И ПараметрыЗакупки.ИспользуетсяТолькоДля223ФЗ = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПараметрыЗакупки.Ссылка КАК Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.ПараметрыЗакупки КАК ПараметрыЗакупки
	|ГДЕ
	|	ПараметрыЗакупки.Ссылка В(&ПараметрыСтроки)
	|	И ПараметрыЗакупки.ЭтоПараметрПозиции = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ПараметрыЗакупки.СпособЗакупки) КАК Параметр,
	|	1 КАК Номер
	|ПОМЕСТИТЬ ВТ_Параметры
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ПараметрыЗакупки.Электронно),
	|	2
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ПараметрыЗакупки.Обоснование),
	|	3
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ПараметрыЗакупки.Организатор),
	|	4
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ПараметрыЗакупки.ДлительностьПодготовки),
	|	5
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ПараметрыЗакупки.ДлительностьПроцедуры),
	|	6
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ПараметрыЗакупки.ГосТайна),
	|	7
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ПараметрыЗакупки.ЗакупкаИВПиЛС),
	|	8
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ПараметрыЗакупки.ПриоритетСМП),
	|	9
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ПараметрыЗакупки.СубподрядчикиСМП),
	|	10
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПараметрыЗакупки.Ссылка КАК Ссылка,
	|	ПараметрыЗакупки.НомерПараметра КАК НомерПараметра,
	|	ВТ_Параметры.Параметр КАК Параметр,
	|	ВТ_Параметры.Номер КАК Номер
	|ИЗ
	|	ВТ_Параметры КАК ВТ_Параметры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ПараметрыЗакупки КАК ПараметрыЗакупки
	|		ПО ВТ_Параметры.Параметр = ПараметрыЗакупки.Ссылка
	|ГДЕ
	|	ПараметрыЗакупки.НомерПараметра = 0";
	Результат = Запрос.ВыполнитьПакет();
	
	
	Если НЕ Результат[0].Пустой() Тогда
		
		Выборка = Результат[0].Выбрать();
		Пока Выборка.Следующий() Цикл
			Объект = Выборка.Ссылка.ПолучитьОбъект();
			Объект.ИспользуетсяТолькоДля223ФЗ = Истина;
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект);
		КонецЦикла;
		
	КонецЕсли;
	
	Если НЕ Результат[1].Пустой() Тогда
		
		Выборка = Результат[1].Выбрать();
		Пока Выборка.Следующий() Цикл
			Объект = Выборка.Ссылка.ПолучитьОбъект();
			Объект.ЭтоПараметрПозиции = Истина;
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект);
		КонецЦикла;
		
	КонецЕсли;
	
	Если НЕ Результат[3].Пустой() Тогда
		
		Выборка = Результат[3].Выбрать();
		Пока Выборка.Следующий() Цикл
			Объект = Выборка.Ссылка.ПолучитьОбъект();
			Объект.НомерПараметра = Выборка.Номер;
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры
 
#КонецОбласти 

#Область ПрограммныйИнтерфейс
	
Функция ПолучитьТаблицуНастроекПараметровЗакупки() экспорт
	
	Результат = Новый ТаблицаЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 32
	|	ПараметрыЗакупки.Ссылка КАК Параметр,
	|	ПараметрыЗакупки.НомерПараметра КАК ПараметрНомер,
	|	ВЫБОР
	|		КОГДА ПараметрыЗакупки.Предопределенный
	|			ТОГДА ПараметрыЗакупки.ИмяПредопределенныхДанных
	|		ИНАЧЕ ПараметрыЗакупки.Имя
	|	КОНЕЦ КАК ПараметрИмя,
	|	ВЫРАЗИТЬ(""ЗапретИзменения_"" КАК Строка(80)) КАК ЗапретИзменения,
	|	ПараметрыЗакупки.ТипЗначения КАК ТипЗначения,
	|	ПараметрыЗакупки.ЭтоПараметрПозиции КАК ЭтоПараметрПозиции
	|ИЗ
	|	ПланВидовХарактеристик.ПараметрыЗакупки КАК ПараметрыЗакупки
	|ГДЕ
	|	ПараметрыЗакупки.ЭтоГруппа = ЛОЖЬ
	|	И ПараметрыЗакупки.Предопределенный = ИСТИНА
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПараметрыЗакупки.НомерПараметра";
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Строка Из Результат Цикл
		Строка.ЗапретИзменения = Строка.ЗапретИзменения + Строка.ПараметрИмя;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти 

#КонецЕсли

