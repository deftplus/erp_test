&НаКлиенте
Перем ОтветПередЗакрытием;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АдресНастроек = Параметры.АдресНастроек;
	Если ТипЗнч(Параметры.ГруппаОперацийГрафика) = Тип("Массив") Тогда
		ЭтотОбъект.ГруппыОперацийГрафика.ЗагрузитьЗначения(Параметры.ГруппаОперацийГрафика);
	Иначе	
		ЭтотОбъект.ГруппыОперацийГрафика.Добавить(Параметры.ГруппаОперацийГрафика);
	КонецЕсли;	
	
	ТаблицаНастроек = ПолучитьИзВременногоХранилища(АдресНастроек);
	ЗагрузитьПараметрыОпераций(ТаблицаНастроек);
	
	МассивОписанийСтатей = Новый Массив;
		
	АналитикиСтатейБюджетовУХ.СтатьяИАналитикиТаблицыЗначенийРеквизитаФормыВТаблицеФормы(МассивОписанийСтатей, 
		"ПараметрыОпераций", "ПараметрыОпераций", "СтатьяБюджета", "ПараметрыОперацийСтатьяБюджета", 
		"Аналитика%1", "ПараметрыОперацийАналитика%1", ФормыУХ.РазместитьВГруппеСтрокой(Элементы.ПараметрыОперацийГруппаАналитик));
	
	ПараметрыЭлементов = Новый Структура;
	АналитикиСтатейБюджетовУХ.СоздатьСтатьиБюджетовИАналитики(ЭтотОбъект, МассивОписанийСтатей, ПараметрыЭлементов);

	Элементы.ПараметрыОпераций.ТолькоПросмотр = Параметры.ТолькоПросмотр;
	Элементы.ФормаСохранить.Доступность = НЕ Параметры.ТолькоПросмотр;
	Элементы.ФормаСохранить.Видимость = НЕ Параметры.ТолькоПросмотр;
	
	Элементы.ФормаВосстановитьНастройкиПоУмолчанию.Доступность = НЕ Параметры.ТолькоПросмотр;
	Элементы.ФормаВосстановитьНастройкиПоУмолчанию.Видимость = НЕ Параметры.ТолькоПросмотр;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		Если НЕ ЗавершениеРаботы Тогда
			Если ОтветПередЗакрытием <> Истина Тогда
				Отказ = Истина;
				ОбратныйВызов = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
				ПоказатьВопрос(ОбратныйВызов, НСтр("ru = 'Есть несохраненные изменения, продолжить?'"), РежимДиалогаВопрос.ДаНет);
			КонецЕсли;
		Иначе
			Отказ = Истина;
			ТекстПредупреждения = НСтр("ru = 'При закрытии формы изменения будут отменены.'");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	АдресНастроек = ДанныеНастроек();	
	Модифицированность = Ложь;
	Закрыть(АдресНастроек);
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьНастройкиПоУмолчанию(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВосстановитьНастройкиПоУмолчаниюЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, Нстр("ru = 'Восстановить настройки операций по умолчанию?'"), РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры	

#КонецОбласти

#Область ОбработчикиЭлементовПараметрыОпераций

&НаКлиенте
Процедура ПараметрыОперацийСтатьяБюджетаПриИзменении(Элемент)
	АналитикиСтатейБюджетовУХКлиент.ПриИзмененииСтатьиБюджета(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыОперацийПриАктивизацииСтроки(Элемент)
	АналитикиСтатейБюджетовУХКлиент.ПриАктивизацииСтрокиТаблицыФормы(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыОперацийПередНачаломИзменения(Элемент, Отказ)
	АналитикиСтатейБюджетовУХКлиент.ПередНачаломИзмененияСтрокиТаблицыФормы(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СтатьяБюджета_ПриИзменении(Элемент)
	АналитикиСтатейБюджетовУХКлиент.ПриИзмененииСтатьиБюджета(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_АналитикаСтатьиБюджета_ПриИзменении(Элемент)
	АналитикиСтатейБюджетовУХКлиент.ПриИзмененииАналитикиСтатьиБюджета(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьПараметрыОпераций(ТаблицаНастроек)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПараметрыОпераций.ОперацияГрафика КАК ОперацияГрафика,
	|	ПараметрыОпераций.СтатьяБюджета КАК СтатьяБюджета,
	|	ПараметрыОпераций.Аналитика1 КАК Аналитика1,
	|	ПараметрыОпераций.Аналитика2 КАК Аналитика2,
	|	ПараметрыОпераций.Аналитика3 КАК Аналитика3,
	|	ПараметрыОпераций.Аналитика4 КАК Аналитика4,
	|	ПараметрыОпераций.Аналитика5 КАК Аналитика5,
	|	ПараметрыОпераций.Аналитика6 КАК Аналитика6
	|ПОМЕСТИТЬ ВТ_ПараметрыОпераций
	|ИЗ
	|	&ПараметрыОпераций КАК ПараметрыОпераций
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОперацииГрафиковДоговоров.НаправлениеДвижения КАК ПриходРасход,
	|	ОперацииГрафиковДоговоров.ВидБюджета КАК ВидБюджета,
	|	ВТ_ПараметрыОпераций.Аналитика1 КАК Аналитика1,
	|	ВТ_ПараметрыОпераций.Аналитика2 КАК Аналитика2,
	|	ВТ_ПараметрыОпераций.Аналитика3 КАК Аналитика3,
	|	ВТ_ПараметрыОпераций.Аналитика4 КАК Аналитика4,
	|	ВТ_ПараметрыОпераций.Аналитика5 КАК Аналитика5,
	|	ВТ_ПараметрыОпераций.Аналитика6 КАК Аналитика6,
	|	ВТ_ПараметрыОпераций.ОперацияГрафика КАК ОперацияГрафика,
	|	ВТ_ПараметрыОпераций.СтатьяБюджета КАК СтатьяБюджета
	|ИЗ
	|	ВТ_ПараметрыОпераций КАК ВТ_ПараметрыОпераций
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОперацииГрафиковДоговоров КАК ОперацииГрафиковДоговоров
	|		ПО ВТ_ПараметрыОпераций.ОперацияГрафика = ОперацииГрафиковДоговоров.Ссылка"
	);
	Запрос.УстановитьПараметр("ПараметрыОпераций", ТаблицаНастроек); 
	
	ИтоговаяТаблица = Запрос.Выполнить().Выгрузить();
	
	ПараметрыОпераций.Загрузить(ИтоговаяТаблица);
	
КонецПроцедуры

&НаСервере
Функция ДанныеНастроек()
	
	Таблица = ПараметрыОпераций.Выгрузить();
	Адрес = ПоместитьВоВременноеХранилище(Таблица, УникальныйИдентификатор); 
	
	Возврат Адрес;
	
КонецФункции

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОтветПередЗакрытием = Истина;
		Закрыть();
	Иначе
		ОтветПередЗакрытием = Неопределено;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьНастройкиПоУмолчаниюЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		ВосстановитьНастройкиПоУмолчаниюНаСервере();
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьНастройкиПоУмолчаниюНаСервере()
	ЭтотОбъект.ПараметрыОпераций.Загрузить(
		РаботаСДоговорамиКонтрагентовУХ.ПараметрыОперацийГрафикаПоУмолчанию(ГруппыОперацийГрафика.ВыгрузитьЗначения()));
	АналитикиСтатейБюджетовУХ.ЗаполнитьРеквизитыАналитикВсехСтатей(ЭтотОбъект);	
КонецПроцедуры
#КонецОбласти
