
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	#Область УХ_Встраивание
	АналитикиПланированияДокументов.ОбщаяФормаЭтапыОплаты_ПриСозданииНаСервере(ЭтотОбъект);
	Элементы.ЭтапыГрафикаОплаты.УстановитьДействие("ПриНачалеРедактирования", "Подключаемый_ЭтапыГрафикаОплатыПриНачалеРедактирования");
	#КонецОбласти 
	
	ИспользоватьГрафикиОплаты               = ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиОплаты");
	ИспользоватьДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров =
		ПолучитьФункциональнуюОпцию("ИспользоватьДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров");
	ИспользоватьУпрощеннуюСхемуОплатыВПродажах = ПолучитьФункциональнуюОпцию("ИспользоватьУпрощеннуюСхемуОплатыВПродажах");
	
	ВалютаРеглУчета = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Параметры.Организация);
	
	Параметры.ЗакрыватьПриВыборе            = Истина;
	Параметры.ЗакрыватьПриЗакрытииВладельца = Ложь;
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры);
	Если Дата = Дата(1,1,1) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	НеУказыватьИсточникиОплаты = Параметры.Касса = Неопределено И Параметры.БанковскийСчет = Неопределено;
	
	Если ЗначениеЗаполнено(Параметры.АдресВоВременномХранилище) Тогда
		ЭтапыГрафикаОплаты.Загрузить(ПолучитьИзВременногоХранилища(Параметры.АдресВоВременномХранилище));
		#Область УХ_Встраивание
		АналитикиПланированияДокументов.ОбщаяФормаЭтапыОплаты_ПослеЗагрузкиЭтаповОплаты(ЭтотОбъект);
		#КонецОбласти
		УдалитьИзВременногоХранилища(Параметры.АдресВоВременномХранилище);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.АдресСуммПоЗаказам) Тогда
		ТаблицаДокумента = ПолучитьИзВременногоХранилища(Параметры.АдресСуммПоЗаказам);
		ТабличнаяЧасть.Загрузить(ТаблицаДокумента);
		УдалитьИзВременногоХранилища(Параметры.АдресСуммПоЗаказам);
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами") Тогда
		Соглашение = Неопределено;
	КонецЕсли;
	
	Если КлиентскоеПриложение.ТекущийВариантИнтерфейса() = ВариантИнтерфейсаКлиентскогоПриложения.Версия8_2 Тогда
		Элементы.ГруппаИтого.ЦветФона = Новый Цвет();
	КонецЕсли;
	
	ИдентификаторВызывающейФормы        = Параметры.УникальныйИдентификатор;
	
	ГрафикСоглашенияЗаполнен = ПродажиВызовСервера.ГрафикСоглашенияЗаполнен(Соглашение);
	ГрафикИсполненияДоговора = ?(ЗначениеЗаполнено(Договор),
								ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор,"ГрафикИсполненияДоговора"),
								Документы.ГрафикИсполненияДоговора.ПустаяСсылка());
	Если ЭтоЗаказ Тогда
		ВариантОплатыПоУмолчанию = ПредопределенноеЗначение("Перечисление.ВариантыОплатыКлиентом.КредитСдвиг");
	Иначе
		ВариантОплатыПоУмолчанию = ПредопределенноеЗначение("Перечисление.ВариантыОплатыКлиентом.КредитПослеОтгрузки");
	КонецЕсли;
	
	ПредоплатаВНакладной = ТипЗнч(Ключ) = Тип("ДокументСсылка.РеализацияТоваровУслуг") И НЕ НакладнаяПоЗаказам И ПолучитьФункциональнуюОпцию("ИспользоватьСтатусыРеализацийТоваровУслуг");
	
	Если Не Параметры.ИспользоватьОтрицательныеСуммыПлатежа Тогда
		ОграничитьТипЭлементовСуммыПлатежа();
	КонецЕсли;
	
	Если Параметры.ПараметрыВыбораРеквизитов <> Неопределено Тогда
		Для Каждого ЭлНастройки Из Параметры.ПараметрыВыбораРеквизитов Цикл
			ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, ЭлНастройки.Ключ, "ПараметрыВыбора", ЭлНастройки.Значение);
		КонецЦикла;
	КонецЕсли;
	
	ЗаполнитьСписокВыбораФормыОплаты();
	УстановитьДоступностьЭлементовПоФормеОплаты();
	ОбновитьСлужебныеРеквизиты();
	
	УстановитьСпециальныеЗаголовкиЭтаповДляВозврата();
	НастроитьЭлементыФормы();
	УстановитьТекстКнопкиЗаполнения(ЭтаФорма);
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);

	КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);
	
	УстановитьСвойстваЭлементаПорядокРасчетов();
	УстановитьПараметрыВыбораКассыСчета();
	
	#Область УХ_Встраивание
	Если ЭтотОбъект.УстанавливатьДоступность Тогда
		АналитикиПланированияДокументов.ОбщаяФормаЭтапыОплаты_НедоступностьКнопокЗаполнения(ЭтотОбъект);
	КонецЕсли;
	#КонецОбласти
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Модифицированность И Не Готово Тогда
		
		Отказ = Истина;
		
		СписокКнопок = Новый СписокЗначений();
		СписокКнопок.Добавить("Закрыть", НСтр("ru = 'Закрыть';
												|en = 'Close'"));
		СписокКнопок.Добавить("НеЗакрывать", НСтр("ru = 'Не закрывать';
													|en = 'Do not close'"));
		
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект), 
			НСтр("ru = 'Все измененные данные будут потеряны. Закрыть форму?';
				|en = 'All changed data will be lost. Close the form?'"), 
			СписокКнопок);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = "Закрыть" Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьПоУмолчанию()
	
	ОчиститьСообщения();
	
	ЗаполнитьЭтапыГрафикаОплаты();
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределитьСумму(Команда)
	
	ОчиститьСообщения();
	РаспределитьСуммуПоЭтапамГрафикаОплаты();
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ОчиститьСообщения();
	
	Если Не Модифицированность Или ТолькоПросмотр Тогда
		
		Закрыть();
		
	Иначе
		
		СтруктураОбъекта = Новый Структура;
		СтруктураОбъекта.Вставить("ПорядокРасчетов",           ПорядокРасчетов);
		СтруктураОбъекта.Вставить("ФормаОплаты",               ФормаОплаты);
		СтруктураОбъекта.Вставить("Касса",                     Касса);
		СтруктураОбъекта.Вставить("БанковскийСчетОрганизации", БанковскийСчет);
		СтруктураОбъекта.Вставить("ГрафикОплаты",              ГрафикОплаты);
		СтруктураОбъекта.Вставить("ЖелаемаяДатаОтгрузки",      ЖелаемаяДатаОтгрузки);
		СтруктураОбъекта.Вставить("ОплатаВВалюте",             ОплатаВВалюте);
		
		Если РежимСамообслуживания Тогда
			Готово = Истина;
		ИначеЕсли УпрощенныйРежим Тогда
			
			ТекстОшибки = "";
			
			Если Не (ЭтоЗаказ И ПорядокРасчетов = ПредопределенноеЗначение("Перечисление.ПорядокРасчетов.ПоНакладным")
				Или ЗначениеЗаполнено(ГрафикИсполненияДоговора) И ПорядокРасчетов = ПредопределенноеЗначение("Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов"))
				Или ЭтоЗаказ И ЗначениеЗаполнено(ГрафикИсполненияДоговора) И ПорядокРасчетов = ПредопределенноеЗначение("Перечисление.ПорядокРасчетов.ПоДоговорамНакладным") Тогда
				Если Не ЗначениеЗаполнено(ДатаПредоплата) Тогда
					Если СуммаОплатыПоДокументу <> 0 Тогда
						ТекстОшибки = НСтр("ru = 'Поле ""Дата платежа"" не заполнено';
											|en = '""Payment date"" is required'");
					КонецЕсли;
				ИначеЕсли ЗначениеЗаполнено(Дата) И ДатаПредоплата < Дата Тогда
					ТекстОшибки = НСтр("ru = 'Дата платежа должна быть не меньше даты документа %ДатаДокумента%';
										|en = 'Payment date must be not earlier than document date %ДатаДокумента%'");
					ТекстОшибки = СтрЗаменить(ТекстОшибки,"%ДатаДокумента%", Формат(Дата, "ДЛФ=DD"));
				КонецЕсли;
			КонецЕсли;
			
			Если ТекстОшибки = "" Тогда
				СтруктураОбъекта.Вставить("ДатаПлатежа", ДатаПредоплата);
				АдресВоВременномХранилище = ПреобразоватьПоместитьВоВременноеХранилище();
				Готово = АдресВоВременномХранилище <> "";
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "ДатаПредоплата");
				АдресВоВременномХранилище = "";
			КонецЕсли;
			
		Иначе
			АдресВоВременномХранилище = ПроверитьОплатуПоместитьВХранилище();
		КонецЕсли;
		
		Если Не ПустаяСтрока(АдресВоВременномХранилище) Тогда
			Готово = Истина;
			СтруктураОбъекта.Вставить("АдресВоВременномХранилище", АдресВоВременномХранилище);
		КонецЕсли;
		
		Если Готово Тогда
			Закрыть(СтруктураОбъекта);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПорядокРасчетовПриИзменении(Элемент)
	
	ОбновитьСлужебныеРеквизиты();
	ПорядокРасчетовПриИзмененииСервер();
	НастроитьЭлементыФормыПриИзмененииПорядкаРасчетов();
	
КонецПроцедуры

&НаКлиенте
Процедура ГрафикОплатыПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ГрафикОплаты) Тогда
		Если УпрощенныйРежим Тогда
			ЗаполнитьДатуПредоплатыПоГрафикуСервер(Истина);
		Иначе
			ЗаполнитьЭтапыОплатыПоГрафикуСервер(Истина);
			ПронумероватьТаблицуЭтапов();
			РассчитатьИтоговыеПоказатели(ЭтаФорма);
		КонецЕсли;
	КонецЕсли;
	
	УстановитьТекстКнопкиЗаполнения(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ФормаОплатыПриИзменении(Элемент)
	
	ПриИзмененииФормыОплатыСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПлатежаПредоплатаПриИзменении(Элемент)
	
	СуммаПлатежаПриИзменении(СуммаПлатежаПредоплата, ПроцентПлатежаПредоплата, 
							СуммаПлатежаКредит, ПроцентПлатежаКредит, СуммаОплатыПоДокументу);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцентПлатежаПредоплатаПриИзменении(Элемент)
	
	ПроцентПлатежаПриИзменении(СуммаПлатежаПредоплата, ПроцентПлатежаПредоплата,
								СуммаПлатежаКредит, ПроцентПлатежаКредит, СуммаОплатыПоДокументу);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаЗалогаЗаТаруПредоплатаПриИзменении(Элемент)
	
	СуммаПлатежаПриИзменении(СуммаЗалогаЗаТаруПредоплата, ПроцентЗалогаЗаТаруПредоплата, 
							СуммаЗалогаЗаТаруКредит, ПроцентЗалогаЗаТаруКредит, СуммаЗалогаПоДокументу);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцентЗалогаЗаТаруПредоплатаПриИзменении(Элемент)
	
	ПроцентПлатежаПриИзменении(СуммаЗалогаЗаТаруПредоплата, ПроцентЗалогаЗаТаруПредоплата, 
							СуммаЗалогаЗаТаруКредит, ПроцентЗалогаЗаТаруКредит, СуммаЗалогаПоДокументу);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПлатежаКредитПриИзменении(Элемент)
	
	СуммаПлатежаПриИзменении(СуммаПлатежаКредит, ПроцентПлатежаКредит, 
							СуммаПлатежаПредоплата, ПроцентПлатежаПредоплата, СуммаОплатыПоДокументу);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцентПлатежаКредитПриИзменении(Элемент)
	
	ПроцентПлатежаПриИзменении(СуммаПлатежаКредит, ПроцентПлатежаКредит, 
							СуммаПлатежаПредоплата, ПроцентПлатежаПредоплата, СуммаОплатыПоДокументу);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаЗалогаЗаТаруКредитПриИзменении(Элемент)
	
	СуммаПлатежаПриИзменении(СуммаЗалогаЗаТаруКредит, ПроцентЗалогаЗаТаруКредит,
							СуммаЗалогаЗаТаруПредоплата, ПроцентЗалогаЗаТаруПредоплата, СуммаЗалогаПоДокументу);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцентЗалогаЗаТаруКредитПриИзменении(Элемент)
	
	ПроцентПлатежаПриИзменении(СуммаЗалогаЗаТаруКредит, ПроцентЗалогаЗаТаруКредит,
							СуммаЗалогаЗаТаруПредоплата, ПроцентЗалогаЗаТаруПредоплата, СуммаЗалогаПоДокументу);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаВВалютеПриИзменении(Элемент)
	
	ПриИзмененииОплатаВВалютеСервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЭтапыГрафикаОплаты

&НаКлиенте
Процедура ЭтапыГрафикаОплатыПослеУдаления(Элемент)
	
	ПронумероватьТаблицуЭтапов();
	ОчиститьСуммыОтклоненийМерныхТоваров();
	РассчитатьИтоговыеПоказатели(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыГрафикаОплатыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ТекущиеДанные = Элементы.ЭтапыГрафикаОплаты.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Если ПредоплатаВНакладной 
			И ТекущиеДанные.ВариантОплаты = ПредопределенноеЗначение("Перечисление.ВариантыОплатыКлиентом.ПредоплатаДоОтгрузки")Тогда
				ТекущиеДанные.ДатаПлатежа = Дата;
		КонецЕсли;
	КонецЕсли;
	
	СортироватьЭтапыОплаты();
	ПронумероватьТаблицуЭтапов();
	ОчиститьСуммыОтклоненийМерныхТоваров();
	РассчитатьИтоговыеПоказатели(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ЭтапыГрафикаОплатыПроцентПлатежаПриИзменении(Элемент)

	ЭтапыОплатыКлиент.ЭтапыГрафикаОплатыПроцентПлатежаПриИзменении(
		Элементы.ЭтапыГрафикаОплаты.ТекущиеДанные,
		ЭтапыГрафикаОплаты,
		СуммаОплатыПоДокументу);

КонецПроцедуры

&НаКлиенте
Процедура ЭтапыГрафикаОплатыСуммаПлатежаПриИзменении(Элемент)

	ЭтапыОплатыКлиент.ЭтапыГрафикаОплатыСуммаПлатежаПриИзменении(
		Элементы.ЭтапыГрафикаОплаты.ТекущиеДанные,
		ЭтапыГрафикаОплаты,
		СуммаОплатыПоДокументу);

КонецПроцедуры

&НаКлиенте
Процедура ЭтапыГрафикаОплатыПриИзменении(Элемент)
	
	ПронумероватьТаблицуЭтапов();
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыОплатыПроцентЗалогаЗаТаруПриИзменении(Элемент)

	ЭтапыОплатыКлиент.ЭтапыГрафикаОплатыПроцентЗалогаЗаТаруПриИзменении(
		Элементы.ЭтапыГрафикаОплаты.ТекущиеДанные,
		ЭтапыГрафикаОплаты,
		СуммаЗалогаПоДокументу);

КонецПроцедуры

&НаКлиенте
Процедура ЭтапыОплатыСуммаЗалогаЗаТаруПриИзменении(Элемент)

	ЭтапыОплатыКлиент.ЭтапыГрафикаОплатыСуммаЗалогаЗаТаруПриИзменении(
		Элементы.ЭтапыГрафикаОплаты.ТекущиеДанные,
		ЭтапыГрафикаОплаты,
		СуммаЗалогаПоДокументу);

КонецПроцедуры

&НаКлиенте
Процедура ЭтапыГрафикаОплатыСдвигПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ЭтапыГрафикаОплаты.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Если ЗначениеЗаполнено(ЖелаемаяДатаОтгрузки) Тогда
			ТекущиеДанные.ДатаПлатежа = ЖелаемаяДатаОтгрузки + 86400 * ТекущиеДанные.Сдвиг;
		Иначе
			ТекущиеДанные.ДатаПлатежа = Дата + 86400 * ТекущиеДанные.Сдвиг
		КонецЕсли;
	КонецЕсли;
	СортироватьЭтапыОплаты();
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыОплатыВариантОплатыПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ЭтапыГрафикаОплаты.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Если ТекущиеДанные.Сдвиг <> 0 И ТекущиеДанные.ВариантОплаты <> ПредопределенноеЗначение("Перечисление.ВариантыОплатыКлиентом.КредитСдвиг") Тогда
			ТекущиеДанные.Сдвиг = 0;
		ИначеЕсли ТекущиеДанные.ВариантОплаты = ПредопределенноеЗначение("Перечисление.ВариантыОплатыКлиентом.КредитСдвиг") Тогда
			Если ЗначениеЗаполнено(ТекущиеДанные.Сдвиг) Тогда
				Если ЗначениеЗаполнено(ЖелаемаяДатаОтгрузки) Тогда
					ТекущиеДанные.ДатаПлатежа = ЖелаемаяДатаОтгрузки + 86400 * ТекущиеДанные.Сдвиг;
				Иначе
					ТекущиеДанные.ДатаПлатежа = Дата + 86400 * ТекущиеДанные.Сдвиг
				КонецЕсли;
			Иначе
				ТекущиеДанные.ДатаПлатежа = Дата;
			КонецЕсли;
		ИначеЕсли ПредоплатаВНакладной 
			И ТекущиеДанные.ВариантОплаты = ПредопределенноеЗначение("Перечисление.ВариантыОплатыКлиентом.ПредоплатаДоОтгрузки")Тогда
				ТекущиеДанные.ДатаПлатежа = Дата;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыОплатыДатаПлатежаПриИзменении(Элемент)
	СортироватьЭтапыОплаты();
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыГрафикаОплатыПередУдалением(Элемент, Отказ)
	ТекущиеДанные = Элементы.ЭтапыГрафикаОплаты.ТекущиеДанные;
	Если НЕ ЭтоЗаказ И НакладнаяПоЗаказам И ТекущиеДанные <> Неопределено И НЕ ПредоплатаВНакладной
		И ТекущиеДанные.ВариантОплаты = ПредопределенноеЗначение("Перечисление.ВариантыОплатыКлиентом.ПредоплатаДоОтгрузки") Тогда
			Отказ = Истина;
	КонецЕсли;
	
	#Область УХ_Встраивание
	Если ЭтотОбъект.ДолжныФормироватьсяПозиции И ТекущиеДанные.НеУдалять Тогда
		Отказ = Истина;
	КонецЕсли;
	#КонецОбласти
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыГрафикаОплатыПередНачаломИзменения(Элемент, Отказ)
	ТекущиеДанные = Элементы.ЭтапыГрафикаОплаты.ТекущиеДанные;
	Если ПредоплатаВНакладной И Элемент.ТекущийЭлемент.Имя = "ЭтапыОплатыДатаПлатежа"
		И ТекущиеДанные.ВариантОплаты = ПредопределенноеЗначение("Перечисление.ВариантыОплатыКлиентом.ПредоплатаДоОтгрузки") Тогда
			Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыОплатыДатаПлатежа.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтапыГрафикаОплаты.ДатаЗаполненаНеВерно");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.FireBrick);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДатаКредит.Имя);

	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаКредит");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("ДатаПредоплата");

	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаКредит");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Дата(1,1,1);

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.FireBrick);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыОплатыПроцентПлатежа.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтапыГрафикаОплаты.ПроцентЗаполненНеВерно");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.FireBrick);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПроцентПлатежаПредоплата.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПроцентПлатежаКредит.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПроцентПлатежейОбщий");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = 100;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СуммаОплатыПоДокументу");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = 0;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.FireBrick);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПроцентПлатежаПредоплата.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПроцентПлатежаКредит.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПроцентПлатежейОбщий");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 100;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СуммаОплатыПоДокументу");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = 0;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.ЦветМорскойВолны);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДатаПредоплата.Имя);

	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;

	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СуммаПлатежаПредоплата");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;

	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СуммаОплатыПоДокументу");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;

	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;

	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТребуетсяЗалогЗаТару");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СуммаЗалогаЗаТаруПредоплата");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;

	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СуммаЗалогаПоДокументу");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;

	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДатаКредит.Имя);

	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;

	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СуммаПлатежаКредит");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;

	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СуммаОплатыПоДокументу");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;

	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;

	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТребуетсяЗалогЗаТару");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СуммаЗалогаЗаТаруКредит");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;

	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СуммаЗалогаПоДокументу");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;

	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыОплатыПроцентПлатежа.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтапыГрафикаОплаты.ДатаЗаполненаНеВерно");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтапыГрафикаОплаты.НомерСтроки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("НомерСтрокиПолнойОплаты");

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НомерСтрокиПолнойОплаты");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = 0;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Seagreen);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыОплатыПроцентЗалогаЗаТару.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтапыГрафикаОплаты.ПроцентЗалогаЗаполненНеВерно");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.FireBrick);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПроцентЗалогаЗаТаруПредоплата.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПроцентЗалогаЗаТаруКредит.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПроцентЗалогаОбщий");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = 100;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СуммаЗалогаПоДокументу");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = 0;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.FireBrick);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПроцентЗалогаЗаТаруПредоплата.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПроцентЗалогаЗаТаруКредит.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПроцентЗалогаОбщий");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 100;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СуммаЗалогаПоДокументу");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = 0;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.ЦветМорскойВолны);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыОплатыПроцентЗалогаЗаТару.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтапыГрафикаОплаты.ДатаЗаполненаНеВерно");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтапыГрафикаОплаты.НомерСтроки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("НомерСтрокиПолнойОплатыЗалога");

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НомерСтрокиПолнойОплатыЗалога");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = 0;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Seagreen);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыОплатыПроцентПлатежа.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыОплатыПроцентЗалогаЗаТару.Имя);

	ГруппаОтбора1 = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора1.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;

	ОтборЭлемента = ГруппаОтбора1.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтапыГрафикаОплаты.ПроцентЗалогаЗаТару");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;

	ОтборЭлемента = ГруппаОтбора1.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтапыГрафикаОплаты.ПроцентПлатежа");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыОплатыПроцентЗалогаЗаТару.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыОплатыСуммаЗалогаЗаТару.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТребуетсяЗалогЗаТару");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

	//

	Если Параметры.СпециальныеЗаголовкиЭтаповДляВозврата Тогда

		Элемент = УсловноеОформление.Элементы.Добавить();

		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыОплатыВариантОплаты.Имя);

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтапыГрафикаОплаты.ВариантОплаты");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Перечисления.ВариантыОплатыКлиентом.АвансДоОбеспечения;

		Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Аванс или возврат (до обеспечения)';
																	|en = 'Advance or return (before supply)'"));

		//

		Элемент = УсловноеОформление.Элементы.Добавить();

		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыОплатыВариантОплаты.Имя);

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтапыГрафикаОплаты.ВариантОплаты");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Перечисления.ВариантыОплатыКлиентом.ПредоплатаДоОтгрузки;

		Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Предоплата или возврат (до отгрузки)';
																	|en = 'Prepayment or return (before shipment)'"));

		//

		Элемент = УсловноеОформление.Элементы.Добавить();

		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыОплатыВариантОплаты.Имя);

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтапыГрафикаОплаты.ВариантОплаты");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Перечисления.ВариантыОплатыКлиентом.КредитПослеОтгрузки;

		Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Кредит или возврат (плановый)';
																	|en = 'Credit or return (scheduled)'"));
		
		//

		Элемент = УсловноеОформление.Элементы.Добавить();

		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыОплатыВариантОплаты.Имя);

		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтапыГрафикаОплаты.ВариантОплаты");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Перечисления.ВариантыОплатыКлиентом.КредитСдвиг;

		Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Кредит или возврат (после отгрузки)';
																	|en = 'Credit or return (after shipment)'"));

	КонецЕсли;

		//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыОплатыДатаПлатежа.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтапыГрафикаОплаты.ВариантОплаты");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ВариантыОплатыКлиентом.КредитСдвиг;

	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыГрафикаОплатыСдвиг.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтапыГрафикаОплаты.ВариантОплаты");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ВариантыОплатыКлиентом.КредитСдвиг;

	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыОплатыВариантОплаты.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыОплатыДатаПлатежа.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыОплатыПроцентЗалогаЗаТару.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыОплатыПроцентПлатежа.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыОплатыСуммаЗалогаЗаТару.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыГрафикаОплатыСуммаПлатежа.Имя);
	
	ГруппаОтбора1 = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора1.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;

	ОтборЭлемента = ГруппаОтбора1.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтапыГрафикаОплаты.ВариантОплаты");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ВариантыОплатыКлиентом.ПредоплатаДоОтгрузки;

	ОтборЭлемента = ГруппаОтбора1.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтоЗаказ");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	ОтборЭлемента = ГруппаОтбора1.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПредоплатаВНакладной");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	ОтборЭлемента = ГруппаОтбора1.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПорядокРасчетов");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	СписокПорядков = Новый СписокЗначений();
	СписокПорядков.Добавить(Перечисления.ПорядокРасчетов.ПоЗаказамНакладным);
	СписокПорядков.Добавить(Перечисления.ПорядокРасчетов.ПоДоговорамНакладным);
	ОтборЭлемента.ПравоеЗначение = СписокПорядков;

	Элемент.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);
	
КонецПроцедуры

#Область ГрафикОплаты

&НаСервере
Процедура ЗаполнитьЭтапыОплатыПоСоглашениюСервер(ЗаполнятьФормуОплаты = Ложь)
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("ГрафикОплаты",         ГрафикОплаты);
	ПараметрыЗаполнения.Вставить("ФормаОплаты",          ФормаОплаты);
	ПараметрыЗаполнения.Вставить("ЭтапыГрафикаОплаты",   ЭтапыГрафикаОплаты);
	ПараметрыЗаполнения.Вставить("Дата",                 Дата);
	ПараметрыЗаполнения.Вставить("ЖелаемаяДатаОтгрузки", ЖелаемаяДатаОтгрузки);
	ПараметрыЗаполнения.Вставить("Соглашение",           Соглашение);

	ЭтапыОплатыСервер.ЗаполнитьЭтапыОплатыДокументаПродажиПоСоглашению(
		ПараметрыЗаполнения,
		СуммаОплатыПоДокументу,
		СуммаЗалогаПоДокументу);
	
	Если УпрощенныйРежим Тогда
		ПреобразоватьТаблицуЭтаповВДанныеУпрощенногоРежима(ЭтапыГрафикаОплаты.Выгрузить());
	ИначеЕсли НЕ ЭтоЗаказ И НЕ НакладнаяПоЗаказам Тогда
		ЭтапыОплатыСервер.СвернутьЭтапыОплаты(ЭтапыГрафикаОплаты, ЕстьПредоплата);
	КонецЕсли;
	
	Если ЗаполнятьФормуОплаты Тогда
		ФормаОплаты = ПараметрыЗаполнения.ФормаОплаты;
		ПриИзмененииФормыОплатыСервер();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЭтапыОплатыПоГрафикуСервер(ЗаполнятьФормуОплаты = Ложь)
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("ГрафикОплаты",         ГрафикОплаты);
	ПараметрыЗаполнения.Вставить("ФормаОплаты",          ФормаОплаты);
	ПараметрыЗаполнения.Вставить("ЭтапыГрафикаОплаты",   ЭтапыГрафикаОплаты);
	ПараметрыЗаполнения.Вставить("Дата",                 Дата);
	ПараметрыЗаполнения.Вставить("ЖелаемаяДатаОтгрузки", ЖелаемаяДатаОтгрузки);
	
	ЭтапыОплатыСервер.ЗаполнитьЭтапыОплатыДокументаПродажиПоГрафикуОплаты(ПараметрыЗаполнения, СуммаОплатыПоДокументу, СуммаЗалогаПоДокументу, ЗаполнятьФормуОплаты);
	
	Если УпрощенныйРежим Тогда
		ПреобразоватьТаблицуЭтаповВДанныеУпрощенногоРежима(ЭтапыГрафикаОплаты.Выгрузить());
	ИначеЕсли НЕ ЭтоЗаказ И НЕ НакладнаяПоЗаказам Тогда
		ЭтапыОплатыСервер.СвернутьЭтапыОплаты(ЭтапыГрафикаОплаты, ЕстьПредоплата);
	КонецЕсли;
	
	Если ЗаполнятьФормуОплаты Тогда
		ФормаОплаты = ПараметрыЗаполнения.ФормаОплаты;
		ПриИзмененииФормыОплатыСервер();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДатуПредоплатыПоГрафикуСервер(ЗаполнятьФормуОплаты = Ложь)
	
	Если ЗначениеЗаполнено(ГрафикИсполненияДоговора) Тогда
		ДатаПредоплата = ЭтапыОплатыСервер.ДатаПервогоНеоплаченногоЭтапаГрафика(ГрафикИсполненияДоговора, Дата);
	Иначе
		ДатаПредоплата = ПродажиСервер.ПолучитьПоследнююДатуПоГрафику(
		?(ЗначениеЗаполнено(ЖелаемаяДатаОтгрузки), ЖелаемаяДатаОтгрузки, Дата),
		?(ИспользоватьГрафикиОплаты, ГрафикОплаты, Неопределено),
		Соглашение);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаПредоплата) Тогда
		ДатаПредоплата = ?(ЗначениеЗаполнено(ЖелаемаяДатаОтгрузки), ЖелаемаяДатаОтгрузки, Дата);
	КонецЕсли;
	
	Если ЗаполнятьФормуОплаты Тогда
		
		Если ИспользоватьГрафикиОплаты И ЗначениеЗаполнено(ГрафикОплаты) Тогда
			ИсточникФормыОплаты = ГрафикОплаты;
		ИначеЕсли ЗначениеЗаполнено(Соглашение) Тогда
			ИсточникФормыОплаты = Соглашение;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ИсточникФормыОплаты) Тогда
			
			ФормаОплатыИсточника = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ИсточникФормыОплаты, "ФормаОплаты");
		
			Если ЗначениеЗаполнено(ФормаОплатыИсточника) И ФормаОплаты <> ФормаОплатыИсточника Тогда
				ФормаОплаты = ФормаОплатыИсточника;
				ПриИзмененииФормыОплатыСервер();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЖелаемаяДатаОплатыЭтапаПоУмолчанию()
	
	Если ЗначениеЗаполнено(ГрафикИсполненияДоговора) Тогда
		Возврат ЭтапыОплатыСервер.ДатаПервогоНеоплаченногоЭтапаГрафика(ГрафикИсполненияДоговора, Дата);
	Иначе
		Возврат ?(ЗначениеЗаполнено(ЖелаемаяДатаОтгрузки), ЖелаемаяДатаОтгрузки, Дата);
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьЭтапОплатыПоУмолчанию()
	
	ЖелаемаяДата = ЖелаемаяДатаОплатыЭтапаПоУмолчанию();
	
	Если УпрощенныйРежим Тогда
		
		ОчиститьДанныеУпрощенногоРежима(ЭтаФорма);
		
		ДатаКредит                = ЖелаемаяДата;
		СуммаПлатежаКредит        = СуммаОплатыПоДокументу;
		ПроцентПлатежаКредит      = 100;
		СуммаЗалогаЗаТаруКредит   = СуммаЗалогаПоДокументу;
		ПроцентЗалогаЗаТаруКредит = 100;
		ДатаПредоплата            = Дата;
		
	Иначе
		
		#Область УХ_Встраивание
		Если НЕ ЭтотОбъект.ДолжныФормироватьсяПозиции Тогда
		#КонецОбласти
		ЭтапыГрафикаОплаты.Очистить();
		#Область УХ_Встраивание
		КонецЕсли;
		#КонецОбласти
		
		ПараметрыДляДобавления = ЭтапыОплатыКлиентСервер.ПараметрыДляДобавленияЭтапаПоУмолчанию();
		ПараметрыДляДобавления.Объект = ЭтаФорма;
		ПараметрыДляДобавления.ВариантОплаты = ВариантОплатыПоУмолчанию;
		ПараметрыДляДобавления.ЖелаемаяДата = ЖелаемаяДата;
		ПараметрыДляДобавления.ДатаСеанса = ОбщегоНазначенияКлиент.ДатаСеанса();
		ПараметрыДляДобавления.СуммаОплатыПоДокументу = СуммаОплатыПоДокументу;
		ПараметрыДляДобавления.СуммаЗалогаПоДокументу = СуммаЗалогаПоДокументу;
		ПараметрыДляДобавления.СуммаОтклоненияМерныхТоваров = 0;
		
		#Область УХ_Встраивание
		ИтогПроцентов = 0;
		Если ЭтотОбъект.ДолжныФормироватьсяПозиции Тогда
			Для каждого Строка Из ЭтапыГрафикаОплаты Цикл
				Если Строка.НеУдалять Тогда
					ПараметрыДляДобавления.СуммаОплатыПоДокументу = ПараметрыДляДобавления.СуммаОплатыПоДокументу - Строка.СуммаПлатежа;
					ИтогПроцентов = ИтогПроцентов + Строка.ПроцентПлатежа;
				Иначе
					ЭтапыГрафикаОплаты.Удалить(Строка);
				КонецЕсли;
			КонецЦикла;
		Иначе
			ЭтапыГрафикаОплаты.Очистить();
		КонецЕсли;
		#КонецОбласти
		
		ЭтапыОплатыКлиентСервер.ДобавитьЭтапОплатыПоУмолчанию(ПараметрыДляДобавления);
		
		#Область УХ_Встраивание
		Если ЭтотОбъект.ДолжныФормироватьсяПозиции Тогда
			Для каждого Стр Из ЭтапыГрафикаОплаты Цикл
				Если НЕ Строка.НеУдалять Тогда
					Стр.ПроцентПлатежа = Стр.ПроцентПлатежа - ИтогПроцентов;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		#КонецОбласти
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РаспределитьСуммуПоЭтапамГрафикаОплаты()
	
	Если УпрощенныйРежим Тогда
		
		ЭтапыОплаты = ПреобразоватьДанныеУпрощенногоРежимаВТаблицуЭтапов();
		
		ЭтапыОплатыКлиентСервер.РаспределитьСуммуПоЭтапамГрафикаОплаты(
			ЭтапыОплаты,
			СуммаОплатыПоДокументу,
			СуммаЗалогаПоДокументу);
		
		ПреобразоватьТаблицуЭтаповВДанныеУпрощенногоРежима(ЭтапыОплаты);
		
	Иначе
		
		ЭтапыОплатыКлиентСервер.РаспределитьСуммуПоЭтапамГрафикаОплаты(
			ЭтапыГрафикаОплаты,
			СуммаОплатыПоДокументу,
			СуммаЗалогаПоДокументу);
			
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьОплатуПоместитьВХранилище()
	
	Если УпрощенныйРежим Тогда
		ЭтапыГрафикаОплаты.Загрузить(ПреобразоватьДанныеУпрощенногоРежимаВТаблицуЭтапов());
	Иначе
		Если НЕ ЕстьПредоплата Тогда
			Для Каждого Стр Из ЭтапыГрафикаОплаты Цикл
				Стр.ВариантОплаты = ВариантОплатыПоУмолчанию;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Отказ = Ложь;
	Если Не ((ЭтоЗаказ И ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоНакладным)
		Или ЗначениеЗаполнено(ГрафикИсполненияДоговора) И ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов
		Или ЭтоЗаказ И ЗначениеЗаполнено(ГрафикИсполненияДоговора) И ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамНакладным
		Или НеПроверятьКорректностьЭтаповОплаты) Тогда
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("ЖелаемаяДатаОтгрузки", ЖелаемаяДатаОтгрузки);
		СтруктураПараметров.Вставить("Дата", Дата);
		СтруктураПараметров.Вставить("Валюта", Валюта);
		ПродажиСервер.ПроверитьКорректностьЭтаповГрафикаОплатыПоТаблицеЗначений(
			ЭтапыГрафикаОплаты,
			СуммаОплатыПоДокументу,
			СуммаЗалогаПоДокументу,
			ЭтоЗаказ,
			Отказ,
			УпрощенныйРежим,
			СтруктураПараметров);
	КонецЕсли;
	
	Возврат ?(Отказ, "", ПоместитьВоВременноеХранилищеНаСервере());
	
КонецФункции

#КонецОбласти

#Область ПриИзмененииРеквизитов

&НаСервере
Процедура ПриИзмененииФормыОплатыСервер()

	УстановитьДоступностьЭлементовПоФормеОплаты();
	
	ОплатаВРублях = НЕ ОплатаВВалюте;
	
	СтруктураПараметров = ДенежныеСредстваСервер.ПараметрыЗаполненияБанковскогоСчетаОрганизацииПоУмолчанию();
	СтруктураПараметров.Организация 			= Организация;
	СтруктураПараметров.БанковскийСчет 			= БанковскийСчет;
	СтруктураПараметров.НаправлениеДеятельности = НаправлениеДеятельности;
	
	Если ОплатаВРублях Тогда
		СтруктураПараметров.Валюта				= ВалютаРеглУчета;
	КонецЕсли;

	БанковскийСчет = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(СтруктураПараметров);

	СтруктураПараметров = ДенежныеСредстваСервер.ПараметрыЗаполненияКассыОрганизацииПоУмолчанию();
	СтруктураПараметров.Организация 			= Организация;
	СтруктураПараметров.НаправлениеДеятельности = НаправлениеДеятельности;
	СтруктураПараметров.ФормаОплаты 			= ФормаОплаты;
	СтруктураПараметров.Касса 					= Касса;
	
	Если ОплатаВРублях Тогда
		СтруктураПараметров.Валюта				= ВалютаРеглУчета;
	КонецЕсли;

	Касса          = ЗначениеНастроекПовтИсп.ПолучитьКассуОрганизацииПоУмолчанию(СтруктураПараметров);
	
КонецПроцедуры

&НаСервере
Процедура ПорядокРасчетовПриИзмененииСервер()
	
	Если Не ЗначениеЗаполнено(ПорядокРасчетов) Тогда
		ПорядокРасчетов = Элементы.ПорядокРасчетов.СписокВыбора[0].Значение;
	КонецЕсли;
	
	Если ЭтоЗаказ И (ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоНакладным
		Или ЗначениеЗаполнено(ГрафикИсполненияДоговора) И ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов)
		Или ЭтоЗаказ И ЗначениеЗаполнено(ГрафикИсполненияДоговора) И ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамНакладным Тогда
		
		ЭтапыГрафикаОплаты.Очистить();
		СуммаПлатежаПредоплата      = 0;
		СуммаПлатежаКредит          = 0;
		СуммаЗалогаЗаТаруПредоплата = 0;
		СуммаЗалогаЗаТаруКредит     = 0;
		ИдентификаторПлатежа        = Неопределено;
		
	КонецЕсли;
	
	УстановитьДоступностьЭлементовПоПорядкуРасчета();
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПлатежаПриИзменении(Сумма, Процент, ЗависимаяСумма, ЗависимыйПроцент, СуммаВсего)

	Если СуммаВсего <> 0 Тогда
		
		Процент           = Мин(Сумма * 100 / СуммаВсего, 100);
		ЗависимаяСумма    = Макс(СуммаВсего - Сумма, 0);
		ЗависимыйПроцент  = 100 - Процент;
		
	КонецЕсли;
	
	ОчиститьСуммыОтклоненийМерныхТоваров();
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ПроцентПлатежаПриИзменении(Сумма, Процент, ЗависимаяСумма, ЗависимыйПроцент, СуммаВсего)

	Сумма            = СуммаВсего * Процент / 100;
	ЗависимаяСумма   = СуммаВсего - Сумма;
	ЗависимыйПроцент = 100 - Процент;

	ОчиститьСуммыОтклоненийМерныхТоваров();
	
	РассчитатьИтоговыеПоказатели(ЭтаФорма);

КонецПроцедуры

&НаСервере
Процедура ПриИзмененииОплатаВВалютеСервер()
	
	Если НЕ ОплатаВВалюте Тогда
		
		Если ЗначениеЗаполнено(Касса) И НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Касса,"ВалютаДенежныхСредств")=ВалютаРеглУчета Тогда
			Касса = Справочники.Кассы.ПустаяСсылка();
		КонецЕсли;
		
		Если ЗначениеЗаполнено(БанковскийСчет) И НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БанковскийСчет,"ВалютаДенежныхСредств")=ВалютаРеглУчета Тогда
			БанковскийСчет = Справочники.БанковскиеСчетаОрганизаций.ПустаяСсылка();
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьПараметрыВыбораКассыСчета();
	ПриИзмененииФормыОплатыСервер();
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеЭтаповГрафикаОплаты

&НаКлиенте
Процедура ЗаполнитьЭтапыГрафикаОплаты()
	
	ВариантыОтветов = Новый СписокЗначений;
	ВариантыОтветов.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Перезаполнить этапы оплаты';
														|en = 'Refill payment steps'"));
	ВариантыОтветов.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru = 'Отменить';
															|en = 'Cancel'"));
	
	ТекстВопроса = "";
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("ГрафикЗаполнен", ИспользоватьГрафикиОплаты И ЗначениеЗаполнено(ГрафикОплаты));
	ПараметрыЗаполнения.Вставить("ЗаполнитьДату",  УпрощенныйРежим И Не ЕстьПредоплата);
	ПараметрыЗаполнения.Вставить("РаспределитьСумму", Ложь);
	ПараметрыЗаполнения.Вставить("ПерезаполнитьЭтапы", Ложь);
	
	Если Не ПараметрыЗаполнения.ЗаполнитьДату Тогда
		
		Если УпрощенныйРежим Тогда
			СуммаОплатыПоЭтапам    = СуммаПлатежаПредоплата + СуммаПлатежаКредит;
			СуммаЗалогаПоЭтапам    = СуммаЗалогаЗаТаруПредоплата + СуммаЗалогаЗаТаруКредит;
			ЭтапыОплатыНеЗаполнены = СуммаПлатежаПредоплата = 0 И СуммаПлатежаКредит = 0 И СуммаЗалогаПоЭтапам = 0;
		Иначе
			СуммаОплатыПоЭтапам    = ЭтапыГрафикаОплаты.Итог("СуммаПлатежа");
			СуммаЗалогаПоЭтапам    = ЭтапыГрафикаОплаты.Итог("СуммаЗалогаЗаТару");
			ЭтапыОплатыНеЗаполнены = ЭтапыГрафикаОплаты.Количество() = 0;
		КонецЕсли;
		
		Если СуммаОплатыПоДокументу = 0 И СуммаЗалогаПоДокументу = 0 Тогда
			
			Если ЭтапыОплатыНеЗаполнены Тогда
				
				ПоказатьПредупреждение(Неопределено, НСтр("ru = 'Сумма неотмененных строк заказа нулевая. Заполнение этапов оплаты не требуется.';
															|en = 'Amount of uncanceled order lines is zero. It is not required to fill in payment steps.'"));
				Возврат;
				
			КонецЕсли;
			
			ЭтапыГрафикаОплаты.Очистить();
			СуммаПлатежаПредоплата      = 0;
			СуммаПлатежаКредит          = 0;
			СуммаЗалогаЗаТаруПредоплата = 0;
			СуммаЗалогаЗаТаруКредит     = 0;
			ЭтапыОплатыКлиент.ОповеститьОНевозможностиЗаполненияЭтаповГрафикаОплаты();
			РассчитатьИтоговыеПоказатели(ЭтаФорма);
			Возврат;
		
		КонецЕсли;
		
		Если СуммаОплатыПоДокументу = СуммаОплатыПоЭтапам  И СуммаЗалогаПоДокументу = СуммаЗалогаПоЭтапам Тогда
		
			ТекстВопроса = НСтр("ru = 'Сумма заказанных строк совпадает с суммой этапов оплаты';
								|en = 'Amount of order lines is equal to the amount of payment steps'") + Символы.ПС +
								НСтр("ru = 'Перезаполнить этапы оплаты %ИсточникЗаполнения%?';
									|en = 'Refill steps of payment %ИсточникЗаполнения%?'");
		
		ИначеЕсли Не ЭтапыОплатыНеЗаполнены Тогда
		
			ВариантыОтветов.Вставить(1, КодВозвратаДиалога.Нет, НСтр("ru = 'Распределить сумму';
																	|en = 'Allocate amount'"));
			
			ТекстВопроса = НСтр("ru = 'Этапы оплаты заполнены';
								|en = 'Payment steps are filled in'")+ Символы.ПС +
							НСтр("ru = 'Перезаполнить этапы оплаты %ИсточникЗаполнения% или распределить сумму по имеющимся этапам?';
								|en = 'Refill payment steps %ИсточникЗаполнения% or allocate the amount to steps?'");
		
		Иначе
			ПараметрыЗаполнения.ПерезаполнитьЭтапы = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(ТекстВопроса) Тогда
		
		Если ПараметрыЗаполнения.ГрафикЗаполнен Тогда 
			ТекстВопроса = СтрЗаменить(ТекстВопроса, "%ИсточникЗаполнения%", НСтр("ru = 'по графику';
																					|en = 'on schedule'"));
		ИначеЕсли ГрафикСоглашенияЗаполнен Тогда
			ТекстВопроса = СтрЗаменить(ТекстВопроса, "%ИсточникЗаполнения%", НСтр("ru = 'по соглашению';
																					|en = 'by agreement'"));
		Иначе
			ТекстВопроса = СтрЗаменить(ТекстВопроса, "%ИсточникЗаполнения%", НСтр("ru = 'по умолчанию';
																					|en = 'default'"));
		КонецЕсли;
		
		Оповещение = Новый ОписаниеОповещения("ВыборИсточникаЗаполненияЗавершение", ЭтотОбъект, ПараметрыЗаполнения);
		ПоказатьВопрос(Оповещение , ТекстВопроса, ВариантыОтветов);
		Возврат;
		
	КонецЕсли;
	
	ЗаполнитьЭтапыГрафикаОплатыФрагмент(ПараметрыЗаполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборИсточникаЗаполненияЗавершение(РезультатВопроса, ПараметрыЗаполнения) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ПараметрыЗаполнения.ПерезаполнитьЭтапы = Истина;
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		ПараметрыЗаполнения.РаспределитьСумму = Истина;
	Иначе
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЭтапыГрафикаОплатыФрагмент(ПараметрыЗаполнения);

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЭтапыГрафикаОплатыФрагмент(ПараметрыЗаполнения)

	Если (ПараметрыЗаполнения.ЗаполнитьДату Или ПараметрыЗаполнения.ПерезаполнитьЭтапы) Тогда
		
		ПроверитьКорректностьЖелаемойДатыОтгрузки(ПараметрыЗаполнения);
		
	Иначе
		
		ЗаполнитьЭтапыГрафикаОплатыЗавершение(ПараметрыЗаполнения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьКорректностьЖелаемойДатыОтгрузки(ПараметрыЗаполнения)
	
	Если Не НеПроверятьКорректностьЭтаповОплаты
		И ЗначениеЗаполнено(ЖелаемаяДатаОтгрузки) И ЖелаемаяДатаОтгрузки < НачалоДня(Дата) Тогда
		
		Оповещение = Новый ОписаниеОповещения("ВводЖелаемойДатыОтгрузкиФрагмент", ЭтаФорма, ПараметрыЗаполнения);
		
		ТекстВопроса = НСтр("ru = 'Желаемая дата отгрузки меньше даты документа. Необходимо ввести корректную желаемую дату.';
							|en = 'Requested shipment date is less than the document date. Enter a correct required date.'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		
	Иначе
		
		ЗаполнитьЭтапыГрафикаОплатыЗавершение(ПараметрыЗаполнения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВводЖелаемойДатыОтгрузкиФрагмент(Результат, ПараметрыЗаполнения) Экспорт
	
	Если Результат <> Неопределено И Результат = КодВозвратаДиалога.ОК Тогда
		
		ЖелаемаяДата = НачалоДня(Дата);
		
		Оповещение = Новый ОписаниеОповещения("ВводЖелаемойДатыОтгрузкиЗавершение", ЭтаФорма, ПараметрыЗаполнения);
		
		ОбщегоНазначенияУТКлиент.ВвестиДатуСКонтролемПустогоЗначения(
			ЖелаемаяДата,
			НСтр("ru = 'Введите желаемую дату отгрузки';
				|en = 'Enter the required shipment date'"),
			ЧастиДаты.Дата,
			Оповещение);
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВводЖелаемойДатыОтгрузкиЗавершение(ВыбраннаяДата, ПараметрыЗаполнения) Экспорт
	
	Если ВыбраннаяДата <> Неопределено И ЗначениеЗаполнено(ВыбраннаяДата) Тогда
		
		Если ВыбраннаяДата < НачалоДня(Дата) Тогда
			ТекстПредупреждения = НСтр("ru = 'Желаемая дата отгрузки меньше даты документа. Этапы оплаты не могут быть заполнены.';
										|en = 'Requested shipment date is less than the document date. Payment steps cannot be filled in.'");
			ПоказатьПредупреждение(, ТекстПредупреждения);
		Иначе
			ЖелаемаяДатаОтгрузки = ВыбраннаяДата;
			ЗаполнитьЭтапыГрафикаОплатыЗавершение(ПараметрыЗаполнения);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЭтапыГрафикаОплатыЗавершение(ПараметрыЗаполнения)
	
	Если ПараметрыЗаполнения.ЗаполнитьДату Тогда
		ЗаполнитьДатуПредоплатыПоГрафикуСервер();
	КонецЕсли;

	Если ПараметрыЗаполнения.ПерезаполнитьЭтапы Тогда
		
		Если НакладнаяПоЗаказам Тогда
			ЗаполнитьЭтапыПоЗаказамСервер();
		ИначеЕсли ПараметрыЗаполнения.ГрафикЗаполнен Тогда
			ЗаполнитьЭтапыОплатыПоГрафикуСервер();
		ИначеЕсли ГрафикСоглашенияЗаполнен Тогда
			ЗаполнитьЭтапыОплатыПоСоглашениюСервер();
		Иначе
			ДобавитьЭтапОплатыПоУмолчанию();
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПараметрыЗаполнения.РаспределитьСумму Тогда
		Если НакладнаяПоЗаказам Тогда
			РаспределитьСуммуПоЭтапамИЗаказамГрафикаОплаты();
		Иначе
			РаспределитьСуммуПоЭтапамГрафикаОплаты();
		КонецЕсли;
	КонецЕсли;

	ЭтапыОплатыКлиент.ОповеститьОбОкончанииЗаполненияЭтаповГрафикаОплаты();
	ПронумероватьТаблицуЭтапов();
	РассчитатьИтоговыеПоказатели(ЭтаФорма);

КонецПроцедуры

#КонецОбласти

#Область УправлениеЭлементамиФормы

&НаСервере
Процедура УстановитьСпециальныеЗаголовкиЭтаповДляВозврата()
	
	Если Параметры.СпециальныеЗаголовкиЭтаповДляВозврата Тогда
		
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаПредоплата", "Заголовок",
			НСтр("ru = 'Предоплата или возврат (до отгрузки)';
				|en = 'Prepayment or return (before shipment)'"));
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаКредит", "Заголовок",
			НСтр("ru = 'Кредит или возврат (после отгрузки)';
				|en = 'Credit or return (after shipment)'"));
		
		Элементы.ЭтапыОплатыВариантОплаты.РежимВыбораИзСписка = Истина;
		Элементы.ЭтапыОплатыВариантОплаты.СписокВыбора.Добавить(Перечисления.ВариантыОплатыКлиентом.АвансДоОбеспечения, НСтр("ru = 'Аванс или возврат (до обеспечения)';
																															|en = 'Advance or return (before supply)'"));
		Элементы.ЭтапыОплатыВариантОплаты.СписокВыбора.Добавить(Перечисления.ВариантыОплатыКлиентом.ПредоплатаДоОтгрузки, НСтр("ru = 'Предоплата или возврат (до отгрузки)';
																																|en = 'Prepayment or return (before shipment)'"));
		Элементы.ЭтапыОплатыВариантОплаты.СписокВыбора.Добавить(Перечисления.ВариантыОплатыКлиентом.КредитПослеОтгрузки, НСтр("ru = 'Кредит или возврат (плановый)';
																																|en = 'Credit or return (scheduled)'"));
		Элементы.ЭтапыОплатыВариантОплаты.СписокВыбора.Добавить(Перечисления.ВариантыОплатыКлиентом.КредитСдвиг, НСтр("ru = 'Кредит или возврат (после отгрузки)';
																														|en = 'Credit or return (after shipment)'"));
		
	Иначе
		
		Элементы.ЭтапыОплатыВариантОплаты.РежимВыбораИзСписка = Истина;
		Если ЭтоЗаказ Тогда
			Элементы.ЭтапыОплатыВариантОплаты.СписокВыбора.Добавить(Перечисления.ВариантыОплатыКлиентом.АвансДоОбеспечения);
			Элементы.ЭтапыОплатыВариантОплаты.СписокВыбора.Добавить(Перечисления.ВариантыОплатыКлиентом.ПредоплатаДоОтгрузки);
		КонецЕсли;
		Если ПредоплатаВНакладной Тогда
			Элементы.ЭтапыОплатыВариантОплаты.СписокВыбора.Добавить(Перечисления.ВариантыОплатыКлиентом.ПредоплатаДоОтгрузки);
		КонецЕсли;
		Элементы.ЭтапыОплатыВариантОплаты.СписокВыбора.Добавить(Перечисления.ВариантыОплатыКлиентом.КредитПослеОтгрузки);
		Элементы.ЭтапыОплатыВариантОплаты.СписокВыбора.Добавить(Перечисления.ВариантыОплатыКлиентом.КредитСдвиг);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормы()
	
	НастроитьЭлементыФормыПриИзмененииПорядкаРасчетов();
	
	Элементы.ГруппаПредоплата.ОтображатьЗаголовок = Истина;
	Элементы.ГруппаПредоплата.Отображение = ОтображениеОбычнойГруппы.Нет;
		
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДатаПредоплата", "ТолькоПросмотр",
		Не ЭтоЗаказ И ЕстьПредоплата);
	
	МассивЭлементов = Новый Массив;
	МассивЭлементов.Добавить("СуммаЗалогаЗаТаруКредит");
	МассивЭлементов.Добавить("СуммаЗалогаЗаТаруПредоплата");
	МассивЭлементов.Добавить("ПроцентЗалогаЗаТаруКредит");
	МассивЭлементов.Добавить("ПроцентЗалогаЗаТаруПредоплата");
	МассивЭлементов.Добавить("ВалютаЗалогаЗаТаруКредит");
	МассивЭлементов.Добавить("ВалютаЗалогаЗаТаруПредоплата");
	МассивЭлементов.Добавить("СуммаЗалогаПоДокументу");
	МассивЭлементов.Добавить("ВалютаЗалогаПоДокументу");
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(
		Элементы, МассивЭлементов, "Видимость", ТребуетсяЗалогЗаТару);
	
	Если Не НеПроверятьКорректностьЭтаповОплаты Тогда
		
		Если СуммаЗалогаПоДокументу = 0 Тогда
			
			МассивЭлементов = Новый Массив;
			МассивЭлементов.Добавить("СуммаЗалогаЗаТаруКредит");
			МассивЭлементов.Добавить("СуммаЗалогаЗаТаруПредоплата");
			МассивЭлементов.Добавить("ПроцентЗалогаЗаТаруКредит");
			МассивЭлементов.Добавить("ПроцентЗалогаЗаТаруПредоплата");
			
			ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "ТолькоПросмотр", Истина);
			
		КонецЕсли;
		
		Если СуммаОплатыПоДокументу = 0 Тогда
			
			МассивЭлементов = Новый Массив;
			МассивЭлементов.Добавить("СуммаПлатежаПредоплата");
			МассивЭлементов.Добавить("СуммаПлатежаКредит");
			МассивЭлементов.Добавить("ПроцентПлатежаПредоплата");
			МассивЭлементов.Добавить("ПроцентПлатежаКредит");
			
			ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "ТолькоПросмотр", Истина);
			
		КонецЕсли;
		
		Если СуммаЗалогаПоДокументу = 0 И СуммаОплатыПоДокументу = 0 И НЕ ЭтоКорректировка Тогда
			
			МассивЭлементов = Новый Массив;
			МассивЭлементов.Добавить("ДатаПредоплата");
			МассивЭлементов.Добавить("ДатаКредит");
			
			ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "ТолькоПросмотр", Истина);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "ГруппаИсточникиОплаты", "Видимость", Не НеУказыватьИсточникиОплаты);
	
	УстановитьДоступностьКнопокЗаполнения();
	
	Если РежимСамообслуживания И НЕ НакладнаяПоЗаказам Тогда
		
		МассивЭлементов = Новый Массив;
		МассивЭлементов.Добавить("ПорядокРасчетов");
		МассивЭлементов.Добавить("Касса");
		МассивЭлементов.Добавить("БанковскийСчет");
		МассивЭлементов.Добавить("ДатаПредоплата");
		МассивЭлементов.Добавить("СуммаПлатежаПредоплата");
		МассивЭлементов.Добавить("ПроцентПлатежаПредоплата");
		МассивЭлементов.Добавить("СуммаЗалогаЗаТаруПредоплата");
		МассивЭлементов.Добавить("ДатаКредит");
		МассивЭлементов.Добавить("СуммаПлатежаКредит");
		МассивЭлементов.Добавить("СуммаЗалогаЗаТаруКредит");
		МассивЭлементов.Добавить("ПроцентПлатежаКредит");
		МассивЭлементов.Добавить("ПроцентЗалогаЗаТаруКредит");
		МассивЭлементов.Добавить("ЭтапыГрафикаОплаты");
		Если ЗначениеЗаполнено(Соглашение) И ЗначениеЗаполнено(Соглашение.ФормаОплаты) Тогда
			МассивЭлементов.Добавить("ФормаОплаты");
		КонецЕсли;
		
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "ТолькоПросмотр", Истина);
		
		СамообслуживаниеСервер.УправлениеЭлементомФормыФормаОплаты(ЭтаФорма, Соглашение, Элементы.ФормаОплаты);
		
		МассивЭлементов = Новый Массив;
		МассивЭлементов.Добавить("ГрафикОплаты");
		МассивЭлементов.Добавить("ЗаполнитьЭтапыГрафикаОплатыФорма");
		МассивЭлементов.Добавить("ЗаполнитьЭтапыГрафикаОплатыТаблица");
		МассивЭлементов.Добавить("РаспределитьСуммуФорма");
		МассивЭлементов.Добавить("РаспределитьСуммуТаблица");
		
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "Видимость", Ложь);
		
	КонецЕсли;
	
	ВидимостьЭлементовЗаказов = НакладнаяПоЗаказам 
		И (ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоЗаказам
			ИЛИ НЕ ЗначениеЗаполнено(ГрафикИсполненияДоговора) 
					И ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов);
	
	Элементы.ЭтапыГрафикаОплатыЗаказ.Видимость = ВидимостьЭлементовЗаказов;
	Элементы.ЭтапыГрафикаОплатыСверхЗаказа.Видимость = ВидимостьЭлементовЗаказов;
	
	Если СуммаДопустимогоОтклоненияПоДокументу > 0 И ЭтоЗаказ
		И ИспользоватьДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров Тогда
		
		Массив = Новый Массив;
		Массив.Добавить(БиблиотекаКартинок.Внимание16);
		Массив.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Вследствие превышения отгрузки мерных товаров в пределах допустимых отклонений
						| сумма предоплаты может быть превышена на %1 %2';
						|en = 'As measured goods shipment is exceeded within the allowable deviation,
						| the prepayment amount can be exceeded by %1 %2'"),
			СуммаДопустимогоОтклоненияПоДокументу,
			Валюта));
		
		Элементы.НадписьОтклонениеОплатыМерныхТоваров.Заголовок = Новый ФорматированнаяСтрока(Массив);
		
	Иначе
		
		МассивЭлементов = Новый Массив;
		МассивЭлементов.Добавить("ГруппаОтклонениеОплатыМерныхТоваров");
		МассивЭлементов.Добавить("НадписьОтклонениеОплатыМерныхТоваров");
		МассивЭлементов.Добавить("СуммаОтклоненияМерныхТоваров");
		МассивЭлементов.Добавить("СуммаОтклоненийПоДокументу");
		
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "Видимость", Ложь);
		
	КонецЕсли;
	
	Элементы.ОплатаВВалюте.Видимость = НЕ НеОтображатьОплатаВВалюте;
	
	Если ПраваПользователяПовтИсп.ЭтоПартнер() Тогда
		Элементы.ЗаполнитьЭтапыГрафикаОплатыФорма.Видимость = Ложь;
		Элементы.ЗаполнитьЭтапыГрафикаОплатыТаблица.Видимость = Ложь;
	КонецЕсли;
	
	Если ИспользоватьОтрицательныеСуммыПлатежа Тогда
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЭтапыГрафикаОплатыДатаСдвиг", "Заголовок",
			НСтр("ru = 'Дата платежа';
				|en = 'Payment date'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормыПриИзмененииПорядкаРасчетов()
	
	МассивЭлементов = Новый Массив;
	МассивЭлементов.Добавить("ЭтапыОплатыВариантОплаты");
	МассивЭлементов.Добавить("ГруппаКредит");
		
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(
		Элементы, МассивЭлементов, "Видимость", ЕстьПредоплата);
	
	МассивЭлементов = Новый Массив;
	МассивЭлементов.Добавить("СуммаПлатежаПредоплата");
	МассивЭлементов.Добавить("СуммаЗалогаЗаТаруПредоплата");
	МассивЭлементов.Добавить("ПроцентПлатежаПредоплата");
	МассивЭлементов.Добавить("ПроцентЗалогаЗаТаруПредоплата");
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(
		Элементы, МассивЭлементов, "ТолькоПросмотр", Не ЕстьПредоплата);
	
	МассивЭлементов = Новый Массив;
	МассивЭлементов.Добавить("СтраницаРасширеннаяНастройка");
	МассивЭлементов.Добавить("СтраницаУпрощеннаяСхема");
	МассивЭлементов.Добавить("ЗаполнитьЭтапыГрафикаОплатыФорма");
	МассивЭлементов.Добавить("ГруппаПредоплата");
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "Видимость", Истина);
	
	МассивЭлементов = Новый Массив;
	Если УпрощенныйРежим Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаУпрощеннаяСхема;
		МассивЭлементов.Добавить("СтраницаРасширеннаяНастройка");
	Иначе
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаРасширеннаяНастройка;
		МассивЭлементов.Добавить("СтраницаУпрощеннаяСхема");
		МассивЭлементов.Добавить("ЗаполнитьЭтапыГрафикаОплатыФорма");
	КонецЕсли;
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "Видимость", Ложь);
	
	Элементы.РаспределитьСуммуТаблица.Видимость = НЕ УпрощенныйРежим;
	Элементы.РаспределитьСуммуФорма.Видимость = УпрощенныйРежим И ЕстьПредоплата;
	
	ПоЗаказам = 
		НакладнаяПоЗаказам 
			И ПолучитьФункциональнуюОпцию("ИспользоватьРасширенныеВозможностиЗаказаКлиента")
			И ПорядокРасчетов <> ПредопределенноеЗначение("Перечисление.ПорядокРасчетов.ПоНакладным")
		ИЛИ НакладнаяПоЗаказам
			И НЕ ЗначениеЗаполнено(ГрафикИсполненияДоговора) 
			И (ПорядокРасчетов = ПредопределенноеЗначение("Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов")
				ИЛИ ПорядокРасчетов = ПредопределенноеЗначение("Перечисление.ПорядокРасчетов.ПоДоговорамНакладным"));
	
	Элементы.ЭтапыГрафикаОплаты.ИзменятьСоставСтрок = НЕ ПоЗаказам;
	
	Если ПоЗаказам Тогда
		
		МассивЭлементов = Новый Массив;
		МассивЭлементов.Добавить("ФормаОплаты");
		МассивЭлементов.Добавить("ОплатаВВалюте");
		МассивЭлементов.Добавить("Касса");
		МассивЭлементов.Добавить("БанковскийСчет");
		МассивЭлементов.Добавить("ЭтапыОплатыВариантОплаты");
		МассивЭлементов.Добавить("ЭтапыОплатыПроцентПлатежа");
		МассивЭлементов.Добавить("ЭтапыГрафикаОплатыСуммаПлатежа");
		МассивЭлементов.Добавить("ЭтапыОплатыПроцентЗалогаЗаТару");
		МассивЭлементов.Добавить("ЭтапыОплатыСуммаЗалогаЗаТару");
		
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "ТолькоПросмотр", Истина);
	КонецЕсли;
	
	Если Не ЕстьПредоплата И УпрощенныйРежим Тогда
		
		МассивЭлементов = Новый Массив;
		МассивЭлементов.Добавить("СуммаОплатыПоДокументу");
		МассивЭлементов.Добавить("ВалютаОплатыПоДокументу");
		Если ТребуетсяЗалогЗаТару Тогда
			МассивЭлементов.Добавить("СуммаЗалогаПоДокументу");
			МассивЭлементов.Добавить("ВалютаЗалогаПоДокументу");
		КонецЕсли;
		
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "Видимость", Истина);
		
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаПредоплата", "Заголовок",
			НСтр("ru = 'Кредит';
				|en = 'Credit'"));
		Элементы.ГруппаПредоплата.Отображение = ОтображениеОбычнойГруппы.Нет;
	Иначе 
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаПредоплата", "Заголовок",
			НСтр("ru = 'Предоплата (до отгрузки)';
				|en = 'Prepayment (before shipment)'"));
		
	КонецЕсли;
	
	Если ЭтоЗаказ И ПорядокРасчетов = ПредопределенноеЗначение("Перечисление.ПорядокРасчетов.ПоНакладным") тогда
		МассивЭлементов = Новый Массив;
		МассивЭлементов.Добавить("СтраницаРасширеннаяНастройка");
		МассивЭлементов.Добавить("СтраницаУпрощеннаяСхема");
		МассивЭлементов.Добавить("ГруппаПредоплата");
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "Видимость", Ложь);
	КонецЕсли;
	
	Если Не ЕстьПредоплата И УпрощенныйРежим Тогда
		Элементы.Переместить(Элементы.ГруппаПредоплата, Элементы.ГруппаГрафикИФормаОплаты);
	Иначе
		Элементы.Переместить(Элементы.ГруппаПредоплата, Элементы.ГруппаПредоплатаКредит, Элементы.ГруппаКредит);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьКнопокЗаполнения()
	
	ДоступностьЭлемента = НеПроверятьКорректностьЭтаповОплаты Или СуммаЗалогаПоДокументу <> 0 Или СуммаОплатыПоДокументу <> 0;
	Элементы.ЗаполнитьЭтапыГрафикаОплатыФорма.Доступность   = ДоступностьЭлемента;
	Элементы.ЗаполнитьЭтапыГрафикаОплатыТаблица.Доступность = ДоступностьЭлемента;
	Элементы.РаспределитьСуммуФорма.Доступность             = ДоступностьЭлемента;
	Элементы.РаспределитьСуммуТаблица.Доступность           = ДоступностьЭлемента;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементовПоФормеОплаты()
	
	ЛюбаяОплата      = Не ЗначениеЗаполнено(ФормаОплаты);
	ДоступностьКассы = ЛюбаяОплата Или (ФормаОплаты = Перечисления.ФормыОплаты.Наличная);
	ДоступностьСчета = ЛюбаяОплата Или (ФормаОплаты = Перечисления.ФормыОплаты.Безналичная);
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Касса", "Доступность", ДоступностьКассы);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "БанковскийСчет", "Доступность", ДоступностьСчета);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементаПорядокРасчетов()
	
	Если Параметры.Свойство("ПорядокРасчетов") Тогда
		
		ПорядокРасчетов = Параметры.ПорядокРасчетов;
		
		Если Параметры.Свойство("ДоступныеПорядкиРасчетов") Тогда
			ДоступныеПорядкиРасчетов = Параметры.ДоступныеПорядкиРасчетов;
		Иначе
			Структура = Новый Структура("Соглашение,Договор", Соглашение, Договор);
			ДоступныеПорядкиРасчетов = ВзаиморасчетыСервер.ДоступныеПорядкиРасчетовПоДокументу(
				ПорядокРасчетов,
				ВзаиморасчетыСервер.ПорядокРасчетовПоУмолчанию(Структура),
				Параметры.НакладнаяПоЗаказам,
				Параметры.ЭтоЗаказ,
				ЗаказКакСчет);
		КонецЕсли;
			
		Если ДоступныеПорядкиРасчетов.Количество() > 1 И НЕ РежимСамообслуживания Тогда
			
			Элементы.ПорядокРасчетов.СписокВыбора.ЗагрузитьЗначения(ДоступныеПорядкиРасчетов.ВыгрузитьЗначения());
			Элементы.ПорядокРасчетов.ТолькоПросмотр = Ложь;
			Элементы.ПорядокРасчетов.РежимВыбораИзСписка = Истина;
			
		КонецЕсли;
		
		УстановитьДоступностьЭлементовПоПорядкуРасчета();
		
	Иначе
		
		Элементы.ПорядокРасчетов.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементовПоПорядкуРасчета()
	
	ЗапретРедактированияГрафика = ЗначениеЗаполнено(ГрафикИсполненияДоговора) И ЭтоЗаказ
									И (ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов
										ИЛИ ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамНакладным) ;
	
	Если ЭтоЗаказ И ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоНакладным
		Или ЗапретРедактированияГрафика Тогда
		
		Элементы.ГруппаСтраницы.Доступность                   = Ложь;
		Элементы.ЗаполнитьЭтапыГрафикаОплатыФорма.Доступность = Ложь;
		Элементы.РаспределитьСуммуФорма.Доступность           = Ложь;
		Элементы.ГрафикОплаты.Доступность                     = Ложь;
		Элементы.ФормаОплаты.Доступность                      = Ложь;
		Элементы.ОплатаВВалюте.Доступность                    = Ложь;
		Элементы.Касса.Доступность                            = Ложь;
		Элементы.БанковскийСчет.Доступность                   = Ложь;
		
	Иначе
		
		Элементы.ГруппаСтраницы.Доступность = Истина;
		Элементы.ГрафикОплаты.Доступность   = Не НакладнаяПоЗаказам;
		Элементы.ФормаОплаты.Доступность    = Истина;
		Элементы.ОплатаВВалюте.Доступность  = Истина;
		
		УстановитьДоступностьКнопокЗаполнения();
		УстановитьДоступностьЭлементовПоФормеОплаты();
		
	КонецЕсли;
	
	Если ЗапретРедактированияГрафика Тогда
		Элементы.ПорядокРасчетов.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
	Иначе
		Элементы.ПорядокРасчетов.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	КонецЕсли;
	
	Элементы.ГрафикОплаты.Видимость = Не ЗапретРедактированияГрафика;
	Элементы.ГруппаПредоплата.Доступность = Не ЗапретРедактированияГрафика;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТекстКнопкиЗаполнения(Форма)
	
	ТекстКнопкиЗаполнения = НСтр("ru = 'Заполнить по умолчанию';
								|en = 'Fill in by default'");
	Если Форма.НакладнаяПоЗаказам Тогда
		ТекстКнопкиЗаполнения = НСтр("ru = 'Заполнить по заказам';
									|en = 'Fill in from orders'");
	ИначеЕсли Форма.ИспользоватьГрафикиОплаты И ЗначениеЗаполнено(Форма.ГрафикОплаты) Тогда
		ТекстКнопкиЗаполнения = НСтр("ru = 'Заполнить по графику';
									|en = 'Fill in by schedule'");
	ИначеЕсли Форма.ГрафикСоглашенияЗаполнен Тогда
		ТекстКнопкиЗаполнения = НСтр("ru = 'Заполнить по соглашению';
									|en = 'Fill in by agreement'");
	ИначеЕсли ЗначениеЗаполнено(Форма.ГрафикИсполненияДоговора) Тогда
		ТекстКнопкиЗаполнения = НСтр("ru = 'Заполнить по графику договора';
									|en = 'Fill in by contract schedule'");
	КонецЕсли;
	
	МассивЭлементов = Новый Массив;
	МассивЭлементов.Добавить("ЗаполнитьЭтапыГрафикаОплатыФорма");
	МассивЭлементов.Добавить("ЗаполнитьЭтапыГрафикаОплатыТаблица");
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(
		Форма.Элементы, МассивЭлементов, "Заголовок", ТекстКнопкиЗаполнения);
	
КонецПроцедуры

&НаСервере
Процедура ОграничитьТипЭлементовСуммыПлатежа()
	
	ОграниченныйТип = Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой));
	
	МассивЭлементов = Новый Массив;
	
	МассивЭлементов.Добавить("ЭтапыГрафикаОплатыСуммаПлатежа");
	МассивЭлементов.Добавить("СуммаПлатежаПредоплата");
	МассивЭлементов.Добавить("СуммаПлатежаКредит");
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "ОграничениеТипа", ОграниченныйТип);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСлужебныеРеквизиты();
	
	ЕстьПредоплата = ЭтоЗаказ И НЕ ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоНакладным И НЕ ЗначениеЗаполнено(ГрафикИсполненияДоговора)
					ИЛИ ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоЗаказамНакладным И НакладнаяПоЗаказам И ЗначениеЗаполнено(АдресВоВременномХранилище)
					ИЛИ ПредоплатаВНакладной;
	
	УпрощенныйРежим = НЕ ЗначениеЗаполнено(АдресВоВременномХранилище)
		ИЛИ ЭтоЗаказ И ЗначениеЗаполнено(ГрафикИсполненияДоговора) И ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамНакладным
		ИЛИ ЗначениеЗаполнено(ГрафикИсполненияДоговора) И ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов
		ИЛИ ИспользоватьУпрощеннуюСхемуОплатыВПродажах
			И НЕ (НакладнаяПоЗаказам И (ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоЗаказам
										ИЛИ ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов));
	
	Если УпрощенныйРежим Тогда
		
		Если ЭтапыГрафикаОплаты.Количество()>0 Тогда
			ПреобразоватьТаблицуЭтаповВДанныеУпрощенногоРежима(ЭтапыГрафикаОплаты);
		ИначеЕсли НЕ ЕстьПредоплата Тогда
			СуммаПлатежаПредоплата        = СуммаОплатыПоДокументу;
			СуммаЗалогаЗаТаруПредоплата   = СуммаЗалогаПоДокументу;
			СуммаОтклоненияМерныхТоваров  = СуммаОтклоненияПоДокументу;
			ПроцентЗалогаЗаТаруПредоплата = 100;
			ПроцентПлатежаПредоплата      = 100;
			
			ДатаПредоплата = ДатаПлатежа;
			
		Иначе
			ДатаПредоплата = Дата;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Функция ПоместитьВоВременноеХранилищеНаСервере()
	
	Возврат ПоместитьВоВременноеХранилище(ЭтапыГрафикаОплаты.Выгрузить(), ИдентификаторВызывающейФормы);
	
КонецФункции

&НаСервере
Функция ПреобразоватьПоместитьВоВременноеХранилище()
	
	ЭтапыГрафикаОплаты.Загрузить(ПреобразоватьДанныеУпрощенногоРежимаВТаблицуЭтапов());
	Возврат ПоместитьВоВременноеХранилищеНаСервере();
	
КонецФункции

&НаСервере
Функция ПреобразоватьДанныеУпрощенногоРежимаВТаблицуЭтапов()
	
	ТаблицаРезультат = ЭтапыГрафикаОплаты.Выгрузить();
	ТаблицаРезультат.Очистить();
	
	СуммаОплатыВсего = СуммаПлатежаПредоплата + СуммаПлатежаКредит;
	СуммаОтклоненияКРаспределению = СуммаОтклоненияМерныхТоваров;
	Если ЕстьПредоплата Тогда
		Если ЭтапСодержитДанные("Предоплата") И СуммаПлатежаПредоплата + СуммаЗалогаЗаТаруПредоплата > 0 Тогда
			
			СтрокаПредоплаты = ТаблицаРезультат.Добавить();
			СтрокаПредоплаты.ВариантОплаты = Перечисления.ВариантыОплатыКлиентом.ПредоплатаДоОтгрузки;
			
			СтрокаПредоплаты.ДатаПлатежа             = ДатаПредоплата;
			СтрокаПредоплаты.СуммаПлатежа            = СуммаПлатежаПредоплата;
			СтрокаПредоплаты.ПроцентПлатежа          = ПроцентПлатежаПредоплата;
			Если ТребуетсяЗалогЗаТару Тогда
				СтрокаПредоплаты.СуммаЗалогаЗаТару   = СуммаЗалогаЗаТаруПредоплата;
				СтрокаПредоплаты.ПроцентЗалогаЗаТару = ПроцентЗалогаЗаТаруПредоплата;
			КонецЕсли;
			
			Если СуммаОплатыВсего > 0 Тогда
				КоэффициентОтОбщего = (СуммаПлатежаПредоплата + СуммаПлатежаКредит)/СуммаОплатыВсего;
			Иначе
				КоэффициентОтОбщего = 0;
			КонецЕсли;
			
			Если НакладнаяПоЗаказам Тогда
				СтрокаПредоплаты.Заказ = Заказ;
			КонецЕсли;
			
			СтрокаПредоплаты.СуммаОтклоненияМерныхТоваров = Окр(СуммаОтклоненияКРаспределению * КоэффициентОтОбщего, 2, РежимОкругления.Окр15как20);
			СуммаОтклоненияКРаспределению = СуммаОтклоненияКРаспределению - СтрокаПредоплаты.СуммаОтклоненияМерныхТоваров;
			
		КонецЕсли;
		
		Если ЭтапСодержитДанные("Кредит") И СуммаПлатежаКредит + СуммаЗалогаЗаТаруКредит > 0 Тогда
			
			СтрокаКредита = ТаблицаРезультат.Добавить();
			СтрокаКредита.ВариантОплаты = Перечисления.ВариантыОплатыКлиентом.КредитПослеОтгрузки;
			
			СтрокаКредита.ДатаПлатежа             = ДатаКредит;
			СтрокаКредита.СуммаПлатежа            = СуммаПлатежаКредит;
			СтрокаКредита.ПроцентПлатежа          = ПроцентПлатежаКредит;
			Если ТребуетсяЗалогЗаТару Тогда
				СтрокаКредита.СуммаЗалогаЗаТару   = СуммаЗалогаЗаТаруКредит;
				СтрокаКредита.ПроцентЗалогаЗаТару = ПроцентЗалогаЗаТаруКредит;
			КонецЕсли;
			
			Если НакладнаяПоЗаказам Тогда
				СтрокаКредита.Заказ = Заказ;
			КонецЕсли;
			
			СтрокаКредита.СуммаОтклоненияМерныхТоваров = СуммаОтклоненияКРаспределению;
			
		КонецЕсли;
	Иначе
		СтрокаКредита = ТаблицаРезультат.Добавить();
		СтрокаКредита.ВариантОплаты = Перечисления.ВариантыОплатыКлиентом.КредитПослеОтгрузки;
		
		СтрокаКредита.ДатаПлатежа             = ДатаПредоплата;
		СтрокаКредита.СуммаПлатежа            = СуммаПлатежаПредоплата;
		СтрокаКредита.ПроцентПлатежа          = ПроцентПлатежаПредоплата;
		Если ТребуетсяЗалогЗаТару Тогда
			СтрокаКредита.СуммаЗалогаЗаТару   = СуммаЗалогаЗаТаруПредоплата;
			СтрокаКредита.ПроцентЗалогаЗаТару = ПроцентЗалогаЗаТаруПредоплата;
		КонецЕсли;
		
		Если НакладнаяПоЗаказам Тогда
			СтрокаКредита.Заказ = Заказ;
		КонецЕсли;
		
		СтрокаКредита.СуммаОтклоненияМерныхТоваров = СуммаОтклоненияКРаспределению;
	КонецЕсли;
	
	Возврат ТаблицаРезультат;
	
КонецФункции

&НаСервере
Функция ЭтапСодержитДанные(ВариантОплаты)
	
	Если ВариантОплаты = "Кредит" Тогда
		Возврат ЗначениеЗаполнено(СуммаПлатежаКредит)
			ИЛИ ?(ТребуетсяЗалогЗаТару, ЗначениеЗаполнено(СуммаЗалогаЗаТаруКредит), ЛОЖЬ);
	Иначе
		Возврат ЗначениеЗаполнено(СуммаПлатежаПредоплата)
			ИЛИ ?(ТребуетсяЗалогЗаТару, ЗначениеЗаполнено(СуммаЗалогаЗаТаруПредоплата), ЛОЖЬ);
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ПреобразоватьТаблицуЭтаповВДанныеУпрощенногоРежима(ЭтапыОплаты)
	
	ОчиститьДанныеУпрощенногоРежима(ЭтаФорма);
	
	Если ЭтапыОплаты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекСтрока Из ЭтапыОплаты Цикл
		
		Если ТекСтрока.ВариантОплаты = Перечисления.ВариантыОплатыКлиентом.КредитПослеОтгрузки
			ИЛИ ТекСтрока.ВариантОплаты = Перечисления.ВариантыОплатыКлиентом.КредитСдвиг
			ИЛИ НЕ ЕстьПредоплата Тогда
			СуммаПлатежаКредит   = СуммаПлатежаКредит + ТекСтрока.СуммаПлатежа;
			ПроцентПлатежаКредит = ПроцентПлатежаКредит + ТекСтрока.ПроцентПлатежа;
			Если ТребуетсяЗалогЗаТару Тогда
				СуммаЗалогаЗаТаруКредит   = СуммаЗалогаЗаТаруКредит + ТекСтрока.СуммаЗалогаЗаТару;
				ПроцентЗалогаЗаТаруКредит = ПроцентЗалогаЗаТаруКредит + ТекСтрока.ПроцентЗалогаЗаТару;
			КонецЕсли;
			ДатаКредит = Макс(ДатаКредит, ТекСтрока.ДатаПлатежа);
		Иначе //аванс и предоплата суммируются в общую сумму
			СуммаПлатежаПредоплата   = СуммаПлатежаПредоплата + ТекСтрока.СуммаПлатежа;
			ПроцентПлатежаПредоплата = ПроцентПлатежаПредоплата + ТекСтрока.ПроцентПлатежа;
			Если ТребуетсяЗалогЗаТару Тогда
				СуммаЗалогаЗаТаруПредоплата   = СуммаЗалогаЗаТаруПредоплата + ТекСтрока.СуммаЗалогаЗаТару;
				ПроцентЗалогаЗаТаруПредоплата = ПроцентЗалогаЗаТаруПредоплата + ТекСтрока.ПроцентЗалогаЗаТару;
			КонецЕсли;
			ДатаПредоплата = Макс(ДатаПредоплата, ТекСтрока.ДатаПлатежа);
		КонецЕсли;
		
		СуммаОтклоненияМерныхТоваров = СуммаОтклоненияМерныхТоваров + ТекСтрока.СуммаОтклоненияМерныхТоваров;
		
	КонецЦикла;
	
	Если НакладнаяПоЗаказам Тогда
		Заказ = ЭтапыОплаты[0].Заказ;
	КонецЕсли; 
	
	Если ЕстьПредоплата И НЕ ЭтоЗаказ Тогда
		Если ЗначениеЗаполнено(ДатаКредит) И ЗначениеЗаполнено(Дата) И ЗначениеЗаполнено(ДатаПредоплата)Тогда
			ДатаКредит = Дата + (ДатаКредит - ДатаПредоплата);
		КонецЕсли;
		ДатаПредоплата = Дата;
	ИначеЕсли НЕ ЕстьПредоплата Тогда
		ДатаПредоплата                = ДатаКредит;
		СуммаПлатежаПредоплата        = СуммаПлатежаКредит;
		ПроцентПлатежаПредоплата      = ПроцентПлатежаКредит;
		СуммаЗалогаЗаТаруПредоплата   = СуммаЗалогаЗаТаруКредит;
		ПроцентЗалогаЗаТаруПредоплата = ПроцентЗалогаЗаТаруКредит;
		СуммаПлатежаКредит            = 0;
		ПроцентПлатежаКредит          = 0;
		СуммаЗалогаЗаТаруКредит       = 0;
		ПроцентЗалогаЗаТаруКредит     = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОчиститьДанныеУпрощенногоРежима(Форма)
	
	Форма.ДатаПредоплата                = Неопределено;
	Форма.ДатаКредит                    = Неопределено;
	Форма.СуммаПлатежаПредоплата        = 0;
	Форма.СуммаПлатежаКредит            = 0;
	Форма.ПроцентПлатежаПредоплата      = 0;
	Форма.ПроцентПлатежаКредит          = 0;
	Форма.СуммаЗалогаЗаТаруПредоплата   = 0;
	Форма.СуммаЗалогаЗаТаруКредит       = 0;
	Форма.ПроцентЗалогаЗаТаруПредоплата = 0;
	Форма.ПроцентЗалогаЗаТаруКредит     = 0;
	Форма.СуммаОтклоненияМерныхТоваров  = 0;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьИтоговыеПоказатели(Форма)
	
	ПредыдущееЗначениеДаты = Дата(1,1,1);
	Форма.НомерСтрокиПолнойОплаты = 0;
	Форма.НомерСтрокиПолнойОплатыЗалога = 0;
	Форма.ПроцентПлатежейОбщий = 0;
	Форма.ПроцентЗалогаОбщий   = 0;
	
	Если Форма.УпрощенныйРежим Тогда
		
		Форма.ПроцентПлатежейОбщий = Форма.ПроцентПлатежаПредоплата + Форма.ПроцентПлатежаКредит;
		Форма.ПроцентЗалогаОбщий   = Форма.ПроцентЗалогаЗаТаруПредоплата + Форма.ПроцентЗалогаЗаТаруКредит;
		
	Иначе
		
		Форма.СуммаОтклоненияМерныхТоваров = 0;
		
		Для Каждого ТекСтрока Из Форма.ЭтапыГрафикаОплаты Цикл
			
			Форма.ПроцентПлатежейОбщий = Форма.ПроцентПлатежейОбщий + ТекСтрока.ПроцентПлатежа;
			ТекСтрока.ПроцентЗаполненНеВерно = (Форма.ПроцентПлатежейОбщий > 100);
			Если Форма.ПроцентПлатежейОбщий = 100 Тогда
				Форма.НомерСтрокиПолнойОплаты = ТекСтрока.НомерСтроки;
			КонецЕсли;
			
			Форма.ПроцентЗалогаОбщий = Форма.ПроцентЗалогаОбщий + ТекСтрока.ПроцентЗалогаЗаТару;
			ТекСтрока.ПроцентЗалогаЗаполненНеВерно = (Форма.ПроцентЗалогаОбщий > 100);
			Если Форма.ПроцентЗалогаОбщий = 100 Тогда
				Форма.НомерСтрокиПолнойОплатыЗалога = ТекСтрока.НомерСтроки;
			КонецЕсли;
			
			ТекСтрока.ДатаЗаполненаНеВерно = (ПредыдущееЗначениеДаты > ТекСтрока.ДатаПлатежа);
			ПредыдущееЗначениеДаты = ТекСтрока.ДатаПлатежа;
			
			Форма.СуммаОтклоненияМерныхТоваров = Форма.СуммаОтклоненияМерныхТоваров + ТекСтрока.СуммаОтклоненияМерныхТоваров;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СортироватьЭтапыОплаты()
	
	Для Каждого ЭтапОплаты Из ЭтапыГрафикаОплаты Цикл
		Если ЭтапОплаты.ВариантОплаты = ПредопределенноеЗначение("Перечисление.ВариантыОплатыКлиентом.АвансДоОбеспечения") Тогда
			ЭтапОплаты.НомерВариантаОплаты = 1;
		ИначеЕсли ЭтапОплаты.ВариантОплаты = ПредопределенноеЗначение("Перечисление.ВариантыОплатыКлиентом.ПредоплатаДоОтгрузки") Тогда
			ЭтапОплаты.НомерВариантаОплаты = 2;
		Иначе
			ЭтапОплаты.НомерВариантаОплаты = 3;
		КонецЕсли;
	КонецЦикла;
	
	ЭтапыГрафикаОплаты.Сортировать("НомерВариантаОплаты, ДатаПлатежа");
	
КонецПроцедуры

&НаКлиенте
Процедура ПронумероватьТаблицуЭтапов()
	НомерСтроки = 1;
	Для каждого СтрокаТаблицы Из ЭтапыГрафикаОплаты Цикл
		СтрокаТаблицы.НомерСтроки = НомерСтроки;
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораФормыОплаты()
	
	Перем ОтборПоСсылке;
	
	Для Каждого ТекПараметр Из Элементы.ФормаОплаты.ПараметрыВыбора Цикл
		Если ТекПараметр.Имя = "Отбор.Ссылка" Тогда
			ОтборПоСсылке = ТекПараметр.Значение;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ФормыОплаты.Ссылка
	|ИЗ
	|	Перечисление.ФормыОплаты КАК ФормыОплаты
	|ГДЕ
	|	&Условие";
	
	Если ОтборПоСсылке <> Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Условие", "Ссылка В(&ОтборПоСсылке)");
		Запрос.УстановитьПараметр("ОтборПоСсылке", ОтборПоСсылке);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Условие", "ИСТИНА");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Выборка      = Запрос.Выполнить().Выбрать();
	СписокВыбора = Элементы.ФормаОплаты.СписокВыбора;
	Пока Выборка.Следующий() Цикл
		СписокВыбора.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Если Не РежимСамообслуживания Тогда
		СписокВыбора.Добавить(Перечисления.ФормыОплаты.ПустаяСсылка(), НСтр("ru = 'Любая';
																			|en = 'Any'"));
	КонецЕсли;
	
	ДенежныеСредстваСервер.УстановитьВидимостьОплатыПлатежнойКартой(Элементы.ФормаОплаты);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбораКассыСчета()
	
	Если НЕ ОплатаВВалюте Тогда
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.ВалютаДенежныхСредств", ВалютаРеглУчета));
		НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.Закрыт", Ложь));
		НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
	Иначе
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(Новый ПараметрВыбора("Отбор.Закрыт", Ложь));
		НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив)
	КонецЕсли;
	
	Элементы.БанковскийСчет.ПараметрыВыбора = НовыеПараметры; 
	Элементы.Касса.ПараметрыВыбора          = НовыеПараметры;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьСуммыОтклоненийМерныхТоваров()
	
	Если СуммаОтклоненияМерныхТоваров > 0 Тогда
		
		ОбщегоНазначенияУТКлиентСервер.ЗаполнитьЗначенияСвойствКоллекции(ЭтапыГрафикаОплаты, 0, "СуммаОтклоненияМерныхТоваров");
		СуммаОтклоненияМерныхТоваров = 0;
		
		Если УпрощенныйРежим Тогда
			ПроцентПлатежаПриИзменении(СуммаПлатежаПредоплата, ПроцентПлатежаПредоплата,
									СуммаПлатежаКредит, ПроцентПлатежаКредит, СуммаОплатыПоДокументу);
			ПроцентПлатежаПриИзменении(СуммаЗалогаЗаТаруПредоплата, ПроцентЗалогаЗаТаруПредоплата, 
								СуммаЗалогаЗаТаруКредит, ПроцентЗалогаЗаТаруКредит, СуммаЗалогаПоДокументу);
			ПроцентПлатежаПриИзменении(СуммаПлатежаКредит, ПроцентПлатежаКредит, 
								СуммаПлатежаПредоплата, ПроцентПлатежаПредоплата, СуммаОплатыПоДокументу);
			ПроцентПлатежаПриИзменении(СуммаЗалогаЗаТаруКредит, ПроцентЗалогаЗаТаруКредит,
								СуммаЗалогаЗаТаруПредоплата, ПроцентЗалогаЗаТаруПредоплата, СуммаЗалогаПоДокументу);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЭтапыПоЗаказамСервер()
	
	ПараметрыЗаполнения = ЭтапыОплатыСервер.ПараметрыЗаполненияЭтаповОплатыПоЗаказам();
	ЗаполнитьЗначенияСвойств(ПараметрыЗаполнения, ЭтаФорма);
	ТабличнаяЧастьФормы = ТабличнаяЧасть.Выгрузить();
	ПараметрыЗаполнения.ТабличнаяЧасть  = ТабличнаяЧастьФормы;
	ПараметрыЗаполнения.ТипРасчетов     = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом; 
	ПараметрыЗаполнения.УпрощеннаяСхема = ИспользоватьУпрощеннуюСхемуОплатыВПродажах;
	ПараметрыЗаполнения.ЗаказКакСчет    = ЗаказКакСчет;
	
	ЭтапыОплатыСервер.ЗаполнитьЭтапыОплатыДокументаПоЗаказам(ПараметрыЗаполнения);
	
	Если УпрощенныйРежим Тогда
		ПреобразоватьТаблицуЭтаповВДанныеУпрощенногоРежима(ЭтапыГрафикаОплаты.Выгрузить());
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РаспределитьСуммуПоЭтапамИЗаказамГрафикаОплаты()
	
	ПараметрыЗаполнения = ЭтапыОплатыСервер.ПараметрыЗаполненияЭтаповОплатыПоЗаказам();
	ЗаполнитьЗначенияСвойств(ПараметрыЗаполнения, ЭтаФорма);
	ТабличнаяЧастьФормы = ТабличнаяЧасть.Выгрузить();
	ТабличнаяЧастьФормы.Колонки.Заказ.Имя  = ИмяПоляЗаказ;
	ПараметрыЗаполнения.ТабличнаяЧасть     = ТабличнаяЧастьФормы;
	ПараметрыЗаполнения.ПоЗаказам          = НакладнаяПоЗаказам;
	ПараметрыЗаполнения.УпрощеннаяСхема    = ИспользоватьУпрощеннуюСхемуОплатыВПродажах;
	ПараметрыЗаполнения.ЗаказКакСчет       = ЗаказКакСчет;
	
	ЭтапыОплатыСервер.РаспределитьСуммыЭтаповОплатыДокументаПоЗаказам(ПараметрыЗаполнения);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область УХ_Встраивание

&НаКлиенте
Процедура Подключаемый_ЭтапыГрафикаОплатыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И Копирование Тогда
		Элемент.ТекущиеДанные.ИдентификаторПозиции = неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 