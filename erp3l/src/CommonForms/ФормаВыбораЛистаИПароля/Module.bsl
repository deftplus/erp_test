&НаКлиенте
Процедура ПодтверждениеБезПароля_Завершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		ИмяЛистаРабочий		 = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "ИмяЛиста", "");
		ПарольРабочий		 = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "Пароль", "");
		НуженПарольРабочий	 = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "НуженПароль", Ложь);
		СтруктураЗакрытия = Новый Структура;
		СтруктураЗакрытия.Вставить("ИмяЛиста", ИмяЛистаРабочий);
		СтруктураЗакрытия.Вставить("Пароль", ПарольРабочий);
		СтруктураЗакрытия.Вставить("НуженПароль", НуженПарольРабочий);
		Закрыть(СтруктураЗакрытия);
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда	
		Возврат;			// Пользователь отказался продолжать закрытие.
	Иначе
		ТекстСообщения = НСтр("ru = 'Неизвестный вариант ответа: %ВыбранноеЗначение%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ВыбранноеЗначение%", Строка(Результат));
		ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВернутьСтруктуруОтвета()
	
	Если Элементы.СписокЛистов.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = СписокЛистов.НайтиПоИдентификатору(Элементы.СписокЛистов.ТекущаяСтрока);
	Если ТекущаяСтрока.Пометка И ПустаяСтрока(Пароль) Тогда
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("ИмяЛиста", ТекущаяСтрока.Значение);
		СтруктураПараметров.Вставить("Пароль", Пароль);
		СтруктураПараметров.Вставить("НуженПароль", ТекущаяСтрока.Пометка);
		ТекстВопроса = НСтр("ru = 'Не указан пароль для защищенного листа. Продолжить?'");
		Режим = РежимДиалогаВопрос.ДаНет;
		Оповещение = Новый ОписаниеОповещения("ПодтверждениеБезПароля_Завершение", ЭтаФорма, СтруктураПараметров);
		ПоказатьВопрос(Оповещение, ТекстВопроса, Режим, 0);
		Возврат;
		
	КонецЕсли;
	
	Закрыть(Новый Структура("ИмяЛиста, Пароль, НуженПароль", ТекущаяСтрока.Значение, Пароль, ТекущаяСтрока.Пометка));
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ВернутьСтруктуруОтвета();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЛистовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВернутьСтруктуруОтвета();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокЛистов = Параметры.СписокЛистов;
	
КонецПроцедуры
