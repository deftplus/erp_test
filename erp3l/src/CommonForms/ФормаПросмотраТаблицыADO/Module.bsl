
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипБД=Параметры.ТипБД;
	ВнешняяИнформационнаяБаза=ТипБД.ВИБПоУмолчанию;
	МножественныйВыбор=Параметры.МножественныйВыбор;
	
	Если НЕ ЗначениеЗаполнено(Параметры.ТаблицаADO) Тогда
		
		ТаблицаADO=Справочники.ТаблицыADO.НайтиПоНаименованию(Параметры.СвязаннаяТаблица,Истина,,ТипБД);
		
		Если Не ЗначениеЗаполнено(ТаблицаADO) Тогда
			
			Справочники.ТаблицыADO.ОбновитьТаблицыADO(ТипБД);
			ТаблицаADO=Справочники.ТаблицыADO.НайтиПоНаименованию(Параметры.СвязаннаяТаблица,Истина,,ТипБД);
			
			Если Не ЗначениеЗаполнено(ТаблицаADO) Тогда
				
				ОбщегоНазначенияУХ.СообщитьОбОшибке("Не найдена таблица "+Параметры.СвязаннаяТаблица+" в подключении "+ТипБД,,,СтатусСообщения.Внимание);
				
				Возврат;
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		ТаблицаADO=Параметры.ТаблицаADO;	
		
	КонецЕсли;
	
	Элементы.ВнешняяИнформационнаяБаза.Доступность=НЕ Параметры.РежимВыбора;
	Элементы.ТаблицаADO.Доступность=НЕ Параметры.РежимВыбора;
	Элементы.ДанныеТаблицыВыбратьСтроки.Видимость=МножественныйВыбор;
	Элементы.ДанныеТаблицы.МножественныйВыбор=МножественныйВыбор;
	
	ПодготовитьТаблицуДляЗаполнения();
	ОбновитьДанныеТаблицы();
	
	Если ТипЗнч(Параметры.СписокСтрок)=Тип("СписокЗначений") И Параметры.СписокСтрок.Количество()=1 Тогда
		
		СтруктураПоиска=Новый Структура;
		ДанныеСтроки=Параметры.СписокСтрок[0].Значение;
		
		Для Каждого Колонка ИЗ СписокКолонок Цикл
			
			СтруктураПоиска.Вставить(Колонка.Значение,ДанныеСтроки["["+Колонка.Представление+"]"]);
			
		КонецЦикла;
		
		МассивСтрок=ДанныеТаблицы.НайтиСтроки(СтруктураПоиска);
		
		Если МассивСтрок.Количество()>0 Тогда
			
			Элементы.ДанныеТаблицы.ТекущаяСтрока=ДанныеТаблицы.Индекс(МассивСтрок[0]);
			
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры


&НаСервере
Процедура ПодготовитьТаблицуДляЗаполнения()
	
	РеквизитыКДобавлению=Новый Массив;
	РеквизитыКУдалению=Новый Массив;
	
	Если СписокКолонок.Количество()>0 Тогда
		
		Для Каждого Колонка ИЗ СписокКолонок Цикл	
			
			РеквизитыКУдалению.Добавить("ДанныеТаблицы."+Колонка.Значение);	
			ЭтаФорма.Элементы.Удалить(ЭтаФорма.Элементы[Колонка.Значение]);
			
		КонецЦикла;
		
		СписокКолонок.Очистить();
				
	КонецЕсли;
	
	Для Каждого Колонка ИЗ ТаблицаADO.Реквизиты Цикл
		
		ТекущийТип=ОбщегоНазначенияУХ.ПреобразоватьТипИзСтроки(Колонка.ТипЗначения);
		
		Если ТекущийТип=Неопределено Тогда
			ТекущийТип=Тип("Строка");
		КонецЕсли;
		
		МассивТипов=Новый Массив;
		МассивТипов.Добавить(ТекущийТип);
		
		Попытка
			
			Если СписокКолонок.НайтиПоЗначению(Колонка.ВнутреннееПредставление)=Неопределено Тогда
				
				СписокКолонок.Добавить(Колонка.ВнутреннееПредставление,Колонка.Имя);
				
				РеквизитыКДобавлению.Добавить(Новый РеквизитФормы(Колонка.ВнутреннееПредставление,
				Новый ОписаниеТипов(МассивТипов),
				"ДанныеТаблицы",
				Колонка.Имя,
				Ложь));
				
			КонецЕсли;
			
		Исключение
			
		КонецПопытки;
		
	КонецЦикла;
	
	ИзменитьРеквизиты(РеквизитыКДобавлению,РеквизитыКУдалению);
	
	Для Каждого Колонка ИЗ ТаблицаADO.Реквизиты Цикл
		
		ТекущийТип=ОбщегоНазначенияУХ.ПреобразоватьТипИзСтроки(Колонка.ТипЗначения);
		
		Если ТекущийТип=Неопределено Тогда
			ТекущийТип=Тип("Строка");
		КонецЕсли;
		
		МассивТипов=Новый Массив;
		МассивТипов.Добавить(ТекущийТип);
		
		Если ЭтаФорма.Элементы.Найти(Колонка.ВнутреннееПредставление)=Неопределено Тогда
			
			Попытка
				
				ЭтаФорма.Элементы.Добавить(Колонка.ВнутреннееПредставление,Тип("ПолеФормы"),ЭтаФорма.Элементы.ДанныеТаблицы);	
				ЭтаФорма.Элементы[Колонка.ВнутреннееПредставление].ПутьКДанным="ДанныеТаблицы."+Колонка.ВнутреннееПредставление;
				ЭтаФорма.Элементы[Колонка.ВнутреннееПредставление].Вид=ВидПоляФормы.ПолеВвода;
				ЭтаФорма.Элементы[Колонка.ВнутреннееПредставление].Заголовок=Колонка.Имя;
				ЭтаФорма.Элементы[Колонка.ВнутреннееПредставление].ДоступныеТипы=Новый ОписаниеТипов(МассивТипов);
				
			Исключение
				
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;
			
КонецПроцедуры // ПодготовитьТаблицуДляЗаполнения()

&НаСервере
Процедура ОбновитьДанныеТаблицы()
	
	ТекТаблицаВИБ=УправлениеСоединениямиВИБУХ.ПолучитьДанныеИзТаблицы(ВнешняяИнформационнаяБаза,ТаблицаADO);
	ДанныеТаблицы.Очистить();
	
	Для Каждого СтрТаблица ИЗ ТекТаблицаВИБ Цикл
		
		НоваяСтрока=ДанныеТаблицы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрТаблица);
		
	КонецЦикла;
		
КонецПроцедуры // ОбновитьДанныеТаблицы() 

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьДанныеТаблицы();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаADOПриИзменении(Элемент)
	
	ДанныеТаблицы.Очистить();
	ПодготовитьТаблицуДляЗаполнения();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьВыборСтрок()
	
	СписокСтрок=Новый СписокЗначений;
	
	Для Каждого Строка ИЗ Элементы.ДанныеТаблицы.ВыделенныеСтроки Цикл
		
		ДанныеСтроки=Новый Соответствие;
		
		Для Каждого Колонка ИЗ СписокКолонок Цикл
			
			ДанныеСтроки.Вставить("["+Колонка.Представление+"]",ДанныеТаблицы.НайтиПоИдентификатору(Строка)[Колонка.Значение]);
			
		КонецЦикла;
		
		СписокСтрок.Добавить(ДанныеСтроки);
		
	КонецЦикла;
	
	Если НЕ МножественныйВыбор Тогда
		Оповестить("ИзмененОтборЗаписейADO",СписокСтрок);
		Закрыть();
	Иначе	
		Закрыть(СписокСтрок);
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьВыборСтрок() 

&НаКлиенте
Процедура ВыбратьСтроки(Команда)
	
	ВыполнитьВыборСтрок();
			
КонецПроцедуры

&НаКлиенте
Процедура ДанныеТаблицыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыполнитьВыборСтрок();	
	
КонецПроцедуры
