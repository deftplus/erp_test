
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПрикладныеПодсистемыУХ = РасширениеБизнесЛогикиУХ.ПрикладныеПодсистемыУХМСФО();
		
	МетаданныеУХ = Новый ТаблицаЗначений;
	МетаданныеУХ.Колонки.Добавить("ОбъектМД");
	
	Для каждого ПодсистемаУХ Из ПрикладныеПодсистемыУХ Цикл
	    Для каждого ЭлементСостава Из ПодсистемаУХ.Состав Цикл
			МетаданныеУХ.Добавить().ОбъектМД = ЭлементСостава;
		КонецЦикла;
	КонецЦикла;
	
	МетаданныеУХ.Свернуть("ОбъектМД");
	МассивОбъектовМД = МетаданныеУХ.ВыгрузитьКолонку("ОбъектМД");

	СписокПеречисленийУХ = ПолучитьСписокИменПеречислений(МассивОбъектовМД);	
	СписокСправочниковУХ = ПолучитьСписокИменСправочников(МассивОбъектовМД);
		
	Если (СписокПеречисленийУХ.Количество() = 0) И (СписокСправочниковУХ.Количество() = 0) Тогда		
		Возврат;
	КонецЕсли;
	
	Реквизиты = Новый Массив;	
	Для Каждого Элемент Из СписокПеречисленийУХ Цикл
		Реквизиты.Добавить(Новый РеквизитФормы(Элемент.Значение, Новый ОписаниеТипов("ДинамическийСписок")));
	КонецЦикла; 
	
	ИзменитьРеквизиты(Реквизиты);
	
	ТипКнопкаФормы = Тип("КнопкаФормы");
	
	Для Каждого Реквизит Из Реквизиты Цикл
		
		ЭтаФорма[Реквизит.Имя].ОсновнаяТаблица = "Перечисление." + Реквизит.Имя;
		
		Страница = Элементы.Добавить("Страница" + Реквизит.Имя, Тип("ГруппаФормы"), Элементы.СтраницыПеречисления);
		Страница.Заголовок = Метаданные.Перечисления[Реквизит.Имя].Представление();
		
		Таблица = Элементы.Добавить(Реквизит.Имя, Тип("ТаблицаФормы"), Страница);
		Таблица.ПутьКДанным = Реквизит.Имя;
		Таблица.Шапка = Ложь;
		
		Поле = Элементы.Добавить(Реквизит.Имя + "Ссылка", Тип("ПолеФормы"), Таблица);
		Поле.ПутьКДанным = Реквизит.Имя + ".Ссылка";
		
	КонецЦикла; 
	
	Для каждого СправочникКнопка Из СписокСправочниковУХ Цикл
		
		КомандаСправочника = ЭтаФорма.Команды.Добавить("ОткрытьСписок_"+СправочникКнопка.Значение);
		КомандаСправочника.Действие = "Подключаемый_ОткрытьФормуСпискаСправочника";
						
		КнопкаСписка = Элементы.Добавить("Справочник_" + СправочникКнопка.Значение, Тип("КнопкаФормы"), Элементы.СтраницаСправочники);		
		КнопкаСписка.Заголовок = СправочникКнопка.Представление;
		КнопкаСписка.Вид = ВидКнопкиФормы.Гиперссылка;
		КнопкаСписка.ИмяКоманды = КомандаСправочника.Имя;
					
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокИменПеречислений(МетаданныеИсточник)

	МетаданныеПеречисления = Метаданные.Перечисления;
	
	Список = Новый СписокЗначений;
	
	Для Каждого ОбъектМД Из МетаданныеИсточник Цикл
		
		Если НЕ МетаданныеПеречисления.Содержит(ОбъектМД) Тогда
			Продолжить;
		КонецЕсли;
		
		Список.Добавить(ОбъектМД.Имя, ОбъектМД.Представление());
		
	КонецЦикла; 
	
	Список.СортироватьПоПредставлению();	
	Возврат Список;

КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСписокИменСправочников(МетаданныеИсточник)

	МетаданныеСправочники = Метаданные.Справочники;
	
	Список = Новый СписокЗначений;
	
	Для Каждого ОбъектМД Из МетаданныеИсточник Цикл
		
		Если НЕ МетаданныеСправочники.Содержит(ОбъектМД) Тогда
			Продолжить;
		КонецЕсли;
		
		Список.Добавить(ОбъектМД.Имя, ОбъектМД.Представление());
		
	КонецЦикла; 
	
	Список.СортироватьПоПредставлению();	
	Возврат Список;

КонецФункции

&НаКлиенте
Процедура Подключаемый_ОткрытьФормуСпискаСправочника(Команда)
	ОткрытьФорму("Справочник." + СтрЗаменить(Команда.Имя, "ОткрытьСписок_", "") + ".ФормаСписка");	
КонецПроцедуры
