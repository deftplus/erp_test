
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("Подразделение", ПодразделениеИсходное);
	Параметры.Свойство("Организация",   Организация);
	Параметры.Свойство("Исполнитель",   ИсполнительИсходный);
	
	РежимВыбораПоДокументу = Неопределено;
	Если ЗначениеЗаполнено(ИсполнительИсходный) Тогда
		Если ТипЗнч(ИсполнительИсходный) = Тип("СправочникСсылка.Бригады") Тогда
			Элементы.Бригады.ТекущаяСтрока = ИсполнительИсходный;
			РежимВыбораПоДокументу = "Бригады";
			ИспользоватьБригадныеНаряды = Истина;
		Иначе
			Элементы.Работники.ТекущаяСтрока = ИсполнительИсходный;
			РежимВыбораПоДокументу = "Работники";
			ИспользоватьПерсональныеНаряды = Истина;
		КонецЕсли;
	КонецЕсли;
	
	РежимВыбораПоПодразделению = Неопределено;
	Если ЗначениеЗаполнено(ПодразделениеИсходное) Тогда
		
		ПараметрыПодразделения = ПроизводствоСервер.ПараметрыПроизводственногоПодразделения(ПодразделениеИсходное);
		
		Если ПараметрыПодразделения.ИспользоватьБригадныеНаряды Тогда
			РежимВыбораПоПодразделению = "Бригады";
			ИспользоватьБригадныеНаряды = Истина;
		КонецЕсли;
		
		Если ПараметрыПодразделения.ИспользоватьПерсональныеНаряды Тогда
			РежимВыбораПоПодразделению = "Работники";
			ИспользоватьПерсональныеНаряды = Истина;
		КонецЕсли;
		
	Иначе
		ИспользоватьБригадныеНаряды = Истина;
		ИспользоватьПерсональныеНаряды = Истина;
		РежимВыбораПоПодразделению = "Работники";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РежимВыбораПоДокументу) Тогда
		РежимВыбора = РежимВыбораПоДокументу;
	Иначе
		РежимВыбора = РежимВыбораПоПодразделению;
	КонецЕсли;
	
	Подразделение = ПодразделениеИсходное;
	
	ИспользоватьНачислениеЗарплаты = ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплаты");
	
	УстановитьОтборПоПодразделению();
	УстановитьОтборПоОрганизации();
	
	УстановитьВидимость(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	
	УстановитьОтборПоПодразделению();
	
КонецПроцедуры

&НаКлиенте
Процедура РежимВыбораПриИзменении(Элемент)
	УстановитьВидимость(ЭтаФорма);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыБригады

&НаКлиенте
Процедура БригадыВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	ОповеститьОВыборе(Элементы.Бригады.ТекущаяСтрока);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРаботники

&НаКлиенте
Процедура РаботникиВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	ОповеститьОВыборе(Элементы.Работники.ТекущаяСтрока);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимость(Форма)
	
	Форма.Элементы.РежимВыбора.Видимость = Форма.ИспользоватьБригадныеНаряды И Форма.ИспользоватьПерсональныеНаряды;
	
	Если Форма.РежимВыбора = "Бригады" Тогда
		Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница = Форма.Элементы.ГруппаБригады;
		Форма.Элементы.БригадыВыбрать.КнопкаПоУмолчанию = Истина;
	Иначе
		Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница = Форма.Элементы.ГруппаРаботники;
		Форма.Элементы.РаботникиВыбрать.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоПодразделению()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Бригады, 
		"Подразделение", 
		Подразделение, 
		ВидСравненияКомпоновкиДанных.Равно,
		, // Представление - автоматически
		ЗначениеЗаполнено(Подразделение));
	
	Если ИспользоватьНачислениеЗарплаты И ИспользоватьПерсональныеНаряды Тогда
		МассивФизЛиц = Новый Массив;
		ИнтеграцияБЗК.ЗаполнитьФизическиеЛицаПодразделения(МассивФизЛиц, Подразделение, Организация);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Работники, 
			"Ссылка", 
			МассивФизЛиц,
			ВидСравненияКомпоновкиДанных.ВСписке,
			, // Представление - автоматически
			ЗначениеЗаполнено(Подразделение));
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоОрганизации()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Бригады, 
		"Организация", 
		Организация, 
		ВидСравненияКомпоновкиДанных.Равно,
		, // Представление - автоматически
		ЗначениеЗаполнено(Организация));
	
КонецПроцедуры

#КонецОбласти
