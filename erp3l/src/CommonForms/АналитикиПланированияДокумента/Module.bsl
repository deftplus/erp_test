
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("АналитикиПланированияДокумента") Тогда
		Отказ = Истина;
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	ТипИсточника = ТипЗнч(Параметры.Документ);
	ЭтоПоставщик = ТипИсточника = Тип("ДокументСсылка.ЗаказПоставщику")
		ИЛИ ТипИсточника = Тип("ДокументСсылка.ПриобретениеТоваровУслуг");
	
	//
	АналитикиПланированияДокумента = Новый ФиксированныйМассив(Параметры.АналитикиПланированияДокумента);
	ВидБюджета = ПланыВидовХарактеристик.ВидыБюджетов.БюджетДвиженияДенежныхСредств;
	
	Для Каждого Строка Из АналитикиПланированияДокумента Цикл
		Если Строка.ВидБюджета = ВидБюджета Тогда
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, Строка);
		Иначе
			ВидБюджетаПоставка = Строка.ВидБюджета;
			ДокументРезервированияПоставка = Строка.ДокументРезервирования;
			ЦФОПоставка = Строка.ЦФО;
			ПроектПоставка = Строка.Проект;
			СтатьяБюджетаПоставка = Строка.СтатьяБюджета;
			Аналитика1Поставка = Строка.Аналитика1;
			Аналитика2Поставка = Строка.Аналитика2;
			Аналитика3Поставка = Строка.Аналитика3;
			Аналитика4Поставка = Строка.Аналитика4;
			Аналитика5Поставка = Строка.Аналитика5;
			Аналитика6Поставка = Строка.Аналитика6;
		КонецЕсли;
	КонецЦикла;
	
	Элементы.ВидБюджетаПоставка.СписокВыбора.Добавить(ПланыВидовХарактеристик.ВидыБюджетов.БюджетДоходовИРасходов);
	Если ЭтоПоставщик = Истина Тогда
		Элементы.ВидБюджетаПоставка.СписокВыбора.Добавить(ПланыВидовХарактеристик.ВидыБюджетов.БюджетДвиженияРесурсов);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВидБюджетаПоставка) Тогда
		ВидБюджетаПоставка = ПланыВидовХарактеристик.ВидыБюджетов.БюджетДоходовИРасходов;
	КонецЕсли;
	
	// Массив описаний статей формы
	МассивОписанийСтатей = Новый Массив;
	
	// Статья и Аналитика в Объекте (ВидАналитики нельзя создать в реквизите формы Объект. Должны быть созданы реквизиты формы)
	АналитикиСтатейБюджетовУХ.СтатьяИАналитикиРеквизитовФормыВПолях(МассивОписанийСтатей,
		"СтатьяБюджета", "СтатьяБюджета", 
		"Аналитика%1", "Аналитика%1", 
		ФормыУХ.РазместитьВГруппеСтрокой(Элементы.ГруппаОплата)
	);
	
	// Статья и Аналитика в Объекте (ВидАналитики нельзя создать в реквизите формы Объект. Должны быть созданы реквизиты формы)
	АналитикиСтатейБюджетовУХ.СтатьяИАналитикиРеквизитовФормыВПолях(МассивОписанийСтатей,
		"СтатьяБюджетаПоставка", "СтатьяБюджетаПоставка", 
		"Аналитика%1Поставка", "Аналитика%1Поставка", 
		ФормыУХ.РазместитьВГруппеСтрокой(Элементы.ГруппаИсполнение)
	);
	
	
	// Создать элементы формы для статей бюджетов и их аналитик
	АналитикиСтатейБюджетовУХ.СоздатьСтатьиБюджетовИАналитики(ЭтаФорма, МассивОписанийСтатей, ПараметрыПоляВвода());
	
	ПредназначениеПоставка = ВидБюджетаПоставка.Предназначение;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Подключаемый_СтатьяБюджета_ПриИзменении(Элемент)
	АналитикиСтатейБюджетовУХКлиент.ПриИзмененииСтатьиБюджета(ЭтотОбъект, Элемент.Имя);
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_АналитикаСтатьиБюджета_ПриИзменении(Элемент)
	АналитикиСтатейБюджетовУХКлиент.ПриИзмененииАналитикиСтатьиБюджета(ЭтотОбъект, Элемент.Имя);
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЦФОПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПроектПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДокументРезервированияПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДокументРезервированияПоставкаПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ВидБюджетаПоставкаПриИзменении(Элемент)
	ВидБюджетаПоставкаПриИзмененииНаСервере();
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьПоУмолчанию()
	
	ОчиститьСообщения();
	
	ЗаполнитьПоДоговору();
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ОчиститьСообщения();
	Если Не Модифицированность Или ТолькоПросмотр Тогда
		Закрыть();
	Иначе
		ИменаРеквизитов = "ВидБюджета, ДокументРезервирования, ЦФО, Проект, СтатьяБюджета, Аналитика1, Аналитика2, Аналитика3, Аналитика4, Аналитика5, Аналитика6";
		
		РезультатЗакрытия = Новый Массив;
		// Оплата
		Данные = Новый Структура(ИменаРеквизитов);
		ЗаполнитьЗначенияСвойств(Данные, ЭтотОбъект, ИменаРеквизитов);
		РезультатЗакрытия.Добавить(Данные);
		// Поставка
		Данные = Новый Структура(ИменаРеквизитов);
		ЗаполнитьЗначенияСвойств(Данные, ЭтотОбъект, ИменаРеквизитов);
		Данные.ВидБюджета = ВидБюджетаПоставка;
		Данные.ЦФО = ЦФОПоставка;
		Данные.ДокументРезервирования = ДокументРезервированияПоставка;
		Данные.Проект = ПроектПоставка;
		Данные.СтатьяБюджета = СтатьяБюджетаПоставка;
		Данные.Аналитика1 = Аналитика1Поставка;
		Данные.Аналитика2 = Аналитика2Поставка;
		Данные.Аналитика3 = Аналитика3Поставка;
		Данные.Аналитика4 = Аналитика4Поставка;
		Данные.Аналитика5 = Аналитика5Поставка;
		Данные.Аналитика6 = Аналитика6Поставка;
		
		РезультатЗакрытия.Добавить(Данные);
		
		Закрыть(РезультатЗакрытия);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПараметрыПоляВвода()
	ПараметрыПоляВвода = Новый Структура;
	ПараметрыПоляВвода.Вставить("ПоложениеЗаголовка", 		ПоложениеЗаголовкаЭлементаФормы.Лево);
	ПараметрыПоляВвода.Вставить("АвтоМаксимальнаяШирина",	Ложь);
	ПараметрыПоляВвода.Вставить("МаксимальнаяШирина",		40);
	
	Возврат ПараметрыПоляВвода;

КонецФункции

&НаСервере
Процедура ЗаполнитьПоДоговору()
	
	
	
КонецПроцедуры

&НаСервере
Процедура ВидБюджетаПоставкаПриИзмененииНаСервере()
	ПредназначениеПоставка = ВидБюджетаПоставка.Предназначение;
КонецПроцедуры

#КонецОбласти

