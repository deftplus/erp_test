//  Пример использования:
//		// I. Получение значения реквизитов по структуре реквизитов
//		СтруктураРеквизитов = Новый Структура;
//		СтруктураРеквизитов.Вставить("УсловиеОплаты", "УсловиеОплаты");
//		СтруктураРеквизитов.Вставить("ВерсияСоглашения", "Договор.ВерсияСоглашения");
//		СтруктураРеквизитов.Вставить("ВерсияСоглашения", "%ЗаказПоставщику.ДоговорКонтрагента%.ВерсияСоглашения");
//		
//		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
//			Документы.ЗаказПоставщику.ПустаяСсылка(),
//			ОбщиеОбъектыОПККлиентСервер.ЗаменитьИменаРеквизитовВСтруктуре(СтруктураРеквизитов)
//		);
//		
//		// II. Получение значения реквизита
//		Реквизит = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
//			Справочники.ДоговорыКонтрагентов.ПустаяСсылка(),
//			ОбщиеОбъектыОПККлиентСервер.ЗаменитьИменаРеквизитов("%ДоговорыКонтрагентов.Контрагент%")
//		);

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет замену шаблонов общих реквизитов в структуре предназначенной для получения значений реквизитов объектов
Функция ЗаменитьИменаРеквизитовВСтруктуре(СтруктураРеквизитов) Экспорт

	Результат = Новый Структура;
	
	Реквизиты = ОбщиеОбъектыОПКВызовСервера.ОбщиеОбъекты();
	
	Для Каждого КлючЗначение Из СтруктураРеквизитов Цикл
		Результат.Вставить(КлючЗначение.Ключ, ЗаменитьИменаРеквизитов(КлючЗначение.Значение, Реквизиты));
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Выполняет замену описания реквизита общего объекта 
Функция ЗаменитьИменаРеквизитов(СтрокаСРеквизитами, Знач Реквизиты = неопределено) Экспорт
	
	Если СтрЧислоВхождений(СтрокаСРеквизитами, "%") <> 2 Тогда
		Возврат СтрокаСРеквизитами;
	КонецЕсли;
	
	Если Реквизиты = неопределено Тогда
		Реквизиты = ОбщиеОбъектыОПКВызовСервера.ОбщиеОбъекты();
	КонецЕсли;
	
	Вхождение1 = СтрНайти(СтрокаСРеквизитами, "%", , , 1);
	Вхождение2 = СтрНайти(СтрокаСРеквизитами, "%", , , 2);
	Подстановка = Сред(СтрокаСРеквизитами, Вхождение1+1, Вхождение2-Вхождение1-1);
	
	Замена = ПолучитьЗамену(Реквизиты, Подстановка);
	Если Замена = неопределено Тогда
		Возврат СтрокаСРеквизитами;
	КонецЕсли;
	
	Возврат СтрЗаменить(СтрокаСРеквизитами, "%"+Подстановка+"%", Замена);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьЗамену(ОбщиеОбъектыРеквизиты, Подстановка)
	
	Реквизит = ОбщиеОбъектыРеквизиты.Реквизиты[Подстановка];
	Если Реквизит = неопределено Тогда
		Возврат неопределено;
	КонецЕсли;
	
	Возврат Реквизит;
	
КонецФункции

#КонецОбласти
 