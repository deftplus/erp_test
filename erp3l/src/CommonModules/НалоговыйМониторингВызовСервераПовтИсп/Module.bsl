
Функция ПолучитьМассивБезусловноПередаваемыхОбъектов() Экспорт
	
	Массив = Новый Массив;
	ДополнитьОбъектамиПодсистемы(Массив, Метаданные.Подсистемы.СтандартныеПодсистемы);
	
	//Массив.Добавить(Метаданные.Константы.ИнтерфейсТакси);
	Возврат ОбщегоНазначенияКлиентСервер.СвернутьМассив(Массив);
	
КонецФункции

Функция ПодсистемаСуществует(Имя) Экспорт
	Возврат Метаданные.Подсистемы.Найти(Имя) <> Неопределено;
КонецФункции

Процедура ДополнитьОбъектамиПодсистемы(МассивОбъектов, Подсистема)
	
	Для каждого Объект Из Подсистема.Состав Цикл
		//Если Метаданные.Справочники.Содержит(Объект) ИЛИ Метаданные.Документы.Содержит(Объект) Тогда
			МассивОбъектов.Добавить(Объект);
		//КонецЕсли;
	КонецЦикла;
	
	Для каждого ВложеннаяПодсистема Из Подсистема.Подсистемы Цикл
		ДополнитьОбъектамиПодсистемы(МассивОбъектов, ВложеннаяПодсистема);
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьУзлыНМ() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НалоговыйМониторинг.Ссылка КАК Ссылка,
	|	НалоговыйМониторинг.Организации.(
	|		Организация КАК Организация
	|	) КАК Организации
	|ИЗ
	|	ПланОбмена.НалоговыйМониторинг КАК НалоговыйМониторинг
	|ГДЕ
	|	НалоговыйМониторинг.ЭтотУзел = ЛОЖЬ
	|	И НалоговыйМониторинг.ПометкаУдаления = ЛОЖЬ");
	
	РезультатыЗапроса = Запрос.Выполнить();
	
	Если РезультатыЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат РезультатыЗапроса.Выгрузить();

КонецФункции

Функция ПолучитьМассивФормОтчетности() Экспорт
	Массив = Новый Массив;
	Макет = Документы.ЗапросДанныхНалоговогоМониторинга.ПолучитьМакет("МакетФормОтчетности");
	Инд = 1;
	Пока Истина Цикл
		ИмяФормы = Макет.Область(Инд, 1, Инд, 1).Текст;
		Если ПустаяСтрока(ИмяФормы) Тогда
			Прервать;
		КонецЕсли;
		Инд = Инд + 1;
		Массив.Добавить(ИмяФормы);
	КонецЦикла;
	Возврат Массив;
КонецФункции

Функция ПолучитьСтатусРегистрацииДляТекущегоУзла() Экспорт
	Возврат ПланыОбмена.НалоговыйМониторинг.ЭтотУзел().ОтключитьРегистрациюИзменений;
КонецФункции

Функция ПолучитьМассивОбъектовИсключенийДляРегистрацииОбмена() Экспорт
	МассивОбъектов = Новый Массив;
	МассивОбъектов.Добавить(Метаданные.Документы.ЗапросДанныхНалоговогоМониторинга);
	МассивОбъектов.Добавить(Метаданные.Документы.ОтветНаЗапросНалоговогоМониторинга);
	МассивОбъектов.Добавить(Метаданные.РегистрыСведений.ОценкаОтветаНаЗапросНалоговогоОргана);
	МассивОбъектов.Добавить(Метаданные.РегистрыСведений.ВыгрузкиОбъектовИнспекторами);
	МассивОбъектов.Добавить(Метаданные.РегистрыСведений.ДействияПользователяНалоговогоМониторинга);
	
	МассивОбъектов.Добавить(Метаданные.РегистрыСведений.СостоянияОбменовДанными);
	МассивОбъектов.Добавить(Метаданные.РегистрыСведений.СостоянияУспешныхОбменовДанными);
	
	НалоговыйМониторингПереопределяемый.МассивОбъектовИсключенийДляРегистрацииОбмена(МассивОбъектов);
	
	Возврат МассивОбъектов;
КонецФункции

#Область Документы

Функция СрокПредоставленияДокументов() Экспорт
	Возврат 10;
КонецФункции

#КонецОбласти
