#Область ПрограммныйИнтерфейс

// Дополняет профили ERP и УХ спецификой объединенного решения.
Процедура ПриЗаполненииПоставляемыхПрофилейГруппДоступа(ОписанияПрофилей, ПараметрыОбновления) Экспорт
	
	ИндексПрофилей = ИндексироватьПрофили(ОписанияПрофилей, "Имя");
	
	// Профиль ERP "Казначей"
	ОписаниеПрофиля = НайтиПрофиль("Казначей", ОписанияПрофилей, ИндексПрофилей);
	ДобавитьРолиКазначействаЕХ(ОписаниеПрофиля);
	
	// Профиль УХ "БюджетированиеКазначействоУХ"
	ОписаниеПрофиля = НайтиПрофиль("БюджетированиеКазначействоУХ", ОписанияПрофилей, ИндексПрофилей);
	ДобавитьРолиКазначействаЕХ(ОписаниеПрофиля);
	
	ДобавитьДоступКВерсиямСоглашенийВПрофили(ОписанияПрофилей);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьРолиКазначействаЕХ(ОписаниеПрофиля)
	
	ОписаниеПрофиля.Роли.Добавить("КазначействоЕХ");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеБюджетированиеКазначейство");
	
КонецПроцедуры		// ДобавитьРолиЕХ()

Процедура ДобавитьРолиНалоговыйМониторингЕХ(ОписаниеПрофиля)
	
	ОписаниеПрофиля.Роли.Добавить("ЧтениеДанныхРегламентированногоУчета");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеДанныхРегламентированнойОтчетности");
	
КонецПроцедуры		// ДобавитьРолиЕХ()

Функция ИндексироватьПрофили(ОписанияПрофилей, ПолеПрофиля = "Имя")
	
	Результат = Новый Соответствие;
	Для Индекс = 0 По ОписанияПрофилей.ВГраница() Цикл
		
		Профиль = ОписанияПрофилей[Индекс];
		Результат.Вставить(Профиль[ПолеПрофиля], Индекс);
		
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

Функция НайтиПрофиль(ИмяПрофиля, ОписанияПрофилей, ИндексПрофилей)

	ИндексПрофиля = ИндексПрофилей[ИмяПрофиля];
	Если ИндексПрофиля = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Не обнаружен дополняемый профиль доступа'")+" ""%1""";
		ВызватьИсключение СтрШаблон(ТекстСообщения, ИмяПрофиля);
	КонецЕсли;
	Возврат ОписанияПрофилей[ИндексПрофиля];
	
КонецФункции

Процедура ДобавитьДоступКВерсиямСоглашенийВПрофили(ОписанияПрофилей)
	
	// если есть права на договор контрагента, то также добавляем права на версии соглашений
	Для каждого Профиль ИЗ ОписанияПрофилей Цикл
		Если НЕ Профиль.Свойство("Роли") Тогда
		    Продолжить;    	
		КонецЕсли;
		Если Профиль.Роли.Найти("ЧтениеДоговоровКонтрагентов") <> Неопределено
			ИЛИ	Профиль.Роли.Найти("ЧтениеДоговоровКредитовИДепозитов") <> Неопределено Тогда
			
			Если Профиль.Роли.Найти("ЧтениеВерсийСоглашений") = Неопределено Тогда
				Профиль.Роли.Добавить("ЧтениеВерсийСоглашений");
			КонецЕсли;
			
		КонецЕсли;
		
		Если Профиль.Роли.Найти("ДобавлениеИзменениеДоговоровКонтрагентов") <> Неопределено
			ИЛИ	Профиль.Роли.Найти("ДобавлениеИзменениеДоговоровКредитовИДепозитов") <> Неопределено Тогда
			
			Если Профиль.Роли.Найти("ДобавлениеИзменениеВерсийСоглашений") = Неопределено Тогда
				Профиль.Роли.Добавить("ДобавлениеИзменениеВерсийСоглашений");
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
#КонецОбласти
