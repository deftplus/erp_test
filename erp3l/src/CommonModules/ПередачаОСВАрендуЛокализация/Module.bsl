////////////////////////////////////////////////////////////////////////////////
// Процедуры, используемые для локализации документа "Передача ОС в аренду".
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

// Добавляет команды печати.
// 
// Параметры:
// 	КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	//++ Локализация
	
	// ОС-1 (Акт о приеме-передаче ОС)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОС1";
	КомандаПечати.Идентификатор = "ОС1";
	КомандаПечати.Представление = НСтр("ru = 'ОС-1 (Акт о приеме-передаче ОС)';
										|en = 'FA-1 (Certificate of FA handover)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ФункциональныеОпции = "ИспользоватьРеглУчет";
	
	//-- Локализация

КонецПроцедуры

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация
	
	ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаСобытияОСОрганизаций(ТекстыЗапроса, Регистры);

	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
	
	//-- Локализация
	
КонецПроцедуры

Функция ПолучитьДанныеДляПечатнойФормыОС1(ПараметрыПечати, МассивОбъектов) Экспорт
	
	ДанныеДляПечатнойФормы = Неопределено;
	
	//++ Локализация
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр(
		"ИспользоватьУчетДрагоценныхМатериалов",
		ПолучитьФункциональнуюОпцию("ИспользоватьУчетДрагоценныхМатериалов"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Дата,
	|	Операция.Организация КАК Организация,
	|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	ТабличнаяЧасть.ОсновноеСредство КАК ОсновноеСредство,
	|	ЕСТЬNULL(НастройкиПоАналитике.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) КАК СчетУчета
	|ПОМЕСТИТЬ втОбъектыДокументов
	|ИЗ
	|	Документ.ПередачаОСВАренду2_4 КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПередачаОСВАренду2_4.ОС КАК ТабличнаяЧасть
	|		ПО Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК НастройкиПоАналитике
	|		ПО НастройкиПоАналитике.ВидСчета = ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА)
	|		И НастройкиПоАналитике.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		И НастройкиПоАналитике.АналитикаУчета = Операция.ГруппаФинансовогоУчета
	|		И НастройкиПоАналитике.МестоУчета = НЕОПРЕДЕЛЕНО
	|ГДЕ
	|	Операция.Ссылка В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация,
	|	Дата";
	
	Запрос.Выполнить();
	
	ВнеоборотныеАктивыЛокализация.ПолучитьСведенияОбОсновныхСредствахВДокументах(Запрос.МенеджерВременныхТаблиц, МассивОбъектов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Шапка.Ссылка КАК Ссылка,
	|	Шапка.Номер КАК НомерДокумента,
	|	Шапка.Дата КАК ДатаДокумента,
	|	Шапка.Дата КАК ДатаВвода,
	|	НЕОПРЕДЕЛЕНО КАК ДатаПередачи,
	|	Шапка.Организация.Префикс КАК Префикс,
	|	Шапка.Организация КАК Сдатчик,
	|	Шапка.Подразделение КАК ПодразделениеСдатчика,
	|	Шапка.Подразделение КАК МестонахождениеОС,
	|	Шапка.Договор.Наименование КАК Основание,
	|	НЕОПРЕДЕЛЕНО КАК БанковскийСчетСдатчика,
	|	Шапка.Арендатор КАК Получатель,
	|	ВЫБОР
	|		КОГДА Шапка.РасчетыМеждуОрганизациямиАрендатор
	|			ТОГДА Шапка.ПодразделениеАрендатора
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ПодразделениеПолучателя,
	|	НЕОПРЕДЕЛЕНО КАК БанковскийСчетПолучателя,
	|	НЕОПРЕДЕЛЕНО КАК Руководитель,
	|	НЕОПРЕДЕЛЕНО КАК ДолжностьРуководителя,
	|	НЕОПРЕДЕЛЕНО КАК ГлавныйБухгалтер,
	|	ВЫБОР
	|		КОГДА втОбщиеСведенияОбъектовДокументов.КоличествоРазличныхМОЛ = 1
	|			ТОГДА втОбщиеСведенияОбъектовДокументов.МОЛ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СдалМОЛ,
	|	НЕОПРЕДЕЛЕНО КАК ДолжностьСдалМОЛ,
	|	НЕОПРЕДЕЛЕНО КАК ПринялМОЛ,
	|	НЕОПРЕДЕЛЕНО КАК ДолжностьПринялМОЛ
	|ИЗ
	|	Документ.ПередачаОСВАренду2_4 КАК Шапка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОбщиеСведенияОбъектовДокументов КАК втОбщиеСведенияОбъектовДокументов
	|		ПО Шапка.Ссылка = втОбщиеСведенияОбъектовДокументов.Ссылка
	|ГДЕ
	|	Шапка.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОсновныеСредства.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА втОсновныеСредства.ОсновноеСредство.НаименованиеПолное = """"
	|			ТОГДА втОсновныеСредства.ОсновноеСредство
	|		ИНАЧЕ втОсновныеСредства.ОсновноеСредство.НаименованиеПолное
	|	КОНЕЦ КАК НаименованиеОС,
	|	втСведенияОбъектовДокументов.ГруппаОС КАК ГруппаОС,
	|	ЕСТЬNULL(втСведенияОбъектовДокументов.АмортизационнаяГруппа.Порядок + 1, 0) КАК АмортизационнаяГруппа,
	|	втСведенияОбъектовДокументов.КодПоОКОФ.Код КАК КодОКОФ,
	|	втСведенияОбъектовДокументов.ИнвентарныйНомер КАК НомерИнвентарный,
	|	втОсновныеСредства.ОсновноеСредство.ЗаводскойНомер КАК НомерЗаводской,
	|	НЕОПРЕДЕЛЕНО КАК ДатаНачалаСтроительства,
	|	втОсновныеСредства.ОсновноеСредство.ДатаВыпуска КАК ДатаВыпуска,
	|	втСведенияОбъектовДокументов.ДатаПринятияКУчету КАК ДатаВводаВЭксплуатацию,
	|	НЕОПРЕДЕЛЕНО КАК ДатаМодернизации,
	|	НЕОПРЕДЕЛЕНО КАК ДатаКапитальногоРемонта,
	|	РАЗНОСТЬДАТ(втСведенияОбъектовДокументов.ДатаПринятияКУчету, втОсновныеСредства.Дата, МЕСЯЦ) КАК СрокИспользованияФакт,
	|	втСведенияОбъектовДокументов.СрокПолезногоИспользования КАК СрокИспользования,
	|	втСведенияОбъектовДокументов.СрокПолезногоИспользования КАК СрокИспользованияРаздел2,
	|	втСведенияОбъектовДокументов.НакопленнаяСтоимость КАК СтоимостьПервоначальная,
	|	втСведенияОбъектовДокументов.ОстаточнаяСтоимость КАК СтоимостьОстаточная,
	|	втОсновныеСредства.СчетУчета КАК СчетУчетаОС,
	|	0 КАК ЦенаПродажи,
	|	втСведенияОбъектовДокументов.НакопленнаяАмортизация КАК АмортизацияСумма,
	|	втСведенияОбъектовДокументов.СпособНачисленияАмортизации КАК АмортизацияСпособ,
	|	втСведенияОбъектовДокументов.КоэффициентАмортизации КАК АмортизацияНорма,
	|	ЕСТЬNULL(втСведенияОбъектовДокументов.СведенияАктуальны, ЛОЖЬ) КАК СведенияАктуальны
	|ИЗ
	|	втОбъектыДокументов КАК втОсновныеСредства
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
	|		ПО втОсновныеСредства.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСведенияОбъектовДокументов КАК втСведенияОбъектовДокументов
	|		ПО втОсновныеСредства.Ссылка = втСведенияОбъектовДокументов.Ссылка
	|			И втОсновныеСредства.ОсновноеСредство = втСведенияОбъектовДокументов.ОсновноеСредство
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	втОсновныеСредства.НомерСтроки
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Ссылка КАК Ссылка,
	|	ДрагоценныеМатериалы.ДрагоценныйМатериал КАК ДрагоценныйМатериал,
	|	ДрагоценныеМатериалы.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СУММА(ДрагоценныеМатериалы.Количество) КАК Количество
	|ИЗ
	|	Документ.ПередачаОСВАренду2_4.ОС КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации.ДрагоценныеМатериалы КАК ДрагоценныеМатериалы
	|		ПО ТабличнаяЧасть.ОсновноеСредство = ДрагоценныеМатериалы.Ссылка
	|ГДЕ
	|	&ИспользоватьУчетДрагоценныхМатериалов
	|	И ТабличнаяЧасть.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧасть.Ссылка,
	|	ДрагоценныеМатериалы.ДрагоценныйМатериал,
	|	ДрагоценныеМатериалы.ЕдиницаИзмерения
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТабличнаяЧасть.Ссылка
	|ИТОГИ ПО
	|	Ссылка";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ДанныеДляПечатнойФормы = Новый Структура;
	ДанныеДляПечатнойФормы.Вставить("РезультатПоШапке", РезультатыЗапроса[0]);
	ДанныеДляПечатнойФормы.Вставить("РезультатПоТабличнойЧасти", РезультатыЗапроса[1]);
	ДанныеДляПечатнойФормы.Вставить("РезультатПоДрагоценнымМатериалам", РезультатыЗапроса[2]);
	
	//-- Локализация

	Возврат ДанныеДляПечатнойФормы;
	
КонецФункции

Процедура ДополнитьБлокировкуЧитаемыхДанныхПриПроведении(Объект, Блокировка) Экспорт

//++ Локализация

	Если Объект.ОтражатьВРеглУчете Тогда
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПорядокУчетаОСБУ");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = Объект.ОС;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ОсновноеСредство", "ОсновноеСредство");
		ЭлементБлокировки.УстановитьЗначение("Организация", Объект.Организация);
		
	КонецЕсли;

//-- Локализация
	
КонецПроцедуры

Функция ТекстОтраженияВРеглУчете() Экспорт
	
	ТекстЗапроса = "";
	
	//++ Локализация
	
	СписокЗапросов = Новый Массив;
	
	ВнеоборотныеАктивыЛокализация.ТекстОтраженияВРеглУчетеПеремещенияСтоимостиОС(СписокЗапросов);
	ВнеоборотныеАктивыЛокализация.ТекстОтраженияВРеглУчетеПеремещенияАмортизацииОС(СписокЗапросов);
	ВнеоборотныеАктивыЛокализация.ТекстОтраженияВРеглУчетеПеремещенияДоходовОСЗаСчетЦФ(СписокЗапросов);
	ЗабалансовыйУчетАрендованныхОС(СписокЗапросов);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	ТекстЗапроса = "";
	
	//++ Локализация
	
	ТекстыСозданияВременныхТаблиц = Новый Массив;
	ТекстыСозданияВременныхТаблиц.Добавить(ВнеоборотныеАктивыЛокализация.ТекстЗапросаВтДоходыОСЗаСчетЦФ());
	ТекстыСозданияВременныхТаблиц.Добавить(Символы.ПС);
	
	ТекстЗапроса = СтрСоединить(ТекстыСозданияВременныхТаблиц, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	//-- Локализация

	Возврат ТекстЗапроса;
	
КонецФункции

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	//-- Локализация
	
КонецПроцедуры

//++ Локализация

Процедура ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПорядокУчетаОСБУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыЛокализация.ТекстЗапросаТаблицаВтПорядокУчетаОСБУ(ТекстыЗапроса, ПолноеИмяОбъекта());
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПервоначальныеСведенияОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                 КАК Период,
	|	&Организация                            КАК Организация,
	|	втПорядокУчетаОСБУ.ОсновноеСредство     КАК ОсновноеСредство,
	|
	|	втПорядокУчетаОСБУ.СостояниеБУ          КАК СостояниеБУ,
	|	втПорядокУчетаОСБУ.СостояниеНУ          КАК СостояниеНУ,
	|
	// НачислятьАмортизациюБУ
	|	ВЫБОР 
	|		КОГДА &НачислениеАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыИзмененияНачисленияАмортизации.Действует)
	|			И (втПорядокУчетаОСБУ.НачислятьАмортизациюБУ
	|				ИЛИ втПервоначальныеСведенияОС.ПорядокУчетаБУ В (
	|					ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеАмортизации),
	|					ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НачислятьАмортизациюБУ,
	|
	// НачислятьАмортизациюНУ
	|	ВЫБОР 
	|		КОГДА НЕ ТаблицаОС.ОсновноеСредство ЕСТЬ NULL
	|			И &НачислениеАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыИзмененияНачисленияАмортизации.Действует)
	|			И (втПорядокУчетаОСБУ.НачислятьАмортизациюНУ
	|				ИЛИ втПервоначальныеСведенияОС.ПорядокУчетаНУ В (
	|					ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации),
	|					ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.АмортизацияНачислена)))
	|				ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НачислятьАмортизациюНУ,
	|
	|	&СтатьяРасходовБУ                       КАК СтатьяРасходовБУ,
	|	&АналитикаРасходовБУ                    КАК АналитикаРасходовБУ,
	|
	// СтатьяРасходовНУ
	|	ВЫБОР 
	|		КОГДА НЕ ТаблицаОС.ОсновноеСредство ЕСТЬ NULL
	|			И &НачислениеАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыИзмененияНачисленияАмортизации.Действует)
	|			И (втПорядокУчетаОСБУ.НачислятьАмортизациюНУ
	|				ИЛИ втПервоначальныеСведенияОС.ПорядокУчетаНУ В (
	|					ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации),
	|					ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.АмортизацияНачислена)))
	|				ТОГДА &СтатьяРасходовБУ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                   КАК СтатьяРасходовНУ,
	|
	// АналитикаРасходовНУ
	|	ВЫБОР 
	|		КОГДА НЕ ТаблицаОС.ОсновноеСредство ЕСТЬ NULL
	|			И &НачислениеАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.ВариантыИзмененияНачисленияАмортизации.Действует)
	|			И (втПорядокУчетаОСБУ.НачислятьАмортизациюНУ
	|				ИЛИ втПервоначальныеСведенияОС.ПорядокУчетаНУ В (
	|					ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации),
	|					ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.АмортизацияНачислена)))
	|				ТОГДА &АналитикаРасходовБУ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                   КАК АналитикаРасходовНУ,
	|
	|	втПорядокУчетаОСБУ.АмортизационнаяГруппа                  КАК АмортизационнаяГруппа,
	|	втПорядокУчетаОСБУ.НедвижимоеИмущество                    КАК НедвижимоеИмущество,
	|	втПорядокУчетаОСБУ.СтатьяРасходовАмортизационнойПремии    КАК СтатьяРасходовАмортизационнойПремии,
	|	втПорядокУчетаОСБУ.АналитикаРасходовАмортизационнойПремии КАК АналитикаРасходовАмортизационнойПремии,
	|	втПорядокУчетаОСБУ.НалогообложениеНДС                     КАК НалогообложениеНДС
	|ИЗ
	|	втПорядокУчетаОСБУ КАК втПорядокУчетаОСБУ
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК втПервоначальныеСведенияОС
	|		ПО втПервоначальныеСведенияОС.ОсновноеСредство = втПорядокУчетаОСБУ.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаОСВАренду2_4.ОС КАК ТаблицаОС
	|		ПО втПорядокУчетаОСБУ.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|		И ТаблицаОС.Ссылка = &Ссылка
	|ГДЕ
	|	&ОтражатьВРеглУчете
	|	И &НачислениеАмортизацииБУ В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыИзмененияНачисленияАмортизации.Действует),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыИзмененияНачисленияАмортизации.Остановлено))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                                 КАК Период,
	|	&Арендатор                              КАК Организация,
	|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство,
	|
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКЗабалансовомуУчету) КАК СостояниеБУ,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКЗабалансовомуУчету) КАК СостояниеНУ,
	|
	|	ЛОЖЬ КАК НачислятьАмортизациюБУ,
	|	ЛОЖЬ КАК НачислятьАмортизациюНУ,
	|
	|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходовБУ,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходовБУ,
	|
	|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходовНУ,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходовНУ,
	|
	|	НЕОПРЕДЕЛЕНО КАК АмортизационнаяГруппа,
	|	ЛОЖЬ         КАК НедвижимоеИмущество,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходовАмортизационнойПремии,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходовАмортизационнойПремии,
	|	НЕОПРЕДЕЛЕНО КАК НалогообложениеНДС
	|ИЗ
	|	Документ.ПередачаОСВАренду2_4.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &РасчетыМеждуОрганизациямиАрендатор
	|	И &ОтражатьВРеглУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаСобытияОСОрганизаций(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СобытияОСОрганизаций";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтУзлыОсновныхСредств(ТекстыЗапроса, "Документ.ПередачаОСВАренду2_4.ОС");
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	&СобытиеОС КАК Событие,
	|	&НазваниеДокумента КАК НазваниеДокумента,
	|	&Номер КАК НомерДокумента
	|ИЗ
	|	Документ.ПередачаОСВАренду2_4.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &СобытиеОС <> ЗНАЧЕНИЕ(Справочник.СобытияОС.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	УзлыКомпоненты.УзелКомпонент КАК ОсновноеСредство,
	|	&СобытиеОС КАК Событие,
	|	&НазваниеДокумента КАК НазваниеДокумента,
	|	&Номер КАК НомерДокумента
	|ИЗ
	|	Документ.ПередачаОСВАренду2_4.ОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УзлыКомпонентыАмортизации.СрезПоследних(&Период, УзелКомпонент В
	|			(ВЫБРАТЬ
	|				ТаблицаУзлов.УзелКомпонент КАК УзелКомпонент
	|			ИЗ
	|				ВтУзлыОсновныхСредств КАК ТаблицаУзлов)) КАК УзлыКомпоненты
	|		ПО ТаблицаОС.ОсновноеСредство = УзлыКомпоненты.ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &СобытиеОС <> ЗНАЧЕНИЕ(Справочник.СобытияОС.ПустаяСсылка)";	
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                      КАК Период,
	|	&Организация                 КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                      КАК Период,
	|	&Арендатор                   КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(&Арендатор) = ТИП(Справочник.Организации)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ЗабалансовыйУчетАрендованныхОС(СписокЗапросов)
	
	ТекстЗапроса = 
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Поступление на забалансовый учет у арендатора ОС (Дт 001 :: Кт ---)
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Арендатор КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.ЗалоговаяСтоимость КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.КорПодразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.АрендованныеОсновныеСредства) КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	Строки.Организация КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Поступление на забалансовый учет у арендатора ОС"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК Строки
	|			ПО Строки.Регистратор = ДокументыКОтражению.Ссылка
	|ГДЕ
	|	Строки.ЗалоговаяСтоимость <> 0
	|	И ТИПЗНАЧЕНИЯ(Строки.Арендатор) = ТИП(Справочник.Организации)
	|	И Строки.Арендатор <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Функция ПолноеИмяОбъекта()

	Возврат "Документ.ПередачаОСВАренду2_4";

КонецФункции

//-- Локализация

#КонецОбласти
