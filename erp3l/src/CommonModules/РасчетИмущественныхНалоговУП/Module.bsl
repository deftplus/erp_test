////////////////////////////////////////////////////////////////////////////////
// Подсистема "Расчет имущественных налогов".
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

#Область РасчетНалогаНаИмущество
 
Процедура ДобавитьТекстЗапросаУчетОсновныхСредств(Период, ПараметрыРасчета, СписокЗапросов) Экспорт

	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МестонахождениеОС.Организация КАК Организация,
	|	МестонахождениеОС.ОсновноеСредство КАК ОсновноеСредство,
	|	МестонахождениеОС.Местонахождение КАК Местонахождение
	|ПОМЕСТИТЬ МестонахождениеОС
	|ИЗ
	|	РегистрСведений.МестонахождениеОС.СрезПоследних(
	|			&Период,
	|			Организация В (&Организации)
	|				И ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						СписокОС.ОсновноеСредство
	|					ИЗ
	|						СписокОС)) КАК МестонахождениеОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Регистрации.Организация КАК Организация,
	|	Регистрации.Подразделение КАК СтруктурнаяЕдиница,
	|	ПОДСТРОКА(Регистрации.РегистрацияВНалоговомОргане.Код,1,2) КАК КодРегиона
	|ПОМЕСТИТЬ АктуальныеРегистрацииВНалоговыхОрганах
	|ИЗ
	|	РегистрСведений.РегистрацииВНалоговомОргане КАК Регистрации
	|ГДЕ
	|	(Регистрации.Организация, Регистрации.Подразделение) В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				МестонахождениеОС.Организация КАК Организация,
	|				МестонахождениеОС.Местонахождение КАК Местонахождение
	|			ИЗ
	|				МестонахождениеОС КАК МестонахождениеОС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Организации.Ссылка КАК Организация,
	|	Организации.Ссылка КАК СтруктурнаяЕдиница,
	|	ПОДСТРОКА(Организации.РегистрацияВНалоговомОргане.Код,1,2) КАК КодРегиона
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка В (&Организации)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОС.ПорядокПогашенияСтоимости КАК ПорядокПогашенияСтоимости,
	|	ЕСТЬNULL(Регистрации.СтруктурнаяЕдиница, СписокОС.Организация) КАК Местонахождение,
	|	СчетаБухгалтерскогоУчета.СчетУчета КАК СчетУчета,
	|	СчетаБухгалтерскогоУчета.СчетОбесценения КАК СчетОбесценения,
	|	СчетаБухгалтерскогоУчета.СчетНачисленияАмортизации КАК СчетНачисленияАмортизации
	|ПОМЕСТИТЬ УчетОсновныхСредств_Предварительная
	|ИЗ
	|	СписокОС КАК СписокОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОС КАК МестонахождениеОС
	|		ПО СписокОС.Организация = МестонахождениеОС.Организация
	|			И СписокОС.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ АктуальныеРегистрацииВНалоговыхОрганах КАК Регистрации
	|		ПО Регистрации.Организация = МестонахождениеОС.Организация
	|			И Регистрации.СтруктурнаяЕдиница = МестонахождениеОС.Местонахождение
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СчетаБухгалтерскогоУчета КАК СчетаБухгалтерскогоУчета
	|		ПО СписокОС.ОсновноеСредство = СчетаБухгалтерскогоУчета.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УчетОсновныхСредств.Организация КАК Организация,
	|	УчетОсновныхСредств.ОсновноеСредство КАК ОсновноеСредство,
	|	УчетОсновныхСредств.ПорядокПогашенияСтоимости КАК ПорядокПогашенияСтоимости,
	|	ЕСТЬNULL(АктуальныеРегистрации.КодРегиона, 0) КАК КодРегиона,
	|	УчетОсновныхСредств.СчетУчета КАК СчетУчета,
	|	УчетОсновныхСредств.СчетОбесценения КАК СчетОбесценения,
	|	УчетОсновныхСредств.СчетНачисленияАмортизации КАК СчетНачисленияАмортизации
	|ПОМЕСТИТЬ УчетОсновныхСредств
	|ИЗ
	|	УчетОсновныхСредств_Предварительная КАК УчетОсновныхСредств
	|		ЛЕВОЕ СОЕДИНЕНИЕ АктуальныеРегистрацииВНалоговыхОрганах КАК АктуальныеРегистрации
	|		ПО УчетОсновныхСредств.Организация = АктуальныеРегистрации.Организация
	|			И УчетОсновныхСредств.Местонахождение = АктуальныеРегистрации.СтруктурнаяЕдиница
	|";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ДобавитьТекстЗапросаКоэффициентыЕНВД_ПоОС(СписокЗапросов) Экспорт

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СпособыОтраженияРасходов.ОсновноеСредство КАК ОсновноеСредство,
	|	КоэффициентыЕНВД.НеЕНВД КАК НеЕНВД,
	|	КоэффициентыЕНВД.Распределение КАК Распределение
	|ПОМЕСТИТЬ КоэффициентыЕНВД_ПоОС
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				И ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						СписокОС.ОсновноеСредство
	|					ИЗ
	|						СписокОС)) КАК СпособыОтраженияРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентыЕНВД КАК КоэффициентыЕНВД
	|		ПО СпособыОтраженияРасходов.ОсновноеСредство = КоэффициентыЕНВД.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
		
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура СоздатьВТКоэффициентыЕНВД(ПараметрыРасчета) Экспорт

	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУчетаОСБУ.ОсновноеСредство КАК ОсновноеСредство,
	|	ПорядокУчетаОСБУ.СтатьяРасходовБУ КАК СтатьяРасходов,
	|	ПорядокУчетаОСБУ.СпособОтраженияРасходовБУ <> НЕОПРЕДЕЛЕНО КАК СпособОтраженияРасходовЗаданДокументом,
	|	ПорядокУчетаОСБУ.СпособОтраженияРасходовБУ КАК СпособОтраженияРасходов
	|ПОМЕСТИТЬ СпособыОтраженияРасходовПоАмортизации
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(&Период, Организация = &Организация) КАК ПорядокУчетаОСБУ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СпособОтраженияРасходов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СпособыОтраженияРасходовПоАмортизации.ОсновноеСредство КАК ОсновноеСредство,
	|	ВЫБОР
	|		КОГДА СпособыОтраженияРасходовПоАмортизации.СпособОтраженияРасходовЗаданДокументом
	|			ТОГДА ЕСТЬNULL(СпособыОтраженияРасходовПоАмортизацииТаблица.СтатьяРасходов, СпособыОтраженияРасходовПоАмортизацииТаблица24.СтатьяРасходов)
	|		ИНАЧЕ СпособыОтраженияРасходовПоАмортизации.СтатьяРасходов
	|	КОНЕЦ КАК СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА СпособыОтраженияРасходовПоАмортизации.СпособОтраженияРасходовЗаданДокументом
	|			ТОГДА ЕСТЬNULL(СпособыОтраженияРасходовПоАмортизацииТаблица.Коэффициент, СпособыОтраженияРасходовПоАмортизацииТаблица24.Коэффициент)
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Коэффициент
	|ПОМЕСТИТЬ ВТ_ВыборкаКоэффициентов
	|ИЗ
	|	СпособыОтраженияРасходовПоАмортизации КАК СпособыОтраженияРасходовПоАмортизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровОС.ОтражениеАмортизационныхРасходов КАК СпособыОтраженияРасходовПоАмортизацииТаблица
	|		ПО СпособыОтраженияРасходовПоАмортизации.СпособОтраженияРасходов = СпособыОтраженияРасходовПоАмортизацииТаблица.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровОС2_4.Амортизация КАК СпособыОтраженияРасходовПоАмортизацииТаблица24
	|		ПО СпособыОтраженияРасходовПоАмортизации.СпособОтраженияРасходов = СпособыОтраженияРасходовПоАмортизацииТаблица24.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СпособыОтраженияРасходовПоАмортизации.ОсновноеСредство КАК ОсновноеСредство,
	|	СУММА(ЕСТЬNULL(СпособыОтраженияРасходовПоАмортизацииТаблица.Коэффициент, СпособыОтраженияРасходовПоАмортизацииТаблица24.Коэффициент)) КАК СуммаКоэффициентов
	|ПОМЕСТИТЬ ВТ_ВыборкаСуммыКоэффициентов
	|ИЗ
	|	СпособыОтраженияРасходовПоАмортизации КАК СпособыОтраженияРасходовПоАмортизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровОС.ОтражениеАмортизационныхРасходов КАК СпособыОтраженияРасходовПоАмортизацииТаблица
	|		ПО СпособыОтраженияРасходовПоАмортизации.СпособОтраженияРасходов = СпособыОтраженияРасходовПоАмортизацииТаблица.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровОС2_4.Амортизация КАК СпособыОтраженияРасходовПоАмортизацииТаблица24
	|		ПО СпособыОтраженияРасходовПоАмортизации.СпособОтраженияРасходов = СпособыОтраженияРасходовПоАмортизацииТаблица24.Ссылка
	|ГДЕ
	|	СпособыОтраженияРасходовПоАмортизации.СпособОтраженияРасходовЗаданДокументом
	|
	|СГРУППИРОВАТЬ ПО
	|	СпособыОтраженияРасходовПоАмортизации.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыборкаКоэффициентов.ОсновноеСредство КАК ОсновноеСредство,
	|	СУММА(ВЫБОР
	|			КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|				ТОГДА ВыборкаКоэффициентов.Коэффициент / ЕСТЬNULL(ВыборкаСуммыКоэффициентов.СуммаКоэффициентов, 1)
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НеЕНВД,
	|	СУММА(ВЫБОР
	|			КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|				ТОГДА ВыборкаКоэффициентов.Коэффициент / ЕСТЬNULL(ВыборкаСуммыКоэффициентов.СуммаКоэффициентов, 1)
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Распределение
	|ПОМЕСТИТЬ КоэффициентыЕНВД
	|ИЗ
	|	ВТ_ВыборкаКоэффициентов КАК ВыборкаКоэффициентов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|		ПО ВыборкаКоэффициентов.СтатьяРасходов = СтатьиРасходов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВыборкаСуммыКоэффициентов КАК ВыборкаСуммыКоэффициентов
	|		ПО ВыборкаКоэффициентов.ОсновноеСредство = ВыборкаСуммыКоэффициентов.ОсновноеСредство
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыборкаКоэффициентов.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Период", ПараметрыРасчета.ПериодРасчета);
	Запрос.УстановитьПараметр("Организация", ПараметрыРасчета.Организация);
	Запрос.УстановитьПараметр("ИспользуетсяУправлениеВНА_2_4", ВнеоборотныеАктивы.ИспользуетсяУправлениеВНА_2_4(ПараметрыРасчета.ПериодРасчета));
	Запрос.Выполнить();
	
КонецПроцедуры
 
Процедура СоздатьВТДвижимоеИмуществоПринятоеКУчетуПосле2013(ПараметрыРасчета) Экспорт

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПервоначальныеСведенияОС.ОсновноеСредство КАК ОС
	|ПОМЕСТИТЬ ВТДвижимоеИмуществоПринятоеКУчетуПосле2013
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|ГДЕ
	|	ПервоначальныеСведенияОС.Активность
	|	И ПервоначальныеСведенияОС.Период <= &КонецПериодаОтчета
	|	И ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ >= ДАТАВРЕМЯ(2013, 1, 1)
	|	И ПервоначальныеСведенияОС.Организация В
	|			(ВЫБРАТЬ
	|				Организации.Ссылка КАК Ссылка
	|			ИЗ
	|				Справочник.Организации КАК Организации
	|			ГДЕ
	|				(Организации.Ссылка = &ГоловнаяОрганизация
	|					ИЛИ Организации.ГоловнаяОрганизация = &ГоловнаяОрганизация))
	|	И НЕ ПервоначальныеСведенияОС.ОсновноеСредство.ГруппаОС В (
	|				ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Здания), 
	|				ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Сооружения), 
	|				ЗНАЧЕНИЕ(Перечисление.ГруппыОС.МноголетниеНасаждения), 
	|				ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ЗемельныеУчастки), 
	|				ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ПрочееИмуществоТребующееГосударственнойРегистрации), 
	|				ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ОбъектыПриродопользования))
	|
	|СГРУППИРОВАТЬ ПО
	|	ПервоначальныеСведенияОС.ОсновноеСредство
	|
	|ИМЕЮЩИЕ
	|	МИНИМУМ(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ) >= ДАТАВРЕМЯ(2013, 1, 1)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОС";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("КонецПериодаОтчета", КонецГода(ПараметрыРасчета.ПериодРасчета));
	
	ГоловнаяОрганизация = ПараметрыРасчета.Организация.ГоловнаяОрганизация;
	Если Не ЗначениеЗаполнено(ГоловнаяОрганизация) Тогда
		ГоловнаяОрганизация = ПараметрыРасчета.Организация;
	КонецЕсли;
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
	
	Запрос.Выполнить();
	
КонецПроцедуры
 
Процедура ДобавитьТекстЗапросаОССостоящиеНаУчете(ПараметрыРасчета, СписокЗапросов) Экспорт

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	АрендованныеОС.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ ВТ_АрендованныеОСНаБалансеАрендодателя
	|ИЗ
	|	РегистрСведений.АрендованныеОС.СрезПоследних(
	|			&Период,
	|			&ТекстОтбораОС) КАК АрендованныеОС
	|ГДЕ
	|	АрендованныеОС.Состояние В (
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ЗаключенДоговорАренды),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ВАренде))
	|	И АрендованныеОС.Договор.Балансодержатель = ЗНАЧЕНИЕ(Перечисление.БалансодержательПредметовАренды.Арендодатель)	
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|ВЫБРАТЬ
	|	ПорядокУчетаОСБУ.ОсновноеСредство КАК ОсновноеСредство,
	|	ПорядокУчетаОСБУ.ОсновноеСредство.ГруппаОС КАК ГруппаОС,
	|	ПорядокУчетаОСБУ.АмортизационнаяГруппа КАК АмортизационнаяГруппа,
	|	ПорядокУчетаОСБУ.НедвижимоеИмущество КАК НедвижимоеИмущество
	|ПОМЕСТИТЬ ВТ_ОССостоящиеНаУчете
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
	|			&Период,
	|			Организация В (&Организации)
	|				И НЕ ОсновноеСредство В (ВЫБРАТЬ Т.ОсновноеСредство ИЗ ВТ_АрендованныеОСНаБалансеАрендодателя КАК Т)
	|				И &ТекстОтбораОС
	|	) КАК ПорядокУчетаОСБУ
	|ГДЕ
	|	ПорядокУчетаОСБУ.СостояниеБУ = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|УНИЧТОЖИТЬ ВТ_АрендованныеОСНаБалансеАрендодателя";
	
	Если ПараметрыРасчета.РасчетПоФактическойСтоимости Тогда
		ТекстОтбораОС = "ОсновноеСредство В
		|					(ВЫБРАТЬ
		|						ВТ_РазницаВСтоимостиОС.ОбъектУчета
		|					ИЗ
		|						ВТ_РазницаВСтоимостиОС КАК ВТ_РазницаВСтоимостиОС
		|					ГДЕ
		|						ВТ_РазницаВСтоимостиОС.СтоимостьРегл > 0)";
	Иначе
		ТекстОтбораОС = "ИСТИНА";
	КонецЕсли; 
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстОтбораОС", ТекстОтбораОС);
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ДобавитьТекстЗапросаСписокОС(СписокЗапросов) Экспорт

	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПервоначальныеСведенияОС.Организация КАК Организация,
	|	ПервоначальныеСведенияОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ПервоначальныеСведенияОС.ПорядокУчетаБУ КАК ПорядокПогашенияСтоимости,
	|	ПорядокУчетаОСБУ.АмортизационнаяГруппа КАК АмортизационнаяГруппа,
	|	ПорядокУчетаОСБУ.НедвижимоеИмущество КАК НедвижимоеИмущество
	|ПОМЕСТИТЬ СписокОС
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
	|			&Период,
	|			Организация В (&Организации)
	|				И ОсновноеСредство В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						СписокРазрешенныхОС.ОсновноеСредство
	|					ИЗ
	|						СписокРазрешенныхОС КАК СписокРазрешенныхОС)) КАК ПервоначальныеСведенияОС
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
	|			&Период,
	|			Организация В (&Организации)
	|				И ОсновноеСредство В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						СписокРазрешенныхОС.ОсновноеСредство
	|					ИЗ
	|						СписокРазрешенныхОС КАК СписокРазрешенныхОС)) КАК ПорядокУчетаОСБУ
	|	ПО ПорядокУчетаОСБУ.ОсновноеСредство = ПервоначальныеСведенияОС.ОсновноеСредство
	|		И ПорядокУчетаОСБУ.Организация = ПервоначальныеСведенияОС.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СписокРазрешенныхОС";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ДобавитьТекстЗапросаСчетаБухгалтерскогоУчета(СписокЗапросов) Экспорт

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетУчета,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетОбесценения,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетНачисленияАмортизации
	|ПОМЕСТИТЬ ВТ_СчетаБухгалтерскогоУчета
	|ИЗ
	|	СписокОС КАК СписокОС
	|ГДЕ
	|	&ИспользуетсяУправлениеВНА_2_4
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПорядокУчетаОС.ОсновноеСредство,
	|	ПорядокУчетаОС.СчетУчета,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка),
	|	ПорядокУчетаОС.СчетНачисленияАмортизации
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОС.СрезПоследних(
	|			&Период,
	|			НЕ Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
	|				И ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						СписокОС.ОсновноеСредство
	|					ИЗ
	|						СписокОС)) КАК ПорядокУчетаОС
	|ГДЕ
	|	НЕ &ИспользуетсяУправлениеВНА_2_4
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ПараметрыЦелевогоФинансированияОС.ОсновноеСредство,
	|	ПараметрыЦелевогоФинансированияОС.СчетУчета,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка),
	|	ПараметрыЦелевогоФинансированияОС.СчетАмортизации
	|ИЗ
	|	РегистрСведений.ПараметрыЦелевогоФинансированияОС.СрезПоследних(
	|			&Период,
	|			НЕ Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
	|				И ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						СписокОС.ОсновноеСредство
	|					ИЗ
	|						СписокОС)) КАК ПараметрыЦелевогоФинансированияОС
	|ГДЕ
	|	НЕ &ИспользуетсяУправлениеВНА_2_4
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ДобавитьТекстЗапросаСтоимостьИАмортизация(Период, ПараметрыРасчета, СписокЗапросов) Экспорт

	Если ВнеоборотныеАктивы.ИспользуетсяУправлениеВНА_2_4(ДобавитьМесяц(Период, -1)) Тогда
		
		Если Месяц(ПараметрыРасчета.мДатаКонцаПериодаОтчета) + 1 = Месяц(Период) 
			И НЕ ПараметрыРасчета.РасчетПоФактическойСтоимости Тогда
			
			// Это расчет в выбранном месяце, в нем еще не выполнен расчет стоимости поступивших или модернизированных ОС
			
			ТекстЗапроса =
			"ВЫБРАТЬ
			|	ПринятиеКУчетуОС.Организация КАК Организация,
			|	СтоимостьОС.СтоимостьБУ КАК СтоимостьРегл,
			|	СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство
			|ПОМЕСТИТЬ СписокОС_РасчетПоПредварительнойСтоимости
			|ИЗ
			|	Документ.ПринятиеКУчетуОС2_4 КАК ПринятиеКУчетуОС
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС2_4.ОС КАК СтоимостьОС
			|		ПО (СтоимостьОС.Ссылка = ПринятиеКУчетуОС.Ссылка)
			|ГДЕ
			|	ПринятиеКУчетуОС.Организация В (&Организации)
			|	И ПринятиеКУчетуОС.Проведен
			|	И ПринятиеКУчетуОС.ОтражатьВРеглУчете
			|	И ПринятиеКУчетуОС.Дата МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
			|	И СтоимостьОС.ОсновноеСредство В
			|			(ВЫБРАТЬ
			|				СписокОС.ОсновноеСредство
			|			ИЗ
			|				СписокОС КАК СписокОС)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	МодернизацияОС2_4.Организация,
			|	СтоимостьОС.СтоимостьБУ,
			|	СтоимостьОС.ОсновноеСредство
			|ИЗ
			|	Документ.МодернизацияОС2_4 КАК МодернизацияОС2_4
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МодернизацияОС2_4.ОС КАК СтоимостьОС
			|		ПО (СтоимостьОС.Ссылка = МодернизацияОС2_4.Ссылка)
			|ГДЕ
			|	МодернизацияОС2_4.Организация В (&Организации)
			|	И МодернизацияОС2_4.Проведен
			|	И МодернизацияОС2_4.ОтражатьВРеглУчете
			|	И МодернизацияОС2_4.Дата МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
			|	И СтоимостьОС.ОсновноеСредство В
			|			(ВЫБРАТЬ
			|				СписокОС.ОсновноеСредство
			|			ИЗ
			|				СписокОС)
			|ИНДЕКСИРОВАТЬ ПО
			|	Организация,
			|	ОсновноеСредство
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СписокОС.Организация КАК Организация,
			|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
			|	СУММА(СписокОС.СтоимостьРегл) КАК СтоимостьРегл
			|ПОМЕСТИТЬ ВТ_ПредварительнаяСтоимостьОС
			|ИЗ
			|	(ВЫБРАТЬ
			|		СтоимостьОС.Организация КАК Организация,
			|		СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство,
			|		СтоимостьОС.СтоимостьРеглОстаток + СтоимостьОС.СтоимостьЦФОстаток КАК СтоимостьРегл
			|	ИЗ
			|		РегистрНакопления.СтоимостьОС.Остатки(
			|				&НачалоПредыдущегоМесяца,
			|				Организация В (&Организации)
			|					И ОсновноеСредство В
			|						(ВЫБРАТЬ
			|							СписокОС.ОсновноеСредство
			|						ИЗ
			|							СписокОС_РасчетПоПредварительнойСтоимости КАК СписокОС)) КАК СтоимостьОС
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		СтоимостьОС.Организация КАК Организация,
			|		СтоимостьОС.ОсновноеСредство,
			|		СтоимостьОС.СтоимостьРегл
			|	ИЗ
			|		СписокОС_РасчетПоПредварительнойСтоимости КАК СтоимостьОС
			|
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			// Уменьшение стоимости (здесь нет разукомплектации и объединения, т.к. их нельзя оформить в одном месяце с принятием к учету или модернизацией)
			|	ВЫБРАТЬ
			|		СтоимостьОС.Организация КАК Организация,
			|		СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство,
			|		-(СтоимостьОС.СтоимостьРегл + СтоимостьОС.СтоимостьЦФ) КАК СтоимостьРегл
			|	ИЗ
			|		РегистрНакопления.СтоимостьОС КАК СтоимостьОС
			|	ГДЕ
			|		СтоимостьОС.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
			|		И СтоимостьОС.Активность
			|		И СтоимостьОС.Организация В (&Организации)
			|		И СтоимостьОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
			|		И ТИПЗНАЧЕНИЯ(СтоимостьОС.Регистратор) В (ТИП(Документ.СписаниеОС2_4))
			|		И СтоимостьОС.ОсновноеСредство В
			|						(ВЫБРАТЬ
			|							СписокОС.ОсновноеСредство
			|						ИЗ
			|							СписокОС_РасчетПоПредварительнойСтоимости КАК СписокОС)
			|	
			|	) КАК СписокОС
			|
			|СГРУППИРОВАТЬ ПО
			|	СписокОС.Организация,
			|	СписокОС.ОсновноеСредство
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Организация,
			|	ОсновноеСредство
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СУММА(СтоимостьОС.СтоимостьРеглОстаток + СтоимостьОС.СтоимостьЦФОстаток) КАК СтоимостьРегл,
			|	СтоимостьОС.Организация КАК Организация,
			|	СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство
			|ПОМЕСТИТЬ ВТ_СтоимостьОС
			|ИЗ
			|	РегистрНакопления.СтоимостьОС.Остатки(
			|			&ПериодГраница,
			|			Организация В (&Организации)
			|				И ОсновноеСредство В
			|					(ВЫБРАТЬ
			|						СписокОС.ОсновноеСредство
			|					ИЗ
			|						СписокОС КАК СписокОС)) КАК СтоимостьОС
			|
			|СГРУППИРОВАТЬ ПО
			|	СтоимостьОС.Организация,
			|	СтоимостьОС.ОсновноеСредство
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЕСТЬNULL(ПредварительнаяСтоимостьОС.СтоимостьРегл, СтоимостьОС.СтоимостьРегл) КАК СуммаОстатокДт,
			|	СтоимостьОС.Организация КАК Организация,
			|	СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство,
			|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК Счет
			|ПОМЕСТИТЬ ВТ_ПервоначальнаяСтоимостьОС
			|ИЗ
			|	ВТ_СтоимостьОС КАК СтоимостьОС
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПредварительнаяСтоимостьОС КАК ПредварительнаяСтоимостьОС
			|		ПО ПредварительнаяСтоимостьОС.Организация = СтоимостьОС.Организация
			|			И ПредварительнаяСтоимостьОС.ОсновноеСредство = СтоимостьОС.ОсновноеСредство
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Организация,
			|	ОсновноеСредство
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СтоимостьОС.СтоимостьРеглОстаток + СтоимостьОС.СтоимостьЦФОстаток КАК СтоимостьРегл,
			|	СтоимостьОС.Организация КАК Организация,
			|	СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство
			|ПОМЕСТИТЬ ВТ_СтоимостьОСНаНачалоМесяца
			|ИЗ
			|	РегистрНакопления.СтоимостьОС.Остатки(
			|			&НачалоПредыдущегоМесяца,
			|			Организация В (&Организации)
			|				И ОсновноеСредство В
			|					(ВЫБРАТЬ
			|						СписокОС.ОсновноеСредство
			|					ИЗ
			|						СписокОС)) КАК СтоимостьОС
			|ИНДЕКСИРОВАТЬ ПО
			|	Организация,
			|	ОсновноеСредство
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СУММА(АмортизацияОС.СуммаОстатокДт) КАК СуммаОстатокДт,
			|	СУММА(АмортизацияОС.СуммаОстатокКт) КАК СуммаОстатокКт,
			|	АмортизацияОС.Организация КАК Организация,
			|	АмортизацияОС.ОсновноеСредство КАК ОсновноеСредство
			|ПОМЕСТИТЬ ВТ_АмортизацияОС_НаНачалоМесяца
			|ИЗ
			|	(ВЫБРАТЬ
			|		-(АмортизацияОС.АмортизацияРеглОстаток + АмортизацияОС.АмортизацияЦФОстаток) КАК СуммаОстатокДт,
			|		-(АмортизацияОС.АмортизацияРеглОстаток + АмортизацияОС.АмортизацияЦФОстаток) КАК СуммаОстатокКт,
			|		АмортизацияОС.Организация,
			|		АмортизацияОС.ОсновноеСредство КАК ОсновноеСредство
			|	ИЗ
			|		РегистрНакопления.АмортизацияОС.Остатки(
			|				&ПериодГраница,
			|				Организация В (&Организации)
			|					И ОсновноеСредство В
			|						(ВЫБРАТЬ
			|							СписокОС.ОсновноеСредство
			|						ИЗ
			|							СписокОС)) КАК АмортизацияОС
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ВЫБОР
			|			КОГДА АмортизацияОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
			|				ТОГДА -1
			|			ИНАЧЕ 1
			|		КОНЕЦ * (АмортизацияОС.АмортизацияРегл + АмортизацияОС.АмортизацияЦФ),
			|		ВЫБОР
			|			КОГДА АмортизацияОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
			|				ТОГДА -1
			|			ИНАЧЕ 1
			|		КОНЕЦ * (АмортизацияОС.АмортизацияРегл + АмортизацияОС.АмортизацияЦФ),
			|		АмортизацияОС.Организация,
			|		АмортизацияОС.ОсновноеСредство
			|	ИЗ
			|		РегистрНакопления.АмортизацияОС КАК АмортизацияОС
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокОС КАК СписокОС
			|		ПО СписокОС.ОсновноеСредство = АмортизацияОС.ОсновноеСредство
			|	ГДЕ
			|		АмортизацияОС.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
			|		И АмортизацияОС.ХозяйственнаяОперация В (
			|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСПолная),
			|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РазукомплектацияОСЧастичная),
			|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыделениеУзловКомпонентовАмортизации))
			|
			|	) КАК АмортизацияОС
			|
			|СГРУППИРОВАТЬ ПО
			|	АмортизацияОС.Организация,
			|	АмортизацияОС.ОсновноеСредство
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Организация,
			|	ОсновноеСредство
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	РазукомплектацияОС.ОсновноеСредство КАК ОсновноеСредствоИсходное,
			|	РазукомплектацияОС.Организация КАК Организация,
			|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) <> 0
			|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(АмортизацияОС.СуммаОстатокКт, 0) * ТаблицаОС.СтоимостьБУ / ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) КАК ЧИСЛО (31, 2))
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК Амортизация
			|ПОМЕСТИТЬ ВтВыделениеАмортизации
			|ИЗ
			|	Документ.РазукомплектацияОС КАК РазукомплектацияОС
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РазукомплектацияОС.ОС КАК ТаблицаОС
			|			ПО ТаблицаОС.Ссылка = РазукомплектацияОС.Ссылка
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокОС КАК СписокОС
			|			ПО СписокОС.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьОСНаНачалоМесяца КАК СтоимостьОС
			|		ПО СтоимостьОС.Организация = РазукомплектацияОС.Организация
			|			И СтоимостьОС.ОсновноеСредство = РазукомплектацияОС.ОсновноеСредство
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияОС_НаНачалоМесяца КАК АмортизацияОС
			|		ПО АмортизацияОС.Организация = РазукомплектацияОС.Организация
			|			И АмортизацияОС.ОсновноеСредство = РазукомплектацияОС.ОсновноеСредство
			|ГДЕ
			|	РазукомплектацияОС.Организация В (&Организации)
			|	И РазукомплектацияОС.Проведен
			|	И РазукомплектацияОС.ОтражатьВРеглУчете
			|	И РазукомплектацияОС.Дата МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
			|	И ТаблицаОС.СтоимостьБУ <> 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ОбесценениеВНАОстатки.Организация КАК Организация,
			|	ОбесценениеВНАОстатки.ОбесценениеРеглОстаток КАК СуммаОстатокКт,
			|	ОбесценениеВНАОстатки.ВнеоборотныйАктив КАК ОсновноеСредство,
			|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК Счет
			|ПОМЕСТИТЬ ВТ_ОбесценениеОС
			|ИЗ
			|	РегистрНакопления.ОбесценениеВНА.Остатки(
			|			&ПериодГраница,
			|			Организация В (&Организации)
			|				И ВнеоборотныйАктив В
			|					(ВЫБРАТЬ
			|						СписокОС.ОсновноеСредство
			|					ИЗ
			|						СписокОС)) КАК ОбесценениеВНАОстатки
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Организация,
			|	ОсновноеСредство
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СУММА(АмортизацияОС.СуммаОстатокДт) КАК СуммаОстатокДт,
			|	СУММА(АмортизацияОС.СуммаОстатокКт) КАК СуммаОстатокКт,
			|	АмортизацияОС.Организация КАК Организация,
			|	АмортизацияОС.ОсновноеСредство КАК ОсновноеСредство,
			|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК Счет
			|ПОМЕСТИТЬ ВТ_АмортизацияОС
			|ИЗ
			|	(ВЫБРАТЬ
			|		АмортизацияОС.СуммаОстатокДт КАК СуммаОстатокДт,
			|		АмортизацияОС.СуммаОстатокКт КАК СуммаОстатокКт,
			|		АмортизацияОС.Организация КАК Организация,
			|		АмортизацияОС.ОсновноеСредство КАК ОсновноеСредство
			|	ИЗ
			|		ВТ_АмортизацияОС_НаНачалоМесяца КАК АмортизацияОС
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			// Уменьшение амортизации исходного ОС при разукомплектации.
			|	ВЫБРАТЬ
			|		-АмортизацияОС.Амортизация,
			|		-АмортизацияОС.Амортизация,
			|		АмортизацияОС.Организация,
			|		АмортизацияОС.ОсновноеСредствоИсходное
			|	ИЗ
			|		ВтВыделениеАмортизации КАК АмортизацияОС
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			// Увеличение амортизации новых ОС при разукомплектации.
			|	ВЫБРАТЬ
			|		АмортизацияОС.Амортизация,
			|		АмортизацияОС.Амортизация,
			|		АмортизацияОС.Организация,
			|		АмортизацияОС.ОсновноеСредство
			|	ИЗ
			|		ВтВыделениеАмортизации КАК АмортизацияОС) КАК АмортизацияОС
			|
			|СГРУППИРОВАТЬ ПО
			|	АмортизацияОС.Организация,
			|	АмортизацияОС.ОсновноеСредство
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Организация,
			|	ОсновноеСредство";
			
		Иначе
			
			ТекстЗапроса =
			"ВЫБРАТЬ
			|	СтоимостьОС.СтоимостьРеглОстаток + СтоимостьОС.СтоимостьЦФОстаток  КАК СуммаОстатокДт,
			|	СтоимостьОС.Организация КАК Организация,
			|	СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство,
			|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК Счет
			|ПОМЕСТИТЬ ВТ_ПервоначальнаяСтоимостьОС
			|ИЗ
			|	РегистрНакопления.СтоимостьОС.Остатки(
			|			&ПериодГраница,
			|			Организация В (&Организации)
			|				И ОсновноеСредство В
			|					(ВЫБРАТЬ
			|						СписокОС.ОсновноеСредство
			|					ИЗ
			|						СписокОС)) КАК СтоимостьОС
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Организация,
			|	ОсновноеСредство
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ОбесценениеВНАОстатки.Организация КАК Организация,
			|	ОбесценениеВНАОстатки.ОбесценениеРеглОстаток КАК СуммаОстатокКт,
			|	ОбесценениеВНАОстатки.ВнеоборотныйАктив КАК ОсновноеСредство,
			|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК Счет
			|ПОМЕСТИТЬ ВТ_ОбесценениеОС
			|ИЗ
			|	РегистрНакопления.ОбесценениеВНА.Остатки(
			|			&ПериодГраница,
			|			Организация В (&Организации)
			|				И ВнеоборотныйАктив В
			|					(ВЫБРАТЬ
			|						СписокОС.ОсновноеСредство
			|					ИЗ
			|						СписокОС)) КАК ОбесценениеВНАОстатки
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Организация,
			|	ОсновноеСредство
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	-(АмортизацияОС.АмортизацияРеглОстаток + АмортизацияОС.АмортизацияЦФОстаток) КАК СуммаОстатокДт,
			|	-(АмортизацияОС.АмортизацияРеглОстаток + АмортизацияОС.АмортизацияЦФОстаток) КАК СуммаОстатокКт,
			|	АмортизацияОС.Организация КАК Организация,
			|	АмортизацияОС.ОсновноеСредство КАК ОсновноеСредство,
			|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК Счет
			|ПОМЕСТИТЬ ВТ_АмортизацияОС
			|ИЗ
			|	РегистрНакопления.АмортизацияОС.Остатки(
			|			&ПериодГраница,
			|			Организация В (&Организации)
			|				И ОсновноеСредство В
			|					(ВЫБРАТЬ
			|						СписокОС.ОсновноеСредство
			|					ИЗ
			|						СписокОС)) КАК АмортизацияОС
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Организация,
			|	ОсновноеСредство";
		
		КонецЕсли; 
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ПервоначальнаяСтоимостьОС.СуммаОстатокДт,
		|	ПервоначальнаяСтоимостьОС.Организация КАК Организация,
		|	ПервоначальнаяСтоимостьОС.Субконто1 КАК ОсновноеСредство,
		|	ПервоначальнаяСтоимостьОС.Счет
		|ПОМЕСТИТЬ ВТ_ПервоначальнаяСтоимостьОС
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(
		|			&ПериодГраница,
		|			Счет В
		|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					ВТ_СчетаБухгалтерскогоУчета.СчетУчета
		|				ИЗ
		|					ВТ_СчетаБухгалтерскогоУчета),
		|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
		|			Организация В (&Организации)) КАК ПервоначальнаяСтоимостьОС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
		|	0 КАК СуммаОстатокКт,
		|	ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка) КАК ОсновноеСредство,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК Счет
		|ПОМЕСТИТЬ ВТ_ОбесценениеОС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	АмортизацияОС.СуммаОстатокДт,
		|	АмортизацияОС.СуммаОстатокКт,
		|	АмортизацияОС.Организация КАК Организация,
		|	АмортизацияОС.Субконто1 КАК ОсновноеСредство,
		|	АмортизацияОС.Счет
		|ПОМЕСТИТЬ ВТ_АмортизацияОС
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(
		|			&ПериодГраница,
		|			Счет В
		|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					ВТ_СчетаБухгалтерскогоУчета.СчетНачисленияАмортизации
		|				ИЗ
		|					ВТ_СчетаБухгалтерскогоУчета),
		|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
		|			Организация В (&Организации)) КАК АмортизацияОС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	ОсновноеСредство";
		
	КонецЕсли; 
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Возвращает необходимость уплаты авансов по налогу на имущество.
//
// Параметры:
// 	Организации - СправочникСсылка.Организации - Организация, для которой необходимо выполнить проверку.
// 	Период - Дата - Дата закрытия месяца.
//
// Возвращаемое значение:
// 	Булево - Признак необходимости уплаты аванса.
//
Функция УплачиваютсяАвансыПоНалогуНаИмущество(Организации, Период) Экспорт
	
	УплачиваютсяАвансы = Ложь;
	
	Запрос = Новый Запрос;         
	
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.УстановитьПараметр("Период", Период);
	
	Запрос.Текст =                                     
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.НалоговыйОрган КАК НалоговыйОрган
	|ПОМЕСТИТЬ ВТ_НалоговыеОрганы
	|ИЗ
	|	(ВЫБРАТЬ
	|		СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация КАК Организация,
	|		СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговыйОрган КАК НалоговыйОрган
	|	ИЗ
	|		РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СрезПоследних(
	|				КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&Период, ГОД, -1), ГОД),
	|				Организация В (&Организации)
	|					И ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)) КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация,
	|		СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговыйОрган
	|	ИЗ
	|		РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Период, КВАРТАЛ),
	|				Организация В (&Организации)
	|					И ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)) КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация,
	|		СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговыйОрган
	|	ИЗ
	|		РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
	|	ГДЕ
	|		СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация В (&Организации)
	|		И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|		И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период МЕЖДУ НАЧАЛОПЕРИОДА(&Период, ГОД) И КОНЕЦПЕРИОДА(&Период, КВАРТАЛ)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Организации.Ссылка,
	|		Организации.РегистрацияВНалоговомОргане
	|	ИЗ
	|		Справочник.Организации КАК Организации
	|	ГДЕ
	|		Организации.Ссылка В (&Организации)) КАК ВложенныйЗапрос
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	НалоговыйОрган
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.Организация КАК Организация,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.УплачиваютсяАвансы КАК УплачиваютсяАвансы
	|ПОМЕСТИТЬ ВТ_УплачиваютсяАвансыПоОрганизации
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(
	|			&Период,
	|			Организация В (&Организации)
	|				И РегистрацияВНалоговомОргане = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|				И Налог = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.НалогНаИмущество)) КАК ПорядокУплатыНалоговНаМестахСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.Организация КАК Организация,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.УплачиваютсяАвансы КАК УплачиваютсяАвансы
	|ПОМЕСТИТЬ ВТ_УплачиваютсяАвансыПоИФНС
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(
	|			&Период,
	|			Организация В (&Организации)
	|				И РегистрацияВНалоговомОргане <> ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|				И Налог = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.НалогНаИмущество)) КАК ПорядокУплатыНалоговНаМестахСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(ВЫБОР
	|			КОГДА ВТ_УплачиваютсяАвансыПоИФНС.УплачиваютсяАвансы ЕСТЬ NULL 
	|				ТОГДА ВЫБОР
	|						КОГДА ВТ_УплачиваютсяАвансыПоОрганизации.УплачиваютсяАвансы ЕСТЬ NULL 
	|							ТОГДА ЛОЖЬ
	|						ИНАЧЕ ВТ_УплачиваютсяАвансыПоОрганизации.УплачиваютсяАвансы
	|					КОНЕЦ
	|			ИНАЧЕ ВТ_УплачиваютсяАвансыПоИФНС.УплачиваютсяАвансы
	|		КОНЕЦ), ЛОЖЬ) КАК УплачиваютсяАвансы
	|ИЗ
	|	ВТ_НалоговыеОрганы КАК ВТ_НалоговыеОрганы
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			ВТ_УплачиваютсяАвансыПоОрганизации КАК ВТ_УплачиваютсяАвансыПоОрганизации
	|		ПО 
	|			ВТ_НалоговыеОрганы.Организация = ВТ_УплачиваютсяАвансыПоОрганизации.Организация
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			ВТ_УплачиваютсяАвансыПоИФНС КАК ВТ_УплачиваютсяАвансыПоИФНС
	|		ПО 
	|			ВТ_НалоговыеОрганы.Организация = ВТ_УплачиваютсяАвансыПоИФНС.Организация
	|			И ВТ_НалоговыеОрганы.НалоговыйОрган = ВТ_УплачиваютсяАвансыПоИФНС.РегистрацияВНалоговомОргане";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		Пока Выборка.Следующий() Цикл
			УплачиваютсяАвансы = Выборка.УплачиваютсяАвансы;
		КонецЦикла;
		
	КонецЕсли;
		
	Возврат УплачиваютсяАвансы;
	
КонецФункции
 
#КонецОбласти

#Область РасчетТранспортногоНалога

Функция ПолучитьРасчетПоТранспортномуНалогу(Организация, ПериодРасчета, ДополнительныеПараметры) Экспорт
	
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "РегистрацияВНалоговомОргане");
	
	ГодовойРасчет = КонецКвартала(ПериодРасчета) = КонецГода(ПериодРасчета);
	
	ДатаНачалаПериодаОтчета = ?(ГодовойРасчет, НачалоГода(ПериодРасчета), НачалоКвартала(ПериодРасчета));
	ДатаКонцаПериодаОтчета = КонецКвартала(ПериодРасчета);
	
	ТаблицаРасчетТранспортногоНалога = РасчетИмущественныхНалогов.ПустаяСправкаРасчет("РасчетТранспортногоНалога");
	
	// Если для каких-то ТС в отчетном периоде отсутствуют ставки налога (не НалоговаяСтавка = 0, а именно отсутствует подходящая запись в регистре ставок),
	// то ниже отметим такие строки расчета.
	// Обработка таких ошибок выполняется вне данной процедуры.
	ТаблицаРасчетТранспортногоНалога.Колонки.Добавить("ОтсутствуетСтавка", Новый ОписаниеТипов("Булево"));
	
	ЭтоНеЮрЛицо = НЕ ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация);
	
	Если Месяц(ПериодРасчета)%3 <> 0 ИЛИ ЭтоНеЮрЛицо Тогда
		Возврат Новый Структура("Налог, Платон", ТаблицаРасчетТранспортногоНалога, Неопределено);
	КонецЕсли;

	НетТСЗарегистрированныхВРеестреСистемыПлатон = Истина;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПериодРасчета", ПериодРасчета);
	Запрос.УстановитьПараметр("ОсновнойНалоговыйОрган", РеквизитыОрганизации.РегистрацияВНалоговомОргане);
	
	ТекстОбъединяющегоЗапроса = "";
	
	Период = ДатаНачалаПериодаОтчета;
	
	Пока Период <= ДатаКонцаПериодаОтчета Цикл
		
		// С 2016 года, если регистрация ТС произошла до 15-го числа соответствующего месяца включительно
		// или снятие транспортного средства с регистрации (снятие с учета, исключение из государственного
		// судового реестра и так далее) произошло после 15-го числа соответствующего месяца, за полный 
		// месяц принимается месяц регистрации (снятия с регистрации) транспортного средства.
		
		Если Год(Период) < 2016 Тогда
			КонецПериода = КонецМесяца(Период);
			КонецПрошлогоПериода = НачалоМесяца(Период) - 1;
		Иначе
			КонецПериода = Дата(Год(Период), Месяц(Период), 15, 23, 59, 59);
			КонецПрошлогоПериода = КонецПериода;
		КонецЕсли;
		
		Запрос.УстановитьПараметр("КонецПрошлогоПериода", КонецПрошлогоПериода);
		Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Период));
		Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РегистрацияТранспортныхСредствСрезПоследних.Период КАК Период,
		|	РегистрацияТранспортныхСредствСрезПоследних.Организация КАК Организация,
		|	РегистрацияТранспортныхСредствСрезПоследних.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ ВТ_РегистрацииНаКонецПрошлогоПериода
		|ИЗ
		|	РегистрСведений.РегистрацияТранспортныхСредств.СрезПоследних(&КонецПрошлогоПериода, Организация = &Организация) КАК РегистрацияТранспортныхСредствСрезПоследних
		|ГДЕ
		|	РегистрацияТранспортныхСредствСрезПоследних.ВключатьВНалоговуюБазу
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РегистрацияТранспортныхСредств.Период КАК Период,
		|	РегистрацияТранспортныхСредств.Организация КАК Организация,
		|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ ВТ_РегистрацииВПервыйДеньМесяца
		|ИЗ
		|	РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
		|ГДЕ
		|	РегистрацияТранспортныхСредств.Организация = &Организация
		|	И РегистрацияТранспортныхСредств.Период = &НачалоМесяца
		|	И РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЕСТЬNULL(ВТ_РегистрацииВПервыйДеньМесяца.Период, ВТ_РегистрацииНаКонецПрошлогоПериода.Период) КАК Период,
		|	ВТ_РегистрацииНаКонецПрошлогоПериода.Организация,
		|	ВТ_РегистрацииНаКонецПрошлогоПериода.ОсновноеСредство
		|ПОМЕСТИТЬ ВТ_СписокОсновныхСредств
		|ИЗ
		|	ВТ_РегистрацииНаКонецПрошлогоПериода КАК ВТ_РегистрацииНаКонецПрошлогоПериода
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РегистрацииВПервыйДеньМесяца КАК ВТ_РегистрацииВПервыйДеньМесяца
		|		ПО ВТ_РегистрацииНаКонецПрошлогоПериода.Организация = ВТ_РегистрацииВПервыйДеньМесяца.Организация
		|			И ВТ_РегистрацииНаКонецПрошлогоПериода.ОсновноеСредство = ВТ_РегистрацииВПервыйДеньМесяца.ОсновноеСредство
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РегистрацияТранспортныхСредств.Период,
		|	РегистрацияТранспортныхСредств.Организация,
		|	РегистрацияТранспортныхСредств.ОсновноеСредство
		|ИЗ
		|	РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
		|ГДЕ
		|	РегистрацияТранспортныхСредств.Организация = &Организация
		|	И РегистрацияТранспортныхСредств.Период МЕЖДУ &НачалоМесяца И &КонецПериода
		|	И РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_РегистрацииНаКонецПрошлогоПериода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_РегистрацииВПервыйДеньМесяца
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МИНИМУМ(СписокОсновныхСредств.Период) КАК Период,
		|	СписокОсновныхСредств.Организация КАК Организация,
		|	СписокОсновныхСредств.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ ВТ_ПериодРегистрацииТранспортныхСредств
		|ИЗ
		|	ВТ_СписокОсновныхСредств КАК СписокОсновныхСредств
		|
		|СГРУППИРОВАТЬ ПО
		|	СписокОсновныхСредств.ОсновноеСредство,
		|	СписокОсновныхСредств.Организация
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период,
		|	Организация,
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВТ_ПериодРегистрацииТранспортныхСредств.Период КАК ДатаРегистрационныхДанных,
		|	ВТ_ПериодРегистрацииТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство,
		|	РегистрацияТранспортныхСредств.РегистрационныйЗнак КАК РегистрационныйЗнак,
		|	РегистрацияТранспортныхСредств.Марка КАК Марка,
		|	РегистрацияТранспортныхСредств.ИдентификационныйНомер КАК ИдентификационныйНомер,
		|	ВЫБОР
		|		КОГДА РегистрацияТранспортныхСредств.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА РегистрацияТранспортныхСредств.НалоговыйОрган
		|		ИНАЧЕ &ОсновнойНалоговыйОрган
		|	КОНЕЦ КАК РегистрацияВНалоговомОргане,
		|	ВЫБОР
		|		КОГДА РегистрацияТранспортныхСредств.НалоговыйОрган = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
		|			ТОГДА ЕСТЬNULL(РегистрацияТранспортныхСредств.Организация.РегистрацияВНалоговомОргане.КодПоОКТМО, """")
		|		ИНАЧЕ РегистрацияТранспортныхСредств.КодПоОКТМО
		|	КОНЕЦ КАК КодПоОКТМО,
		|	РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства КАК КодВидаТранспортногоСредства,
		|	РегистрацияТранспортныхСредств.НалоговаяБаза КАК НалоговаяБаза,
		|	РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы КАК ЕдиницаИзмеренияНалоговойБазы,
		|	РегистрацияТранспортныхСредств.НалоговаяСтавка КАК НалоговаяСтавка,
		|	РегистрацияТранспортныхСредств.СтавкаОпределяетсяАвтоматически КАК СтавкаОпределяетсяАвтоматически,
		|	РегистрацияТранспортныхСредств.НалоговаяЛьгота КАК НалоговаяЛьгота,
		|	РегистрацияТранспортныхСредств.КодНалоговойЛьготы КАК КодНалоговойЛьготы,
		|	РегистрацияТранспортныхСредств.РегиональныйКодЛьготы КАК РегиональныйКодЛьготы,
		|	РегистрацияТранспортныхСредств.ЛьготнаяСтавка КАК ЛьготнаяСтавка,
		|	РегистрацияТранспортныхСредств.ПроцентУменьшения КАК ПроцентУменьшения,
		|	РегистрацияТранспортныхСредств.СуммаУменьшения КАК СуммаУменьшения,
		|	РегистрацияТранспортныхСредств.ЭкологическийКласс КАК ЭкологическийКласс,
		|	ВЫБОР
		|		КОГДА РегистрацияТранспортныхСредств.ОбщаяСобственность
		|			ТОГДА РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК ДоляВПравеОбщейСобственностиЧислитель,
		|	ВЫБОР
		|		КОГДА РегистрацияТранспортныхСредств.ОбщаяСобственность
		|			ТОГДА РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК ДоляВПравеОбщейСобственностиЗнаменатель,
		|	РегистрацияТранспортныхСредств.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
		|	РегистрацияТранспортныхСредств.НалоговаяСтавкаЗависитОтГодаВыпускаТС КАК НалоговаяСтавкаЗависитОтГодаВыпускаТС,
		|	ВТ_ПериодРегистрацииТранспортныхСредств.ОсновноеСредство.ЗарегистрированоВРеестреСистемыПлатон КАК ЗарегистрированоВРеестреСистемыПлатон,
		|	1 КАК КоличествоМесяцев
		|ПОМЕСТИТЬ РегистрацияТранспортныхСредств
		|ИЗ
		|	ВТ_ПериодРегистрацииТранспортныхСредств КАК ВТ_ПериодРегистрацииТранспортныхСредств
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
		|		ПО ВТ_ПериодРегистрацииТранспортныхСредств.Период = РегистрацияТранспортныхСредств.Период
		|			И ВТ_ПериодРегистрацииТранспортныхСредств.Организация = РегистрацияТранспортныхСредств.Организация
		|			И ВТ_ПериодРегистрацииТранспортныхСредств.ОсновноеСредство = РегистрацияТранспортныхСредств.ОсновноеСредство";
		
		Подстрока = "ПОМЕСТИТЬ РегистрацияТранспортныхСредств";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, Подстрока, Подстрока + Формат(Период, "ДФ=MM"));
		
		Запрос.Выполнить();
		
		ТекстОбъединяющегоЗапроса = ТекстОбъединяющегоЗапроса
								  + ?(ПустаяСтрока(ТекстОбъединяющегоЗапроса), 
									  "ВЫБРАТЬ РАЗРЕШЕННЫЕ *
		                              |",
									  "ОБЪЕДИНИТЬ ВСЕ
									  |ВЫБРАТЬ *
		                              |")
								  + ?(ПустаяСтрока(ТекстОбъединяющегоЗапроса), 
									  "ПОМЕСТИТЬ РегистрацияТранспортныхСредств
									  |",
									  "")
								  + "ИЗ РегистрацияТранспортныхСредств" + Формат(Период, "ДФ=MM") + "
									|";
									
		Запрос.Текст = 
		"УНИЧТОЖИТЬ ВТ_СписокОсновныхСредств
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ПериодРегистрацииТранспортныхСредств";
		Запрос.Выполнить();
		
		Период = ДобавитьМесяц(Период, 1);
		
	КонецЦикла;
	
	Запрос.Текст = ТекстОбъединяющегоЗапроса;
	Запрос.Выполнить();
	
	СоздатьНалоговыеОрганыСУстановленнойУплатойАвансов(
		Запрос.МенеджерВременныхТаблиц, Организация, ПериодРасчета, "ТранспортныйНалог");
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегистрацияТранспортныхСредств.Период КАК Период,
	|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ ВТ_Регистрации
	|ИЗ
	|	РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
	|ГДЕ
	|	РегистрацияТранспортныхСредств.Организация = &Организация
	|	И РегистрацияТранспортныхСредств.Период <= &ПериодРасчета
	|	И РегистрацияТранспортныхСредств.ВидЗаписи = ЗНАЧЕНИЕ(Перечисление.ВидЗаписиОРегистрации.Регистрация)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрацияТранспортныхСредств.Период КАК Период,
	|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ ВТ_Снятия
	|ИЗ
	|	РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
	|ГДЕ
	|	РегистрацияТранспортныхСредств.Организация = &Организация
	|	И РегистрацияТранспортныхСредств.Период <= &ПериодРасчета
	|	И РегистрацияТранспортныхСредств.ВидЗаписи = ЗНАЧЕНИЕ(Перечисление.ВидЗаписиОРегистрации.СнятиеСРегистрационногоУчета)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Регистрации.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_Регистрации.Период КАК Период,
	|	МАКСИМУМ(ЕСТЬNULL(ВТ_Снятия.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК ПериодПредыдущегоСнятия
	|ПОМЕСТИТЬ ВТ_ПредыдущиеСнятия
	|ИЗ
	|	ВТ_Регистрации КАК ВТ_Регистрации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Снятия КАК ВТ_Снятия
	|		ПО ВТ_Регистрации.ОсновноеСредство = ВТ_Снятия.ОсновноеСредство
	|			И ВТ_Регистрации.Период > ВТ_Снятия.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Регистрации.ОсновноеСредство,
	|	ВТ_Регистрации.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПредыдущиеСнятия.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_ПредыдущиеСнятия.Период КАК Период,
	|	МИНИМУМ(ВТ_Регистрации.Период) КАК ПериодПервоначальнойРегистрации
	|ПОМЕСТИТЬ ВТ_ПервоначальныеРегистрации
	|ИЗ
	|	ВТ_ПредыдущиеСнятия КАК ВТ_ПредыдущиеСнятия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Регистрации КАК ВТ_Регистрации
	|		ПО ВТ_ПредыдущиеСнятия.ОсновноеСредство = ВТ_Регистрации.ОсновноеСредство
	|			И ВТ_ПредыдущиеСнятия.ПериодПредыдущегоСнятия < ВТ_Регистрации.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПредыдущиеСнятия.ОсновноеСредство,
	|	ВТ_ПредыдущиеСнятия.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Регистрации.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_Регистрации.Период КАК Период,
	|	МИНИМУМ(ЕСТЬNULL(ВТ_Снятия.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК ПериодСнятияСУчета
	|ПОМЕСТИТЬ ВТ_СнятияСУчета
	|ИЗ
	|	ВТ_Регистрации КАК ВТ_Регистрации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Снятия КАК ВТ_Снятия
	|		ПО ВТ_Регистрации.ОсновноеСредство = ВТ_Снятия.ОсновноеСредство
	|			И ВТ_Регистрации.Период < ВТ_Снятия.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Регистрации.ОсновноеСредство,
	|	ВТ_Регистрации.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Регистрации.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_Регистрации.Период КАК Период,
	|	МАКСИМУМ(ВТ_ПервоначальныеРегистрации.ПериодПервоначальнойРегистрации) КАК ПериодПервоначальнойРегистрации,
	|	МАКСИМУМ(ВТ_СнятияСУчета.ПериодСнятияСУчета) КАК ПериодСнятияСУчета
	|ПОМЕСТИТЬ ВТ_ДатыПостановкиИСнятия
	|ИЗ
	|	ВТ_Регистрации КАК ВТ_Регистрации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПервоначальныеРегистрации КАК ВТ_ПервоначальныеРегистрации
	|		ПО ВТ_Регистрации.ОсновноеСредство = ВТ_ПервоначальныеРегистрации.ОсновноеСредство
	|			И ВТ_Регистрации.Период = ВТ_ПервоначальныеРегистрации.Период
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СнятияСУчета КАК ВТ_СнятияСУчета
	|		ПО ВТ_Регистрации.ОсновноеСредство = ВТ_СнятияСУчета.ОсновноеСредство
	|			И ВТ_Регистрации.Период = ВТ_СнятияСУчета.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Регистрации.ОсновноеСредство,
	|	ВТ_Регистрации.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(РегистрацияТранспортныхСредств.КоличествоМесяцев) КАК КоличествоМесяцев,
	|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство,
	|	РегистрацияТранспортныхСредств.РегистрационныйЗнак КАК РегистрационныйЗнак,
	|	РегистрацияТранспортныхСредств.Марка КАК Марка,
	|	РегистрацияТранспортныхСредств.ИдентификационныйНомер КАК ИдентификационныйНомер,
	|	РегистрацияТранспортныхСредств.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	РегистрацияТранспортныхСредств.КодПоОКТМО КАК КодПоОКТМО,
	|	РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства КАК КодВидаТранспортногоСредства,
	|	РегистрацияТранспортныхСредств.НалоговаяБаза КАК НалоговаяБаза,
	|	РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы КАК ЕдиницаИзмеренияНалоговойБазы,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавка КАК НалоговаяСтавка,
	|	РегистрацияТранспортныхСредств.НалоговаяЛьгота КАК НалоговаяЛьгота,
	|	РегистрацияТранспортныхСредств.КодНалоговойЛьготы КАК КодНалоговойЛьготы,
	|	РегистрацияТранспортныхСредств.РегиональныйКодЛьготы КАК РегиональныйКодЛьготы,
	|	РегистрацияТранспортныхСредств.ЛьготнаяСтавка КАК ЛьготнаяСтавка,
	|	РегистрацияТранспортныхСредств.ПроцентУменьшения КАК ПроцентУменьшения,
	|	РегистрацияТранспортныхСредств.СуммаУменьшения КАК СуммаУменьшения,
	|	РегистрацияТранспортныхСредств.ЭкологическийКласс КАК ЭкологическийКласс,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РегистрацияТранспортныхСредств.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
	|	РегистрацияТранспортныхСредств.ЗарегистрированоВРеестреСистемыПлатон КАК ЗарегистрированоВРеестреСистемыПлатон,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавкаЗависитОтГодаВыпускаТС КАК НалоговаяСтавкаЗависитОтГодаВыпускаТС
	|ПОМЕСТИТЬ ВТ_РазличныеЗаписи
	|ИЗ
	|	РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрацияТранспортныхСредств.ОсновноеСредство,
	|	РегистрацияТранспортныхСредств.РегистрационныйЗнак,
	|	РегистрацияТранспортныхСредств.Марка,
	|	РегистрацияТранспортныхСредств.ИдентификационныйНомер,
	|	РегистрацияТранспортныхСредств.РегистрацияВНалоговомОргане,
	|	РегистрацияТранспортныхСредств.КодПоОКТМО,
	|	РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства,
	|	РегистрацияТранспортныхСредств.НалоговаяБаза,
	|	РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавка,
	|	РегистрацияТранспортныхСредств.НалоговаяЛьгота,
	|	РегистрацияТранспортныхСредств.КодНалоговойЛьготы,
	|	РегистрацияТранспортныхСредств.РегиональныйКодЛьготы,
	|	РегистрацияТранспортныхСредств.ЛьготнаяСтавка,
	|	РегистрацияТранспортныхСредств.ПроцентУменьшения,
	|	РегистрацияТранспортныхСредств.СуммаУменьшения,
	|	РегистрацияТранспортныхСредств.ЭкологическийКласс,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РегистрацияТранспортныхСредств.ПовышающийКоэффициент,
	|	РегистрацияТранспортныхСредств.ЗарегистрированоВРеестреСистемыПлатон,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавкаЗависитОтГодаВыпускаТС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТ_РазличныеЗаписи.КоличествоМесяцев) КАК КоличествоМесяцев,
	|	ВТ_РазличныеЗаписи.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_РазличныеЗаписи.РегистрационныйЗнак КАК РегистрационныйЗнак,
	|	ВТ_РазличныеЗаписи.Марка КАК Марка,
	|	ВТ_РазличныеЗаписи.ИдентификационныйНомер КАК ИдентификационныйНомер,
	|	ВТ_РазличныеЗаписи.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ВТ_РазличныеЗаписи.КодПоОКТМО КАК КодПоОКТМО,
	|	ВТ_РазличныеЗаписи.КодВидаТранспортногоСредства КАК КодВидаТранспортногоСредства,
	|	ВТ_РазличныеЗаписи.НалоговаяБаза КАК НалоговаяБаза,
	|	ВТ_РазличныеЗаписи.ЕдиницаИзмеренияНалоговойБазы КАК ЕдиницаИзмеренияНалоговойБазы,
	|	ВТ_РазличныеЗаписи.НалоговаяСтавка КАК НалоговаяСтавка,
	|	ВТ_РазличныеЗаписи.ЭкологическийКласс КАК ЭкологическийКласс,
	|	ВТ_РазличныеЗаписи.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	ВТ_РазличныеЗаписи.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	ВТ_РазличныеЗаписи.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
	|	ВТ_РазличныеЗаписи.ЗарегистрированоВРеестреСистемыПлатон КАК ЗарегистрированоВРеестреСистемыПлатон,
	|	ВТ_РазличныеЗаписи.НалоговаяСтавкаЗависитОтГодаВыпускаТС КАК НалоговаяСтавкаЗависитОтГодаВыпускаТС,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_РазличныеЗаписи.НалоговаяЛьгота <> ЗНАЧЕНИЕ(Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.НеПрименяется)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоЗаписейЛьгот
	|ПОМЕСТИТЬ ВТ_ЗаписиБезУчетаЛьгот
	|ИЗ
	|	ВТ_РазличныеЗаписи КАК ВТ_РазличныеЗаписи
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_РазличныеЗаписи.ОсновноеСредство,
	|	ВТ_РазличныеЗаписи.РегистрационныйЗнак,
	|	ВТ_РазличныеЗаписи.Марка,
	|	ВТ_РазличныеЗаписи.ИдентификационныйНомер,
	|	ВТ_РазличныеЗаписи.РегистрацияВНалоговомОргане,
	|	ВТ_РазличныеЗаписи.КодПоОКТМО,
	|	ВТ_РазличныеЗаписи.КодВидаТранспортногоСредства,
	|	ВТ_РазличныеЗаписи.НалоговаяБаза,
	|	ВТ_РазличныеЗаписи.ЕдиницаИзмеренияНалоговойБазы,
	|	ВТ_РазличныеЗаписи.НалоговаяСтавка,
	|	ВТ_РазличныеЗаписи.ЭкологическийКласс,
	|	ВТ_РазличныеЗаписи.ДоляВПравеОбщейСобственностиЧислитель,
	|	ВТ_РазличныеЗаписи.ДоляВПравеОбщейСобственностиЗнаменатель,
	|	ВТ_РазличныеЗаписи.ПовышающийКоэффициент,
	|	ВТ_РазличныеЗаписи.ЗарегистрированоВРеестреСистемыПлатон,
	|	ВТ_РазличныеЗаписи.НалоговаяСтавкаЗависитОтГодаВыпускаТС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(РегистрацияТранспортныхСредств.ДатаРегистрационныхДанных) КАК ДатаРегистрационныхДанных,
	|	СУММА(РегистрацияТранспортныхСредств.КоличествоМесяцев) КАК КоличествоМесяцевЛьготы,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_ЗаписиБезУчетаЛьгот.КоличествоМесяцев, 0) = 0
	|			ТОГДА СУММА(РегистрацияТранспортныхСредств.КоличествоМесяцев)
	|		ИНАЧЕ ВТ_ЗаписиБезУчетаЛьгот.КоличествоМесяцев
	|	КОНЕЦ КАК КоличествоМесяцевВладения,
	|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство,
	|	РегистрацияТранспортныхСредств.РегистрационныйЗнак КАК РегистрационныйЗнак,
	|	РегистрацияТранспортныхСредств.Марка КАК Марка,
	|	РегистрацияТранспортныхСредств.ИдентификационныйНомер КАК ИдентификационныйНомер,
	|	РегистрацияТранспортныхСредств.РегистрацияВНалоговомОргане КАК ИФНС,
	|	РегистрацияТранспортныхСредств.КодПоОКТМО КАК КодПоОКТМО,
	|	РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства КАК КодВидаТранспортногоСредства,
	|	РегистрацияТранспортныхСредств.НалоговаяБаза КАК НалоговаяБаза,
	|	РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы КАК ЕдиницаИзмерения,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавка КАК НалоговаяСтавка,
	|	РегистрацияТранспортныхСредств.СтавкаОпределяетсяАвтоматически КАК СтавкаОпределяетсяАвтоматически,
	|	РегистрацияТранспортныхСредств.НалоговаяЛьгота КАК НалоговаяЛьгота,
	|	РегистрацияТранспортныхСредств.КодНалоговойЛьготы КАК КодНалоговойЛьготы,
	|	РегистрацияТранспортныхСредств.РегиональныйКодЛьготы КАК РегиональныйКодЛьготы,
	|	РегистрацияТранспортныхСредств.ЛьготнаяСтавка КАК ЛьготнаяСтавка,
	|	РегистрацияТранспортныхСредств.ПроцентУменьшения КАК ПроцентУменьшения,
	|	РегистрацияТранспортныхСредств.СуммаУменьшения КАК СуммаУменьшения,
	|	РегистрацияТранспортныхСредств.ЭкологическийКласс КАК ЭкологическийКласс,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РегистрацияТранспортныхСредств.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
	|	РегистрацияТранспортныхСредств.ЗарегистрированоВРеестреСистемыПлатон КАК ЗарегистрированоВРеестреСистемыПлатон,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавкаЗависитОтГодаВыпускаТС КАК НалоговаяСтавкаЗависитОтГодаВыпускаТС,
	|	ЕСТЬNULL(ВТ_ЗаписиБезУчетаЛьгот.КоличествоЗаписейЛьгот, 0) КАК КоличествоЗаписейЛьгот,
	|	ВЫБОР
	|		КОГДА ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК УплачиваютсяАвансы,
	|	ОсновныеСредства.ДатаВыпуска КАК ДатаВыпуска,
	|	ВТ_ДатыПостановкиИСнятия.ПериодПервоначальнойРегистрации КАК ДатаРегистрации,
	|	ВТ_ДатыПостановкиИСнятия.ПериодСнятияСУчета КАК ДатаПрекращенияРегистрации
	|ИЗ
	|	РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗаписиБезУчетаЛьгот КАК ВТ_ЗаписиБезУчетаЛьгот
	|		ПО РегистрацияТранспортныхСредств.ОсновноеСредство = ВТ_ЗаписиБезУчетаЛьгот.ОсновноеСредство
	|			И РегистрацияТранспортныхСредств.РегистрационныйЗнак = ВТ_ЗаписиБезУчетаЛьгот.РегистрационныйЗнак
	|			И РегистрацияТранспортныхСредств.Марка = ВТ_ЗаписиБезУчетаЛьгот.Марка
	|			И РегистрацияТранспортныхСредств.ИдентификационныйНомер = ВТ_ЗаписиБезУчетаЛьгот.ИдентификационныйНомер
	|			И РегистрацияТранспортныхСредств.РегистрацияВНалоговомОргане = ВТ_ЗаписиБезУчетаЛьгот.РегистрацияВНалоговомОргане
	|			И РегистрацияТранспортныхСредств.КодПоОКТМО = ВТ_ЗаписиБезУчетаЛьгот.КодПоОКТМО
	|			И РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства = ВТ_ЗаписиБезУчетаЛьгот.КодВидаТранспортногоСредства
	|			И РегистрацияТранспортныхСредств.НалоговаяБаза = ВТ_ЗаписиБезУчетаЛьгот.НалоговаяБаза
	|			И РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы = ВТ_ЗаписиБезУчетаЛьгот.ЕдиницаИзмеренияНалоговойБазы
	|			И РегистрацияТранспортныхСредств.НалоговаяСтавка = ВТ_ЗаписиБезУчетаЛьгот.НалоговаяСтавка
	|			И РегистрацияТранспортныхСредств.ЭкологическийКласс = ВТ_ЗаписиБезУчетаЛьгот.ЭкологическийКласс
	|			И РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель = ВТ_ЗаписиБезУчетаЛьгот.ДоляВПравеОбщейСобственностиЧислитель
	|			И РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель = ВТ_ЗаписиБезУчетаЛьгот.ДоляВПравеОбщейСобственностиЗнаменатель
	|			И РегистрацияТранспортныхСредств.ПовышающийКоэффициент = ВТ_ЗаписиБезУчетаЛьгот.ПовышающийКоэффициент
	|			И РегистрацияТранспортныхСредств.НалоговаяСтавкаЗависитОтГодаВыпускаТС = ВТ_ЗаписиБезУчетаЛьгот.НалоговаяСтавкаЗависитОтГодаВыпускаТС
	|			И (ВТ_ЗаписиБезУчетаЛьгот.КоличествоЗаписейЛьгот = 1)
	|			И РегистрацияТранспортныхСредств.ЗарегистрированоВРеестреСистемыПлатон = ВТ_ЗаписиБезУчетаЛьгот.ЗарегистрированоВРеестреСистемыПлатон
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов КАК ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов
	|		ПО РегистрацияТранспортныхСредств.РегистрацияВНалоговомОргане = ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОсновныеСредства
	|		ПО РегистрацияТранспортныхСредств.ОсновноеСредство = ОсновныеСредства.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДатыПостановкиИСнятия КАК ВТ_ДатыПостановкиИСнятия
	|		ПО РегистрацияТранспортныхСредств.ОсновноеСредство = ВТ_ДатыПостановкиИСнятия.ОсновноеСредство
	|			И РегистрацияТранспортныхСредств.ДатаРегистрационныхДанных = ВТ_ДатыПостановкиИСнятия.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрацияТранспортныхСредств.ОсновноеСредство,
	|	РегистрацияТранспортныхСредств.РегистрационныйЗнак,
	|	РегистрацияТранспортныхСредств.Марка,
	|	РегистрацияТранспортныхСредств.ИдентификационныйНомер,
	|	РегистрацияТранспортныхСредств.РегистрацияВНалоговомОргане,
	|	РегистрацияТранспортныхСредств.КодПоОКТМО,
	|	РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства,
	|	РегистрацияТранспортныхСредств.НалоговаяБаза,
	|	РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавка,
	|	РегистрацияТранспортныхСредств.СтавкаОпределяетсяАвтоматически,
	|	РегистрацияТранспортныхСредств.НалоговаяЛьгота,
	|	РегистрацияТранспортныхСредств.КодНалоговойЛьготы,
	|	РегистрацияТранспортныхСредств.РегиональныйКодЛьготы,
	|	РегистрацияТранспортныхСредств.ЛьготнаяСтавка,
	|	РегистрацияТранспортныхСредств.ПроцентУменьшения,
	|	РегистрацияТранспортныхСредств.СуммаУменьшения,
	|	РегистрацияТранспортныхСредств.ЭкологическийКласс,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РегистрацияТранспортныхСредств.ПовышающийКоэффициент,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавкаЗависитОтГодаВыпускаТС,
	|	ВТ_ЗаписиБезУчетаЛьгот.КоличествоМесяцев,
	|	ВЫБОР
	|		КОГДА ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	ОсновныеСредства.ДатаВыпуска,
	|	ЕСТЬNULL(ВТ_ЗаписиБезУчетаЛьгот.КоличествоЗаписейЛьгот, 0),
	|	РегистрацияТранспортныхСредств.ЗарегистрированоВРеестреСистемыПлатон,
	|	ВТ_ДатыПостановкиИСнятия.ПериодПервоначальнойРегистрации,
	|	ВТ_ДатыПостановкиИСнятия.ПериодСнятияСУчета
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИФНС,
	|	КодПоОКТМО,
	|	ОсновноеСредство";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		РасчетТранспортногоНалога = Результат.Выгрузить();
		
		РасчетТранспортногоНалога.Колонки.Добавить("КоличествоЛетПрошедшихСГодаВыпускаТС", 
			ОбщегоНазначения.ОписаниеТипаЧисло(3, 0));
		
		ДополнитьРасчетТранспортногоНалога(
			РасчетТранспортногоНалога, ДатаКонцаПериодаОтчета);
		
		// Если применяется освобождение от налога, то в ТаблицаРасчетТранспортногоНалога должна быть служебная колонка СуммаОсвобожденияОтУплатыНалога
		ПрименяетсяОсвобождениеОтНалога = Ложь;
		РасчетСуммыОсвобожденияОтНалога = Ложь;
		Если ДополнительныеПараметры <> Неопределено Тогда
			ПрименяетсяОсвобождениеОтНалога = ГодовойРасчет
				И ДополнительныеПараметры.Свойство("ПрименяетсяОсвобождениеОтУплатыНалогаВНалогомПериоде")
				И ДополнительныеПараметры.ПрименяетсяОсвобождениеОтУплатыНалогаВНалогомПериоде;
			РасчетСуммыОсвобожденияОтНалога = ДополнительныеПараметры.Свойство("РасчетСуммыОсвобожденияОтНалога") 
				И ДополнительныеПараметры.РасчетСуммыОсвобожденияОтНалога;	// устанавливается при "вложенном" вызове расчета за период освобождения			
		КонецЕсли;
				
		Если ГодовойРасчет Тогда
			
			РасчетНалога1Кв = ТаблицаРасчетТранспортногоНалога.СкопироватьКолонки();
			РасчетНалога2Кв = ТаблицаРасчетТранспортногоНалога.СкопироватьКолонки();
			РасчетНалога3Кв = ТаблицаРасчетТранспортногоНалога.СкопироватьКолонки();
			
			Отбор = Новый Структура("ИФНС, КодПоОКТМО, ОсновноеСредство, ДатаРегистрационныхДанных");
			
			ЕстьОбъектыСУплатойАвансов = (РасчетТранспортногоНалога.Найти(Истина, "УплачиваютсяАвансы") <> Неопределено);
			ЕстьОбъектыБезУплатыАвансов = (РасчетТранспортногоНалога.Найти(Ложь, "УплачиваютсяАвансы") <> Неопределено);
			
			Если ЕстьОбъектыСУплатойАвансов Тогда
				РасчетНалога1Кв = РасчетИмущественныхНалогов.ПолучитьАвансовыйРасчетПоТранспортномуНалогу(Организация, Дата(Год(ПериодРасчета), 03, 31));
				РасчетНалога1Кв.Индексы.Добавить("ИФНС, КодПоОКТМО, ОсновноеСредство, ДатаРегистрационныхДанных");
				
				РасчетНалога2Кв = РасчетИмущественныхНалогов.ПолучитьАвансовыйРасчетПоТранспортномуНалогу(Организация, Дата(Год(ПериодРасчета), 06, 30));
				РасчетНалога2Кв.Индексы.Добавить("ИФНС, КодПоОКТМО, ОсновноеСредство, ДатаРегистрационныхДанных");
				
				РасчетНалога3Кв = РасчетИмущественныхНалогов.ПолучитьАвансовыйРасчетПоТранспортномуНалогу(Организация, Дата(Год(ПериодРасчета), 09, 30));
				РасчетНалога3Кв.Индексы.Добавить("ИФНС, КодПоОКТМО, ОсновноеСредство, ДатаРегистрационныхДанных");
			КонецЕсли;
			
			// Если в каких-то регионах (ИФНС) не уплачиваются авансы, то для расчета налога к уплате за год нужны данные 2-го квартала.
			// Для этого из годового расчета вызываем "вложенный" расчет за 2-й квартал.
			// Взять готовые данные из регистра сведений нельзя, т.к. если авансы не уплачиваются, то и расчета за 2-й квартал нет.
			// ТаблицаРасчетТранспортногоНалога и РасчетНалога2Кв имеют одинаковый состав основных колонок, т.к. созданы по структуре регистра сведений. 
			Если ЕстьОбъектыБезУплатыАвансов И ПрименяетсяОсвобождениеОтНалога Тогда
				ТаблицыРасчетаНалогаКОсвобождению = ПолучитьРасчетПоТранспортномуНалогу(
					Организация,
					Дата(Год(ПериодРасчета), 06, 30),
					Новый Структура("РасчетСуммыОсвобожденияОтНалога", Истина));
				РасчетНалогаКОсвобождению = ТаблицыРасчетаНалогаКОсвобождению.Налог;	
				РасчетНалогаКОсвобождению.Индексы.Добавить("ИФНС, КодПоОКТМО, ОсновноеСредство, ДатаРегистрационныхДанных");
			КонецЕсли;

		КонецЕсли;
		
		Для Каждого СтрокаРасчета Из РасчетТранспортногоНалога Цикл
			
			// Если расчет вызван для определения суммы освобождения от налога, то рассчитывать нужно только для тех ИФНС, 
			// где не уплачиваются авансы. В ином случае расчет будет взят из регистра без необходимости расчета.
			Если НЕ ГодовойРасчет И НЕ СтрокаРасчета.УплачиваютсяАвансы И НЕ РасчетСуммыОсвобожденияОтНалога Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрокаРасчета.УплачиваютсяАвансы И РасчетСуммыОсвобожденияОтНалога Тогда
				Продолжить;
			КонецЕсли;
			
			Если НетТСЗарегистрированныхВРеестреСистемыПлатон И СтрокаРасчета.ЗарегистрированоВРеестреСистемыПлатон Тогда
				НетТСЗарегистрированныхВРеестреСистемыПлатон = Ложь;
			КонецЕсли;
			
			// Для расчета используется та строка, в которой содержится информация о льготе.
			Если СтрокаРасчета.КоличествоЗаписейЛьгот = 1
			   И СтрокаРасчета.НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.НеПрименяется Тогда
				Продолжить;
			КонецЕсли;
			
			Если Год(ДатаКонцаПериодаОтчета) > 2015 
			   И НачалоМесяца(СтрокаРасчета.ДатаРегистрации) = НачалоМесяца(СтрокаРасчета.ДатаПрекращенияРегистрации)
			   И ((День(СтрокаРасчета.ДатаРегистрации) <= 15 И День(СтрокаРасчета.ДатаПрекращенияРегистрации) <= 15)
			       ИЛИ (День(СтрокаРасчета.ДатаРегистрации) > 15 И День(СтрокаРасчета.ДатаПрекращенияРегистрации) > 15)) Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаРасчетТранспортногоНалога.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасчета, ,
				"КодПоОКТМО, КоличествоМесяцевЛьготы,КодНалоговойЛьготы,РегиональныйКодЛьготы,
				|ПроцентУменьшения,ЛьготнаяСтавка,КоличествоЛетПрошедшихСГодаВыпускаТС");
			
			НоваяСтрока.КодПоОКТМО = СокрЛП(СтрокаРасчета.КодПоОКТМО);
			
			Если СтрокаРасчета.НалоговаяСтавкаЗависитОтГодаВыпускаТС Тогда
				НоваяСтрока.КоличествоЛетПрошедшихСГодаВыпускаТС = СтрокаРасчета.КоличествоЛетПрошедшихСГодаВыпускаТС;
			КонецЕсли;
			
			КоличествоМесяцевВПериоде = ?(ГодовойРасчет, 12, 3);
			Коэффициент = Окр(НоваяСтрока.КоличествоМесяцевВладения / КоличествоМесяцевВПериоде, 4);
			
			Если Год(ДатаКонцаПериодаОтчета) > 2014 ИЛИ (ГодовойРасчет И Год(ДатаКонцаПериодаОтчета) = 2014) Тогда
				
				Если НоваяСтрока.ДоляВПравеОбщейСобственностиЗнаменатель = 0 
					ИЛИ НоваяСтрока.ДоляВПравеОбщейСобственностиЧислитель = 0 Тогда
					
					ДоляВПравеОбщейСобственности = 1;
					
				Иначе
					
					ДоляВПравеОбщейСобственности = НоваяСтрока.ДоляВПравеОбщейСобственностиЧислитель 
						/ НоваяСтрока.ДоляВПравеОбщейСобственностиЗнаменатель;
					
				КонецЕсли;
					
			Иначе
				
				НоваяСтрока.ДоляВПравеОбщейСобственностиЗнаменатель = 0;
				НоваяСтрока.ДоляВПравеОбщейСобственностиЧислитель = 0;
				ДоляВПравеОбщейСобственности = 1;
				
			КонецЕсли;
			
			Если СтрокаРасчета.ПовышающийКоэффициент <> 0
				И (Год(ДатаКонцаПериодаОтчета) >= 2015 ИЛИ (ГодовойРасчет И Год(ДатаКонцаПериодаОтчета) = 2014)) Тогда
				
				ПовышающийКоэффициент = СтрокаРасчета.ПовышающийКоэффициент;
				
			Иначе
				
				ПовышающийКоэффициент = 1;
				НоваяСтрока.ПовышающийКоэффициент = 0;
				
			КонецЕсли;
			
			НоваяСтрока.СуммаНалога = Окр(Окр(НоваяСтрока.НалоговаяБаза 
				* НоваяСтрока.НалоговаяСтавка 
				* ДоляВПравеОбщейСобственности 
				* Коэффициент
				* ПовышающийКоэффициент
				* ?(ГодовойРасчет, 1, 1 / 4), 2));
			
			НалоговаяЛьгота = СтрокаРасчета.НалоговаяЛьгота;
			
			Если НалоговаяЛьгота <> Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.НеПрименяется
				И НалоговаяЛьгота <> Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму Тогда 
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасчета,
					"КоличествоМесяцевЛьготы,КодНалоговойЛьготы,РегиональныйКодЛьготы,ПроцентУменьшения,ЛьготнаяСтавка");
				
				КоэффициентЛьготы = Окр(НоваяСтрока.КоличествоМесяцевЛьготы / 12, 4);
					
				Если НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения Тогда 
					
					НоваяСтрока.СуммаНалоговойЛьготы = 
						Окр(Окр(НоваяСтрока.НалоговаяБаза 
							* НоваяСтрока.НалоговаяСтавка 
							* КоэффициентЛьготы
							* ДоляВПравеОбщейСобственности
							* ПовышающийКоэффициент, 2));
					
				ИначеЕсли НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах Тогда 
					
					НоваяСтрока.СуммаНалоговойЛьготы = 
						Окр(Окр(НоваяСтрока.НалоговаяБаза 
							* НоваяСтрока.НалоговаяСтавка 
							* КоэффициентЛьготы 
							* (НоваяСтрока.ПроцентУменьшения / 100)
							* ДоляВПравеОбщейСобственности
							* ПовышающийКоэффициент, 2));
					
				ИначеЕсли НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки Тогда 
					
					НоваяСтрока.СуммаНалоговойЛьготы = 
						Окр(Окр(НоваяСтрока.НалоговаяБаза 
						* (НоваяСтрока.НалоговаяСтавка - НоваяСтрока.ЛьготнаяСтавка)
						* КоэффициентЛьготы
						* ДоляВПравеОбщейСобственности
						* ПовышающийКоэффициент, 2));

				КонецЕсли;
				
				
			КонецЕсли;
			
			НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога - НоваяСтрока.СуммаНалоговойЛьготы;
			
			Если ГодовойРасчет И СтрокаРасчета.УплачиваютсяАвансы Тогда
				
				ЗаполнитьЗначенияСвойств(Отбор, НоваяСтрока);
				
				СуммаНалоговогоВычета1Кв = 0;
				НоваяСтрока.СуммаАвансовыхПлатежей1Кв = 0;
				СтрокиРасчетаАвансовыхПлатежей = РасчетНалога1Кв.НайтиСтроки(Отбор);
				Для Каждого СтрокаРасчетаАвансовыхПлатежей Из СтрокиРасчетаАвансовыхПлатежей Цикл
					НоваяСтрока.СуммаАвансовыхПлатежей1Кв = НоваяСтрока.СуммаАвансовыхПлатежей1Кв
						+ СтрокаРасчетаАвансовыхПлатежей.СуммаНалогаКУплате;
					СуммаНалоговогоВычета1Кв = СуммаНалоговогоВычета1Кв + СтрокаРасчетаАвансовыхПлатежей.СуммаНалоговогоВычета;
				КонецЦикла;
				
				СуммаНалоговогоВычета2Кв = 0;
				НоваяСтрока.СуммаАвансовыхПлатежей2Кв = 0;
				СтрокиРасчетаАвансовыхПлатежей = РасчетНалога2Кв.НайтиСтроки(Отбор);
				Для Каждого СтрокаРасчетаАвансовыхПлатежей Из СтрокиРасчетаАвансовыхПлатежей Цикл
					НоваяСтрока.СуммаАвансовыхПлатежей2Кв = НоваяСтрока.СуммаАвансовыхПлатежей2Кв
						+ СтрокаРасчетаАвансовыхПлатежей.СуммаНалогаКУплате;
					СуммаНалоговогоВычета2Кв = СуммаНалоговогоВычета2Кв + СтрокаРасчетаАвансовыхПлатежей.СуммаНалоговогоВычета;
				КонецЦикла;
				
				СуммаНалоговогоВычета3Кв = 0;
				НоваяСтрока.СуммаАвансовыхПлатежей3Кв = 0;
				СтрокиРасчетаАвансовыхПлатежей = РасчетНалога3Кв.НайтиСтроки(Отбор);
				Для Каждого СтрокаРасчетаАвансовыхПлатежей Из СтрокиРасчетаАвансовыхПлатежей Цикл
					НоваяСтрока.СуммаАвансовыхПлатежей3Кв = НоваяСтрока.СуммаАвансовыхПлатежей3Кв
						+ СтрокаРасчетаАвансовыхПлатежей.СуммаНалогаКУплате;
					СуммаНалоговогоВычета3Кв = СуммаНалоговогоВычета3Кв + СтрокаРасчетаАвансовыхПлатежей.СуммаНалоговогоВычета;
				КонецЦикла;
				
				НоваяСтрока.СуммаНалоговогоВычета = СуммаНалоговогоВычета1Кв + СуммаНалоговогоВычета2Кв + СуммаНалоговогоВычета3Кв;
				
				НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалогаКУплате - НоваяСтрока.СуммаАвансовыхПлатежей1Кв
					- НоваяСтрока.СуммаАвансовыхПлатежей2Кв - НоваяСтрока.СуммаАвансовыхПлатежей3Кв;
				
			КонецЕсли;

			Если ПрименяетсяОсвобождениеОтНалога И НЕ СтрокаРасчета.УплачиваютсяАвансы Тогда
				
				ЗаполнитьЗначенияСвойств(Отбор, НоваяСтрока);
				СтрокиРасчетаНалогаКОсвобождению = РасчетНалогаКОсвобождению.НайтиСтроки(Отбор);
				Для Каждого СтрокаРасчетаНалогаКОсвобождению Из СтрокиРасчетаНалогаКОсвобождению Цикл
					НоваяСтрока.СуммаОсвобожденияОтУплатыНалога = НоваяСтрока.СуммаОсвобожденияОтУплатыНалога
						+ СтрокаРасчетаНалогаКОсвобождению.СуммаНалогаКУплате;
				КонецЦикла;
				
			КонецЕсли;	
			
			Если НЕ ГодовойРасчет И СтрокаРасчета.УплачиваютсяАвансы Тогда
				НоваяСтрока["СуммаАвансовыхПлатежей" + Формат(ПериодРасчета, "ДФ='q''Кв'''")] = НоваяСтрока.СуммаНалогаКУплате;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ТаблицаРасчетТранспортногоНалога.ЗаполнитьЗначения(ДатаКонцаПериодаОтчета, "ПериодРасчета");
	ТаблицаРасчетТранспортногоНалога.ЗаполнитьЗначения(Организация, "Организация");

	// Учет платы в систему "Платон".
	// Вычет действует в 2016 - 2018 годах.
	Если НетТСЗарегистрированныхВРеестреСистемыПлатон ИЛИ Год(ПериодРасчета) < 2016 ИЛИ Год(ПериодРасчета) > 2018 Тогда
		Возврат Новый Структура("Налог, Платон", ТаблицаРасчетТранспортногоНалога, Неопределено);
	КонецЕсли;
	
	РасходыПлатон = ПолучитьРасходыНаПлатон(
						Организация, 
						ПериодРасчета, 
						ТаблицаРасчетТранспортногоНалога);
						
	Если ПериодРасчета < ВнеоборотныеАктивыЛокализация.ДатаПрекращенияДействияЛьготПлатон() Тогда
		РасчетИмущественныхНалогов.УчестьРасходыНаПлатонВРасчетеТранспортногоНалога(РасходыПлатон, ТаблицаРасчетТранспортногоНалога);
	КонецЕсли; 
	
	СтрокиРасчетаНалога = ТаблицаРасчетТранспортногоНалога.НайтиСтроки(
		Новый Структура("ЗарегистрированоВРеестреСистемыПлатон", Истина));
	
	Для Каждого СтрокаРасчета Из СтрокиРасчетаНалога Цикл
		
		Если ГодовойРасчет Тогда
			СтрокаРасчета.СуммаНалогаКУплате = СтрокаРасчета.СуммаНалогаКУплате - СтрокаРасчета.СуммаНалоговогоВычета;
		Иначе
			СтрокаРасчета["СуммаАвансовыхПлатежей" + Формат(ПериодРасчета, "ДФ='q''Кв'''")] = 0;
			СтрокаРасчета.СуммаНалогаКУплате = 0;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Новый Структура("Налог, Платон", ТаблицаРасчетТранспортногоНалога, РасходыПлатон);

КонецФункции

Функция ПолучитьРасходыНаПлатон(Организация, ПериодРасчета, ТаблицаРасчетТранспортногоНалога) Экспорт

	ГодовойРасчет = КонецКвартала(ПериодРасчета) = КонецГода(ПериодРасчета);
	ДатаКонцаПериодаОтчета = КонецКвартала(ПериодРасчета);
	
	СтруктураПоиска = Новый Структура("УплачиваютсяАвансы", Ложь);
	СписокОСБезАвансов = ТаблицаРасчетТранспортногоНалога.Скопировать(СтруктураПоиска).ВыгрузитьКолонку("ОсновноеСредство");
	УплачиваютсяАвансы = СписокОСБезАвансов.Количество() = 0;
	
	СписокЗапросов = Новый Массив;
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(РасчетыСПоставщиками.Период, КВАРТАЛ) КАК Период,
		|	ВЫБОР
		|		КОГДА РасчетыСПоставщиками.РасчетныйДокумент ССЫЛКА Документ.ОтчетОператораСистемыПлатон
		|			ТОГДА РасчетыСПоставщиками.РасчетныйДокумент
		|		ИНАЧЕ РасчетыСПоставщиками.ДокументРегистратор
		|	КОНЕЦ КАК ОтчетОператора,
		|	СУММА(РасчетыСПоставщиками.ДолгРегл) КАК СуммаПлатежа,
		|	СУММА(ВЫБОР
		|			КОГДА РасчетыСПоставщиками.Период МЕЖДУ &НачалоКвартала И &КонецКвартала
		|				ТОГДА РасчетыСПоставщиками.ДолгРегл
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаПлатежаЗаКвартал
		|ПОМЕСТИТЬ ФактическиеПлатежи
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК РасчетыСПоставщиками
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОператораСистемыПлатон КАК ОтчетОператораСистемыПлатон
		|		ПО ОтчетОператораСистемыПлатон.Ссылка = РасчетыСПоставщиками.РасчетныйДокумент
		|			ИЛИ ОтчетОператораСистемыПлатон.Ссылка = РасчетыСПоставщиками.ДокументРегистратор
		|ГДЕ
		|	РасчетыСПоставщиками.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И (ОтчетОператораСистемыПлатон.Дата < &ДатаПрекращенияДействияЛьготПлатон
		|		ИЛИ &ПериодРасчета < &ДатаПрекращенияДействияЛьготПлатон)
		|	И РасчетыСПоставщиками.АналитикаУчетаПоПартнерам.Организация = &Организация
		|	И РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И РасчетыСПоставщиками.ХозяйственнаяОперация В (
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетАвансаПоставщику),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПогашениеЗадолженностиПоставщику))
		|	И РасчетыСПоставщиками.Активность
		|	И (РасчетыСПоставщиками.РасчетныйДокумент ССЫЛКА Документ.ОтчетОператораСистемыПлатон
		|			ИЛИ РасчетыСПоставщиками.ДокументРегистратор ССЫЛКА Документ.ОтчетОператораСистемыПлатон)
		|
		|СГРУППИРОВАТЬ ПО
		|	НАЧАЛОПЕРИОДА(РасчетыСПоставщиками.Период, КВАРТАЛ),
		|	ВЫБОР
		|		КОГДА РасчетыСПоставщиками.РасчетныйДокумент ССЫЛКА Документ.ОтчетОператораСистемыПлатон
		|			ТОГДА РасчетыСПоставщиками.РасчетныйДокумент
		|		ИНАЧЕ РасчетыСПоставщиками.ДокументРегистратор
		|	КОНЕЦ
		|
		|ИМЕЮЩИЕ
		|	СУММА(РасчетыСПоставщиками.ДолгРегл) > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОтчетОператора";
		
		СписокЗапросов.Добавить(ТекстЗапроса);
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(РасчетыСПоставщиками.Период, КВАРТАЛ) КАК Период,
		|	ВЫБОР
		|		КОГДА РасчетыСПоставщиками.РасчетныйДокумент ССЫЛКА Документ.ОтчетОператораСистемыПлатон
		|			ТОГДА РасчетыСПоставщиками.РасчетныйДокумент
		|		ИНАЧЕ РасчетыСПоставщиками.Регистратор
		|	КОНЕЦ КАК ОтчетОператора,
		|	СУММА(ВЫБОР
		|			КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА РасчетыСПоставщиками.ДолгРегл
		|			ИНАЧЕ 0
		|		КОНЕЦ + ВЫБОР
		|			КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				ТОГДА РасчетыСПоставщиками.ПредоплатаРегл
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаПлатежа,
		|	СУММА(ВЫБОР
		|			КОГДА РасчетыСПоставщиками.Период < &НачалоКвартала 
		|					ИЛИ РасчетыСПоставщиками.Период > &КонецКвартала
		|				ТОГДА 0
		|			КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА РасчетыСПоставщиками.ДолгРегл
		|			ИНАЧЕ 0
		|		КОНЕЦ + ВЫБОР
		|			КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				ТОГДА РасчетыСПоставщиками.ПредоплатаРегл
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаПлатежаЗаКвартал
		|ПОМЕСТИТЬ ФактическиеПлатежи
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК РасчетыСПоставщиками
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОператораСистемыПлатон КАК ОтчетОператораСистемыПлатон
		|		ПО ОтчетОператораСистемыПлатон.Ссылка = РасчетыСПоставщиками.РасчетныйДокумент
		|			ИЛИ ОтчетОператораСистемыПлатон.Ссылка = РасчетыСПоставщиками.Регистратор
		|ГДЕ
		|	РасчетыСПоставщиками.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И (ОтчетОператораСистемыПлатон.Дата < &ДатаПрекращенияДействияЛьготПлатон
		|		ИЛИ &ПериодРасчета < &ДатаПрекращенияДействияЛьготПлатон)
		|	И РасчетыСПоставщиками.АналитикаУчетаПоПартнерам.Организация = &Организация
		|	И РасчетыСПоставщиками.ХозяйственнаяОперация В (
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика), 
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаПоставщику),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности))
		|	И РасчетыСПоставщиками.Активность
		|	И (РасчетыСПоставщиками.РасчетныйДокумент ССЫЛКА Документ.ОтчетОператораСистемыПлатон
		|			ИЛИ РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.ОтчетОператораСистемыПлатон)
		|
		|СГРУППИРОВАТЬ ПО
		|	НАЧАЛОПЕРИОДА(РасчетыСПоставщиками.Период, КВАРТАЛ),
		|	ВЫБОР
		|		КОГДА РасчетыСПоставщиками.РасчетныйДокумент ССЫЛКА Документ.ОтчетОператораСистемыПлатон
		|			ТОГДА РасчетыСПоставщиками.РасчетныйДокумент
		|		ИНАЧЕ РасчетыСПоставщиками.Регистратор
		|	КОНЕЦ
		|
		|ИМЕЮЩИЕ
		|	СУММА(ВЫБОР
		|			КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА РасчетыСПоставщиками.ДолгРегл
		|			ИНАЧЕ 0
		|		КОНЕЦ + ВЫБОР
		|			КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				ТОГДА РасчетыСПоставщиками.ПредоплатаРегл
		|			ИНАЧЕ 0
		|		КОНЕЦ) > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОтчетОператора";
		
		СписокЗапросов.Добавить(ТекстЗапроса);
		
	КонецЕсли; 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РасчетТранспортногоНалога.ПериодРасчета КАК Период,
	|	РасчетТранспортногоНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	СУММА(РасчетТранспортногоНалога.СуммаПлатыОператоруСистемыПлатон) КАК СуммаПлатежа
	|ИЗ
	|	РегистрСведений.РасчетТранспортногоНалога КАК РасчетТранспортногоНалога
	|ГДЕ
	|	РасчетТранспортногоНалога.ПериодРасчета >= &НачалоПериода
	|	И РасчетТранспортногоНалога.ПериодРасчета < &НачалоКвартала
	|	И РасчетТранспортногоНалога.Организация = &Организация
	|	И РасчетТранспортногоНалога.Активность
	|	И &ГодовойРасчет
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетТранспортногоНалога.ПериодРасчета,
	|	РасчетТранспортногоНалога.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФактическиеПлатежи.Период                 КАК Период,
	|	ФактическиеПлатежи.ОтчетОператора         КАК ОтчетОператора,
	|	ФактическиеПлатежи.СуммаПлатежа           КАК СуммаПлатежа,
	|	ФактическиеПлатежи.СуммаПлатежаЗаКвартал  КАК СуммаПлатежаЗаКвартал
	|ИЗ
	|	ФактическиеПлатежи КАК ФактическиеПлатежи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Операция.Ссылка                           КАК ОтчетОператора,
	|	РасходыНаПлатон.ОсновноеСредство          КАК ОсновноеСредство,
	|	РасходыНаПлатон.Подразделение             КАК Подразделение,
	|	РасходыНаПлатон.СтатьяРасходов            КАК СтатьяРасходов,
	|	РасходыНаПлатон.АналитикаРасходов         КАК АналитикаРасходов,
	|	РасходыНаПлатон.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|	0                                         КАК СуммаНУДт,
	|	0                                         КАК СуммаНУКт,
	|	РасходыНаПлатон.Сумма                     КАК База,
	|	ВЫБОР 
	|		КОГДА РасходыНаПлатон.ОсновноеСредство В (&СписокОСБезАвансов)
	|			ТОГДА ЛОЖЬ 
	|		ИНАЧЕ ИСТИНА 
	|	КОНЕЦ КАК УплачиваютсяАвансы
	|ИЗ
	|	Документ.ОтчетОператораСистемыПлатон КАК Операция
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОператораСистемыПлатон.ОС КАК РасходыНаПлатон
	|		ПО РасходыНаПлатон.Ссылка = Операция.Ссылка
	|ГДЕ
	|	Операция.Проведен
	|	И Операция.Ссылка В
	|			(ВЫБРАТЬ
	|				ФактическиеПлатежи.ОтчетОператора
	|			ИЗ
	|				ФактическиеПлатежи КАК ФактическиеПлатежи)";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Организация",    Организация);
	Запрос.УстановитьПараметр("СледующийМесяц", ДатаКонцаПериодаОтчета + 1);
	Запрос.УстановитьПараметр("ПериодРасчета",  ПериодРасчета);
	Запрос.УстановитьПараметр("ГодовойРасчет",  ГодовойРасчет);
	Запрос.УстановитьПараметр("НачалоКвартала", НачалоКвартала(ПериодРасчета));
	Запрос.УстановитьПараметр("КонецКвартала",  КонецКвартала(ПериодРасчета));
	Запрос.УстановитьПараметр("НачалоПериода",  ?(НЕ УплачиваютсяАвансы И ГодовойРасчет, НачалоГода(ПериодРасчета), НачалоКвартала(ПериодРасчета)));
	Запрос.УстановитьПараметр("КонецПериода",   ?(НЕ УплачиваютсяАвансы И ГодовойРасчет, КонецГода(ПериодРасчета), КонецКвартала(ПериодРасчета)));
	Запрос.УстановитьПараметр("ДатаПрекращенияДействияЛьготПлатон", ВнеоборотныеАктивыЛокализация.ДатаПрекращенияДействияЛьготПлатон());
	Запрос.УстановитьПараметр("СписокОСБезАвансов", СписокОСБезАвансов);
	
	Результат = Запрос.ВыполнитьПакет();

	ТаблицаРасходыНаПлатон = ПустаяТаблицаРасходыНаПлатон();
	
	Если Не Результат[Результат.ВГраница()].Пустой() Тогда
		
		ТаблицаРасходыНаПлатон.Колонки.Добавить("УплачиваютсяАвансы", Новый ОписаниеТипов("Булево"));
		
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Результат[Результат.ВГраница()].Выгрузить(), ТаблицаРасходыНаПлатон);
		ТаблицаРасходыНаПлатон.Индексы.Добавить("ОтчетОператора");
		ТаблицаРасходыНаПлатон.Индексы.Добавить("ОсновноеСредство");
		
		УчтенныеПлатежи = Результат[Результат.ВГраница() - 2].Выгрузить();
		УчтенныеПлатежи.Индексы.Добавить("ОсновноеСредство");
		
		// 1. Распределение платежа по ОС.
		ВыборкаПлатежи = Результат[Результат.ВГраница()-1].Выбрать();
		Пока ВыборкаПлатежи.Следующий() Цикл
			
			СтруктураПоиска = Новый Структура("ОтчетОператора");
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, ВыборкаПлатежи);
			СписокСтрок = ТаблицаРасходыНаПлатон.НайтиСтроки(СтруктураПоиска);
			КоэффициентыОплаты = Новый Массив;
			Для каждого Расход Из СписокСтрок Цикл
				КоэффициентыОплаты.Добавить(Расход.База);
			КонецЦикла;
			
			РаспределениеПлатежа = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(
										ВыборкаПлатежи.СуммаПлатежа, КоэффициентыОплаты);
										
			РаспределениеПлатежаЗаКвартал = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(
										ВыборкаПлатежи.СуммаПлатежаЗаКвартал, КоэффициентыОплаты);
										
			СтрокиКУдалению = Новый Массив;
			Для Сч = 0 По КоэффициентыОплаты.ВГраница() Цикл
				Расход = СписокСтрок[Сч];
				
				Если Расход.УплачиваютсяАвансы Тогда
					Расход.СуммаНУДт = ?(РаспределениеПлатежаЗаКвартал <> Неопределено, РаспределениеПлатежаЗаКвартал[Сч], 0);
				Иначе
					Расход.СуммаНУДт = РаспределениеПлатежа[Сч];
				КонецЕсли; 
				
				// Нужно уменьшить сумму оплат на суммы, которые были учтены при авансовых платежах по налогу.
				Если ГодовойРасчет Тогда
					СтруктураПоиска = Новый Структура("ОсновноеСредство", Расход.ОсновноеСредство);
					СписокСтрокУчтенныхПлатежей = УчтенныеПлатежи.НайтиСтроки(СтруктураПоиска);
					Для каждого СтрокаУчтенныеПлатежи Из СписокСтрокУчтенныхПлатежей Цикл
						Если НачалоКвартала(СтрокаУчтенныеПлатежи.Период) = ВыборкаПлатежи.Период
							И СтрокаУчтенныеПлатежи.СуммаПлатежа > 0 Тогда
							СуммаПлатежа = Мин(Расход.СуммаНУДт, СтрокаУчтенныеПлатежи.СуммаПлатежа);
							Расход.СуммаНУДт = Расход.СуммаНУДт - СуммаПлатежа;
							СтрокаУчтенныеПлатежи.СуммаПлатежа = СтрокаУчтенныеПлатежи.СуммаПлатежа - СуммаПлатежа;
						КонецЕсли; 
					КонецЦикла; 
				КонецЕсли; 
				
				Расход.СуммаНУКт = Расход.СуммаНУДт;
				
				Если Расход.СуммаНУДт = 0 Тогда
					СтрокиКУдалению.Добавить(Расход);
				КонецЕсли;
			КонецЦикла;
			
			Для каждого Расход Из СтрокиКУдалению Цикл
				ТаблицаРасходыНаПлатон.Удалить(Расход);
			КонецЦикла; 
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ТаблицаРасходыНаПлатон;
	
КонецФункции

Процедура ДополнитьРасчетТранспортногоНалога(РасчетТранспортногоНалога, Знач ДатаКонцаПериодаОтчета) Экспорт
	
	РасчетТранспортногоНалога.Колонки.Добавить("КодПоОКТМОРегиона", 
		ОбщегоНазначения.ОписаниеТипаСтрока(8));
		
	РасчетТранспортногоНалога.Колонки.Добавить("КатегорияТС", 
		Новый ОписаниеТипов("ПеречислениеСсылка.КатегорииТранспортныхСредств"));
		
	РасчетТранспортногоНалога.Колонки.Добавить("ОтсутствуетСтавка", Новый ОписаниеТипов("Булево"));
		
	Макет = Документы.РегистрацияТранспортныхСредств.ПолучитьМакет("КодыВидовИКатегорииТС");
	КодыВидовИКатегорииТС = ОбщегоНазначения.ПрочитатьXMLВТаблицу(Макет.ПолучитьТекст()).Данные;
	
	Для Каждого СтрокаРасчета Из РасчетТранспортногоНалога Цикл
		
		КодПоОКТМО = Лев(СтрокаРасчета.КодПоОКТМО, 3);

		Если НЕ (КодПоОКТМО = "118" ИЛИ КодПоОКТМО = "718" ИЛИ КодПоОКТМО = "719") Тогда
			СтрокаРасчета.КодПоОКТМОРегиона = Лев(КодПоОКТМО, 2) + "000000";
		Иначе
			СтрокаРасчета.КодПоОКТМОРегиона = КодПоОКТМО + "00000";
		КонецЕсли;
		
		НайденнаяСтрока = КодыВидовИКатегорииТС.Найти(СтрокаРасчета.КодВидаТранспортногоСредства, "КодВида");
		Если НайденнаяСтрока <> Неопределено Тогда
			СтрокаРасчета.КатегорияТС = Перечисления.КатегорииТранспортныхСредств[НайденнаяСтрока.Категория];
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаРасчета.ДатаВыпуска) Тогда
			СтрокаРасчета.КоличествоЛетПрошедшихСГодаВыпускаТС = 
				Год(ДатаКонцаПериодаОтчета) - Год(СтрокаРасчета.ДатаВыпуска);
		КонецЕсли;
		
	КонецЦикла;
	
	КолонкиДляОпределенияСтавок = "КодПоОКТМОРегиона,КатегорияТС,НалоговаяБаза,КоличествоЛетПрошедшихСГодаВыпускаТС";
	СтруктураОтбора = Новый Структура("СтавкаОпределяетсяАвтоматически", Истина);
	ТаблицаДляОпределенияСтавок = РасчетТранспортногоНалога.Скопировать(СтруктураОтбора,КолонкиДляОпределенияСтавок);
	
	Если ТаблицаДляОпределенияСтавок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаДляОпределенияСтавок.Свернуть(КолонкиДляОпределенияСтавок);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаДляОпределенияСтавок", ТаблицаДляОпределенияСтавок);
	Запрос.УстановитьПараметр("Период", ДатаКонцаПериодаОтчета);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДляОпределенияСтавок.КодПоОКТМОРегиона КАК КодПоОКТМОРегиона,
	|	ТаблицаДляОпределенияСтавок.КатегорияТС КАК КатегорияТС,
	|	ТаблицаДляОпределенияСтавок.НалоговаяБаза КАК НалоговаяБаза,
	|	ТаблицаДляОпределенияСтавок.КоличествоЛетПрошедшихСГодаВыпускаТС КАК КоличествоЛетПрошедшихСГодаВыпускаТС
	|ПОМЕСТИТЬ ВТДляОпределенияСтавок
	|ИЗ
	|	&ТаблицаДляОпределенияСтавок КАК ТаблицаДляОпределенияСтавок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтавкиТранспортногоНалога.Период КАК Период,
	|	СтавкиТранспортногоНалога.ОКТМО КАК ОКТМО,
	|	СтавкиТранспортногоНалога.НаименованиеОбъектаНалогообложения КАК НаименованиеОбъектаНалогообложения,
	|	СтавкиТранспортногоНалога.МинимальноеЗначениеМощности КАК МинимальноеЗначениеМощности,
	|	СтавкиТранспортногоНалога.МаксимальноеЗначениеМощности КАК МаксимальноеЗначениеМощности,
	|	СтавкиТранспортногоНалога.МинимальноеКоличествоЛетСГодаВыпускаТС КАК МинимальноеКоличествоЛетСГодаВыпускаТС,
	|	СтавкиТранспортногоНалога.МаксимальноеКоличествоЛетСГодаВыпускаТС КАК МаксимальноеКоличествоЛетСГодаВыпускаТС,
	|	СтавкиТранспортногоНалога.НалоговаяСтавка КАК НалоговаяСтавка,
	|	ВТДляОпределенияСтавок.НалоговаяБаза КАК НалоговаяБаза,
	|	ВТДляОпределенияСтавок.КоличествоЛетПрошедшихСГодаВыпускаТС КАК КоличествоЛетПрошедшихСГодаВыпускаТС
	|ПОМЕСТИТЬ ВТВсеПодходящиеЗаписи
	|ИЗ
	|	ВТДляОпределенияСтавок КАК ВТДляОпределенияСтавок
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиТранспортногоНалога КАК СтавкиТранспортногоНалога
	|		ПО ВТДляОпределенияСтавок.КодПоОКТМОРегиона = СтавкиТранспортногоНалога.ОКТМО
	|			И ВТДляОпределенияСтавок.КатегорияТС = СтавкиТранспортногоНалога.НаименованиеОбъектаНалогообложения
	|			И (СтавкиТранспортногоНалога.МинимальноеЗначениеМощности <= ВТДляОпределенияСтавок.НалоговаяБаза)
	|			И (СтавкиТранспортногоНалога.МаксимальноеЗначениеМощности = 0
	|				ИЛИ ВТДляОпределенияСтавок.НалоговаяБаза <= СтавкиТранспортногоНалога.МаксимальноеЗначениеМощности)
	|			И (СтавкиТранспортногоНалога.МинимальноеКоличествоЛетСГодаВыпускаТС <= ВТДляОпределенияСтавок.КоличествоЛетПрошедшихСГодаВыпускаТС)
	|			И (СтавкиТранспортногоНалога.МаксимальноеКоличествоЛетСГодаВыпускаТС = 0
	|				ИЛИ ВТДляОпределенияСтавок.КоличествоЛетПрошедшихСГодаВыпускаТС <= СтавкиТранспортногоНалога.МаксимальноеКоличествоЛетСГодаВыпускаТС)
	|ГДЕ
	|	СтавкиТранспортногоНалога.Период < &Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТВсеПодходящиеЗаписи.Период) КАК Период,
	|	ВТВсеПодходящиеЗаписи.ОКТМО КАК ОКТМО,
	|	ВТВсеПодходящиеЗаписи.НаименованиеОбъектаНалогообложения КАК НаименованиеОбъектаНалогообложения,
	|	ВТВсеПодходящиеЗаписи.НалоговаяБаза КАК НалоговаяБаза,
	|	ВТВсеПодходящиеЗаписи.КоличествоЛетПрошедшихСГодаВыпускаТС КАК КоличествоЛетПрошедшихСГодаВыпускаТС
	|ПОМЕСТИТЬ ВТПоследниеСтавки
	|ИЗ
	|	ВТВсеПодходящиеЗаписи КАК ВТВсеПодходящиеЗаписи
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТВсеПодходящиеЗаписи.КоличествоЛетПрошедшихСГодаВыпускаТС,
	|	ВТВсеПодходящиеЗаписи.НалоговаяБаза,
	|	ВТВсеПодходящиеЗаписи.НаименованиеОбъектаНалогообложения,
	|	ВТВсеПодходящиеЗаписи.ОКТМО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВсеПодходящиеЗаписи.ОКТМО КАК КодПоОКТМОРегиона,
	|	ВТВсеПодходящиеЗаписи.НаименованиеОбъектаНалогообложения КАК КатегорияТС,
	|	ВТВсеПодходящиеЗаписи.НалоговаяБаза КАК НалоговаяБаза,
	|	ВТВсеПодходящиеЗаписи.КоличествоЛетПрошедшихСГодаВыпускаТС КАК КоличествоЛетПрошедшихСГодаВыпускаТС,
	|	ВТВсеПодходящиеЗаписи.МинимальноеКоличествоЛетСГодаВыпускаТС <> 0
	|		ИЛИ ВТВсеПодходящиеЗаписи.МаксимальноеКоличествоЛетСГодаВыпускаТС <> 0 КАК НалоговаяСтавкаЗависитОтГодаВыпускаТС,
	|	ВТВсеПодходящиеЗаписи.НалоговаяСтавка КАК НалоговаяСтавка
	|ИЗ
	|	ВТВсеПодходящиеЗаписи КАК ВТВсеПодходящиеЗаписи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПоследниеСтавки КАК ВТПоследниеСтавки
	|		ПО ВТВсеПодходящиеЗаписи.Период = ВТПоследниеСтавки.Период 
	|			И ВТВсеПодходящиеЗаписи.ОКТМО = ВТПоследниеСтавки.ОКТМО
	|			И ВТВсеПодходящиеЗаписи.НаименованиеОбъектаНалогообложения = ВТПоследниеСтавки.НаименованиеОбъектаНалогообложения
	|			И ВТВсеПодходящиеЗаписи.НалоговаяБаза = ВТПоследниеСтавки.НалоговаяБаза
	|			И ВТВсеПодходящиеЗаписи.КоличествоЛетПрошедшихСГодаВыпускаТС = ВТПоследниеСтавки.КоличествоЛетПрошедшихСГодаВыпускаТС";
	
	ТаблицаСтавок = Запрос.Выполнить().Выгрузить();
	Отбор = Новый Структура(КолонкиДляОпределенияСтавок);
	Для Каждого СтрокаРасчета Из РасчетТранспортногоНалога Цикл
		
		Если НЕ СтрокаРасчета.СтавкаОпределяетсяАвтоматически Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаРасчета);
		НайденныеСтроки = ТаблицаСтавок.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(СтрокаРасчета, НайденныеСтроки[0],, КолонкиДляОпределенияСтавок);
		Иначе
			СтрокаРасчета.ОтсутствуетСтавка = Истина;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

// Возвращает необходимость уплаты авансов по транспортному налогу.
//
// Параметры:
// 	Организации - СправочникСсылка.Организации - Организация, для которой необходимо выполнить расчет.
// 	Период - Дата - Дата закрытия месяца.
//
// Возвращаемое значение:
// 	Булево - Признак необходимости уплаты аванса.
//
Функция УплачиваютсяАвансыПоТранспортномуНалогу(Организации, Период) Экспорт
	
	УплачиваютсяАвансы = Ложь;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.УстановитьПараметр("Период", Период);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.НалоговыйОрган КАК НалоговыйОрган
	|ПОМЕСТИТЬ ВТ_НалоговыеОрганы
	|ИЗ
	|	(ВЫБРАТЬ
	|		РегистрацияТранспортныхСредств.Организация КАК Организация,
	|		РегистрацияТранспортныхСредств.НалоговыйОрган КАК НалоговыйОрган
	|	ИЗ
	|		РегистрСведений.РегистрацияТранспортныхСредств.СрезПоследних(
	|				КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&Период, КВАРТАЛ, -1), КВАРТАЛ),
	|				Организация В (&Организации)
	|					И ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|					И ВключатьВНалоговуюБазу) КАК РегистрацияТранспортныхСредств
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РегистрацияТранспортныхСредств.Организация,
	|		РегистрацияТранспортныхСредств.НалоговыйОрган
	|	ИЗ
	|		РегистрСведений.РегистрацияТранспортныхСредств.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Период, КВАРТАЛ),
	|				Организация В (&Организации)
	|					И ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|					И ВключатьВНалоговуюБазу) КАК РегистрацияТранспортныхСредств
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РегистрацияТранспортныхСредств.Организация,
	|		РегистрацияТранспортныхСредств.НалоговыйОрган
	|	ИЗ
	|		РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
	|	ГДЕ
	|		РегистрацияТранспортныхСредств.Организация В (&Организации)
	|		И РегистрацияТранспортныхСредств.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|		И РегистрацияТранспортныхСредств.Период МЕЖДУ НАЧАЛОПЕРИОДА(&Период, КВАРТАЛ) И КОНЕЦПЕРИОДА(&Период, КВАРТАЛ)
	|		И РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Организации.Ссылка,
	|		Организации.РегистрацияВНалоговомОргане
	|	ИЗ
	|		Справочник.Организации КАК Организации
	|	ГДЕ
	|		Организации.Ссылка В (&Организации)) КАК ВложенныйЗапрос
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	НалоговыйОрган
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.Организация КАК Организация,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.УплачиваютсяАвансы КАК УплачиваютсяАвансы
	|ПОМЕСТИТЬ ВТ_УплачиваютсяАвансыПоОрганизации
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(
	|			&Период,
	|			Организация В (&Организации)
	|				И РегистрацияВНалоговомОргане = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|				И Налог = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ТранспортныйНалог)) КАК ПорядокУплатыНалоговНаМестахСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.Организация КАК Организация,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.УплачиваютсяАвансы КАК УплачиваютсяАвансы
	|ПОМЕСТИТЬ ВТ_УплачиваютсяАвансыПоИФНС
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(
	|			&Период,
	|			Организация В (&Организации)
	|				И РегистрацияВНалоговомОргане <> ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|				И Налог = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ТранспортныйНалог)) КАК ПорядокУплатыНалоговНаМестахСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(ВЫБОР
	|			КОГДА ВТ_УплачиваютсяАвансыПоИФНС.УплачиваютсяАвансы ЕСТЬ NULL 
	|				ТОГДА ВЫБОР
	|						КОГДА ВТ_УплачиваютсяАвансыПоОрганизации.УплачиваютсяАвансы ЕСТЬ NULL 
	|							ТОГДА ЛОЖЬ
	|						ИНАЧЕ ВТ_УплачиваютсяАвансыПоОрганизации.УплачиваютсяАвансы
	|					КОНЕЦ
	|			ИНАЧЕ ВТ_УплачиваютсяАвансыПоИФНС.УплачиваютсяАвансы
	|		КОНЕЦ), ЛОЖЬ) КАК УплачиваютсяАвансы
	|ИЗ
	|	ВТ_НалоговыеОрганы КАК ВТ_НалоговыеОрганы
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			ВТ_УплачиваютсяАвансыПоОрганизации КАК ВТ_УплачиваютсяАвансыПоОрганизации
	|		ПО 
	|			ВТ_НалоговыеОрганы.Организация = ВТ_УплачиваютсяАвансыПоОрганизации.Организация
	|			
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_УплачиваютсяАвансыПоИФНС КАК ВТ_УплачиваютсяАвансыПоИФНС
	|		ПО 
	|			ВТ_НалоговыеОрганы.Организация = ВТ_УплачиваютсяАвансыПоИФНС.Организация
	|			И ВТ_НалоговыеОрганы.НалоговыйОрган = ВТ_УплачиваютсяАвансыПоИФНС.РегистрацияВНалоговомОргане";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		Пока Выборка.Следующий() Цикл
			УплачиваютсяАвансы = Выборка.УплачиваютсяАвансы;
		КонецЦикла;
		
	КонецЕсли;
		
	Возврат УплачиваютсяАвансы;
	
КонецФункции

Функция ПустаяТаблицаРасходыНаПлатон()

	РасходыНаПлатон = Новый ТаблицаЗначений;
	
	РасходыНаПлатон.Колонки.Добавить("ОтчетОператора", Новый ОписаниеТипов("ДокументСсылка.ОтчетОператораСистемыПлатон"));
	РасходыНаПлатон.Колонки.Добавить("ОсновноеСредство", Новый ОписаниеТипов("СправочникСсылка.ОбъектыЭксплуатации"));
	РасходыНаПлатон.Колонки.Добавить("СтатьяРасходов", Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.СтатьиРасходов"));
	РасходыНаПлатон.Колонки.Добавить("АналитикаРасходов", Метаданные.ПланыВидовХарактеристик.СтатьиРасходов.Тип);
	РасходыНаПлатон.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия"));
	РасходыНаПлатон.Колонки.Добавить("НаправлениеДеятельности", Новый ОписаниеТипов("СправочникСсылка.НаправленияДеятельности"));
	РасходыНаПлатон.Колонки.Добавить("СуммаНУДт", РаботаСКурсамиВалют.ОписаниеТипаДенежногоПоля());
	РасходыНаПлатон.Колонки.Добавить("СуммаНУКт", РаботаСКурсамиВалют.ОписаниеТипаДенежногоПоля());
	РасходыНаПлатон.Колонки.Добавить("База", РаботаСКурсамиВалют.ОписаниеТипаДенежногоПоля());
	
	Возврат РасходыНаПлатон;

КонецФункции

#КонецОбласти

#Область РасчетЗемельногоНалога

// Возвращает необходимость уплаты авансов по земельному налогу.
//
// Параметры:
// 	Организации - СправочникСсылка.Организации - Организация, для которой необходимо выполнить проверку.
// 	Период - Дата - Дата закрытия месяца.
//
// Возвращаемое значение:
// 	Булево - Признак необходимости уплаты аванса.
//
Функция УплачиваютсяАвансыПоЗемельномуНалогу(Организации, Период) Экспорт
	
	УплачиваютсяАвансы = Ложь;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.УстановитьПараметр("Период", Период);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.НалоговыйОрган КАК НалоговыйОрган
	|ПОМЕСТИТЬ ВТ_НалоговыеОрганы
	|ИЗ
	|	(ВЫБРАТЬ
	|		РегистрацияЗемельныхУчастков.Организация КАК Организация,
	|		РегистрацияЗемельныхУчастков.НалоговыйОрган КАК НалоговыйОрган
	|	ИЗ
	|		РегистрСведений.РегистрацияЗемельныхУчастков.СрезПоследних(
	|				КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&Период, КВАРТАЛ, -1), КВАРТАЛ),
	|				Организация В (&Организации)
	|					И ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|					И ВключатьВНалоговуюБазу) КАК РегистрацияЗемельныхУчастков
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РегистрацияЗемельныхУчастков.Организация,
	|		РегистрацияЗемельныхУчастков.НалоговыйОрган
	|	ИЗ
	|		РегистрСведений.РегистрацияЗемельныхУчастков.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Период, КВАРТАЛ),
	|				Организация В (&Организации)
	|					И ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|					И ВключатьВНалоговуюБазу) КАК РегистрацияЗемельныхУчастков
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РегистрацияЗемельныхУчастков.Организация,
	|		РегистрацияЗемельныхУчастков.НалоговыйОрган
	|	ИЗ
	|		РегистрСведений.РегистрацияЗемельныхУчастков КАК РегистрацияЗемельныхУчастков
	|	ГДЕ
	|		РегистрацияЗемельныхУчастков.Организация В (&Организации)
	|		И РегистрацияЗемельныхУчастков.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|		И РегистрацияЗемельныхУчастков.Период МЕЖДУ НАЧАЛОПЕРИОДА(&Период, КВАРТАЛ) И КОНЕЦПЕРИОДА(&Период, КВАРТАЛ)
	|		И РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Организации.Ссылка,
	|		Организации.РегистрацияВНалоговомОргане
	|	ИЗ
	|		Справочник.Организации КАК Организации
	|	ГДЕ
	|		Организации.Ссылка В (&Организации)) КАК ВложенныйЗапрос
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	НалоговыйОрган
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.Организация КАК Организация,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.УплачиваютсяАвансы КАК УплачиваютсяАвансы
	|ПОМЕСТИТЬ ВТ_УплачиваютсяАвансыПоОрганизации
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(
	|			&Период,
	|			Организация В (&Организации)
	|				И РегистрацияВНалоговомОргане = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|				И Налог = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ЗемельныйНалог)) КАК ПорядокУплатыНалоговНаМестахСрезПоследних
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.Организация КАК Организация,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.УплачиваютсяАвансы КАК УплачиваютсяАвансы
	|ПОМЕСТИТЬ ВТ_УплачиваютсяАвансыПоИФНС
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(
	|			&Период,
	|			Организация В (&Организации)
	|				И РегистрацияВНалоговомОргане <> ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|				И Налог = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ЗемельныйНалог)) КАК ПорядокУплатыНалоговНаМестахСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(ВЫБОР
	|			КОГДА ВТ_УплачиваютсяАвансыПоИФНС.УплачиваютсяАвансы ЕСТЬ NULL 
	|				ТОГДА ВЫБОР
	|						КОГДА ВТ_УплачиваютсяАвансыПоОрганизации.УплачиваютсяАвансы ЕСТЬ NULL 
	|							ТОГДА ЛОЖЬ
	|						ИНАЧЕ ВТ_УплачиваютсяАвансыПоОрганизации.УплачиваютсяАвансы
	|					КОНЕЦ
	|			ИНАЧЕ ВТ_УплачиваютсяАвансыПоИФНС.УплачиваютсяАвансы
	|		КОНЕЦ), ЛОЖЬ) КАК УплачиваютсяАвансы
	|ИЗ
	|	ВТ_НалоговыеОрганы КАК ВТ_НалоговыеОрганы
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			ВТ_УплачиваютсяАвансыПоОрганизации КАК ВТ_УплачиваютсяАвансыПоОрганизации
	|		ПО
	|			ВТ_НалоговыеОрганы.Организация = ВТ_УплачиваютсяАвансыПоОрганизации.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			ВТ_УплачиваютсяАвансыПоИФНС КАК ВТ_УплачиваютсяАвансыПоИФНС
	|		ПО 
	|			ВТ_НалоговыеОрганы.Организация = ВТ_УплачиваютсяАвансыПоИФНС.Организация
	|			И ВТ_НалоговыеОрганы.НалоговыйОрган = ВТ_УплачиваютсяАвансыПоИФНС.РегистрацияВНалоговомОргане";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		Пока Выборка.Следующий() Цикл
			УплачиваютсяАвансы = Выборка.УплачиваютсяАвансы;
		КонецЦикла;
		
	КонецЕсли;
		
	Возврат УплачиваютсяАвансы;
	
КонецФункции

#КонецОбласти

#Область ЗаданияКЗакрытиюМесяца

// Формирует задание к расчету налога на имущество, на основании измененных данных.
//
// Параметры:
//  Документ - ДокументОбъект - Ссылка на документ.
//  ДанныеТаблиц - Структура - Содержит список таблиц в которых произошли изменения.
//
Процедура СформироватьЗаданиеКРасчетуНалогаНаИмущество(Документ, ДанныеТаблиц) Экспорт

	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет") Тогда
		Возврат;
	КонецЕсли;

	ТипОбъекта = ТипЗнч(Документ);
	
	ЕстьОсновныеСредства = 
		ТипОбъекта <> Тип("ДокументОбъект.РегламентнаяОперация")
		И Документ <> Неопределено;
								
	ПроверитьЧтоОСПринятоКУчету = 
		ТипОбъекта = Тип("ДокументОбъект.ВводОстатковВнеоборотныхАктивов")
		ИЛИ ТипОбъекта = Тип("ДокументОбъект.ВводОстатковВнеоборотныхАктивов2_4")
		ИЛИ ТипОбъекта = Тип("ДокументОбъект.ПринятиеКУчетуОС")
		ИЛИ ТипОбъекта = Тип("ДокументОбъект.РегистрацияТранспортныхСредств");
									
	ПринятиеКУчетуЗаданоДокументом = 
		ТипОбъекта = Тип("ДокументОбъект.ВводОстатковВнеоборотныхАктивов")
		ИЛИ ТипОбъекта = Тип("ДокументОбъект.ВводОстатковВнеоборотныхАктивов2_4")
		ИЛИ ТипОбъекта = Тип("ДокументОбъект.ПринятиеКУчетуОС");
	
	СписокТаблиц = Новый Массив;
	СписокТаблиц.Добавить("ИзменениеПараметровОС");
	СписокТаблиц.Добавить("ИзменениеСпособаОтраженияИмущественныхНалогов_НалогНаИмущество");
	СписокТаблиц.Добавить("РегламентнаяОперацияРасчетНалогаНаИмущество");
	СписокТаблиц.Добавить("РегистрацияПорядкаНалогообложенияПоНалогуНаИмущество");
	СписокТаблиц.Добавить("СпособыОтраженияРасходовПоИмущественнымНалогам");
	СписокТаблиц.Добавить("СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам");
	СписокТаблиц.Добавить("СтавкиНалогаНаИмущество");
	СписокТаблиц.Добавить("ПорядокУплатыНалоговНаМестах");
	
	// Версия 2.4
	СписокТаблиц.Добавить("СтоимостьОС");
	СписокТаблиц.Добавить("АмортизацияОС");
	СписокТаблиц.Добавить("МестонахождениеОС");
	СписокТаблиц.Добавить("ПервоначальныеСведенияОС");
	СписокТаблиц.Добавить("ПорядокУчетаОС");
	СписокТаблиц.Добавить("ПорядокУчетаОСБУ");
	
	СписокДопПолей = "ОтражатьВРеглУчете" + ?(ЕстьОсновныеСредства, ",ОсновноеСредство", "");
	
	СписокПолейТаблиц = Новый Структура;
	ТаблицыВидНалога = Новый Массив;
	ТаблицыВидНалога.Добавить("СпособыОтраженияРасходовПоИмущественнымНалогам");
	СписокПолейТаблиц.Вставить("ВидНалога", ТаблицыВидНалога);
	
	ТекстОбъединенияДанных = ВнеоборотныеАктивы.СформироватьТекстОбъединенияДанныхДляФормированияЗадания(
									СписокТаблиц, ДанныеТаблиц, СписокДопПолей, СписокПолейТаблиц);
	
	Если ТекстОбъединенияДанных = "" Тогда
		Возврат;
	КонецЕсли;
	
	СписокЗапросов = Новый Массив;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МИНИМУМ(ВЫБОР 
	|				КОГДА ИзмененныеДанные.Период < &ДатаНачалаВеденияРеглУчета
	|						И &ДатаНачалаВеденияРеглУчета <> ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА &ДатаНачалаВеденияРеглУчета
	|
	|				КОГДА ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.ВводОстатковВнеоборотныхАктивов)
	|					ТОГДА ДОБАВИТЬКДАТЕ(ВводОстатков22.Дата, МЕСЯЦ, 1)
	|
	|				КОГДА ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.ВводОстатковВнеоборотныхАктивов2_4)
	|					ТОГДА ДОБАВИТЬКДАТЕ(ВводОстатков24.Дата, МЕСЯЦ, 1)
	|
	|				КОГДА ИзмененныеДанные.ИмяТаблицы = ""ПорядокУчетаОСБУ""
	|						И ИзмененныеДанные.Период < &ДатаНачалаУчета24
	|					ТОГДА &ДатаНачалаУчета24
	|
	|				ИНАЧЕ ИзмененныеДанные.Период 
	|			КОНЕЦ) КАК Период,
	|	ИзмененныеДанные.Организация       КАК Организация,
	|	ИзмененныеДанные.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	" + ?(ЕстьОсновныеСредства, "ИзмененныеДанные.ОсновноеСредство КАК ОсновноеСредство,", "") + "
	|	ИзмененныеДанные.Документ          КАК Документ
	|ПОМЕСТИТЬ ИзмененныеДанные
	|ИЗ
	|	(" + ТекстОбъединенияДанных + ") КАК ИзмененныеДанные
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов КАК ВводОстатков22
	|	ПО ВводОстатков22.Ссылка = ИзмененныеДанные.Документ
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов2_4 КАК ВводОстатков24
	|	ПО ВводОстатков24.Ссылка = ИзмененныеДанные.Документ
	|
	|ГДЕ
	|	ИзмененныеДанные.ОтражатьВРеглУчете
	|	И ИзмененныеДанные.ВидНалога В (
	|		ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.НалогНаИмущество),
	|		НЕОПРЕДЕЛЕНО)
	|
	|СГРУППИРОВАТЬ ПО
	|	ИзмененныеДанные.Организация,
	|	ИзмененныеДанные.Организация.ГоловнаяОрганизация,
	|	" + ?(ЕстьОсновныеСредства, "ИзмененныеДанные.ОсновноеСредство,", "") + "
	|	ИзмененныеДанные.Документ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|	" + ?(ЕстьОсновныеСредства, ",ОсновноеСредство", "");
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	Если ЕстьОсновныеСредства Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ИзмененныеДанные.Период КАК Период,
		|	ИзмененныеДанные.Организация КАК Организация,
		|	ИзмененныеДанные.ОсновноеСредство КАК ОсновноеСредство,
		|	МАКСИМУМ(ПорядокУчетаОСБУ.Период) КАК ПериодПоследнее
		|ПОМЕСТИТЬ ПорядокУчетаОСБУ_Период
		|ИЗ
		|	ИзмененныеДанные КАК ИзмененныеДанные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ КАК ПорядокУчетаОСБУ
		|		ПО (ПорядокУчетаОСБУ.Организация = ИзмененныеДанные.Организация)
		|			И (ПорядокУчетаОСБУ.ОсновноеСредство = ИзмененныеДанные.ОсновноеСредство)
		|			И (ПорядокУчетаОСБУ.Период <= ИзмененныеДанные.Период)
		|
		|СГРУППИРОВАТЬ ПО
		|	ИзмененныеДанные.Период,
		|	ИзмененныеДанные.Организация,
		|	ИзмененныеДанные.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПорядокУчетаОСБУ_Период.Период КАК Период,
		|	ПорядокУчетаОСБУ_Период.Организация КАК Организация,
		|	ПорядокУчетаОСБУ_Период.ОсновноеСредство КАК ОсновноеСредство,
		|	ПорядокУчетаОСБУ.НедвижимоеИмущество КАК НедвижимоеИмущество
		|ПОМЕСТИТЬ ПорядокУчетаОСБУ
		|ИЗ
		|	ПорядокУчетаОСБУ_Период КАК ПорядокУчетаОСБУ_Период
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ КАК ПорядокУчетаОСБУ
		|		ПО (ПорядокУчетаОСБУ.Организация = ПорядокУчетаОСБУ_Период.Организация)
		|			И (ПорядокУчетаОСБУ.ОсновноеСредство = ПорядокУчетаОСБУ_Период.ОсновноеСредство)
		|			И (ПорядокУчетаОСБУ.Период = ПорядокУчетаОСБУ_Период.ПериодПоследнее)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	ОсновноеСредство,
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПорядокУчетаОСБУ_Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИзмененныеДанные.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ СписокОС
		|ИЗ
		|	ИзмененныеДанные КАК ИзмененныеДанные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
		|		ПО (ОбъектыЭксплуатации.Ссылка = ИзмененныеДанные.ОсновноеСредство)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСБУ КАК ПорядокУчетаОСБУ
		|		ПО (ПорядокУчетаОСБУ.Организация = ИзмененныеДанные.Организация)
		|			И (ПорядокУчетаОСБУ.ОсновноеСредство = ИзмененныеДанные.ОсновноеСредство)
		|			И (ПорядокУчетаОСБУ.Период = ИзмененныеДанные.Период)
		|ГДЕ
		|	(ИзмененныеДанные.Период < ДАТАВРЕМЯ(2019, 1, 1)
		|			ИЛИ НЕ ПорядокУчетаОСБУ.ОсновноеСредство ЕСТЬ NULL
		|				И ПорядокУчетаОСБУ.НедвижимоеИмущество
		|			ИЛИ ПорядокУчетаОСБУ.ОсновноеСредство ЕСТЬ NULL
		|				И ОбъектыЭксплуатации.ГруппаОС В (&ГруппыНедвижимогоИмущества))
		|	И НЕ ОбъектыЭксплуатации.ГруппаОС В (ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ЗемельныеУчастки), ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ОбъектыПриродопользования))
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПорядокУчетаОСБУ";
		СписокЗапросов.Добавить(ТекстЗапроса);
		
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ИзмененныеДанные.Организация КАК Организация,
	|	УчетнаяПолитикаОрганизаций.СистемаНалогообложения КАК СистемаНалогообложения,
	|	ВЫБОР
	|		КОГДА УчетнаяПолитикаОрганизаций.Период > ИзмененныеДанные.Период
	|				И УчетнаяПолитикаОрганизаций.Период > &ДатаНачалаВеденияРеглУчета
	|			ТОГДА УчетнаяПолитикаОрганизаций.Период
	|		КОГДА &ДатаНачалаВеденияРеглУчета > ИзмененныеДанные.Период
	|			ТОГДА &ДатаНачалаВеденияРеглУчета
	|		ИНАЧЕ ИзмененныеДанные.Период
	|	КОНЕЦ КАК Начало
	|ПОМЕСТИТЬ УчетнаяПолитикаОрганизаций
	|ИЗ
	|	ИзмененныеДанные КАК ИзмененныеДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСистемыНалогообложения КАК УчетнаяПолитикаОрганизаций
	|		ПО (УчетнаяПолитикаОрганизаций.Организация = ИзмененныеДанные.ГоловнаяОрганизация)
	|			И (УчетнаяПолитикаОрганизаций.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Общая)
	|				ИЛИ ИзмененныеДанные.Период >= ДАТАВРЕМЯ(2015, 1, 1))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСистемыНалогообложения КАК СледующаяУчетнаяПолитикаОрганизаций
	|		ПО (СледующаяУчетнаяПолитикаОрганизаций.Организация = УчетнаяПолитикаОрганизаций.Организация)
	|			И (СледующаяУчетнаяПолитикаОрганизаций.Период > УчетнаяПолитикаОрганизаций.Период)
	|
	|СГРУППИРОВАТЬ ПО
	|	ИзмененныеДанные.Организация,
	|	УчетнаяПолитикаОрганизаций.СистемаНалогообложения,
	|	ВЫБОР
	|		КОГДА УчетнаяПолитикаОрганизаций.Период > ИзмененныеДанные.Период
	|				И УчетнаяПолитикаОрганизаций.Период > &ДатаНачалаВеденияРеглУчета
	|			ТОГДА УчетнаяПолитикаОрганизаций.Период
	|		КОГДА &ДатаНачалаВеденияРеглУчета > ИзмененныеДанные.Период
	|			ТОГДА &ДатаНачалаВеденияРеглУчета
	|		ИНАЧЕ ИзмененныеДанные.Период
	|	КОНЕЦ
	|
	|ИМЕЮЩИЕ
	|	(МИНИМУМ(ЕСТЬNULL(СледующаяУчетнаяПолитикаОрганизаций.Период, ДАТАВРЕМЯ(1, 1, 1))) > МИНИМУМ(ИзмененныеДанные.Период)
	|			И МИНИМУМ(ЕСТЬNULL(СледующаяУчетнаяПолитикаОрганизаций.Период, ДАТАВРЕМЯ(1, 1, 1))) > &ДатаНачалаВеденияРеглУчета
	|		ИЛИ МИНИМУМ(ЕСТЬNULL(СледующаяУчетнаяПолитикаОрганизаций.Период, ДАТАВРЕМЯ(1, 1, 1))) = ДАТАВРЕМЯ(1, 1, 1))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация";
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	Если ПроверитьЧтоОСПринятоКУчету Тогда
		
		Если ПринятиеКУчетуЗаданоДокументом Тогда
			
			Если ДанныеТаблиц.МенеджерВременныхТаблиц.Таблицы.Найти("ПорядокУчетаОСБУ") <> Неопределено Тогда
			
				ТекстЗапроса =
					"ВЫБРАТЬ
					|	УчетнаяПолитикаОрганизаций.Организация КАК Организация,
					|	МИНИМУМ(КОНЕЦПЕРИОДА(ВЫБОР 
					|							КОГДА ПорядокУчетаОСБУ.Период > УчетнаяПолитикаОрганизаций.Начало 
					|								ТОГДА ПорядокУчетаОСБУ.Период 
					|							ИНАЧЕ 
					|								УчетнаяПолитикаОрганизаций.Начало 
					|						КОНЕЦ, КВАРТАЛ)) КАК Период
					|ПОМЕСТИТЬ ПериодЗадания
					|ИЗ
					|	УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
					|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПорядокУчетаОСБУ КАК ПорядокУчетаОСБУ
					|		ПО (ПорядокУчетаОСБУ.Организация = УчетнаяПолитикаОрганизаций.Организация)
					|			И (ПорядокУчетаОСБУ.СостояниеБУ = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету))
					|ГДЕ
					|	УчетнаяПолитикаОрганизаций.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Общая)
					|	
					|СГРУППИРОВАТЬ ПО
					|	УчетнаяПолитикаОрганизаций.Организация
					|	
					|ИНДЕКСИРОВАТЬ ПО
					|	Организация";
				СписокЗапросов.Добавить(ТекстЗапроса);
					
			Иначе
					
				ТекстЗапроса =
					"ВЫБРАТЬ
					|	УчетнаяПолитикаОрганизаций.Организация КАК Организация,
					|	МИНИМУМ(КОНЕЦПЕРИОДА(ВЫБОР 
					|							КОГДА ПорядокУчетаОСБУ.Период > УчетнаяПолитикаОрганизаций.Начало 
					|								ТОГДА ПорядокУчетаОСБУ.Период 
					|							ИНАЧЕ 
					|								УчетнаяПолитикаОрганизаций.Начало 
					|						КОНЕЦ, КВАРТАЛ)) КАК Период
					|ПОМЕСТИТЬ ПериодЗадания
					|ИЗ
					|	УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
					|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИзмененныеДанные КАК ИзмененныеДанные
					|		ПО ИзмененныеДанные.Организация = УчетнаяПолитикаОрганизаций.Организация
					|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ КАК ПорядокУчетаОСБУ
					|		ПО (ПорядокУчетаОСБУ.Организация = УчетнаяПолитикаОрганизаций.Организация)
					|			И (ПорядокУчетаОСБУ.СостояниеБУ = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету))
					|			И (ПорядокУчетаОСБУ.Регистратор = ИзмененныеДанные.Документ)
					|			И (ПорядокУчетаОСБУ.Активность)
					|ГДЕ
					|	УчетнаяПолитикаОрганизаций.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Общая)
					|	
					|СГРУППИРОВАТЬ ПО
					|	УчетнаяПолитикаОрганизаций.Организация
					|	
					|ИНДЕКСИРОВАТЬ ПО
					|	Организация";
				СписокЗапросов.Добавить(ТекстЗапроса);

			КонецЕсли;		
		Иначе
		
			ТекстЗапроса =
				"ВЫБРАТЬ
				|	ВложенныйЗапрос.Организация,
				|	МИНИМУМ(НАЧАЛОПЕРИОДА(ВложенныйЗапрос.Период, МЕСЯЦ)) КАК Период
				|ПОМЕСТИТЬ ПериодЗадания
				|ИЗ
				|	(ВЫБРАТЬ
				|		УчетнаяПолитикаОрганизаций.Организация КАК Организация,
				|		МИНИМУМ(КОНЕЦПЕРИОДА(ВЫБОР 
				|								КОГДА ПорядокУчетаОСБУ.Период > УчетнаяПолитикаОрганизаций.Начало 
				|									ТОГДА ПорядокУчетаОСБУ.Период 
				|								ИНАЧЕ 
				|									УчетнаяПолитикаОрганизаций.Начало 
				|							КОНЕЦ, КВАРТАЛ)) КАК Период
				|	ИЗ
				|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
				|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ КАК ПорядокУчетаОСБУ
				|			ПО (ПорядокУчетаОСБУ.Организация = УчетнаяПолитикаОрганизаций.Организация)
				|				И (ПорядокУчетаОСБУ.СостояниеБУ = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету))
				|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ КАК СледующееСостояниеОС
				|			ПО (СледующееСостояниеОС.Организация = ПорядокУчетаОСБУ.Организация)
				|				И (СледующееСостояниеОС.ОсновноеСредство = ПорядокУчетаОСБУ.ОсновноеСредство)
				|				И (СледующееСостояниеОС.Период > ПорядокУчетаОСБУ.Период)
				|				И (СледующееСостояниеОС.СостояниеБУ <> ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету))
				|	ГДЕ
				|		УчетнаяПолитикаОрганизаций.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Общая)
				|		" + ?(ЕстьОсновныеСредства, "И ПорядокУчетаОСБУ.ОсновноеСредство В
				|				(ВЫБРАТЬ
				|					СписокОС.ОсновноеСредство
				|				ИЗ
				|					СписокОС КАК СписокОС)", "") + "
				|	
				|	СГРУППИРОВАТЬ ПО
				|		УчетнаяПолитикаОрганизаций.Организация
				|	
				|	ИМЕЮЩИЕ
				|		(МИНИМУМ(СледующееСостояниеОС.Период) >= МИНИМУМ(УчетнаяПолитикаОрганизаций.Начало)
				|			ИЛИ МИНИМУМ(ЕСТЬNULL(СледующееСостояниеОС.Период, ДАТАВРЕМЯ(1, 1, 1))) = ДАТАВРЕМЯ(1, 1, 1))
				|	
				|	ОБЪЕДИНИТЬ ВСЕ
				|	
				|	ВЫБРАТЬ
				|		УчетнаяПолитикаОрганизаций.Организация,
				|		МИНИМУМ(КОНЕЦПЕРИОДА(ВЫБОР 
				|								КОГДА СтавкиНалога.Период > УчетнаяПолитикаОрганизаций.Начало 
				|									ТОГДА СтавкиНалога.Период 
				|								ИНАЧЕ 
				|									УчетнаяПолитикаОрганизаций.Начало 
				|							КОНЕЦ, КВАРТАЛ))
				|	ИЗ
				|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
				|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалога
				|			ПО (СтавкиНалога.Организация = УчетнаяПолитикаОрганизаций.Организация)
				|				И (СтавкиНалога.НалоговаяБаза = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость))
				|				И (СтавкиНалога.Период >= УчетнаяПолитикаОрганизаций.Начало)
				|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СледующееСтавкиНалога
				|			ПО (СледующееСтавкиНалога.Организация = СтавкиНалога.Организация)
				|				И (СледующееСтавкиНалога.ОсновноеСредство = СтавкиНалога.ОсновноеСредство)
				|				И (СледующееСтавкиНалога.Период > СтавкиНалога.Период)
				|				И (СледующееСтавкиНалога.НалоговаяБаза <> ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость))
				|	" + ?(ЕстьОсновныеСредства, "ГДЕ
				|		СтавкиНалога.ОсновноеСредство В
				|				(ВЫБРАТЬ
				|					СписокОС.ОсновноеСредство
				|				ИЗ
				|					СписокОС КАК СписокОС)", "") + "
				|	
				|	СГРУППИРОВАТЬ ПО
				|		УчетнаяПолитикаОрганизаций.Организация
				|	
				|	ИМЕЮЩИЕ
				|		(МИНИМУМ(СледующееСтавкиНалога.Период) >= МИНИМУМ(УчетнаяПолитикаОрганизаций.Начало)
				|			ИЛИ МИНИМУМ(ЕСТЬNULL(СледующееСтавкиНалога.Период, ДАТАВРЕМЯ(1, 1, 1))) = ДАТАВРЕМЯ(1, 1, 1))) КАК ВложенныйЗапрос
				|
				|СГРУППИРОВАТЬ ПО
				|	ВложенныйЗапрос.Организация
				|
				|ИНДЕКСИРОВАТЬ ПО
				|	Организация";
			СписокЗапросов.Добавить(ТекстЗапроса);
			
		КонецЕсли;
		
	Иначе

		ТекстЗапроса =
			"ВЫБРАТЬ
			|	ВложенныйЗапрос.Организация,
			|	МИНИМУМ(НАЧАЛОПЕРИОДА(ВложенныйЗапрос.Период, МЕСЯЦ)) КАК Период
			|ПОМЕСТИТЬ ПериодЗадания
			|ИЗ
			|	(ВЫБРАТЬ
			|		УчетнаяПолитикаОрганизаций.Организация КАК Организация,
			|		МИНИМУМ(КОНЕЦПЕРИОДА(УчетнаяПолитикаОрганизаций.Начало, КВАРТАЛ)) КАК Период
			|	ИЗ
			|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
			|	ГДЕ
			|		УчетнаяПолитикаОрганизаций.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Общая)
			|	
			|	СГРУППИРОВАТЬ ПО
			|		УчетнаяПолитикаОрганизаций.Организация
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		УчетнаяПолитикаОрганизаций.Организация,
			|		МИНИМУМ(КОНЕЦПЕРИОДА(ВЫБОР 
			|								КОГДА СтавкиНалога.Период > УчетнаяПолитикаОрганизаций.Начало 
			|									ТОГДА СтавкиНалога.Период 
			|								ИНАЧЕ 
			|									УчетнаяПолитикаОрганизаций.Начало 
			|							КОНЕЦ, КВАРТАЛ))
			|	ИЗ
			|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалога
			|			ПО (СтавкиНалога.Организация = УчетнаяПолитикаОрганизаций.Организация)
			|				И (СтавкиНалога.НалоговаяБаза = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость))
			|				И (СтавкиНалога.Период >= УчетнаяПолитикаОрганизаций.Начало)
			|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СледующееСтавкиНалога
			|			ПО (СледующееСтавкиНалога.Организация = СтавкиНалога.Организация)
			|				И (СледующееСтавкиНалога.ОсновноеСредство = СтавкиНалога.ОсновноеСредство)
			|				И (СледующееСтавкиНалога.Период > СтавкиНалога.Период)
			|				И (СледующееСтавкиНалога.НалоговаяБаза <> ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость))
			|	" + ?(ЕстьОсновныеСредства, "ГДЕ
			|		СтавкиНалога.ОсновноеСредство В
			|				(ВЫБРАТЬ
			|					СписокОС.ОсновноеСредство
			|				ИЗ
			|					СписокОС КАК СписокОС)", "") + "
			|	
			|	СГРУППИРОВАТЬ ПО
			|		УчетнаяПолитикаОрганизаций.Организация
			|	
			|	ИМЕЮЩИЕ
			|		(МИНИМУМ(СледующееСтавкиНалога.Период) >= МИНИМУМ(УчетнаяПолитикаОрганизаций.Начало)
			|			ИЛИ МИНИМУМ(ЕСТЬNULL(СледующееСтавкиНалога.Период, ДАТАВРЕМЯ(1, 1, 1))) = ДАТАВРЕМЯ(1, 1, 1))) КАК ВложенныйЗапрос
			|
			|СГРУППИРОВАТЬ ПО
			|	ВложенныйЗапрос.Организация
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ВложенныйЗапрос.Организация";
		СписокЗапросов.Добавить(ТекстЗапроса);
			
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ИзмененныеДанные.Организация,
	|	ИзмененныеДанные.Документ,
	|	&Операция КАК Операция,
	|	МИНИМУМ(НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(ПериодЗадания.Период, КВАРТАЛ), МЕСЯЦ)) КАК Месяц
	|ИЗ
	|	ИзмененныеДанные КАК ИзмененныеДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодЗадания КАК ПериодЗадания
	|		ПО (ПериодЗадания.Организация = ИзмененныеДанные.Организация)
	|
	|	" + ?(ЕстьОсновныеСредства, "ГДЕ
	|		ИзмененныеДанные.ОсновноеСредство В
	|				(ВЫБРАТЬ
	|					СписокОС.ОсновноеСредство
	|				ИЗ
	|					СписокОС КАК СписокОС)", "") + "
	|СГРУППИРОВАТЬ ПО
	|	ИзмененныеДанные.Организация,
	|	ИзмененныеДанные.Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПериодЗадания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ИзмененныеДанные";
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	Если ЕстьОсновныеСредства Тогда
		СписокЗапросов.Добавить("УНИЧТОЖИТЬ СписокОС");
	КонецЕсли;

	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДанныеТаблиц.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДатаНачалаВеденияРеглУчета", Константы.ДатаНачалаВеденияРеглУчета.Получить());
	Запрос.УстановитьПараметр("ДатаНачалаУчета24", ВнеоборотныеАктивыЛокализация.ДатаНачалаУчетаВнеоборотныхАктивов2_4());
	Запрос.УстановитьПараметр("Операция", Перечисления.ОперацииЗакрытияМесяца.РасчетНалогаНаИмущество);
	Запрос.УстановитьПараметр("ГруппыНедвижимогоИмущества", ВнеоборотныеАктивыКлиентСерверЛокализация.ГруппыНедвижимогоИмущества());
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	РегистрыСведений.ЗаданияКЗакрытиюМесяца.СоздатьЗаписиРегистраПоДаннымВыборки(РезультатЗапроса.Выбрать());
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Формирует задание к расчету транспортного налога, на основании измененных данных.
//
// Параметры:
//  Документ - ДокументОбъект - Ссылка на документ.
//  ДанныеТаблиц - Структура - Содержит список таблиц в которых произошли изменения.
//  ЕстьОсновныеСредства - Булево - Истина, если измененные данные содержат информацию об основных средствах.
//
Процедура СформироватьЗаданиеКРасчетуТранспортногоНалога(Документ, ДанныеТаблиц, ЕстьОсновныеСредства = Неопределено) Экспорт

	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет") Тогда
		Возврат;
	КонецЕсли;

	ТипОбъекта = ТипЗнч(Документ);
	
	ЕстьОсновныеСредства = 
		ТипОбъекта <> Тип("ДокументОбъект.РегламентнаяОперация")
			И Документ <> Неопределено
		ИЛИ ЕстьОсновныеСредства <> Неопределено 
			И ЕстьОсновныеСредства;
	
	ПроверитьЧтоОСЗарегистрировано = 
		ТипОбъекта <> Тип("ДокументОбъект.ОтменаРегистрацииТранспортныхСредств")
		И ТипОбъекта <> Тип("ДокументОбъект.РегистрацияТранспортныхСредств");
	
	СформироватьТаблицыИзмененийДляФормированияЗаданийКРасчетуТранспортногоНалога(ДанныеТаблиц);
	
	СписокТаблиц = Новый Массив;
	СписокТаблиц.Добавить("ИзменениеПараметровОС");
	СписокТаблиц.Добавить("ИзменениеСпособаОтраженияИмущественныхНалогов_ТранспортныйНалог");
	СписокТаблиц.Добавить("ПорядокУплатыНалоговНаМестах");
	СписокТаблиц.Добавить("РегламентнаяОперацияРасчетТранспортногоНалога");
	СписокТаблиц.Добавить("РегистрацияТранспортныхСредств");
	СписокТаблиц.Добавить("СпособыОтраженияРасходовПоИмущественнымНалогам");
	СписокТаблиц.Добавить("РасходыНаПлатон");
	СписокТаблиц.Добавить("РасчетыСПоставщиками_ЗаданияКРасчетуТранспортногоНалога");
	СписокТаблиц.Добавить("РасчетыСПоставщикамиПоДокументам_ЗаданияКРасчетуТранспортногоНалога");
	
	// Версия 2.4
	СписокТаблиц.Добавить("МестонахождениеОС");
	СписокТаблиц.Добавить("ПорядокУчетаОС");
	
	СписокДопПолей = "ОтражатьВРеглУчете" + ?(ЕстьОсновныеСредства, ",ОсновноеСредство", "");
	
	СписокПолейТаблиц = Новый Структура;
	ТаблицыВидНалога = Новый Массив;
	ТаблицыВидНалога.Добавить("СпособыОтраженияРасходовПоИмущественнымНалогам");
	СписокПолейТаблиц.Вставить("ВидНалога", ТаблицыВидНалога);
	
	ТекстОбъединенияДанных = ВнеоборотныеАктивы.СформироватьТекстОбъединенияДанныхДляФормированияЗадания(
									СписокТаблиц, ДанныеТаблиц, СписокДопПолей, СписокПолейТаблиц);
	
	Если ТекстОбъединенияДанных = "" Тогда
		Возврат;
	КонецЕсли;
	
	// Условия формирования задания:
	// - если с начала квартала были зарегистрированы транспортные средства (дата задания - наименьшая дата регистрации)
	// - или если на начало квартала есть зарегистрированные транспортные средства (дата задания - начало квартала).
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МИНИМУМ(ВЫБОР 
	|				КОГДА ИзмененныеДанные.Период < &ДатаНачалаВеденияРеглУчета
	|						И &ДатаНачалаВеденияРеглУчета <> ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА &ДатаНачалаВеденияРеглУчета
	|
	|				КОГДА ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.ВводОстатковВнеоборотныхАктивов)
	|					ТОГДА ДОБАВИТЬКДАТЕ(ВводОстатков22.Дата, МЕСЯЦ, 1)
	|
	|				КОГДА ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.ВводОстатковВнеоборотныхАктивов2_4)
	|					ТОГДА ДОБАВИТЬКДАТЕ(ВводОстатков24.Дата, МЕСЯЦ, 1)
	|
	|				КОГДА ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.РегистрацияТранспортныхСредств)
	|					ТОГДА РегистрацияТранспортныхСредств.Дата
	|
	|				ИНАЧЕ ИзмененныеДанные.Период 
	|			КОНЕЦ)   КАК Период,
	|	ИзмененныеДанные.Организация       КАК Организация,
	|	" + ?(ЕстьОсновныеСредства, "ИзмененныеДанные.ОсновноеСредство КАК ОсновноеСредство,", "") + "
	|	ИзмененныеДанные.Документ          КАК Документ
	|ПОМЕСТИТЬ ИзмененныеДанные
	|ИЗ
	|	(" + ТекстОбъединенияДанных + ") КАК ИзмененныеДанные
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов КАК ВводОстатков22
	|	ПО ВводОстатков22.Ссылка = ИзмененныеДанные.Документ
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов2_4 КАК ВводОстатков24
	|	ПО ВводОстатков24.Ссылка = ИзмененныеДанные.Документ
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
	|	ПО РегистрацияТранспортныхСредств.Ссылка = ИзмененныеДанные.Документ
	|ГДЕ
	|	ИзмененныеДанные.ОтражатьВРеглУчете
	|	И ВЫРАЗИТЬ(ИзмененныеДанные.Организация КАК Справочник.Организации).ЮридическоеФизическоеЛицо <> ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|	И ИзмененныеДанные.ВидНалога В (
	|		ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ТранспортныйНалог),
	|		НЕОПРЕДЕЛЕНО)
	|
	|СГРУППИРОВАТЬ ПО
	|	ИзмененныеДанные.Организация,
	|	" + ?(ЕстьОсновныеСредства, "ИзмененныеДанные.ОсновноеСредство,", "") + "
	|	ИзмененныеДанные.Документ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|	" + ?(ЕстьОсновныеСредства, ",ОсновноеСредство", "");
	
	Если ПроверитьЧтоОСЗарегистрировано Тогда
		
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовВПакете() +
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	РегистрацияТранспортныхСредствПоследние.Организация КАК Организация
			|ПОМЕСТИТЬ РегистрацияТранспортныхСредствНаНачало
			|ИЗ
			|	(ВЫБРАТЬ
			|		РегистрацияТранспортныхСредств.Организация КАК Организация,
			|		РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство,
			|		МАКСИМУМ(РегистрацияТранспортныхСредств.Период) КАК Период
			|	ИЗ
			|		ИзмененныеДанные КАК ИзмененныеДанные
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
			|			ПО (РегистрацияТранспортныхСредств.Организация = ИзмененныеДанные.Организация)
			|				И (РегистрацияТранспортныхСредств.Период < НАЧАЛОПЕРИОДА(ИзмененныеДанные.Период, КВАРТАЛ))
			|				" + ?(ЕстьОсновныеСредства, "И РегистрацияТранспортныхСредств.ОсновноеСредство = ИзмененныеДанные.ОсновноеСредство", "") + "
			|	
			|	СГРУППИРОВАТЬ ПО
			|		РегистрацияТранспортныхСредств.Организация,
			|		РегистрацияТранспортныхСредств.ОсновноеСредство
			|	) КАК РегистрацияТранспортныхСредствПоследние
			|
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
			|		ПО (РегистрацияТранспортныхСредств.Организация = РегистрацияТранспортныхСредствПоследние.Организация)
			|			И (РегистрацияТранспортныхСредств.ОсновноеСредство = РегистрацияТранспортныхСредствПоследние.ОсновноеСредство)
			|			И (РегистрацияТранспортныхСредств.Период = РегистрацияТранспортныхСредствПоследние.Период)
			|ГДЕ
			|	РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу
			|
			|СГРУППИРОВАТЬ ПО
			|	РегистрацияТранспортныхСредствПоследние.Организация
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Организация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	РегистрацияТранспортныхСредств.Организация,
			|	МИНИМУМ(РегистрацияТранспортныхСредств.Период) КАК Период
			|ПОМЕСТИТЬ ПериодЗадания
			|ИЗ
			|	(ВЫБРАТЬ
			|		ИзмененныеДанные.Организация КАК Организация,
			|		МИНИМУМ(НАЧАЛОПЕРИОДА(ИзмененныеДанные.Период, МЕСЯЦ)) КАК Период
			|	ИЗ
			|		ИзмененныеДанные КАК ИзмененныеДанные
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрацияТранспортныхСредствНаНачало КАК РегистрацияТранспортныхСредствНаНачало
			|			ПО (РегистрацияТранспортныхСредствНаНачало.Организация = ИзмененныеДанные.Организация)
			|	
			|	СГРУППИРОВАТЬ ПО
			|		ИзмененныеДанные.Организация
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ИзмененныеДанные.Организация,
			|		МИНИМУМ(НАЧАЛОПЕРИОДА(РегистрацияТранспортныхСредств.Период, МЕСЯЦ))
			|	ИЗ
			|		ИзмененныеДанные КАК ИзмененныеДанные
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
			|			ПО (РегистрацияТранспортныхСредств.Организация = ИзмененныеДанные.Организация)
			|				И (РегистрацияТранспортныхСредств.Период >= НАЧАЛОПЕРИОДА(ИзмененныеДанные.Период, КВАРТАЛ))
			|				И (РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу)
			|				" + ?(ЕстьОсновныеСредства, "И РегистрацияТранспортныхСредств.ОсновноеСредство = ИзмененныеДанные.ОсновноеСредство", "") + "
			|	
			|	СГРУППИРОВАТЬ ПО
			|		ИзмененныеДанные.Организация) КАК РегистрацияТранспортныхСредств
			|
			|СГРУППИРОВАТЬ ПО
			|	РегистрацияТранспортныхСредств.Организация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИзмененныеДанные.Организация,
			|	ИзмененныеДанные.Документ,
			|	&Операция КАК Операция,
			|	МИНИМУМ(НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(ПериодЗадания.Период, КВАРТАЛ), МЕСЯЦ)) КАК Месяц
			|ИЗ
			|	ИзмененныеДанные КАК ИзмененныеДанные
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодЗадания КАК ПериодЗадания
			|		ПО (ПериодЗадания.Организация = ИзмененныеДанные.Организация)
			|
			|СГРУППИРОВАТЬ ПО
			|	ИзмененныеДанные.Организация,
			|	ИзмененныеДанные.Документ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ПериодЗадания
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ИзмененныеДанные";
	Иначе
		
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовВПакете() +
			"ВЫБРАТЬ
			|	ИзмененныеДанные.Организация,
			|	ИзмененныеДанные.Документ,
			|	&Операция КАК Операция,
			|	МИНИМУМ(НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(ИзмененныеДанные.Период, КВАРТАЛ), МЕСЯЦ)) КАК Месяц
			|ИЗ
			|	ИзмененныеДанные КАК ИзмененныеДанные
			|
			|СГРУППИРОВАТЬ ПО
			|	ИзмененныеДанные.Организация,
			|	ИзмененныеДанные.Документ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ИзмененныеДанные";

	КонецЕсли; 
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДанныеТаблиц.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДатаНачалаВеденияРеглУчета", Константы.ДатаНачалаВеденияРеглУчета.Получить());
	Запрос.УстановитьПараметр("Операция", Перечисления.ОперацииЗакрытияМесяца.РасчетТранспортногоНалога);
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	РегистрыСведений.ЗаданияКЗакрытиюМесяца.СоздатьЗаписиРегистраПоДаннымВыборки(РезультатЗапроса.Выбрать());
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Формирует задание к расчету земельного налога, на основании измененных данных.
//
// Параметры:
//  Документ - ДокументОбъект - Ссылка на документ.
//  ДанныеТаблиц - Структура - Содержит список таблиц в которых произошли изменения.
//
Процедура СформироватьЗаданиеКРасчетуЗемельногоНалога(Документ, ДанныеТаблиц) Экспорт
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет") Тогда
		Возврат;
	КонецЕсли;

	ТипОбъекта = ТипЗнч(Документ);
	
	ЕстьОсновныеСредства = 
		ТипОбъекта <> Тип("ДокументОбъект.РегламентнаяОперация")
		И Документ<> Неопределено;
	
	ПроверитьЧтоОСЗарегистрировано = 
		ТипОбъекта <> Тип("ДокументОбъект.ОтменаРегистрацииЗемельныхУчастков")
		И ТипОбъекта <> Тип("ДокументОбъект.РегистрацияЗемельныхУчастков");
	
	СписокТаблиц = Новый Массив;
	СписокТаблиц.Добавить("ИзменениеПараметровОС");
	СписокТаблиц.Добавить("ИзменениеСпособаОтраженияИмущественныхНалогов_ЗемельныйНалог");
	СписокТаблиц.Добавить("ПорядокУплатыНалоговНаМестах");
	СписокТаблиц.Добавить("РегистрацияЗемельныхУчастков");
	СписокТаблиц.Добавить("РегламентнаяОперацияРасчетЗемельногоНалога");
	СписокТаблиц.Добавить("СпособыОтраженияРасходовПоИмущественнымНалогам");
	СписокТаблиц.Добавить("СтавкиНалогаНаИмущество");
	СписокТаблиц.Добавить("СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам");
	
	// Версия 2.4
	СписокТаблиц.Добавить("МестонахождениеОС");
	СписокТаблиц.Добавить("ПорядокУчетаОС");
	
	СписокДопПолей = "ОтражатьВРеглУчете" + ?(ЕстьОсновныеСредства, ",ОсновноеСредство", "");
	
	СписокПолейТаблиц = Новый Структура;
	ТаблицыВидНалога = Новый Массив;
	ТаблицыВидНалога.Добавить("СпособыОтраженияРасходовПоИмущественнымНалогам");
	СписокПолейТаблиц.Вставить("ВидНалога", ТаблицыВидНалога);
	
	ТекстОбъединенияДанных = ВнеоборотныеАктивы.СформироватьТекстОбъединенияДанныхДляФормированияЗадания(
									СписокТаблиц, ДанныеТаблиц, СписокДопПолей, СписокПолейТаблиц);
	
	Если ТекстОбъединенияДанных = "" Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МИНИМУМ(ВЫБОР 
	|				КОГДА ИзмененныеДанные.Период < &ДатаНачалаВеденияРеглУчета
	|						И &ДатаНачалаВеденияРеглУчета <> ДАТАВРЕМЯ(1, 1, 1)
	|					ТОГДА &ДатаНачалаВеденияРеглУчета
	|
	|				КОГДА ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.ВводОстатковВнеоборотныхАктивов)
	|					ТОГДА ДОБАВИТЬКДАТЕ(ВводОстатков22.Дата, МЕСЯЦ, 1)
	|
	|				КОГДА ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.ВводОстатковВнеоборотныхАктивов2_4)
	|					ТОГДА ДОБАВИТЬКДАТЕ(ВводОстатков24.Дата, МЕСЯЦ, 1)
	|
	|				ИНАЧЕ ИзмененныеДанные.Период 
	|			КОНЕЦ)   КАК Период,
	|	ИзмененныеДанные.Организация       КАК Организация,
	|	" + ?(ЕстьОсновныеСредства, "ИзмененныеДанные.ОсновноеСредство КАК ОсновноеСредство,", "") + "
	|	ИзмененныеДанные.Документ          КАК Документ
	|ПОМЕСТИТЬ ИзмененныеДанные
	|ИЗ
	|	(" + ТекстОбъединенияДанных + ") КАК ИзмененныеДанные
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов КАК ВводОстатков22
	|	ПО ВводОстатков22.Ссылка = ИзмененныеДанные.Документ
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов2_4 КАК ВводОстатков24
	|	ПО ВводОстатков24.Ссылка = ИзмененныеДанные.Документ
	|
	|ГДЕ
	|	ИзмененныеДанные.ОтражатьВРеглУчете
	|	И ИзмененныеДанные.ВидНалога В (
	|		ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ЗемельныйНалог),
	|		НЕОПРЕДЕЛЕНО)
	|
	|СГРУППИРОВАТЬ ПО
	|	ИзмененныеДанные.Организация,
	|	" + ?(ЕстьОсновныеСредства, "ИзмененныеДанные.ОсновноеСредство,", "") + "
	|	ИзмененныеДанные.Документ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|	" + ?(ЕстьОсновныеСредства, ",ОсновноеСредство", "");
	
	Если ПроверитьЧтоОСЗарегистрировано Тогда
		
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовВПакете() +
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	РегистрацияЗемельныхУчастковПоследние.Организация КАК Организация
			|ПОМЕСТИТЬ РегистрацияЗемельныхУчастковНаНачало
			|ИЗ
			|	(ВЫБРАТЬ
			|		РегистрацияЗемельныхУчастков.Организация КАК Организация,
			|		РегистрацияЗемельныхУчастков.ОсновноеСредство КАК ОсновноеСредство,
			|		МАКСИМУМ(РегистрацияЗемельныхУчастков.Период) КАК Период
			|	ИЗ
			|		ИзмененныеДанные КАК ИзмененныеДанные
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияЗемельныхУчастков КАК РегистрацияЗемельныхУчастков
			|			ПО (РегистрацияЗемельныхУчастков.Организация = ИзмененныеДанные.Организация)
			|				И (РегистрацияЗемельныхУчастков.Период < НАЧАЛОПЕРИОДА(ИзмененныеДанные.Период, КВАРТАЛ))
			|				" + ?(ЕстьОсновныеСредства, "И РегистрацияЗемельныхУчастков.ОсновноеСредство = ИзмененныеДанные.ОсновноеСредство", "") + "
			|	
			|	СГРУППИРОВАТЬ ПО
			|		РегистрацияЗемельныхУчастков.Организация,
			|		РегистрацияЗемельныхУчастков.ОсновноеСредство
			|	) КАК РегистрацияЗемельныхУчастковПоследние
			|
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияЗемельныхУчастков КАК РегистрацияЗемельныхУчастков
			|		ПО (РегистрацияЗемельныхУчастков.Организация = РегистрацияЗемельныхУчастковПоследние.Организация)
			|			И (РегистрацияЗемельныхУчастков.ОсновноеСредство = РегистрацияЗемельныхУчастковПоследние.ОсновноеСредство)
			|			И (РегистрацияЗемельныхУчастков.Период = РегистрацияЗемельныхУчастковПоследние.Период)
			|ГДЕ
			|	РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу
			|
			|СГРУППИРОВАТЬ ПО
			|	РегистрацияЗемельныхУчастковПоследние.Организация
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Организация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	РегистрацияЗемельныхУчастков.Организация,
			|	МИНИМУМ(РегистрацияЗемельныхУчастков.Период) КАК Период
			|ПОМЕСТИТЬ ПериодЗадания
			|ИЗ
			|	(ВЫБРАТЬ
			|		ИзмененныеДанные.Организация КАК Организация,
			|		МИНИМУМ(НАЧАЛОПЕРИОДА(ИзмененныеДанные.Период, МЕСЯЦ)) КАК Период
			|	ИЗ
			|		ИзмененныеДанные КАК ИзмененныеДанные
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрацияЗемельныхУчастковНаНачало КАК РегистрацияЗемельныхУчастковНаНачало
			|			ПО (РегистрацияЗемельныхУчастковНаНачало.Организация = ИзмененныеДанные.Организация)
			|	
			|	СГРУППИРОВАТЬ ПО
			|		ИзмененныеДанные.Организация
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ИзмененныеДанные.Организация,
			|		МИНИМУМ(НАЧАЛОПЕРИОДА(РегистрацияЗемельныхУчастков.Период, МЕСЯЦ))
			|	ИЗ
			|		ИзмененныеДанные КАК ИзмененныеДанные
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияЗемельныхУчастков КАК РегистрацияЗемельныхУчастков
			|			ПО (РегистрацияЗемельныхУчастков.Организация = ИзмененныеДанные.Организация)
			|				И (РегистрацияЗемельныхУчастков.Период >= НАЧАЛОПЕРИОДА(ИзмененныеДанные.Период, КВАРТАЛ))
			|				И (РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу)
			|				" + ?(ЕстьОсновныеСредства, "И РегистрацияЗемельныхУчастков.ОсновноеСредство = ИзмененныеДанные.ОсновноеСредство", "") + "
			|	
			|	СГРУППИРОВАТЬ ПО
			|		ИзмененныеДанные.Организация) КАК РегистрацияЗемельныхУчастков
			|
			|СГРУППИРОВАТЬ ПО
			|	РегистрацияЗемельныхУчастков.Организация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИзмененныеДанные.Организация,
			|	ИзмененныеДанные.Документ,
			|	&Операция КАК Операция,
			|	МИНИМУМ(НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(ПериодЗадания.Период, КВАРТАЛ), МЕСЯЦ)) КАК Месяц
			|ИЗ
			|	ИзмененныеДанные КАК ИзмененныеДанные
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодЗадания КАК ПериодЗадания
			|		ПО (ПериодЗадания.Организация = ИзмененныеДанные.Организация)
			|
			|СГРУППИРОВАТЬ ПО
			|	ИзмененныеДанные.Организация,
			|	ИзмененныеДанные.Документ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ПериодЗадания
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ИзмененныеДанные";
	Иначе
		
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовВПакете() +
			"ВЫБРАТЬ
			|	ИзмененныеДанные.Организация,
			|	ИзмененныеДанные.Документ,
			|	&Операция КАК Операция,
			|	МИНИМУМ(НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(ИзмененныеДанные.Период, КВАРТАЛ), МЕСЯЦ)) КАК Месяц
			|ИЗ
			|	ИзмененныеДанные КАК ИзмененныеДанные
			|
			|СГРУППИРОВАТЬ ПО
			|	ИзмененныеДанные.Организация,
			|	ИзмененныеДанные.Документ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ИзмененныеДанные";
			
	КонецЕсли; 
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДанныеТаблиц.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДатаНачалаВеденияРеглУчета", Константы.ДатаНачалаВеденияРеглУчета.Получить());
	Запрос.УстановитьПараметр("Операция", Перечисления.ОперацииЗакрытияМесяца.РасчетЗемельногоНалога);
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	РегистрыСведений.ЗаданияКЗакрытиюМесяца.СоздатьЗаписиРегистраПоДаннымВыборки(РезультатЗапроса.Выбрать());
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура СформироватьТаблицыИзмененийДляФормированияЗаданийКРасчетуТранспортногоНалога(ДанныеТаблиц)

	Если НЕ ПолучитьФункциональнуюОпцию("ВестиУчетПлатежейВПлатон") Тогда
		Возврат;
	КонецЕсли;

	Если ДанныеТаблиц.МенеджерВременныхТаблиц.Таблицы.Найти("РасчетыСПоставщикамиИзменения") <> Неопределено Тогда

		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(Изменения.Месяц, МЕСЯЦ)                  КАК Период,
		|	РасходыНаПлатон.Ссылка.Организация                     КАК Организация,
		|	РасходыНаПлатон.ОсновноеСредство                       КАК ОсновноеСредство,
		|	ИСТИНА                                                 КАК ОтражатьВРеглУчете,
		|	ЛОЖЬ                                                   КАК ОтражатьВУпрУчете,
		|	Изменения.Документ                                     КАК Документ
		|ПОМЕСТИТЬ РасчетыСПоставщиками_ЗаданияКРасчетуТранспортногоНалогаИзменение
		|ИЗ
		|	РасчетыСПоставщикамиИзменения КАК Изменения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетОператораСистемыПлатон.ОС КАК РасходыНаПлатон
		|		ПО (РасходыНаПлатон.Ссылка = Изменения.ОбъектРасчетов)
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(Изменения.ОбъектРасчетов) = ТИП(Документ.ОтчетОператораСистемыПлатон)	
		|	И РасходыНаПлатон.Ссылка.Дата < &ДатаПрекращенияДействияЛьготПлатон
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(Изменения.Месяц, МЕСЯЦ)                  КАК Период,
		|	РасходыНаПлатон.Ссылка.Организация                     КАК Организация,
		|	РасходыНаПлатон.ОсновноеСредство                       КАК ОсновноеСредство,
		|	ИСТИНА                                                 КАК ОтражатьВРеглУчете,
		|	ЛОЖЬ                                                   КАК ОтражатьВУпрУчете,
		|	Изменения.Документ                                     КАК Документ
		|ИЗ
		|	РасчетыСПоставщикамиИзменения КАК Изменения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетОператораСистемыПлатон.ОС КАК РасходыНаПлатон
		|		ПО (РасходыНаПлатон.Ссылка = Изменения.Документ)
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(Изменения.Документ) = ТИП(Документ.ОтчетОператораСистемыПлатон)
		|	И РасходыНаПлатон.Ссылка.Дата < &ДатаПрекращенияДействияЛьготПлатон";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.МенеджерВременныхТаблиц = ДанныеТаблиц.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("ДатаПрекращенияДействияЛьготПлатон", ВнеоборотныеАктивыЛокализация.ДатаПрекращенияДействияЛьготПлатон());
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		ДанныеТаблиц.Вставить("РасчетыСПоставщиками_ЗаданияКРасчетуТранспортногоНалогаИзменение", Выборка.Количество > 0);
		
	КонецЕсли;
	
	Если ДанныеТаблиц.МенеджерВременныхТаблиц.Таблицы.Найти("РасчетыСПоставщикамиПоДокументамИзменения") <> Неопределено Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Изменения.Период                                       КАК Период,
		|	РасходыНаПлатон.Ссылка.Организация                     КАК Организация,
		|	РасходыНаПлатон.ОсновноеСредство                       КАК ОсновноеСредство,
		|	ИСТИНА                                                 КАК ОтражатьВРеглУчете,
		|	ЛОЖЬ                                                   КАК ОтражатьВУпрУчете,
		|	Изменения.Регистратор                                  КАК Документ
		|ПОМЕСТИТЬ РасчетыСПоставщикамиПоДокументам_ЗаданияКРасчетуТранспортногоНалогаИзменение
		|ИЗ 
		|	РасчетыСПоставщикамиПоДокументамИзменения КАК Изменения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетОператораСистемыПлатон.ОС КАК РасходыНаПлатон
		|		ПО (РасходыНаПлатон.Ссылка = Изменения.РасчетныйДокумент)
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(Изменения.РасчетныйДокумент) = ТИП(Документ.ОтчетОператораСистемыПлатон) 
		|	И РасходыНаПлатон.Ссылка.Дата < &ДатаПрекращенияДействияЛьготПлатон
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Изменения.Период                                       КАК Период,
		|	РасходыНаПлатон.Ссылка.Организация                     КАК Организация,
		|	РасходыНаПлатон.ОсновноеСредство                       КАК ОсновноеСредство,
		|	ИСТИНА                                                 КАК ОтражатьВРеглУчете,
		|	ЛОЖЬ                                                   КАК ОтражатьВУпрУчете,
		|	Изменения.Регистратор                                  КАК Документ 
		|ИЗ
		|	РасчетыСПоставщикамиПоДокументамИзменения КАК Изменения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетОператораСистемыПлатон.ОС КАК РасходыНаПлатон
		|		ПО (РасходыНаПлатон.Ссылка = Изменения.Регистратор)
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(Изменения.Регистратор) = ТИП(Документ.ОтчетОператораСистемыПлатон)
		|	И РасходыНаПлатон.Ссылка.Дата < &ДатаПрекращенияДействияЛьготПлатон";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.МенеджерВременныхТаблиц = ДанныеТаблиц.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("ДатаПрекращенияДействияЛьготПлатон", ВнеоборотныеАктивыЛокализация.ДатаПрекращенияДействияЛьготПлатон());
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		ДанныеТаблиц.Вставить("РасчетыСПоставщикамиПоДокументам_ЗаданияКРасчетуТранспортногоНалогаИзменение", Выборка.Количество > 0);
		
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти

#Область ПрочиеЭкспортныеПроцедуры

Процедура УточнитьПараметрыРегистрацииОрганизацииВФНС(Параметры, Организация, Период) Экспорт

	ОсновнойНалоговыйОрган = Справочники.РегистрацииВНалоговомОргане.РегистрацияВНалоговомОргане(Организация, Период);
	
	Параметры.Вставить("ОсновнойНалоговыйОрган", ОсновнойНалоговыйОрган);
	Параметры.Вставить("КодПоОКТМО",ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОсновнойНалоговыйОрган, "КодПоОКТМО", Истина));
	
КонецПроцедуры

Функция ПустаяСправкаРасчет(ИмяРегистраСведений) Экспорт

	СправкаРасчет = РегистрыСведений[ИмяРегистраСведений].СоздатьНаборЗаписей().ВыгрузитьКолонки();
	
	СправкаРасчет.Колонки.Удалить("Регистратор");
	СправкаРасчет.Колонки.Удалить("Активность");
	СправкаРасчет.Колонки.Удалить("НомерСтроки");
	
	Если ИмяРегистраСведений = "РасчетТранспортногоНалога" Тогда
		СправкаРасчет.Колонки.Добавить("УплачиваютсяАвансы", Новый ОписаниеТипов("Булево"));
	КонецЕсли; 
	
	Возврат СправкаРасчет;
	
КонецФункции

Процедура ДополнитьПараметрыРасчетаНалогаНаИмущество(ПараметрыРасчета, Организация, ПериодРасчета, ДополнительныеПараметрыРасчета) Экспорт

	Если ДополнительныеПараметрыРасчета <> Неопределено
	 	И ДополнительныеПараметрыРасчета.Свойство("МенеджерВременныхТаблиц") Тогда
	 		
		ПараметрыРасчета.МенеджерВременныхТаблиц = ДополнительныеПараметрыРасчета.МенеджерВременныхТаблиц;
		ПараметрыРасчета.Вставить("РасчетПоФактическойСтоимости", Истина);
	Иначе
		ПараметрыРасчета.Вставить("РасчетПоФактическойСтоимости", Ложь);
	КонецЕсли; 
	
КонецПроцедуры

Процедура УстановитьПараметрыЗапросаПриРасчетеНалогаЗаПериод(Период, Запрос) Экспорт

	Запрос.УстановитьПараметр("ИспользуетсяУправлениеВНА_2_4", ВнеоборотныеАктивы.ИспользуетсяУправлениеВНА_2_4(ДобавитьМесяц(Период, -1)));
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Период));
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(Период));
	Запрос.УстановитьПараметр("НачалоПредыдущегоМесяца", ДобавитьМесяц(Период, -1));
	Запрос.УстановитьПараметр("КонецПредыдущегоМесяца", Период-1);
	
КонецПроцедуры

// Процедура формирует временную таблицу в менеджере временных таблиц,
// содержащую сведения о регистрации в налоговых органах, в которые 
// уплачивается авансовые платежи организации по указанному виду налога.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер, в котором создать таблицы.
//	Организация - СправочникСсылка.Организации - организация, для которой определяются авансовые платежи.
//	Период - Дата - Дата, по состоянию на которую определяются сведения.
//	Налог - Строка - Может содержать одно из значений:
//			- "ЗемельныйНалог";
//			- "ТранспортныйНалог";
//			- "НалогНаИмущество".
// 
Процедура СоздатьНалоговыеОрганыСУстановленнойУплатойАвансов(МенеджерВременныхТаблиц, Организация, Период, Налог) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ВидНалога = Перечисления.ВидыИмущественныхНалогов.НалогНаИмущество;
	
	Если Налог = "ЗемельныйНалог" Тогда
		ВидНалога = Перечисления.ВидыИмущественныхНалогов.ЗемельныйНалог;
	ИначеЕсли Налог = "ТранспортныйНалог" Тогда
		ВидНалога = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог;
	КонецЕсли;
	
	ГоловнаяОрганизация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ГоловнаяОрганизация");
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("Период",      Период);
	Запрос.УстановитьПараметр("Налог",       ВидНалога);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РегистрацииВНалоговомОргане.Ссылка КАК НалоговыйОрган
	|ПОМЕСТИТЬ ВТ_ВсеРегистрацииВНалоговомОргане
	|ИЗ
	|	Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|ГДЕ
	|	РегистрацииВНалоговомОргане.Владелец = &ГоловнаяОрганизация
	|	И НЕ РегистрацииВНалоговомОргане.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.РегистрацияВНалоговомОргане КАК НалоговыйОрган
	|ПОМЕСТИТЬ ВТ_ПорядокУплатыНалоговНаМестах
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				И Налог = &Налог
	|				И УплачиваютсяАвансы) КАК ПорядокУплатыНалоговНаМестахСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА ВТ_ПорядокУплатыНалоговНаМестах.НалоговыйОрган = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|			ТОГДА ВТ_ВсеРегистрацииВНалоговомОргане.НалоговыйОрган
	|		ИНАЧЕ ВТ_ПорядокУплатыНалоговНаМестах.НалоговыйОрган
	|	КОНЕЦ КАК НалоговыйОрган
	|ПОМЕСТИТЬ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов
	|ИЗ
	|	ВТ_ПорядокУплатыНалоговНаМестах КАК ВТ_ПорядокУплатыНалоговНаМестах
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВсеРегистрацииВНалоговомОргане КАК ВТ_ВсеРегистрацииВНалоговомОргане
	|		ПО (ВТ_ПорядокУплатыНалоговНаМестах.НалоговыйОрган = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НалоговыйОрган";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура УточнитьПараметрыОсвобожденияОтУплатыНалогов(Параметры, Организация, Период) Экспорт
	
	Если Не НалоговыйУчет.ДеятельностьОтнесенаКПострадавшимОтКоронавируса(Организация) Тогда
		Возврат;
	КонецЕсли;
		
	Если Параметры = Неопределено Тогда
		Параметры = Новый Структура;
	КонецЕсли;	
			
	ПериодОсвобожденияОтНалогов = НалоговыйУчет.ПериодОсвобожденияОтНалоговПострадавшимОтКоронавируса();
			
	Параметры.Вставить("ПрименяетсяОсвобождениеОтУплатыНалогаВНалогомПериоде", 
		Год(ПериодОсвобожденияОтНалогов.Начало) = Год(Период));
		
	Параметры.Вставить("ПрименяетсяОсвобождениеОтУплатыНалогаВТекущемПериоде", 
	    ПериодОсвобожденияОтНалогов.Начало <= Период 
		И Период <= ПериодОсвобожденияОтНалогов.Конец);
	
	Параметры.Вставить("ГодовойРасчет", Месяц(Период) = 12);
	
КонецПроцедуры

Функция ПодготовитьТаблицыРасчетНалога(ТаблицаРеквизиты, Отказ, ДополнительныеПараметры = Неопределено) Экспорт
	
	СтруктураТаблиц = Новый Структура();
	СтруктураТаблиц.Вставить("ТаблицаСправкиРасчета");
	СтруктураТаблиц.Вставить("ТаблицаРасходыПоНалогу");
	СтруктураТаблиц.Вставить("ТаблицаРасходыНаПлатон");
	
	Параметры = ПодготовитьПараметрыРасчетНалога(ТаблицаРеквизиты);
	Если Параметры.Реквизиты.Количество() = 0 Тогда
		Возврат СтруктураТаблиц;
	КонецЕсли;
	
	Реквизиты = Параметры.Реквизиты[0];
	ТаблицыРасчетНалога = ТаблицыРасчетНалога(Реквизиты, ДополнительныеПараметры);
	
	Результат = РасходыПоНалогу(
					Реквизиты, 
					ТаблицыРасчетНалога.СправкаРасчет, 
					ТаблицыРасчетНалога.РасходыНаПлатон, 
					ДополнительныеПараметры,
					Отказ);
	
	ПроверитьСправкуРасчет(ТаблицыРасчетНалога.СправкаРасчет, ДополнительныеПараметры.СписокОшибок);
	
	СтруктураТаблиц.Вставить("ТаблицаСправкиРасчета",      ТаблицыРасчетНалога.СправкаРасчет);
	СтруктураТаблиц.Вставить("ТаблицаРасходыПоНалогу",     Результат.ПрочиеРасходы);
	СтруктураТаблиц.Вставить("ТаблицаПрочиеАктивыПассивы", Результат.ПрочиеАктивыПассивы);
	СтруктураТаблиц.Вставить("ТаблицаДвиженияПоПрочимАктивамПассивам",          Результат.ДвиженияПоПрочимАктивамПассивам);
	СтруктураТаблиц.Вставить("ТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы", Результат.ДвиженияДоходыРасходыПрочиеАктивыПассивы);
	СтруктураТаблиц.Вставить("ТаблицаРасходыНаПлатон",     ТаблицыРасчетНалога.РасходыНаПлатон);
	
	Возврат СтруктураТаблиц;

КонецФункции

Процедура СформироватьДвиженияРасчетНалога(ТаблицыНачислениеНалога, ТаблицаРеквизитов, Движения, Отказ) Экспорт

	Параметры = ПодготовитьПараметрыДвиженийРасчетНалога(ТаблицаРеквизитов);
	Если Параметры.Реквизиты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = Параметры.Реквизиты[0];
	
	РасчетИмущественныхНалогов.ДополнитьПараметрыДвиженийСправкаРасчет(Параметры, Реквизиты, ТаблицыНачислениеНалога.ТаблицаСправкиРасчета);
	
	// Данные для справки-расчета
	ЗакрытиеМесяца.ЗаписьВоВспомогательныеРегистрыСведений(
		Движения, 
		Параметры.СправкаРасчет, 
		Параметры.Реквизиты, 
		Реквизиты.ИмяРегистраРасчетНалогов);
		
	Если ТаблицыНачислениеНалога.ТаблицаРасходыПоНалогу.Количество() <> 0 Тогда
		Движения.ПрочиеРасходы.Записывать = Истина;
		Движения.ПрочиеРасходы.Загрузить(ТаблицыНачислениеНалога.ТаблицаРасходыПоНалогу);
	КонецЕсли;
	
	Если ТаблицыНачислениеНалога.ТаблицаПрочиеАктивыПассивы.Количество() <> 0 Тогда
		Движения.ПрочиеАктивыПассивы.Записывать = Истина;
		Движения.ПрочиеАктивыПассивы.Загрузить(ТаблицыНачислениеНалога.ТаблицаПрочиеАктивыПассивы);
	КонецЕсли;
	
	Если ТаблицыНачислениеНалога.ТаблицаДвиженияПоПрочимАктивамПассивам.Количество() <> 0 Тогда
		Движения.ДвиженияПоПрочимАктивамПассивам.Записывать = Истина;
		Движения.ДвиженияПоПрочимАктивамПассивам.Загрузить(ТаблицыНачислениеНалога.ТаблицаДвиженияПоПрочимАктивамПассивам);
	КонецЕсли;
	
	Если ТаблицыНачислениеНалога.ТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы.Количество() <> 0 Тогда
		Движения.ДвиженияДоходыРасходыПрочиеАктивыПассивы.Записывать = Истина;
		Движения.ДвиженияДоходыРасходыПрочиеАктивыПассивы.Загрузить(ТаблицыНачислениеНалога.ТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы);
	КонецЕсли;

КонецПроцедуры

Процедура ДополнитьПараметрыДвиженийСправкаРасчет(Реквизиты, СписокОбязательныхКолонок) Экспорт

	Если Реквизиты.ВидНалога = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог Тогда
		СписокОбязательныхКолонок = СписокОбязательныхКолонок
		+ ",СуммаПлатыОператоруСистемыПлатон";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ПодготовитьПараметрыДвиженийРасчетНалога(ТаблицаРеквизитов)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы Параметры.Реквизиты
	
	СписокОбязательныхКолонок = ""
	+ "Период,"						// <Дата>
	+ "Организация,"				// <СправочникСсылка.Организации>
	+ "Регистратор,"				// <ДокументСсылка.*>
	+ "ВидНалога,"					// <ПеречислениеСсылка.ВидыИмущественныхНалогов>
	+ "ИмяРегистраРасчетНалогов"	// <Строка, 0>
	;
	
	Параметры.Вставить("Реквизиты", 
		УправлениеВнеоборотнымиАктивамиПереопределяемый.ПараметрыПроведения(ТаблицаРеквизитов, СписокОбязательныхКолонок));
	
	Возврат Параметры;
	
КонецФункции

Функция ПодготовитьПараметрыРасчетНалога(ТаблицаРеквизиты) Экспорт
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы Параметры.Реквизиты
	
	СписокОбязательныхКолонок = ""
	+ "Период,"						// <Дата>
	+ "Организация,"				// <СправочникСсылка.Организации>
	+ "Регистратор,"				// <ДокументСсылка.*>
	+ "ВидНалога,"					// <Перечисление.ВидыИмущественныхНалогов>
	+ "Содержание,"					// <Строка, 150>
	+ "ИмяРегистраРасчетНалогов"	// <Строка, 0>
	;
	
	Параметры.Вставить("Реквизиты", 
		УправлениеВнеоборотнымиАктивамиПереопределяемый.ПараметрыПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));
	
	Возврат Параметры;
	
КонецФункции

Функция ТаблицыРасчетНалога(Реквизиты, ДополнительныеПараметры)
	
	СправкаРасчет = РасчетИмущественныхНалогов.ПустаяСправкаРасчет(Реквизиты.ИмяРегистраРасчетНалогов);
	РасходыНаПлатон = ПустаяТаблицаРасходыНаПлатон();
	
	Если Реквизиты.ВидНалога = Перечисления.ВидыИмущественныхНалогов.НалогНаИмущество Тогда
		
		РасчетПоНалогу = РасчетИмущественныхНалогов.ПолучитьРасчетПоНалогуНаИмущество(
							Реквизиты.Организация, Реквизиты.Период, ДополнительныеПараметры);
		
	ИначеЕсли Реквизиты.ВидНалога = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог Тогда
		
		РасчетыПоНалогу = РасчетИмущественныхНалогов.ПолучитьРасчетПоТранспортномуНалогу(
							Реквизиты.Организация, Реквизиты.Период, ДополнительныеПараметры);
							
		РасчетПоНалогу = РасчетыПоНалогу.Налог;
		
		Если РасчетыПоНалогу.Платон <> Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РасчетыПоНалогу.Платон, РасходыНаПлатон);
		КонецЕсли;
		
		ПроверитьРасчетТранспортногоНалога(РасчетыПоНалогу, ДополнительныеПараметры.СписокОшибок);
		
	ИначеЕсли Реквизиты.ВидНалога = Перечисления.ВидыИмущественныхНалогов.ЗемельныйНалог Тогда
		
		РасчетПоНалогу = РасчетИмущественныхНалогов.ПолучитьРасчетПоЗемельномуНалогу(
							Реквизиты.Организация, Реквизиты.Период, ДополнительныеПараметры);
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РасчетПоНалогу, СправкаРасчет);	
	
	ТаблицыРасчетНалога = Новый Структура;
	ТаблицыРасчетНалога.Вставить("СправкаРасчет", СправкаРасчет);
	ТаблицыРасчетНалога.Вставить("РасходыНаПлатон", РасходыНаПлатон);
	
	Возврат ТаблицыРасчетНалога;
	
КонецФункции

Процедура ПроверитьСправкуРасчет(СправкаРасчет, СписокОшибок)

	ДанныеСтроки = СправкаРасчет.Найти("", "КодПоОКТМО");
	Если ДанныеСтроки <> Неопределено Тогда
		
		ТекстСообщения = НСтр("ru = 'Не указан код по ОКТМО при регистрации в налоговом органе ""%1""';
								|en = 'RNCMT code for registration in the tax authority ""%1"" is not specified.'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Строка(ДанныеСтроки.ИФНС));
		
		ВнеоборотныеАктивыСлужебный.ДобавитьОписаниеОшибки(
			ТекстСообщения, 
			ДанныеСтроки.ИФНС,
			ДанныеСтроки.Организация,
			СписокОшибок);
			
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПроверитьРасчетТранспортногоНалога(РасчетыПоНалогу, СписокОшибок)

	СтрокиБезСтавки = РасчетыПоНалогу.Налог.НайтиСтроки(Новый Структура("ОтсутствуетСтавка", Истина));
	Для каждого ДанныеСтроки Из СтрокиБезСтавки Цикл
		
		ТекстСообщения = НСтр("ru = 'Не удалось определить ставку налога для транспортного средства ""%1""';
								|en = 'Cannot determine tax rate for vehicle ""%1""'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Строка(ДанныеСтроки.ОсновноеСредство));
		
		ВнеоборотныеАктивыСлужебный.ДобавитьОписаниеОшибки(
			ТекстСообщения, 
			ДанныеСтроки.ОсновноеСредство,
			ДанныеСтроки.Организация,
			СписокОшибок);
		
	КонецЦикла;

КонецПроцедуры

Функция РасходыПоНалогу(Реквизиты, СправкаРасчет, РасходыНаПлатон, ДополнительныеПараметры, Отказ)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СправкаРасчет.ОсновноеСредство КАК ОсновноеСредство,
	|	СправкаРасчет.ИФНС КАК РегистрацияВНалоговомОргане,
	|
	|	&СуммаНалогаКУплате КАК СуммаРегл,
	|
	|	&СуммаНалогаКУплате * &КоэффициентПересчетаВУпр КАК СуммаБезНДС,
	|
	|	&СуммаНалогаКУплате * &КоэффициентПересчетаВУпр КАК Сумма,
	|
	|	ВЫБОР 
	|		КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА (&СуммаНалогаКУплате) * &КоэффициентПересчетаВУпр
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаУпр
	|
	|ПОМЕСТИТЬ СправкаРасчет
	|ИЗ
	|	&СправкаРасчет КАК СправкаРасчет
	|ГДЕ
	|	&СуммаНалогаКУплате <> 0
	|	И &ФормироватьПроводкиПоНалогу
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|ВЫБРАТЬ
	|	РасходыНаПлатон.ОсновноеСредство КАК ОсновноеСредство,
	|	РасходыНаПлатон.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	РасходыНаПлатон.Подразделение КАК Подразделение,
	|	РасходыНаПлатон.СтатьяРасходов КАК СтатьяРасходов,
	|	РасходыНаПлатон.АналитикаРасходов КАК АналитикаРасходов,
	|
	|	РасходыНаПлатон.СуммаНУДт КАК СуммаНУ,
	|	РасходыНаПлатон.СуммаНУКт - РасходыНаПлатон.СуммаНУДт КАК ПостояннаяРазница,
	|	-РасходыНаПлатон.СуммаНУКт КАК ВременнаяРазница
	|
	|ПОМЕСТИТЬ РасходыНаПлатон
	|ИЗ
	|	&РасходыНаПлатон КАК РасходыНаПлатон
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|ВЫБРАТЬ
	|	ПорядокУчетаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ПорядокУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ ПорядокУчетаОС
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОС.СрезПоследних(
	|		&Период, 
	|		ОсновноеСредство В (
	|			ВЫБРАТЬ 
	|				СправкаРасчет.ОсновноеСредство 
	|			ИЗ 
	|				СправкаРасчет КАК СправкаРасчет)) КАК ПорядокУчетаОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|ВЫБРАТЬ
	|	СпособыОтраженияРасходов.ОсновноеСредство КАК ОсновноеСредство,
	|	СпособыОтраженияРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|	СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом КАК СпособОтраженияРасходовЗаданДокументом,
	|	СпособыОтраженияРасходов.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	СпособыОтраженияРасходов.АналитикаРасходов КАК АналитикаРасходов
	|ПОМЕСТИТЬ СпособыОтраженияРасходов
	|ИЗ
	|	РегистрСведений.СпособыОтраженияРасходовПоИмущественнымНалогам.СрезПоследних(
	|		&Период, 
	|		ОсновноеСредство В (
	|			ВЫБРАТЬ 
	|				СправкаРасчет.ОсновноеСредство 
	|			ИЗ 
	|				СправкаРасчет КАК СправкаРасчет)
	|		И Организация = &Организация
	|		И ВидНалога = &ВидНалога) КАК СпособыОтраженияРасходов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	СпособОтраженияРасходов
	|;
	|
	|ВЫБРАТЬ
	|	МестонахождениеОС.ОсновноеСредство КАК ОсновноеСредство,
	|	МестонахождениеОС.Местонахождение КАК Подразделение
	|ПОМЕСТИТЬ МестонахождениеОС
	|ИЗ
	|	РегистрСведений.МестонахождениеОС.СрезПоследних(
	|		&Период, 
	|		ОсновноеСредство В (
	|			ВЫБРАТЬ 
	|				СправкаРасчет.ОсновноеСредство 
	|			ИЗ 
	|				СправкаРасчет КАК СправкаРасчет)
	|		И Организация = &Организация) КАК МестонахождениеОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ПорядокУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СправкаРасчет.ОсновноеСредство КАК ОсновноеСредство,
	|	СправкаРасчет.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	СправкаРасчет.СуммаРегл КАК СуммаРегл,
	|	СправкаРасчет.Сумма КАК Сумма,
	|	СправкаРасчет.СуммаБезНДС КАК СуммаБезНДС,
	|	СправкаРасчет.СуммаУпр КАК СуммаУпр,
	|	ВЫБОР КОГДА НЕ ЕСТЬNULL(СпособыОтраженияРасходов.СтатьяРасходов.ПринятиеКНалоговомуУчету, ИСТИНА) ТОГДА
	|		СправкаРасчет.СуммаРегл
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	МестонахождениеОС.Подразделение КАК Подразделение,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.СтатьяРасходов, НЕОПРЕДЕЛЕНО) КАК СтатьяРасходов,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.АналитикаРасходов, НЕОПРЕДЕЛЕНО) КАК АналитикаРасходов,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	СправкаРасчет КАК СправкаРасчет
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОС КАК ПорядокУчетаОС
	|	ПО СправкаРасчет.ОсновноеСредство = ПорядокУчетаОС.ОсновноеСредство
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ СпособыОтраженияРасходов КАК СпособыОтраженияРасходов
	|	ПО СправкаРасчет.ОсновноеСредство = СпособыОтраженияРасходов.ОсновноеСредство
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОС КАК МестонахождениеОС
	|	ПО СправкаРасчет.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОсновныеСредства
	|	ПО СправкаРасчет.ОсновноеСредство = ОсновныеСредства.Ссылка
	|	
	|ГДЕ
	|	НЕ ЕСТЬNULL(СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом, ЛОЖЬ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	РасходыНаПлатон.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	РасходыНаПлатон.ОсновноеСредство КАК ОсновноеСредство,
	|	НЕОПРЕДЕЛЕНО КАК РегистрацияВНалоговомОргане,
	|	0 КАК СуммаРегл,
	|	0 КАК Сумма,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаУпр,
	|	ВЫБОР КОГДА НЕ ВЫРАЗИТЬ(РасходыНаПлатон.СтатьяРасходов КАК ПланВидовХарактеристик.СтатьиРасходов).ПринятиеКНалоговомуУчету ТОГДА
	|		РасходыНаПлатон.ПостояннаяРазница + РасходыНаПлатон.СуммаНУ
	|	ИНАЧЕ
	|		РасходыНаПлатон.ПостояннаяРазница
	|	КОНЕЦ КАК ПостояннаяРазница,
	|	РасходыНаПлатон.ВременнаяРазница КАК ВременнаяРазница,
	|	РасходыНаПлатон.Подразделение КАК Подразделение,
	|	РасходыНаПлатон.СтатьяРасходов КАК СтатьяРасходов,
	|	РасходыНаПлатон.АналитикаРасходов КАК АналитикаРасходов,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРБП),
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.РаспределениеРБП)
	|ИЗ
	|	РасходыНаПлатон КАК РасходыНаПлатон
	|;
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ПорядокУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	МестонахождениеОС.Подразделение КАК Местонахождение,
	|	СправкаРасчет.ОсновноеСредство КАК ОсновноеСредство,
	|	СправкаРасчет.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	СправкаРасчет.Сумма КАК Сумма,
	|	СправкаРасчет.СуммаРегл КАК СуммаРегл,
	|	СправкаРасчет.СуммаБезНДС КАК СуммаБезНДС,
	|	СправкаРасчет.СуммаУпр КАК СуммаУпр,
	|	ИзменениеПараметровОС.ОтражениеАмортизационныхРасходов.(
	|		Ссылка КАК Ссылка,
	|		Подразделение КАК Подразделение,
	|		НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		СтатьяРасходовНалог КАК СтатьяРасходов,
	|		АналитикаРасходовНалог КАК АналитикаРасходов,
	|		ЕСТЬNULL(СтатьяРасходовНалог.ПринятиеКНалоговомуУчету, ИСТИНА) КАК ПринятиеКНалоговомуУчету,
	|		Коэффициент КАК Коэффициент
	|	) КАК ТаблицаРаспределения,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	СправкаРасчет КАК СправкаРасчет
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО СправкаРасчет.ОсновноеСредство = ПорядокУчетаОС.ОсновноеСредство
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпособыОтраженияРасходов КАК СпособыОтраженияРасходов
	|		ПО СправкаРасчет.ОсновноеСредство = СпособыОтраженияРасходов.ОсновноеСредство
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровОС КАК ИзменениеПараметровОС
	|		ПО СпособыОтраженияРасходов.СпособОтраженияРасходов = ИзменениеПараметровОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОсновныеСредства
	|		ПО СправкаРасчет.ОсновноеСредство = ОсновныеСредства.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОС КАК МестонахождениеОС
	|		ПО СправкаРасчет.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
	|	
	|ГДЕ
	|	ЕСТЬNULL(СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом, ЛОЖЬ)
	|	И НЕ ИзменениеПараметровОС.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ПорядокУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	МестонахождениеОС.Подразделение КАК Местонахождение,
	|	СправкаРасчет.ОсновноеСредство КАК ОсновноеСредство,
	|	СправкаРасчет.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	СправкаРасчет.Сумма КАК Сумма,
	|	СправкаРасчет.СуммаРегл КАК СуммаРегл,
	|	СправкаРасчет.СуммаБезНДС КАК СуммаБезНДС,
	|	СправкаРасчет.СуммаУпр КАК СуммаУпр,
	|	ДокументСпособ.ОтражениеРасходов.(
	|		Ссылка КАК Ссылка,
	|		Подразделение КАК Подразделение,
	|		НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		СтатьяРасходов КАК СтатьяРасходов,
	|		АналитикаРасходов КАК АналитикаРасходов,
	|		ЕСТЬNULL(СтатьяРасходов.ПринятиеКНалоговомуУчету, ИСТИНА) КАК ПринятиеКНалоговомуУчету,
	|		Коэффициент КАК Коэффициент
	|	) КАК ТаблицаРаспределения,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	СправкаРасчет КАК СправкаРасчет
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО СправкаРасчет.ОсновноеСредство = ПорядокУчетаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпособыОтраженияРасходов КАК СпособыОтраженияРасходов
	|		ПО СправкаРасчет.ОсновноеСредство = СпособыОтраженияРасходов.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РегистрацияЗемельныхУчастков КАК ДокументСпособ
	|		ПО СпособыОтраженияРасходов.СпособОтраженияРасходов = ДокументСпособ.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОсновныеСредства
	|		ПО СправкаРасчет.ОсновноеСредство = ОсновныеСредства.Ссылка
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОС КАК МестонахождениеОС
	|		ПО СправкаРасчет.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
	|	
	|ГДЕ
	|	ЕСТЬNULL(СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом, ЛОЖЬ)
	|	И НЕ ДокументСпособ.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ПорядокУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	МестонахождениеОС.Подразделение КАК Местонахождение,
	|	СправкаРасчет.ОсновноеСредство КАК ОсновноеСредство,
	|	СправкаРасчет.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	СправкаРасчет.Сумма КАК Сумма,
	|	СправкаРасчет.СуммаРегл КАК СуммаРегл,
	|	СправкаРасчет.СуммаБезНДС КАК СуммаБезНДС,
	|	СправкаРасчет.СуммаУпр КАК СуммаУпр,
	|	ДокументСпособ.ОтражениеРасходов.(
	|		Ссылка КАК Ссылка,
	|		Подразделение КАК Подразделение,
	|		НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		СтатьяРасходов КАК СтатьяРасходов,
	|		АналитикаРасходов КАК АналитикаРасходов,
	|		ЕСТЬNULL(СтатьяРасходов.ПринятиеКНалоговомуУчету, ИСТИНА) КАК ПринятиеКНалоговомуУчету,
	|		Коэффициент КАК Коэффициент
	|	) КАК ТаблицаРаспределения,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	СправкаРасчет КАК СправкаРасчет
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО СправкаРасчет.ОсновноеСредство = ПорядокУчетаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпособыОтраженияРасходов КАК СпособыОтраженияРасходов
	|		ПО СправкаРасчет.ОсновноеСредство = СпособыОтраженияРасходов.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РегистрацияТранспортныхСредств КАК ДокументСпособ
	|		ПО СпособыОтраженияРасходов.СпособОтраженияРасходов = ДокументСпособ.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОсновныеСредства
	|		ПО СправкаРасчет.ОсновноеСредство = ОсновныеСредства.Ссылка
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОС КАК МестонахождениеОС
	|		ПО СправкаРасчет.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
	|	
	|ГДЕ
	|	ЕСТЬNULL(СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом, ЛОЖЬ)
	|	И НЕ ДокументСпособ.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ПорядокУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	МестонахождениеОС.Подразделение КАК Местонахождение,
	|	СправкаРасчет.ОсновноеСредство КАК ОсновноеСредство,
	|	СправкаРасчет.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	СправкаРасчет.Сумма КАК Сумма,
	|	СправкаРасчет.СуммаРегл КАК СуммаРегл,
	|	СправкаРасчет.СуммаБезНДС КАК СуммаБезНДС,
	|	СправкаРасчет.СуммаУпр КАК СуммаУпр,
	|	ДокументСпособ.ОтражениеРасходов.(
	|		Ссылка КАК Ссылка,
	|		Подразделение КАК Подразделение,
	|		НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		СтатьяРасходов КАК СтатьяРасходов,
	|		АналитикаРасходов КАК АналитикаРасходов,
	|		ЕСТЬNULL(СтатьяРасходов.ПринятиеКНалоговомуУчету, ИСТИНА) КАК ПринятиеКНалоговомуУчету,
	|		Коэффициент КАК Коэффициент
	|	) КАК ТаблицаРаспределения,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	СправкаРасчет КАК СправкаРасчет
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО СправкаРасчет.ОсновноеСредство = ПорядокУчетаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпособыОтраженияРасходов КАК СпособыОтраженияРасходов
	|		ПО СправкаРасчет.ОсновноеСредство = СпособыОтраженияРасходов.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РегистрацияПорядкаНалогообложенияПоНалогуНаИмущество КАК ДокументСпособ
	|		ПО СпособыОтраженияРасходов.СпособОтраженияРасходов = ДокументСпособ.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОсновныеСредства
	|		ПО СправкаРасчет.ОсновноеСредство = ОсновныеСредства.Ссылка
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОС КАК МестонахождениеОС
	|		ПО СправкаРасчет.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
	|	
	|ГДЕ
	|	ЕСТЬNULL(СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом, ЛОЖЬ)
	|	И НЕ ДокументСпособ.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ПорядокУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	МестонахождениеОС.Подразделение КАК Местонахождение,
	|	СправкаРасчет.ОсновноеСредство КАК ОсновноеСредство,
	|	СправкаРасчет.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	СправкаРасчет.Сумма КАК Сумма,
	|	СправкаРасчет.СуммаРегл КАК СуммаРегл,
	|	СправкаРасчет.СуммаБезНДС КАК СуммаБезНДС,
	|	СправкаРасчет.СуммаУпр КАК СуммаУпр,
	|	ДокументСпособ.ОтражениеРасходов.(
	|		Ссылка КАК Ссылка,
	|		Подразделение КАК Подразделение,
	|		НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		СтатьяРасходов КАК СтатьяРасходов,
	|		АналитикаРасходов КАК АналитикаРасходов,
	|		ЕСТЬNULL(СтатьяРасходов.ПринятиеКНалоговомуУчету, ИСТИНА) КАК ПринятиеКНалоговомуУчету,
	|		Коэффициент КАК Коэффициент
	|	) КАК ТаблицаРаспределения,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	СправкаРасчет КАК СправкаРасчет
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО СправкаРасчет.ОсновноеСредство = ПорядокУчетаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпособыОтраженияРасходов КАК СпособыОтраженияРасходов
	|		ПО СправкаРасчет.ОсновноеСредство = СпособыОтраженияРасходов.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеСпособаОтраженияИмущественныхНалогов КАК ДокументСпособ
	|		ПО СпособыОтраженияРасходов.СпособОтраженияРасходов = ДокументСпособ.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОсновныеСредства
	|		ПО СправкаРасчет.ОсновноеСредство = ОсновныеСредства.Ссылка
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОС КАК МестонахождениеОС
	|		ПО СправкаРасчет.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
	|	
	|ГДЕ
	|	ЕСТЬNULL(СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом, ЛОЖЬ)
	|	И НЕ ДокументСпособ.Ссылка ЕСТЬ NULL";
	
	УчестьОсвобождениеОтУплатыНалога(ТекстЗапроса, ДополнительныеПараметры);
	
	ВалютаУпр = Константы.ВалютаУправленческогоУчета.Получить();
	ВалютаРегл = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Реквизиты.Организация);

	КоэффициентПересчетаВУпр = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентПересчетаИзВалютыВВалюту(
												ВалютаРегл, ВалютаУпр, Реквизиты.Период);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("СправкаРасчет",   СправкаРасчет);
	Запрос.УстановитьПараметр("РасходыНаПлатон", РасходыНаПлатон);
	Запрос.УстановитьПараметр("Период",          Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация",     Реквизиты.Организация);
	Запрос.УстановитьПараметр("ВидНалога",       Реквизиты.ВидНалога);
	Запрос.УстановитьПараметр("КоэффициентПересчетаВУпр", КоэффициентПересчетаВУпр);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.НачислениеИмущественныхНалогов);
	Запрос.УстановитьПараметр("НастройкаХозяйственнойОперации", Справочники.НастройкиХозяйственныхОпераций.НачислениеИмущественныхНалогов);
	Запрос.УстановитьПараметр("ДатаПрекращенияДействияЛьготПлатон", ВнеоборотныеАктивыЛокализация.ДатаПрекращенияДействияЛьготПлатон());
	
	Запрос.УстановитьПараметр("УправленческийУчетОрганизаций",
		РасчетСебестоимостиПовтИсп.УправленческийУчетОрганизаций(НачалоМесяца(Реквизиты.Период)));
		
	ФормироватьПроводкиПоНалогу =
		ДополнительныеПараметры = Неопределено 
		ИЛИ Не ДополнительныеПараметры.Свойство("ПрименяетсяОсвобождениеОтУплатыНалогаВТекущемПериоде")
		ИЛИ Не ДополнительныеПараметры.ПрименяетсяОсвобождениеОтУплатыНалогаВТекущемПериоде;
			
	Запрос.УстановитьПараметр("ФормироватьПроводкиПоНалогу", ФормироватьПроводкиПоНалогу);

	Результат = Запрос.ВыполнитьПакет();
	
	ВыборкаРасходыБезРаспределения = Результат[Результат.ВГраница()-1].Выбрать();
	ВыборкаРасходыСРаспределением = Результат[Результат.ВГраница()].Выбрать();
	
	ТаблицаРасходовПоНалогу = ТаблицаРасходовПоНалогу(
		Реквизиты.ВидНалога, 
		ВыборкаРасходыБезРаспределения,
		ВыборкаРасходыСРаспределением,
		Отказ,
		ДополнительныеПараметры.СписокОшибок);
	
	Возврат ТаблицаРасходовПоНалогу;
	
КонецФункции

Функция ТаблицаРасходовПоНалогу(ВидНалога, ВыборкаРасходыБезРаспределения, ВыборкаРасходыСРаспределением, Отказ, СписокОшибок = Неопределено) Экспорт

	ПрочиеРасходы = РегистрыНакопления.ПрочиеРасходы.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	ПрочиеАктивыПассивы = РегистрыНакопления.ПрочиеАктивыПассивы.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	ДвиженияПоПрочимАктивамПассивам = РегистрыНакопления.ДвиженияПоПрочимАктивамПассивам.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	ДвиженияДоходыРасходыПрочиеАктивыПассивы = РегистрыНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	
	СтатьяАктивовПассивов = ПланыВидовХарактеристик.СтатьиАктивовПассивов.Налоги;
	АналитикаАктивовПассивов = Неопределено;
	Если ВидНалога = Перечисления.ВидыИмущественныхНалогов.ЗемельныйНалог Тогда
		АналитикаАктивовПассивов = Перечисления.ТипыНалогов.ЗемельныйНалог;
	ИначеЕсли ВидНалога = Перечисления.ВидыИмущественныхНалогов.НалогНаИмущество Тогда
		АналитикаАктивовПассивов = Перечисления.ТипыНалогов.НалогНаИмущество;
	ИначеЕсли ВидНалога = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог Тогда
		АналитикаАктивовПассивов = Перечисления.ТипыНалогов.ТранспортныйНалог;
	КонецЕсли;
	
	Пока ВыборкаРасходыБезРаспределения.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(ВыборкаРасходыБезРаспределения.СтатьяРасходов) Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Для основного средства ""%1"" не установлен способ отражения расходов по налогам.';
									|en = 'Method of recording tax expenses is not set for the ""%1"" fixed asset.'"),
								ВыборкаРасходыБезРаспределения.ОсновноеСредство);
								
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения, 
				ВыборкаРасходыБезРаспределения.ОсновноеСредство,,, 
				Отказ);
				
			ВнеоборотныеАктивыСлужебный.ДобавитьОписаниеОшибки(
				ТекстСообщения, 
				ВыборкаРасходыБезРаспределения.ОсновноеСредство,
				ВыборкаРасходыБезРаспределения.Организация,
				СписокОшибок);
				
		КонецЕсли;
		
		УникальныйИдентификатор = Строка(Новый УникальныйИдентификатор());
		
		Запись = ПрочиеРасходы.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, ВыборкаРасходыБезРаспределения);
		Запись.ИдентификаторФинЗаписи = УникальныйИдентификатор;
		
		Если ВыборкаРасходыБезРаспределения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РаспределениеРБП Тогда
			Запись = ПрочиеРасходы.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, ВыборкаРасходыБезРаспределения);
			Запись.ВидДвижения = ВидДвиженияНакопления.Расход;
			Запись.СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.РасходыНаПлатон;
			Запись.АналитикаРасходов = Неопределено;
			Запись.ПостояннаяРазница = 0;
			Запись.ИдентификаторФинЗаписи = УникальныйИдентификатор;
		КонецЕсли;
		
		Запись = ПрочиеАктивыПассивы.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, ВыборкаРасходыБезРаспределения);
		Запись.Статья = СтатьяАктивовПассивов;
		Запись.Аналитика = АналитикаАктивовПассивов;
		
		Запись = ДвиженияПоПрочимАктивамПассивам.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, ВыборкаРасходыБезРаспределения);
		Запись.Статья = СтатьяАктивовПассивов;
		Запись.Аналитика = АналитикаАктивовПассивов;
		Запись.СуммаУпр  = ВыборкаРасходыБезРаспределения.Сумма;
		Запись.СуммаСНДС = Запись.СуммаБезНДС;
		Запись.ИдентификаторФинЗаписи = УникальныйИдентификатор;
		
		Запись = ДвиженияДоходыРасходыПрочиеАктивыПассивы.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, ВыборкаРасходыБезРаспределения, "Период, ХозяйственнаяОперация, Организация, Сумма, СуммаРегл, СуммаУпр");
		
		Запись.Статья = СтатьяАктивовПассивов;
		Запись.АналитикаАктивовПассивов = АналитикаАктивовПассивов;
		Запись.Подразделение = ВыборкаРасходыБезРаспределения.Подразделение;
		Запись.НаправлениеДеятельности = ВыборкаРасходыБезРаспределения.НаправлениеДеятельности;
		
		Запись.КорСтатья = ВыборкаРасходыБезРаспределения.СтатьяРасходов;
		Запись.КорАналитикаРасходов = ВыборкаРасходыБезРаспределения.АналитикаРасходов;
		Запись.КорПодразделение = ВыборкаРасходыБезРаспределения.Подразделение;
		Запись.КорНаправлениеДеятельности = ВыборкаРасходыБезРаспределения.НаправлениеДеятельности;
		
	КонецЦикла;
	
	Если ВыборкаРасходыСРаспределением <> Неопределено Тогда
		
		Пока ВыборкаРасходыСРаспределением.Следующий() Цикл
			
			ТаблицаРаспределения = ВыборкаРасходыСРаспределением.ТаблицаРаспределения.Выгрузить();
			
			Если ТаблицаРаспределения.Количество() = 0 Тогда
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = 'Для основного средства ""%1"" не установлен способ отражения расходов по налогам.';
										|en = 'Method of recording tax expenses is not set for the ""%1"" fixed asset.'"),
									ВыборкаРасходыСРаспределением.ОсновноеСредство);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения, 
					ВыборкаРасходыСРаспределением.ОсновноеСредство,,, 
					Отказ);
					
				ВнеоборотныеАктивыСлужебный.ДобавитьОписаниеОшибки(
					ТекстСообщения, 
					ВыборкаРасходыСРаспределением.ОсновноеСредство, 
					ВыборкаРасходыСРаспределением.Организация,
					СписокОшибок);
					
			КонецЕсли;
		
			Коэффициенты = ТаблицаРаспределения.ВыгрузитьКолонку("Коэффициент");
			Суммы = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(ВыборкаРасходыСРаспределением.Сумма, Коэффициенты);
			СуммыРегл = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(ВыборкаРасходыСРаспределением.СуммаРегл, Коэффициенты);
			СуммыБезНДС = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(ВыборкаРасходыСРаспределением.СуммаБезНДС, Коэффициенты);
			СуммыУпр = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(ВыборкаРасходыСРаспределением.СуммаУпр, Коэффициенты);
			
			Если Суммы = Неопределено И СуммыРегл = Неопределено И СуммыБезНДС = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Для Индекс = 0 По ТаблицаРаспределения.Количество()-1 Цикл
				
				ПравилоОтражения = ТаблицаРаспределения[Индекс];
				
				Если Не ЗначениеЗаполнено(ПравилоОтражения.Подразделение)
					И Не ЗначениеЗаполнено(ВыборкаРасходыСРаспределением.Местонахождение) Тогда
					
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
										НСтр("ru = 'Для основного средства ""%1"" не удалось определить подразделение, на которое будут отнесены расходы.
                                              |Необходимо либо явно указать подразделение, на которое будут отнесены расходы.
                                              |Либо проверить, что основное средство принято к учету. 
                                              |Способ отражения расходов задан в документе %2.';
                                              |en = 'Cannot determine business unit to which other expenses will be allocated for the ""%1"" fixed asset.
                                              |Clearly specify the business unit to which expenses will be allocated
                                              |or check whether fixed asset is recognized. 
                                              |Expense record method is specified in the %2 document.'"),
										ВыборкаРасходыСРаспределением.ОсновноеСредство,
										ПравилоОтражения.Ссылка);
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						ТекстСообщения, 
						ПравилоОтражения.Ссылка,,, 
						Отказ);
							
					ВнеоборотныеАктивыСлужебный.ДобавитьОписаниеОшибки(
						ТекстСообщения, 
						ПравилоОтражения.Ссылка,
						ВыборкаРасходыСРаспределением.Организация,
						СписокОшибок);
						
				КонецЕсли;
				
				УникальныйИдентификатор = Строка(Новый УникальныйИдентификатор());
				
				// ПрочиеРасходы
				Запись = ПрочиеРасходы.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, ВыборкаРасходыСРаспределением, "Период, Организация, РегистрацияВНалоговомОргане, ХозяйственнаяОперация, НастройкаХозяйственнойОперации");
				ЗаполнитьЗначенияСвойств(Запись, ПравилоОтражения, "СтатьяРасходов, АналитикаРасходов");
				
				Запись.Подразделение = 
					?(ЗначениеЗаполнено(ПравилоОтражения.Подразделение), 
							ПравилоОтражения.Подразделение, 
							ВыборкаРасходыСРаспределением.Местонахождение);
							
				Запись.НаправлениеДеятельности = 
					?(ЗначениеЗаполнено(ПравилоОтражения.НаправлениеДеятельности), 
							ПравилоОтражения.НаправлениеДеятельности, 
							ВыборкаРасходыСРаспределением.НаправлениеДеятельности);
							
				Запись.Сумма = ?(Суммы = Неопределено, 0, Суммы[Индекс]);
				Запись.СуммаРегл = ?(СуммыРегл = Неопределено, 0, СуммыРегл[Индекс]);
				Запись.СуммаБезНДС = ?(СуммыБезНДС = Неопределено, 0, СуммыБезНДС[Индекс]);
				Запись.СуммаУпр = ?(СуммыУпр = Неопределено, 0, СуммыУпр[Индекс]);
				Если НЕ ПравилоОтражения.ПринятиеКНалоговомуУчету Тогда
					Запись.ПостояннаяРазница = Запись.СуммаРегл; 
				КонецЕсли;
				Запись.ИдентификаторФинЗаписи = УникальныйИдентификатор;
				
				// ДвиженияДоходыРасходыПрочиеАктивыПассивы
				Запись = ДвиженияДоходыРасходыПрочиеАктивыПассивы.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, ВыборкаРасходыСРаспределением, "Период, ХозяйственнаяОперация, Организация");
				
				Запись.Статья = СтатьяАктивовПассивов;
				Запись.АналитикаАктивовПассивов = АналитикаАктивовПассивов;
				
				Запись.Подразделение = 
					?(ЗначениеЗаполнено(ПравилоОтражения.Подразделение), 
							ПравилоОтражения.Подразделение, 
							ВыборкаРасходыСРаспределением.Местонахождение);
				
				Запись.НаправлениеДеятельности = 
					?(ЗначениеЗаполнено(ПравилоОтражения.НаправлениеДеятельности), 
							ПравилоОтражения.НаправлениеДеятельности, 
							ВыборкаРасходыСРаспределением.НаправлениеДеятельности);
			
				Запись.КорСтатья = ПравилоОтражения.СтатьяРасходов;
				Запись.КорАналитикаРасходов = ПравилоОтражения.АналитикаРасходов;
				
				Запись.КорПодразделение = 
					?(ЗначениеЗаполнено(ПравилоОтражения.Подразделение), 
							ПравилоОтражения.Подразделение, 
							ВыборкаРасходыСРаспределением.Местонахождение);
				
				Запись.КорНаправлениеДеятельности = 
					?(ЗначениеЗаполнено(ПравилоОтражения.НаправлениеДеятельности), 
							ПравилоОтражения.НаправлениеДеятельности, 
							ВыборкаРасходыСРаспределением.НаправлениеДеятельности);
				
				Запись.Сумма = ?(Суммы = Неопределено, 0, Суммы[Индекс]);
				Запись.СуммаРегл = ?(СуммыРегл = Неопределено, 0, СуммыРегл[Индекс]);
				Запись.СуммаУпр = ?(СуммыУпр = Неопределено, 0, СуммыУпр[Индекс]);
				
				// ПрочиеАктивыПассивы
				Запись = ПрочиеАктивыПассивы.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, ВыборкаРасходыСРаспределением);
				Запись.Статья = СтатьяАктивовПассивов;
				Запись.Аналитика = АналитикаАктивовПассивов;
				
				Запись.Подразделение = 
					?(ЗначениеЗаполнено(ПравилоОтражения.Подразделение), 
							ПравилоОтражения.Подразделение, 
							ВыборкаРасходыСРаспределением.Местонахождение);
				
				Запись.НаправлениеДеятельности = 
					?(ЗначениеЗаполнено(ПравилоОтражения.НаправлениеДеятельности), 
							ПравилоОтражения.НаправлениеДеятельности, 
							ВыборкаРасходыСРаспределением.НаправлениеДеятельности);
							
				Запись.Сумма = ?(Суммы = Неопределено, 0, Суммы[Индекс]);
				
				// ДвиженияПоПрочимАктивамПассивам
				Запись = ДвиженияПоПрочимАктивамПассивам.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, ВыборкаРасходыСРаспределением);
				Запись.Статья = СтатьяАктивовПассивов;
				Запись.Аналитика = АналитикаАктивовПассивов;
				
				Запись.Подразделение = 
					?(ЗначениеЗаполнено(ПравилоОтражения.Подразделение), 
							ПравилоОтражения.Подразделение, 
							ВыборкаРасходыСРаспределением.Местонахождение);
				
				Запись.НаправлениеДеятельности = 
					?(ЗначениеЗаполнено(ПравилоОтражения.НаправлениеДеятельности), 
							ПравилоОтражения.НаправлениеДеятельности, 
							ВыборкаРасходыСРаспределением.НаправлениеДеятельности);
							
				Запись.СуммаРегл   = ?(СуммыРегл = Неопределено, 0, СуммыРегл[Индекс]);
				Запись.СуммаБезНДС = ?(СуммыБезНДС = Неопределено, 0, СуммыБезНДС[Индекс]);
				Запись.СуммаСНДС   = ?(СуммыБезНДС = Неопределено, 0, СуммыБезНДС[Индекс]);
				Запись.СуммаУпр    = ?(СуммыУпр = Неопределено, 0, СуммыУпр[Индекс]);
				
				Если НЕ ПравилоОтражения.ПринятиеКНалоговомуУчету Тогда
					Запись.ПостояннаяРазница = Запись.СуммаРегл; 
				КонецЕсли;
		
				Запись.ИдентификаторФинЗаписи = УникальныйИдентификатор;
				
			КонецЦикла;
					
		КонецЦикла;
	
	КонецЕсли; 
	
	ПрочиеАктивыПассивы.ЗаполнитьЗначения(ВидДвиженияНакопления.Расход, "ВидДвижения");
	КолонкиГруппировки = "ВидДвижения, Период, Организация, Подразделение, НаправлениеДеятельности, Статья, Аналитика";
	КолонкиСуммирования = "Сумма";
	ПрочиеАктивыПассивы.Свернуть(КолонкиГруппировки, КолонкиСуммирования);
	
	ДвиженияПоПрочимАктивамПассивам.ЗаполнитьЗначения(Истина, "Активность");
	ДвиженияПоПрочимАктивамПассивам.ЗаполнитьЗначения(Перечисления.ВидыДвиженийПрочихАктивовПассивов.Кредит, "ДебетКредит");
	
	КолонкиГруппировки = "Период, ХозяйственнаяОперация, Организация, 
		|Подразделение, Статья, АналитикаРасходов, АналитикаДоходов, АналитикаАктивовПассивов, НаправлениеДеятельности,
		|КорПодразделение, КорСтатья, КорАналитикаРасходов, КорАналитикаДоходов, КорАналитикаАктивовПассивов, КорНаправлениеДеятельности";
	КолонкиСуммирования = "Сумма, СуммаРегл, СуммаУпр";
	ДвиженияДоходыРасходыПрочиеАктивыПассивы.Свернуть(КолонкиГруппировки, КолонкиСуммирования);
	
	Результат = Новый Структура;
	Результат.Вставить("ПрочиеРасходы", ПрочиеРасходы(ПрочиеРасходы));
	Результат.Вставить("ПрочиеАктивыПассивы", ПрочиеАктивыПассивы);
	Результат.Вставить("ДвиженияПоПрочимАктивамПассивам", ДвиженияПоПрочимАктивамПассивам);
	Результат.Вставить("ДвиженияДоходыРасходыПрочиеАктивыПассивы", ДвиженияДоходыРасходыПрочиеАктивыПассивы);
	
	Возврат Результат;

КонецФункции

Функция ПрочиеРасходы(ТаблицаПрочиеРасходы)

	Если ТаблицаПрочиеРасходы.Количество() = 0 Тогда
		Возврат ТаблицаПрочиеРасходы;
	КонецЕсли;
	
	ДополнительныеПоля = "РегистрацияВНалоговомОргане";
	
	ТекстыЗапроса = Новый Массив;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РасчетРасходов.Период                        КАК Период,
	|	РасчетРасходов.ВидДвижения                   КАК ВидДвижения,
	|	РасчетРасходов.Организация                   КАК Организация,
	|	РасчетРасходов.Подразделение                 КАК Подразделение,
	|	РасчетРасходов.СтатьяРасходов                КАК СтатьяРасходов,
	|	РасчетРасходов.АналитикаРасходов             КАК АналитикаРасходов,
	|	РасчетРасходов.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	РасчетРасходов.ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
	|
	|	РасчетРасходов.Сумма                         КАК Сумма,
	|	РасчетРасходов.СуммаБезНДС                   КАК СуммаБезНДС,
	|	РасчетРасходов.СуммаУпр                      КАК СуммаУпр,
	|	РасчетРасходов.СуммаРегл                     КАК СуммаРегл,
	|	РасчетРасходов.ПостояннаяРазница             КАК ПостояннаяРазница,
	|	РасчетРасходов.ВременнаяРазница              КАК ВременнаяРазница,
	|
	|	РасчетРасходов.ИдентификаторФинЗаписи         КАК ИдентификаторФинЗаписи,
	|	РасчетРасходов.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
	|	
	|	РасчетРасходов.РегистрацияВНалоговомОргане   КАК РегистрацияВНалоговомОргане
	|
	|ПОМЕСТИТЬ втТаблицаПрочиеРасходы
	|ИЗ
	|	&ТаблицаПрочиеРасходы КАК РасчетРасходов";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	#Область ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы
	ЗапросыРегистра = Новый Массив;
	ЗапросыРегистра.Добавить(РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы(ДополнительныеПоля));
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РасчетРасходов.Период                        КАК Период,
	|	РасчетРасходов.ВидДвижения                   КАК ВидДвижения,
	|	РасчетРасходов.Организация                   КАК Организация,
	|	РасчетРасходов.Подразделение                 КАК Подразделение,
	|	РасчетРасходов.СтатьяРасходов                КАК СтатьяРасходов,
	|	РасчетРасходов.АналитикаРасходов             КАК АналитикаРасходов,
	|	РасчетРасходов.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                 КАК ВидДеятельностиНДС,
	|
	|	РасчетРасходов.Сумма                         КАК СуммаСНДС,
	|	РасчетРасходов.СуммаБезНДС                   КАК СуммаБезНДС,
	|	РасчетРасходов.СуммаУпр                      КАК СуммаБезНДСУпр,
	|
	|	РасчетРасходов.СуммаРегл                     КАК СуммаСНДСРегл,
	|	РасчетРасходов.СуммаРегл                     КАК СуммаБезНДСРегл,
	|	РасчетРасходов.ПостояннаяРазница             КАК ПостояннаяРазница,
	|	РасчетРасходов.ВременнаяРазница              КАК ВременнаяРазница,
	|	РасчетРасходов.ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                 КАК АналитикаУчетаНоменклатуры,
	|
	|	РасчетРасходов.ИдентификаторФинЗаписи         КАК ИдентификаторФинЗаписи,
	|	РасчетРасходов.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
	|	
	|	РасчетРасходов.РегистрацияВНалоговомОргане   КАК РегистрацияВНалоговомОргане
	|ИЗ
	|	втТаблицаПрочиеРасходы КАК РасчетРасходов
	|";
	
	ЗапросыРегистра.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(ЗапросыРегистра, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ТекстЗапросаТаблицаВтПрочиеРасходы
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы(ДополнительныеПоля);
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ТекстЗапросаТаблицаПрочиеРасходы
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы(ДополнительныеПоля);
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаПрочиеРасходы", ТаблицаПрочиеРасходы);
	
	Реквизиты = Новый Структура("Период,Организация", ТаблицаПрочиеРасходы[0].Период, ТаблицаПрочиеРасходы[0].Организация);
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Выгрузить();

КонецФункции

Процедура УчестьОсвобождениеОтУплатыНалога(ТекстЗапроса, ДополнительныеПараметры)
	
	// В 2020 году некоторые организации освобождены от уплаты имущественных налогов (за 2-й квартал).
	// В этом случае рассчитать налог нужно в обычном порядке(требуется при заполнении декларации и/или справки-расчета налога), 
	// а уплачивать не нужно (проводки в части начисления налога за период освобождения не формируются).
	// Законопроект 959325-7
	
	// Для "освобожденных" организаций авансовый платеж за период освобождения (2-й квартал) рассчитывается в обычном порядке 
	// (для тех ИФНС, где платятся авансы), но проводки не формируются.
	// Налог по результатам 2020 года для "освобожденных" организаций рассчитывается следующим образом.
	// Если организация уплачивает ежеквартальные авансы в каких-то регионах (ИФНС), 
	// то в расчете и проводках ничего не меняется: налог, начисленный по результатам года,
	// уменьшается в том числе и на авансовый платеж за период освобождения, как будто организация его заплатила.
	// Для тех регионов (ИФНС), где не уплачиваются авансы, исчисленный за год налог нужно уменьшить на сумму налога за период освобождения (2-й квартал), 
	// рассчитанную аналогично авансу за такой период для тех, кто платит авансы.

	Если ДополнительныеПараметры <> Неопределено
		И ДополнительныеПараметры.Свойство("ГодовойРасчет")
		И ДополнительныеПараметры.ГодовойРасчет
		И ДополнительныеПараметры.Свойство("ПрименяетсяОсвобождениеОтУплатыНалогаВНалогомПериоде")
		И ДополнительныеПараметры.ПрименяетсяОсвобождениеОтУплатыНалогаВНалогомПериоде Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаНалогаКУплате", "(СправкаРасчет.СуммаНалогаКУплате - СправкаРасчет.СуммаОсвобожденияОтУплатыНалога)");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаНалогаКУплате", "СправкаРасчет.СуммаНалогаКУплате");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти