
#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьПараметрыКонтроляСтатьиБюджета(СтатьяБюджета, НаДату = неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СтатьяБюджета", СтатьяБюджета);
	Запрос.УстановитьПараметр("НаДату", НаДату);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПараметрыКонтроляЛимитаСтатейБюджетовСрезПоследних.СтатьяБюджета КАК СтатьяБюджета,
	|	ПараметрыКонтроляЛимитаСтатейБюджетовСрезПоследних.КонтролироватьСтатью КАК КонтролироватьСтатью,
	|	ПараметрыКонтроляЛимитаСтатейБюджетовСрезПоследних.КонтролироватьАналитику1 КАК КонтролироватьАналитику1,
	|	ПараметрыКонтроляЛимитаСтатейБюджетовСрезПоследних.КонтролироватьАналитику2 КАК КонтролироватьАналитику2,
	|	ПараметрыКонтроляЛимитаСтатейБюджетовСрезПоследних.КонтролироватьАналитику3 КАК КонтролироватьАналитику3,
	|	ПараметрыКонтроляЛимитаСтатейБюджетовСрезПоследних.КонтролироватьАналитику4 КАК КонтролироватьАналитику4,
	|	ПараметрыКонтроляЛимитаСтатейБюджетовСрезПоследних.КонтролироватьАналитику5 КАК КонтролироватьАналитику5,
	|	ПараметрыКонтроляЛимитаСтатейБюджетовСрезПоследних.КонтролироватьАналитику6 КАК КонтролироватьАналитику6,
	|	ПараметрыКонтроляЛимитаСтатейБюджетовСрезПоследних.СтатьяБюджета.ВидАналитики1 КАК ВидАналитики1,
	|	ПараметрыКонтроляЛимитаСтатейБюджетовСрезПоследних.СтатьяБюджета.ВидАналитики2 КАК ВидАналитики2,
	|	ПараметрыКонтроляЛимитаСтатейБюджетовСрезПоследних.СтатьяБюджета.ВидАналитики3 КАК ВидАналитики3,
	|	ПараметрыКонтроляЛимитаСтатейБюджетовСрезПоследних.СтатьяБюджета.ВидАналитики4 КАК ВидАналитики4,
	|	ПараметрыКонтроляЛимитаСтатейБюджетовСрезПоследних.СтатьяБюджета.ВидАналитики5 КАК ВидАналитики5,
	|	ПараметрыКонтроляЛимитаСтатейБюджетовСрезПоследних.СтатьяБюджета.ВидАналитики6 КАК ВидАналитики6
	|ИЗ
	|	РегистрСведений.ПараметрыКонтроляЛимитаСтатейБюджетов.СрезПоследних(&НаДату, СтатьяБюджета = &СтатьяБюджета) КАК ПараметрыКонтроляЛимитаСтатейБюджетовСрезПоследних";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Результат = Новый Структура;
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		Результат.Вставить(Колонка.Имя);
	КонецЦикла;
	
	Если РезультатЗапроса.Пустой() Тогда
		Результат.СтатьяБюджета = СтатьяБюджета;
		Результат.КонтролироватьСтатью = Ложь;
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	// Для совместимости со старой структурой данных
	Результат.Вставить("УровеньКонтроляЛимитов", 
		?(Результат.КонтролироватьСтатью, 
			Перечисления.УровниКонтроляЛимитовСтатей.ПоБюджетамИРезервам, 
			Перечисления.УровниКонтроляЛимитовСтатей.НеКонтролируется));
	
	Возврат Результат;
	
КонецФункции

Функция РегистрироватьДефицитРезерва() Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Возврат Константы.РегистрироватьДефицитРезерва.Получить();
КонецФункции

Функция ВалютаЛимитированияЦФО(ЦФО) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЦФО", ЦФО);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПараметрыЛимитированияЦФО.ЦФО КАК ЦФО,
	|	ПараметрыЛимитированияЦФО.ВалютаЛимитирования КАК ВалютаЛимитирования
	|ИЗ
	|	РегистрСведений.ПараметрыЛимитированияЦФО КАК ПараметрыЛимитированияЦФО
	|ГДЕ
	|	ПараметрыЛимитированияЦФО.ЦФО = &ЦФО";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.ВалютаЛимитирования;
	КонецЦикла;
	
	Возврат Справочники.Валюты.ПустаяСсылка();
	
КонецФункции

#КонецОбласти

