
#Область СлужебныеПроцедурыИФункции

Функция ВидЦеныПрайсЛист() Экспорт

	Если ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВидовЦен") Тогда
		Возврат Неопределено;
	Иначе
		Возврат Ценообразование.ВидЦеныПрайсЛист();
	КонецЕсли;

КонецФункции

// Определяет, используется ли ценообразование версии 2.5 на указанную дату.
//
// Параметры:
//	Дата - Дата - дата, для которой надо определить режим ценообразования.
//
// Возвращаемое значение:
//	Булево - признак использования ценообразования версии 2.5 на указанную дату
//	Если дата не указана, то определяется сам факт использования ценообразования версии 2.5.
//
Функция ИспользуетсяЦенообразование25(Дата = Неопределено) Экспорт

	Если НЕ ПолучитьФункциональнуюОпцию("ИспользуетсяЦенообразование25") Тогда
		Возврат Ложь; // ценообразование 2.5 выключено
	КонецЕсли;
	
	Если Дата = Неопределено Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ИспользуетсяЦенообразование25 = (Дата >= ДатаПереходаНаЦенообразованиеВерсии25());
	
	Возврат ИспользуетсяЦенообразование25;

КонецФункции

// Возвращает дату перехода на ценообразование версии 2.5.
// Дата может быть пустой - значит ценообразование версии 2.5 включено для всех периодов.
//
// Возвращаемое значение:
//	Дата - начало дня перехода на ценообразование версии 2.5.
//
Функция ДатаПереходаНаЦенообразованиеВерсии25() Экспорт
	
	ДатаПереходаНаЦенообразованиеВерсии25 = НачалоДня(Константы.ДатаПереходаНаЦенообразование25.Получить());
	
	Возврат ДатаПереходаНаЦенообразованиеВерсии25;
	
КонецФункции

Функция ОбработкаПечатьЭтикетокИЦенников() Экспорт

	ВозвращаемоеЗначение = Неопределено;
	
	Если ИспользуетсяЦенообразование25() Тогда
		ВозвращаемоеЗначение = Обработки.ПечатьЭтикетокИЦенников2_5;
	Иначе
		ВозвращаемоеЗначение = Обработки.ПечатьЭтикетокИЦенников;
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ИмяОбработкиПечатьЭтикетокИЦенников() Экспорт
	
	Если ИспользуетсяЦенообразование25() Тогда
		ИмяОбработки = "Обработка.ПечатьЭтикетокИЦенников2_5";
	Иначе
		ИмяОбработки = "Обработка.ПечатьЭтикетокИЦенников";
	КонецЕсли;
	
	Возврат ИмяОбработки;
	
КонецФункции

Процедура ПолучитьСхемуПоВерсииЦенообразования(СхемаКомпоновкиДанных) Экспорт
	ИспользуетсяЦенообразование25 = ИспользуетсяЦенообразование25();
	счНаборов = СхемаКомпоновкиДанных.НаборыДанных.Количество()-1;
	Если счНаборов > 0 Тогда
		Пока счНаборов >= 0 Цикл
			Набор = СхемаКомпоновкиДанных.НаборыДанных[счНаборов];//НаборДанныхОбъектСхемыКомпоновкиДанных
			Если ИспользуетсяЦенообразование25 Тогда
				Если СтрНайти(Набор.Имя, "25", НаправлениеПоиска.СКонца) = 0  Тогда
					СхемаКомпоновкиДанных.НаборыДанных.Удалить(Набор);
				КонецЕсли;
			Иначе	
				Если НЕ СтрНайти(Набор.Имя, "25", НаправлениеПоиска.СКонца) = 0  Тогда
					СхемаКомпоновкиДанных.НаборыДанных.Удалить(Набор);
				КонецЕсли;
			КонецЕсли;	
			счНаборов = счНаборов - 1;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
