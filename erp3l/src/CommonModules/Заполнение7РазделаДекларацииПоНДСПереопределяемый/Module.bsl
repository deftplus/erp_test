#Область ПрограммныйИнтерфейс

// В процедуре отбираются необлагаемые НДС операции и подтверждающие документы
//
// Параметры:
//	СтруктураПараметров - Структура - Организация, дата.
//	ДанныеДляЗаполнения - Структура - две таблицы: НеоблагаемыеНДСОперации, ПодтверждающиеДокументы.
//
Процедура ЗапросПоДаннымЗаполнения7РазделаДекларацииПоНДС(СтруктураПараметров, ДанныеДляЗаполнения) Экспорт
	
	НеоблагаемыеНДСОперации = ДанныеДляЗаполнения.НеоблагаемыеНДСОперации;
	ПодтверждающиеДокументы = ДанныеДляЗаполнения.ПодтверждающиеДокументы;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация", СтруктураПараметров.Организация);
	Запрос.УстановитьПараметр("ДатаГраница", Новый Граница(КонецДня(СтруктураПараметров.Дата), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоКвартала(СтруктураПараметров.Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецКвартала(СтруктураПараметров.Дата));
	Запрос.УстановитьПараметр("ПустойКод", Справочники.КодыОперацийРаздела7ДекларацииПоНДС.ПустаяСсылка());
	Запрос.УстановитьПараметр("ТипЗапасовСобственные", УчетНДСУПСлужебный.ТипыЗапасовСобственные());
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.Регистратор КАК Регистратор,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот КАК Сумма,
	|	НДСЗаписиКнигиПродажОбороты.Покупатель КАК Покупатель
	|ПОМЕСТИТЬ ПродажиБезНДССводныеСправки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Регистратор,
	|			Организация = &Организация
	|				И СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|				И Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.Реализация)) КАК НДСЗаписиКнигиПродажОбороты
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(НДСЗаписиКнигиПродажОбороты.Регистратор) = ТИП(Документ.СводнаяСправкаНДС)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СводнаяСправкаНДС.Организация КАК Организация,
	|	СводнаяСправкаНДС.Дата КАК КонецПериода,
	|	ВЫБОР
	|		КОГДА СводнаяСправкаНДС.ПериодичностьОформления = ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|			ТОГДА НАЧАЛОПЕРИОДА(СводнаяСправкаНДС.Дата, МЕСЯЦ)
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(СводнаяСправкаНДС.Дата, КВАРТАЛ)
	|	КОНЕЦ КАК НачалоПериода
	|ПОМЕСТИТЬ ПериодыРозничныхПродаж
	|ИЗ
	|	ПродажиБезНДССводныеСправки КАК СписокСчетовФактур
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СводнаяСправкаНДС КАК СводнаяСправкаНДС
	|		ПО СписокСчетовФактур.Регистратор = СводнаяСправкаНДС.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтчетОРозничныхПродажах.КассоваяСмена КАК КассоваяСмена
	|ПОМЕСТИТЬ КассовыеСменыСОтчетами
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыРозничныхПродаж КАК ПериодыРозничныхПродаж
	|		ПО ОтчетОРозничныхПродажах.Организация = ПериодыРозничныхПродаж.Организация
	|			И (ОтчетОРозничныхПродажах.Дата МЕЖДУ ПериодыРозничныхПродаж.НачалоПериода И ПериодыРозничныхПродаж.КонецПериода)
	|ГДЕ
	|	ОтчетОРозничныхПродажах.Проведен
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтчетОРозничныхВозвратах.КассоваяСмена
	|ИЗ
	|	Документ.ОтчетОРозничныхВозвратах КАК ОтчетОРозничныхВозвратах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыРозничныхПродаж КАК ПериодыРозничныхПродаж
	|		ПО ОтчетОРозничныхВозвратах.Организация = ПериодыРозничныхПродаж.Организация
	|			И (ОтчетОРозничныхВозвратах.Дата МЕЖДУ ПериодыРозничныхПродаж.НачалоПериода И ПериодыРозничныхПродаж.КонецПериода)
	|ГДЕ
	|	ОтчетОРозничныхВозвратах.Проведен
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КассоваяСмена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтчетОРозничныхПродажахТовары.Ссылка КАК Регистратор,
	|	ОтчетОРозничныхПродажахТовары.Сумма КАК Сумма,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.РозничныйПокупатель) КАК Покупатель
	|ПОМЕСТИТЬ РозничныеПродажи
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах.Товары КАК ОтчетОРозничныхПродажахТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КассовыеСменыСОтчетами КАК КассовыеСменыСОтчетами
	|		ПО (КассовыеСменыСОтчетами.КассоваяСмена = ОтчетОРозничныхПродажахТовары.Ссылка.КассоваяСмена)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыРозничныхПродаж КАК ПериодыРозничныхПродаж
	|		ПО ОтчетОРозничныхПродажахТовары.Ссылка.Организация = ПериодыРозничныхПродаж.Организация
	|			И (ОтчетОРозничныхПродажахТовары.Ссылка.Дата МЕЖДУ ПериодыРозничныхПродаж.НачалоПериода И ПериодыРозничныхПродаж.КонецПериода)
	|ГДЕ
	|	ОтчетОРозничныхПродажахТовары.Ссылка.Проведен
	|	И ОтчетОРозничныхПродажахТовары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга))
	|	И ОтчетОРозничныхПродажахТовары.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтчетОРозничныхПродажахТовары.Ссылка,
	|	ОтчетОРозничныхПродажахТовары.СуммаСНДС,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.РозничныйПокупатель)
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах.ВидыЗапасов КАК ОтчетОРозничныхПродажахТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КассовыеСменыСОтчетами КАК КассовыеСменыСОтчетами
	|		ПО (КассовыеСменыСОтчетами.КассоваяСмена = ОтчетОРозничныхПродажахТовары.Ссылка.КассоваяСмена)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыРозничныхПродаж КАК ПериодыРозничныхПродаж
	|		ПО ОтчетОРозничныхПродажахТовары.Ссылка.Организация = ПериодыРозничныхПродаж.Организация
	|			И (ОтчетОРозничныхПродажахТовары.Ссылка.Дата МЕЖДУ ПериодыРозничныхПродаж.НачалоПериода И ПериодыРозничныхПродаж.КонецПериода)
	|ГДЕ
	|	ОтчетОРозничныхПродажахТовары.Ссылка.Проведен
	|	И ОтчетОРозничныхПродажахТовары.ВидЗапасов.ТипЗапасов В(&ТипЗапасовСобственные)
	|	И ОтчетОРозничныхПродажахТовары.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтчетКомиссионераТовары.Ссылка,
	|	ОтчетКомиссионераТовары.СуммаСНДС,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.РозничныйПокупатель)
	|ИЗ
	|	Документ.ОтчетКомиссионера.ВидыЗапасов КАК ОтчетКомиссионераТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыРозничныхПродаж КАК ПериодыРозничныхПродаж
	|		ПО ОтчетКомиссионераТовары.Ссылка.Организация = ПериодыРозничныхПродаж.Организация
	|			И (ОтчетКомиссионераТовары.Ссылка.Дата МЕЖДУ ПериодыРозничныхПродаж.НачалоПериода И ПериодыРозничныхПродаж.КонецПериода)
	|ГДЕ
	|	ОтчетКомиссионераТовары.Ссылка.Проведен
	|	И ОтчетКомиссионераТовары.ВидЗапасов.ТипЗапасов В(&ТипЗапасовСобственные)
	|	И (НЕ ОтчетКомиссионераТовары.Покупатель.ЮрФизЛицо В (ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо), ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель))
	|			ИЛИ ОтчетКомиссионераТовары.Покупатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	|	И ОтчетКомиссионераТовары.НомерСчетаФактурыКомиссионера = """"
	|	И ОтчетКомиссионераТовары.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтчетКомиссионераТовары.Ссылка,
	|	ОтчетКомиссионераТовары.СуммаСНДС,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.РозничныйПокупатель)
	|ИЗ
	|	Документ.ОтчетКомиссионера.Товары КАК ОтчетКомиссионераТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыРозничныхПродаж КАК ПериодыРозничныхПродаж
	|		ПО ОтчетКомиссионераТовары.Ссылка.Организация = ПериодыРозничныхПродаж.Организация
	|			И (ОтчетКомиссионераТовары.Ссылка.Дата МЕЖДУ ПериодыРозничныхПродаж.НачалоПериода И ПериодыРозничныхПродаж.КонецПериода)
	|ГДЕ
	|	ОтчетКомиссионераТовары.Ссылка.Проведен
	|	И ОтчетКомиссионераТовары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга))
	|	И (НЕ ОтчетКомиссионераТовары.Покупатель.ЮрФизЛицо В (ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо), ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель))
	|			ИЛИ ОтчетКомиссионераТовары.Покупатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	|	И ОтчетКомиссионераТовары.НомерСчетаФактурыКомиссионера = """"
	|	И ОтчетКомиссионераТовары.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтчетОРозничныхВозвратахТовары.Ссылка,
	|	ОтчетОРозничныхВозвратахТовары.Сумма,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.РозничныйПокупатель)
	|ИЗ
	|	Документ.ОтчетОРозничныхВозвратах.Товары КАК ОтчетОРозничныхВозвратахТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КассовыеСменыСОтчетами КАК КассовыеСменыСОтчетами
	|		ПО (КассовыеСменыСОтчетами.КассоваяСмена = ОтчетОРозничныхВозвратахТовары.Ссылка.КассоваяСмена)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыРозничныхПродаж КАК ПериодыРозничныхПродаж
	|		ПО ОтчетОРозничныхВозвратахТовары.Ссылка.Организация = ПериодыРозничныхПродаж.Организация
	|			И (ОтчетОРозничныхВозвратахТовары.Ссылка.Дата МЕЖДУ ПериодыРозничныхПродаж.НачалоПериода И ПериодыРозничныхПродаж.КонецПериода)
	|			И (ОтчетОРозничныхВозвратахТовары.ДокументРеализации.Дата МЕЖДУ ПериодыРозничныхПродаж.НачалоПериода И ПериодыРозничныхПродаж.КонецПериода)
	|ГДЕ
	|	ОтчетОРозничныхВозвратахТовары.Ссылка.Проведен
	|	И ОтчетОРозничныхВозвратахТовары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга))
	|	И ОтчетОРозничныхВозвратахТовары.ПоЧекуКоррекции
	|	И ОтчетОРозничныхВозвратахТовары.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтчетОРозничныхВозвратахТовары.Ссылка,
	|	ОтчетОРозничныхВозвратахТовары.СуммаСНДС,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.РозничныйПокупатель)
	|ИЗ
	|	Документ.ОтчетОРозничныхВозвратах.ВидыЗапасов КАК ОтчетОРозничныхВозвратахТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КассовыеСменыСОтчетами КАК КассовыеСменыСОтчетами
	|		ПО (КассовыеСменыСОтчетами.КассоваяСмена = ОтчетОРозничныхВозвратахТовары.Ссылка.КассоваяСмена)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыРозничныхПродаж КАК ПериодыРозничныхПродаж
	|		ПО ОтчетОРозничныхВозвратахТовары.Ссылка.Организация = ПериодыРозничныхПродаж.Организация
	|			И (ОтчетОРозничныхВозвратахТовары.Ссылка.Дата МЕЖДУ ПериодыРозничныхПродаж.НачалоПериода И ПериодыРозничныхПродаж.КонецПериода)
	|			И (ОтчетОРозничныхВозвратахТовары.ДокументРеализации.Дата МЕЖДУ ПериодыРозничныхПродаж.НачалоПериода И ПериодыРозничныхПродаж.КонецПериода)
	|ГДЕ
	|	ОтчетОРозничныхВозвратахТовары.Ссылка.Проведен
	|	И ОтчетОРозничныхВозвратахТовары.ВидЗапасов.ТипЗапасов В(&ТипЗапасовСобственные)
	|	И ОтчетОРозничныхВозвратахТовары.ПоЧекуКоррекции
	|	И ОтчетОРозничныхВозвратахТовары.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПериодыРозничныхПродаж
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ КассовыеСменыСОтчетами
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.Регистратор КАК Регистратор,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот КАК Сумма,
	|	НДСЗаписиКнигиПродажОбороты.Покупатель КАК Покупатель
	|ПОМЕСТИТЬ ПродажиБезНДС
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Регистратор,
	|			Организация = &Организация
	|				И СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|				И Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.Реализация)) КАК НДСЗаписиКнигиПродажОбороты
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(НДСЗаписиКнигиПродажОбороты.Регистратор) <> ТИП(Документ.СводнаяСправкаНДС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РозничныеПродажи.Регистратор,
	|	РозничныеПродажи.Сумма,
	|	РозничныеПродажи.Покупатель
	|ИЗ
	|	РозничныеПродажи КАК РозничныеПродажи
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеОснованийСчетовФактур.Регистратор,
	|	ДанныеОснованийСчетовФактур.БазаНДСРегл,
	|	ДанныеОснованийСчетовФактур.Контрагент
	|ИЗ
	|	РегистрСведений.ДанныеОснованийСчетовФактур КАК ДанныеОснованийСчетовФактур
	|
	|ГДЕ
	|	ДанныеОснованийСчетовФактур.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДанныеОснованийСчетовФактур.Организация = &Организация
	|	И ДанныеОснованийСчетовФактур.КорректировкаПоСогласованиюСторон
	|	И ДанныеОснованийСчетовФактур.СуммаБезНДС < 0
	|	И ДанныеОснованийСчетовФактур.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
	|	И ДанныеОснованийСчетовФактур.ТипЗапасов В (&ТипЗапасовСобственные)
	|	И НЕ ДанныеОснованийСчетовФактур.НалогообложениеНДС В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД),
	|														ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.РеализацияРаботУслугНаЭкспорт))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленияПоЗаймамВыданным.Регистратор КАК Регистратор,
	|	НачисленияПоЗаймамВыданным.СуммаРеглОборот КАК Сумма,
	|	ВЫБОР
	|		КОГДА НачисленияПоЗаймамВыданным.Договор.КодРаздел7ДекларацииНДС <> &ПустойКод
	|			ТОГДА НачисленияПоЗаймамВыданным.Договор.КодРаздел7ДекларацииНДС
	|		ИНАЧЕ &ПустойКод
	|	КОНЕЦ КАК КодОперации,
	|	НачисленияПоЗаймамВыданным.Контрагент КАК Контрагент,
	|	НачисленияПоЗаймамВыданным.Договор КАК Договор
	|ПОМЕСТИТЬ НачисленияПоЗаймам
	|ИЗ
	|	РегистрНакопления.ДвиженияКонтрагентДоходыРасходы.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Регистратор,
	|			Организация = &Организация
	|				И ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачисленияПоЗаймамВыданным)) КАК НачисленияПоЗаймамВыданным
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НачисленияПоЗаймамВыданным.Регистратор,
	|	НачисленияПоЗаймамВыданным.СуммаРеглОборот,
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(НачисленияПоЗаймамВыданным.АналитикаДоходов КАК Справочник.ДоговорыКредитовИДепозитов).КодРаздел7ДекларацииНДС <> &ПустойКод
	|			ТОГДА ВЫРАЗИТЬ(НачисленияПоЗаймамВыданным.АналитикаДоходов КАК Справочник.ДоговорыКредитовИДепозитов).КодРаздел7ДекларацииНДС
	|		ИНАЧЕ &ПустойКод
	|	КОНЕЦ,
	|	НачисленияПоЗаймамВыданным.Контрагент,
	|	ВЫРАЗИТЬ(НачисленияПоЗаймамВыданным.АналитикаДоходов КАК Справочник.ДоговорыКредитовИДепозитов)
	|ИЗ
	|	РегистрНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Регистратор,
	|			Организация = &Организация
	|				И ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачисленияПоЗаймамВыданным)) КАК НачисленияПоЗаймамВыданным
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыручкаИСебестоимостьПродажОбороты.Регистратор КАК Регистратор,
	|	СУММА(ВыручкаИСебестоимостьПродажОбороты.СуммаВыручкиРеглОборот) КАК Сумма,
	|	ВЫБОР
	|		КОГДА ВыручкаИСебестоимостьПродажОбороты.Договор.КодРаздел7ДекларацииНДС <> &ПустойКод
	|			ТОГДА ВыручкаИСебестоимостьПродажОбороты.Договор.КодРаздел7ДекларацииНДС
	|		КОГДА ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Номенклатура.КодРаздел7ДекларацииНДС <> &ПустойКод
	|			ТОГДА ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Номенклатура.КодРаздел7ДекларацииНДС
	|		ИНАЧЕ &ПустойКод
	|	КОНЕЦ КАК КодОперации,
	|	ВыручкаИСебестоимостьПродажОбороты.Договор КАК Договор,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ВыручкаИСебестоимостьПродажОбороты.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|					ИЛИ ВыручкаИСебестоимостьПродажОбороты.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ЕстьТовары
	|ПОМЕСТИТЬ ПродажиПоКодам
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(&НачалоПериода, &КонецПериода, Регистратор, АналитикаУчетаПоПартнерам.Организация = &Организация) КАК ВыручкаИСебестоимостьПродажОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПродажиБезНДС КАК ПродажиБезНДС
	|		ПО ВыручкаИСебестоимостьПродажОбороты.Регистратор = ПродажиБезНДС.Регистратор
	|
	|ГДЕ ВыручкаИСебестоимостьПродажОбороты.СуммаВыручкиОборот = ВыручкаИСебестоимостьПродажОбороты.СуммаВыручкиБезНДСОборот
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыручкаИСебестоимостьПродажОбороты.Регистратор,
	|	ВЫБОР
	|		КОГДА ВыручкаИСебестоимостьПродажОбороты.Договор.КодРаздел7ДекларацииНДС <> &ПустойКод
	|			ТОГДА ВыручкаИСебестоимостьПродажОбороты.Договор.КодРаздел7ДекларацииНДС
	|		КОГДА ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Номенклатура.КодРаздел7ДекларацииНДС <> &ПустойКод
	|			ТОГДА ВыручкаИСебестоимостьПродажОбороты.АналитикаУчетаНоменклатуры.Номенклатура.КодРаздел7ДекларацииНДС
	|		ИНАЧЕ &ПустойКод
	|	КОНЕЦ,
	|	ВыручкаИСебестоимостьПродажОбороты.Договор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РеализацияПрочихАктивов.Регистратор,
	|	РеализацияПрочихАктивов.СуммаРеглБезНДСОборот,
	|	ВЫБОР
	|		КОГДА РеализацияПрочихАктивов.Договор.КодРаздел7ДекларацииНДС <> &ПустойКод
	|			ТОГДА РеализацияПрочихАктивов.Договор.КодРаздел7ДекларацииНДС
	|		ИНАЧЕ &ПустойКод
	|	КОНЕЦ,
	|	РеализацияПрочихАктивов.Договор,
	|	0
	|ИЗ
	|	РегистрНакопления.ДвиженияКонтрагентДоходыРасходы.Обороты(&НачалоПериода, &КонецПериода, Регистратор, Организация = &Организация) КАК РеализацияПрочихАктивов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПродажиБезНДС КАК ПродажиБезНДС
	|		ПО РеализацияПрочихАктивов.Регистратор = ПродажиБезНДС.Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗакупкиОбороты.Регистратор,
	|	ЗакупкиОбороты.СуммаРеглБезНДСОборот,
	|	ВЫБОР
	|		КОГДА ЗакупкиОбороты.Договор.КодРаздел7ДекларацииНДС <> &ПустойКод
	|			ТОГДА ЗакупкиОбороты.Договор.КодРаздел7ДекларацииНДС
	|		КОГДА ЗакупкиОбороты.АналитикаУчетаНоменклатуры.Номенклатура.КодРаздел7ДекларацииНДС <> &ПустойКод
	|			ТОГДА ЗакупкиОбороты.АналитикаУчетаНоменклатуры.Номенклатура.КодРаздел7ДекларацииНДС
	|		ИНАЧЕ &ПустойКод
	|	КОНЕЦ,
	|	ЗакупкиОбороты.Договор,
	|	1
	|ИЗ
	|	РегистрНакопления.Закупки.Обороты(&НачалоПериода, &КонецПериода, Регистратор, Организация = &Организация) КАК ЗакупкиОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПродажиБезНДС КАК ПродажиБезНДС
	|		ПО ЗакупкиОбороты.Регистратор = ПродажиБезНДС.Регистратор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|";
	
	Запрос.Выполнить();
	
	// Получим данные партий НДС (ФИФО скользящая)
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	ПродажиПоКодам КАК Т
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор,
	|	Т.Партия,
	|	Т.АналитикаУчетаПартий
	|ПОМЕСТИТЬ ВРегистраторыИПартии
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистраторы КАК Отбор
	|	ПО Т.Регистратор = Отбор.Регистратор
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И Т.Активность";
	
	Запрос.Выполнить();
	
	СтруктураПолей  = Новый Структура;
	СтруктураПолей.Вставить("Регистратор", "Регистратор");
	СтруктураПолей.Вставить("Партия", "Партия");
	СтруктураПолей.Вставить("АналитикаУчетаПартий", "АналитикаУчетаПартий");
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ВРегистраторыИПартии", СтруктураПолей);
	
	РасчетСебестоимостиНДС.СформироватьДетализациюПартийНДС(
		Запрос.МенеджерВременныхТаблиц,
		СтруктураПараметров.Организация,
		НачалоКвартала(СтруктураПараметров.Дата),
		КонецКвартала(СтруктураПараметров.Дата),
		,
		СтруктураОтбора);
		
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПродажиПоКодам.Регистратор КАК Регистратор,
	|	СУММА(ПродажиПоКодам.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ИтогиПоДокументам
	|ИЗ
	|	ПродажиПоКодам КАК ПродажиПоКодам
	|
	|СГРУППИРОВАТЬ ПО
	|	ПродажиПоКодам.Регистратор
	|
	|ИМЕЮЩИЕ
	|	СУММА(ПродажиПоКодам.Сумма) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВложенныйЗапрос.СуммаПриобретения) КАК СуммаПриобретения,
	|	СУММА(ВложенныйЗапрос.НДС) КАК НДС,
	|	ВложенныйЗапрос.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ПрямыеРасходы
	|ИЗ
	|	(ВЫБРАТЬ
	|		СУММА(ВЫБОР
	|				КОГДА ПартииТоваровОрганизаций.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|					ТОГДА ПартииТоваровОрганизаций.СтоимостьРегл
	|				ИНАЧЕ 0
	|			КОНЕЦ) КАК СуммаПриобретения,
	|		СУММА(ВЫБОР
	|				КОГДА ПартииТоваровОрганизаций.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|					ТОГДА 0
	|				ИНАЧЕ ПартииТоваровОрганизаций.НДСРегл
	|			КОНЕЦ) КАК НДС,
	|		ПартииТоваровОрганизаций.Регистратор КАК Регистратор
	|	ИЗ
	|		РегистрНакопления.ПартииТоваровОрганизаций КАК ПартииТоваровОрганизаций
	|	ГДЕ
	|		ПартииТоваровОрганизаций.Регистратор В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Т.Регистратор КАК Регистратор
	|				ИЗ
	|					ПродажиПоКодам КАК Т)
	|		И ПартииТоваровОрганизаций.Активность
	|		И ПартииТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПартииТоваровОрганизаций.Регистратор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СУММА(ВЫБОР
	|				КОГДА ПартииЗатратНаВыпуск.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|					ТОГДА ПартииЗатратНаВыпуск.СтоимостьРегл
	|				ИНАЧЕ 0
	|			КОНЕЦ),
	|		СУММА(ВЫБОР
	|				КОГДА ПартииЗатратНаВыпуск.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|					ТОГДА 0
	|				ИНАЧЕ ПартииЗатратНаВыпуск.НДСРегл
	|			КОНЕЦ),
	|		ПартииЗатратНаВыпуск.Регистратор
	|	ИЗ
	|		РегистрНакопления.ПартииЗатратНаВыпуск КАК ПартииЗатратНаВыпуск
	|	ГДЕ
	|		ПартииЗатратНаВыпуск.Регистратор В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Т.Регистратор КАК Регистратор
	|				ИЗ
	|					ПродажиПоКодам КАК Т)
	|		И ПартииЗатратНаВыпуск.Активность
	|		И ПартииЗатратНаВыпуск.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПартииЗатратНаВыпуск.Регистратор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СУММА(ВЫБОР
	|				КОГДА ПартииРасходовНаСебестоимостьТоваров.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|					ТОГДА ПартииРасходовНаСебестоимостьТоваров.СтоимостьРегл
	|				ИНАЧЕ 0
	|			КОНЕЦ),
	|		СУММА(ВЫБОР
	|				КОГДА ПартииРасходовНаСебестоимостьТоваров.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|					ТОГДА 0
	|				ИНАЧЕ ПартииРасходовНаСебестоимостьТоваров.НДСРегл
	|			КОНЕЦ),
	|		ПартииРасходовНаСебестоимостьТоваров.Регистратор
	|	ИЗ
	|		РегистрНакопления.ПартииРасходовНаСебестоимостьТоваров КАК ПартииРасходовНаСебестоимостьТоваров
	|	ГДЕ
	|		ПартииРасходовНаСебестоимостьТоваров.Регистратор В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Т.Регистратор КАК Регистратор
	|				ИЗ
	|					ПродажиПоКодам КАК Т)
	|		И ПартииРасходовНаСебестоимостьТоваров.Активность
	|		И ПартииРасходовНаСебестоимостьТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПартииРасходовНаСебестоимостьТоваров.Регистратор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СУММА(ВЫБОР
	|				КОГДА ПартииПрочихРасходов.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|					ТОГДА ПартииПрочихРасходов.СтоимостьРегл
	|				ИНАЧЕ 0
	|			КОНЕЦ),
	|		СУММА(ВЫБОР
	|				КОГДА ПартииПрочихРасходов.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|					ТОГДА 0
	|				ИНАЧЕ ПартииПрочихРасходов.НДСРегл
	|			КОНЕЦ),
	|		ПартииПрочихРасходов.Регистратор
	|	ИЗ
	|		РегистрНакопления.ПартииПрочихРасходов КАК ПартииПрочихРасходов
	|	ГДЕ
	|		ПартииПрочихРасходов.Регистратор В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Т.Регистратор КАК Регистратор
	|				ИЗ
	|					ПродажиПоКодам КАК Т)
	|		И ПартииПрочихРасходов.Активность
	|		И ПартииПрочихРасходов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПартииПрочихРасходов.Регистратор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СУММА(ВЫБОР
	|				КОГДА ПартииПроизводственныхЗатрат.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|					ТОГДА ПартииПроизводственныхЗатрат.СтоимостьРегл
	|				ИНАЧЕ 0
	|			КОНЕЦ),
	|		СУММА(ВЫБОР
	|				КОГДА ПартииПроизводственныхЗатрат.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|					ТОГДА 0
	|				ИНАЧЕ ПартииПроизводственныхЗатрат.НДСРегл
	|			КОНЕЦ),
	|		ПартииПроизводственныхЗатрат.Регистратор
	|	ИЗ
	|		РегистрНакопления.ПартииПроизводственныхЗатрат КАК ПартииПроизводственныхЗатрат
	|	ГДЕ
	|		ПартииПроизводственныхЗатрат.Регистратор В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Т.Регистратор КАК Регистратор
	|				ИЗ
	|					ПродажиПоКодам КАК Т)
	|		И ПартииПроизводственныхЗатрат.Активность
	|		И ПартииПроизводственныхЗатрат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПартииПроизводственныхЗатрат.Регистратор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СУММА(ВЫБОР
	|				КОГДА ПартииТоваровПереданныеНаКомиссию.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|					ТОГДА ПартииТоваровПереданныеНаКомиссию.СтоимостьРегл
	|				ИНАЧЕ 0
	|			КОНЕЦ),
	|		СУММА(ВЫБОР
	|				КОГДА ПартииТоваровПереданныеНаКомиссию.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|					ТОГДА 0
	|				ИНАЧЕ ПартииТоваровПереданныеНаКомиссию.НДСРегл
	|			КОНЕЦ),
	|		ПартииТоваровПереданныеНаКомиссию.Регистратор
	|	ИЗ
	|		РегистрНакопления.ПартииТоваровПереданныеНаКомиссию КАК ПартииТоваровПереданныеНаКомиссию
	|	ГДЕ
	|		ПартииТоваровПереданныеНаКомиссию.Регистратор В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Т.Регистратор КАК Регистратор
	|				ИЗ
	|					ПродажиПоКодам КАК Т)
	|		И ПартииТоваровПереданныеНаКомиссию.Активность
	|		И ПартииТоваровПереданныеНаКомиссию.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПартииТоваровПереданныеНаКомиссию.Регистратор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СУММА(ВЫБОР
	|				КОГДА ДетализацияПартийТоваровДляНДСиУСН.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|					ТОГДА ДетализацияПартийТоваровДляНДСиУСН.СтоимостьБезНДС
	|				ИНАЧЕ 0
	|			КОНЕЦ),
	|		СУММА(ВЫБОР
	|				КОГДА ДетализацияПартийТоваровДляНДСиУСН.АналитикаУчетаПартий.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|					ТОГДА 0
	|				ИНАЧЕ ДетализацияПартийТоваровДляНДСиУСН.НДС
	|			КОНЕЦ),
	|		ДетализацияПартийТоваровДляНДСиУСН.Регистратор
	|	ИЗ
	|		РегистрНакопления.ДетализацияПартийТоваровДляНДСиУСН КАК ДетализацияПартийТоваровДляНДСиУСН
	|	ГДЕ
	|		ДетализацияПартийТоваровДляНДСиУСН.Регистратор В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Т.Регистратор КАК Регистратор
	|				ИЗ
	|					ПродажиПоКодам КАК Т)
	|		И ДетализацияПартийТоваровДляНДСиУСН.Активность
	|		И ДетализацияПартийТоваровДляНДСиУСН.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДетализацияПартийТоваровДляНДСиУСН.Регистратор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СУММА(ВЫБОР
	|				КОГДА ДетализацияПартийТоваровДляНДСиУСН.АналитикаУчетаДокументаПоступления.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|					ТОГДА ДетализацияПартийТоваровДляНДСиУСН.СтоимостьБезНДС
	|				ИНАЧЕ 0
	|			КОНЕЦ),
	|		СУММА(ВЫБОР
	|				КОГДА ДетализацияПартийТоваровДляНДСиУСН.АналитикаУчетаДокументаПоступления.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|					ТОГДА 0
	|				ИНАЧЕ ДетализацияПартийТоваровДляНДСиУСН.НДС
	|			КОНЕЦ),
	|		ДетализацияПартийТоваровДляНДСиУСН.Регистратор
	|	ИЗ
	|		РегистрНакопления.ДетализацияПартийТоваровДляНДСиУСН2_4 КАК ДетализацияПартийТоваровДляНДСиУСН
	|	ГДЕ
	|		ДетализацияПартийТоваровДляНДСиУСН.Регистратор В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Т.Регистратор КАК Регистратор
	|				ИЗ
	|					ПродажиПоКодам КАК Т)
	|		И ДетализацияПартийТоваровДляНДСиУСН.Активность
	|		И ДетализацияПартийТоваровДляНДСиУСН.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДетализацияПартийТоваровДляНДСиУСН.Регистратор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СУММА(ВЫБОР
	|				КОГДА ДетализацияПартийТоваровДляНДСиУСН.АналитикаУчетаПартийДокументаПоступления.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|					ТОГДА ДетализацияПартийТоваровДляНДСиУСН.СтоимостьБезНДС
	|				ИНАЧЕ 0
	|			КОНЕЦ),
	|		СУММА(ВЫБОР
	|				КОГДА ДетализацияПартийТоваровДляНДСиУСН.АналитикаУчетаПартийДокументаПоступления.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|					ТОГДА 0
	|				ИНАЧЕ ДетализацияПартийТоваровДляНДСиУСН.НДС
	|			КОНЕЦ),
	|		ДетализацияПартийТоваровДляНДСиУСН.Регистратор
	|	ИЗ
	|		ВТСтоимостьПартийНДС КАК ДетализацияПартийТоваровДляНДСиУСН
	|	ГДЕ
	|		ДетализацияПартийТоваровДляНДСиУСН.Регистратор В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Т.Регистратор КАК Регистратор
	|				ИЗ
	|					ПродажиПоКодам КАК Т)
	|		И ДетализацияПартийТоваровДляНДСиУСН.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДетализацияПартийТоваровДляНДСиУСН.Регистратор
	|) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Регистратор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПродажиБезНДС.Покупатель КАК Контрагент,
	|	ПродажиБезНДС.Регистратор КАК ДокументРеализации,
	|	СУММА(ЕСТЬNULL(ПродажиБезНДС.Сумма * ПродажиПоКодам.Сумма / ИтогиПоДокументам.Сумма, ПродажиБезНДС.Сумма)) КАК СуммаРеализации,
	|	ЕСТЬNULL(ПродажиПоКодам.КодОперации, &ПустойКод) КАК КодОперации,
	|	СУММА(ЕСТЬNULL(ПрямыеРасходы.СуммаПриобретения, 0)) КАК СуммаПриобретения,
	|	СУММА(ЕСТЬNULL(ПрямыеРасходы.НДС, 0)) КАК НДС,
	|	ПродажиПоКодам.Договор КАК Договор,
	|	ПродажиПоКодам.ЕстьТовары КАК ЕстьТовары
	|ПОМЕСТИТЬ Реализация
	|ИЗ
	|	ПродажиБезНДС КАК ПродажиБезНДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИтогиПоДокументам КАК ИтогиПоДокументам
	|		ПО ПродажиБезНДС.Регистратор = ИтогиПоДокументам.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПродажиПоКодам КАК ПродажиПоКодам
	|		ПО ПродажиБезНДС.Регистратор = ПродажиПоКодам.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПрямыеРасходы КАК ПрямыеРасходы
	|		ПО ПродажиБезНДС.Регистратор = ПрямыеРасходы.Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	ПродажиБезНДС.Покупатель,
	|	ПродажиБезНДС.Регистратор,
	|	ЕСТЬNULL(ПродажиПоКодам.КодОперации, &ПустойКод),
	|	ПродажиПоКодам.Договор,
	|	ПродажиПоКодам.ЕстьТовары
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НачисленияПоЗаймам.Контрагент,
	|	НачисленияПоЗаймам.Регистратор,
	|	СУММА(НачисленияПоЗаймам.Сумма),
	|	НачисленияПоЗаймам.КодОперации,
	|	0,
	|	0,
	|	НачисленияПоЗаймам.Договор,
	|	0
	|ИЗ
	|	НачисленияПоЗаймам КАК НачисленияПоЗаймам
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленияПоЗаймам.Контрагент,
	|	НачисленияПоЗаймам.Регистратор,
	|	НачисленияПоЗаймам.КодОперации,
	|	НачисленияПоЗаймам.Договор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Контрагент,
	|	Договор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Реализация.ДокументРеализации КАК ДокументРеализации,
	|	Реализация.Контрагент КАК Контрагент,
	|	Реализация.Договор КАК Договор,
	|	ДанныеПервичныхДокументов.Номер КАК НомерДокумента,
	|	ДанныеПервичныхДокументов.Дата КАК ДатаДокумента,
	|	ВЫБОР
	|		КОГДА Реализация.ДокументРеализации ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.СправкаРасчет)
	|		КОГДА Реализация.ДокументРеализации ССЫЛКА Документ.ОтчетКомитенту
	//++ НЕ УТКА
	|				ИЛИ Реализация.ДокументРеализации ССЫЛКА Документ.ОтчетДавальцу
	//-- НЕ УТКА
	|				ИЛИ Реализация.ДокументРеализации ССЫЛКА Документ.РеализацияУслугПрочихАктивов
	|				ИЛИ Реализация.ДокументРеализации ССЫЛКА Документ.АктВыполненныхРабот
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.Акт)
	|		КОГДА Реализация.ДокументРеализации ССЫЛКА Документ.ВозвратТоваровПоставщику
	|				ИЛИ Реализация.ДокументРеализации ССЫЛКА Документ.ВыкупВозвратнойТарыКлиентом
	|				ИЛИ Реализация.ДокументРеализации ССЫЛКА Документ.ВозвратТоваровМеждуОрганизациями
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.Накладная)
	|		КОГДА Реализация.ДокументРеализации ССЫЛКА Документ.НачисленияКредитовИДепозитов
	|				ИЛИ Реализация.ДокументРеализации ССЫЛКА Документ.ЗаписьКнигиПродаж
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.ПустаяСсылка)
	|		КОГДА Реализация.ДокументРеализации ССЫЛКА Документ.ОтчетКомиссионера
	|				ИЛИ Реализация.ДокументРеализации ССЫЛКА Документ.КорректировкаРеализации
	|				ИЛИ Реализация.ДокументРеализации ССЫЛКА Документ.РеализацияТоваровУслуг
	|				ИЛИ Реализация.ДокументРеализации ССЫЛКА Документ.ОтчетПоКомиссииМеждуОрганизациями
	|				ИЛИ Реализация.ДокументРеализации ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями
	|			ТОГДА ВЫБОР
	|					КОГДА Реализация.ЕстьТовары > 0
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.ТипыДокументов.Накладная)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТипыДокументов.Акт)
	|				КОНЕЦ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТипыДокументов.ПустаяСсылка)
	|	КОНЕЦ КАК ТипДокумента,
	|	Реализация.ЕстьТовары КАК ЕстьТовары
	|ПОМЕСТИТЬ ПодтверждающиеДокументы
	|ИЗ
	|	Реализация КАК Реализация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО Реализация.ДокументРеализации = ДанныеПервичныхДокументов.Документ
	|			И ДанныеПервичныхДокументов.Организация = &Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Реализация.ДокументРеализации,
	|	Реализация.Контрагент,
	|	Реализация.Договор,
	|	Реализация.Договор.Номер,
	|	Реализация.Договор.Дата,
	|	ЗНАЧЕНИЕ(Справочник.ТипыДокументов.Договор),
	|	Реализация.ЕстьТовары
	|ИЗ
	|	Реализация КАК Реализация
	|ГДЕ
	|	НЕ Реализация.Договор.Номер ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументРеализации,
	|	Контрагент,
	|	Договор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реализация.Контрагент КАК Контрагент,
	|	Реализация.ДокументРеализации КАК ДокументРеализации,
	|	Реализация.СуммаРеализации КАК СуммаРеализации,
	|	Реализация.КодОперации КАК КодОперации,
	|	Реализация.СуммаПриобретения КАК СуммаПриобретения,
	|	Реализация.НДС КАК НДС,
	|	ПодтверждающиеДокументы.НомерДокумента КАК НомерДокумента,
	|	ПодтверждающиеДокументы.ДатаДокумента КАК ДатаДокумента,
	|	ПодтверждающиеДокументы.ТипДокумента КАК ТипДокумента,
	|	0 КАК ПорядокДокументов,
	|	Реализация.КодОперации.Код КАК КодОперацииКод,
	|	Реализация.КодОперации.Наименование КАК КодОперацииНаименование,
	|	ВЫБОР
	|		КОГДА Реализация.КодОперации.ВключаетсяВРеестрПодтверждающихДокументов
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ВключаетсяВРеестрСортировка,
	|	ВЫБОР
	|		КОГДА Реализация.КодОперации.ОперацияНеПодлежитНалогообложению
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СТ149Сортировка
	|ИЗ
	|	Реализация КАК Реализация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|		ПО Реализация.ДокументРеализации = ДанныеПервичныхДокументов.Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПодтверждающиеДокументы КАК ПодтверждающиеДокументы
	|		ПО Реализация.Контрагент = ПодтверждающиеДокументы.Контрагент
	|			И Реализация.ДокументРеализации = ПодтверждающиеДокументы.ДокументРеализации
	|			И Реализация.Договор = ПодтверждающиеДокументы.Договор
	|			И Реализация.ЕстьТовары = ПодтверждающиеДокументы.ЕстьТовары
	|ГДЕ
	|	ДанныеПервичныхДокументов.Организация = &Организация
	|
	|УПОРЯДОЧИТЬ ПО
	|	СТ149Сортировка,
	|	ВключаетсяВРеестрСортировка,
	|	КодОперацииКод,
	|	КодОперацииНаименование,
	|	ДанныеПервичныхДокументов.ДатаРегистратора,
	|	ПорядокДокументов
	|ИТОГИ
	|	МАКСИМУМ(СуммаРеализации),
	|	МАКСИМУМ(СуммаПриобретения),
	|	МАКСИМУМ(НДС),
	|	МАКСИМУМ(НомерДокумента),
	|	МАКСИМУМ(ДатаДокумента),
	|	МАКСИМУМ(ТипДокумента)
	|ПО
	|	КодОперации,
	|	Контрагент,
	|	Реализация.Договор,
	|	ДокументРеализации";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда 
		Возврат;
	КонецЕсли;
	
	ВыборкаПоКодам = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоКодам.Следующий() Цикл
		
		ВыборкаПоКонтрагентам = ВыборкаПоКодам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоКонтрагентам.Следующий() Цикл
			
			ВыборкаПоДоговорам = ВыборкаПоКонтрагентам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПоДоговорам.Следующий() Цикл
			
				ВыборкаПоДокументам = ВыборкаПоДоговорам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаПоДокументам.Следующий() Цикл
					
					КлючСтроки = Документы.ФормированиеЗаписейРаздела7ДекларацииНДС.ПолучитьНовыйКлючСтроки(НеоблагаемыеНДСОперации);
					
					СтрокаНеоблагаемыеНДСОперации = НеоблагаемыеНДСОперации.Добавить();
					СтрокаНеоблагаемыеНДСОперации.КодОперации = ВыборкаПоДокументам.КодОперации;
					СтрокаНеоблагаемыеНДСОперации.ДокументРеализации = ВыборкаПоДокументам.ДокументРеализации;
					СтрокаНеоблагаемыеНДСОперации.Контрагент = ВыборкаПоДокументам.Контрагент;
					СтрокаНеоблагаемыеНДСОперации.СуммаРеализации = ВыборкаПоДокументам.СуммаРеализации;
					СтрокаНеоблагаемыеНДСОперации.СуммаПриобретения = ВыборкаПоДокументам.СуммаПриобретения;
					СтрокаНеоблагаемыеНДСОперации.НДСПрямой= ВыборкаПоДокументам.НДС;
					СтрокаНеоблагаемыеНДСОперации.КлючСтроки = КлючСтроки;
					
					ВыборкаПоСтрокам = ВыборкаПоДокументам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаПоСтрокам.Следующий() Цикл
						Если ЗначениеЗаполнено(ВыборкаПоСтрокам.ТипДокумента) Тогда 
							СтрокаПодтверждающиеДокументы = ПодтверждающиеДокументы.Добавить();
							СтрокаПодтверждающиеДокументы.ТипДокумента = ВыборкаПоСтрокам.ТипДокумента;
							СтрокаПодтверждающиеДокументы.НомерДокумента = ВыборкаПоСтрокам.НомерДокумента;
							СтрокаПодтверждающиеДокументы.ДатаДокумента= ВыборкаПоСтрокам.ДатаДокумента;
							СтрокаПодтверждающиеДокументы.КлючСтроки = КлючСтроки;
						КонецЕсли;
					КонецЦикла;
					
				КонецЦикла;
			
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	// Отдельно распределим включенный в стоимость НДС по кодам операций
	МассивКоэффициентов = НеоблагаемыеНДСОперации.ВыгрузитьКолонку("СуммаРеализации");
	Для Индекс = 0 По МассивКоэффициентов.Количество() - 1 Цикл
		Если МассивКоэффициентов[Индекс] < 0 Тогда
			МассивКоэффициентов[Индекс] = 0;
		КонецЕсли;
	КонецЦикла;
	РаспределитьНДСПоКодамРаздела7(СтруктураПараметров, НеоблагаемыеНДСОперации, МассивКоэффициентов);
	
КонецПроцедуры

// В процедуре распределяется НДС по кодам операции
//
// Параметры:
//	СтруктураПараметров - Структура - Организация, дата.
//	НеоблагаемыеНДСОперации - ТаблицаЗначений - таблица необлагаемых операций
//	МассивКоэффициентов - ТаблицаЗначений - колонка СуммаРеализации таблицы НеоблагаемыеНДСОперации
//
Процедура РаспределитьНДСПоКодамРаздела7(СтруктураПараметров, НеоблагаемыеНДСОперации, МассивКоэффициентов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", СтруктураПараметров.Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоКвартала(СтруктураПараметров.Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецКвартала(СтруктураПараметров.Дата));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СУММА(ПартииНДС.НДСРегл) КАК НДС
	|ИЗ
	|	РегистрНакопления.ПартииНДСКРаспределению КАК ПартииНДС
	|ГДЕ
	|	ПартииНДС.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ПартииНДС.Организация = &Организация
	|	И ПартииНДС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ПартииНДС.Активность
	|	И ПартииНДС.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|	И ПартииНДС.Регистратор ССЫЛКА Документ.РаспределениеНДС";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.НДС) Тогда 
		НДСКРаспределению = Выборка.НДС;
		Если НеоблагаемыеНДСОперации.Количество() > 0 Тогда 
			МассивРаспределеннаяСуммаНДС = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(НДСКРаспределению, МассивКоэффициентов);
			Для Инд = 0 По НеоблагаемыеНДСОперации.Количество() - 1 Цикл
				НеоблагаемыеНДСОперации[Инд].НДСРаспределенный = МассивРаспределеннаяСуммаНДС[Инд];
			КонецЦикла;
		КонецЕсли;
	Иначе
		Для Инд = 0 По НеоблагаемыеНДСОперации.Количество() - 1 Цикл
			НеоблагаемыеНДСОперации[Инд].НДСРаспределенный = 0;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
