
#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация
	//++ НЕ УТ
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	//-- НЕ УТ
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый документ.
//  Отказ - Булево - Признак проведения документа.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то проведение документа выполнено не будет.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
//@skip-warning
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
	
	//++ Локализация
	ЗарегистрироватьСчетаФактурыОжидаетОплатыНДС(Объект, Истина);
	//-- Локализация
		
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
//@skip-warning
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	//++ Локализация
	УчетПрослеживаемыхТоваровЛокализация.ПроверитьЗаполнениеКоличестваПоРНПТ(Объект, Отказ, Неопределено);
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект.
//  ДанныеЗаполнения - Произвольный - Значение, которое используется как основание для заполнения.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки события.
//
//@skip-warning
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	//++ Локализация	
	//-- Локализация
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//
//@skip-warning
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	//++ Локализация	
	//-- Локализация
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	//++ Локализация
	Если РежимЗаписи=РежимЗаписиДокумента.Проведение И Объект.Статус = Перечисления.СтатусыТаможенныхДеклараций.ВыпущеноСТаможни Тогда
		ЗаполнитьНомераГТД(Объект);
	КонецЕсли;

	Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		Объект.РучнаяКорректировкаЖурналаСФ = Ложь;
	КонецЕсли;
	//-- Локализация
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
//@skip-warning
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	//++ Локализация
	Если Не Отказ
		И Не ПроведениеДокументов.СвойстваДокумента(Объект).РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ЗарегистрироватьСчетаФактурыОжидаетОплатыНДС(Объект, Ложь);
	КонецЕсли;
	//-- Локализация
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  ОбъектКопирования - ДокументОбъект.ТаможеннаяДекларацияИмпорт - Исходный документ, который является источником копирования.
//
//@skip-warning
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	//++ Локализация
	//-- Локализация
КонецПроцедуры

#КонецОбласти

// Устанавливает маску ввода номера таможенной декларации.
//
// Параметры:
// 	Маска - Строка - Маска ввода номера таможенной декларации
//
Процедура УстановитьМаскуНомераТаможеннойДекларации(Маска) Экспорт
	
	//++ Локализация
	Маска = "99999999/999999/NN999999";
	//-- Локализация
	
КонецПроцедуры

#Область ПодключаемыеКоманды

//@skip-warning
// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	//++ Локализация
	//-- Локализация
	
КонецПроцедуры

//@skip-warning
// Добавляет команду создания документа "Авансовый отчет".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	//++ Локализация
	//-- Локализация
КонецПроцедуры

// @skip-warning
// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры.
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	//++ Локализация
	//-- Локализация
КонецПроцедуры

//@skip-warning
// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	//++ Локализация
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

//++ НЕ УТ
#Область ПроводкиРегУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	//++ Локализация
	#Область ТаможенныйСбор
		ТекстТаможенныйСбор = "
		|ВЫБРАТЬ // Таможенный сбор (Дт 44 :: Кт 60.01)
		|
		|	Операция.Ссылка КАК Ссылка,
		|	Операция.Дата КАК Период,
		|	Операция.Организация КАК Организация,
		|	Операция.ИдентификаторСтрокиСбор КАК ИдентификаторСтроки,
		|
		|	Операция.ТаможенныйСбор КАК Сумма,
		|	Операция.ТаможенныйСбор / КурсВалютыУпрУчета.Курс КАК СуммаУУ,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
		|	Операция.СтатьяРасходовСбор КАК АналитикаУчетаДт,
		|	Операция.Подразделение КАК МестоУчетаДт,
		|
		|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
		|	Операция.Подразделение КАК ПодразделениеДт,
		|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
		|
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
		|
		|	Операция.СтатьяРасходовСбор КАК СубконтоДт1,
		|	Операция.АналитикаРасходовСбор КАК СубконтоДт2,
		|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
		|				ВЫБОР КОГДА ОбъектыСтроительства.СпособСтроительства <> ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка) 
		|						ТОГДА ОбъектыСтроительства.СпособСтроительства
		|					ИНАЧЕ 
		|						ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный) 
		|				КОНЕЦ
		|	ИНАЧЕ
		|		ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
		|	КОНЕЦ КАК СубконтоДт3,
		|	
		|	0 КАК ВалютнаяСуммаДт,
		|	0 КАК КоличествоДт,
		|	0 КАК СуммаНУДт,
		|	0 КАК СуммаПРДт,
		|	0 КАК СуммаВРДт,
		|	
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками) КАК ВидСчетаКт,
		|	Расчеты.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
		|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
		|
		|	Операция.ВалютаВзаиморасчетов КАК ВалютаКт,
		|	Расчеты.Подразделение КАК ПодразделениеКт,
		|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
		|
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
		|	Расчеты.Контрагент КАК СубконтоКт1,
		|	Расчеты.Договор КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|
		|	Операция.ТаможенныйСбор КАК ВалютнаяСуммаКт,
		|	0 КАК КоличествоКт,
		|	0 КАК СуммаНУКт,
		|	0 КАК СуммаПРКт,
		|	0 КАК СуммаВРКт,
		|	""Таможенный сбор"" КАК Содержание
		|
		|ИЗ
		|	ДокументыКОтражению КАК ДокументыКОтражению
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		Документ.ТаможеннаяДекларацияИмпорт КАК Операция
		|	ПО
		|		ДокументыКОтражению.Ссылка = Операция.Ссылка
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
		|	ПО
		|		Операция.СтатьяРасходовСбор = СтатьиСтроительства.Ссылка
		|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		ВТРасчетыСПоставщиками КАК Расчеты
		|	ПО
		|		Операция.Ссылка = Расчеты.Ссылка
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		КурсыВалют КАК КурсВалютыУпрУчета
		|	ПО
		|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
		|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ 
		|		Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
		|	ПО 
		|		ОбъектыСтроительства.Ссылка = Операция.АналитикаРасходовСбор
		|ГДЕ
		|	Операция.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыТаможенныхДеклараций.ВыпущеноСТаможни)
		|	И Операция.ТаможенныйСбор <> 0
		|";
	#КонецОбласти

	#Область ТаможенныйШтраф
		ТекстТаможенныйШтраф = "
		|ВЫБРАТЬ // Таможенный штраф (Дт 44 :: Кт 60.01)
		|
		|	Операция.Ссылка КАК Ссылка,
		|	Операция.Дата КАК Период,
		|	Операция.Организация КАК Организация,
		|	Операция.ИдентификаторСтрокиШтраф КАК ИдентификаторСтроки,
		|
		|	Операция.ТаможенныйШтраф КАК Сумма,
		|	Операция.ТаможенныйШтраф / КурсВалютыУпрУчета.Курс КАК СуммаУУ,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
		|	Операция.СтатьяРасходовШтраф КАК АналитикаУчетаДт,
		|	Операция.Подразделение КАК МестоУчетаДт,
		|
		|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
		|	Операция.Подразделение КАК ПодразделениеДт,
		|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
		|
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
		|
		|	Операция.СтатьяРасходовШтраф КАК СубконтоДт1,
		|	Операция.АналитикаРасходовШтраф КАК СубконтоДт2,
		|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
		|				ВЫБОР КОГДА ОбъектыСтроительства.СпособСтроительства <> ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка) 
		|						ТОГДА ОбъектыСтроительства.СпособСтроительства
		|					ИНАЧЕ 
		|						ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный) 
		|				КОНЕЦ
		|	ИНАЧЕ
		|		ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
		|	КОНЕЦ КАК СубконтоДт3,
		|	
		|	0 КАК ВалютнаяСуммаДт,
		|	0 КАК КоличествоДт,
		|	0 КАК СуммаНУДт,
		|	0 КАК СуммаПРДт,
		|	0 КАК СуммаВРДт,
		|	
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками) КАК ВидСчетаКт,
		|	Расчеты.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
		|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
		|
		|	Операция.ВалютаВзаиморасчетов КАК ВалютаКт,
		|	Расчеты.Подразделение КАК ПодразделениеКт,
		|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
		|
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
		|	Расчеты.Контрагент КАК СубконтоКт1,
		|	Расчеты.Договор КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|
		|	Операция.ТаможенныйШтраф КАК ВалютнаяСуммаКт,
		|	0 КАК КоличествоКт,
		|	0 КАК СуммаНУКт,
		|	0 КАК СуммаПРКт,
		|	0 КАК СуммаВРКт,
		|	""Таможенный штраф"" КАК Содержание
		|
		|ИЗ
		|	ДокументыКОтражению КАК ДокументыКОтражению
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		Документ.ТаможеннаяДекларацияИмпорт КАК Операция
		|	ПО
		|		ДокументыКОтражению.Ссылка = Операция.Ссылка
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
		|	ПО
		|		Операция.СтатьяРасходовШтраф = СтатьиСтроительства.Ссылка
		|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		ВТРасчетыСПоставщиками КАК Расчеты
		|	ПО
		|		Операция.Ссылка = Расчеты.Ссылка
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		КурсыВалют КАК КурсВалютыУпрУчета
		|	ПО
		|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
		|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ 
		|		Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
		|	ПО 
		|		ОбъектыСтроительства.Ссылка = Операция.АналитикаРасходовШтраф
		|ГДЕ
		|	Операция.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыТаможенныхДеклараций.ВыпущеноСТаможни)
		|	И Операция.ТаможенныйШтраф <> 0
		|";
	#КонецОбласти

	#Область Пошлина
		ТекстПошлина = "
		|ВЫБРАТЬ // Таможенная пошлина (Дт 41 :: Кт 60, 76)
		|
		|	Операция.Ссылка КАК Ссылка,
		|	Операция.Дата КАК Период,
		|	Операция.Организация КАК Организация,
		|	Строки.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|
		|	Строки.СуммаПошлины КАК Сумма,
		|	Строки.СуммаПошлины / КурсВалютыУпрУчета.Курс КАК СуммаУУ,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе) КАК ВидСчетаДт,
		|	ВЫБОР КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
		|		Строки.ВидЗапасов.ГруппаФинансовогоУчета
		|	ИНАЧЕ
		|		Строки.Номенклатура.ГруппаФинансовогоУчета
		|	КОНЕЦ КАК АналитикаУчетаДт,
		|	Строки.Склад КАК МестоУчетаДт,
		|
		|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
		|	Строки.Склад.Подразделение КАК ПодразделениеДт,
		|	ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности,
		|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельностиДт,
		|
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
		|	Строки.Номенклатура КАК СубконтоДт1,
		|	Строки.Склад КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	
		|	0 КАК ВалютнаяСуммаДт,
		|	0 КАК КоличествоДт,
		|	0 КАК СуммаНУДт,
		|	0 КАК СуммаПРДт,
		|	0 КАК СуммаВРДт,
		|	
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками) КАК ВидСчетаКт,
		|	Расчеты.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
		|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
		|
		|	Операция.ВалютаВзаиморасчетов КАК ВалютаКт,
		|	Расчеты.Подразделение КАК ПодразделениеКт,
		|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
		|
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
		|	Расчеты.Контрагент КАК СубконтоКт1,
		|	Расчеты.Договор КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|
		|	Строки.СуммаПошлины КАК ВалютнаяСуммаКт,
		|	0 КАК КоличествоКт,
		|	0 КАК СуммаНУКт,
		|	0 КАК СуммаПРКт,
		|	0 КАК СуммаВРКт,
		|	""Таможенная пошлина"" КАК Содержание
		|
		|ИЗ
		|	ДокументыКОтражению КАК ДокументыКОтражению
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		Документ.ТаможеннаяДекларацияИмпорт КАК Операция
		|	ПО
		|		ДокументыКОтражению.Ссылка = Операция.Ссылка
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		Документ.ТаможеннаяДекларацияИмпорт.Товары КАК Строки
		|	ПО
		|		Строки.Ссылка = Операция.Ссылка
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		ВТРасчетыСПоставщиками КАК Расчеты
		|	ПО
		|		Операция.Ссылка = Расчеты.Ссылка
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		КурсыВалют КАК КурсВалютыУпрУчета
		|	ПО
		|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
		|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
		|
		|ГДЕ
		|	Операция.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыТаможенныхДеклараций.ВыпущеноСТаможни)
		|	И Строки.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
		|	И НЕ Строки.Склад.ЦеховаяКладовая
		|";
	#КонецОбласти

	#Область ПошлинаСкладПроизводства
		ТекстПошлинаСкладПроизводства = "
		|ВЫБРАТЬ // Таможенная пошлина (Дт 20 :: Кт 60, 76)
		|
		|	Операция.Ссылка КАК Ссылка,
		|	Операция.Дата КАК Период,
		|	Операция.Организация КАК Организация,
		|	Строки.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|
		|	Строки.СуммаПошлины КАК Сумма,
		|	Строки.СуммаПошлины / КурсВалютыУпрУчета.Курс КАК СуммаУУ,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчетаДт,
		|	ВЫБОР
		|		КОГДА &АналитическийУчетПоГруппамПродукции
		|		ТОГДА Строки.Номенклатура.ГруппаАналитическогоУчета
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
		|	КОНЕЦ КАК АналитикаУчетаДт,
		|	Строки.Склад.Подразделение КАК МестоУчетаДт,
		|
		|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
		|	Строки.Склад.Подразделение КАК ПодразделениеДт,
		|	ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности,
		|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельностиДт,
		|
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
		|	Строки.Номенклатура КАК СубконтоДт1,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты) КАК СубконтоДт2,
		|	ВЫБОР
		|		КОГДА &АналитическийУчетПоГруппамПродукции
		|		ТОГДА Строки.Номенклатура.ГруппаАналитическогоУчета
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
		|	КОНЕЦ КАК СубконтоДт3,
		|	
		|	0 КАК ВалютнаяСуммаДт,
		|	0 КАК КоличествоДт,
		|	0 КАК СуммаНУДт,
		|	0 КАК СуммаПРДт,
		|	0 КАК СуммаВРДт,
		|	
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками) КАК ВидСчетаКт,
		|	Расчеты.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
		|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
		|
		|	Операция.ВалютаВзаиморасчетов КАК ВалютаКт,
		|	Расчеты.Подразделение КАК ПодразделениеКт,
		|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
		|
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
		|	Расчеты.Контрагент КАК СубконтоКт1,
		|	Расчеты.Договор КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|
		|	Строки.СуммаПошлины КАК ВалютнаяСуммаКт,
		|	0 КАК КоличествоКт,
		|	0 КАК СуммаНУКт,
		|	0 КАК СуммаПРКт,
		|	0 КАК СуммаВРКт,
		|	""Таможенная пошлина"" КАК Содержание
		|
		|ИЗ
		|	ДокументыКОтражению КАК ДокументыКОтражению
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		Документ.ТаможеннаяДекларацияИмпорт КАК Операция
		|	ПО
		|		ДокументыКОтражению.Ссылка = Операция.Ссылка
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		Документ.ТаможеннаяДекларацияИмпорт.Товары КАК Строки
		|	ПО
		|		Строки.Ссылка = Операция.Ссылка
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		ВТРасчетыСПоставщиками КАК Расчеты
		|	ПО
		|		Операция.Ссылка = Расчеты.Ссылка
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		КурсыВалют КАК КурсВалютыУпрУчета
		|	ПО
		|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
		|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
		|
		|ГДЕ
		|	Операция.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыТаможенныхДеклараций.ВыпущеноСТаможни)
		|	И Строки.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
		|	И Строки.Склад.ЦеховаяКладовая
		|";
	#КонецОбласти

	#Область ПошлинаТоварыПути
		ТекстПошлинаТоварыПути = "
		|ВЫБРАТЬ // Таможенная пошлина (Дт 15 :: Кт 60, 76)
		|
		|	Операция.Ссылка КАК Ссылка,
		|	Операция.Дата КАК Период,
		|	Операция.Организация КАК Организация,
		|	Строки.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|
		|	Строки.СуммаПошлины КАК Сумма,
		|	Строки.СуммаПошлины / КурсВалютыУпрУчета.Курс КАК СуммаУУ,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ЗатратыНаПриобретениеТМЦ) КАК ВидСчетаДт,
		|	ВЫБОР КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
		|		Строки.ВидЗапасов.ГруппаФинансовогоУчета
		|	ИНАЧЕ
		|		Строки.Номенклатура.ГруппаФинансовогоУчета
		|	КОНЕЦ КАК АналитикаУчетаДт,
		|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
		|
		|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
		|	ПриобретениеТоваровУслуг.Договор.Подразделение  КАК ПодразделениеДт,
		|	ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности,
		|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельностиДт,
		|
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
		|	Строки.Номенклатура КАК СубконтоДт1,
		|	ПриобретениеТоваровУслуг.Контрагент КАК СубконтоДт2,
		|	ПриобретениеТоваровУслуг.Договор КАК СубконтоДт3,
		|	
		|	0 КАК ВалютнаяСуммаДт,
		|	0 КАК КоличествоДт,
		|	0 КАК СуммаНУДт,
		|	0 КАК СуммаПРДт,
		|	0 КАК СуммаВРДт,
		|	
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками) КАК ВидСчетаКт,
		|	Расчеты.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
		|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
		|
		|	Операция.ВалютаВзаиморасчетов КАК ВалютаКт,
		|	Расчеты.Подразделение КАК ПодразделениеКт,
		|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
		|
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
		|	Расчеты.Контрагент КАК СубконтоКт1,
		|	Расчеты.Договор КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|
		|	Строки.СуммаПошлины КАК ВалютнаяСуммаКт,
		|	0 КАК КоличествоКт,
		|	0 КАК СуммаНУКт,
		|	0 КАК СуммаПРКт,
		|	0 КАК СуммаВРКт,
		|	""Таможенная пошлина по товарам в пути"" КАК Содержание
		|
		|ИЗ
		|	ДокументыКОтражению КАК ДокументыКОтражению
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		Документ.ТаможеннаяДекларацияИмпорт КАК Операция
		|	ПО
		|		ДокументыКОтражению.Ссылка = Операция.Ссылка
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		Документ.ТаможеннаяДекларацияИмпорт.Товары КАК Строки
		|	ПО
		|		Строки.Ссылка = Операция.Ссылка
		|		
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		ВТРасчетыСПоставщиками КАК Расчеты
		|	ПО
		|		Операция.Ссылка = Расчеты.Ссылка
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		КурсыВалют КАК КурсВалютыУпрУчета
		|	ПО
		|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
		|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ 
		|		Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
		|	ПО 
		|		Строки.ДокументПоступления = ПриобретениеТоваровУслуг.Ссылка
		|	
		|ГДЕ
		|	Операция.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыТаможенныхДеклараций.ВыпущеноСТаможни)
		|	И Строки.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути)
		|";
	#КонецОбласти

	#Область НДСВСтоимость
		ТекстНДСВСтоимость = "
		|ВЫБРАТЬ // Включение НДС в стоимость (Дт 41 :: Кт 60, 76)
		|
		|	Операция.Ссылка КАК Ссылка,
		|	Операция.Дата КАК Период,
		|	Операция.Организация КАК Организация,
		|	Строки.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|
		|	Строки.СуммаНДС КАК Сумма,
		|	Строки.СуммаНДС / КурсВалютыУпрУчета.Курс КАК СуммаУУ,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе) КАК ВидСчетаДт,
		|	ВЫБОР КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
		|		Строки.ВидЗапасов.ГруппаФинансовогоУчета
		|	ИНАЧЕ
		|		Строки.Номенклатура.ГруппаФинансовогоУчета
		|	КОНЕЦ КАК АналитикаУчетаДт,
		|	Строки.Склад КАК МестоУчетаДт,
		|
		|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
		|	Строки.Склад.Подразделение КАК ПодразделениеДт,
		|	ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности,
		|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельностиДт,
		|
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
		|	Строки.Номенклатура КАК СубконтоДт1,
		|	Строки.Склад КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	
		|	0 КАК ВалютнаяСуммаДт,
		|	0 КАК КоличествоДт,
		|	0 КАК СуммаНУДт,
		|	0 КАК СуммаПРДт,
		|	0 КАК СуммаВРДт,
		|	
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками) КАК ВидСчетаКт,
		|	Расчеты.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
		|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
		|
		|	Операция.ВалютаВзаиморасчетов КАК ВалютаКт,
		|	Расчеты.Подразделение КАК ПодразделениеКт,
		|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
		|
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
		|	Расчеты.Контрагент КАК СубконтоКт1,
		|	Расчеты.Договор КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|
		|	Строки.СуммаНДС КАК ВалютнаяСуммаКт,
		|	0 КАК КоличествоКт,
		|	0 КАК СуммаНУКт,
		|	0 КАК СуммаПРКт,
		|	0 КАК СуммаВРКт,
		|	""Включение НДС в стоимость"" КАК Содержание
		|
		|ИЗ
		|	ДокументыКОтражению КАК ДокументыКОтражению
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		Документ.ТаможеннаяДекларацияИмпорт КАК Операция
		|	ПО
		|		ДокументыКОтражению.Ссылка = Операция.Ссылка
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		Документ.ТаможеннаяДекларацияИмпорт.Товары КАК Строки
		|	ПО
		|		Строки.Ссылка = Операция.Ссылка
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		ВТРасчетыСПоставщиками КАК Расчеты
		|	ПО
		|		Операция.Ссылка = Расчеты.Ссылка
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		КурсыВалют КАК КурсВалютыУпрУчета
		|	ПО
		|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
		|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
		|	
		|ГДЕ
		|	Операция.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыТаможенныхДеклараций.ВыпущеноСТаможни)
		|	И Строки.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
		|	И НЕ Строки.Склад.ЦеховаяКладовая
		|	И Строки.ЗакупкаПодДеятельность В
		|			(ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
		|";
	#КонецОбласти

	#Область НДСВСтоимостьСкладПроизводства
		ТекстНДСВСтоимостьСкладПроизводства = "
		|ВЫБРАТЬ // Включение НДС в стоимость (Дт 20 :: Кт 60, 76)
		|
		|	Операция.Ссылка КАК Ссылка,
		|	Операция.Дата КАК Период,
		|	Операция.Организация КАК Организация,
		|	Строки.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|
		|	Строки.СуммаНДС КАК Сумма,
		|	Строки.СуммаНДС / КурсВалютыУпрУчета.Курс КАК СуммаУУ,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчетаДт,
		|	ВЫБОР
		|		КОГДА &АналитическийУчетПоГруппамПродукции
		|		ТОГДА Строки.Номенклатура.ГруппаАналитическогоУчета
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
		|	КОНЕЦ КАК АналитикаУчетаДт,
		|	Строки.Склад.Подразделение КАК МестоУчетаДт,
		|
		|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
		|	Строки.Склад.Подразделение КАК ПодразделениеДт,
		|	ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности,
		|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельностиДт,
		|
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
		|	Строки.Номенклатура КАК СубконтоДт1,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты) КАК СубконтоДт2,
		|	ВЫБОР
		|		КОГДА &АналитическийУчетПоГруппамПродукции
		|		ТОГДА Строки.Номенклатура.ГруппаАналитическогоУчета
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
		|	КОНЕЦ КАК СубконтоДт3,
		|	
		|	0 КАК ВалютнаяСуммаДт,
		|	0 КАК КоличествоДт,
		|	0 КАК СуммаНУДт,
		|	0 КАК СуммаПРДт,
		|	0 КАК СуммаВРДт,
		|	
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками) КАК ВидСчетаКт,
		|	Расчеты.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
		|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
		|
		|	Операция.ВалютаВзаиморасчетов КАК ВалютаКт,
		|	Расчеты.Подразделение КАК ПодразделениеКт,
		|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
		|
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
		|	Расчеты.Контрагент КАК СубконтоКт1,
		|	Расчеты.Договор КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|
		|	Строки.СуммаНДС КАК ВалютнаяСуммаКт,
		|	0 КАК КоличествоКт,
		|	0 КАК СуммаНУКт,
		|	0 КАК СуммаПРКт,
		|	0 КАК СуммаВРКт,
		|	""Включение НДС в стоимость"" КАК Содержание
		|
		|ИЗ
		|	ДокументыКОтражению КАК ДокументыКОтражению
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		Документ.ТаможеннаяДекларацияИмпорт КАК Операция
		|	ПО
		|		ДокументыКОтражению.Ссылка = Операция.Ссылка
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		Документ.ТаможеннаяДекларацияИмпорт.Товары КАК Строки
		|	ПО
		|		Строки.Ссылка = Операция.Ссылка
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		ВТРасчетыСПоставщиками КАК Расчеты
		|	ПО
		|		Операция.Ссылка = Расчеты.Ссылка
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		КурсыВалют КАК КурсВалютыУпрУчета
		|	ПО
		|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
		|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
		|	
		|ГДЕ
		|	Операция.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыТаможенныхДеклараций.ВыпущеноСТаможни)
		|	И Строки.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
		|	И Строки.Склад.ЦеховаяКладовая
		|	И Строки.ЗакупкаПодДеятельность В
		|			(ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
		|";
	#КонецОбласти
	
	#Область НДСВСтоимостьТоварыВПути
		ТекстНДСВСтоимостьТоварыВПути = "
		|ВЫБРАТЬ // Включение НДС в стоимость (Дт 15 :: Кт 60, 76)
		|
		|	Операция.Ссылка КАК Ссылка,
		|	Операция.Дата КАК Период,
		|	Операция.Организация КАК Организация,
		|	Строки.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|
		|	Строки.СуммаНДС КАК Сумма,
		|	Строки.СуммаНДС / КурсВалютыУпрУчета.Курс КАК СуммаУУ,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ЗатратыНаПриобретениеТМЦ) КАК ВидСчетаДт,
		|	ВЫБОР КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
		|		Строки.ВидЗапасов.ГруппаФинансовогоУчета
		|	ИНАЧЕ
		|		Строки.Номенклатура.ГруппаФинансовогоУчета
		|	КОНЕЦ КАК АналитикаУчетаДт,
		|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
		|
		|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
		|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
		|	ЕСТЬNULL(Строки.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности,
		|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельностиДт,
		|
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
		|	Строки.Номенклатура КАК СубконтоДт1,
		|	ПриобретениеТоваровУслуг.Контрагент КАК СубконтоДт2,
		|	ПриобретениеТоваровУслуг.Договор КАК СубконтоДт3,
		|	
		|	0 КАК ВалютнаяСуммаДт,
		|	0 КАК КоличествоДт,
		|	0 КАК СуммаНУДт,
		|	0 КАК СуммаПРДт,
		|	0 КАК СуммаВРДт,
		|	
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками) КАК ВидСчетаКт,
		|	Расчеты.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
		|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
		|
		|	Операция.ВалютаВзаиморасчетов КАК ВалютаКт,
		|	Расчеты.Подразделение КАК ПодразделениеКт,
		|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
		|
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
		|	Расчеты.Контрагент КАК СубконтоКт1,
		|	Расчеты.Договор КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|
		|	Строки.СуммаНДС КАК ВалютнаяСуммаКт,
		|	0 КАК КоличествоКт,
		|	0 КАК СуммаНУКт,
		|	0 КАК СуммаПРКт,
		|	0 КАК СуммаВРКт,
		|	""Включение НДС в стоимость товаров в пути"" КАК Содержание
		|
		|ИЗ
		|	ДокументыКОтражению КАК ДокументыКОтражению
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		Документ.ТаможеннаяДекларацияИмпорт КАК Операция
		|	ПО
		|		ДокументыКОтражению.Ссылка = Операция.Ссылка
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		Документ.ТаможеннаяДекларацияИмпорт.Товары КАК Строки
		|	ПО
		|		Строки.Ссылка = Операция.Ссылка
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		ВТРасчетыСПоставщиками КАК Расчеты
		|	ПО
		|		Операция.Ссылка = Расчеты.Ссылка
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		КурсыВалют КАК КурсВалютыУпрУчета
		|	ПО
		|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
		|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ 
		|		Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
		|	ПО 
		|		Строки.ДокументПоступления = ПриобретениеТоваровУслуг.Ссылка
		|	
		|ГДЕ
		|	Операция.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыТаможенныхДеклараций.ВыпущеноСТаможни)
		|	И Строки.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути)
		|	И Строки.ЗакупкаПодДеятельность В
		|			(ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
		|";
	#КонецОбласти

	#Область НДСКВычету
		ТекстНДСКВычету = "
		|ВЫБРАТЬ // Принятие НДС к вычету по рег. НДСПредъявленный (Дт 19.05 :: Кт 60, 76) @НДСПредъявленный
		|
		|	Операция.Ссылка КАК Ссылка,
		|	Операция.Дата КАК Период,
		|	Операция.Организация КАК Организация,
		|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
		|
		|	НДСПредъявленный.НДС КАК Сумма,
		|	НДСПредъявленный.НДСУпр КАК СуммаУУ,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСпоПриобретеннымЦенностям) КАК ВидСчетаДт,
		|	НДСПредъявленный.ВидДеятельностиНДС                                    КАК АналитикаУчетаДт,
		|	НДСПредъявленный.ВидЦенности                                           КАК МестоУчетаДт,
		|
		|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)                               КАК ВалютаДт,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)                 КАК ПодразделениеДт,
		|	ЕСТЬNULL(ВводСобственныхСредств.НаправлениеДеятельности, НДСПредъявленный.НаправлениеДеятельности) КАК НаправлениеДеятельностиДт,
		|
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
		|
		|	Операция.Контрагент КАК СубконтоДт1,
		|	Операция.Ссылка КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	
		|	0 КАК ВалютнаяСуммаДт,
		|	0 КАК КоличествоДт,
		|	0 КАК СуммаНУДт,
		|	0 КАК СуммаПРДт,
		|	0 КАК СуммаВРДт,
		|	
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками) КАК ВидСчетаКт,
		|	Расчеты.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
		|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
		|
		|	Операция.ВалютаВзаиморасчетов КАК ВалютаКт,
		|	Расчеты.Подразделение КАК ПодразделениеКт,
		|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
		|
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
		|	Расчеты.Контрагент КАК СубконтоКт1,
		|	Расчеты.Договор КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|
		|	НДСПредъявленный.НДС КАК ВалютнаяСуммаКт,
		|	0 КАК КоличествоКт,
		|	0 КАК СуммаНУКт,
		|	0 КАК СуммаПРКт,
		|	0 КАК СуммаВРКт,
		|	""Входящий НДС"" КАК Содержание
		|
		|ИЗ
		|	ДокументыКОтражению КАК ДокументыКОтражению
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		Документ.ТаможеннаяДекларацияИмпорт КАК Операция
		|	ПО
		|		ДокументыКОтражению.Ссылка = Операция.Ссылка
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
		|	ПО
		|		НДСПредъявленный.Регистратор = Операция.Ссылка
		|		И НДСПредъявленный.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		И НЕ НДСПредъявленный.РегламентнаяОперация
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		ВТРасчетыСПоставщиками КАК Расчеты
		|	ПО
		|		Операция.Ссылка = Расчеты.Ссылка
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТВводСобственныхСредств КАК ВводСобственныхСредств
		|	ПО ДокументыКОтражению.Ссылка = ВводСобственныхСредств.Ссылка
		|		И НДСПредъявленный.СчетФактура = ВводСобственныхСредств.СчетФактура
		|
		|";
	#КонецОбласти

	#Область ВключениеИсключениеНДСВСтоимость
	ТекстВключениеИсключениеНДСВСтоимость = "
	|ВЫБРАТЬ //// Включение/исключение НДС в стоимость товара на складе - получателе (Дт 41.01 :: Кт 19.03) @НДСПредъявленный
	|	Партии.Ссылка КАК Ссылка,
	|	Партии.Период КАК Период,
	|	Партии.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ВЫБОР КОГДА Партии.ВключениеНДСВСтоимость ТОГДА
	|		Партии.НДСРегл
	|	ИНАЧЕ
	|		-Партии.НДСРегл
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР КОГДА Партии.ВключениеНДСВСтоимость ТОГДА
	|		Партии.НДСУпр
	|	ИНАЧЕ
	|		-Партии.НДСУпр
	|	КОНЕЦ КАК СуммаУУ,
	|
	|	ВЫБОР
	|		КОГДА Партии.Склад.ЦеховаяКладовая
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе)
	|	КОНЕЦ КАК ВидСчетаДт,
	|	ВЫБОР
	|		КОГДА Партии.Склад.ЦеховаяКладовая
	|		ТОГДА Партии.КорГруппаПродукции
	|		ИНАЧЕ Партии.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК АналитикаУчетаДт,
	|	ВЫБОР
	|		КОГДА Партии.Склад.ЦеховаяКладовая
	|		ТОГДА Партии.Склад.Подразделение
	|		ИНАЧЕ Партии.Склад
	|	КОНЕЦ КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Партии.Склад.Подразделение КАК ПодразделениеДт,
	|	Партии.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Партии.Номенклатура КАК СубконтоДт1,
	|	ВЫБОР
	|		КОГДА Партии.Склад.ЦеховаяКладовая
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты)
	|		ИНАЧЕ Партии.Склад
	|	КОНЕЦ КАК СубконтоДт2,
	|	ВЫБОР
	|		КОГДА Партии.Склад.ЦеховаяКладовая
	|		ТОГДА Партии.КорГруппаПродукции
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСпоПриобретеннымЦенностям) КАК ВидСчетаКт,
	|	Партии.ВидДеятельностиНДС                                              КАК АналитикаУчетаКт,
	|	Партии.ВидЦенности                                                     КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЕСТЬNULL(ВводСобственныхСредств.НаправлениеДеятельности, Партии.НаправлениеДеятельности) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Партии.Контрагент КАК СубконтоКт1,
	|	Партии.ДокументПоступления КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Включение/исключение НДС в стоимость товара"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ТаможеннаяДекларацияИмпорт КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Партии КАК Партии
	|	ПО
	|		Партии.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТВводСобственныхСредств КАК ВводСобственныхСредств
	|	ПО Партии.Ссылка = ВводСобственныхСредств.Ссылка
	|		И Партии.ДокументПоступления = ВводСобственныхСредств.СчетФактура
	|ГДЕ
	|	(Партии.ВключениеНДСВСтоимость ИЛИ Партии.ИсключениеНДСИзСтоимости)
		|	И Партии.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути)
	|";
	#КонецОбласти

	#Область ВключениеИсключениеНДСВСтоимостьТоварыВПути
	ТекстВключениеИсключениеНДСВСтоимостьТоварыВПути = "
	|ВЫБРАТЬ //// Включение/исключение НДС в стоимость товара на складе - получателе (Дт 15 :: Кт 19.03) @НДСпредъявленный
	|	Партии.Ссылка КАК Ссылка,
	|	Партии.Период КАК Период,
	|	Партии.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ВЫБОР КОГДА Партии.ВключениеНДСВСтоимость ТОГДА
	|		Партии.НДСРегл
	|	ИНАЧЕ
	|		-Партии.НДСРегл
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР КОГДА Партии.ВключениеНДСВСтоимость ТОГДА
	|		Партии.НДСУпр
	|	ИНАЧЕ
	|		-Партии.НДСУпр
	|	КОНЕЦ КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ЗатратыНаПриобретениеТМЦ) КАК ВидСчетаДт,
	|	Партии.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Партии.Склад.Подразделение КАК ПодразделениеДт,
	|	Партии.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Партии.Номенклатура КАК СубконтоДт1,
	|	Партии.Контрагент КАК СубконтоДт2,
	|	Партии.Договор КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСпоПриобретеннымЦенностям) КАК ВидСчетаКт,
	|	Партии.ВидДеятельностиНДС                                              КАК АналитикаУчетаКт,
	|	Партии.ВидЦенности                                                     КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЕСТЬNULL(ВводСобственныхСредств.НаправлениеДеятельности, Партии.НаправлениеДеятельности) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Партии.Контрагент КАК СубконтоКт1,
	|	Партии.ДокументПоступления КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Включение/исключение НДС в стоимость товара"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ТаможеннаяДекларацияИмпорт КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Партии КАК Партии
	|	ПО
	|		Партии.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТВводСобственныхСредств КАК ВводСобственныхСредств
	|	ПО Партии.Ссылка = ВводСобственныхСредств.Ссылка
	|		И Партии.ДокументПоступления = ВводСобственныхСредств.СчетФактура
	|ГДЕ
	|	(Партии.ВключениеНДСВСтоимость ИЛИ Партии.ИсключениеНДСИзСтоимости)
	|	И Партии.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути)
	|";
	#КонецОбласти
	
		ТекстыОтражения = Новый Массив;
		ТекстыОтражения.Добавить(ТекстТаможенныйСбор);
		ТекстыОтражения.Добавить(ТекстТаможенныйШтраф);
		ТекстыОтражения.Добавить(ТекстПошлина);
		ТекстыОтражения.Добавить(ТекстПошлинаСкладПроизводства);
		ТекстыОтражения.Добавить(ТекстПошлинаТоварыПути);
		ТекстыОтражения.Добавить(ТекстНДСВСтоимость);
		ТекстыОтражения.Добавить(ТекстНДСВСтоимостьСкладПроизводства);
		ТекстыОтражения.Добавить(ТекстНДСВСтоимостьТоварыВПути);
		ТекстыОтражения.Добавить(ТекстНДСКВычету);
		ТекстыОтражения.Добавить(ТекстВключениеИсключениеНДСВСтоимость);
		ТекстыОтражения.Добавить(ТекстВключениеИсключениеНДСВСтоимостьТоварыВПути);
		
		Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
		
	//-- Локализация
	//@skip-warning
	Возврат "";
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	//++ Локализация
	//-- Локализация
	Возврат "";
	
КонецФункции

#КонецОбласти
//-- НЕ УТ

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация
	ТекстЗапросаТаблицаЖурналУчетаСчетовФактур(Запрос, ТекстыЗапроса, Регистры);
	//++ НЕ УТ
	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
	//-- НЕ УТ
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

//++ Локализация

#Область СлужебныеПроцедурыИФункции

Процедура ЗарегистрироватьСчетаФактурыОжидаетОплатыНДС(Объект, Проведен)
	
	СчетаФактуры = УчетНДСРФ.НоваяТаблицаСчетовФактур();
	СтрокаСчетаФактуры = СчетаФактуры.Добавить();
	СтрокаСчетаФактуры.СчетФактура = Объект.Ссылка;
	СтрокаСчетаФактуры.СуммаОплаты = Объект.Товары.Итог("СуммаНДС");
	СтрокаСчетаФактуры.ОплатаЧерезЕдиныйЛицевойСчет = Объект.ОплатаЧерезЕдиныйЛицевойСчет;
	
	УчетНДСРФ.ЗарегистрироватьСчетаФактурыОжидаетОплатыНДС(СчетаФактуры, Объект.Ссылка, Проведен);

КонецПроцедуры

Процедура ЗаполнитьНомераГТД(Объект)
	
	// Создаем новые номера ГТД по временной таблице ГенерацияГТД.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки			КАК НомерСтроки,
	|	ТаблицаТовары.НомерРаздела			КАК НомерРаздела,
	|	ТаблицаТовары.НомерДляСФ			КАК НомерДляСФ,
	|	ТаблицаТовары.Номенклатура			КАК Номенклатура,
	|	ТаблицаТовары.СтранаПроисхождения	КАК СтранаПроисхождения,
	|	ТаблицаТовары.НомерГТД				КАК НомерГТД
	|ПОМЕСТИТЬ ВтТовары
	|ИЗ
	|	&ТаблицаТовары КАК ТаблицаТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки							КАК НомерСтроки,
	|	ТаблицаТовары.НомерРаздела							КАК НомерРаздела,
	|	ТаблицаТовары.НомерДляСФ							КАК НомерДляСФ,
	|	ТаблицаТовары.СтранаПроисхождения					КАК СтранаПроисхождения,
	|	ТаблицаТовары.НомерГТД								КАК НомерГТД,
	|	&ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|		И ЕСТЬNULL(КодыТНВЭД.ПрослеживаемыйТовар, ЛОЖЬ)	КАК РНПТ
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	ВтТовары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ДанныеНоменклатуры
	|		ПО ТаблицаТовары.Номенклатура = ДанныеНоменклатуры.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторТНВЭД КАК КодыТНВЭД
	|		ПО ДанныеНоменклатуры.КодТНВЭД = КодыТНВЭД.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерГТД,
	|	НомерДляСФ,
	|	СтранаПроисхождения,
	|	РНПТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТоваров.НомерДляСФ КАК НомерДляСФ
	|ИЗ
	|	Товары КАК ТаблицаТоваров
	|ГДЕ
	|	ТаблицаТоваров.НомерДляСФ <> """"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВтТовары";
	
	ИменаКолонок = "НомерСтроки, НомерРаздела, НомерДляСФ, Номенклатура, СтранаПроисхождения, НомерГТД";
	
	ИспользоватьУчетПрослеживаемыхИмпортныхТоваров = УчетПрослеживаемыхТоваровЛокализация.ИспользоватьУчетПрослеживаемыхИмпортныхТоваров(
														Объект.Дата);
	
	Запрос.УстановитьПараметр("ТаблицаТовары", Объект.Товары.Выгрузить(, ИменаКолонок));
	Запрос.УстановитьПараметр("ИспользоватьУчетПрослеживаемыхИмпортныхТоваров",
								ИспользоватьУчетПрослеживаемыхИмпортныхТоваров);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("Справочник.НомераГТД");
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Код", "НомерДляСФ");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.НомерДляСФ			КАК НомерДляСФ,
	|	ВЫБОР
	|		КОГДА Товары.РНПТ
	|			ТОГДА Товары.НомерДляСФ
	|			ИНАЧЕ &РегистрационныйНомер
	|	КОНЕЦ						КАК РегистрационныйНомер,
	|	Товары.СтранаПроисхождения	КАК СтранаПроисхождения,
	|	Товары.РНПТ					КАК РНПТ
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НомераГТД КАК УказанныеГТД
	|		ПО УказанныеГТД.Ссылка = Товары.НомерГТД
	|			И УказанныеГТД.РНПТ = Товары.РНПТ
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НомераГТД КАК ПодобранныеГТД
	|		ПО ПодобранныеГТД.Код = Товары.НомерДляСФ
	|			И ПодобранныеГТД.СтранаПроисхождения = Товары.СтранаПроисхождения
	|			И ПодобранныеГТД.РегистрационныйНомер = &РегистрационныйНомер
	|			И ПодобранныеГТД.РНПТ = Товары.РНПТ
	|			И НЕ ПодобранныеГТД.ПометкаУдаления
	|
	|ГДЕ
	|	УказанныеГТД.Ссылка ЕСТЬ NULL
	|	И ПодобранныеГТД.Ссылка ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("РегистрационныйНомер", Объект.НомерДекларации);
	Запрос.УстановитьПараметр("ИспользоватьУчетПрослеживаемыхИмпортныхТоваров",
								ИспользоватьУчетПрослеживаемыхИмпортныхТоваров);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НомерГТД = Справочники.НомераГТД.СоздатьЭлемент();
		
		ПараметрыДляЗаполнения = Справочники.НомераГТД.ПараметрыДляЗаполненияЭлемента(Выборка.НомерДляСФ,
																						Выборка.СтранаПроисхождения);
		ЗаполнитьЗначенияСвойств(ПараметрыДляЗаполнения, Выборка, , "СтранаПроисхождения");
		
		ПараметрыДляЗаполнения.ЗаполнитьПорядковыйНомерТовараАвтоматически = Истина;
		
		НомерГТД.Заполнить(ПараметрыДляЗаполнения);
		НомерГТД.Записать();
	КонецЦикла;
	
	// Подбираем номера ГТД в товары.
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Товары.НомерСтроки,
	|	Товары.СтранаПроисхождения				КАК СтранаПроисхождения,
	|	Товары.НомерДляСФ						КАК НомерДляСФ,
	|	ПодобранныеГТД.Ссылка					КАК НомерГТД,
	|	ПодобранныеГТД.ПорядковыйНомерТовара	КАК ПорядковыйНомерТовараИзПодобранногоНомераГТД
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НомераГТД КАК УказанныеГТД
	|		ПО УказанныеГТД.Ссылка = Товары.НомерГТД
	|			И УказанныеГТД.РНПТ = Товары.РНПТ
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НомераГТД КАК ПодобранныеГТД
	|		ПО ПодобранныеГТД.Код = Товары.НомерДляСФ
	|			И ПодобранныеГТД.СтранаПроисхождения = Товары.СтранаПроисхождения
	|			И ПодобранныеГТД.РНПТ = Товары.РНПТ
	|			И НЕ ПодобранныеГТД.ПометкаУдаления
	|
	|ГДЕ
	|	УказанныеГТД.Ссылка ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	Товары.НомерСтроки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		ОбработанныеНомераГТД = Новый СписокЗначений;
		
		// слияние упорядоченных массивов, |Выборки| <= |Товары|
		Для Каждого Товар Из Объект.Товары Цикл
			Если ЗначениеЗаполнено(Товар.НомерГТД)
				Или Товар.НомерСтроки < Выборка.НомерСтроки Тогда
			
				Продолжить;
				
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(Выборка.НомерГТД) Тогда
				ВызватьИсключение НСтр("ru = 'Обнаружены проблемы в подборе номеров ГТД.';
										|en = 'Issues found with CCD numbers selection.'");
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Выборка.НомерГТД)
				И ОбработанныеНомераГТД.НайтиПоЗначению(Выборка.НомерГТД) = Неопределено Тогда
				
				ПорядковыйНомерТовараИзНомераДляСФ = Справочники.НомераГТД.ПорядковыйНомерТовараИзНомераТаможеннойДекларации(Выборка.НомерДляСФ);
				
				Если Не Выборка.ПорядковыйНомерТовараИзПодобранногоНомераГТД = ПорядковыйНомерТовараИзНомераДляСФ Тогда
					НомерГТД = Выборка.НомерГТД.ПолучитьОбъект();
					
					Если НомерГТД = Неопределено Тогда
						ВызватьИсключение НСтр("ru = 'Обнаружены проблемы при изменении подобранных номеров ГТД.';
												|en = 'Problems were found when changing the selected CCD numbers.'");
					КонецЕсли;
					
					ЗаблокироватьДанныеДляРедактирования(Выборка.НомерГТД);
					НомерГТД.ПорядковыйНомерТовара = ПорядковыйНомерТовараИзНомераДляСФ;
					НомерГТД.Записать();
				КонецЕсли;
				
				ОбработанныеНомераГТД.Добавить(Выборка.НомерГТД);
				
			КонецЕсли;
			
			Товар.НомерГТД = Выборка.НомерГТД;
			
			Если Не Выборка.Следующий() Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#Область Проведение

Функция ТекстЗапросаТаблицаЖурналУчетаСчетовФактур(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ЖурналУчетаСчетовФактур";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Документы.ТаможеннаяДекларацияИмпорт.УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЖурналУчетаСчетовФактур.Период КАК Период,
	|	ЖурналУчетаСчетовФактур.Организация КАК Организация,
	|	ЖурналУчетаСчетовФактур.Контрагент КАК Контрагент,
	|	ЖурналУчетаСчетовФактур.СчетФактура КАК СчетФактура,
	|	ЖурналУчетаСчетовФактур.ЧастьЖурнала КАК ЧастьЖурнала,
	|	ЖурналУчетаСчетовФактур.НомерСчетаФактуры КАК НомерСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.КодВидаОперации КАК КодВидаОперации,
	|	ЖурналУчетаСчетовФактур.КодВидаСделки КАК КодВидаСделки,
	|	ЖурналУчетаСчетовФактур.КодСпособаВыставленияПолучения КАК КодСпособаВыставленияПолучения,
	|	ЖурналУчетаСчетовФактур.Валюта КАК Валюта,
	|	ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуре КАК СуммаПоСчетуФактуре,
	|	ЖурналУчетаСчетовФактур.СуммаНДС КАК СуммаНДС,
	|	ЖурналУчетаСчетовФактур.ИндексСтроки КАК ИндексСтроки,
	|	ЖурналУчетаСчетовФактур.Сторно КАК Сторно,
	|	ЖурналУчетаСчетовФактур.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
	|	ЖурналУчетаСчетовФактур.СчетФактураВыданныйПокупателю КАК СчетФактураВыданныйПокупателю,
	|	ЖурналУчетаСчетовФактур.СчетФактураПолученныйОтПродавца КАК СчетФактураПолученныйОтПродавца,
	|	ЖурналУчетаСчетовФактур.ИсправлениеСобственнойОшибки КАК ИсправлениеСобственнойОшибки,
	|	ЖурналУчетаСчетовФактур.ДатаВыставленияПолучения КАК ДатаВыставленияПолучения,
	|	ЖурналУчетаСчетовФактур.НомерИсправления КАК НомерИсправления,
	|	ЖурналУчетаСчетовФактур.ДатаИсправления КАК ДатаИсправления,
	|	ЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры КАК НомерКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры КАК ДатаКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.НомерИсправленияКорректировочногоСчетаФактуры КАК НомерИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.ДатаИсправленияКорректировочногоСчетаФактуры КАК ДатаИсправленияКорректировочногоСчетаФактуры,
	|	ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУменьшение КАК СуммаПоСчетуФактуреРазницаУменьшение,
	|	ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУвеличение КАК СуммаПоСчетуФактуреРазницаУвеличение,
	|	ЖурналУчетаСчетовФактур.СуммаНДСРазницаУменьшение КАК СуммаНДСРазницаУменьшение,
	|	ЖурналУчетаСчетовФактур.СуммаНДСРазницаУвеличение КАК СуммаНДСРазницаУвеличение,
	|	ЖурналУчетаСчетовФактур.ПоСтавкеБезНДС КАК ПоСтавкеБезНДС,
	|	ЖурналУчетаСчетовФактур.СчетФактураНеВыставляется КАК СчетФактураНеВыставляется,
	|	ЖурналУчетаСчетовФактур.Продавец КАК Продавец,
	|	ЖурналУчетаСчетовФактур.КППКонтрагента КАК КППКонтрагента,
	|	ЖурналУчетаСчетовФактур.Посредник КАК Посредник,
	|	ЖурналУчетаСчетовФактур.УдалитьСчетФактураВыданныйПокупателю КАК УдалитьСчетФактураВыданныйПокупателю,
	|	ЖурналУчетаСчетовФактур.Субкомиссионер КАК Субкомиссионер,
	|	ЖурналУчетаСчетовФактур.НомерСчетаФактурыПродавца КАК НомерСчетаФактурыПродавца,
	|	ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреКомиссия КАК СуммаПоСчетуФактуреКомиссия,
	|	ЖурналУчетаСчетовФактур.СуммаНДСКомиссия КАК СуммаНДСКомиссия,
	|	ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУменьшениеКомиссия КАК СуммаПоСчетуФактуреРазницаУменьшениеКомиссия,
	|	ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуреРазницаУвеличениеКомиссия КАК СуммаПоСчетуФактуреРазницаУвеличениеКомиссия,
	|	ЖурналУчетаСчетовФактур.СуммаНДСРазницаУменьшениеКомиссия КАК СуммаНДСРазницаУменьшениеКомиссия,
	|	ЖурналУчетаСчетовФактур.СуммаНДСРазницаУвеличениеКомиссия КАК СуммаНДСРазницаУвеличениеКомиссия,
	|	ЖурналУчетаСчетовФактур.КодВидаОперацииКомиссия КАК КодВидаОперацииКомиссия,
	|	ЖурналУчетаСчетовФактур.ДатаСчетаФактурыПродавца КАК ДатаСчетаФактурыПродавца,
	|	ЖурналУчетаСчетовФактур.ИННКонтрагента КАК ИННКонтрагента,
	|	ЖурналУчетаСчетовФактур.ИННПродавца КАК ИННПродавца,
	|	ЖурналУчетаСчетовФактур.КПППродавца КАК КПППродавца,
	|	ЖурналУчетаСчетовФактур.ИННСубкомиссионера КАК ИННСубкомиссионера,
	|	ЖурналУчетаСчетовФактур.КППСубкомиссионера КАК КППСубкомиссионера
	|ИЗ
	|	РегистрСведений.ЖурналУчетаСчетовФактур КАК ЖурналУчетаСчетовФактур
	|ГДЕ
	|	ЖурналУчетаСчетовФактур.Регистратор = &Ссылка
	|	И &РучнаяКорректировкаЖурналаСФ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                         КАК Период,
	|	&Организация                    КАК Организация,
	|	ВЫБОР
	|		КОГДА &КонтрагентПоставщика = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА &Контрагент
	|		ИНАЧЕ &КонтрагентПоставщика
	|	КОНЕЦ                           КАК Контрагент,
	|	&Ссылка                         КАК СчетФактура,
	|	ЗНАЧЕНИЕ(Перечисление.ЧастиЖурналаУчетаСчетовФактур.ПолученныеСчетаФактуры) КАК ЧастьЖурнала,
	|	&НомерДекларации                КАК НомерСчетаФактуры,
	|	&ДатаДекларации                 КАК ДатаСчетаФактуры,
	|	20                              КАК КодВидаОперации,
	|	2                               КАК КодВидаСделки,
	|	1                               КАК КодСпособаПолучения,
	|	&Валюта                         КАК Валюта,
	|	СУММА(ВЫРАЗИТЬ(ДанныеДокумента.СуммаПоСчетуФактуре* &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2))) КАК СуммаПоСчетуФактуре,
	|	СУММА(ВЫРАЗИТЬ(ДанныеДокумента.СуммаНДС * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(31,2)))           КАК СуммаНДС,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL
	|ИЗ
	// таможенная стоимость (т.к. нет фактурной) и пошлины
	|	(ВЫБРАТЬ
	|		СУММА(Разделы.ТаможеннаяСтоимость + Разделы.СуммаПошлины) КАК СуммаПоСчетуФактуре,
	|		СУММА(Разделы.СуммаНДС)                                   КАК СуммаНДС
	|	ИЗ
	|		Документ.ТаможеннаяДекларацияИмпорт.Разделы КАК Разделы
	|	ГДЕ
	|		Разделы.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// НДС, включенный в стоимость
	|	ВЫБРАТЬ
	|		СУММА(ТаможеннаяДекларацияИмпортТовары.СуммаНДС),
	|		0
	|	ИЗ
	|		Документ.ТаможеннаяДекларацияИмпорт.Товары КАК ТаможеннаяДекларацияИмпортТовары
	|	ГДЕ
	|		ТаможеннаяДекларацияИмпортТовары.Ссылка = &Ссылка
	|		И ТаможеннаяДекларацияИмпортТовары.ЗакупкаПодДеятельность В(
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// Таможенный сбор и штрафы, если они включаются в стоимость импортированных ценностей.
	|	ВЫБРАТЬ
	|		ВЫБОР
	|				КОГДА ТаможеннаяДекларацияИмпорт.СтатьяРасходовСбор.ВариантРаспределенияРасходовРегл В (
	|						ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы),
	|						ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров))
	|					ТОГДА ТаможеннаяДекларацияИмпорт.ТаможенныйСбор
	|				ИНАЧЕ 0
	|			КОНЕЦ + ВЫБОР
	|				КОГДА ТаможеннаяДекларацияИмпорт.СтатьяРасходовШтраф.ВариантРаспределенияРасходовРегл В (
	|						ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы),
	|						ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров))
	|					ТОГДА ТаможеннаяДекларацияИмпорт.ТаможенныйШтраф
	|				ИНАЧЕ 0
	|			КОНЕЦ ,
	|		0
	|	ИЗ
	|		Документ.ТаможеннаяДекларацияИмпорт КАК ТаможеннаяДекларацияИмпорт
	|	ГДЕ
	|		ТаможеннаяДекларацияИмпорт.Ссылка = &Ссылка) КАК ДанныеДокумента
	|ГДЕ
	|	НЕ &РучнаяКорректировкаЖурналаСФ
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

//++ НЕ УТ

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		Документы.ТаможеннаяДекларацияИмпорт.ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		Документы.ТаможеннаяДекларацияИмпорт.ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                      КАК Период,
	|	&Организация                 КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|";
	ТекстЗапроса = ТекстЗапроса + "ОБЪЕДИНИТЬ ВСЕ"
		+ ДоходыИРасходыСервер.ДополнитьТекстЗапросаТаблицаОтражениеДокументовВРеглУчете();
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции
//-- НЕ УТ

#КонецОбласти

#КонецОбласти

//-- Локализация
