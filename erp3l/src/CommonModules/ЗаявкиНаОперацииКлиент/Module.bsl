
#Область ПрограммныйИнтерфейс
	
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ФормаЗаявки

Функция ТребуетсяОбработкаСобытия(ИмяСобытия, ИменаСобытийОбновленияПлатежнойПозиции = "") Экспорт
	
	Если ИменаСобытийОбновленияПлатежнойПозиции = "" Тогда
		События = Новый Массив;
	Иначе
		События = СтрРазделить(ИменаСобытийОбновленияПлатежнойПозиции, ",");
	КонецЕсли;
	
	События.Добавить("ОбъектСогласован");
	События.Добавить("ОбъектОтклонен");
	События.Добавить("МаршрутИнициализирован");
	События.Добавить("СостояниеЗаявкиПриИзменении");
	
	Возврат События.Найти(ИмяСобытия) <> неопределено;
	
КонецФункции

Процедура ПослеЗаписи(ПараметрыЗаписи) Экспорт
	Оповестить("ИзмененДокументТранзакции", ПараметрыЗаписи.ПараметрыОповещения);
КонецПроцедуры

Процедура РазбитьСтроку(ТабличнаяЧасть, ТаблицаФормы, ИмяРеквизита = "Сумма", ТекстВводаНовогоЗначения = "", ТекстПредупреждения = "") Экспорт
	
	ИсходнаяСтрока = ТаблицаФормы.ТекущиеДанные;
	
	Если ИсходнаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПараметровПереноса = Новый Структура;
	СтруктураПараметровПереноса.Вставить("ТабличнаяЧасть", ТабличнаяЧасть);
	СтруктураПараметровПереноса.Вставить("ТаблицаФормы", ТаблицаФормы);
	СтруктураПараметровПереноса.Вставить("ИмяРеквизита", ИмяРеквизита);
	СтруктураПараметровПереноса.Вставить("ИсходнаяСтрока", ИсходнаяСтрока);
	
	СтруктураПараметровПереноса.Вставить("ТекстПредупреждения", 
		?(ПустаяСтрока(ТекстПредупреждения), 
			НСтр("ru = 'Сумма к переносу не должна превышать сумму исходной строки.'"), 
			ТекстПредупреждения));
	
	Если ИсходнаяСтрока[ИмяРеквизита] = 0 Тогда
		// Возможен единственный вариант.
		ДобавитьСтрокуРазбиения(0, СтруктураПараметровПереноса);
	Иначе
		ЗаголовокВводаНовогоЗначения =  ?(ПустаяСтрока(ТекстВводаНовогоЗначения), 
			НСтр("ru = 'Введите сумму к переносу в новую строку.'"), 
			ТекстВводаНовогоЗначения);
		ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьСтрокуРазбиения", ЭтотОбъект, СтруктураПараметровПереноса);	
		ПоказатьВводЧисла(ОписаниеОповещения, ИсходнаяСтрока[ИмяРеквизита], ЗаголовокВводаНовогоЗначения, 15, 2);
	КонецЕсли;
	
КонецПроцедуры

Процедура РеестрДокументовНажатие(Форма) Экспорт
	Если ЗначениеЗаполнено(Форма.РеестрДокументов) Тогда
		ПоказатьЗначение( ,Форма.РеестрДокументов);
	Иначе
		ТекстСообщения = НСтр("ru = 'Не задан реестр согласования'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 

#Область ФормаСпискаВыбора

// Осуществляет обработку оповещения формы списка объекта в контексте согласования. 
Процедура ОбработкаОповещенияФормыСписка(ИмяСобытияВход, ЭлементСписокВход) Экспорт
	ВстраиваниеОПККлиентПереопределяемый.ОбработкаОповещенияФормыСписка(ИмяСобытияВход, ЭлементСписокВход);
КонецПроцедуры

Процедура ОткрытьСписокРеестров() Экспорт
	ИмяФормы = "Справочник.РеестрыСогласуемыхОбъектов.ФормаСписка";
	ОткрытьФорму(ИмяФормы);
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьСтрокуРазбиения(Результат, ПараметрыПереноса)
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат < 0 Тогда
		
		ПоказатьПредупреждение(,НСтр("ru = 'Необходимо ввести положительное значение.'"));
		Возврат;
		
	КонецЕсли;
	
	ИсходнаяСтрока = ПараметрыПереноса.ИсходнаяСтрока;
	ТаблицаФормы = ПараметрыПереноса.ТаблицаФормы;
	ТабличнаяЧасть =  ПараметрыПереноса.ТабличнаяЧасть;
	ИмяРеквизита = ПараметрыПереноса.ИмяРеквизита;
	
	Если ИсходнаяСтрока[ИмяРеквизита] < Результат Тогда
		ПоказатьПредупреждение(,ПараметрыПереноса.ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ИндексСтроки = ТабличнаяЧасть.Индекс(ИсходнаяСтрока);
	НоваяСтрока = ТабличнаяЧасть.Вставить(ИндексСтроки + 1);
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ИсходнаяСтрока);
	НоваяСтрока[ИмяРеквизита] = Результат;
	ИсходнаяСтрока[ИмяРеквизита] = ИсходнаяСтрока[ИмяРеквизита] - Результат;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ИсходнаяСтрока, "СуммаНДС") Тогда
		ОбщегоНазначенияОПККлиентСервер.ПересчитатьСуммуНДС(ИсходнаяСтрока);
		ОбщегоНазначенияОПККлиентСервер.ПересчитатьСуммуНДС(НоваяСтрока);
	КонецЕсли;
	
	ТаблицаФормы.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
	
КонецПроцедуры

#КонецОбласти
 