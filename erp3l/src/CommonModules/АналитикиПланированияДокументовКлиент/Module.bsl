
#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура НадписьАналитикиПланированияДокументаНажатие(Форма, Элемент, СтандартнаяОбработка, Оповещение = Неопределено) Экспорт
	
	Попытка
		Форма.ЗаблокироватьДанныеФормыДляРедактирования();
	Исключение
		ПоказатьПредупреждение(Неопределено, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат;
	КонецПопытки;
	
	ПараметрыОткрытия = Новый Структура();
	ПараметрыОткрытия.Вставить("ТолькоПросмотр", Форма.ТолькоПросмотр);
	ПараметрыОткрытия.Вставить("Документ", Форма.Объект.Ссылка);
	ПараметрыОткрытия.Вставить("АналитикиПланированияДокумента", СформироватьАналитикиПланированияДокумента(Форма));
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Форма);
	ДополнительныеПараметры.Вставить("Элемент", Элемент);
	ДополнительныеПараметры.Вставить("ОповещениеПослеЗавершения", Оповещение);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("НадписьАналитикиПланированияДокументаНажатиеЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму("ОбщаяФорма.АналитикиПланированияДокумента", 
		ПараметрыОткрытия,
		Форма,,,,
		ОповещениеОЗакрытии,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// Служебный обработчик закрытия формы правил оплаты.
Процедура НадписьАналитикиПланированияДокументаНажатиеЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия <> Неопределено Тогда 
		ЗагрузитьАналитикиПланированияДокумента(ДополнительныеПараметры.Форма, РезультатЗакрытия);
	КонецЕсли;
	
	Если ДополнительныеПараметры.ОповещениеПослеЗавершения <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеЗавершения, РезультатЗакрытия);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьАналитикиПланированияДокумента(Форма)
	
	Объект = Форма.Объект;
	
	Результат = Новый Массив;
	
	ИменаРеквизитов = ИменаРеквизитов();
	Таблица = Форма.Объект.АналитикиПланирования;
	
	Для Каждого Строка Из Таблица Цикл
		Данные = Новый Структура(ИменаРеквизитов);
		ЗаполнитьЗначенияСвойств(Данные, Строка);
		Результат.Добавить(Данные);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗагрузитьАналитикиПланированияДокумента(Форма, РезультатЗакрытия)
	
	Объект = Форма.Объект;
	
	ИменаРеквизитов = ИменаРеквизитов();
	Таблица = Форма.Объект.АналитикиПланирования;
	Таблица.Очистить();
	Для Каждого Строка Из РезультатЗакрытия Цикл
		ЗаполнитьЗначенияСвойств(Таблица.Добавить(), Строка, ИменаРеквизитов);
	КонецЦикла;
	Форма.Модифицированность = Истина;
	
	АналитикиПланированияДокументовКлиентСервер.ЗаполнитьЗаголовокДекорации(Форма);
	
КонецПроцедуры

Функция ИменаРеквизитов()
	
	Возврат "ВидБюджета, ДокументРезервирования, ЦФО, Проект, СтатьяБюджета,"
		+ АналитикиСтатейБюджетовУХКлиентСервер.РеквизитыАналитикИзШаблона("Аналитика%1");
	
КонецФункции

#КонецОбласти
 
