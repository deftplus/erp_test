
#Область ПрограммныйИнтерфейс
	
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ФормаСтатьиБюджета
	
// Действия, при изменении вида аналитики в таблице видов аналитик
Процедура ВидыАналитикПриИзменении(Форма) Экспорт
	
	АналитикиСтатейБюджетовУХКлиентСервер.ВычислитьМаксимальныйНомерЗаполненнойАналитики(Форма);
	Форма.Модифицированность = Истина;
	
КонецПроцедуры

Процедура ДекорацияБлокирующиеСсылкиНажатие(СтатьяСсылка) Экспорт
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("БлокированныйОбъект", СтатьяСсылка);
	ДопПараметры.Вставить("БлокирующиеСсылки", АналитикиСтатейБюджетовУХВызовСервера.БлокирующиеСсылки(СтатьяСсылка));
	
	ОткрытьФорму("ОбщаяФорма.СписокБлокирующихСсылок", ДопПараметры, ЭтотОбъект, ,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытий
	
Процедура ПриИзмененииСтатьиБюджета(Форма, ИмяЭлемента) Экспорт
	
	Если НЕ ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ОписаниеФормы") Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеФормы = Форма.ОписаниеФормы;
	Если ОписаниеФормы = Неопределено 
		ИЛИ ТипЗнч(ОписаниеФормы) <> Тип("ФиксированнаяСтруктура") 
		ИЛИ НЕ ОписаниеФормы.Свойство("СтатьиБюджетов") 
		ИЛИ ТипЗнч(ОписаниеФормы.СтатьиБюджетов) <> Тип("Соответствие")	Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеСтатьи = ОписаниеФормы.СтатьиБюджетов[ИмяЭлемента];
	Если ОписаниеСтатьи = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//
	Если ОписаниеСтатьи.ЭтоТаблица Тогда
		Таблица = АналитикиСтатейБюджетовУХКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, ОписаниеСтатьи.ПутьКДанным);
		СтрокаТаблицы = Таблица.НайтиПоИдентификатору(Форма.Элементы[ОписаниеСтатьи.ИмяЭлементаТаблицаФормы].ТекущаяСтрока);
		ЗначениеСтатьи = СтрокаТаблицы[ОписаниеСтатьи.ИмяРеквизита];
		ДанныеВидАналитики	= СтрокаТаблицы;
		ДанныеАналитика		= СтрокаТаблицы;
	Иначе
		ПутьКДанным = ОписаниеСтатьи.ИмяРеквизита;
		Если ЗначениеЗаполнено(ОписаниеСтатьи.ПутьКДанным) Тогда
			ПутьКДанным = ОписаниеСтатьи.ПутьКДанным + "." + ПутьКДанным;
		КонецЕсли;
		ЗначениеСтатьи = АналитикиСтатейБюджетовУХКлиентСервер.ПолучитьЗначениеПоПутиКДанным(Форма, ПутьКДанным);
		ДанныеВидАналитики	= АналитикиСтатейБюджетовУХКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, ОписаниеСтатьи.Аналитики.ВидАналитикиПуть);
		ДанныеАналитика		= АналитикиСтатейБюджетовУХКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, ОписаниеСтатьи.Аналитики.АналитикаПуть);
	КонецЕсли;
	
	Если ОписаниеСтатьи.ТолькоЛимитируемыеАналитики Тогда
		ПараметрыЛимитирования = АналитикиСтатейБюджетовУХКлиентСервер.ПолучитьПараметрыЛимитированияСтатьиБюджета(Форма, ИмяЭлемента);
	КонецЕсли;
	
	АналитикиСтатейБюджетовУХКлиентСервер.ЗаполнитьСведенияОВидахАналитик(ЗначениеСтатьи, ДанныеВидАналитики, ОписаниеСтатьи, ПараметрыЛимитирования);
	
	Действия = Новый Структура("ПривестиЗначенияАналитик");
	АналитикиСтатейБюджетовУХКлиентСервер.ОбработатьЗначенияАналитик(Форма, ОписаниеСтатьи, ДанныеВидАналитики, ДанныеАналитика, Действия);
	
	АналитикиСтатейБюджетовУХКлиентСервер.НастроитьПоляВводаАналитик(Форма, ОписаниеСтатьи, ДанныеВидАналитики);
	АналитикиСтатейБюджетовУХКлиентСервер.НастроитьСвязиАналитик(Форма, ОписаниеСтатьи, ДанныеВидАналитики, ДанныеАналитика);
		
КонецПроцедуры

Процедура ПриИзмененииАналитикиСтатьиБюджета(Форма, ИмяЭлемента) Экспорт
	АналитикиСтатейБюджетовУХКлиентСервер.ИнициализироватьСвязиАналитикСтатьиБюджета(Форма, ИмяЭлемента, Истина);
КонецПроцедуры

Процедура ПриАктивизацииСтрокиТаблицыФормы(Форма, ИмяЭлемента) Экспорт
	НастроитьПоляВводаТаблицыФормы(Форма, ИмяЭлемента);
КонецПроцедуры

Процедура ПередНачаломИзмененияСтрокиТаблицыФормы(Форма, ИмяЭлемента) Экспорт
	НастроитьПоляВводаТаблицыФормы(Форма, ИмяЭлемента);
КонецПроцедуры

#КонецОбласти 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура НастроитьПоляВводаТаблицыФормы(Форма, ИмяЭлемента)
	
	Если НЕ ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ОписаниеФормы") Тогда
		Возврат; // нет описания формы
	КонецЕсли;
	
	ОписаниеФормы = Форма.ОписаниеФормы;
	Если ОписаниеФормы = Неопределено ИЛИ ТипЗнч(ОписаниеФормы) <> Тип("ФиксированнаяСтруктура") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ОписаниеФормы.Свойство("СтатьиБюджетов") ИЛИ ТипЗнч(ОписаниеФормы.СтатьиБюджетов) <> Тип("Соответствие") Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыНаТаблицеФормы = ОписаниеФормы.ТаблицыФормы[ИмяЭлемента];
	Если РеквизитыНаТаблицеФормы = неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ОписаниеСтатьи Из РеквизитыНаТаблицеФормы Цикл
		
		Если НЕ ОписаниеСтатьи.ЭтоТаблица Тогда
			Продолжить;
		КонецЕсли;
		
		АналитикиСтатейБюджетовУХКлиентСервер.НастроитьПоляВводаАналитик(Форма, ОписаниеСтатьи, Форма.Элементы[ИмяЭлемента].ТекущиеДанные);
		АналитикиСтатейБюджетовУХКлиентСервер.НастроитьСвязиАналитик(Форма, ОписаниеСтатьи, Форма.Элементы[ИмяЭлемента].ТекущиеДанные, Форма.Элементы[ИмяЭлемента].ТекущиеДанные);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

