#Область Напоминания

// Открывает форму текущих напоминаний пользователя.
Процедура ПроверитьТекущиеНапоминанияПользователя() Экспорт
	// Получим массив актуальны напоминаний.
	СтруктураНапоминания = МодульУправленияОповещениямиУХ.ОбработатьНапоминанияТекущегоПользователя();
	МассивНапоминаний = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураНапоминания, "МассивНапоминаний", Новый Массив);
	ИнтервалДоБлижайшего = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураНапоминания, "ИнтервалДоБлижайшего", 0);
	Если ИнтервалДоБлижайшего = 0 Тогда
		ИнтервалСледующейПроверки = ОбщегоНазначенияКлиентСерверУХ.МаксимальныйИнтервалПроверкиНапоминаний();		// Больше оповещений нет. Проверим снова позже.
	Иначе
		ИнтервалСледующейПроверки = ИнтервалДоБлижайшего;
	КонецЕсли;
	// При необходимости, отобразим оповещение пользователю.
	Если МассивНапоминаний.Количество() > 0 Тогда  	
		Для Каждого ТекМассивНапоминаний Из МассивНапоминаний Цикл
			ТекстОповещения = Строка(ТекМассивНапоминаний.ВидСобытияОповещения);
			КартинкаОповещения = БиблиотекаКартинок.ИнформацияНовости32Статичная;
			Если ЗначениеЗаполнено(ТекМассивНапоминаний.Источник) Тогда
				СсылкаОповещения = ПолучитьНавигационнуюСсылку(ТекМассивНапоминаний.Источник);
				ПояснениеОповещения = НСтр("ru = 'Событие наступит %ВремяСобытия%.%СимволыПС%Перейти к объекту.'");
				ПояснениеОповещения = СтрЗаменить(ПояснениеОповещения, "%ВремяСобытия%", Строка(ТекМассивНапоминаний.ВремяСобытия));
				ПояснениеОповещения = СтрЗаменить(ПояснениеОповещения, "%СимволыПС%", Символы.ПС);
				ПоказатьОповещениеПользователя(ТекстОповещения, СсылкаОповещения, ПояснениеОповещения, КартинкаОповещения);
			Иначе
				ПояснениеОповещения = НСтр("ru = 'Событие наступит %ВремяСобытия%.'");
				ПояснениеОповещения = СтрЗаменить(ПояснениеОповещения, "%ВремяСобытия%", Строка(ТекМассивНапоминаний.ВремяСобытия));
				ПоказатьОповещениеПользователя(ТекстОповещения, , ПояснениеОповещения, КартинкаОповещения);
			КонецЕсли;
		КонецЦикла;
	Иначе
		// Ничего не делаем.
	КонецЕсли;
	// Подключим обработчик ожидания повторно.
	ПодключитьОбработчикОжидания("ПроверитьТекущиеНапоминанияПользователя", ИнтервалСледующейПроверки, Истина);
КонецПроцедуры		// ПроверитьТекущиеНапоминанияПользователя()

#КонецОбласти