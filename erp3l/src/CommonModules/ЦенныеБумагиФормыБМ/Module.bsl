#Область ПрограммныйИнтерфейс

Процедура НастроитьФормуОбъектаУчета(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Элементы	= Форма.Элементы;
	Объект		= Форма.Объект;

	// Добавим группу МСФО.
	ГруппаМСФО = Элементы.Добавить("ГруппаРеквизитыМСФО", Тип("ГруппаФормы"));
	ГруппаМСФО.Заголовок = "МСФО";
	ГруппаМСФО.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМСФО.ОтображатьЗаголовок = Ложь;
	ГруппаМСФО.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаМСФО.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	// Биржевой код
	ДобавитьПолеВвода("БиржевойКод",Элементы, ГруппаМСФО);
	
	// Валюта номинала
	ДобавитьПолеВвода("ВалютаНоминала",Элементы, ГруппаМСФО);
	
	// Валюта котировки
	ДобавитьПолеВвода("ВалютаКотировки",Элементы, ГруппаМСФО);
	
	// Параметры учета ФИ
	ДобавитьПолеВвода("ПараметрыУчетаФИ", Элементы, ГруппаМСФО);

	// Номинал
	ГруппаНоминал = Элементы.Добавить("ГруппаНоминал", Тип("ГруппаФормы"), ГруппаМСФО);
	ГруппаНоминал.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаНоминал.ОтображатьЗаголовок = Ложь;
	ГруппаНоминал.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаНоминал.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	ДобавитьПолеВвода("Номинал",Элементы, ГруппаНоминал);
	НовыйЭлемент = Элементы.Добавить("ВалютаНоминалаНадпись", Тип("ПолеФормы"),ГруппаНоминал);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеНадписи;
	НовыйЭлемент.ПутьКДанным = "Объект.ВалютаНоминала";
	НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	
	// Количество
	ДобавитьПолеВвода("Количество",Элементы, ГруппаМСФО);
	
	// Дата выпуска
	ДобавитьПолеВвода("ДатаВыпуска",Элементы, ГруппаМСФО);
	
	// Срок погашения
	ДобавитьПолеВвода("СрокПогашения",Элементы, ГруппаМСФО);

	// Должник
	ДобавитьПолеВвода("Должник",Элементы, ГруппаМСФО);
	
	// Уровень иерархии справедливой стоимости
	НовыйЭлемент = ДобавитьПолеВвода("УровеньИерархииСправедливойСтоимости",Элементы, ГруппаМСФО);
	НовыйЭлемент.ВысотаЗаголовка = 2;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДобавитьПолеВвода(Имя, Элементы, Родитель)
	
	НовыйЭлемент = Элементы.Добавить(Имя, Тип("ПолеФормы"),Родитель);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "Объект."+Имя;
	
	Возврат НовыйЭлемент;
	
КонецФункции


#КонецОбласти