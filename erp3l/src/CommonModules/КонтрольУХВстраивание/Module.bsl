
#Область ПрограммныйИнтерфейс

// Регистрирует проверки
Процедура ВсеПроверки(ОбщиеМодулиКонтроля) Экспорт
	
	//ОбщиеМодулиКонтроля.Вставить(<ПланыВидовХарактеристик.ВидыКонтроляДокументов.НовыйВидКонтроля>, <ИмяМодуляОтвечающегоЗаПроверку>);
	
КонецПроцедуры

//
Процедура РазмещениеИсточниковКонтроля(РазмещениеИсточников) Экспорт
	
	РазмещениеИсточников.Вставить(Метаданные.Документы.ЗаказПоставщику, ЗаказПоставщикуСерверВнедрениеЕХ);
	РазмещениеИсточников.Вставить(Метаданные.Документы.ЗаказКлиента, ВстраиваниеУХЗаказКлиента);
	РазмещениеИсточников.Вставить(Метаданные.Документы.ПриобретениеТоваровУслуг, ВстраиваниеУХПриобретениеТоваровУслуг);
	РазмещениеИсточников.Вставить(Метаданные.Документы.РеализацияТоваровУслуг, ВстраиваниеУХРеализацияТоваровУслуг);
	
	РазмещениеИсточников.Вставить(Метаданные.Документы.АктВыполненныхРабот, ВстраиваниеУХАктВыполненныхРабот);
	РазмещениеИсточников.Вставить(Метаданные.Документы.ЗаявкаНаВозвратТоваровОтКлиента, ВстраиваниеУХЗаявкаНаВозвратТоваровОтКлиента);
	РазмещениеИсточников.Вставить(Метаданные.Документы.ПриобретениеУслугПрочихАктивов, ВстраиваниеУХПриобретениеУслугПрочихАктивов);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Функция возвращает запрос для получения исполнения по контрагенту по роли Поставщик
Функция ТекстЗапроса_ТекущаяЗадолженностьКонтрагентаПоЕгоРоли() Экспорт
	
	Возврат 
	"ВЫБРАТЬ
	|	РасчетыСПоставщикамиПоСрокамОстатки.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|	ВалютаУправленческогоУчета.Значение КАК ВалютаУпрУчета,
	|	СУММА(РасчетыСПоставщикамиПоСрокамОстатки.ДолгУпрОстаток - РасчетыСПоставщикамиПоСрокамОстатки.ПредоплатаУпрОстаток) КАК ЗадолженностьТекущая
	|ПОМЕСТИТЬ ВТ_ЗадолженностьКонтрагента
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам.Остатки(, АналитикаУчетаПоПартнерам.Контрагент = &Контрагент) КАК РасчетыСПоставщикамиПоСрокамОстатки,
	|	Константа.ВалютаУправленческогоУчета КАК ВалютаУправленческогоУчета
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСПоставщикамиПоСрокамОстатки.АналитикаУчетаПоПартнерам.Контрагент,
	|	ВалютаУправленческогоУчета.Значение";
	
КонецФункции

// Функция возвращает текст запроса для получения исполнения по договору
Функция ТекстЗапроса_ИсполнениеПоДоговору() Экспорт
	
	Возврат
	"ВЫБРАТЬ
	|	РасчетыСПоставщикамиПоСрокам.ДокументРегистратор КАК ДокументРегистратор,
	|	РасчетыСПоставщикамиПоСрокам.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСПоставщикамиПоСрокам.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСПоставщикамиПоСрокам.Валюта КАК Валюта,
	|	РасчетыСПоставщикамиПоСрокам.РасчетныйДокумент КАК РасчетныйДокумент,
	|	РасчетыСПоставщикамиПоСрокам.ВидДвижения КАК ВидДвижения,
	|	РасчетыСПоставщикамиПоСрокам.Предоплата КАК Предоплата,
	|	РасчетыСПоставщикамиПоСрокам.Долг КАК Долг,
	|	РасчетыСПоставщикамиПоСрокам.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА РасчетыСПоставщикамиПоСрокам.ДокументРегистратор ССЫЛКА Документ.РасходныйКассовыйОрдер
	|				ИЛИ РасчетыСПоставщикамиПоСрокам.ДокументРегистратор ССЫЛКА Документ.ПриходныйКассовыйОрдер
	|				ИЛИ РасчетыСПоставщикамиПоСрокам.ДокументРегистратор ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств
	|				ИЛИ РасчетыСПоставщикамиПоСрокам.ДокументРегистратор ССЫЛКА Документ.ПоступлениеБезналичныхДенежныхСредств
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоОплата
	|ПОМЕСТИТЬ ВТ_Данные
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК РасчетыСПоставщикамиПоСрокам
	|ГДЕ
	|	РасчетыСПоставщикамиПоСрокам.АналитикаУчетаПоПартнерам В
	|			(ВЫБРАТЬ
	|				АналитикаУчетаПоПартнерам.КлючАналитики КАК КлючАналитики
	|			ИЗ
	|				РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|			ГДЕ
	|				АналитикаУчетаПоПартнерам.Организация = &Организация
	|				И АналитикаУчетаПоПартнерам.Контрагент = &Контрагент
	|				И АналитикаУчетаПоПартнерам.Договор = &Договор)
	|	И РасчетыСПоставщикамиПоСрокам.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетАвансаПоставщику)
	|	И РасчетыСПоставщикамиПоСрокам.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеКредиторскойЗадолженности)
	|	И НЕ РасчетыСПоставщикамиПоСрокам.ДокументРегистратор ССЫЛКА Документ.КорректировкаРегистров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Запрос1.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Запрос1.АналитикаУчетаПоПартнерам.Договор КАК Договор,
	|	Запрос1.Валюта КАК Валюта,
	|	СУММА(Запрос1.Исполнено) КАК Исполнено
	|ПОМЕСТИТЬ ВТ_Исполнено
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ_Данные.ДокументРегистратор КАК ДокументРегистратор,
	|		ВТ_Данные.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		ВТ_Данные.ОбъектРасчетов КАК ОбъектРасчетов,
	|		ВТ_Данные.Валюта КАК Валюта,
	|		ВТ_Данные.РасчетныйДокумент КАК РасчетныйДокумент,
	|		ВТ_Данные.ВидДвижения КАК ВидДвижения,
	|		ВЫБОР
	|			КОГДА ВТ_Данные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА 1
	|			ИНАЧЕ -1
	|		КОНЕЦ * ВТ_Данные.Предоплата КАК Исполнено,
	|		ВТ_Данные.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|	ИЗ
	|		ВТ_Данные КАК ВТ_Данные
	|	ГДЕ
	|		&Оплата = ИСТИНА
	|		И ВТ_Данные.Предоплата <> 0
	|		И ВТ_Данные.ЭтоОплата
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_Данные.ДокументРегистратор,
	|		ВТ_Данные.АналитикаУчетаПоПартнерам,
	|		ВТ_Данные.ОбъектРасчетов,
	|		ВТ_Данные.Валюта,
	|		ВТ_Данные.РасчетныйДокумент,
	|		ВТ_Данные.ВидДвижения,
	|		ВЫБОР
	|			КОГДА ВТ_Данные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -1
	|			ИНАЧЕ 1
	|		КОНЕЦ * ВТ_Данные.Долг,
	|		ВТ_Данные.ХозяйственнаяОперация
	|	ИЗ
	|		ВТ_Данные КАК ВТ_Данные
	|	ГДЕ
	|		&Оплата = ИСТИНА
	|		И ВТ_Данные.Долг <> 0
	|		И ВТ_Данные.ЭтоОплата
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_Данные.ДокументРегистратор,
	|		ВТ_Данные.АналитикаУчетаПоПартнерам,
	|		ВТ_Данные.ОбъектРасчетов,
	|		ВТ_Данные.Валюта,
	|		ВТ_Данные.РасчетныйДокумент,
	|		ВТ_Данные.ВидДвижения,
	|		ВЫБОР
	|			КОГДА ВТ_Данные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА 1
	|			ИНАЧЕ -1
	|		КОНЕЦ * ВТ_Данные.Долг,
	|		ВТ_Данные.ХозяйственнаяОперация
	|	ИЗ
	|		ВТ_Данные КАК ВТ_Данные
	|	ГДЕ
	|		&Оплата = ЛОЖЬ
	|		И ВТ_Данные.Долг <> 0
	|		И НЕ ВТ_Данные.ЭтоОплата
	|		И НЕ ВТ_Данные.ДокументРегистратор ССЫЛКА Документ.ВозвратТоваровПоставщику
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_Данные.ДокументРегистратор,
	|		ВТ_Данные.АналитикаУчетаПоПартнерам,
	|		ВТ_Данные.ОбъектРасчетов,
	|		ВТ_Данные.Валюта,
	|		ВТ_Данные.РасчетныйДокумент,
	|		ВТ_Данные.ВидДвижения,
	|		ВЫБОР
	|			КОГДА ВТ_Данные.Долг = 0
	|				ТОГДА -ВТ_Данные.Предоплата
	|			ИНАЧЕ -ВТ_Данные.Долг
	|		КОНЕЦ,
	|		ВТ_Данные.ХозяйственнаяОперация
	|	ИЗ
	|		ВТ_Данные КАК ВТ_Данные
	|	ГДЕ
	|		&Оплата = ЛОЖЬ
	|		И ВТ_Данные.ДокументРегистратор ССЫЛКА Документ.ВозвратТоваровПоставщику
	|		И ВТ_Данные.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности)) КАК Запрос1
	|
	|СГРУППИРОВАТЬ ПО
	|	Запрос1.АналитикаУчетаПоПартнерам,
	|	Запрос1.Валюта,
	|	Запрос1.АналитикаУчетаПоПартнерам.Договор
	|
	|ИМЕЮЩИЕ
	|	СУММА(Запрос1.Исполнено) <> 0";
	
КонецФункции

// Возвращает массив типов ДокументСсылка, для которых не надо формировать подключаемую команду КонтрольДокументов
Процедура ТипыДокументовИсключенияДляПодключаемойКоманды(Исключения) Экспорт
	
	Исключения.Добавить(Тип("ДокументСсылка.ВерсияСоглашенияАккредитив"));
	Исключения.Добавить(Тип("ДокументСсылка.ВерсияСоглашенияБанковскаяГарантия"));
	Исключения.Добавить(Тип("ДокументСсылка.ВерсияСоглашенияВалютноПроцентныйСвоп"));
	Исключения.Добавить(Тип("ДокументСсылка.ВерсияСоглашенияВалютныйСвоп"));
	Исключения.Добавить(Тип("ДокументСсылка.ВерсияСоглашенияВалютныйФорвард"));
	Исключения.Добавить(Тип("ДокументСсылка.ВерсияСоглашенияДепозит"));
	Исключения.Добавить(Тип("ДокументСсылка.ВерсияСоглашенияКоммерческийДоговор"));
	Исключения.Добавить(Тип("ДокументСсылка.ВерсияСоглашенияКредит"));
	Исключения.Добавить(Тип("ДокументСсылка.ПланЗакупок"));
	Исключения.Добавить(Тип("ДокументСсылка.ЗаключениеДоговораАренды"));
	Исключения.Добавить(Тип("ДокументСсылка.ИзменениеУсловийДоговораАренды"));
	Исключения.Добавить(Тип("ДокументСсылка.ЗаявкаНаКомандировку"));
	
КонецПроцедуры

#Область ВзаиморасчетыПоДоговору
	
Функция ТекстЗапроса_ВзаиморасчетыПоДоговору_ЗаявленоИзменение() Экспорт
	Возврат
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ВТ_РасшифровкаПлатежа.ОбъектРасчетов.Организация, ВТ_РасшифровкаПлатежа.Организация) КАК Организация,
	|	ЕСТЬNULL(ВТ_РасшифровкаПлатежа.ОбъектРасчетов.Контрагент, ВТ_РасшифровкаПлатежа.Контрагент) КАК Контрагент,
	|	ЕСТЬNULL(ВТ_РасшифровкаПлатежа.ОбъектРасчетов.Договор, ВТ_РасшифровкаПлатежа.Договор) КАК Договор,
	|	ВТ_РасшифровкаПлатежа.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ЕСТЬNULL(ВТ_РасшифровкаПлатежа.ОбъектРасчетов.ВалютаВзаиморасчетов, ВТ_РасшифровкаПлатежа.ВалютаВзаиморасчетов) КАК Валюта,
	|	ВТ_РасшифровкаПлатежа.СуммаВзаиморасчетов КАК ЗаявленоИзменение
	|ПОМЕСТИТЬ ВТ_ЗаявленоИзменение
	|ИЗ
	|	ВТ_РасшифровкаПлатежа КАК ВТ_РасшифровкаПлатежа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АналитикаУчетаПоПартнерам.КлючАналитики КАК КлючАналитики
	|ПОМЕСТИТЬ ВТ_АналитикиУчетаПоПартнерам
	|ИЗ
	|	РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|ГДЕ
	|	АналитикаУчетаПоПартнерам.Договор В
	|			(ВЫБРАТЬ
	|				ВТ_ЗаявленоИзменение.Договор КАК Договор
	|			ИЗ
	|				ВТ_ЗаявленоИзменение КАК ВТ_ЗаявленоИзменение)";
	
КонецФункции

Функция ТекстЗапроса_ВзаиморасчетыПоДоговору_Факт() Экспорт
	Возврат
	"ВЫБРАТЬ
	|	РасчетыСПоставщикамиОстатки.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	РасчетыСПоставщикамиОстатки.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|	РасчетыСПоставщикамиОстатки.АналитикаУчетаПоПартнерам.Договор КАК Договор,
	|	РасчетыСПоставщикамиОстатки.Валюта КАК Валюта,
	|	РасчетыСПоставщикамиОстатки.СуммаОстаток КАК Исполнено
	|ПОМЕСТИТЬ ВТ_Факт
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.Остатки(
	|			,
	|			АналитикаУчетаПоПартнерам.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТ_АналитикиУчетаПоПартнерам.КлючАналитики КАК КлючАналитики
	|				ИЗ
	|					ВТ_АналитикиУчетаПоПартнерам КАК ВТ_АналитикиУчетаПоПартнерам)) КАК РасчетыСПоставщикамиОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасчетыСКлиентамиОстатки.АналитикаУчетаПоПартнерам.Организация,
	|	РасчетыСКлиентамиОстатки.АналитикаУчетаПоПартнерам.Контрагент,
	|	РасчетыСКлиентамиОстатки.АналитикаУчетаПоПартнерам.Договор,
	|	РасчетыСКлиентамиОстатки.Валюта,
	|	РасчетыСКлиентамиОстатки.СуммаОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.Остатки(
	|			,
	|			АналитикаУчетаПоПартнерам.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТ_АналитикиУчетаПоПартнерам.КлючАналитики КАК КлючАналитики
	|				ИЗ
	|					ВТ_АналитикиУчетаПоПартнерам КАК ВТ_АналитикиУчетаПоПартнерам)) КАК РасчетыСКлиентамиОстатки";
	
КонецФункции

Функция ТекстЗапроса_ВзаиморасчетыПоДоговору_Заявлено() Экспорт
	Возврат
	"ВЫБРАТЬ
	|	РасчетыСПоставщикамиОстатки.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	РасчетыСПоставщикамиОстатки.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|	РасчетыСПоставщикамиОстатки.АналитикаУчетаПоПартнерам.Договор КАК Договор,
	|	РасчетыСПоставщикамиОстатки.Валюта КАК Валюта,
	|	-РасчетыСПоставщикамиОстатки.ОплачиваетсяОстаток КАК Заявлено
	|ПОМЕСТИТЬ ВТ_Заявлено
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.Остатки(
	|			,
	|			АналитикаУчетаПоПартнерам.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТ_АналитикиУчетаПоПартнерам.КлючАналитики КАК КлючАналитики
	|				ИЗ
	|					ВТ_АналитикиУчетаПоПартнерам КАК ВТ_АналитикиУчетаПоПартнерам)) КАК РасчетыСПоставщикамиОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасчетыСКлиентамиОстатки.АналитикаУчетаПоПартнерам.Организация,
	|	РасчетыСКлиентамиОстатки.АналитикаУчетаПоПартнерам.Контрагент,
	|	РасчетыСКлиентамиОстатки.АналитикаУчетаПоПартнерам.Договор,
	|	РасчетыСКлиентамиОстатки.Валюта,
	|	-РасчетыСКлиентамиОстатки.ОплачиваетсяОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.Остатки(
	|			,
	|			АналитикаУчетаПоПартнерам.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТ_АналитикиУчетаПоПартнерам.КлючАналитики КАК КлючАналитики
	|				ИЗ
	|					ВТ_АналитикиУчетаПоПартнерам КАК ВТ_АналитикиУчетаПоПартнерам)) КАК РасчетыСКлиентамиОстатки";
	
КонецФункции

#КонецОбласти 
 
#Область ВзаиморасчетыПоКонтрагенту

Функция ТекстЗапроса_ВзаиморасчетыПоКонтрагенту_Факт() Экспорт
	Возврат
	"ВЫБРАТЬ
	|	&Контрагент КАК Контрагент,
	|	РасчетыСПоставщикамиОстатки.Валюта КАК Валюта,
	|	РасчетыСПоставщикамиОстатки.СуммаОстаток КАК Исполнено
	|ПОМЕСТИТЬ ВТ_Факт
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.Остатки(
	|			,
	|			АналитикаУчетаПоПартнерам В
	|				(ВЫБРАТЬ
	|					АналитикаУчетаПоПартнерам.КлючАналитики КАК КлючАналитики
	|				ИЗ
	|					РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|				ГДЕ
	|					АналитикаУчетаПоПартнерам.Контрагент = &Контрагент)) КАК РасчетыСПоставщикамиОстатки
	|ГДЕ
	|	&РольКонтрагента = ЗНАЧЕНИЕ(Перечисление.РолиКонтрагентов.Поставщики)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Контрагент,
	|	РасчетыСКлиентамиОстатки.Валюта,
	|	РасчетыСКлиентамиОстатки.СуммаОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.Остатки(
	|			,
	|			АналитикаУчетаПоПартнерам В
	|				(ВЫБРАТЬ
	|					АналитикаУчетаПоПартнерам.КлючАналитики КАК КлючАналитики
	|				ИЗ
	|					РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|				ГДЕ
	|					АналитикаУчетаПоПартнерам.Контрагент = &Контрагент)) КАК РасчетыСКлиентамиОстатки
	|ГДЕ
	|	&РольКонтрагента = ЗНАЧЕНИЕ(Перечисление.РолиКонтрагентов.Покупатели)";
	
КонецФункции

Функция ТекстЗапроса_ВзаиморасчетыПоКонтрагенту_Заявлено() Экспорт
	Возврат
	"ВЫБРАТЬ
	|	&Контрагент КАК Контрагент,
	|	РасчетыСПоставщикамиОстатки.Валюта КАК Валюта,
	|	-РасчетыСПоставщикамиОстатки.ОплачиваетсяОстаток КАК Заявлено
	|ПОМЕСТИТЬ ВТ_Заявлено
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.Остатки(
	|			,
	|			АналитикаУчетаПоПартнерам.Ссылка В
	|				(ВЫБРАТЬ
	|					АналитикаУчетаПоПартнерам.КлючАналитики КАК КлючАналитики
	|				ИЗ
	|					РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|				ГДЕ
	|					АналитикаУчетаПоПартнерам.Контрагент = &Контрагент)) КАК РасчетыСПоставщикамиОстатки
	|ГДЕ
	|	&РольКонтрагента = ЗНАЧЕНИЕ(Перечисление.РолиКонтрагентов.Поставщики)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Контрагент,
	|	РасчетыСКлиентамиОстатки.Валюта,
	|	-РасчетыСКлиентамиОстатки.ОплачиваетсяОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.Остатки(
	|			,
	|			АналитикаУчетаПоПартнерам.Ссылка В
	|				(ВЫБРАТЬ
	|					АналитикаУчетаПоПартнерам.КлючАналитики КАК КлючАналитики
	|				ИЗ
	|					РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|				ГДЕ
	|					АналитикаУчетаПоПартнерам.Контрагент = &Контрагент)) КАК РасчетыСКлиентамиОстатки
	|ГДЕ
	|	&РольКонтрагента = ЗНАЧЕНИЕ(Перечисление.РолиКонтрагентов.Покупатели)";
	
КонецФункции

#КонецОбласти 

#Область РасходыПоДоговору
	
Функция ТекстЗапроса_Предел() Экспорт
	
	Возврат
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Организация КАК Организация,
	|	ДоговорыКонтрагентов.Контрагент КАК Контрагент,
	|	ДоговорыКонтрагентов.Ссылка КАК Договор,
	|	ДоговорыКонтрагентов.ВерсияСоглашения КАК ВерсияСоглашения,
	|	ДоговорыКонтрагентов.ВалютаВзаиморасчетов КАК Валюта,
	|	ВложенныйЗапрос.ЭтоОплата КАК ЭтоОплата,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.ВерсияСоглашения.Сумма, 0) КАК Лимит
	|ПОМЕСТИТЬ ВТ_Лимит
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов,
	|	(ВЫБРАТЬ
	|		ЛОЖЬ КАК ЭтоОплата
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ИСТИНА) КАК ВложенныйЗапрос
	|ГДЕ
	|	ДоговорыКонтрагентов.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ_ЗаявленоИзменение.Договор
	|			ИЗ
	|				ВТ_ЗаявленоИзменение КАК ВТ_ЗаявленоИзменение)";
	
КонецФункции

Функция ТекстЗапроса_РасходыПоДоговору_ЗаявленоИзменение() Экспорт
	Возврат
	"ВЫБРАТЬ
	|	ВТ_ДанныеДляПроверки.ЭтоОплата КАК ЭтоОплата,
	|	ЕСТЬNULL(ВТ_ДанныеДляПроверки.ОбъектРасчетов.Организация, ВТ_ДанныеДляПроверки.Организация) КАК Организация,
	|	ЕСТЬNULL(ВТ_ДанныеДляПроверки.ОбъектРасчетов.Контрагент, ВТ_ДанныеДляПроверки.Контрагент) КАК Контрагент,
	|	ЕСТЬNULL(ВТ_ДанныеДляПроверки.ОбъектРасчетов.Договор, ВТ_ДанныеДляПроверки.Договор) КАК Договор,
	|	ВТ_ДанныеДляПроверки.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ЕСТЬNULL(ВТ_ДанныеДляПроверки.ОбъектРасчетов.ВалютаВзаиморасчетов, ВТ_ДанныеДляПроверки.Валюта) КАК Валюта,
	|	ВТ_ДанныеДляПроверки.Сумма КАК ЗаявленоИзменение
	|ПОМЕСТИТЬ ВТ_ЗаявленоИзменение
	|ИЗ
	|	ВТ_ДанныеДляПроверки КАК ВТ_ДанныеДляПроверки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АналитикаУчетаПоПартнерам.КлючАналитики КАК КлючАналитики
	|ПОМЕСТИТЬ ВТ_АналитикиУчетаПоПартнерам
	|ИЗ
	|	РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|ГДЕ
	|	АналитикаУчетаПоПартнерам.Договор В
	|			(ВЫБРАТЬ
	|				ВТ_ЗаявленоИзменение.Договор КАК Договор
	|			ИЗ
	|				ВТ_ЗаявленоИзменение КАК ВТ_ЗаявленоИзменение)";
	
КонецФункции

Функция ТекстЗапроса_РасходыПоДоговору_Факт() Экспорт
	Возврат
	"ВЫБРАТЬ
	|	РасчетыСПоставщиками.Регистратор КАК Регистратор,
	|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСПоставщиками.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСПоставщиками.Валюта КАК Валюта,
	|	РасчетыСПоставщиками.РасчетныйДокумент КАК РасчетныйДокумент,
	|	РасчетыСПоставщиками.ВидДвижения КАК ВидДвижения,
	|	РасчетыСПоставщиками.Сумма КАК Сумма,
	|	РасчетыСПоставщиками.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.РасходныйКассовыйОрдер
	|				ИЛИ РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.ПриходныйКассовыйОрдер
	|				ИЛИ РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств
	|				ИЛИ РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.ПоступлениеБезналичныхДенежныхСредств
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоОплата
	|ПОМЕСТИТЬ ВТ_Данные
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
	|ГДЕ
	|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам В
	|			(ВЫБРАТЬ
	|				ВТ_АналитикиУчетаПоПартнерам.КлючАналитики КАК КлючАналитики
	|			ИЗ
	|				ВТ_АналитикиУчетаПоПартнерам КАК ВТ_АналитикиУчетаПоПартнерам)
	|	И РасчетыСПоставщиками.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетАвансаПоставщику)
	|	И РасчетыСПоставщиками.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеКредиторскойЗадолженности)
	|	И НЕ РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|	И РасчетыСПоставщиками.Сумма <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Запрос1.ЭтоОплата КАК ЭтоОплата,
	|	ВЫРАЗИТЬ(Запрос1.АналитикаУчетаПоПартнерам КАК Справочник.КлючиАналитикиУчетаПоПартнерам).Организация КАК Организация,
	|	ВЫРАЗИТЬ(Запрос1.АналитикаУчетаПоПартнерам КАК Справочник.КлючиАналитикиУчетаПоПартнерам).Контрагент КАК Контрагент,
	|	ВЫРАЗИТЬ(Запрос1.АналитикаУчетаПоПартнерам КАК Справочник.КлючиАналитикиУчетаПоПартнерам).Договор КАК Договор,
	|	Запрос1.Валюта КАК Валюта,
	|	СУММА(Запрос1.Исполнено) КАК Исполнено
	|ПОМЕСТИТЬ ВТ_Факт
	|ИЗ
	|	(ВЫБРАТЬ
	|		ИСТИНА КАК ЭтоОплата,
	|		ВТ_Данные.Регистратор КАК ДокументРегистратор,
	|		ВТ_Данные.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		ВТ_Данные.ОбъектРасчетов КАК ОбъектРасчетов,
	|		ВТ_Данные.Валюта КАК Валюта,
	|		ВТ_Данные.РасчетныйДокумент КАК РасчетныйДокумент,
	|		ВТ_Данные.ВидДвижения КАК ВидДвижения,
	|		ВЫБОР
	|			КОГДА ВТ_Данные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА 1
	|			ИНАЧЕ -1
	|		КОНЕЦ * ВТ_Данные.Сумма КАК Исполнено,
	|		ВТ_Данные.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|	ИЗ
	|		ВТ_Данные КАК ВТ_Данные
	|	ГДЕ
	|		ВТ_Данные.Сумма <> 0
	|		И ВТ_Данные.ЭтоОплата
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЛОЖЬ,
	|		ВТ_Данные.Регистратор,
	|		ВТ_Данные.АналитикаУчетаПоПартнерам,
	|		ВТ_Данные.ОбъектРасчетов,
	|		ВТ_Данные.Валюта,
	|		ВТ_Данные.РасчетныйДокумент,
	|		ВТ_Данные.ВидДвижения,
	|		ВЫБОР
	|			КОГДА ВТ_Данные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА 1
	|			ИНАЧЕ -1
	|		КОНЕЦ * ВТ_Данные.Сумма,
	|		ВТ_Данные.ХозяйственнаяОперация
	|	ИЗ
	|		ВТ_Данные КАК ВТ_Данные
	|	ГДЕ
	|		ВТ_Данные.Сумма <> 0
	|		И НЕ ВТ_Данные.ЭтоОплата
	|		И НЕ ВТ_Данные.Регистратор ССЫЛКА Документ.ВозвратТоваровПоставщику
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЛОЖЬ,
	|		ВТ_Данные.Регистратор,
	|		ВТ_Данные.АналитикаУчетаПоПартнерам,
	|		ВТ_Данные.ОбъектРасчетов,
	|		ВТ_Данные.Валюта,
	|		ВТ_Данные.РасчетныйДокумент,
	|		ВТ_Данные.ВидДвижения,
	|		-ВТ_Данные.Сумма,
	|		ВТ_Данные.ХозяйственнаяОперация
	|	ИЗ
	|		ВТ_Данные КАК ВТ_Данные
	|	ГДЕ
	|		ВТ_Данные.Регистратор ССЫЛКА Документ.ВозвратТоваровПоставщику
	|		И ВТ_Данные.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности)) КАК Запрос1
	|
	|СГРУППИРОВАТЬ ПО
	|	Запрос1.ЭтоОплата,
	|	Запрос1.АналитикаУчетаПоПартнерам,
	|	Запрос1.Валюта,
	|	ВЫРАЗИТЬ(Запрос1.АналитикаУчетаПоПартнерам КАК Справочник.КлючиАналитикиУчетаПоПартнерам).Договор,
	|	ВЫРАЗИТЬ(Запрос1.АналитикаУчетаПоПартнерам КАК Справочник.КлючиАналитикиУчетаПоПартнерам).Организация,
	|	ВЫРАЗИТЬ(Запрос1.АналитикаУчетаПоПартнерам КАК Справочник.КлючиАналитикиУчетаПоПартнерам).Контрагент
	|
	|ИМЕЮЩИЕ
	|	СУММА(Запрос1.Исполнено) <> 0";
	
КонецФункции

Функция ТекстЗапроса_РасходыПоДоговору_Заявлено() Экспорт
	Возврат
	"ВЫБРАТЬ
	|	ИСТИНА КАК ЭтоОплата,
	|	РасчетыСПоставщикамиОстатки.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	РасчетыСПоставщикамиОстатки.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|	РасчетыСПоставщикамиОстатки.АналитикаУчетаПоПартнерам.Договор КАК Договор,
	|	РасчетыСПоставщикамиОстатки.Валюта КАК Валюта,
	|	-РасчетыСПоставщикамиОстатки.ОплачиваетсяОстаток КАК Заявлено
	|ПОМЕСТИТЬ ВТ_Заявлено
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.Остатки(
	|			,
	|			АналитикаУчетаПоПартнерам.Ссылка В
	|				(ВЫБРАТЬ
	|					АналитикаУчетаПоПартнерам.КлючАналитики КАК КлючАналитики
	|				ИЗ
	|					РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|				ГДЕ
	|					АналитикаУчетаПоПартнерам.Договор В
	|						(ВЫБРАТЬ
	|							ВТ_ЗаявленоИзменение.Договор
	|						ИЗ
	|							ВТ_ЗаявленоИзменение КАК ВТ_ЗаявленоИзменение))) КАК РасчетыСПоставщикамиОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА,
	|	РасчетыСКлиентамиОстатки.АналитикаУчетаПоПартнерам.Организация,
	|	РасчетыСКлиентамиОстатки.АналитикаУчетаПоПартнерам.Контрагент,
	|	РасчетыСКлиентамиОстатки.АналитикаУчетаПоПартнерам.Договор,
	|	РасчетыСКлиентамиОстатки.Валюта,
	|	-РасчетыСКлиентамиОстатки.ОплачиваетсяОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.Остатки(
	|			,
	|			АналитикаУчетаПоПартнерам.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТ_АналитикиУчетаПоПартнерам.КлючАналитики КАК КлючАналитики
	|				ИЗ
	|					ВТ_АналитикиУчетаПоПартнерам КАК ВТ_АналитикиУчетаПоПартнерам)) КАК РасчетыСКлиентамиОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЛОЖЬ,
	|	РасчетыСПоставщикамиОстатки.АналитикаУчетаПоПартнерам.Организация,
	|	РасчетыСПоставщикамиОстатки.АналитикаУчетаПоПартнерам.Контрагент,
	|	РасчетыСПоставщикамиОстатки.АналитикаУчетаПоПартнерам.Договор,
	|	РасчетыСПоставщикамиОстатки.Валюта,
	|	РасчетыСПоставщикамиОстатки.КПоступлениюОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.Остатки(
	|			,
	|			АналитикаУчетаПоПартнерам.Ссылка В
	|				(ВЫБРАТЬ
	|					АналитикаУчетаПоПартнерам.КлючАналитики КАК КлючАналитики
	|				ИЗ
	|					РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|				ГДЕ
	|					АналитикаУчетаПоПартнерам.Договор В
	|						(ВЫБРАТЬ
	|							ВТ_ЗаявленоИзменение.Договор
	|						ИЗ
	|							ВТ_ЗаявленоИзменение КАК ВТ_ЗаявленоИзменение))) КАК РасчетыСПоставщикамиОстатки
	|ГДЕ
	|	ВЫРАЗИТЬ(ВЫРАЗИТЬ(РасчетыСПоставщикамиОстатки.ОбъектРасчетов.Договор КАК Справочник.ДоговорыКонтрагентов).ВерсияСоглашения КАК Документ.ВерсияСоглашенияКоммерческийДоговор).РежимИспользованияГрафика = ЗНАЧЕНИЕ(Перечисление.РежимыИспользованияГрафика.КонтрольЛимитовИСозданиеПозиций)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА,
	|	РасчетыСПоставщикамиОстатки.АналитикаУчетаПоПартнерам.Организация,
	|	РасчетыСПоставщикамиОстатки.АналитикаУчетаПоПартнерам.Контрагент,
	|	РасчетыСПоставщикамиОстатки.АналитикаУчетаПоПартнерам.Договор,
	|	РасчетыСПоставщикамиОстатки.Валюта,
	|	РасчетыСПоставщикамиОстатки.КОплатеОстаток
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.Остатки(
	|			,
	|			АналитикаУчетаПоПартнерам.Ссылка В
	|				(ВЫБРАТЬ
	|					АналитикаУчетаПоПартнерам.КлючАналитики КАК КлючАналитики
	|				ИЗ
	|					РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|				ГДЕ
	|					АналитикаУчетаПоПартнерам.Договор В
	|						(ВЫБРАТЬ
	|							ВТ_ЗаявленоИзменение.Договор
	|						ИЗ
	|							ВТ_ЗаявленоИзменение КАК ВТ_ЗаявленоИзменение))) КАК РасчетыСПоставщикамиОстатки
	|ГДЕ
	|	ВЫРАЗИТЬ(ВЫРАЗИТЬ(РасчетыСПоставщикамиОстатки.ОбъектРасчетов.Договор КАК Справочник.ДоговорыКонтрагентов).ВерсияСоглашения КАК Документ.ВерсияСоглашенияКоммерческийДоговор).РежимИспользованияГрафика = ЗНАЧЕНИЕ(Перечисление.РежимыИспользованияГрафика.КонтрольЛимитовИСозданиеПозиций)";
	
КонецФункции

#КонецОбласти 

#КонецОбласти

