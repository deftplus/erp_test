
#Область ПрограммныйИнтерфейс

// Функция возвращает период по дате и периодичности
//
// Параметры:
//  Периодичность  - ПеречислениеСсылка.Периодичность - Требуемая периодичность
//  ДатаПериода  - Дата - Дата
//
// Возвращаемое значение:
//   СправочникСсылка.Периоды, неопределено   - Найденный по параметрам элемент справочника Периоды. Если не найдено, то неопределено
//
Функция ПолучитьПериодПоДате(Периодичность, ДатаПериода) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Периодичность", Периодичность);
	Запрос.УстановитьПараметр("ДатаПериода", НачалоДня(ДатаПериода));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Периоды.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Периоды КАК Периоды
	|ГДЕ
	|	Периоды.Периодичность = &Периодичность
	|	И Периоды.Произвольный = ЛОЖЬ
	|	И Периоды.ПометкаУдаления = ЛОЖЬ
	|	И Периоды.ДатаНачала <= &ДатаПериода
	|	И Периоды.ДатаОкончания >= &ДатаПериода";
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат неопределено;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.Ссылка;
	
КонецФункции

// Функция возвращает таблицу движений по регистру накопления ЛимитыПоБюджетам для заявки/версии соглашения
Функция ПодготовитьТаблицуЛимитовПоЗаявкеВерсииСоглашения(Запрос, ДатаСовершенияОперации) экспорт
	
	КонтрольЛимитовУХ.ПолучитьТаблицуЛимитов(Запрос, ДатаСовершенияОперации, Истина);
	
	// Итоговый запрос складывается из:
	// 1. Рассчитанные лимиты
	// 2. сторнирование суммы Заявлено добавленных корректировками
	
	Запрос.УстановитьПараметр("ДатаСовершенияОперации", ДатаСовершенияОперации);
	
	Запрос.Текст  = 
	"ВЫБРАТЬ
	|	ВТ_ДвиженияЛимитыПоБюджетам.Период КАК Период,
	|	ВТ_ДвиженияЛимитыПоБюджетам.Предназначение КАК Предназначение,
	|	ВТ_ДвиженияЛимитыПоБюджетам.ДокументРезервирования КАК ДокументРезервирования,
	|	ВТ_ДвиженияЛимитыПоБюджетам.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ВТ_ДвиженияЛимитыПоБюджетам.ЦФО КАК ЦФО,
	|	ВТ_ДвиженияЛимитыПоБюджетам.Проект КАК Проект,
	|	ВТ_ДвиженияЛимитыПоБюджетам.СтатьяБюджета КАК СтатьяБюджета,
	|	ВТ_ДвиженияЛимитыПоБюджетам.Аналитика1 КАК Аналитика1,
	|	ВТ_ДвиженияЛимитыПоБюджетам.Аналитика2 КАК Аналитика2,
	|	ВТ_ДвиженияЛимитыПоБюджетам.Аналитика3 КАК Аналитика3,
	|	ВТ_ДвиженияЛимитыПоБюджетам.Аналитика4 КАК Аналитика4,
	|	ВТ_ДвиженияЛимитыПоБюджетам.Аналитика5 КАК Аналитика5,
	|	ВТ_ДвиженияЛимитыПоБюджетам.Аналитика6 КАК Аналитика6,
	|	ВТ_ДвиженияЛимитыПоБюджетам.Валюта КАК Валюта,
	|	ВТ_ДвиженияЛимитыПоБюджетам.ДокументПланирования КАК ДокументПланирования,
	|	ВТ_ДвиженияЛимитыПоБюджетам.Лимит КАК Лимит,
	|	0 КАК Корректировка,
	|	ВТ_ДвиженияЛимитыПоБюджетам.Зарезервировано КАК Зарезервировано,
	|	ВТ_ДвиженияЛимитыПоБюджетам.Заявлено КАК Заявлено,
	|	ВТ_ДвиженияЛимитыПоБюджетам.Исполнено КАК Исполнено,
	|	ВТ_ДвиженияЛимитыПоБюджетам.КОбеспечению КАК КОбеспечению,
	|	ВТ_ДвиженияЛимитыПоБюджетам.ДефицитРезерва КАК ДефицитРезерва
	|ИЗ
	|	ВТ_ДвиженияЛимитыПоБюджетам КАК ВТ_ДвиженияЛимитыПоБюджетам
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&ДатаСовершенияОперации,
	|	ЛимитыПоБюджетам.Предназначение,
	|	ЛимитыПоБюджетам.ДокументРезервирования,
	|	ЛимитыПоБюджетам.ПериодЛимитирования,
	|	ЛимитыПоБюджетам.ЦФО,
	|	ЛимитыПоБюджетам.Проект,
	|	ЛимитыПоБюджетам.СтатьяБюджета,
	|	ЛимитыПоБюджетам.Аналитика1,
	|	ЛимитыПоБюджетам.Аналитика2,
	|	ЛимитыПоБюджетам.Аналитика3,
	|	ЛимитыПоБюджетам.Аналитика4,
	|	ЛимитыПоБюджетам.Аналитика5,
	|	ЛимитыПоБюджетам.Аналитика6,
	|	ЛимитыПоБюджетам.Валюта,
	|	ЛимитыПоБюджетам.ДокументПланирования,
	|	0,
	|	0,
	|	-ЛимитыПоБюджетам.Зарезервировано,
	|	-ЛимитыПоБюджетам.Заявлено,
	|	0,
	|	0,
	|	0
	|ИЗ
	|	РегистрНакопления.ЛимитыПоБюджетам КАК ЛимитыПоБюджетам
	|ГДЕ
	|	(ЛимитыПоБюджетам.Предназначение, ЛимитыПоБюджетам.ДокументРезервирования, ЛимитыПоБюджетам.ДокументПланирования, ЛимитыПоБюджетам.ПериодЛимитирования, ЛимитыПоБюджетам.Валюта, ЛимитыПоБюджетам.ЦФО, ЛимитыПоБюджетам.Проект, ЛимитыПоБюджетам.СтатьяБюджета, ЛимитыПоБюджетам.Аналитика1, ЛимитыПоБюджетам.Аналитика2, ЛимитыПоБюджетам.Аналитика3, ЛимитыПоБюджетам.Аналитика4, ЛимитыПоБюджетам.Аналитика5, ЛимитыПоБюджетам.Аналитика6) В 
	|				(ВЫБРАТЬ
	|					ВТ_ДвиженияЛимитыПоБюджетам.Предназначение КАК Предназначение,
	|					ВТ_ДвиженияЛимитыПоБюджетам.ДокументРезервирования КАК ДокументРезервирования,
	|					ВТ_ДвиженияЛимитыПоБюджетам.ДокументПланирования КАК ДокументПланирования,
	|					ВТ_ДвиженияЛимитыПоБюджетам.ПериодЛимитирования КАК ПериодЛимитирования,
	|					ВТ_ДвиженияЛимитыПоБюджетам.Валюта КАК Валюта,
	|					ВТ_ДвиженияЛимитыПоБюджетам.ЦФО КАК ЦФО,
	|					ВТ_ДвиженияЛимитыПоБюджетам.Проект КАК Проект,
	|					ВТ_ДвиженияЛимитыПоБюджетам.СтатьяБюджета КАК СтатьяБюджета,
	|					ВТ_ДвиженияЛимитыПоБюджетам.Аналитика1 КАК Аналитика1,
	|					ВТ_ДвиженияЛимитыПоБюджетам.Аналитика2 КАК Аналитика2,
	|					ВТ_ДвиженияЛимитыПоБюджетам.Аналитика3 КАК Аналитика3,
	|					ВТ_ДвиженияЛимитыПоБюджетам.Аналитика4 КАК Аналитика4,
	|					ВТ_ДвиженияЛимитыПоБюджетам.Аналитика5 КАК Аналитика5,
	|					ВТ_ДвиженияЛимитыПоБюджетам.Аналитика6 КАК Аналитика6
	|				ИЗ
	|					ВТ_ДвиженияЛимитыПоБюджетам КАК ВТ_ДвиженияЛимитыПоБюджетам
	|				ГДЕ
	|					ВТ_ДвиженияЛимитыПоБюджетам.ДокументПланирования <> НЕОПРЕДЕЛЕНО)
	|	И (ЛимитыПоБюджетам.Регистратор ССЫЛКА Документ.ЗаявкаНаКорректировкуЛимитов
	|				ИЛИ ЛимитыПоБюджетам.Регистратор ССЫЛКА Документ.КорректировкаЛимитов)
	|	И ЛимитыПоБюджетам.Заявлено <> 0";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Возвращает таблицу лимитов для записи в регистр ЛимитыПоБюджетам.
//
// Параметры:
//   Запрос  	 - <Запрос> - Запрос, в котором сформированы следующие временные таблицы: ВТ_ТаблицаПлановССуммамиЛимитирования
//                 <продолжение описания параметра>
//   ДатаСовершенияОперации  - <Дата> - Дата получения совершения операции.
//                 Используется для получения курсов валют.
//   ПоместитьРезультатВоВременнуюТаблицу - Булево - Если Истина, то результат во временной таблице запросе, иначе возвращает ТаблицуЗначений
//
// Возвращаемое значение:
//   ТаблицаЗначений - ТаблицаЗначений  - Таблица движений по регистру ЛимитыПоБюджетам
//
Функция ПолучитьТаблицуЛимитов(Запрос, ДатаСовершенияОперации = неопределено, ПоместитьРезультатВоВременнуюТаблицу=Ложь) экспорт
	
	Если Запрос.МенеджерВременныхТаблиц = неопределено 
		ИЛИ Запрос.МенеджерВременныхТаблиц.Таблицы.Найти("ВТ_ТаблицаПлановССуммамиЛимитирования") = неопределено Тогда
		Возврат Новый ТаблицаЗначений;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	//
	КолонкиВТ = Запрос.МенеджерВременныхТаблиц.Таблицы.Найти("ВТ_ТаблицаПлановССуммамиЛимитирования").Колонки;
	ДатаОперацииВТаблице = КолонкиВТ.Найти("ДатаОперации") <> неопределено;
	ПериодВТаблице = КолонкиВТ.Найти("Период") <> неопределено;
	ПериодОтчетаВТаблице = КолонкиВТ.Найти("ПериодОтчета") <> неопределено;
	УказанаДатаСовершенияОперации = ДатаСовершенияОперации <> неопределено;
	
	ТипДокумента = ТипЗнч(Запрос.Параметры.Ссылка);
	ЭтоОперПлан = ТипДокумента = Тип("ДокументСсылка.ОперативныйПлан");
	
	ТекстыЗапросов = Новый Массив;
	
	// Сформировать ВТ_ПланыССуммамиЛимитирования
	ТребуемыеКолонки = Новый Структура;
	Если ДатаОперацииВТаблице Тогда // ОФД
		// Колонка ДатаОперации в ВТ_ТаблицаПлановССуммамиЛимитирования не должна иметь часов, минут и секунд
		ТребуемыеКолонки.Вставить("ДатаОперации",	"НАЧАЛОПЕРИОДА(ДатаОперации, ДЕНЬ)");
	ИначеЕсли ПериодОтчетаВТаблице И ЭтоОперПлан Тогда // ОперПлан
		ТребуемыеКолонки.Вставить("ДатаОперации",	"НАЧАЛОПЕРИОДА(ПериодОтчета.ДатаНачала, ДЕНЬ)");
	ИначеЕсли ПериодВТаблице Тогда // Все документы исполнения
		ТребуемыеКолонки.Вставить("ДатаОперации",	"НАЧАЛОПЕРИОДА(Период, ДЕНЬ)");
	ИначеЕсли УказанаДатаСовершенияОперации Тогда
		ТребуемыеКолонки.Вставить("ДатаОперации",	"НАЧАЛОПЕРИОДА(&ДатаСовершенияОперации, ДЕНЬ)");
		Запрос.УстановитьПараметр("ДатаСовершенияОперации", ДатаСовершенияОперации);
	КонецЕсли;
	ТребуемыеКолонки.Вставить("ПриходРасход",	"ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.ПустаяСсылка)");
	ТребуемыеКолонки.Вставить("Валюта_Доп",		"ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)");
	ТребуемыеКолонки.Вставить("Исполнено_Доп",	"0");
	
	ПоляВыбора = Новый Массив;
	Для Каждого КлючЗначение Из ТребуемыеКолонки Цикл
		Если КолонкиВТ.Найти(КлючЗначение.Ключ) = неопределено Тогда
			ПоляВыбора.Добавить(СтрШаблон("%1 КАК %2", КлючЗначение.Значение, КлючЗначение.Ключ));
		КонецЕсли;
	КонецЦикла;
	ПоляВыбора.Добавить("*");
	
	ТекстЗапросаПоля = СтрСоединить(ПоляВыбора, ",
		|	");
	
	ТекстыЗапросов.Добавить(СтрШаблон(
		"ВЫБРАТЬ
		|	%1
		|ПОМЕСТИТЬ ВТ_ПланыССуммамиЛимитирования
		|ИЗ
		|	ВТ_ТаблицаПлановССуммамиЛимитирования", ТекстЗапросаПоля));
	
	ТекстыЗапросов.Добавить(
	"ВЫБРАТЬ
	|	Запрос.ДатаОперации КАК ДатаОперации,
	|	Запрос.Валюта КАК Валюта,
	|	Запрос.ДатаКурса КАК ДатаКурса,
	|	КурсыВалют.Курс КАК Курс,
	|	КурсыВалют.Кратность КАК Кратность
	|ПОМЕСТИТЬ ВТ_КурсыВалют
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ_Планы.ДатаОперации КАК ДатаОперации,
	|		ВТ_ДатыКурсовВалют.Валюта КАК Валюта,
	|		МАКСИМУМ(ВТ_ДатыКурсовВалют.Период) КАК ДатаКурса
	|	ИЗ
	|		ВТ_ПланыССуммамиЛимитирования КАК ВТ_Планы,
	|		РегистрСведений.КурсыВалют КАК ВТ_ДатыКурсовВалют
	|	ГДЕ
	|		ВТ_ДатыКурсовВалют.Период <= ВТ_Планы.ДатаОперации
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВТ_Планы.ДатаОперации,
	|		ВТ_ДатыКурсовВалют.Валюта) КАК Запрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|		ПО Запрос.Валюта = КурсыВалют.Валюта
	|			И Запрос.ДатаКурса = КурсыВалют.Период");
	
	ТекстыЗапросов.Добавить(
	"ВЫБРАТЬ
	|	ВТ_КурсыВалютПланирования.ДатаОперации КАК ДатаОперации,
	|	ВТ_КурсыВалютПланирования.Валюта КАК ВалютаПланирования,
	|	ВТ_КурсыВалютЛимитирования.Валюта КАК ВалютаЛимитирования,
	|	ВТ_КурсыВалютПланирования.Курс КАК КурсПланирования,
	|	ВТ_КурсыВалютПланирования.Кратность КАК КратностьПланирования,
	|	ВТ_КурсыВалютЛимитирования.Курс КАК КурсЛимитирования,
	|	ВТ_КурсыВалютЛимитирования.Кратность КАК КратностьЛимитирования,
	|	ВЫРАЗИТЬ(ВТ_КурсыВалютПланирования.Курс / ВТ_КурсыВалютПланирования.Кратность / ВТ_КурсыВалютЛимитирования.Курс * ВТ_КурсыВалютЛимитирования.Кратность КАК ЧИСЛО(24, 18)) КАК КроссКурс
	|ПОМЕСТИТЬ ВТ_КроссКурсы
	|ИЗ
	|	ВТ_КурсыВалют КАК ВТ_КурсыВалютПланирования
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КурсыВалют КАК ВТ_КурсыВалютЛимитирования
	|		ПО ВТ_КурсыВалютПланирования.ДатаОперации = ВТ_КурсыВалютЛимитирования.ДатаОперации");
	
	ТекстыЗапросов.Добавить("УНИЧТОЖИТЬ ВТ_КурсыВалют");
	
	Запрос.Текст = СтрСоединить(ТекстыЗапросов, ОбщегоНазначенияОПК.ТекстРазделителяЗапросовПакета());
	Запрос.Выполнить();
	
	//
	Запрос.УстановитьПараметр("РегистрироватьДефицитРезерва", КонтрольЛимитовУХПовтИсп.РегистрироватьДефицитРезерва());
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПараметрыОперативногоПланирования.Период КАК Период,
	|	ПараметрыОперативногоПланирования.ВидБюджета.Предназначение КАК Предназначение,
	|	ПараметрыОперативногоПланирования.ПериодичностьЛимитирования КАК ПериодичностьЛимитирования,
	|	ПараметрыОперативногоПланирования.ИспользоватьЛимитирование КАК ИспользоватьЛимитирование,
	|	ПараметрыОперативногоПланирования.КоличествоПериодовЛимитирования КАК КоличествоПериодовЛимитирования,
	|	ПараметрыОперативногоПланирования.СпособОпределенияВалютыЛимитирования КАК СпособОпределенияВалютыЛимитирования
	|ПОМЕСТИТЬ ВТ_ПараметрыЛимитированияРегистр
	|ИЗ
	|	РегистрСведений.ПараметрыЛимитирования КАК ПараметрыОперативногоПланирования
	|ГДЕ
	|	ПараметрыОперативногоПланирования.ВидБюджета.Предназначение В
	|			(ВЫБРАТЬ
	|				Данные.Предназначение
	|			ИЗ
	|				ВТ_ПланыССуммамиЛимитирования КАК Данные)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПараметрыЛимитирования.Предназначение КАК Предназначение,
	|	ВТ_ПараметрыЛимитирования.Период КАК НачалоДействия,
	|	МИНИМУМ(ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ВТ_ПараметрыЛимитирования1.Период, ДЕНЬ, -1), ДАТАВРЕМЯ(2100, 1, 1))) КАК ОкончаниеДействия
	|ПОМЕСТИТЬ ВТ_ДатыОкончанияДействияНастроекЛимитирования
	|ИЗ
	|	ВТ_ПараметрыЛимитированияРегистр КАК ВТ_ПараметрыЛимитирования
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПараметрыЛимитированияРегистр КАК ВТ_ПараметрыЛимитирования1
	|		ПО ВТ_ПараметрыЛимитирования.Предназначение = ВТ_ПараметрыЛимитирования1.Предназначение
	|			И ВТ_ПараметрыЛимитирования.Период < ВТ_ПараметрыЛимитирования1.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПараметрыЛимитирования.Предназначение,
	|	ВТ_ПараметрыЛимитирования.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДатыОкончанияДействияНастроекЛимитирования.Предназначение КАК Предназначение,
	|	ВТ_ДатыОкончанияДействияНастроекЛимитирования.НачалоДействия КАК НачалоДействия,
	|	ВТ_ДатыОкончанияДействияНастроекЛимитирования.ОкончаниеДействия КАК ОкончаниеДействия,
	|	ВТ_ПараметрыЛимитирования.ИспользоватьЛимитирование КАК ИспользоватьЛимитирование,
	|	ВТ_ПараметрыЛимитирования.ПериодичностьЛимитирования КАК ПериодичностьЛимитирования,
	|	ВТ_ПараметрыЛимитирования.СпособОпределенияВалютыЛимитирования КАК СпособОпределенияВалютыЛимитирования,
	|	ВТ_ПараметрыЛимитирования.КоличествоПериодовЛимитирования КАК КоличествоПериодовЛимитирования
	|ПОМЕСТИТЬ ВТ_ПараметрыЛимитирования
	|ИЗ
	|	ВТ_ДатыОкончанияДействияНастроекЛимитирования КАК ВТ_ДатыОкончанияДействияНастроекЛимитирования
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПараметрыЛимитированияРегистр КАК ВТ_ПараметрыЛимитирования
	|		ПО ВТ_ДатыОкончанияДействияНастроекЛимитирования.Предназначение = ВТ_ПараметрыЛимитирования.Предназначение
	|			И ВТ_ДатыОкончанияДействияНастроекЛимитирования.НачалоДействия = ВТ_ПараметрыЛимитирования.Период
	|ГДЕ
	|	ВТ_ПараметрыЛимитирования.ИспользоватьЛимитирование = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПараметрыЛимитирования.Предназначение КАК Предназначение,
	|	ВТ_ПараметрыЛимитирования.НачалоДействия КАК НачалоДействия,
	|	ВТ_ПараметрыЛимитирования.ОкончаниеДействия КАК ОкончаниеДействия,
	|	ВТ_ПараметрыЛимитирования.ПериодичностьЛимитирования КАК ПериодичностьЛимитирования,
	|	ВТ_ПараметрыЛимитирования.ИспользоватьЛимитирование КАК ИспользоватьЛимитирование,
	|	ВТ_ПараметрыЛимитирования.КоличествоПериодовЛимитирования КАК КоличествоПериодовЛимитирования,
	|	Периоды.Ссылка КАК ПериодЛимитирования,
	|	Периоды.ДатаНачала КАК НачалоПериода,
	|	Периоды.ДатаОкончания КАК ОкончаниеПериода
	|ПОМЕСТИТЬ ВТ_ПериодыЛимитирования
	|ИЗ
	|	ВТ_ПараметрыЛимитирования КАК ВТ_ПараметрыЛимитирования
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Периоды КАК Периоды
	|		ПО ВТ_ПараметрыЛимитирования.ПериодичностьЛимитирования = Периоды.Периодичность
	|			И (Периоды.Произвольный = ЛОЖЬ)
	|ГДЕ
	|	Периоды.ДатаНачала МЕЖДУ ВТ_ПараметрыЛимитирования.НачалоДействия И ВТ_ПараметрыЛимитирования.ОкончаниеДействия
	|	И ВТ_ПараметрыЛимитирования.ИспользоватьЛимитирование = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.Период КАК ДействуетС,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.ВидБюджета КАК ВидБюджета,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.ВидБюджета.Предназначение КАК Предназначение,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.СтатьяБюджета КАК СтатьяБюджета,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.КонтролироватьСтатью КАК КонтролироватьСтатью,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.КонтролироватьАналитику1 КАК КонтролироватьАналитику1,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.КонтролироватьАналитику2 КАК КонтролироватьАналитику2,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.КонтролироватьАналитику3 КАК КонтролироватьАналитику3,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.КонтролироватьАналитику4 КАК КонтролироватьАналитику4,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.КонтролироватьАналитику5 КАК КонтролироватьАналитику5,
	|	ПараметрыКонтроляЛимитаСтатейБюджетов.КонтролироватьАналитику6 КАК КонтролироватьАналитику6
	|ПОМЕСТИТЬ ВТ_ПараметрыКонтроляСтатей
	|ИЗ
	|	РегистрСведений.ПараметрыКонтроляЛимитаСтатейБюджетов КАК ПараметрыКонтроляЛимитаСтатейБюджетов
	|ГДЕ
	|	(ПараметрыКонтроляЛимитаСтатейБюджетов.ВидБюджета.Предназначение, ПараметрыКонтроляЛимитаСтатейБюджетов.СтатьяБюджета) В
	|			(ВЫБРАТЬ
	|				Данные.Предназначение,
	|				Данные.СтатьяБюджета
	|			ИЗ
	|				ВТ_ПланыССуммамиЛимитирования КАК Данные)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПериодыЛимитирования.Предназначение КАК Предназначение,
	|	ВТ_ПериодыЛимитирования.НачалоПериода КАК НачалоПериода,
	|	ВТ_ПараметрыКонтроляСтатей.СтатьяБюджета КАК СтатьяБюджета,
	|	МАКСИМУМ(ВТ_ПараметрыКонтроляСтатей.ДействуетС) КАК ДействуетС
	|ПОМЕСТИТЬ ВТ_СтатьиПоПериодамНастроек
	|ИЗ
	|	ВТ_ПериодыЛимитирования КАК ВТ_ПериодыЛимитирования
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПараметрыКонтроляСтатей КАК ВТ_ПараметрыКонтроляСтатей
	|		ПО ВТ_ПериодыЛимитирования.Предназначение = ВТ_ПараметрыКонтроляСтатей.Предназначение
	|			И ВТ_ПериодыЛимитирования.НачалоПериода >= ВТ_ПараметрыКонтроляСтатей.ДействуетС
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПериодыЛимитирования.Предназначение,
	|	ВТ_ПериодыЛимитирования.НачалоПериода,
	|	ВТ_ПараметрыКонтроляСтатей.СтатьяБюджета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПериодыЛимитирования.Предназначение КАК Предназначение,
	|	ВТ_ПериодыЛимитирования.НачалоДействия КАК НачалоДействия,
	|	ВТ_ПериодыЛимитирования.ОкончаниеДействия КАК ОкончаниеДействия,
	|	ВТ_ПериодыЛимитирования.ПериодичностьЛимитирования КАК ПериодичностьЛимитирования,
	|	ВТ_ПериодыЛимитирования.ИспользоватьЛимитирование КАК ИспользоватьЛимитирование,
	|	ВТ_ПериодыЛимитирования.КоличествоПериодовЛимитирования КАК КоличествоПериодовЛимитирования,
	|	ВТ_ПериодыЛимитирования.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ВТ_ПериодыЛимитирования.НачалоПериода КАК НачалоПериода,
	|	ВТ_ПериодыЛимитирования.ОкончаниеПериода КАК ОкончаниеПериода,
	|	ВТ_СтатьиПоПериодамНастроек.СтатьяБюджета КАК СтатьяБюджета,
	|	ВТ_СтатьиПоПериодамНастроек.ДействуетС КАК ДействуетС,
	|	ВТ_ПараметрыКонтроляСтатей.КонтролироватьСтатью КАК КонтролироватьСтатью,
	|	ВТ_ПараметрыКонтроляСтатей.КонтролироватьАналитику1 КАК КонтролироватьАналитику1,
	|	ВТ_ПараметрыКонтроляСтатей.КонтролироватьАналитику2 КАК КонтролироватьАналитику2,
	|	ВТ_ПараметрыКонтроляСтатей.КонтролироватьАналитику3 КАК КонтролироватьАналитику3,
	|	ВТ_ПараметрыКонтроляСтатей.КонтролироватьАналитику4 КАК КонтролироватьАналитику4,
	|	ВТ_ПараметрыКонтроляСтатей.КонтролироватьАналитику5 КАК КонтролироватьАналитику5,
	|	ВТ_ПараметрыКонтроляСтатей.КонтролироватьАналитику6 КАК КонтролироватьАналитику6
	|ПОМЕСТИТЬ ВТ_ПараметрыЛимитированияСтатейБюджетов
	|ИЗ
	|	ВТ_ПериодыЛимитирования КАК ВТ_ПериодыЛимитирования
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтатьиПоПериодамНастроек КАК ВТ_СтатьиПоПериодамНастроек
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПараметрыКонтроляСтатей КАК ВТ_ПараметрыКонтроляСтатей
	|			ПО ВТ_СтатьиПоПериодамНастроек.Предназначение = ВТ_ПараметрыКонтроляСтатей.Предназначение
	|				И ВТ_СтатьиПоПериодамНастроек.СтатьяБюджета = ВТ_ПараметрыКонтроляСтатей.СтатьяБюджета
	|				И ВТ_СтатьиПоПериодамНастроек.ДействуетС = ВТ_ПараметрыКонтроляСтатей.ДействуетС
	|		ПО ВТ_ПериодыЛимитирования.Предназначение = ВТ_СтатьиПоПериодамНастроек.Предназначение
	|			И ВТ_ПериодыЛимитирования.НачалоПериода = ВТ_СтатьиПоПериодамНастроек.НачалоПериода
	|ГДЕ
	|	ВТ_ПериодыЛимитирования.ИспользоватьЛимитирование = ИСТИНА
	|	И ВТ_ПараметрыКонтроляСтатей.КонтролироватьСтатью = ИСТИНА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Предназначение,
	|	СтатьяБюджета,
	|	НачалоПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ПланыССуммамиЛимитирования.Предназначение КАК Предназначение,
	|	ВТ_ПланыССуммамиЛимитирования.ЦФО КАК ЦФО,
	|	ВТ_ПланыССуммамиЛимитирования.ДатаОперации КАК ДатаОперации
	|ПОМЕСТИТЬ ВТ_ЦФОВсе
	|ИЗ
	|	ВТ_ПланыССуммамиЛимитирования КАК ВТ_ПланыССуммамиЛимитирования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ПараметрыЛимитирования.Предназначение КАК Предназначение,
	|	ВТ_ПараметрыЛимитирования.НачалоДействия КАК НачалоДействия,
	|	ВТ_ПараметрыЛимитирования.ОкончаниеДействия КАК ОкончаниеДействия,
	|	ВТ_ПараметрыЛимитирования.СпособОпределенияВалютыЛимитирования КАК СпособОпределенияВалютыЛимитирования,
	|	ВТ_ЦФОВсе.ЦФО КАК ЦФО,
	|	ВЫБОР
	|		КОГДА ВТ_ПараметрыЛимитирования.СпособОпределенияВалютыЛимитирования = ЗНАЧЕНИЕ(Перечисление.СпособыОпределенияВалютыЛимитирования.ВалютаЛимитированияЦФО)
	|			ТОГДА ЕСТЬNULL(ПараметрыЛимитированияЦФО.ВалютаЛимитирования, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка))
	|		КОГДА ВТ_ПараметрыЛимитирования.СпособОпределенияВалютыЛимитирования = ЗНАЧЕНИЕ(Перечисление.СпособыОпределенияВалютыЛимитирования.ВалютаОперации)
	|			ТОГДА NULL
	|		ИНАЧЕ ВалютаУправленческогоУчета.Значение
	|	КОНЕЦ КАК ВалютаЛимитирования
	|ПОМЕСТИТЬ ВТ_ВалютыЛимитирования
	|ИЗ
	|	Константа.ВалютаУправленческогоУчета КАК ВалютаУправленческогоУчета,
	|	ВТ_ЦФОВсе КАК ВТ_ЦФОВсе
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПараметрыЛимитирования КАК ВТ_ПараметрыЛимитирования
	|		ПО ВТ_ЦФОВсе.Предназначение = ВТ_ПараметрыЛимитирования.Предназначение
	|			И ВТ_ЦФОВсе.ДатаОперации >= ВТ_ПараметрыЛимитирования.НачалоДействия
	|			И ВТ_ЦФОВсе.ДатаОперации <= ВТ_ПараметрыЛимитирования.ОкончаниеДействия
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыЛимитированияЦФО КАК ПараметрыЛимитированияЦФО
	|		ПО ВТ_ЦФОВсе.ЦФО = ПараметрыЛимитированияЦФО.ЦФО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПланыССуммамиЛимитирования.Период КАК Период,
	|	ВТ_ПланыССуммамиЛимитирования.Предназначение КАК Предназначение,
	|	ВТ_ПараметрыЛимитирования.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ВТ_ПланыССуммамиЛимитирования.СтатьяБюджета КАК СтатьяБюджета,
	|	ВЫБОР
	|		КОГДА ВТ_ПараметрыЛимитирования.КонтролироватьАналитику1
	|			ТОГДА ВТ_ПланыССуммамиЛимитирования.Аналитика1
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Аналитика1,
	|	ВЫБОР
	|		КОГДА ВТ_ПараметрыЛимитирования.КонтролироватьАналитику2
	|			ТОГДА ВТ_ПланыССуммамиЛимитирования.Аналитика2
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Аналитика2,
	|	ВЫБОР
	|		КОГДА ВТ_ПараметрыЛимитирования.КонтролироватьАналитику3
	|			ТОГДА ВТ_ПланыССуммамиЛимитирования.Аналитика3
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Аналитика3,
	|	ВЫБОР
	|		КОГДА ВТ_ПараметрыЛимитирования.КонтролироватьАналитику4
	|			ТОГДА ВТ_ПланыССуммамиЛимитирования.Аналитика4
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Аналитика4,
	|	ВЫБОР
	|		КОГДА ВТ_ПараметрыЛимитирования.КонтролироватьАналитику5
	|			ТОГДА ВТ_ПланыССуммамиЛимитирования.Аналитика5
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Аналитика5,
	|	ВЫБОР
	|		КОГДА ВТ_ПараметрыЛимитирования.КонтролироватьАналитику6
	|			ТОГДА ВТ_ПланыССуммамиЛимитирования.Аналитика6
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Аналитика6,
	|	ВТ_ПланыССуммамиЛимитирования.ЦФО КАК ЦФО,
	|	ВТ_ПланыССуммамиЛимитирования.Проект КАК Проект,
	|	ВТ_ПланыССуммамиЛимитирования.ДокументРезервирования КАК ДокументРезервирования,
	|	ВТ_ПланыССуммамиЛимитирования.ДокументПланирования КАК ДокументПланирования,
	|	ВЫБОР
	|		КОГДА ВТ_ВалютыЛимитирования.СпособОпределенияВалютыЛимитирования = ЗНАЧЕНИЕ(Перечисление.СпособыОпределенияВалютыЛимитирования.ВалютаОперации)
	|			ТОГДА ВТ_ПланыССуммамиЛимитирования.Валюта
	|		ИНАЧЕ ВТ_ВалютыЛимитирования.ВалютаЛимитирования
	|	КОНЕЦ КАК Валюта,
	|	СУММА(ВЫРАЗИТЬ(ВТ_ПланыССуммамиЛимитирования.Лимит * ЕСТЬNULL(ВТ_КроссКурсы.КроссКурс, 1) КАК ЧИСЛО(18, 2))) КАК Лимит,
	|	СУММА(ВЫРАЗИТЬ(ВТ_ПланыССуммамиЛимитирования.Зарезервировано * ЕСТЬNULL(ВТ_КроссКурсы.КроссКурс, 1) КАК ЧИСЛО(18, 2))) КАК Зарезервировано,
	|	СУММА(ВЫРАЗИТЬ(ВТ_ПланыССуммамиЛимитирования.Заявлено * ЕСТЬNULL(ВТ_КроссКурсы.КроссКурс, 1) КАК ЧИСЛО(18, 2))) КАК Заявлено,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_ПланыССуммамиЛимитирования.Валюта_Доп = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|					ИЛИ ВТ_КроссКурсы.ВалютаЛимитирования ЕСТЬ NULL
	|				ТОГДА ВЫРАЗИТЬ(ВТ_ПланыССуммамиЛимитирования.Исполнено * ЕСТЬNULL(ВТ_КроссКурсы.КроссКурс, 1) КАК ЧИСЛО(18, 2))
	|			ИНАЧЕ ВЫРАЗИТЬ(ВТ_ПланыССуммамиЛимитирования.Исполнено_Доп * ЕСТЬNULL(ВТ_КроссКурсы_Доп.КроссКурс, 1) КАК ЧИСЛО(18, 2))
	|		КОНЕЦ * ВЫБОР
	|			КОГДА ВТ_ПланыССуммамиЛимитирования.ПриходРасход = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Приход)
	|				ТОГДА -1
	|			ИНАЧЕ 1
	|		КОНЕЦ) КАК Исполнено
	|ПОМЕСТИТЬ ВТ_ЛимитыПредварительные
	|ИЗ
	|	ВТ_ПланыССуммамиЛимитирования КАК ВТ_ПланыССуммамиЛимитирования
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПараметрыЛимитированияСтатейБюджетов КАК ВТ_ПараметрыЛимитирования
	|		ПО ВТ_ПланыССуммамиЛимитирования.Предназначение = ВТ_ПараметрыЛимитирования.Предназначение
	|			И ВТ_ПланыССуммамиЛимитирования.СтатьяБюджета = ВТ_ПараметрыЛимитирования.СтатьяБюджета
	|			И ВТ_ПланыССуммамиЛимитирования.ДатаОперации >= ВТ_ПараметрыЛимитирования.НачалоПериода
	|			И ВТ_ПланыССуммамиЛимитирования.ДатаОперации <= ВТ_ПараметрыЛимитирования.ОкончаниеПериода
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВалютыЛимитирования КАК ВТ_ВалютыЛимитирования
	|		ПО ВТ_ПланыССуммамиЛимитирования.Предназначение = ВТ_ВалютыЛимитирования.Предназначение
	|			И ВТ_ПланыССуммамиЛимитирования.ЦФО = ВТ_ВалютыЛимитирования.ЦФО
	|			И ВТ_ПланыССуммамиЛимитирования.ДатаОперации >= ВТ_ВалютыЛимитирования.НачалоДействия
	|			И ВТ_ПланыССуммамиЛимитирования.ДатаОперации <= ВТ_ВалютыЛимитирования.ОкончаниеДействия
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КроссКурсы КАК ВТ_КроссКурсы
	|		ПО ВТ_ПланыССуммамиЛимитирования.Валюта = ВТ_КроссКурсы.ВалютаПланирования
	|			И (ВТ_ВалютыЛимитирования.ВалютаЛимитирования = ВТ_КроссКурсы.ВалютаЛимитирования)
	|			И ВТ_ПланыССуммамиЛимитирования.ДатаОперации = ВТ_КроссКурсы.ДатаОперации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КроссКурсы КАК ВТ_КроссКурсы_Доп
	|		ПО ВТ_ПланыССуммамиЛимитирования.Валюта_Доп = ВТ_КроссКурсы_Доп.ВалютаПланирования
	|			И (ВТ_ВалютыЛимитирования.ВалютаЛимитирования = ВТ_КроссКурсы_Доп.ВалютаЛимитирования)
	|			И ВТ_ПланыССуммамиЛимитирования.ДатаОперации = ВТ_КроссКурсы_Доп.ДатаОперации
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПланыССуммамиЛимитирования.Предназначение,
	|	ВЫБОР
	|		КОГДА ВТ_ПараметрыЛимитирования.КонтролироватьАналитику2
	|			ТОГДА ВТ_ПланыССуммамиЛимитирования.Аналитика2
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВТ_ПланыССуммамиЛимитирования.СтатьяБюджета,
	|	ВЫБОР
	|		КОГДА ВТ_ПараметрыЛимитирования.КонтролироватьАналитику1
	|			ТОГДА ВТ_ПланыССуммамиЛимитирования.Аналитика1
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВТ_ПараметрыЛимитирования.КонтролироватьАналитику3
	|			ТОГДА ВТ_ПланыССуммамиЛимитирования.Аналитика3
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВТ_ПараметрыЛимитирования.КонтролироватьАналитику4
	|			ТОГДА ВТ_ПланыССуммамиЛимитирования.Аналитика4
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВТ_ПараметрыЛимитирования.КонтролироватьАналитику5
	|			ТОГДА ВТ_ПланыССуммамиЛимитирования.Аналитика5
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВТ_ПараметрыЛимитирования.КонтролироватьАналитику6
	|			ТОГДА ВТ_ПланыССуммамиЛимитирования.Аналитика6
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВТ_ПараметрыЛимитирования.ПериодЛимитирования,
	|	ВТ_ПланыССуммамиЛимитирования.ЦФО,
	|	ВТ_ПланыССуммамиЛимитирования.Проект,
	|	ВТ_ПланыССуммамиЛимитирования.ДокументРезервирования,
	|	ВТ_ПланыССуммамиЛимитирования.ДокументПланирования,
	|	ВТ_ПланыССуммамиЛимитирования.Период,
	|	ВЫБОР
	|		КОГДА ВТ_ВалютыЛимитирования.СпособОпределенияВалютыЛимитирования = ЗНАЧЕНИЕ(Перечисление.СпособыОпределенияВалютыЛимитирования.ВалютаОперации)
	|			ТОГДА ВТ_ПланыССуммамиЛимитирования.Валюта
	|		ИНАЧЕ ВТ_ВалютыЛимитирования.ВалютаЛимитирования
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЛимитыПредварительные.Период КАК Период,
	|	ВТ_ЛимитыПредварительные.Предназначение КАК Предназначение,
	|	ВТ_ЛимитыПредварительные.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ВТ_ЛимитыПредварительные.СтатьяБюджета КАК СтатьяБюджета,
	|	ВТ_ЛимитыПредварительные.Аналитика1 КАК Аналитика1,
	|	ВТ_ЛимитыПредварительные.Аналитика2 КАК Аналитика2,
	|	ВТ_ЛимитыПредварительные.Аналитика3 КАК Аналитика3,
	|	ВТ_ЛимитыПредварительные.Аналитика4 КАК Аналитика4,
	|	ВТ_ЛимитыПредварительные.Аналитика5 КАК Аналитика5,
	|	ВТ_ЛимитыПредварительные.Аналитика6 КАК Аналитика6,
	|	ВТ_ЛимитыПредварительные.ЦФО КАК ЦФО,
	|	ВТ_ЛимитыПредварительные.Проект КАК Проект,
	|	ВТ_ЛимитыПредварительные.ДокументРезервирования КАК ДокументРезервирования,
	|	ВТ_ЛимитыПредварительные.ДокументПланирования КАК ДокументПланирования,
	|	ВТ_ЛимитыПредварительные.Валюта КАК Валюта,
	|	ВТ_ЛимитыПредварительные.Лимит КАК Лимит,
	|	ВТ_ЛимитыПредварительные.Зарезервировано КАК Зарезервировано,
	|	ВТ_ЛимитыПредварительные.Заявлено КАК Заявлено,
	|	ВТ_ЛимитыПредварительные.Исполнено КАК Исполнено
	|ПОМЕСТИТЬ ВТ_ЗаявленоПоРезерву
	|ИЗ
	|	ВТ_ЛимитыПредварительные КАК ВТ_ЛимитыПредварительные
	|ГДЕ
	|	ВТ_ЛимитыПредварительные.ДокументРезервирования <> ЗНАЧЕНИЕ(Документ.ОперативныйПлан.ПустаяСсылка)
	|	И ВТ_ЛимитыПредварительные.Заявлено <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Запрос1.Предназначение КАК Предназначение,
	|	Запрос1.ПериодЛимитирования КАК ПериодЛимитирования,
	|	Запрос1.ЦФО КАК ЦФО,
	|	Запрос1.Проект КАК Проект,
	|	Запрос1.СтатьяБюджета КАК СтатьяБюджета,
	|	Запрос1.Аналитика1 КАК Аналитика1,
	|	Запрос1.Аналитика2 КАК Аналитика2,
	|	Запрос1.Аналитика3 КАК Аналитика3,
	|	Запрос1.Аналитика4 КАК Аналитика4,
	|	Запрос1.Аналитика5 КАК Аналитика5,
	|	Запрос1.Аналитика6 КАК Аналитика6,
	|	Запрос1.Валюта КАК Валюта,
	|	Запрос1.ДокументРезервирования КАК ДокументРезервирования,
	|	СУММА(Запрос1.ЗарезервированоОборот) КАК ЗарезервированоОстаток
	|ПОМЕСТИТЬ ВТ_ОстаткиРезервов
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЛимитыПоБюджетамОбороты.Предназначение КАК Предназначение,
	|		ЛимитыПоБюджетамОбороты.ПериодЛимитирования КАК ПериодЛимитирования,
	|		ЛимитыПоБюджетамОбороты.ЦФО КАК ЦФО,
	|		ЛимитыПоБюджетамОбороты.Проект КАК Проект,
	|		ЛимитыПоБюджетамОбороты.СтатьяБюджета КАК СтатьяБюджета,
	|		ЛимитыПоБюджетамОбороты.Аналитика1 КАК Аналитика1,
	|		ЛимитыПоБюджетамОбороты.Аналитика2 КАК Аналитика2,
	|		ЛимитыПоБюджетамОбороты.Аналитика3 КАК Аналитика3,
	|		ЛимитыПоБюджетамОбороты.Аналитика4 КАК Аналитика4,
	|		ЛимитыПоБюджетамОбороты.Аналитика5 КАК Аналитика5,
	|		ЛимитыПоБюджетамОбороты.Аналитика6 КАК Аналитика6,
	|		ЛимитыПоБюджетамОбороты.Валюта КАК Валюта,
	|		ЛимитыПоБюджетамОбороты.ДокументРезервирования КАК ДокументРезервирования,
	|		ЛимитыПоБюджетамОбороты.ЗарезервированоОборот КАК ЗарезервированоОборот
	|	ИЗ
	|		РегистрНакопления.ЛимитыПоБюджетам.Обороты(
	|				,
	|				,
	|				,
	|				(Предназначение, ПериодЛимитирования, СтатьяБюджета, Аналитика1, Аналитика2, Аналитика3, Аналитика4, Аналитика5, Аналитика6, ЦФО, Проект, ДокументРезервирования) В
	|					(ВЫБРАТЬ
	|						ВТ_ЗаявленоПоРезерву.Предназначение КАК Предназначение,
	|						ВТ_ЗаявленоПоРезерву.ПериодЛимитирования КАК ПериодЛимитирования,
	|						ВТ_ЗаявленоПоРезерву.СтатьяБюджета КАК СтатьяБюджета,
	|						ВТ_ЗаявленоПоРезерву.Аналитика1 КАК Аналитика1,
	|						ВТ_ЗаявленоПоРезерву.Аналитика2 КАК Аналитика2,
	|						ВТ_ЗаявленоПоРезерву.Аналитика3 КАК Аналитика3,
	|						ВТ_ЗаявленоПоРезерву.Аналитика4 КАК Аналитика4,
	|						ВТ_ЗаявленоПоРезерву.Аналитика5 КАК Аналитика5,
	|						ВТ_ЗаявленоПоРезерву.Аналитика6 КАК Аналитика6,
	|						ВТ_ЗаявленоПоРезерву.ЦФО КАК ЦФО,
	|						ВТ_ЗаявленоПоРезерву.Проект КАК Проект,
	|						ВТ_ЗаявленоПоРезерву.ДокументРезервирования КАК ДокументРезервирования
	|					ИЗ
	|						ВТ_ЗаявленоПоРезерву КАК ВТ_ЗаявленоПоРезерву)) КАК ЛимитыПоБюджетамОбороты
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЛимитыПоБюджетам.Предназначение,
	|		ЛимитыПоБюджетам.ПериодЛимитирования,
	|		ЛимитыПоБюджетам.ЦФО,
	|		ЛимитыПоБюджетам.Проект,
	|		ЛимитыПоБюджетам.СтатьяБюджета,
	|		ЛимитыПоБюджетам.Аналитика1,
	|		ЛимитыПоБюджетам.Аналитика2,
	|		ЛимитыПоБюджетам.Аналитика3,
	|		ЛимитыПоБюджетам.Аналитика4,
	|		ЛимитыПоБюджетам.Аналитика5,
	|		ЛимитыПоБюджетам.Аналитика6,
	|		ЛимитыПоБюджетам.Валюта,
	|		ЛимитыПоБюджетам.ДокументРезервирования,
	|		-ЛимитыПоБюджетам.Зарезервировано
	|	ИЗ
	|		ВТ_ЗаявленоПоРезерву КАК ВТ_ЗаявленоПоРезерву
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЛимитыПоБюджетам КАК ЛимитыПоБюджетам
	|			ПО ВТ_ЗаявленоПоРезерву.Предназначение = ЛимитыПоБюджетам.Предназначение
	|				И ВТ_ЗаявленоПоРезерву.ПериодЛимитирования = ЛимитыПоБюджетам.ПериодЛимитирования
	|				И ВТ_ЗаявленоПоРезерву.СтатьяБюджета = ЛимитыПоБюджетам.СтатьяБюджета
	|				И ВТ_ЗаявленоПоРезерву.Аналитика1 = ЛимитыПоБюджетам.Аналитика1
	|				И ВТ_ЗаявленоПоРезерву.Аналитика2 = ЛимитыПоБюджетам.Аналитика2
	|				И ВТ_ЗаявленоПоРезерву.Аналитика3 = ЛимитыПоБюджетам.Аналитика3
	|				И ВТ_ЗаявленоПоРезерву.Аналитика4 = ЛимитыПоБюджетам.Аналитика4
	|				И ВТ_ЗаявленоПоРезерву.Аналитика5 = ЛимитыПоБюджетам.Аналитика5
	|				И ВТ_ЗаявленоПоРезерву.Аналитика6 = ЛимитыПоБюджетам.Аналитика6
	|				И ВТ_ЗаявленоПоРезерву.ЦФО = ЛимитыПоБюджетам.ЦФО
	|				И ВТ_ЗаявленоПоРезерву.Проект = ЛимитыПоБюджетам.Проект
	|				И ВТ_ЗаявленоПоРезерву.ДокументРезервирования = ЛимитыПоБюджетам.ДокументРезервирования
	|				И ВТ_ЗаявленоПоРезерву.Валюта = ЛимитыПоБюджетам.Валюта
	|	ГДЕ
	|		ЛимитыПоБюджетам.Регистратор = &Ссылка) КАК Запрос1
	|
	|СГРУППИРОВАТЬ ПО
	|	Запрос1.Предназначение,
	|	Запрос1.ПериодЛимитирования,
	|	Запрос1.ЦФО,
	|	Запрос1.Аналитика1,
	|	Запрос1.Валюта,
	|	Запрос1.Аналитика2,
	|	Запрос1.Аналитика3,
	|	Запрос1.Аналитика4,
	|	Запрос1.Аналитика5,
	|	Запрос1.Аналитика6,
	|	Запрос1.Проект,
	|	Запрос1.ДокументРезервирования,
	|	Запрос1.СтатьяБюджета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЛимитыПредварительные.Период КАК Период,
	|	ВТ_ЛимитыПредварительные.Предназначение КАК Предназначение,
	|	ВТ_ЛимитыПредварительные.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ВТ_ЛимитыПредварительные.СтатьяБюджета КАК СтатьяБюджета,
	|	ВТ_ЛимитыПредварительные.Аналитика1 КАК Аналитика1,
	|	ВТ_ЛимитыПредварительные.Аналитика2 КАК Аналитика2,
	|	ВТ_ЛимитыПредварительные.Аналитика3 КАК Аналитика3,
	|	ВТ_ЛимитыПредварительные.Аналитика4 КАК Аналитика4,
	|	ВТ_ЛимитыПредварительные.Аналитика5 КАК Аналитика5,
	|	ВТ_ЛимитыПредварительные.Аналитика6 КАК Аналитика6,
	|	ВТ_ЛимитыПредварительные.ЦФО КАК ЦФО,
	|	ВТ_ЛимитыПредварительные.Проект КАК Проект,
	|	ВТ_ЛимитыПредварительные.ДокументРезервирования КАК ДокументРезервирования,
	|	ВТ_ЛимитыПредварительные.ДокументПланирования КАК ДокументПланирования,
	|	ВТ_ЛимитыПредварительные.Валюта КАК Валюта,
	|	ВТ_ЛимитыПредварительные.Лимит КАК Лимит,
	|	ВТ_ЛимитыПредварительные.Зарезервировано КАК Зарезервировано,
	|	ВТ_ЛимитыПредварительные.Заявлено КАК Заявлено,
	|	ВТ_ЛимитыПредварительные.Исполнено КАК Исполнено
	|ПОМЕСТИТЬ ВТ_ИсполненоПоЗаявке
	|ИЗ
	|	ВТ_ЛимитыПредварительные КАК ВТ_ЛимитыПредварительные
	|ГДЕ
	|	ВТ_ЛимитыПредварительные.Предназначение <> ЗНАЧЕНИЕ(Перечисление.ПредназначенияЭлементовСтруктурыОтчета.БюджетДвиженияДенежныхСредств)
	|	И ВТ_ЛимитыПредварительные.ДокументПланирования <> НЕОПРЕДЕЛЕНО
	|	И ВТ_ЛимитыПредварительные.ДокументПланирования <> ЗНАЧЕНИЕ(Документ.ОперативныйПлан.ПустаяСсылка)
	|	И ВТ_ЛимитыПредварительные.ДокументПланирования <> ЗНАЧЕНИЕ(Документ.ОперативныйПлан.ПустаяСсылка)
	|	И ВТ_ЛимитыПредварительные.Исполнено <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Запрос1.Предназначение КАК Предназначение,
	|	Запрос1.ПериодЛимитирования КАК ПериодЛимитирования,
	|	Запрос1.ЦФО КАК ЦФО,
	|	Запрос1.Проект КАК Проект,
	|	Запрос1.СтатьяБюджета КАК СтатьяБюджета,
	|	Запрос1.Аналитика1 КАК Аналитика1,
	|	Запрос1.Аналитика2 КАК Аналитика2,
	|	Запрос1.Аналитика3 КАК Аналитика3,
	|	Запрос1.Аналитика4 КАК Аналитика4,
	|	Запрос1.Аналитика5 КАК Аналитика5,
	|	Запрос1.Аналитика6 КАК Аналитика6,
	|	Запрос1.Валюта КАК Валюта,
	|	Запрос1.ДокументРезервирования КАК ДокументРезервирования,
	|	Запрос1.ДокументПланирования КАК ДокументПланирования,
	|	СУММА(Запрос1.ЗаявленоОборот) КАК ЗаявленоОстаток
	|ПОМЕСТИТЬ ВТ_ОстаткиЗаявлено
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЛимитыПоБюджетамОбороты.Предназначение КАК Предназначение,
	|		ЛимитыПоБюджетамОбороты.ПериодЛимитирования КАК ПериодЛимитирования,
	|		ЛимитыПоБюджетамОбороты.ЦФО КАК ЦФО,
	|		ЛимитыПоБюджетамОбороты.Проект КАК Проект,
	|		ЛимитыПоБюджетамОбороты.СтатьяБюджета КАК СтатьяБюджета,
	|		ЛимитыПоБюджетамОбороты.Аналитика1 КАК Аналитика1,
	|		ЛимитыПоБюджетамОбороты.Аналитика2 КАК Аналитика2,
	|		ЛимитыПоБюджетамОбороты.Аналитика3 КАК Аналитика3,
	|		ЛимитыПоБюджетамОбороты.Аналитика4 КАК Аналитика4,
	|		ЛимитыПоБюджетамОбороты.Аналитика5 КАК Аналитика5,
	|		ЛимитыПоБюджетамОбороты.Аналитика6 КАК Аналитика6,
	|		ЛимитыПоБюджетамОбороты.Валюта КАК Валюта,
	|		ЛимитыПоБюджетамОбороты.ДокументРезервирования КАК ДокументРезервирования,
	|		ЛимитыПоБюджетамОбороты.ДокументПланирования КАК ДокументПланирования,
	|		ЛимитыПоБюджетамОбороты.ЗаявленоОборот КАК ЗаявленоОборот
	|	ИЗ
	|		РегистрНакопления.ЛимитыПоБюджетам.Обороты(
	|				,
	|				,
	|				,
	|				(Предназначение, ПериодЛимитирования, СтатьяБюджета, Аналитика1, Аналитика2, Аналитика3, Аналитика4, Аналитика5, Аналитика6, ЦФО, Проект, ДокументПланирования) В
	|					(ВЫБРАТЬ
	|						ВТ_ИсполненоПоЗаявке.Предназначение КАК Предназначение,
	|						ВТ_ИсполненоПоЗаявке.ПериодЛимитирования КАК ПериодЛимитирования,
	|						ВТ_ИсполненоПоЗаявке.СтатьяБюджета КАК СтатьяБюджета,
	|						ВТ_ИсполненоПоЗаявке.Аналитика1 КАК Аналитика1,
	|						ВТ_ИсполненоПоЗаявке.Аналитика2 КАК Аналитика2,
	|						ВТ_ИсполненоПоЗаявке.Аналитика3 КАК Аналитика3,
	|						ВТ_ИсполненоПоЗаявке.Аналитика4 КАК Аналитика4,
	|						ВТ_ИсполненоПоЗаявке.Аналитика5 КАК Аналитика5,
	|						ВТ_ИсполненоПоЗаявке.Аналитика6 КАК Аналитика6,
	|						ВТ_ИсполненоПоЗаявке.ЦФО КАК ЦФО,
	|						ВТ_ИсполненоПоЗаявке.Проект КАК Проект,
	|						ВТ_ИсполненоПоЗаявке.ДокументПланирования КАК ДокументПланирования
	|					ИЗ
	|						ВТ_ИсполненоПоЗаявке КАК ВТ_ИсполненоПоЗаявке)) КАК ЛимитыПоБюджетамОбороты
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЛимитыПоБюджетам.Предназначение,
	|		ЛимитыПоБюджетам.ПериодЛимитирования,
	|		ЛимитыПоБюджетам.ЦФО,
	|		ЛимитыПоБюджетам.Проект,
	|		ЛимитыПоБюджетам.СтатьяБюджета,
	|		ЛимитыПоБюджетам.Аналитика1,
	|		ЛимитыПоБюджетам.Аналитика2,
	|		ЛимитыПоБюджетам.Аналитика3,
	|		ЛимитыПоБюджетам.Аналитика4,
	|		ЛимитыПоБюджетам.Аналитика5,
	|		ЛимитыПоБюджетам.Аналитика6,
	|		ЛимитыПоБюджетам.Валюта,
	|		ЛимитыПоБюджетам.ДокументРезервирования,
	|		ЛимитыПоБюджетам.ДокументПланирования,
	|		-ЛимитыПоБюджетам.Заявлено
	|	ИЗ
	|		ВТ_ИсполненоПоЗаявке КАК ВТ_ИсполненоПоЗаявке
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЛимитыПоБюджетам КАК ЛимитыПоБюджетам
	|			ПО ВТ_ИсполненоПоЗаявке.Предназначение = ЛимитыПоБюджетам.Предназначение
	|				И ВТ_ИсполненоПоЗаявке.ПериодЛимитирования = ЛимитыПоБюджетам.ПериодЛимитирования
	|				И ВТ_ИсполненоПоЗаявке.СтатьяБюджета = ЛимитыПоБюджетам.СтатьяБюджета
	|				И ВТ_ИсполненоПоЗаявке.Аналитика1 = ЛимитыПоБюджетам.Аналитика1
	|				И ВТ_ИсполненоПоЗаявке.Аналитика2 = ЛимитыПоБюджетам.Аналитика2
	|				И ВТ_ИсполненоПоЗаявке.Аналитика3 = ЛимитыПоБюджетам.Аналитика3
	|				И ВТ_ИсполненоПоЗаявке.Аналитика4 = ЛимитыПоБюджетам.Аналитика4
	|				И ВТ_ИсполненоПоЗаявке.Аналитика5 = ЛимитыПоБюджетам.Аналитика5
	|				И ВТ_ИсполненоПоЗаявке.Аналитика6 = ЛимитыПоБюджетам.Аналитика6
	|				И ВТ_ИсполненоПоЗаявке.ЦФО = ЛимитыПоБюджетам.ЦФО
	|				И ВТ_ИсполненоПоЗаявке.Проект = ЛимитыПоБюджетам.Проект
	|				И ВТ_ИсполненоПоЗаявке.ДокументПланирования = ЛимитыПоБюджетам.ДокументПланирования
	|				И ВТ_ИсполненоПоЗаявке.Валюта = ЛимитыПоБюджетам.Валюта
	|	ГДЕ
	|		ЛимитыПоБюджетам.Регистратор = &Ссылка) КАК Запрос1
	|
	|СГРУППИРОВАТЬ ПО
	|	Запрос1.Предназначение,
	|	Запрос1.ПериодЛимитирования,
	|	Запрос1.ЦФО,
	|	Запрос1.Аналитика1,
	|	Запрос1.Валюта,
	|	Запрос1.Аналитика2,
	|	Запрос1.Аналитика3,
	|	Запрос1.Аналитика4,
	|	Запрос1.Аналитика5,
	|	Запрос1.Аналитика6,
	|	Запрос1.Проект,
	|	Запрос1.ДокументРезервирования,
	|	Запрос1.СтатьяБюджета,
	|	Запрос1.ДокументПланирования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЛимитыПредварительные.Период КАК Период,
	|	ВТ_ЛимитыПредварительные.Предназначение КАК Предназначение,
	|	ВТ_ЛимитыПредварительные.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ВТ_ЛимитыПредварительные.СтатьяБюджета КАК СтатьяБюджета,
	|	ВТ_ЛимитыПредварительные.Аналитика1 КАК Аналитика1,
	|	ВТ_ЛимитыПредварительные.Аналитика2 КАК Аналитика2,
	|	ВТ_ЛимитыПредварительные.Аналитика3 КАК Аналитика3,
	|	ВТ_ЛимитыПредварительные.Аналитика4 КАК Аналитика4,
	|	ВТ_ЛимитыПредварительные.Аналитика5 КАК Аналитика5,
	|	ВТ_ЛимитыПредварительные.Аналитика6 КАК Аналитика6,
	|	ВТ_ЛимитыПредварительные.ЦФО КАК ЦФО,
	|	ВТ_ЛимитыПредварительные.Проект КАК Проект,
	|	ВТ_ЛимитыПредварительные.ДокументРезервирования КАК ДокументРезервирования,
	|	ВТ_ЛимитыПредварительные.ДокументПланирования КАК ДокументПланирования,
	|	ВТ_ЛимитыПредварительные.Валюта КАК Валюта,
	|	ВТ_ЛимитыПредварительные.Лимит КАК Лимит,
	|	ВЫБОР
	|		КОГДА ВТ_ЛимитыПредварительные.Исполнено < 0
	|			ТОГДА 0
	|		КОГДА ВТ_ЛимитыПредварительные.Заявлено > 0
	|				И НЕ ВТ_ОстаткиРезервов.ЗарезервированоОстаток ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_ЛимитыПредварительные.Заявлено > ВТ_ОстаткиРезервов.ЗарезервированоОстаток
	|						ТОГДА -ВТ_ОстаткиРезервов.ЗарезервированоОстаток
	|					ИНАЧЕ -ВТ_ЛимитыПредварительные.Заявлено
	|				КОНЕЦ
	|		ИНАЧЕ ВТ_ЛимитыПредварительные.Зарезервировано
	|	КОНЕЦ КАК Зарезервировано,
	|	ВЫБОР
	|		КОГДА ВТ_ЛимитыПредварительные.Исполнено > 0
	|				И НЕ ВТ_ОстаткиЗаявлено.ЗаявленоОстаток ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|					КОГДА ВТ_ЛимитыПредварительные.Исполнено > ВТ_ОстаткиЗаявлено.ЗаявленоОстаток
	|						ТОГДА -ВТ_ОстаткиЗаявлено.ЗаявленоОстаток
	|					ИНАЧЕ -ВТ_ЛимитыПредварительные.Исполнено
	|				КОНЕЦ
	|		ИНАЧЕ ВТ_ЛимитыПредварительные.Заявлено
	|	КОНЕЦ КАК Заявлено,
	|	ВТ_ЛимитыПредварительные.Исполнено КАК Исполнено,
	|	ВЫБОР
	|		КОГДА &РегистрироватьДефицитРезерва
	|				И ВТ_ЛимитыПредварительные.ДокументРезервирования <> ЗНАЧЕНИЕ(Документ.ОперативныйПлан.ПустаяСсылка)
	|				И ВТ_ЛимитыПредварительные.Заявлено > ЕСТЬNULL(ВТ_ОстаткиРезервов.ЗарезервированоОстаток, 0)
	|			ТОГДА ЕСТЬNULL(ВТ_ОстаткиРезервов.ЗарезервированоОстаток, 0) - ВТ_ЛимитыПредварительные.Заявлено
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ДефицитРезерва
	|ПОМЕСТИТЬ ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке
	|ИЗ
	|	ВТ_ЛимитыПредварительные КАК ВТ_ЛимитыПредварительные
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОстаткиРезервов КАК ВТ_ОстаткиРезервов
	|		ПО ВТ_ЛимитыПредварительные.Предназначение = ВТ_ОстаткиРезервов.Предназначение
	|			И ВТ_ЛимитыПредварительные.ПериодЛимитирования = ВТ_ОстаткиРезервов.ПериодЛимитирования
	|			И ВТ_ЛимитыПредварительные.СтатьяБюджета = ВТ_ОстаткиРезервов.СтатьяБюджета
	|			И ВТ_ЛимитыПредварительные.Аналитика1 = ВТ_ОстаткиРезервов.Аналитика1
	|			И ВТ_ЛимитыПредварительные.Аналитика2 = ВТ_ОстаткиРезервов.Аналитика2
	|			И ВТ_ЛимитыПредварительные.Аналитика3 = ВТ_ОстаткиРезервов.Аналитика3
	|			И ВТ_ЛимитыПредварительные.Аналитика4 = ВТ_ОстаткиРезервов.Аналитика4
	|			И ВТ_ЛимитыПредварительные.Аналитика5 = ВТ_ОстаткиРезервов.Аналитика5
	|			И ВТ_ЛимитыПредварительные.Аналитика6 = ВТ_ОстаткиРезервов.Аналитика6
	|			И ВТ_ЛимитыПредварительные.ЦФО = ВТ_ОстаткиРезервов.ЦФО
	|			И ВТ_ЛимитыПредварительные.Проект = ВТ_ОстаткиРезервов.Проект
	|			И ВТ_ЛимитыПредварительные.ДокументРезервирования = ВТ_ОстаткиРезервов.ДокументРезервирования
	|			И ВТ_ЛимитыПредварительные.Валюта = ВТ_ОстаткиРезервов.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОстаткиЗаявлено КАК ВТ_ОстаткиЗаявлено
	|		ПО ВТ_ЛимитыПредварительные.Предназначение = ВТ_ОстаткиЗаявлено.Предназначение
	|			И ВТ_ЛимитыПредварительные.ПериодЛимитирования = ВТ_ОстаткиЗаявлено.ПериодЛимитирования
	|			И ВТ_ЛимитыПредварительные.СтатьяБюджета = ВТ_ОстаткиЗаявлено.СтатьяБюджета
	|			И ВТ_ЛимитыПредварительные.Аналитика1 = ВТ_ОстаткиЗаявлено.Аналитика1
	|			И ВТ_ЛимитыПредварительные.Аналитика2 = ВТ_ОстаткиЗаявлено.Аналитика2
	|			И ВТ_ЛимитыПредварительные.Аналитика3 = ВТ_ОстаткиЗаявлено.Аналитика3
	|			И ВТ_ЛимитыПредварительные.Аналитика4 = ВТ_ОстаткиЗаявлено.Аналитика4
	|			И ВТ_ЛимитыПредварительные.Аналитика5 = ВТ_ОстаткиЗаявлено.Аналитика5
	|			И ВТ_ЛимитыПредварительные.Аналитика6 = ВТ_ОстаткиЗаявлено.Аналитика6
	|			И ВТ_ЛимитыПредварительные.ЦФО = ВТ_ОстаткиЗаявлено.ЦФО
	|			И ВТ_ЛимитыПредварительные.Проект = ВТ_ОстаткиЗаявлено.Проект
	|			И ВТ_ЛимитыПредварительные.ДокументРезервирования = ВТ_ОстаткиЗаявлено.ДокументРезервирования
	|			И ВТ_ЛимитыПредварительные.ДокументПланирования = ВТ_ОстаткиЗаявлено.ДокументПланирования
	|			И ВТ_ЛимитыПредварительные.Валюта = ВТ_ОстаткиЗаявлено.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке.Период КАК Период,
	|	ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке.Предназначение КАК Предназначение,
	|	ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке.СтатьяБюджета КАК СтатьяБюджета,
	|	ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке.Аналитика1 КАК Аналитика1,
	|	ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке.Аналитика2 КАК Аналитика2,
	|	ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке.Аналитика3 КАК Аналитика3,
	|	ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке.Аналитика4 КАК Аналитика4,
	|	ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке.Аналитика5 КАК Аналитика5,
	|	ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке.Аналитика6 КАК Аналитика6,
	|	ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке.ЦФО КАК ЦФО,
	|	ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке.Проект КАК Проект,
	|	ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке.ДокументРезервирования КАК ДокументРезервирования,
	|	ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке.ДокументПланирования КАК ДокументПланирования,
	|	ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке.Валюта КАК Валюта,
	|	ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке.Лимит КАК Лимит,
	|	ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке.Зарезервировано КАК Зарезервировано,
	|	ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке.Заявлено КАК Заявлено,
	|	ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке.Исполнено КАК Исполнено,
	|	0 КАК КОбеспечению,
	|	ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке.ДефицитРезерва КАК ДефицитРезерва
	|ПОМЕСТИТЬ ВТ_ДвиженияЛимитыПоБюджетам
	|ИЗ
	|	ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке КАК ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке
	|ГДЕ
	|	(ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке.Лимит <> 0
	|			ИЛИ ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке.Зарезервировано <> 0
	|			ИЛИ ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке.Заявлено <> 0
	|			ИЛИ ВТ_ДвиженияПоЛимитамПоБюджетамКДопОбработке.Исполнено <> 0)";
	
	Если ПоместитьРезультатВоВременнуюТаблицу <> Истина Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПОМЕСТИТЬ ВТ_ДвиженияЛимитыПоБюджетам", "");
	КонецЕсли; 
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции // ПолучитьТаблицуЛимитов(Запрос, ДатаСовершенияОперации)

//
Процедура ПолучитьВТ_ТекущиеЛимиты(Запрос, ДатаКонтроляЛимита) Экспорт
	
	ТекстЗапроса = Запрос.Текст;
	
	Запрос.УстановитьПараметр("ДатаКонтроляЛимита", ДатаКонтроляЛимита);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЛимитыПоБюджетамОстатки.Предназначение КАК Предназначение,
	|	ЛимитыПоБюджетамОстатки.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ЛимитыПоБюджетамОстатки.ЦФО КАК ЦФО,
	|	ЛимитыПоБюджетамОстатки.Проект КАК Проект,
	|	ЛимитыПоБюджетамОстатки.СтатьяБюджета КАК СтатьяБюджета,
	|	ЛимитыПоБюджетамОстатки.Аналитика1 КАК Аналитика1,
	|	ЛимитыПоБюджетамОстатки.Аналитика2 КАК Аналитика2,
	|	ЛимитыПоБюджетамОстатки.Аналитика3 КАК Аналитика3,
	|	ЛимитыПоБюджетамОстатки.Аналитика4 КАК Аналитика4,
	|	ЛимитыПоБюджетамОстатки.Аналитика5 КАК Аналитика5,
	|	ЛимитыПоБюджетамОстатки.Аналитика6 КАК Аналитика6,
	|	ЛимитыПоБюджетамОстатки.Валюта КАК Валюта,
	|	ЛимитыПоБюджетамОстатки.ДокументРезервирования КАК ДокументРезервирования,
	|	ЛимитыПоБюджетамОстатки.ДокументПланирования КАК ДокументПланирования,
	|	ЛимитыПоБюджетамОстатки.ЛимитОборот КАК Лимит,
	|	ЛимитыПоБюджетамОстатки.КорректировкаОборот КАК Корректировка,
	|	ЛимитыПоБюджетамОстатки.ЗарезервированоОборот КАК Зарезервировано,
	|	ЛимитыПоБюджетамОстатки.ЗаявленоОборот КАК Заявлено,
	|	ЛимитыПоБюджетамОстатки.ИсполненоОборот КАК Исполнено,
	|	ЛимитыПоБюджетамОстатки.КОбеспечениюОборот КАК КОбеспечениюОборот,
	|	ПараметрыЛимитирования.КонтрольЛимитов КАК КонтрольЛимитов,
	|	ПараметрыЛимитирования.КонтрольРезервов КАК КонтрольРезервов
	|ПОМЕСТИТЬ ВТ_ТекущиеЛимиты
	|ИЗ
	|	РегистрНакопления.ЛимитыПоБюджетам.Обороты(
	|			,
	|			,
	|			,
	|			(Предназначение, ПериодЛимитирования, СтатьяБюджета, Аналитика1, Аналитика2, Аналитика3, Аналитика4, Аналитика5, Аналитика6, ЦФО, Проект, Валюта) В
	|				(ВЫБРАТЬ
	|					Таблица.Предназначение,
	|					Таблица.ПериодЛимитирования,
	|					Таблица.СтатьяБюджета,
	|					Таблица.Аналитика1,
	|					Таблица.Аналитика2,
	|					Таблица.Аналитика3,
	|					Таблица.Аналитика4,
	|					Таблица.Аналитика5,
	|					Таблица.Аналитика6,
	|					Таблица.ЦФО,
	|					Таблица.Проект,
	|					Таблица.Валюта
	|				ИЗ
	|					ДвиженияЛимитыПоБюджетамИзменение КАК Таблица)) КАК ЛимитыПоБюджетамОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыЛимитирования КАК ПараметрыЛимитирования
	|		ПО ЛимитыПоБюджетамОстатки.Предназначение = ПараметрыЛимитирования.ВидБюджета.Предназначение
	|			И (НАЧАЛОПЕРИОДА(ЛимитыПоБюджетамОстатки.ПериодЛимитирования.ДатаНачала, ГОД) = ПараметрыЛимитирования.Период)
	|			И ЛимитыПоБюджетамОстатки.ПериодЛимитирования.Периодичность = ПараметрыЛимитирования.ПериодичностьЛимитирования
	|			И (ПараметрыЛимитирования.ИспользоватьЛимитирование = ИСТИНА)";
	Запрос.Выполнить();
	
	Запрос.Текст = ТекстЗапроса;
КонецПроцедуры

//
Функция ДобавитьКонтролиЛимитов(МассивКонтролей, ТекстЗапроса) Экспорт
	
	/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	МассивКонтролей.Добавить(ВРег("КонтрольЛимитовПоБюджетам"));
	МассивКонтролей.Добавить(ВРег("КонтрольЛимитовПоРезервам"));
	МассивКонтролей.Добавить(ВРег("КонтрольЛимитовКОбеспечению"));
	
	ТекстЗапроса = ТекстЗапроса
		+ ТекстЗапросаПроверкиБюджета()	+ ОбщегоНазначенияОПК.ТекстРазделителяЗапросовПакета()
		+ ТекстЗапросаПроверкиРезерва() + ОбщегоНазначенияОПК.ТекстРазделителяЗапросовПакета()
		+ ТекстЗапросаПроверкиКОбеспечению() + ОбщегоНазначенияОПК.ТекстРазделителяЗапросовПакета();
		
КонецФункции

// Функция возвращает период контроля лимита для вида бюджета и желаемой дате платежа
Функция ПолучитьПериодКонтроля(ВидБюджета, ДатаНачала) экспорт
	
	ПараметрыОперПланирования = ОперативноеПланированиеПовтИспУХ.ПолучитьПараметрыОперПланирования(ВидБюджета, ДатаНачала);
	Если ПараметрыОперПланирования.ИспользоватьЛимитирование Тогда
		
		Возврат	ВстраиваниеОПКПереопределяемый.глОтносительныйПериодПоДате(ДатаНачала,
					ПараметрыОперПланирования.ПериодичностьЛимитирования,0);
		
	Иначе
					
		Возврат Справочники.Периоды.ПустаяСсылка();
		
	КонецЕсли;
	
КонецФункции

// 
Функция ПолучитьЗаполненностьДоговораЗаявки(Организация, Контрагент, ДоговорКонтрагента) экспорт
	
	//
	Орг = ЗначениеЗаполнено(Организация);
	Кон = ЗначениеЗаполнено(Контрагент);
	Дог = ЗначениеЗаполнено(ДоговорКонтрагента);
	Если Орг И Кон И Дог Тогда
		ЗаполненностьДоговораЗаявки = 6;
	ИначеЕсли НЕ Орг И Кон И Дог Тогда
		ЗаполненностьДоговораЗаявки = 5;
	ИначеЕсли Орг И Кон И НЕ Дог Тогда
		ЗаполненностьДоговораЗаявки = 4;
	ИначеЕсли НЕ Орг И Кон И НЕ Дог Тогда
		ЗаполненностьДоговораЗаявки = 3;
	ИначеЕсли Орг И НЕ Кон И НЕ Дог Тогда
		ЗаполненностьДоговораЗаявки = 2;
	Иначе
		ЗаполненностьДоговораЗаявки = 1;
	КонецЕсли;
	
	Возврат ЗаполненностьДоговораЗаявки;
	
КонецФункции

#Область КонтрольНаличияПериодов

// Процедуры и функции контроля наличия элементов справочника Периоды для документов оперативного планирования
//
//	I. В процедуре менеджера документа ИнициализироватьДанныеДокумента после заполнения параметров инициализации добавить код:
//
//	// Если в справочнике периоды нет требуемых элементов, то прекращаем собирать данные
//	Если НЕ КонтрольЛимитовУХ.ПодготовитьТаблицуДатИПериодов(Запрос, ДокументСсылка, ДополнительныеСвойства) Тогда
//		Возврат;
//	КонецЕсли;
//
//  II. В процедуре ОбработкаПроведения, после вызова процедуры ИнициализироватьДанныеДокумента добавить код
//
//	// 
//	КонтрольЛимитовУХ.ВыполнитьПроверкуНаличияПериодов(Ссылка, ДополнительныеСвойства, Отказ);
//	Если Отказ Тогда
//		Возврат;
//	КонецЕсли;
//
//  III. В модуле менеджера документа должна быть функция ПолучитьДатыОпераций, которая 
//
//  Функция возвращает текст запроса, который формирует временную таблицу запроса ВТ_ДатыОпераций с колонками
//  Дата 		- Дата - Дата операции по документу
//	ВидБюджета - ПланВидовХарактеристикСсылка.ВидыБюджетов - Вид бюджета, по которому будет операция
//  Текст запроса уникален для каждого вида документа
//
//Функция ТексЗапроса_ДатыОперацийДокумента() экспорт
//	
//	Запрос.Текст = 
//	"ВЫБРАТЬ
//	|	НАЧАЛОПЕРИОДА(ТЧ.Ссылка.Дата, ДЕНЬ) КАК Дата,
//	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыБюджетов.БюджетДвиженияДенежныхСредств) КАК ВидБюджета
//	|ПОМЕСТИТЬ ВТ_ДатыОпераций
//	|ИЗ
//	|	Документ.<Документ>.<ТабличнаяЧасть> КАК ТЧ
//	|ГДЕ
//	|	ТЧ1.Ссылка = &Ссылка";
//	
//	Запрос.Выполнить();
//	
//КонецПроцедуры

// 
Функция ПодготовитьТаблицуДатИПериодов(Запрос, ДокументСсылка, ДополнительныеСвойства) экспорт
	
	Если НЕ ДополнительныеСвойства.Свойство("ПараметрыПроведения") Тогда
		ДополнительныеСвойства.Вставить("ПараметрыПроведения", Новый Структура);
	КонецЕсли;
	
	// Доступность планирования для бюджета закупок
	ДоступныеОперации = ОперативноеПланированиеПовтИспУХ.ДоступныеОперацииОперПлана(
		Перечисления.ПредназначенияЭлементовСтруктурыОтчета.БюджетДвиженияРесурсов);
		
	Запрос.УстановитьПараметр("ПланированиеПоБюджетуЗакупокДоступно",
		 ДоступныеОперации.НайтиПоЗначению(Перечисления.ВидыОперацийОперативныйПлан.Планирование) <> неопределено);
	
	// Контроль периодов планирования не всегда нужен. Например для ОперПлана в режимах Лимиты или Резервы
	Если ДополнительныеСвойства.Свойство("ДляПроведения") 
		И ДополнительныеСвойства.ДляПроведения.Свойство("КонтролироватьПериодыПланирования") Тогда
		
		Запрос.УстановитьПараметр("КонтролироватьПериодыПланирования", 
			ДополнительныеСвойства.ДляПроведения.КонтролироватьПериодыПланирования);
	Иначе
		Запрос.УстановитьПараметр("КонтролироватьПериодыПланирования", Ложь);
	КонецЕсли;
	
	// Контроль лимитирования не нужен для версии соглашения депозит
	Если ДополнительныеСвойства.Свойство("ДляПроведения") 
		И ДополнительныеСвойства.ДляПроведения.Свойство("КонтролироватьПериодыЛимитирования") Тогда
		
		Запрос.УстановитьПараметр("КонтролироватьПериодыЛимитирования", 
			ДополнительныеСвойства.ДляПроведения.КонтролироватьПериодыЛимитирования);
	Иначе
		Запрос.УстановитьПараметр("КонтролироватьПериодыЛимитирования", Истина);
	КонецЕсли;
	
	Если Запрос.МенеджерВременныхТаблиц.Таблицы.Найти("ВТ_ТаблицаДвиженияОперации") = неопределено Тогда
		Менеджер = ОбщегоНазначения.МенеджерОбъектаПоСсылке(ДокументСсылка);
		ТекстДатыОпераций = Менеджер.ТексЗапроса_ДатыОперацийДокумента();
	Иначе
		ТекстДатыОпераций =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НАЧАЛОПЕРИОДА(Данные.Период, ДЕНЬ) КАК Дата,
		|	Данные.ВидБюджета КАК ВидБюджета
		|ПОМЕСТИТЬ ВТ_ДатыОпераций
		|ИЗ
		|	ВТ_ТаблицаДвиженияОперации КАК Данные";
	КонецЕсли;
	
	// Формируем таблицу дат и видов бюджета. Уникальна для каждого типа документа
	
	Запрос.Текст = ТекстДатыОпераций
		+ ОбщегоНазначенияОПК.ТекстРазделителяЗапросовПакета()
		+ "ВЫБРАТЬ
		  |	ВТ_ДатыОпераций.Дата КАК Дата,
		  |	ВТ_ДатыОпераций.ВидБюджета КАК ВидБюджета,
		  |	ЕСТЬNULL(ПараметрыОперативногоПланирования.Использовать, ЛОЖЬ) КАК ИспользоватьПланирование,
		  |	ЕСТЬNULL(ПараметрыОперативногоПланирования.ПериодичностьОперативногоПланирования, ЗНАЧЕНИЕ(Перечисление.Периодичность.ПустаяСсылка)) КАК ПериодичностьОперативногоПланирования,
		  |	ЕСТЬNULL(ПараметрыЛимитирования.ИспользоватьЛимитирование, ЛОЖЬ) КАК ИспользоватьЛимитирование,
		  |	ЕСТЬNULL(ПараметрыЛимитирования.ПериодичностьЛимитирования, ЗНАЧЕНИЕ(Перечисление.Периодичность.ПустаяСсылка)) КАК ПериодичностьЛимитирования
		  |ПОМЕСТИТЬ ВТ_ДатыОплатСПериодичностью
		  |ИЗ
		  |	ВТ_ДатыОпераций КАК ВТ_ДатыОпераций
		  |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыОперативногоПланирования КАК ПараметрыОперативногоПланирования
		  |		ПО ВТ_ДатыОпераций.ВидБюджета = ПараметрыОперативногоПланирования.ВидБюджета
		  |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыЛимитирования КАК ПараметрыЛимитирования
		  |		ПО ВТ_ДатыОпераций.ВидБюджета = ПараметрыЛимитирования.ВидБюджета
		  |			И НАЧАЛОПЕРИОДА(ВТ_ДатыОпераций.Дата, ГОД) = ПараметрыЛимитирования.Период
		  |;
		  |
		  |////////////////////////////////////////////////////////////////////////////////
		  |ВЫБРАТЬ
		  |	ВТ_ДатыОплатСПериодичностью.Дата КАК Дата,
		  |	ВТ_ДатыОплатСПериодичностью.ВидБюджета КАК ВидБюджета,
		  |	ВТ_ДатыОплатСПериодичностью.ИспользоватьПланирование КАК ИспользоватьПланирование,
		  |	ВТ_ДатыОплатСПериодичностью.ПериодичностьОперативногоПланирования КАК ПериодичностьОперативногоПланирования,
		  |	ВТ_ДатыОплатСПериодичностью.ИспользоватьЛимитирование КАК ИспользоватьЛимитирование,
		  |	ВТ_ДатыОплатСПериодичностью.ПериодичностьЛимитирования КАК ПериодичностьЛимитирования,
		  |	ЕСТЬNULL(ПериодыПланирования.Ссылка, ЗНАЧЕНИЕ(Справочник.Периоды.ПустаяСсылка)) КАК ПериодПланирования,
		  |	ВЫБОР
		  |		КОГДА &КонтролироватьПериодыПланирования = ИСТИНА
		  |				И ВТ_ДатыОплатСПериодичностью.ИспользоватьПланирование
		  |				И ПериодыПланирования.Ссылка ЕСТЬ NULL
		  |			ТОГДА ИСТИНА
		  |		ИНАЧЕ ЛОЖЬ
		  |	КОНЕЦ КАК ЕстьОшибкаПериодПланирования,
		  |	ЕСТЬNULL(ПериодыЛимитирования.Ссылка, ЗНАЧЕНИЕ(Справочник.Периоды.ПустаяСсылка)) КАК ПериодЛимитирования,
		  |	ВЫБОР
		  |		КОГДА &КонтролироватьПериодыЛимитирования = ИСТИНА И ВТ_ДатыОплатСПериодичностью.ИспользоватьЛимитирование
		  |				И ПериодыЛимитирования.Ссылка ЕСТЬ NULL
		  |			ТОГДА ИСТИНА
		  |		ИНАЧЕ ЛОЖЬ
		  |	КОНЕЦ КАК ЕстьОшибкаПериодЛимитирования
		  |ПОМЕСТИТЬ ВТ_ДатыИПериоды
		  |ИЗ
		  |	ВТ_ДатыОплатСПериодичностью КАК ВТ_ДатыОплатСПериодичностью
		  |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Периоды КАК ПериодыПланирования
		  |		ПО ВТ_ДатыОплатСПериодичностью.ПериодичностьОперативногоПланирования = ПериодыПланирования.Периодичность
		  |			И (ПериодыПланирования.Произвольный = ЛОЖЬ)
		  |			И ВТ_ДатыОплатСПериодичностью.Дата >= ПериодыПланирования.ДатаНачала
		  |			И ВТ_ДатыОплатСПериодичностью.Дата <= ПериодыПланирования.ДатаОкончания
		  |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Периоды КАК ПериодыЛимитирования
		  |		ПО ВТ_ДатыОплатСПериодичностью.ПериодичностьЛимитирования = ПериодыЛимитирования.Периодичность
		  |			И (ПериодыЛимитирования.Произвольный = ЛОЖЬ)
		  |			И ВТ_ДатыОплатСПериодичностью.Дата >= ПериодыЛимитирования.ДатаНачала
		  |			И ВТ_ДатыОплатСПериодичностью.Дата <= ПериодыЛимитирования.ДатаОкончания
		  |;
		  |
		  |////////////////////////////////////////////////////////////////////////////////
		  |УНИЧТОЖИТЬ ВТ_ДатыОпераций
		  |;
		  |
		  |////////////////////////////////////////////////////////////////////////////////
		  |УНИЧТОЖИТЬ ВТ_ДатыОплатСПериодичностью
		  |;
		  |
		  |////////////////////////////////////////////////////////////////////////////////
		  |ВЫБРАТЬ
		  |	ВТ_ДатыИПериоды.Дата КАК Дата,
		  |	ВТ_ДатыИПериоды.ВидБюджета КАК ВидБюджета,
		  |	ВТ_ДатыИПериоды.ИспользоватьПланирование КАК ИспользоватьПланирование,
		  |	ВТ_ДатыИПериоды.ПериодичностьОперативногоПланирования КАК ПериодичностьОперативногоПланирования,
		  |	ВТ_ДатыИПериоды.ИспользоватьЛимитирование КАК ИспользоватьЛимитирование,
		  |	ВТ_ДатыИПериоды.ПериодичностьЛимитирования КАК ПериодичностьЛимитирования,
		  |	ВТ_ДатыИПериоды.ПериодПланирования КАК ПериодПланирования,
		  |	ВТ_ДатыИПериоды.ЕстьОшибкаПериодПланирования КАК ЕстьОшибкаПериодПланирования,
		  |	ВТ_ДатыИПериоды.ПериодЛимитирования КАК ПериодЛимитирования,
		  |	ВТ_ДатыИПериоды.ЕстьОшибкаПериодЛимитирования КАК ЕстьОшибкаПериодЛимитирования
		  |ИЗ
		  |	ВТ_ДатыИПериоды КАК ВТ_ДатыИПериоды
		  |ГДЕ
		  |	(ВТ_ДатыИПериоды.ЕстьОшибкаПериодПланирования = ИСТИНА
		  |			ИЛИ ВТ_ДатыИПериоды.ЕстьОшибкаПериодЛимитирования = ИСТИНА)";
		
	ТаблицаОшибок = Запрос.Выполнить().Выгрузить();
		
	ДополнительныеСвойства.ПараметрыПроведения.Вставить("ДатыИПериодыОперации", ТаблицаОшибок);
	Возврат ДополнительныеСвойства.ПараметрыПроведения.ДатыИПериодыОперации.Количество() = 0;
	
КонецФункции

Процедура ВыполнитьПроверкуНаличияПериодов(Ссылка, ДополнительныеСвойства, Отказ) экспорт
	
	// Проверка, что для каждой дате операции есть соответствующий период
	Если ДополнительныеСвойства.ПараметрыПроведения.Свойство("ДатыИПериодыОперации") Тогда
		
		Если ДополнительныеСвойства.ПараметрыПроведения.ДатыИПериодыОперации.Количество() > 0 Тогда
		
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(
					НСтр("ru = 'При проведении документа %1 не обнаружен(ы):'"), Ссылка),
				Ссылка,,,Отказ);
			
			ШаблонТекстаОбОшибке = НСтр("ru = '-  элемент справочника ""Периоды"" с периодичностью ""%1"" для даты %2;'");
			Для Каждого СтрокаОшибка Из ДополнительныеСвойства.ПараметрыПроведения.ДатыИПериодыОперации Цикл
				
				Если СтрокаОшибка.ЕстьОшибкаПериодПланирования Тогда 
					ОбщегоНазначения.СообщитьПользователю(
						СтрШаблон(
							ШаблонТекстаОбОшибке, 
							СтрокаОшибка.ПериодичностьОперативногоПланирования,
							Формат(СтрокаОшибка.Дата, "ДЛФ=D")),
					Ссылка,,,Отказ);
				КонецЕсли;
				
				Если СтрокаОшибка.ЕстьОшибкаПериодЛимитирования Тогда 
					ОбщегоНазначения.СообщитьПользователю(
						СтрШаблон(
							ШаблонТекстаОбОшибке, 
							СтрокаОшибка.ПериодичностьЛимитирования,
							Формат(СтрокаОшибка.Дата, "ДЛФ=D")),
					Ссылка,,,Отказ);
				КонецЕсли;
				
			КонецЦикла;
			
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Обратитесь к администратору. Проведение отменено.'"), Ссылка,,,Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Процедура возвращает пустую таблицу планов документа
//
// Возвращаемое значение:
//   ТаблицаЗначений   - Таблица планов документа
//
Функция Новый_ТаблицаПланыДокумента() Экспорт
	
	// Колонка Период содержит дату операции (не дату регистратора, а дату операции)
	Реквизиты = "Предназначение, Период, ПериодОтчета, ПриходРасход, ЦФО, Проект, СтатьяБюджета, "
		+ АналитикиСтатейБюджетовУХКлиентСервер.РеквизитыАналитикИзШаблона("Аналитика%1")+", "
		+ "Организация, Контрагент, ДоговорКонтрагента, "
		+ "Заявлено, Валюта, ДокументПланирования";
	
	//
	ТаблицаПланов = РегистрыНакопления.ОперативныйПланПоБюджетам.СоздатьНаборЗаписей().ВыгрузитьКолонки(Реквизиты);
	ТаблицаПланов.Колонки.Добавить("ВидБюджета", Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ВидыБюджетов"));
	ТаблицаПланов.Колонки.Добавить("ДокументРезервирования", Новый ОписаниеТипов("ДокументСсылка.ОперативныйПлан"));
	ТаблицаПланов.Колонки.Заявлено.Имя = "Сумма";
	
	Возврат ТаблицаПланов;
	
КонецФункции

// Процедура помещает в Параметры.Отбор.Ссылка лимитируемые в указанном периоде статьи вида бюджетов.
Процедура ОбработкаПолученияКонтролируемыхСтатейБюджетов(ДанныеВыбора, Параметры, СтандартнаяОбработка) Экспорт
	
	Если Параметры.Отбор.Свойство("ПараметрыЛимитирования_ВидБюджета") 
		И Параметры.Отбор.Свойство("ПараметрыЛимитирования_Период") Тогда
		
		Запрос = Новый Запрос;
		Если ТипЗнч(Параметры.Отбор.ПараметрыЛимитирования_Период) = Тип("СправочникСсылка.Периоды") Тогда
			Запрос.УстановитьПараметр("Период", Параметры.Отбор.ПараметрыЛимитирования_Период.ДатаНачала);
		Иначе
			Запрос.УстановитьПараметр("Период", Параметры.Отбор.ПараметрыЛимитирования_Период);
		КонецЕсли;
		
		Если Параметры.Отбор.ПараметрыЛимитирования_ВидБюджета = Перечисления.ПредназначенияЭлементовСтруктурыОтчета.БюджетДвиженияДенежныхСредств Тогда
			Запрос.УстановитьПараметр("ВидБюджета", ПланыВидовХарактеристик.ВидыБюджетов.БюджетДвиженияДенежныхСредств);
		ИначеЕсли Параметры.Отбор.ПараметрыЛимитирования_ВидБюджета = Перечисления.ПредназначенияЭлементовСтруктурыОтчета.БюджетДоходовИРасходов Тогда
			Запрос.УстановитьПараметр("ВидБюджета", ПланыВидовХарактеристик.ВидыБюджетов.БюджетДоходовИРасходов);
		ИначеЕсли Параметры.Отбор.ПараметрыЛимитирования_ВидБюджета = Перечисления.ПредназначенияЭлементовСтруктурыОтчета.БюджетДвиженияРесурсов Тогда
			Запрос.УстановитьПараметр("ВидБюджета", ПланыВидовХарактеристик.ВидыБюджетов.БюджетДвиженияРесурсов);
		Иначе
			Запрос.УстановитьПараметр("ВидБюджета", Параметры.Отбор.ПараметрыЛимитирования_ВидБюджета);
		КонецЕсли;
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПараметрыКонтроляЛимитаСтатейБюджетовСрезПоследних.СтатьяБюджета КАК СтатьяБюджета
		|ИЗ
		|	РегистрСведений.ПараметрыКонтроляЛимитаСтатейБюджетов.СрезПоследних(&Период, ВидБюджета = &ВидБюджета) КАК ПараметрыКонтроляЛимитаСтатейБюджетовСрезПоследних
		|ГДЕ
		|	ПараметрыКонтроляЛимитаСтатейБюджетовСрезПоследних.КонтролироватьСтатью = ИСТИНА";
		
		Параметры.Отбор.Вставить("Ссылка", Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("СтатьяБюджета"));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтразитьОперативныйПланПоБюджету(ДополнительныеСвойства, Движения, Отказ) Экспорт

	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаОперативныйПланПоБюджетам") Тогда
		Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаОперативныйПланПоБюджетам;
		Движения.ОперативныйПланПоБюджетам.Загрузить(Таблица);
	Иначе
		Движения.ОперативныйПланПоБюджетам.Очистить();
	КонецЕсли;
	
	Движения.ОперативныйПланПоБюджетам.Записывать = Истина;	
	
КонецПроцедуры

Процедура ОтразитьЛимитыПоБюджетам(ДополнительныеСвойства, Движения, Отказ) Экспорт

	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаЛимитыПоБюджетам") Тогда
		Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЛимитыПоБюджетам;
		Движения.ЛимитыПоБюджетам.Загрузить(Таблица);
	Иначе
		Движения.ЛимитыПоБюджетам.Очистить();
	КонецЕсли;
	
	Движения.ЛимитыПоБюджетам.Записывать = Истина;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаПроверкиБюджета()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Запрос1.Предназначение КАК Предназначение,
	|	Запрос1.ПериодЛимитирования КАК ПериодЛимитирования,
	|	Запрос1.ЦФО КАК ЦФО,
	|	Запрос1.Проект КАК Проект,
	|	Запрос1.СтатьяБюджета КАК СтатьяБюджета,
	|	Запрос1.Аналитика1 КАК Аналитика1,
	|	Запрос1.Аналитика2 КАК Аналитика2,
	|	Запрос1.Аналитика3 КАК Аналитика3,
	|	Запрос1.Аналитика4 КАК Аналитика4,
	|	Запрос1.Аналитика5 КАК Аналитика5,
	|	Запрос1.Аналитика6 КАК Аналитика6,
	|	Запрос1.Валюта КАК Валюта,
	|	СУММА(Запрос1.Лимит) КАК Лимит,
	|	СУММА(Запрос1.Зарезервировано) КАК Зарезервировано,
	|	СУММА(Запрос1.Заявлено) КАК Заявлено,
	|	СУММА(Запрос1.Исполнено) КАК Исполнено,
	|	ВЫБОР
	|		КОГДА СУММА(Запрос1.Лимит) + СУММА(Запрос1.Корректировка) - СУММА(Запрос1.Зарезервировано) - СУММА(Запрос1.Заявлено) - СУММА(Запрос1.Исполнено) >= 0
	|			ТОГДА 0
	|		ИНАЧЕ СУММА(Запрос1.Лимит) + СУММА(Запрос1.Корректировка) - СУММА(Запрос1.Зарезервировано) - СУММА(Запрос1.Заявлено) - СУММА(Запрос1.Исполнено)
	|	КОНЕЦ КАК Превышение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ_ТекущиеЛимиты.Предназначение КАК Предназначение,
	|		ВТ_ТекущиеЛимиты.ПериодЛимитирования КАК ПериодЛимитирования,
	|		ВТ_ТекущиеЛимиты.ЦФО КАК ЦФО,
	|		ВТ_ТекущиеЛимиты.Проект КАК Проект,
	|		ВТ_ТекущиеЛимиты.СтатьяБюджета КАК СтатьяБюджета,
	|		ВТ_ТекущиеЛимиты.Аналитика1 КАК Аналитика1,
	|		ВТ_ТекущиеЛимиты.Аналитика2 КАК Аналитика2,
	|		ВТ_ТекущиеЛимиты.Аналитика3 КАК Аналитика3,
	|		ВТ_ТекущиеЛимиты.Аналитика4 КАК Аналитика4,
	|		ВТ_ТекущиеЛимиты.Аналитика5 КАК Аналитика5,
	|		ВТ_ТекущиеЛимиты.Аналитика6 КАК Аналитика6,
	|		ВТ_ТекущиеЛимиты.Валюта КАК Валюта,
	|		ВТ_ТекущиеЛимиты.Лимит КАК Лимит,
	|		ВТ_ТекущиеЛимиты.Корректировка КАК Корректировка,
	|		ВТ_ТекущиеЛимиты.Зарезервировано КАК Зарезервировано,
	|		ВТ_ТекущиеЛимиты.Заявлено КАК Заявлено,
	|		ВТ_ТекущиеЛимиты.Исполнено КАК Исполнено
	|	ИЗ
	|		ВТ_ТекущиеЛимиты КАК ВТ_ТекущиеЛимиты
	|	ГДЕ
	|		ВТ_ТекущиеЛимиты.ДокументРезервирования = ЗНАЧЕНИЕ(Документ.ОперативныйПлан.ПустаяСсылка)
	|		И ВТ_ТекущиеЛимиты.КонтрольЛимитов = ЗНАЧЕНИЕ(Перечисление.РежимыКонтроляДокументов.Блокировать)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_ТекущиеЛимиты.Предназначение,
	|		ВТ_ТекущиеЛимиты.ПериодЛимитирования,
	|		ВТ_ТекущиеЛимиты.ЦФО,
	|		ВТ_ТекущиеЛимиты.Проект,
	|		ВТ_ТекущиеЛимиты.СтатьяБюджета,
	|		ВТ_ТекущиеЛимиты.Аналитика1,
	|		ВТ_ТекущиеЛимиты.Аналитика2,
	|		ВТ_ТекущиеЛимиты.Аналитика3,
	|		ВТ_ТекущиеЛимиты.Аналитика4,
	|		ВТ_ТекущиеЛимиты.Аналитика5,
	|		ВТ_ТекущиеЛимиты.Аналитика6,
	|		ВТ_ТекущиеЛимиты.Валюта,
	|		0,
	|		0,
	|		ВТ_ТекущиеЛимиты.Зарезервировано,
	|		0,
	|		0
	|	ИЗ
	|		ВТ_ТекущиеЛимиты КАК ВТ_ТекущиеЛимиты
	|	ГДЕ
	|		ВТ_ТекущиеЛимиты.ДокументРезервирования <> ЗНАЧЕНИЕ(Документ.ОперативныйПлан.ПустаяСсылка)
	|		И ВТ_ТекущиеЛимиты.КонтрольЛимитов = ЗНАЧЕНИЕ(Перечисление.РежимыКонтроляДокументов.Блокировать)) КАК Запрос1
	|
	|СГРУППИРОВАТЬ ПО
	|	Запрос1.ПериодЛимитирования,
	|	Запрос1.Проект,
	|	Запрос1.Аналитика1,
	|	Запрос1.Аналитика3,
	|	Запрос1.Аналитика4,
	|	Запрос1.Аналитика5,
	|	Запрос1.Аналитика6,
	|	Запрос1.Валюта,
	|	Запрос1.СтатьяБюджета,
	|	Запрос1.Предназначение,
	|	Запрос1.Аналитика2,
	|	Запрос1.ЦФО
	|ИМЕЮЩИЕ
	|	СУММА(Запрос1.Лимит) + СУММА(Запрос1.Корректировка) - СУММА(Запрос1.Зарезервировано) - СУММА(Запрос1.Заявлено) - СУММА(Запрос1.Исполнено) < 0";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПроверкиРезерва()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЛимитыПоБюджетамОстатки.Предназначение КАК Предназначение,
	|	ЛимитыПоБюджетамОстатки.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ЛимитыПоБюджетамОстатки.ЦФО КАК ЦФО,
	|	ЛимитыПоБюджетамОстатки.Проект КАК Проект,
	|	ЛимитыПоБюджетамОстатки.СтатьяБюджета КАК СтатьяБюджета,
	|	ЛимитыПоБюджетамОстатки.Аналитика1 КАК Аналитика1,
	|	ЛимитыПоБюджетамОстатки.Аналитика2 КАК Аналитика2,
	|	ЛимитыПоБюджетамОстатки.Аналитика3 КАК Аналитика3,
	|	ЛимитыПоБюджетамОстатки.Аналитика4 КАК Аналитика4,
	|	ЛимитыПоБюджетамОстатки.Аналитика5 КАК Аналитика5,
	|	ЛимитыПоБюджетамОстатки.Аналитика6 КАК Аналитика6,
	|	ЛимитыПоБюджетамОстатки.Валюта КАК Валюта,
	|	ЛимитыПоБюджетамОстатки.ДокументРезервирования КАК ДокументРезервирования,
	|	ЛимитыПоБюджетамОстатки.ЗарезервированоОборот КАК ЗарезервированоОборот,
	|	ЛимитыПоБюджетамОстатки.ЗаявленоОборот КАК Заявлено,
	|	ЛимитыПоБюджетамОстатки.ИсполненоОборот КАК Исполнено,
	|	ВЫБОР
	|		КОГДА ЛимитыПоБюджетамОстатки.ЗарезервированоОборот >= 0
	|			ТОГДА 0
	|		ИНАЧЕ ЛимитыПоБюджетамОстатки.ЗарезервированоОборот
	|	КОНЕЦ КАК Превышение
	|ИЗ
	|	РегистрНакопления.ЛимитыПоБюджетам.Обороты(
	|			,
	|			,
	|			,
	|			(Предназначение, ПериодЛимитирования, СтатьяБюджета, Аналитика1, Аналитика2, Аналитика3, Аналитика4, Аналитика5, Аналитика6, ЦФО, Проект, ДокументРезервирования, Валюта) В
	|				(ВЫБРАТЬ
	|					Таблица.Предназначение КАК Предназначение,
	|					Таблица.ПериодЛимитирования КАК ПериодЛимитирования,
	|					Таблица.СтатьяБюджета КАК СтатьяБюджета,
	|					Таблица.Аналитика1 КАК Аналитика1,
	|					Таблица.Аналитика2 КАК Аналитика2,
	|					Таблица.Аналитика3 КАК Аналитика3,
	|					Таблица.Аналитика4 КАК Аналитика4,
	|					Таблица.Аналитика5 КАК Аналитика5,
	|					Таблица.Аналитика6 КАК Аналитика6,
	|					Таблица.ЦФО КАК ЦФО,
	|					Таблица.Проект КАК Проект,
	|					Таблица.ДокументРезервирования КАК ДокументРезервирования,
	|					Таблица.Валюта КАК Валюта
	|				ИЗ
	|					ДвиженияЛимитыПоБюджетамИзменение КАК Таблица
	|				ГДЕ
	|					Таблица.ДокументРезервирования <> ЗНАЧЕНИЕ(Документ.ОперативныйПлан.ПустаяСсылка))) КАК ЛимитыПоБюджетамОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыЛимитирования КАК ПараметрыЛимитирования
	|		ПО ЛимитыПоБюджетамОстатки.Предназначение = ПараметрыЛимитирования.ВидБюджета.Предназначение
	|			И (НАЧАЛОПЕРИОДА(ЛимитыПоБюджетамОстатки.ПериодЛимитирования.ДатаНачала, ГОД) = ПараметрыЛимитирования.Период)
	|			И ЛимитыПоБюджетамОстатки.ПериодЛимитирования.Периодичность = ПараметрыЛимитирования.ПериодичностьЛимитирования
	|			И (ПараметрыЛимитирования.ИспользоватьЛимитирование = ИСТИНА)
	|ГДЕ
	|	ЛимитыПоБюджетамОстатки.ЗарезервированоОборот < 0
	|		И ПараметрыЛимитирования.КонтрольРезервов = ЗНАЧЕНИЕ(Перечисление.РежимыКонтроляДокументов.Блокировать)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПроверкиКОбеспечению()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЛимитыПоБюджетамОстатки.Предназначение КАК Предназначение,
	|	ЛимитыПоБюджетамОстатки.ПериодЛимитирования КАК ПериодЛимитирования,
	|	ЛимитыПоБюджетамОстатки.ЦФО КАК ЦФО,
	|	ЛимитыПоБюджетамОстатки.Проект КАК Проект,
	|	ЛимитыПоБюджетамОстатки.СтатьяБюджета КАК СтатьяБюджета,
	|	ЛимитыПоБюджетамОстатки.Аналитика1 КАК Аналитика1,
	|	ЛимитыПоБюджетамОстатки.Аналитика2 КАК Аналитика2,
	|	ЛимитыПоБюджетамОстатки.Аналитика3 КАК Аналитика3,
	|	ЛимитыПоБюджетамОстатки.Аналитика4 КАК Аналитика4,
	|	ЛимитыПоБюджетамОстатки.Аналитика5 КАК Аналитика5,
	|	ЛимитыПоБюджетамОстатки.Аналитика6 КАК Аналитика6,
	|	ЛимитыПоБюджетамОстатки.Валюта КАК Валюта,
	|	ЛимитыПоБюджетамОстатки.ДокументРезервирования КАК ДокументРезервирования,
	|	ЛимитыПоБюджетамОстатки.ДокументПланирования КАК ДокументПланирования,
	|	ЛимитыПоБюджетамОстатки.КОбеспечениюОборот КАК КОбеспечениюОборот,
	|	ВЫБОР
	|		КОГДА ЛимитыПоБюджетамОстатки.КОбеспечениюОборот < 0
	|			ТОГДА ЛимитыПоБюджетамОстатки.КОбеспечениюОборот
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Превышение
	|ИЗ
	|	РегистрНакопления.ЛимитыПоБюджетам.Обороты(
	|			,
	|			,
	|			,
	|			(Предназначение, ПериодЛимитирования, СтатьяБюджета, Аналитика1, Аналитика2, Аналитика3, Аналитика4, Аналитика5, Аналитика6, ЦФО, Проект, ДокументРезервирования, ДокументПланирования, Валюта) В
	|				(ВЫБРАТЬ
	|					Таблица.Предназначение КАК Предназначение,
	|					Таблица.ПериодЛимитирования КАК ПериодЛимитирования,
	|					Таблица.СтатьяБюджета КАК СтатьяБюджета,
	|					Таблица.Аналитика1 КАК Аналитика1,
	|					Таблица.Аналитика2 КАК Аналитика2,
	|					Таблица.Аналитика3 КАК Аналитика3,
	|					Таблица.Аналитика4 КАК Аналитика4,
	|					Таблица.Аналитика5 КАК Аналитика5,
	|					Таблица.Аналитика6 КАК Аналитика6,
	|					Таблица.ЦФО КАК ЦФО,
	|					Таблица.Проект КАК Проект,
	|					Таблица.ДокументРезервирования КАК ДокументРезервирования,
	|					Таблица.ДокументПланирования КАК ДокументПланирования,
	|					Таблица.Валюта КАК Валюта
	|				ИЗ
	|					ДвиженияЛимитыПоБюджетамИзменение КАК Таблица
	|				ГДЕ
	|					Таблица.ДокументПланирования ССЫЛКА Документ.ЗаявкаНаКорректировкуЛимитов)) КАК ЛимитыПоБюджетамОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыЛимитирования КАК ПараметрыЛимитирования
	|		ПО ЛимитыПоБюджетамОстатки.Предназначение = ПараметрыЛимитирования.ВидБюджета.Предназначение
	|			И (НАЧАЛОПЕРИОДА(ЛимитыПоБюджетамОстатки.ПериодЛимитирования.ДатаНачала, ГОД) = ПараметрыЛимитирования.Период)
	|			И ЛимитыПоБюджетамОстатки.ПериодЛимитирования.Периодичность = ПараметрыЛимитирования.ПериодичностьЛимитирования
	|			И (ПараметрыЛимитирования.ИспользоватьЛимитирование = ИСТИНА)
	|ГДЕ
	|	ЛимитыПоБюджетамОстатки.КОбеспечениюОборот < 0";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

