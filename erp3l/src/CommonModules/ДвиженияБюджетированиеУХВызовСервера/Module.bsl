#Область ПрограммныйИнтерфейс
// Функция возвращает структуру видов аналитик статьи бюджета.
//
// Параметры:
//  СтатьяБюджета	 - 	 СправочникСсылка.СтатьиДвиженияДенежныхСредств, СправочникСсылка.СтатьиДоходовИРасходов, 
// СправочникСсылка.СтатьиДвиженияРесурсов- статья бюджета, для которой необходимо получить аналитики
//  ТолькоЛимитируемыеАналитики - Булево - Если Истина, то возвращаются только лимитируемые аналитики. Значение по умолчанию Ложь
//
// Возвращаемое значение:
//  Структура - Структура, содержащая виды аналитик.
//
Функция ВидыАналитикСтатьи(СтатьяБюджета, ТолькоЛимитируемыеАналитики = Ложь) Экспорт
	
	ВидыАналитик = Новый Структура;
	Для Сч = 1 по АналитикиСтатейБюджетовУХКлиентСервер.КоличествоАналитикСтатьи() Цикл
		
		ВидыАналитик.Вставить("ВидАналитики" + Сч);
		ВидыАналитик.Вставить("ВидАналитики" + Сч + "Наименование");
		ВидыАналитик.Вставить("ВидАналитики" + Сч + "ТипЗначения");
		ВидыАналитик.Вставить("ВидАналитики" + Сч + "ТипыВладельца");
		
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(СтатьяБюджета) Тогда
		Возврат ВидыАналитик;
	КонецЕсли;
	
	СтатьяВМассиве = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтатьяБюджета);
	ОписаниеВидовАналитик = ДвиженияБюджетированиеУХ.ВидыАналитикСтатей(СтатьяВМассиве,,ТолькоЛимитируемыеАналитики);
	
	Если ЗначениеЗаполнено(ОписаниеВидовАналитик) Тогда
		ВидыАналитик = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ОписаниеВидовАналитик[0]);
	КонецЕсли;
	
	Возврат ВидыАналитик;
	
КонецФункции

// Функция возвращает Функциональные направления, статьи которых доступны для данного ЦФО.
//
// Параметры:
//  ЦФО	 - 	СправочникСсылка.Организации - ЦФО, для которого определяются функциональные направления.
// 
// Возвращаемое значение:
//  Массив - массив элементов
//
Функция ФункциональныеНаправленияЦФО(Знач ЦФО) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ЦФО) Тогда
		Возврат ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Справочники.ФункциональныеНаправления.ПустаяСсылка());
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФункциональныеНаправления.Ссылка
	|ИЗ
	|	Справочник.ФункциональныеНаправления КАК ФункциональныеНаправления
	|ГДЕ
	|	ФункциональныеНаправления.Ссылка В ИЕРАРХИИ
	|			(ВЫБРАТЬ
	|				Организации.ФункциональноеНаправление
	|			ИЗ
	|				Справочник.Организации КАК Организации
	|			ГДЕ
	|				Организации.Ссылка = &Ссылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.ФункциональныеНаправления.ПустаяСсылка)";
	Запрос.УстановитьПараметр("Ссылка", ЦФО);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти