#Область СлужебныйПрограммныйИнтерфейс
//++ Локализация

//++ НЕ УТ

Функция ИменаРеквизитовГОЗ() Экспорт
	СтруктураРеквизитов = "ВариантПлатежаГОЗ, ВыполненыОбязательстваПоДоговоруГОЗ, ГосударственныйКонтракт, Дата, Номер, 
		|Сумма, ДоговорСУчастникомГОЗ, ДоговорСсылка, КонтрагентЮрФизЛицо, КонтрактСЗаказчиком, 
		|ОплатаРасходовПоТарифамСГосрегулированием, ПлатежиПо275ФЗ, ПоддержкаПлатежей275ФЗ, ТипДоговора, ТипПлатежаФЗ275,
		|Организация, ОрганизацияПолучатель, СуммаТраншей, ХарактерДоговора, АдресПодтверждающихДокументовВоВременномХранилище";
	Возврат СтруктураРеквизитов
КонецФункции

// Параметры:
// 	Форма - ФормаКлиентскогоПриложения
//
Процедура УправлениеНастройкойГОЗ(Форма) Экспорт
	Форма.ВариантПлатежаГОЗ = ИнициализироватьВариантПлатежаГОЗ(Форма.Объект.ПлатежиПо275ФЗ,
		Форма.Объект.ДоговорСУчастникомГОЗ,
		Форма.Объект.ОплатаРасходовПоТарифамСГосрегулированием);
	Объект = Форма.Объект;
	
	СЗаказчиком =
		Объект.ТипДоговора = ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СПокупателем")
		//++ НЕ УТКА
		Или Объект.ТипДоговора = ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СДавальцем")
		//-- НЕ УТКА
		;
	
	СИсполнителем =
		Объект.ТипДоговора = ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СПереработчиком")
		Или Объект.ТипДоговора = ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СПоставщиком");
		
	ПоддержкаПлатежей275ФЗ = Форма.ПоддержкаПлатежей275ФЗ;
	
	Форма.Элементы.ИдентификаторГосКонтракта.Видимость = НЕ Форма.ПоддержкаПлатежей275ФЗ
		И СЗаказчиком;
			
	Форма.Элементы.ИностранныйИсполнительВУтвержденномПеречнеГОЗ.Видимость =
		(СИсполнителем
			И Форма.Объект.ДоговорСУчастникомГОЗ
			И Форма.КонтрагентЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицоНеРезидент"));
	
	Форма.Элементы.ЛокализацияГруппаНастроитьГОЗ.Видимость = ПоддержкаПлатежей275ФЗ И (СЗаказчиком Или СИсполнителем);
КонецПроцедуры

Функция ИнициализироватьВариантПлатежаГОЗ(ПлатежиПо275ФЗ, ДоговорСУчастникомГОЗ, ОплатаРасходовПоТарифамСГосрегулированием) Экспорт
	ВариантПлатежаГОЗ = 0;
	Если ПлатежиПо275ФЗ И ДоговорСУчастникомГОЗ Тогда
		ВариантПлатежаГОЗ = 1;
	ИначеЕсли ОплатаРасходовПоТарифамСГосрегулированием Тогда
		ВариантПлатежаГОЗ = 2;
	КонецЕсли;
	Возврат ВариантПлатежаГОЗ
КонецФункции

//-- НЕ УТ

//-- Локализация

#КонецОбласти
