
#Область СлужебныйПрограммныйИнтерфейс

Процедура СоздатьРеквизитФормыПереопределенныеСобытияЭлементов(Форма) экспорт
	
	ИмяРеквизита = "ПереопределенныеСобытияЭлементов";
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, ИмяРеквизита) Тогда
		Возврат;
	КонецЕсли;
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(ИмяРеквизита, Новый ОписаниеТипов("ТаблицаЗначений")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ИмяЭлемента", ОбщегоНазначения.ОписаниеТипаСтрока(100), ИмяРеквизита));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ИмяСобытия", ОбщегоНазначения.ОписаниеТипаСтрока(100), ИмяРеквизита));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ОбработчикСобытия", ОбщегоНазначения.ОписаниеТипаСтрока(100), ИмяРеквизита));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ПредыдущийУстановлен", Новый ОписаниеТипов("Булево"), ИмяРеквизита));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ОбработчикСобытияПредыдущий", ОбщегоНазначения.ОписаниеТипаСтрока(100), ИмяРеквизита));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Параметры", ОбщегоНазначения.ОписаниеТипаСтрока(100), ИмяРеквизита));
	
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
КонецПроцедуры

Процедура ПереопределитьСобытиеЭлементаФормы(Форма, Элемент, ИмяСобытия, ИмяОбработчика) экспорт
	
	СоздатьРеквизитФормыПереопределенныеСобытияЭлементов(Форма);
	
	Строка = Форма.ПереопределенныеСобытияЭлементов.Добавить();
	Строка.ИмяЭлемента = Элемент.Имя;
	Строка.ИмяСобытия = ИмяСобытия;
	Строка.ОбработчикСобытия = ИмяОбработчика;
	
	Строка.ОбработчикСобытияПредыдущий = Элемент.ПолучитьДействие(ИмяСобытия);
	Строка.ПредыдущийУстановлен = ЗначениеЗаполнено(Строка.ОбработчикСобытияПредыдущий);
	
	Если ИмяСобытия = "ПриИзменении" Тогда
		Строка.Параметры = "(Элемент);";
	КонецЕсли;
	
	Элемент.УстановитьДействие(ИмяСобытия, ИмяОбработчика);
	
КонецПроцедуры

#КонецОбласти

