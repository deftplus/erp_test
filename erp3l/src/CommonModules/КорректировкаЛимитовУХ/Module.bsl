
#Область СлужебныйПрограммныйИнтерфейс
// Процедура выполняет оповещение ответственных по документам заявка на корректировку и корректировка лимитов,
// которые были оформлены по документу, проведение которого отменяется.
Процедура ОповещениеПоДокументамОбеспеченнымКорректировкойЛимитов(ДокументСсылка, Отказ) Экспорт
	
	Если Отказ = Истина Тогда
		// Если уже отказ, то оповещения не выполняем
		Возврат;
	КонецЕсли;
	
	ТипДокумента = ТипЗнч(ДокументСсылка);
	Если НЕ Метаданные.ОпределяемыеТипы.ОснованияДокументаЗаявкаНаКорректировкуЛимитов.Тип.СодержитТип(ТипДокумента) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументДляОбеспечения", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаявкаНаКорректировкуЛимитов.Ответственный КАК Ответственный,
	|	ЗаявкаНаКорректировкуЛимитов.Ссылка КАК Документ
	|ИЗ
	|	Документ.ЗаявкаНаКорректировкуЛимитов КАК ЗаявкаНаКорректировкуЛимитов
	|ГДЕ
	|	ЗаявкаНаКорректировкуЛимитов.Проведен = ИСТИНА
	|	И ЗаявкаНаКорректировкуЛимитов.Основание = &ДокументДляОбеспечения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаЛимитов.Ответственный,
	|	КорректировкаЛимитов.Ссылка
	|ИЗ
	|	Документ.КорректировкаЛимитов КАК КорректировкаЛимитов
	|ГДЕ
	|	КорректировкаЛимитов.ДокументДляОбеспечения = &ДокументДляОбеспечения
	|	И КорректировкаЛимитов.Проведен = ИСТИНА
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ответственный,
	|	Документ
	|ИТОГИ ПО
	|	Ответственный
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаПользователь = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПользователь.Следующий() Цикл
		
		ДопПараметры = Новый Структура;
		ДопПараметры.Вставить("Ссылка", ДокументСсылка);
		
		ДокументыОбеспечения = Новый Массив;
		
		ВыборкаДокументы = ВыборкаПользователь.Выбрать();
		Пока ВыборкаДокументы.Следующий() Цикл
			ДокументыОбеспечения.Добавить(ВыборкаДокументы.Документ);
		КонецЦикла;
		
		Отступ = "							";
		ДопПараметры.Вставить("ДокументыОбеспечения", Отступ + СтрСоединить(ДокументыОбеспечения, ","+Символы.ПС+Отступ));
		
		ВстраиваниеОПКПереопределяемый.ОповеститьПользователейОбОтменеОбеспеченногоКорректировкойДокумента(
			ДокументСсылка, ВыборкаПользователь.Ответственный, ДопПараметры);
		
	КонецЦикла;
	
КонецПроцедуры
#КонецОбласти

