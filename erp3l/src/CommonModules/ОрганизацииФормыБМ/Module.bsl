////////////////////////////////////////////////////////////////////////////////////////
// Модуль используется для доформирования программно элементов формы 
// справочника Организации.
////////////////////////////////////////////////////////////////////////////////////////

Процедура НастроитьФормуОбъектаУчета(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	// Добавление элементов на форму.
	ГруппаМСФО = ДобавитьГруппуЭлементов(Форма, "ГруппаМСФО", Форма, ГруппировкаПодчиненныхЭлементовФормы.Вертикальная, НСтр("ru = 'МСФО'"), Истина);
	ДобавитьЭлементФормы(Форма, "ВалютаПредоставленияОтчетности",		 ГруппаМСФО, "Объект.ВалютаПредоставленияОтчетности", 		, НСтр("ru = 'Валюта предоставления отчетности'"));
	ДобавитьЭлементФормы(Форма, "ПорогСущественностиВНА",				 ГруппаМСФО, "Объект.ПорогСущественностиВНА", 				, НСтр("ru = 'Порог существенности ВНА'"));
	ДобавитьЭлементФормы(Форма, "ИсточникДляЧистойЦеныПродажиЗапасов",	 ГруппаМСФО, "Объект.ИсточникДляЧистойЦеныПродажиЗапасов", 	, НСтр("ru = 'Источник для чистой цены продажи запасов'"));	
	
	// Настройка элементов формы.
	ГруппаМСФО.Поведение = ПоведениеОбычнойГруппы.Свертываемая;
	ГруппаМСФО.ОтображениеУправления = ОтображениеУправленияОбычнойГруппы.Картинка;
	Форма.Элементы.Переместить(ГруппаМСФО, Форма, Форма.Элементы.ГруппаПодписи);
	
КонецПроцедуры

// Размещает на форме в группе ГруппаРодительВход элемент с  нименованием 
// НаименованиеЭлементаВход, заголовком ЗаголовокВход и типом
// элемента формы ТипЭлементаВход. По умолчанию заголовок пустой. Подключает его к 
// реквизиту НаименованиеРеквизитаВход. Возвращает новый элемент формы.
Функция ДобавитьЭлементФормы(Форма, НаименованиеЭлементаВход, ГруппаРодительВход, НаименованиеРеквизитаВход, ТипЭлементаВход = Неопределено, ЗаголовокВход = "")
	Если ТипЭлементаВход = Неопределено Тогда
		ТипЭлементаРабочий = Тип("ПолеФормы");
	Иначе	
		ТипЭлементаРабочий = ТипЭлементаВход;
	КонецЕсли;
	НовыйЭлемент = Форма.Элементы.Вставить(НаименованиеЭлементаВход, ТипЭлементаРабочий, ГруппаРодительВход);
	НовыйЭлемент.ПутьКДанным = НаименованиеРеквизитаВход;
	НовыйЭлемент.Заголовок = ЗаголовокВход;
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.РастягиватьПоГоризонтали = Ложь;
	Возврат НовыйЭлемент;
КонецФункции

// Добавляет группу элементов формы в родительскую группу ГруппаРодительВход с префиксом ПрефиксВход, 
// идентификатором ИдентификаторВход. ГруппировкаВход задаёт способ размещения (группировку) элементов.
Функция ДобавитьГруппуЭлементов(Форма, НаименованиеНовогоРеквизитаВход, ГруппаРодительВход, ГруппировкаВход, ЗаголовокВход = "", ОтображатьЗаголовокВход = Ложь)
	НовыйЭлемент									 = Форма.Элементы.Вставить(НаименованиеНовогоРеквизитаВход, Тип("ГруппаФормы"), ГруппаРодительВход);
	НовыйЭлемент.Вид								 = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлемент.ОтображатьЗаголовок				 = ОтображатьЗаголовокВход;
	НовыйЭлемент.Заголовок							 = ЗаголовокВход;
	НовыйЭлемент.Группировка						 = ГруппировкаВход;
	Возврат НовыйЭлемент;
КонецФункции