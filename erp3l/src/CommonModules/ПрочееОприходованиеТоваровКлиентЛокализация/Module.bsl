////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции, используемые в документе ПрочееОприходованиеТоваров.
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Функция ОбработкаВыбора(Форма, ВыбранноеЗначение, ИсточникВыбора) Экспорт

	ТребуетсяВызовСервера = Ложь;
	
	//++ Локализация
	
	Если ИсточникВыбора.ИмяФормы = "Справочник.ПартииТМЦВЭксплуатации.Форма.ФормаПодбора" Тогда
		
		Если ВыбранноеЗначение.Количество() > 0 Тогда
			ТребуетсяВызовСервера = Истина;
		КонецЕсли;
		
	КонецЕсли;	
	
	//-- Локализация
	
	Возврат ТребуетсяВызовСервера;
	
КонецФункции

Процедура ТоварыПартияПриИзменении(Форма) Экспорт
	
	//++ Локализация

	//++ НЕ УТ
	
	Элементы = Форма.Элементы;
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	Если Не ЗначениеЗаполнено(ТекущаяСтрока.Партия)
	 Или ТекущаяСтрока.Количество > 0 И ТекущаяСтрока.КоличествоУпаковок > 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИнвентарныйУчет =
		ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(ТекущаяСтрока.Партия, "ИнвентарныйУчет");
	
	Если ИнвентарныйУчет Тогда
		ТекущаяСтрока.Количество = 1;
		ТекущаяСтрока.КоличествоУпаковок = 1;
		ТекущаяСтрока.Упаковка = ПредопределенноеЗначение("Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка");
	КонецЕсли;

	//-- НЕ УТ
	
	Возврат; // в УТ обработчик пустой
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПриВыполненииКоманды(Команда, Форма) Экспорт

	
	//++ Локализация
	
	Элементы = Форма.Элементы;
	
	//++ НЕ УТ
	Если Команда.Имя = Элементы.ТоварыПодобратьТМЦВЭксплуатации.ИмяКоманды Тогда
		
		ПодобратьТМЦВЭксплуатации(Форма);
		
	КонецЕсли; 
	//-- НЕ УТ
	
	//-- Локализация
	
КонецПроцедуры

//++ Локализация

//++ НЕ УТ

Процедура ПодобратьТМЦВЭксплуатации(Форма)
	
	Объект = Форма.Объект;
	
	// &ЗамерПроизводительности
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина,
		"Документ.ПрочееОприходованиеТоваров.ФормаДокумента.Команда.ПодобратьТМЦВЭксплуатации");
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	ПараметрыФормы.Вставить("Дата", Объект.Дата);
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("Подразделение", Объект.Подразделение);
	ПараметрыФормы.Вставить("ТекущийРегистратор", Объект.Ссылка);
	
	ОткрытьФорму("Справочник.ПартииТМЦВЭксплуатации.Форма.ФормаПодбора", ПараметрыФормы, Форма);
	
КонецПроцедуры

//-- НЕ УТ

//-- Локализация

#КонецОбласти
