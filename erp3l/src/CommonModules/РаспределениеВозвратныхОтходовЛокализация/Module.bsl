
#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый документ.
//  Отказ - Булево - Признак проведения документа.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то проведение документа выполнено не будет.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект.
//  ДанныеЗаполнения - Произвольный - Значение, которое используется как основание для заполнения.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  ОбъектКопирования - ДокументОбъект.РаспределениеВозвратныхОтходов - Исходный документ, который является источником копирования.
//
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
КонецПроцедуры

// Добавляет команду создания документа "Авансовый отчет".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт


КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

//++ НЕ УТ
#Область ПроводкиРегУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	//++ Локализация
	
#Область ТекстВыпускСобственнойПродукции // (Дт 10 :: Кт 2Х)
	ТекстВыпускСобственнойПродукции =
	// Выпуск собственной продукции (Дт 10 :: Кт 2Х).
	"ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	Строки.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(Стоимости.СуммаБалансовая, Строки.СуммаБалансовая) КАК Сумма,
	|	ЕСТЬNULL(Стоимости.СуммаБалансоваяУУ, Строки.СуммаБалансоваяУУ) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе) КАК ВидСчетаДт,
	|	Строки.КорГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	Строки.КорСклад КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.КорПодразделениеАналитики КАК ПодразделениеДт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.КорСклад КАК СубконтоДт1,
	|	Строки.Номенклатура КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	Строки.Количество КАК КоличествоДт,
	|	ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ) КАК СуммаНУДт,
	|	ЕСТЬNULL(Стоимости.СуммаПР, Строки.СуммаПР) КАК СуммаПРДт,
	|	ЕСТЬNULL(Стоимости.СуммаВР, Строки.СуммаВР) КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчетаКт,
	|	Строки.ГруппаПродукции КАК АналитикаУчетаКт,
	|	Строки.ПодразделениеАналитики КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.ПодразделениеАналитики КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	Строки.ГруппаПродукции КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ) КАК СуммаНУКт,
	|	ЕСТЬNULL(Стоимости.СуммаПР, Строки.СуммаПР) КАК СуммаПРКт,
	|	ЕСТЬNULL(Стоимости.СуммаВР, Строки.СуммаВР) КАК СуммаВРКт,
	|	""Выпуск собственной продукции"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов КАК Операция
	|		ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСтроки КАК Строки
	|		ПО Операция.Ссылка = Строки.Ссылка
	|			И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимости КАК Стоимости
	|		ПО Строки.Ссылка = Стоимости.Ссылка
	|			И Строки.ВидДвижения = Стоимости.ВидДвижения
	|			И Строки.Номенклатура = Стоимости.Номенклатура
	|			И Строки.КорНоменклатура = Стоимости.КорНоменклатура
	|			И Строки.Склад = Стоимости.Склад
	|			И Строки.КорСклад = Стоимости.КорСклад
	|			И Строки.ГруппаФинансовогоУчета = Стоимости.ГруппаФинансовогоУчета
	|			И Строки.КорГруппаФинансовогоУчета = Стоимости.КорГруппаФинансовогоУчета
	|			И Строки.НаправлениеДеятельности = Стоимости.НаправлениеДеятельности
	|			И Строки.КорНаправлениеДеятельности = Стоимости.КорНаправлениеДеятельности
	|			И Строки.ТипЗапасов = Стоимости.ТипЗапасов
	|			И Строки.Контрагент = Стоимости.Контрагент
	|			И Строки.СтатьяРасходов = Стоимости.СтатьяРасходов
	|			И Строки.АналитикаРасходов = Стоимости.АналитикаРасходов
	|			И Строки.РазделУчета = Стоимости.РазделУчета
	|			И Строки.ГруппаПродукции = Стоимости.ГруппаПродукции
	|			И Строки.КорГруппаПродукции = Стоимости.КорГруппаПродукции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрКорСклады
	|		ПО СпрКорСклады.Ссылка = Строки.КорСклад
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитики
	|			ПО КлючиАналитики.Ссылка = Операция.АналитикаУчетаНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК СкладЦеховаяКладовая
	|			ПО КлючиАналитики.СкладскаяТерритория = СкладЦеховаяКладовая.Ссылка
	|				И СкладЦеховаяКладовая.ЦеховаяКладовая
	|ГДЕ
	|	НЕ СпрКорСклады.ЦеховаяКладовая";
#КонецОбласти
	
#Область ТекстВыпускВозвратныхОтходов // (Дт 2Х :: Кт 2Х)
	ТекстВыпускВозвратныхОтходов =
	// Выпуск возвратных отходов (Дт 2Х :: Кт 2Х).
	"ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Операция.Организация КАК Организация,
	|	Строки.ИдентификаторСтроки КАК ИдентификаторСтроки,

	|	-Строки.СуммаБалансовая КАК Сумма,
	|	-Строки.СуммаБалансоваяУУ КАК СуммаУУ,

	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчетаДт,
	|	КорСтроки.ГруппаПродукции КАК АналитикаУчетаДт,
	|	КорСтроки.ПодразделениеАналитики КАК МестоУчетаДт,

	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	КорСтроки.ПодразделениеАналитики КАК ПодразделениеДт,
	|	КорСтроки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,

	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты) КАК СубконтоДт1,
	|	КорСтроки.Номенклатура КАК СубконтоДт2,
	|	КорСтроки.ГруппаПродукции КАК СубконтоДт3,

	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	-Строки.СуммаНУ КАК СуммаНУДт,
	|	-Строки.СуммаПР КАК СуммаПРДт,
	|	-Строки.СуммаВР КАК СуммаВРДт,
	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчетаКт,
	|	Строки.ГруппаПродукции КАК АналитикаУчетаКт,
	|	Строки.ПодразделениеАналитики КАК МестоУчетаКт,

	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.ПодразделениеАналитики КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,

	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	Строки.ГруппаПродукции КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,

	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	-Строки.СуммаНУ КАК СуммаНУКт,
	|	-Строки.СуммаПР КАК СуммаПРКт,
	|	-Строки.СуммаВР КАК СуммаВРКт,
	|	""Выпуск возвратных отходов"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов КАК Операция
	|		ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСтроки КАК Строки
	|		ПО Операция.Ссылка = Строки.Ссылка
	|		И Строки.Количество < 0
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСтроки КАК КорСтроки
	|		ПО Строки.Ссылка = КорСтроки.Ссылка
	|		И Строки.ИдентификаторФинЗаписи = КорСтроки.ИдентификаторФинЗаписи
	|		И Строки.НастройкаХозяйственнойОперации = КорСтроки.НастройкаХозяйственнойОперации
	|		И КорСтроки.Количество >= 0
	|
	|ГДЕ
	|	Строки.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|	И (Строки.ПодразделениеАналитики <> Строки.КорПодразделениеАналитики
	|		ИЛИ Строки.НаправлениеДеятельности <> Строки.КорНаправлениеДеятельности
	|		ИЛИ Строки.ГруппаПродукции <> Строки.КорГруппаПродукции)
	|";
#КонецОбласти
	
#Область ТекстРаспределениеПоРасходам // (Дт Счет учета возвратных отходов :: Кт СтатьяРасходов.СчетУчета)
	// Списание продукции на статьи расходов (Дт Счет учета возвратных отходов :: Кт СтатьяРасходов.СчетУчета).
	ТекстРаспределениеПоРасходам =
	"ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	ТаблицаРасходов.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|
	|	СуммыДокументовВВалютахУчета.СуммаБезНДСРегл КАК Сумма,
	|	СуммыДокументовВВалютахУчета.СуммаБезНДСУпр КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство)				 КАК ВидСчетаДт,
	|	ВЫБОР
	|		КОГДА &АналитическийУчетПоГруппамПродукции
	|			И НЕ КлючиАналитики.Номенклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА КлючиАналитики.Номенклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																 КАК АналитикаУчетаДт,
	|	ЕСТЬNULL(СкладЦеховаяКладовая.Подразделение, Операция.Подразделение) КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ЕСТЬNULL(СкладЦеховаяКладовая.Подразделение, Операция.Подразделение) КАК ПодразделениеДт,
	|	КлючиАналитики.Назначение.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты) КАК СубконтоДт1,
	|	КлючиАналитики.Номенклатура КАК СубконтоДт2,
	|	ВЫБОР
	|		КОГДА &АналитическийУчетПоГруппамПродукции
	|			И НЕ КлючиАналитики.Номенклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА КлючиАналитики.Номенклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	ТаблицаРасходов.Количество КАК КоличествоДт,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СпрСтатьиРасходов.ПринятиеКНалоговомуУчету, ИСТИНА)
	|			ТОГДА СуммыДокументовВВалютахУчета.СуммаБезНДСРегл
	|		ИНАЧЕ -СуммыДокументовВВалютахУчета.СуммаБезНДСРегл
	|	КОНЕЦ КАК СуммаНУДт,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СпрСтатьиРасходов.ПринятиеКНалоговомуУчету, ИСТИНА)
	|			ТОГДА 0
	|		ИНАЧЕ СуммыДокументовВВалютахУчета.СуммаБезНДСРегл
	|	КОНЕЦ КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	ТаблицаРасходов.СтатьяРасходов КАК АналитикаУчетаКт,
	|	ТаблицаРасходов.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ТаблицаРасходов.Подразделение КАК ПодразделениеКт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	ТаблицаРасходов.СтатьяРасходов КАК СубконтоКт1,
	|	ВЫБОР
	|		КОГДА СпрСтатьиРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|			ТОГДА ТаблицаРасходов.АналитикаРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СубконтоКт2,
	|	ВЫБОР
	|		КОГДА СпрСтатьиРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|			ТОГДА ВЫБОР
	|				КОГДА ОбъектыСтроительства.СпособСтроительства <> ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка) 
	|					ТОГДА ОбъектыСтроительства.СпособСтроительства
	|				ИНАЧЕ
	|					ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный)
	|			КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СпрСтатьиРасходов.ПринятиеКНалоговомуУчету, ИСТИНА)
	|			ТОГДА СуммыДокументовВВалютахУчета.СуммаБезНДСРегл
	|		ИНАЧЕ -СуммыДокументовВВалютахУчета.СуммаБезНДСРегл
	|	КОНЕЦ КАК СуммаНУКт,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СпрСтатьиРасходов.ПринятиеКНалоговомуУчету, ИСТИНА)
	|			ТОГДА 0
	|		ИНАЧЕ СуммыДокументовВВалютахУчета.СуммаБезНДСРегл
	|	КОНЕЦ КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|
	|	""Выпуск продукции по фиксированной стоимости"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов КАК Операция
	|			ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов.ПрочиеРасходы КАК ТаблицаРасходов
	|			ПО ТаблицаРасходов.Ссылка = Операция.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|			ПО ТаблицаРасходов.Ссылка = СуммыДокументовВВалютахУчета.Регистратор
	|			И ТаблицаРасходов.ИдентификаторСтроки = СуммыДокументовВВалютахУчета.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СпрСтатьиРасходов
	|			ПО ТаблицаРасходов.СтатьяРасходов = СпрСтатьиРасходов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|			ПО ОбъектыСтроительства.Ссылка = ТаблицаРасходов.АналитикаРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитики
	|			ПО КлючиАналитики.Ссылка = Операция.АналитикаУчетаНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК СкладЦеховаяКладовая
	|			ПО КлючиАналитики.СкладскаяТерритория = СкладЦеховаяКладовая.Ссылка
	|				И СкладЦеховаяКладовая.ЦеховаяКладовая
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ТаблицаРасходов.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
	|	И НЕ ТаблицаРасходов.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|";
#КонецОбласти
	
#Область ТекстРаспределениеПоПрочимАктивамПассивам // (Дт Счет учета возвратных отходов :: Кт Документ.СчетУчета)
	// Списание продукции на прочие активы/пассивы (Дт Счет учета возвратных отходов :: Кт Документ.СчетУчета).
	ТекстРаспределениеПоПрочимАктивамПассивам =
	"ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	ТаблицаРасходов.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|
	|	СуммыДокументовВВалютахУчета.СуммаБезНДСРегл КАК Сумма,
	|	СуммыДокументовВВалютахУчета.СуммаБезНДСУпр КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство)				 КАК ВидСчетаДт,
	|	ВЫБОР
	|		КОГДА &АналитическийУчетПоГруппамПродукции
	|			И НЕ КлючиАналитики.Номенклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА КлючиАналитики.Номенклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																 КАК АналитикаУчетаДт,
	|	ЕСТЬNULL(СкладЦеховаяКладовая.Подразделение, Операция.Подразделение) КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ЕСТЬNULL(СкладЦеховаяКладовая.Подразделение, Операция.Подразделение) КАК ПодразделениеДт,
	|	КлючиАналитики.Назначение.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты) КАК СубконтоДт1,
	|	КлючиАналитики.Номенклатура КАК СубконтоДт2,
	|	ВЫБОР
	|		КОГДА &АналитическийУчетПоГруппамПродукции
	|			И НЕ КлючиАналитики.Номенклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА КлючиАналитики.Номенклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	ТаблицаРасходов.Количество КАК КоличествоДт,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СпрСтатьиРасходов.ПринятиеКНалоговомуУчету, ИСТИНА)
	|			ТОГДА СуммыДокументовВВалютахУчета.СуммаБезНДСРегл
	|		ИНАЧЕ -СуммыДокументовВВалютахУчета.СуммаБезНДСРегл
	|	КОНЕЦ КАК СуммаНУДт,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СпрСтатьиРасходов.ПринятиеКНалоговомуУчету, ИСТИНА)
	|			ТОГДА 0
	|		ИНАЧЕ СуммыДокументовВВалютахУчета.СуммаБезНДСРегл
	|	КОНЕЦ КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ТаблицаРасходов.Подразделение КАК ПодразделениеКт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ТаблицаРасходов.НастройкаСчетовУчета.СчетУчета КАК СчетКт,
	|	ТаблицаРасходов.НастройкаСчетовУчета.Субконто1 КАК СубконтоКт1,
	|	ТаблицаРасходов.НастройкаСчетовУчета.Субконто2 КАК СубконтоКт2,
	|	ТаблицаРасходов.НастройкаСчетовУчета.Субконто3 КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СпрСтатьиРасходов.ПринятиеКНалоговомуУчету, ИСТИНА)
	|			ТОГДА СуммыДокументовВВалютахУчета.СуммаБезНДСРегл
	|		ИНАЧЕ -СуммыДокументовВВалютахУчета.СуммаБезНДСРегл
	|	КОНЕЦ КАК СуммаНУКт,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СпрСтатьиРасходов.ПринятиеКНалоговомуУчету, ИСТИНА)
	|			ТОГДА 0
	|		ИНАЧЕ СуммыДокументовВВалютахУчета.СуммаБезНДСРегл
	|	КОНЕЦ КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|
	|	""Выпуск продукции по фиксированной стоимости"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов КАК Операция
	|			ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов.ПрочиеРасходы КАК ТаблицаРасходов
	|			ПО ТаблицаРасходов.Ссылка = Операция.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|			ПО ТаблицаРасходов.Ссылка = СуммыДокументовВВалютахУчета.Регистратор
	|			И ТаблицаРасходов.ИдентификаторСтроки = СуммыДокументовВВалютахУчета.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СпрСтатьиРасходов
	|			ПО ТаблицаРасходов.СтатьяРасходов = СпрСтатьиРасходов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|			ПО ОбъектыСтроительства.Ссылка = ТаблицаРасходов.АналитикаРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитики
	|			ПО КлючиАналитики.Ссылка = Операция.АналитикаУчетаНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК СкладЦеховаяКладовая
	|			ПО КлючиАналитики.СкладскаяТерритория = СкладЦеховаяКладовая.Ссылка
	|				И СкладЦеховаяКладовая.ЦеховаяКладовая
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ТаблицаРасходов.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|	И НЕ ТаблицаРасходов.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка)
	|";
#КонецОбласти

#Область ТекстВключениеИсключениеНДСВСтоимость // (Дт 20 :: Кт 19.03)

	ТекстВключениеИсключениеНДСВСтоимость = "
	|ВЫБРАТЬ //// Включение/исключение НДС в стоимость (Дт 20 :: Кт 19.03) @НДСпредъявленный
	|	Партии.Ссылка КАК Ссылка,
	|	Партии.Период КАК Период,
	|	Партии.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ВЫБОР КОГДА Партии.ВключениеНДСВСтоимость ТОГДА
	|		Партии.НДСРегл
	|	ИНАЧЕ
	|		-Партии.НДСРегл
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР КОГДА Партии.ВключениеНДСВСтоимость ТОГДА
	|		Партии.НДСУпр
	|	ИНАЧЕ
	|		-Партии.НДСУпр
	|	КОНЕЦ КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчетаДт,
	|	ВЫБОР КОГДА Партии.ВключениеНДСВСтоимость ТОГДА
	|		Партии.КорГруппаПродукции
	|	ИНАЧЕ
	|		Партии.ГруппаПродукции
	|	КОНЕЦ КАК АналитикаУчетаДт,
	|	Партии.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Партии.Подразделение КАК ПодразделениеДт,
	|	Партии.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	ВЫБОР КОГДА Партии.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Работы)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты) КОНЕЦ КАК СубконтоДт1,
	|	Партии.Номенклатура КАК СубконтоДт2,
	|	ВЫБОР КОГДА Партии.ВключениеНДСВСтоимость ТОГДА
	|		Партии.КорГруппаПродукции
	|	ИНАЧЕ
	|		Партии.ГруппаПродукции
	|	КОНЕЦ КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСпоПриобретеннымЦенностям) КАК ВидСчетаКт,
	|	Партии.ВидДеятельностиНДС                                              КАК АналитикаУчетаКт,
	|	Партии.ВидЦенности                                                     КАК МестоУчетаКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЕСТЬNULL(ВводСобственныхСредств.НаправлениеДеятельности, Партии.НаправлениеДеятельности) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Партии.Контрагент КАК СубконтоКт1,
	|	Партии.ДокументПоступления КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Включение/исключение НДС в стоимость"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РаспределениеВозвратныхОтходов КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		Партии КАК Партии
	|	ПО
	|		ДокументыКОтражению.Ссылка = Партии.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТВводСобственныхСредств КАК ВводСобственныхСредств
	|	ПО Партии.Ссылка = ВводСобственныхСредств.Ссылка
	|		И Партии.ДокументПоступления = ВводСобственныхСредств.СчетФактура
	|
	|ГДЕ
	|	(Партии.ВключениеНДСВСтоимость ИЛИ Партии.ИсключениеНДСИзСтоимости)
	|";
	
#КонецОбласти

	ТекстыОтражения = Новый Массив;
	ТекстыОтражения.Добавить(ТекстВыпускСобственнойПродукции);
	ТекстыОтражения.Добавить(ТекстВыпускВозвратныхОтходов);
	ТекстыОтражения.Добавить(ТекстРаспределениеПоРасходам);
	ТекстыОтражения.Добавить(ТекстРаспределениеПоПрочимАктивамПассивам);
	ТекстыОтражения.Добавить(ТекстВключениеИсключениеНДСВСтоимость);
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());

	//-- Локализация
	Возврат "";
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	//++ Локализация
	//-- Локализация
	Возврат "";
	
КонецФункции

#КонецОбласти
//-- НЕ УТ

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация
	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

//++ Локализация

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	МассивТекстовЗапроса = Новый Массив;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Дата КАК Период,
	|	&Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ) КАК ДатаОтражения";
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Дата КАК Период,
	|	&Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ), ДЕНЬ) КАК ДатаОтражения";
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		Документы.РаспределениеВозвратныхОтходов.ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	ТекстЗапроса = ДоходыИРасходыСервер.ДополнитьТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Ложь, "Дата");
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении(Истина));
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции
//-- Локализация
#КонецОбласти

#КонецОбласти
