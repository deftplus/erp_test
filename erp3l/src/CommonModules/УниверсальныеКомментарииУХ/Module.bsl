// Процедуры и функции работы с универсальными комментариями
//////////////////////////////////////////////////////////////

// Создать таблицу предмета комментирования
//
Функция СоздатьТаблицуПредметаКомментирования() Экспорт
	Перем Таб, Массив;
	
	Массив = Новый Массив;
	
	Таб = Новый ТаблицаЗначений;
	
	Массив.Добавить(Тип("ПланВидовХарактеристикСсылка.ТипыРеквизитовКомментариев"));
	Таб.Колонки.Добавить("ТипРеквизита", Новый ОписаниеТипов(Массив));
	Таб.Колонки.Добавить("Значение");
	Массив.Очистить();
	Массив.Добавить(Тип("Число"));
	Таб.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов(Массив, Новый КвалификаторыЧисла(12,2)));
	
	Возврат Таб;
КонецФункции

// Записываем комментарий и пакет файлов в регистр сведений УниверсальныеКомментарии
// Создаем сопуствующие записи в справочник Адрес, ПакетыФайлов
// и регистр сведений ЗначенияРеквизитовАдреса.
// Параметры:
//		РазделКомментариев - одно из предопределенных значений перечисления РазделыКомментариев.
//			Комментарии обрабатываются только в пределах одного раздела.
//		ОписаниеПредметаКомменитрования - хранилище значений, хранит таблицу значений следующего формата:
//			ТаблицаПредметаКомменитрования - таблица значений описывающая предмет комментирования. Колонки:
//				НомерСтроки - номер реквизита адреса по порядку использования,
//				ТипРеквизита - значение ПланВидовХарактеристикСсылка 
//					ТипыРеквизитовКомментариев, описывает тип реквизита,
//				Значение - значение реквизита.
//		ДатаКомментария - дата и время на которую будет сохранен комментария.
//		Автор - ссылка на пользователя оставившего комментарий.
//		Комментарий - строка с комментарием.
//		мХранимыхФайлов - массив объектов ОписаниеПередаваемогоФайла.
//		ТекстОшибки - если возникла ошибка, то в переменную возвращается ее описание.
// Возвращает:
// 		РегистрСведенийКлючЗаписи - операция прошла успешно, возвращаем ссылку на запись регистра комментария.
//		Неопределено - возникли ошибки, в ТекстОшибки записывает ее описание.
//
Функция ЗаписатьКомментарийПоТаблицеПредметаКомментирования(РазделКомментариев,
				ОписаниеПредметаКомменитрования, Комментарий, ДатаКомментария,
				Автор=Неопределено, мХранимыхФайлов=Неопределено,
				ТекстОшибки) Экспорт
	Ссылка = УниверсальныеКомментарииВызовСервераУХ.ПолучитьПредметКомментирования(
				РазделКомментариев, ОписаниеПредметаКомменитрования, ТекстОшибки);
	Если Ссылка = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат УниверсальныеКомментарииВызовСервераУХ.ЗаписатьКомментарий(
				Ссылка, Комментарий, ДатаКомментария, Автор, мХранимыхФайлов,
				ТекстОшибки);
КонецФункции

// Возвращает ссылку на справочник АдресаКомментариев для переаднных реквизитов
// адреса. Поиск осуществляется по регистру сведений ЗначенияРеквизитовАдреса.
// Если адрес не найден, то он созадется в справочнике и регистре сведений.
// Параметры:
//		РазделКомментариев - одно из предопределенных значений перечисления РазделыКомментариев.
//			Комментарии обрабатываются только в пределах одного раздела.
//		ТаблицаПредметаКомменитрования - таблица значений описывающая предмет комментирования. Колонки:
//				НомерСтроки - номер реквизита адреса по порядку использования,
//				ТипРеквизита - значение ПланВидовХарактеристикСсылка 
//					ТипыРеквизитовКомментариев, описывает тип реквизита,
//				Значение - значение реквизита.
//		ТекстОшибки - если возникла ошибка, то в переменную возвращается ее описание.
// Возвращает:
// 		Неопределено - возникли ошибки, в ТекстОшибки записывает ее описание.
//  	Ссылка на справочник ПредметыКомментирования.
//
Функция ПолучитьПредметКомментированияДляТаблицы(РазделКомментариев, ТаблицаПредметаКомменитрования, ТекстОшибки) Экспорт
	Перем Ссылка, ТекстЗапроса, ЧислоРеквизитов;
	
	ЧислоРеквизитов = ТаблицаПредметаКомменитрования.Количество();
	Если ЧислоРеквизитов = 0 Тогда
		ТекстОшибки = НСтр("ru = 'Нет реквизитов для сравнения!'");
		возврат Неопределено;
	КонецЕсли;
	
	// Вначале ищем в регистре сведений
	Запрос = Новый Запрос;
	ТекстЗапроса = "";
	Ном = 0;
	Для Каждого Стр Из ТаблицаПредметаКомменитрования Цикл
		Ном = Ном + 1;
		ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|	ЗначенияРеквизитов.ПредметКомментирования КАК ПредметКомментирования
			|" + ?(Ном = 1, "ПОМЕСТИТЬ ПодходящиеПредметыКомментирования " + Символы.ПС, "") + "
			|ИЗ
			|	РегистрСведений.ЗначенияРеквизитовПредметаКомментирования КАК ЗначенияРеквизитов
			|ГДЕ
			|	ЗначенияРеквизитов.НомерПоПорядку = &НомерПоПорядку" + Ном + "
			|	И ЗначенияРеквизитов.ТипРеквизита = &ТипРеквизита" + Ном + "
			|	И ЗначенияРеквизитов.ЗначениеРеквизита = &ЗначениеРеквизита" + Ном + "
			|	И ЗначенияРеквизитов.РазделКомментариев = &РазделКомментариев
			|	И НЕ ЗначенияРеквизитов.ПредметКомментирования.ПометкаУдаления
			|
			|" + ?(Ном < ЧислоРеквизитов, "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС, "") + "
			|";

		Запрос.УстановитьПараметр("ЗначениеРеквизита" + Ном, Стр.Значение);
		Запрос.УстановитьПараметр("НомерПоПорядку" + Ном, Стр.НомерСтроки);
		Запрос.УстановитьПараметр("ТипРеквизита" + Ном, Стр.ТипРеквизита);
		
	КонецЦикла;
	
	ТекстЗапроса = ТекстЗапроса + 
		"ИНДЕКСИРОВАТЬ ПО ПредметКомментирования
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПодходящиеПредметыКомментирования.ПредметКомментирования КАК Ссылка,
		|	СУММА(1) КАК КолСовпадений
		|ИЗ
		|	ПодходящиеПредметыКомментирования КАК ПодходящиеПредметыКомментирования
		|
		|СГРУППИРОВАТЬ ПО
		|	ПодходящиеПредметыКомментирования.ПредметКомментирования
		|
		|ИМЕЮЩИЕ
		|	СУММА(1) = &ЧислоРеквизитов";
				   
	Запрос.УстановитьПараметр("ЧислоРеквизитов", ЧислоРеквизитов);	
	Запрос.УстановитьПараметр("РазделКомментариев", РазделКомментариев);
	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	
	Ссылка = Неопределено;
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Ссылка = ВыборкаДетальныеЗаписи.Ссылка;
	КонецЕсли;

	// Если не нашли, то создаем
	Если Ссылка = Неопределено Тогда
		НачатьТранзакцию();
		Попытка
			ОбъектПредметКомментирования = Справочники.ПредметыКомментирования.СоздатьЭлемент();
			ОбъектПредметКомментирования.РазделКомментариев = РазделКомментариев;
			ОбъектПредметКомментирования.Наименование = ПредставлениеПредметаКомментированияДляТаблицы(РазделКомментариев, ТаблицаПредметаКомменитрования);
			ОбъектПредметКомментирования.Записать();
			
		    НаборЗаписей = РегистрыСведений.ЗначенияРеквизитовПредметаКомментирования.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.РазделКомментариев.Установить(РазделКомментариев);
			НаборЗаписей.Отбор.ПредметКомментирования.Установить(ОбъектПредметКомментирования.Ссылка);
			НаборЗаписей.Прочитать();
			
			Для Каждого Стр Из ТаблицаПредметаКомменитрования Цикл
				ЗначениеРеквизита = НаборЗаписей.Добавить();
				ЗначениеРеквизита.РазделКомментариев = РазделКомментариев;
				ЗначениеРеквизита.ПредметКомментирования = ОбъектПредметКомментирования.Ссылка;
				ЗначениеРеквизита.ТипРеквизита = Стр.ТипРеквизита;
				ЗначениеРеквизита.НомерПоПорядку = Стр.НомерСтроки;
				ЗначениеРеквизита.ЗначениеРеквизита = Стр.Значение;
			КонецЦикла;
			НаборЗаписей.Записать();
			
		ЗафиксироватьТранзакцию();
		Ссылка = ОбъектПредметКомментирования.Ссылка;
		Исключение
			ТекстОшибки = ОписаниеОшибки();
			ОтменитьТранзакцию();
		КонецПопытки;
	КонецЕсли;

	Возврат Ссылка;
КонецФункции
	
// Формируем текстовую строку представления для адреса комментария.
//		РазделКомментариев - одно из предопределенных значений перечисления РазделыКомментариев.
//		ТаблицаПредметаКомменитрования - таблица значений описывающая предмет комментирования. Колонки:
//			НомерСтроки - номер реквизита адреса по порядку использования,
//			ТипРеквизита - значение ПланВидовХарактеристикСсылка 
//				ТипыРеквизитовАдресаКомментария, описывает тип реквизита,
//			Значение - значение реквизита.
Функция ПредставлениеПредметаКомментированияДляТаблицы(РазделКомментариев, ТаблицаПредметаКомментирования)
	Перем Представление;
	
	ТаблицаПредметаКомментирования.Сортировать("НомерСтроки");
	
	Представление = Строка(РазделКомментариев);
	Для Каждого Стр Из ТаблицаПредметаКомментирования Цикл
		Представление = Представление + "," + СокрЛП(Лев(Стр.Значение,19)) ;
	КонецЦикла;
	Представление = Лев(Представление, СтрДлина(Представление)-1);
	Возврат Представление;
КонецФункции

// Формируем многострочное представление предмета комментирования
// В формате:
//	[Наименование реквизита] = [Значение реквизита]
//
Функция МногострочноеПредставлениеПредметаКомментирования(ПредметКомментирования) Экспорт
	Перем Представление_;
	
	Представление_ = "";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗначенияРеквизитовПредметаКомментирования.ЗначениеРеквизита,
		|	ЗначенияРеквизитовПредметаКомментирования.ТипРеквизита
		|ИЗ
		|	РегистрСведений.ЗначенияРеквизитовПредметаКомментирования КАК ЗначенияРеквизитовПредметаКомментирования
		|ГДЕ
		|	ЗначенияРеквизитовПредметаКомментирования.ПредметКомментирования = &ПредметКомментирования
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерПоПорядку";

	Запрос.УстановитьПараметр("ПредметКомментирования", ПредметКомментирования);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Представление_ = Представление_ + ВыборкаДетальныеЗаписи.ТипРеквизита.Наименование + "=" + ВыборкаДетальныеЗаписи.ЗначениеРеквизита + Символы.ПС;
	КонецЦикла;

	Если ЗначениеЗаполнено(Представление_) Тогда
		Представление_ = Лев(Представление_, СтрДлина(Представление_)-1);
	КонецЕсли;
	
	Возврат Представление_;
КонецФункции

// Помечаем на удаление предмет комментирования.
// Параметры:
//		РазделКомментариев - одно из предопределенных значений перечисления РазделыКомментариев.
//			Комментарии обрабатываются только в пределах одного раздела.
//		ТаблицаПредметаКомменитрования - таблица значений описывающая предмет комментирования. Колонки:
//			НомерСтроки - номер реквизита адреса по порядку использования,
//			ТипРеквизита - значение ПланВидовХарактеристикСсылка 
//				ТипыРеквизитовКомментариев, описывает тип реквизита,
//			Значение - значение реквизита.
//		ТекстОшибки - если возникла ошибка, то в переменную возвращается ее описание.// Возвращаемое значение:
//   Возвращает:
//		Истина - пометку установили.
//		Ложь - удалить не получилось, в переменной ТекстОшибки описание ошибки.
//
Функция ПометитьНаУдалениеПредметКомментирования(РазделКомментариев, ТаблицаПредметаКомменитирования, ТекстОшибки) Экспорт
	Перем ПредметКомментирования, Объект_;
	
	ПредметКомментирования = УниверсальныеКомментарииУХ.ПолучитьПредметКомментированияДляТаблицы(РазделКомментариев, ТаблицаПредметаКомменитирования, ТекстОшибки);
	Если ПредметКомментирования = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли; 

	Объект_ = ПредметКомментирования.ПолучитьОбъект();
	Если Объект_ = Неопределено Тогда
		ТекстОшибки = ТекстОшибки + НСтр("ru = 'Невозможно получить объект предмета комментирования, чтобы его пометить на удаление.'");
		Возврат Ложь;
	КонецЕсли;
	
	Объект_.УстановитьПометкуУдаления(Истина);
	
	// Пометим на удаление прикрепленные файлы	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УниверсальныеКомментарии.ПакетФайлов.Ссылка КАК ПакетФайловСсылка
		|ИЗ
		|	РегистрСведений.УниверсальныеКомментарии КАК УниверсальныеКомментарии
		|ГДЕ
		|	НЕ УниверсальныеКомментарии.ПакетФайлов.ПометкаУдаления
		|	И УниверсальныеКомментарии.ПредметКомментирования = &Ссылка
		|	И УниверсальныеКомментарии.ПакетФайлов <> НЕОПРЕДЕЛЕНО";

	Запрос.УстановитьПараметр("Ссылка", ПредметКомментирования);

	Результат = Запрос.Выполнить();
	ВыборкаСсылокДляУдаления = Результат.Выбрать();
	Пока ВыборкаСсылокДляУдаления.Следующий() Цикл
		
		ОбъектДляУдаления = ВыборкаСсылокДляУдаления.Ссылка.ПолучитьОбъект();
		ОбъектДляУдаления.УстановитьПометкуУдаления(Истина);
	КонецЦикла;
	
	Возврат Истина;
КонецФункции // УдалитьПредметКомментирования()
 