#Область ПрограммныйИнтерфейс

Процедура УстановитьПараметрыВыбораФормаЗаписи(Запись, ПараметрыВыбораДоговора) Экспорт

	Если Запись.ВидСвязи = Справочники.ВидыСвязейОбъектов.Страхование Тогда
		ПараметрыВыбораДоговора.Добавить(Новый ПараметрВыбора("Отбор.ВидДоговораУХ", Справочники.ВидыДоговоровКонтрагентовУХ.Страхование));
	ИначеЕсли Запись.ВидСвязи = Справочники.ВидыСвязейОбъектов.Спецификация Тогда
		ПараметрыВыбораДоговора.Добавить(Новый ПараметрВыбора("Отбор.БазовыйДоговор", Запись.БазовыйДоговор));
		ПараметрыВыбораДоговора.Добавить(Новый ПараметрВыбора("Отбор.ВидСоглашения", Перечисления.ВидыСоглашений.Спецификация));
	ИначеЕсли Запись.ВидСвязи = Справочники.ВидыСвязейОбъектов.ОбеспечениеВыданное Тогда
		ВидыДоговоров = Новый Массив;
		ВидыДоговоров.Добавить(Справочники.ВидыДоговоровКонтрагентовУХ.ЗалогиИПоручительстваИсходящие);
		ВидыДоговоров.Добавить(Справочники.ВидыДоговоровКонтрагентовУХ.АккредитивВыданный);
		ВидыДоговоров.Добавить(Справочники.ВидыДоговоровКонтрагентовУХ.ГарантияВыданная);
		
		ПараметрыВыбораДоговора.Добавить(Новый ПараметрВыбора("Отбор.ВидДоговораУХ", Новый ФиксированныйМассив(ВидыДоговоров)));
	ИначеЕсли Запись.ВидСвязи = Справочники.ВидыСвязейОбъектов.ОбеспечениеПолученное Тогда
		ПараметрыВыбораДоговора.Добавить(Новый ПараметрВыбора("Отбор.ВидДоговораУХ", Справочники.ВидыДоговоровКонтрагентовУХ.ЗалогиИПоручительстваВходящие));
	КонецЕсли;

КонецПроцедуры

Функция РеквизитыДляПроверки(ДоговорСсылка) Экспорт
	
	ТипОбъекта = ТипЗнч(ДоговорСсылка);
	
	РеквизитыДоговоровДляПроверки = Новый Структура("Организация,Контрагент,Валюта,Сумма");
	
	Если ТипОбъекта = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		
		РеквизитыДоговоровДляПроверки.Организация = "Организация";
		РеквизитыДоговоровДляПроверки.Контрагент = "Контрагент";
		РеквизитыДоговоровДляПроверки.Валюта = "ВалютаВзаиморасчетов";
		РеквизитыДоговоровДляПроверки.Сумма = "Сумма";
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ДоговорыКредитовИДепозитов") Тогда
		
		РеквизитыДоговоровДляПроверки.Организация = "Организация";
		РеквизитыДоговоровДляПроверки.Контрагент = "Контрагент";
		РеквизитыДоговоровДляПроверки.Валюта = "ВалютаВзаиморасчетов";
		РеквизитыДоговоровДляПроверки.Сумма = "СуммаТраншей";
		
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ДоговорыАренды") Тогда
		
		РеквизитыДоговоровДляПроверки.Организация = "Организация";
		РеквизитыДоговоровДляПроверки.Контрагент = "Контрагент";
		РеквизитыДоговоровДляПроверки.Валюта = "ВалютаВзаиморасчетов";
		РеквизитыДоговоровДляПроверки.Удалить("Сумма");

	ИначеЕсли РасширениеБизнесЛогикиУХКлиентСервер.ПроверитьТипЗначения(ДоговорСсылка, "СправочникСсылка.ОбеспечениеТретьихЛиц") Тогда
		
		РеквизитыДоговоровДляПроверки.Организация = "Бенефициар";
		РеквизитыДоговоровДляПроверки.Контрагент = "Контрагент";
		РеквизитыДоговоровДляПроверки.Валюта = "Валюта";
		РеквизитыДоговоровДляПроверки.Сумма = "Сумма";
		
	Иначе
		ТекстШаблона = НСтр("ru = 'Объект %1 имеет некорректный тип %2.'");
		ВызватьИсключение СтрШаблон(ТекстШаблона, ДоговорСсылка, ТипОбъекта);
	КонецЕсли;
	
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДоговорСсылка, РеквизитыДоговоровДляПроверки);
	
КонецФункции

Процедура ПриЗаписиВстречныхДоговоров(ДоговорИсточник, ДоговорПриемник, ЭтоПерваяЗапись) Экспорт
	
	Если ЭтоПерваяЗапись Тогда
		РаботаСДоговорамиКонтрагентовУХ.ЗаписатьСвязьВнутригрупповыхВерсий(ДоговорИсточник.ВерсияСоглашения, ДоговорПриемник.ВерсияСоглашения);
	КонецЕсли;
	
КонецПроцедуры

Функция ОрганизацияКонтрагентаДоговора(Договор) Экспорт
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "Контрагент.ОрганизационнаяЕдиница");
КонецФункции
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти