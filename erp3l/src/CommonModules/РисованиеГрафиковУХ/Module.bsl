////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ РИСОВАНИЯ ЭЛЕМЕНТОВ СХЕМ.
//

Функция НарисоватьЛинию(ТабличныйДокумент, Расшифровка, ЛевоЛинии, ВерхЛинии, ШиринаЛинии, ВысотаЛинии, Цвет = Неопределено, ТипЛинии = Неопределено) Экспорт
	
	Линия = ТабличныйДокумент.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Прямая);
	Линия.Лево   = ЛевоЛинии;
	Линия.Верх   = ВерхЛинии;
	Линия.Ширина = ШиринаЛинии;
	Линия.Высота = ВысотаЛинии;
	Линия.ЦветЛинии   = ?(Цвет = Неопределено, Новый Цвет(0,0,0), Цвет); 
	Линия.Расшифровка = Расшифровка;
	
	Если ТипЛинии = Неопределено Тогда
		ТипЛинии = ТипЛинииРисункаТабличногоДокумента.Сплошная;
	КонецЕсли;
	
	Линия.Линия = Новый Линия(ТипЛинии, 2);
	
	ЛевоЛинии = ЛевоЛинии + ШиринаЛинии;
	ВерхЛинии = ВерхЛинии + ВысотаЛинии;
	
	Возврат Линия;
	
КонецФункции

// Функция выводит рисунок из линий, в виде изгиба, в табличный документ
//
Функция НарисоватьИзгиб(ТабличныйДокумент, Расшифровка, ЛевоЛинии, ВерхЛинии, ШиринаЛинии, ВысотаЛинии, Цвет) Экспорт
	
	Отступ = 0.1;
	СдвигЛево = ?(ШиринаЛинии > 0, Отступ, -Отступ);
	СдвигВерх = ?(ВысотаЛинии > 0, Отступ, -Отступ);
	
	Линия = ТабличныйДокумент.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Прямая);
	Линия.Лево   = ЛевоЛинии + СдвигЛево;
	Линия.Верх   = ВерхЛинии + СдвигВерх;
	Линия.Ширина = ШиринаЛинии - СдвигЛево * 2;
	Линия.Высота = ВысотаЛинии - СдвигВерх * 2;
	
	Линия.Расшифровка = Расшифровка;
	Линия.Линия = Новый Линия(ТипЛинииРисункаТабличногоДокумента.Сплошная, 2);
	Линия.ЦветЛинии = Цвет;
	ЛевоЛинии = ЛевоЛинии + ШиринаЛинии;
	ВерхЛинии = ВерхЛинии + ВысотаЛинии;
	
	Возврат Линия;
	
КонецФункции

// Функция выводит рисунок из линий, в виде стрелки, в табличный документ
//
Процедура НарисоватьСтрелку(ТабличныйДокумент, Расшифровка, ЛевоЛинии, ВерхЛинии, Вертикально = Ложь, Цвет) Экспорт
	
	ТипЛинии = Новый Линия(ТипЛинииРисункаТабличногоДокумента.Сплошная, 2);
	
	Линия = ТабличныйДокумент.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Прямая);
	Линия.Лево   = ЛевоЛинии;
	Линия.Верх   = ВерхЛинии;
	Линия.Ширина =  - 1.5;
	Линия.Высота =  - 1.5;
	Линия.Расшифровка = Расшифровка;
	Линия.ЦветЛинии = Цвет;
	Линия.Линия = ТипЛинии;
	
	Линия = ТабличныйДокумент.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Прямая);
	Линия.Лево   = ЛевоЛинии;
	Линия.Верх   = ВерхЛинии;
	Линия.Ширина = 1.5;
	Линия.Высота = -1.5;
	Линия.ЦветЛинии = Цвет;
	Линия.Расшифровка = Расшифровка;
	Линия.Линия = ТипЛинии;
	
КонецПроцедуры

// Функция выводит рисунок типа "текст" в табличный документ 
//
Функция НарисоватьТекст(ТабличныйДокумент, Текст, Расшифровка, Лево, Верх, Ширина, Высота, Цвет = Неопределено, ЦветУзора, ЦветЛинии = Неопределено) Экспорт

	Рисунок = ТабличныйДокумент.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Текст);
	Рисунок.Лево   = Лево;
	Рисунок.Верх   = Верх;
	Рисунок.Ширина = Ширина;
	Рисунок.Высота = Высота;
	
	Рисунок.Линия  = Новый Линия(ТипЛинииРисункаТабличногоДокумента.Сплошная, 1);
	Рисунок.Расшифровка = Расшифровка;
	Рисунок.Текст = Текст;
	Рисунок.ЦветФона = ?(Цвет = Неопределено, WindowsЦвета.ФонОкна, Цвет);
	Рисунок.ЦветЛинии = ?(ЦветЛинии = Неопределено, WindowsЦвета.ФонОкна, ЦветЛинии);
	Рисунок.ЦветУзора = ЦветУзора;
	Рисунок.Узор      = ТипУзораТабличногоДокумента.Узор17;
	
	Возврат Рисунок;
	
КонецФункции // ()
