
#Область СлужебныйПрограммныйИнтерфейс

#Область ДатыИзмененийЗаконодательства

// Возвращает дату вступления в силу нормативного акта.
//
// Параметры:
//  нет
//
// Возвращаемое значение:
//   дата
//
Функция ДатаВводаВзносовНаДоплатуШахтерам() Экспорт 

	Возврат '20110101'

КонецФункции 

// Возвращает дату вступления в силу нормативного акта.
//
// Параметры:
//  нет
//
// Возвращаемое значение:
//   дата
//
Функция ДатаВводаВзносовЗаЗанятыхНаРаботахСДосрочнойПенсией() Экспорт 

	Возврат '20130101'

КонецФункции 

// Возвращает дату вступления в силу нормативного акта.
//
// Параметры:
//  нет
//
// Возвращаемое значение:
//   дата
//
Функция ДатаОтменыТФОМС() Экспорт 

	Возврат '20120101'

КонецФункции 

// Возвращает дату вступления в силу Федерального закона "О внесении изменений в отдельные законодательные акты
// Российской Федерации по вопросам обязательного пенсионного страхования в части права выбора застрахованными лицами
// варианта пенсионного обеспечения".
//
// Параметры:
//  нет
//
// Возвращаемое значение:
//   дата
//
Функция ДатаОбъединенияСтраховойИНакопительнойЧастейВзносовПФР() Экспорт 

	Возврат '20140101'

КонецФункции 

// Возвращает дату вступления в силу приказа Минфина РФ.
//
// Параметры:
//  нет
//
// Возвращаемое значение:
//   дата
//
Функция ДатаВыделенияВзносовНаОПСсСуммПревышенияПредельнойВеличины() Экспорт 

	Возврат '20160101'

КонецФункции 

#КонецОбласти

// Возвращает строку-перечень полей ресурсов страховых взносов.
//
// Параметры:
//	БезЕНВД - булево - необязательный, если Истина, в строку будут включены парные ЕНВДные поля.
//  ИмяТаблицы - строка - необязательный, если задан, строка результат может использоваться как часть текста запроса к
//                        таблице, содержащей соответствующие поля.
//	ВключатьДопТарифы - булево - необязательный, если Истина, в строку будут включены поля взносов по дополнительным
//	                             тарифам для вредников.
//
// Возвращаемое значение:
//	Строка.
// 
Функция РассчитываемыеВзносыВПФР(БезЕНВД = Ложь, ИмяТаблицы = "", ВключатьДопТарифы = Истина, ВключатьСпецоценку = Истина) Экспорт
	
	ИмяТаблицыСТочкой = "";
	Если ЗначениеЗаполнено(ИмяТаблицы) Тогда
		ИмяТаблицыСТочкой = ИмяТаблицы + "."
	КонецЕсли;
	
	ПереченьРесурсов = "ПФРДоПредельнойВеличины,ПФРСПревышения,ПФРПоСуммарномуТарифу,ПФРСтраховая,ПФРНакопительная" + ?(ВключатьДопТарифы, "," + ВзносыПоДополнительнымТарифам(),"") + ?(БезЕНВД,"",",ПФРДоПредельнойВеличиныЕНВД,ПФРСПревышенияЕНВД,ПФРПоСуммарномуТарифуЕНВД,ПФРСтраховаяЕНВД,ПФРНакопительнаяЕНВД");
	Если ВключатьДопТарифы И ВключатьСпецоценку Тогда
		ПереченьРесурсов = ПереченьРесурсов + "," + ВзносыПоКодамСпециальнойОценки();
	КонецЕсли;
	
	Возврат ИмяТаблицыСТочкой + СтрЗаменить(ПереченьРесурсов, ",", "," + ИмяТаблицыСТочкой)
	
КонецФункции 

Функция РассчитываемыеВзносыВФСС(БезЕНВД = Ложь, ИмяТаблицы = "") Экспорт
	
	ИмяТаблицыСТочкой = "";
	Если ЗначениеЗаполнено(ИмяТаблицы) Тогда
		ИмяТаблицыСТочкой = ИмяТаблицы + "."
	КонецЕсли;
	
	ПереченьРесурсов = "ФСС,ФССНесчастныеСлучаи" + ?(БезЕНВД,"",",ФССЕНВД");
	
	Возврат ИмяТаблицыСТочкой + СтрЗаменить(ПереченьРесурсов, ",", "," + ИмяТаблицыСТочкой)
	
КонецФункции 

Функция РассчитываемыеВзносыВФОМС(БезЕНВД = Ложь, ИмяТаблицы = "") Экспорт
	
	ИмяТаблицыСТочкой = "";
	Если ЗначениеЗаполнено(ИмяТаблицы) Тогда
		ИмяТаблицыСТочкой = ИмяТаблицы + "."
	КонецЕсли;
	
	ПереченьРесурсов = "ФФОМС,ТФОМС" + ?(БезЕНВД,"",",ФФОМСЕНВД,ТФОМСЕНВД");
	
	Возврат ИмяТаблицыСТочкой + СтрЗаменить(ПереченьРесурсов, ",", "," + ИмяТаблицыСТочкой)
	
КонецФункции 

// Возвращает строку-перечень полей ресурсов страховых взносов.
//
// Параметры:
//	БезЕНВД - булево - необязательный, если Истина, в строку будут включены парные ЕНВДные поля.
//  ИмяТаблицы - строка - необязательный, если задан, строка результат может использоваться как часть текста запроса к
//                        таблице, содержащей соответствующие поля.
//	ВключатьДопТарифы - булево - необязательный, если Истина, в строку будут включены поля взносов по дополнительным
//	                             тарифам для вредников.
//
// Возвращаемое значение:
//	Строка.
// 
Функция РассчитываемыеВзносы(БезЕНВД = Ложь, ИмяТаблицы = "", ВключатьДопТарифы = Истина, ВключатьСпецоценку = Истина) Экспорт
	
	Возврат РассчитываемыеВзносыВПФР(БезЕНВД, ИмяТаблицы, ВключатьДопТарифы, ВключатьСпецоценку) + "," + РассчитываемыеВзносыВФСС(БезЕНВД, ИмяТаблицы) + "," + РассчитываемыеВзносыВФОМС(БезЕНВД, ИмяТаблицы)
	
КонецФункции 

// Возвращает строку-перечень полей ресурсов страховых взносов, по которым ведутся расчеты с фондами.
//
// Параметры:
//	БезЕНВД - булево - необязательный, если Истина, в строку будут включены парные ЕНВДные поля.
//  ИмяТаблицы - строка - необязательный, если задан, строка результат может использоваться как часть текста запроса к
//                        таблице, содержащей соответствующие поля.
//	ВключатьДопТарифы - булево - необязательный, если Истина, в строку будут включены поля взносов по дополнительным
//	                             тарифам для вредников.
//
// Возвращаемое значение:
//	Строка.
// 
Функция ОтражаемыеВУчетеВзносы(БезЕНВД = Ложь, ИмяТаблицы = "", ВключатьДопТарифы = Истина) Экспорт
	
	ИмяТаблицыСТочкой = "";
	Если ЗначениеЗаполнено(ИмяТаблицы) Тогда
		ИмяТаблицыСТочкой = ИмяТаблицы + "."
	КонецЕсли;
	
	ПереченьРесурсов = СтрЗаменить(ВзносыЗаВредников(), ",", "БезСпецОценки,") + СтрЗаменить(ВзносыЗаВредников(), ",", "СпецОценка,");
	
	Возврат РассчитываемыеВзносы(БезЕНВД, ИмяТаблицы, ВключатьДопТарифы, Ложь) + ?(ВключатьДопТарифы, "," + ИмяТаблицыСТочкой + СтрЗаменить(Лев(ПереченьРесурсов, СтрДлина(ПереченьРесурсов) - 1), ",", "," + ИмяТаблицыСТочкой), "");
	
КонецФункции 

// Возвращает строку-перечень полей ресурсов страховых взносов по дополнительным тарифам для вредников.
//
// Параметры:
//	нет
//
// Возвращаемое значение:
//	Строка.
// 
Функция ВзносыЗаВредников() Экспорт
	
	Возврат "ПФРЗаЗанятыхНаПодземныхИВредныхРаботах,ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах,"
	
КонецФункции 

// Возвращает строку-перечень полей ресурсов страховых взносов по дополнительным тарифам для вредников.
//
// Параметры:
//	нет
//
// Возвращаемое значение:
//	Строка.
// 
Функция ВзносыПоДополнительнымТарифам() Экспорт
	
	Возврат ВзносыЗаВредников() + "ПФРНаДоплатуЛетчикам,ПФРНаДоплатуШахтерам" 
	
КонецФункции 

// Возвращает строку-перечень полей ресурсов страховых взносов по дополнительному тарифу по классам условий труда.
//
// Параметры:
//	нет
//
// Возвращаемое значение:
//	Строка.
// 
Функция ВзносыПоКодамСпециальнойОценки() Экспорт
	
	Возврат "ПФРЗаЗанятыхНаПодземныхИВредныхРаботахОпасный,ПФРЗаЗанятыхНаПодземныхИВредныхРаботахВредный1,ПФРЗаЗанятыхНаПодземныхИВредныхРаботахВредный2,ПФРЗаЗанятыхНаПодземныхИВредныхРаботахВредный3,ПФРЗаЗанятыхНаПодземныхИВредныхРаботахВредный4,ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахОпасный,ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахВредный1,ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахВредный2,ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахВредный3,ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахВредный4"
	
КонецФункции 

/////////////////////////////////////////////////////////////////////////////////////////

// Возвращает текст подсказки для поля "ДатаРегистрацииИзменений" периодических регистров сведений:
// "ВременноПребывающиеПринятыеПоДолгосрочнымДоговорам", "КлассыУсловийТрудаПоДолжностям", 
// "ПрименяемыеТарифыСтраховыхВзносов", "СведенияОбИнвалидностиФизическихЛиц", "СтатусыЗастрахованныхФизическихЛиц"
//
Функция ТекстПодсказкиПоляДатаРегистрацииПериодическихРегистров() Экспорт 
	Возврат НСтр("ru = 'При регистрации изменений задним числом может потребоваться перерасчет страховых взносов.';
				|en = 'If changes are registered post factum, you may need to recalculate insurance contributions.'");	 	
КонецФункции	

/////////////////////////////////////////////////////////////////////////////////////////

// Возвращает текстовое описание для вида застрахованного лица.
//
// Параметры:
//	ВидЗастрахованногоЛица - ПеречислениеСсылка.ВидыЗастрахованныхЛицОбязательногоСтрахования -
//
// Возвращаемое значение:
//	Строка.
// 
Функция ОписаниеВидаЗастрахованногоЛица(ВидЗастрахованногоЛица) Экспорт

	Если ВидЗастрахованногоЛица = ПредопределенноеЗначение("Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ГражданеРФ") Тогда
		Возврат НСтр("ru = 'На граждан России распространяется обязательное пенсионное, медицинское и социальное страхование в полном объеме.';
					|en = 'Russian citizens are subject to mandatory pension insurance, compulsory health and social insurance in full. '");
	ИначеЕсли ВидЗастрахованногоЛица = ПредопределенноеЗначение("Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ПостоянноПроживающиеИностранцы") Тогда
		Возврат НСтр("ru = 'На приравненных к гражданам РФ иностранных граждан и лиц без гражданства распространяется обязательное пенсионное, медицинское и социальное страхование в полном объеме. Страховые взносы уплачиваются в общеустановленном порядке.';
					|en = 'Full compulsory medical, pension, and social insurance is provided to the foreign citizens equated to the RF citizens and to persons without citizenship. Insurance contributions are paid in the common way. '");
	ИначеЕсли ВидЗастрахованногоЛица = ПредопределенноеЗначение("Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВысококвалифицированныеСпециалистыПостоянноПроживающие") Тогда
		Возврат НСтр("ru = 'На постоянно проживающих на территории РФ иностранных граждан и лиц без гражданства, являющихся высококвалифицированными специалистами и членами их семей, распространяется обязательное пенсионное и социальное страхование в полном объеме. Страховые взносы в ПФР и ФСС уплачиваются в том же порядке, что и за граждан РФ';
					|en = 'Compulsory pension and social insurance is fully provided to the foreign citizens and persons without citizenship permanently staying on the territory of the RF who are highly-skilled specialists, and to the members of their families. Insurance contributions to the PF and SSF are paid in the same way as for the RF citizens.'");
	ИначеЕсли ВидЗастрахованногоЛица = ПредопределенноеЗначение("Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПроживающиеИностранцы") Тогда
		Возврат НСтр("ru = 'На временно проживающих на территории РФ иностранных граждан и лиц без гражданства распространяется обязательное медицинское и социальное страхование в полном объеме. В части пенсионного страхования все уплачиваемые взносы с 2012 года относятся к страховой части пенсии.';
					|en = 'Compulsory medical and social insurance is fully provided to the foreign citizens and persons without citizenship temporary staying on the territory of the RF. As to pension insurance, since 2012 all contributions are credited to the insurance part of labor pension.'");
	ИначеЕсли ВидЗастрахованногоЛица = ПредопределенноеЗначение("Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВысококвалифицированныеСпециалистыВременноПроживающие") Тогда
		Возврат НСтр("ru = 'На временно проживающих на территории РФ иностранных граждан и лиц без гражданства, являющихся высококвалифицированными специалистами и членами их семей, распространяется обязательное пенсионное и социальное страхование в полном объеме. В части пенсионного страхования все уплачиваемые взносы с 2012 года относятся к страховой части пенсии.';
					|en = 'Compulsory pension and social insurance is fully provided to the foreign citizens and persons without citizenship temporary staying on the territory of the RF who are highly-skilled specialists, and to the members of their families. As to pension insurance, since 2012 all contributions are credited to the insurance part of labor pension.'");
	ИначеЕсли ВидЗастрахованногоЛица = ПредопределенноеЗначение("Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВысококвалифицированныеСпециалистыИзЕАЭС") Тогда
		Возврат НСтр("ru = 'На временно пребывающих на территории РФ граждан стран ЕАЭС, являющихся высококвалифицированными специалистами и членами их семей, с 1 января 2015 года распространяется обязательное медицинское и социальное страхование в полном объеме.';
					|en = 'Since January 1, 2015, compulsory medical and social insurance is fully provided to the citizens of the EEU countries temporary staying on the territory of the RF who are highly-skilled specialists, and to the members of their families.'");
	ИначеЕсли ВидЗастрахованногоЛица = ПредопределенноеЗначение("Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцыСтрахуемыеФСС") Тогда
		Возврат НСтр("ru = 'На временно пребывающих на территории РФ иностранных граждан стран, с которыми заключены соответствующие международные договоры, не распространяется медицинское страхование и пенсионное страхование в РФ. Обязательное социальное распространяется на таких лиц с 1 января 2015 года.';
					|en = 'In the Russian Federation, medical and pension insurance is not provided to temporary staying on the RF territory foreign citizens of the countries with which corresponding international agreements are concluded. Compulsory social insurance is provided to such persons since January 1, 2015. '");
	ИначеЕсли ВидЗастрахованногоЛица = ПредопределенноеЗначение("Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцы") Тогда
		Возврат НСтр("ru = 'На временно пребывающих на территории РФ иностранных граждан и лиц без гражданства не распространяется медицинское страхование в РФ. Обязательное социальное и пенсионное страхование распространяется на таких лиц с 1 января 2015 года, с 1 января 2012 года по 31 декабря 2014 года право на пенсионное страхование распространяется при условии заключения трудового договора на срок не менее 6 месяцев.';
					|en = 'In the Russian Federation, medical insurance is not provided to the foreign citizens and persons without citizenship temporary staying on the territory of the RF. Compulsory pension and social insurance is provided to such persons since January 1, 2015. From January 1, 2012 to December 31, 2014, pension insurance is provided on condition that an employment contract is concluded for the period of at least 6 months.'");
	ИначеЕсли ВидЗастрахованногоЛица = ПредопределенноеЗначение("Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцыПризнанныеБеженцами") Тогда
		Возврат НСтр("ru = 'На временно пребывающих на территории РФ иностранных граждан и лиц без гражданства, получивших временное убежище на территории РФ или свидетельство о рассмотрении ходатайства о признании беженцем, распространяется обязательное медицинское страхование в РФ. Обязательное социальное и пенсионное страхование распространяется на таких лиц с 1 января 2015 года, с 1 января 2012 года по 31 декабря 2014 года право на пенсионное страхование распространяется при условии заключения трудового договора на срок не менее 6 месяцев.';
					|en = 'In the Russian Federation, compulsory medical insurance is provided to temporary staying on the territory of the RF foreign citizens and persons without citizenship who were granted temporary refuge on the territory of the RF or received a notification that a request for recognition of refugee status is under consideration. Compulsory social and pension insurance is provided to such persons since January 1, 2015. From January 1, 2012 to December 31, 2014, pension insurance is provided on condition that an employment contract is concluded for the period of at least 6 months.'");
	ИначеЕсли ВидЗастрахованногоЛица = ПредопределенноеЗначение("Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцыНестрахуемые") Тогда
		Возврат НСтр("ru = 'На нестрахуемых граждан, включая временно пребывающих на территории РФ иностранных граждан и лиц без гражданства (в том числе высококвалифицированных иностранных специалистов), не распространяется пенсионное, медицинское и социальное страхование в РФ.';
					|en = 'In the Russian Federation, medical, pension, and social insurance is not provided to uninsurable persons including foreign citizens temporary staying on the territory of the RF and persons without citizenship (including highly-skilled foreign specialists).'");
	КонецЕсли;
	
	Возврат "";

КонецФункции 

// Возвращает текстовое описание для тарифа страховых взносов.
//
// Параметры:
//	ТарифСтраховыхВзносов - СправочникСсылка.ВидыТарифовСтраховыхВзносов -
//
// Возвращаемое значение:
//	Строка.
// 
Функция ОписаниеВидаТарифа(ТарифСтраховыхВзносов) Экспорт
	
	ОписаниеТарифа = "";	
	Если ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.ОбщийНалоговыйРежим") Тогда
		ОписаниеТарифа = НСтр("ru = 'Основной тариф страховых взносов установлен статьями 425 и 426 НК РФ (до 2017 года - статьями 12 и 58.2 Федерального закона от 24 июля 2009 г. № 212-ФЗ)';
								|en = 'The basic insurance premiums rate is established by Articles 425 and 426 of the Tax Code of the Russian Federation (before 2017 - by Articles 12 and 58.2 of the Federal Law of July 24, 2009 No. 212-FZ)'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.УпрощенныйНалоговыйРежим") Тогда
		ОписаниеТарифа = НСтр("ru = 'С 2017 года применяются ставки основного тарифа страховых взносов, установленные статьями 425 и 426 НК РФ (с 2011 по 2016 годы -  статьями 12 и 58.2 Федерального закона от 24 июля 2009 г. № 212-ФЗ)';
								|en = 'Since 2017, the basic insurance premium rates established by Articles 425 and 426 of the Tax Code of the Russian Federation have been applied (from 2011 to 2016 - by Articles 12 and 58.2 of the Federal Law of July 24, 2009 No. 212-FZ)'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.ЕНВД") Тогда
		ОписаниеТарифа = НСтр("ru = 'С 2017 года применяются ставки основного тарифа страховых взносов, установленные статьями 425 и 426 НК РФ (с 2011 по 2016 годы -  статьями 12 и 58.2 Федерального закона от 24 июля 2009 г. № 212-ФЗ)';
								|en = 'Since 2017, the basic insurance premium rates established by Articles 425 and 426 of the Tax Code of the Russian Federation have been applied (from 2011 to 2016 - by Articles 12 and 58.2 of the Federal Law of July 24, 2009 No. 212-FZ)'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.СельХозПроизводители") Тогда
		ОписаниеТарифа = НСтр("ru = 'С 2017 года применяются ставки основного тарифа страховых взносов, установленные статьями 425 и 426 НК РФ (на 2010 год тариф был установлен пунктом 1 части 2 статьи 57, на 2011-2014 годы пунктом 1 части 1 статьи 58, в 2015 и 2016 годах применялись ставки основного тарифа страховых взносов, установленные статьями 12 и 58.2 Федерального закона от 24 июля 2009 г. № 212-ФЗ)';
								|en = 'Since 2017, the basic insurance premium rates established by Articles 425 and 426 of the Tax Code of the Russian Federation have been applied (for 2010 the rate was established by paragraph 1, part 2, Article 57, for 2011-2014 - by paragraph 1, part 1, Article 58, in 2015 and 2016 the basic insurance premium rates were applied as established by Articles 12 and 58.2 of the Federal Law of July 24, 2009 No. 212-FZ)'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.ЕСХН") Тогда
		ОписаниеТарифа = НСтр("ru = 'С 2017 года применяются ставки основного тарифа страховых взносов, установленные статьями 425 и 426 НК РФ (на 2010 год тариф был установлен пунктом 3 части 2 статьи 57, на 2011-2014 годы пунктом 2 части 1 статьи 58, в 2015 и 2016 годах применялись ставки основного тарифа страховых взносов, установленные статьями 12 и 58.2 Федерального закона от 24 июля 2009 г. № 212-ФЗ)';
								|en = 'Since 2017, the basic insurance premium rates established by Articles 425 and 426 of the Tax Code of the Russian Federation have been applied (for 2010 the rate was established by paragraph 3, part 2, Article 57, for 2011-2014 - by paragraph 2, part 1, Article 58, in 2015 and 2016 the basic insurance premium rates were applied as established by Articles 12 and 58.2 of the Federal Law of July 24, 2009 No. 212-FZ)'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.ОрганизацияИнвалидов") Тогда
		ОписаниеТарифа = НСтр("ru = 'С 2017 года применяются ставки основного тарифа страховых взносов, установленные статьями 425 и 426 НК РФ (на 2010 год тариф был установлен пунктом 2 части 2 статьи 57, на 2011-2014 годы пунктом 3 части 1 статьи 58, в 2015 и 2016 годах применялись ставки основного тарифа страховых взносов, установленные статьями 12 и 58.2 Федерального закона от 24 июля 2009 г. № 212-ФЗ)';
								|en = 'Since 2017, the basic insurance premium rates established by Articles 425 and 426 of the Tax Code of the Russian Federation have been applied (for 2010 the rate was established by paragraph 2, part 2, Article 57, for 2011-2014 - by paragraph 3, part 1, Article 58, in 2015 and 2016 the basic insurance premium rates were applied as established by Articles 12 and 58.2 of the Federal Law of July 24, 2009 No. 212-FZ)'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.НародныеХудожественныеПромыслы") Тогда
		ОписаниеТарифа = НСтр("ru = 'С 2017 года применяются ставки основного тарифа страховых взносов, установленные статьями 425 и 426 НК РФ (на 2010 год тариф был установлен пунктом 1 части 2 статьи 57, на 2011-2014 годы пунктом 1 части 1 статьи 58, в 2015 и 2016 годах применялись ставки основного тарифа страховых взносов, установленные статьями 12 и 58.2 Федерального закона от 24 июля 2009 г. № 212-ФЗ)';
								|en = 'Since 2017, the basic insurance premium rates established by Articles 425 and 426 of the Tax Code of the Russian Federation have been applied (for 2010 the rate was established by paragraph 1, part 2, Article 57, for 2011-2014 - by paragraph 1, part 1, Article 58, in 2015 and 2016 the basic insurance premium rates were applied as established by Articles 12 and 58.2 of the Federal Law of July 24, 2009 No. 212-FZ)'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.СМИ") Тогда
		ОписаниеТарифа = НСтр("ru = 'С 2017 года применяются ставки основного тарифа страховых взносов, установленные статьями 425 и 426 НК РФ (на 2011-2014 годы тариф был установлен пунктом 7 части 1 статьи 58, в 2015 и 2016 годах применялись ставки основного тарифа страховых взносов, установленные статьями 12 и 58.2 Федерального закона от 24 июля 2009 г. № 212-ФЗ)';
								|en = 'Since 2017, the basic insurance premium rates established by Articles 425 and 426 of the Tax Code of the Russian Federation have been applied (for 2011-2014 the rate was established by paragraph 7, part 1, Article 58, in 2015-2016 the basic insurance premium rates were applied as established by Articles 12 and 58.2 of the Federal Law of July 24, 2009 No. 212-FZ)'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.ОрганизацииОказывающиеИнжиниринговыеУслуги") Тогда
		ОписаниеТарифа = НСтр("ru = 'С 2017 года применяются ставки основного тарифа страховых взносов, установленные статьями 425 и 426 НК РФ (на 2012-2013 годы тариф был установлен пунктом 13 части 1 статьи 58, в 2014-2016 годах применялись ставки основного тарифа страховых взносов, установленные статьями 12 и 58.2 Федерального закона от 24 июля 2009 г. № 212-ФЗ)';
								|en = 'Since 2017, the basic insurance premium rates established by Articles 425 and 426 of the Tax Code of the Russian Federation have been applied (for 2012-2013 the rate was established by paragraph 13, part 1, Article 58, in 2014-2016 the basic insurance premium rates were applied as established by Articles 12 and 58.2 of the Federal Law of July 24, 2009 No. 212-FZ)'")
		
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.ХозОбществаБюджетныхВУЗов") Тогда
		ОписаниеТарифа = НСтр("ru = 'Тариф установлен на 2017-2019 годы для страхователей, удовлетворяющих условиям подпункта 1 пункта 1 статьи 427 НК РФ (с 2011 по 2016 годы - пункта 4 части 1 статьи 58 Федерального закона от 24 июля 2009 г. № 212-ФЗ)';
								|en = 'The rate is established for 2017-2019 for insurants who meet the conditions of subparagraph 1, paragraph 1, Article 427 of the Tax Code of the Russian Federation (for 2011-2016 - by paragraph 4, part 1, Article 58 of the Federal Law of July 24, 2009 No. 212-FZ)'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.РезидентТехникоВнедренческойЗоны") Тогда
		ОписаниеТарифа = НСтр("ru = 'Тариф установлен на 2017-2019 годы для страхователей, удовлетворяющих условиям подпункта 2 пункта 1 статьи 427 НК РФ (на 2010 год тариф был установлен пунктом 2 части 2 статьи 57, на 2011-2016 годы пунктом 5 части 1 статьи 58 Федерального закона от 24 июля 2009 г. № 212-ФЗ)';
								|en = 'The rate is established for 2017-2019 for insurants who meet the conditions of subparagraph 2, paragraph 1, Article 427 of the Tax Code of the Russian Federation (for 2010 the rate was established by paragraph 2, part 2, Article 57, for 2011-2016 - by paragraph 5, part 1, Article 58 of the Federal Law of July 24, 2009 No. 212-FZ)'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.ITОрганизации") Тогда
		ОписаниеТарифа = НСтр("ru = 'Тариф установлен на 2017-2023 годы для страхователей, удовлетворяющих условиям подпункта 3 пункта 1 статьи 427 НК РФ (на 2010 год тариф был установлен пунктом 4 части 2 статьи 57, на 2011-2016 годы пунктом 6 части 1 статьи 58 Федерального закона от 24 июля 2009 г. № 212-ФЗ)';
								|en = 'The rate is established for 2017-2027 for insurants who meet the conditions of subparagraph 3, paragraph 1, Article 427 of the Tax Code of the Russian Federation (for 2010 the rate was established by paragraph 4, Article 57, for 2011-2016 - by paragraph 6, part 1, Article 58 of the Federal Law of July 24, 2009 No. 212-FZ)'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.ДляЧленовЭкипажейМорскихСудовПодФлагомРФ") Тогда
		ОписаниеТарифа = НСтр("ru = 'Тариф установлен на 2017-2027 годы для страхователей, удовлетворяющих условиям подпункта 4 пункта 1 статьи 427 НК РФ (на 2012-2016 годы пунктом 9 части 1 статьи 58 Федерального закона от 24 июля 2009 г. № 212-ФЗ)';
								|en = 'The rate is established for 2017-2027 for insurants who meet the conditions of subparagraph 4, paragraph 1, Article 427 of the Tax Code of the Russian Federation (for 2012-2016 - by paragraph 9, part 1, Article 58 of the Federal Law of July 24, 2009 No. 212-FZ)'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.УпрощенныйНалоговыйРежимПроизводство") Тогда
		ОписаниеТарифа = НСтр("ru = 'Тариф установлен на 2017-2018 годы для страхователей, удовлетворяющих условиям подпункта 5 пункта 1 статьи 427 НК РФ (с 2011 по 2016 годы - пункта 8 части 1 статьи 58 Федерального закона от 24 июля 2009 г. № 212-ФЗ)';
								|en = 'The rate is established for 2017-2018 for insurants who meet the conditions of subparagraph 5, paragraph 1, Article 427 of the Tax Code of the Russian Federation (for 2011-2016 - by paragraph 8, part 1, Article 58 of the Federal Law of July 24, 2009 No. 212-FZ)'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.ДляФармацевтовАптек") Тогда
		ОписаниеТарифа = НСтр("ru = 'Тариф установлен на 2017-2018 годы для аптечных организаций и индивидуальных предпринимателей подпунктом 6 пункта 1 статьи 427 НК РФ (с 2012 по 2016 годы - пунктом 10 части 1 статьи 58 Федерального закона от 24 июля 2009 г. № 212-ФЗ)';
								|en = 'The rate is established for 2017-2018 for pharmacy companies and individual entrepreneurs by subparagraph 6, paragraph 1, Article 427 of the Tax Code of the Russian Federation (for 2012-2016 - by paragraph 10, part 1, Article 58 of the Federal Law of July 24, 2009 No. 212-FZ)'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.НекоммерческиеОрганизации") Тогда
		ОписаниеТарифа = НСтр("ru = 'Тариф установлен на 2017-2024 годы для страхователей, удовлетворяющих условиям подпункта 7 пункта 1 статьи 427 НК РФ (с 2012 по 2016 годы - пункта 11 части 1 статьи 58 Федерального закона от 24 июля 2009 г. № 212-ФЗ)';
								|en = 'The rate is established for 2017-2024 for insurants who meet the conditions of subparagraph 7, paragraph 1, Article 427 of the Tax Code of the Russian Federation (for 2012-2016 - by paragraph 11, part 1, Article 58 of the Federal Law of July 24, 2009 No. 212-FZ)'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.БлаготворительныеОрганизации") Тогда
		ОписаниеТарифа = НСтр("ru = 'Тариф установлен на 2017-2024 годы для страхователей, удовлетворяющих условиям подпункта 8 пункта 1 статьи 427 НК РФ (с 2012 по 2016 годы - пункта 12 части 1 статьи 58 Федерального закона от 24 июля 2009 г. № 212-ФЗ)';
								|en = 'The rate is established for 2017-2024 for insurants who meet the conditions of subparagraph 8, paragraph 1, Article 427 of the Tax Code of the Russian Federation (for 2012-2016 - by paragraph 12, part 1, Article 58 of the Federal Law of July 24, 2009 No. 212-FZ)'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.ИндивидуальныйПредпринимательПрименяющийПатент") Тогда
		ОписаниеТарифа = НСтр("ru = 'Тариф установлен на 2017-2018 годы для индивидуальных предпринимателей, применяющих патент подпунктом 9 пункта 1 статьи 427 НК РФ (с 2013 по 2016 годы - пункта 14 части 1 статьи 58 Федерального закона от 24 июля 2009 г. № 212-ФЗ)';
								|en = 'The rate is established for 2017-2018 for individual entrepreneurs using a patent by subparagraph 9, paragraph 1, Article 427 of the Tax Code of the Russian Federation (for 2013-2016 - by paragraph 14, part 1, Article 58 of the Federal Law of July 24, 2009 No. 212-FZ)'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.УчастникПроектаИнновационныйЦентрСколково") Тогда
		ОписаниеТарифа = НСтр("ru = 'Тариф установлен для страхователей, удовлетворяющих условиям подпункта 10 пункта 1 статьи 427 НК РФ (до 2017 года - статьей 58.1 Федерального закона от 24 июля 2009 г. № 212-ФЗ)';
								|en = 'The rate is established for insurants who meet the conditions of subparagraph 10, paragraph 1, Article 427 of the Tax Code of the Russian Federation (before 2017 - by Article 58.1 of the Federal Law of July 24, 2009 No. 212-FZ)'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.УчастникОсобойЗоныВКрыму") Тогда
		ОписаниеТарифа = НСтр("ru = 'Тариф установлен для страхователей, удовлетворяющих условиям подпункта 11 пункта 1 статьи 427 НК РФ (в 2015 и 2016 годах - статьей 58.4 Федерального закона от 24 июля 2009 г. № 212-ФЗ)';
								|en = 'The rate is established for insurants who meet the conditions of subparagraph 11, paragraph 1, Article 427 of the Tax Code of the Russian Federation (for 2015-2016 - by Article 58.4 of the Federal Law of July 24, 2009 No. 212-FZ)'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.РезидентТерриторииОпережающегоСоциальноЭкономическогоРазвития") Тогда
		ОписаниеТарифа = НСтр("ru = 'Тариф установлен для страхователей, удовлетворяющих условиям подпункта 12 пункта 1 статьи 427 НК РФ (в 2015 и 2016 годах - статьей 58.5 Федерального закона от 24 июля 2009 г. № 212-ФЗ)';
								|en = 'The rate is established for insurants who meet the conditions of subparagraph 12, paragraph 1, Article 427 of the Tax Code of the Russian Federation (for 2015-2016 - by Article 58.5 of the Federal Law of July 24, 2009 No. 212-FZ)'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.РезидентПортаВладивосток") Тогда
		ОписаниеТарифа = НСтр("ru = 'Тариф установлен для страхователей, удовлетворяющих условиям подпункта 13 пункта 1 статьи 427 НК РФ (в 2016 году - статьей 58.6 Федерального закона от 24 июля 2009 г. № 212-ФЗ)';
								|en = 'The rate is established for insurants who meet the conditions of subparagraph 13, paragraph 1, Article 427 of the Tax Code of the Russian Federation (in 2016 - by Article 58.6 of the Federal Law of July 24, 2009 No. 212-FZ)'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.РезидентОсобойЗоныВКалининградскойОбласти") Тогда
		ОписаниеТарифа = НСтр("ru = 'Тариф установлен для страхователей, удовлетворяющих условиям подпункта 14 пункта 1 статьи 427 НК РФ';
								|en = 'The rate has been established for insurants who meet the conditions of subparagraph 14, paragraph 1, Article 427 of the Tax Code of the Russian Federation'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.Мультипликаторы") Тогда
		ОписаниеТарифа = НСтр("ru = 'Тариф установлен на 2018-2023 годы для страхователей, удовлетворяющих условиям подпункта 15 пункта 1 статьи 427 НК РФ';
								|en = 'The rate has been established for 2018-2023 for insurants who meet the conditions of subparagraph 15, paragraph 1, Article 427 of the Tax Code of the Russian Federation'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.УчастникСпециальныхАдминистративныхРайонов") Тогда
		ОписаниеТарифа = НСтр("ru = 'Тариф установлен на 2020-2027 годы для страхователей, удовлетворяющих условиям подпункта 16 пункта 1 статьи 427 НК РФ';
								|en = 'The rate has been established for 2020-2027 for insurants who meet the conditions of subparagraph 16, paragraph 1, Article 427 of the Tax Code of the Russian Federation'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.ДляСубъектовМалогоПредпринимательства") Тогда
		ОписаниеТарифа = НСтр("ru = 'Тариф установлен с апреля 2020 года для страхователей, удовлетворяющих условиям подпункта 17 пункта 1 статьи 427 НК РФ';
								|en = 'The rate has been established since April 2020 for insurants who meet the conditions of subparagraph 17, paragraph 1, Article 427 of the Tax Code of the Russian Federation'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.ДляОтраслейПострадавшихОтКоронавируснойИнфекции") Тогда
		ОписаниеТарифа = НСтр("ru = 'Тариф установлен в отношении выплат, начисленных за апрель-июнь 2020 года, для организаций, занятых в сферах деятельности, наиболее пострадавших в условиях ухудшения ситуации в связи с распространением коронавирусной инфекции.';
								|en = 'The rate is set for payments accrued for April-June 2020 for companies involved in the business areas most affected by the worsening situation due to the spread of coronavirus infection.'")
	ИначеЕсли ТарифСтраховыхВзносов = ПредопределенноеЗначение("Справочник.ВидыТарифовСтраховыхВзносов.ОрганизацииРазработчикиЭлектроннойПродукции") Тогда
		ОписаниеТарифа = НСтр("ru = 'Тариф установлен с 2021 года для страхователей, удовлетворяющих условиям подпункта 18 пункта 1 статьи 427 НК РФ';
								|en = 'The rate has been established since 2021 for insurants who meet the conditions of subparagraph 18, paragraph 1, Article 427 of the Tax Code of the Russian Federation'")
		
	КонецЕсли;
	
	Возврат ОписаниеТарифа;
	
КонецФункции	

// Формирует соответствие описания реквизитов, содержащих суммы взносов.
//      	 
// Параметры:
//		Месяц - дата - необязательный, указывает на месяц начисления, для которого могут использоваться те или иные колонки
//		               взносов.
//		ВключатьДопТарифы - булево - необязательный, если выставлен в Ложь, взносы по доп.тарифам в описание не включаются.
//
// Возвращаемое значение:
//		Соответствие
//
Функция ОписаниеПолейВзносовВСоответствии(Месяц = '00010101', ВключатьДопТарифы = Истина, ВключатьКолонкиЕНВД = Истина) Экспорт

	ДатаПоявленияВзносовНаДоплатуШахтерам = ДатаВводаВзносовНаДоплатуШахтерам();
	ДатаОтменыТФОМС = ДатаОтменыТФОМС();
	ДатаПоявленияВзносовЗаЗанятыхНаРаботахСДосрочнойПенсией = ДатаВводаВзносовЗаЗанятыхНаРаботахСДосрочнойПенсией();
	ДатаОбъединенияСтраховойИНакопительнойЧастейВзносовПФР = ДатаОбъединенияСтраховойИНакопительнойЧастейВзносовПФР();
	ДатаВыделенияВзносовНаОПСсСуммПревышенияПредельнойВеличины = ДатаВыделенияВзносовНаОПСсСуммПревышенияПредельнойВеличины();
	
	Описание = Новый Соответствие;
	
	Описание.Вставить("ФФОМС", НСтр("ru = 'ФФОМС';
									|en = 'CMIF'"));
	Если ВключатьКолонкиЕНВД Тогда
		Описание.Вставить("ФФОМСЕНВД", НСтр("ru = 'ФФОМС (ЕНВД)';
											|en = 'CMIF (UTII)'"));
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Месяц) Или Месяц < ДатаОтменыТФОМС Тогда
		Описание.Вставить("ТФОМС", НСтр("ru = 'ТФОМС';
										|en = 'TCMIF'"));
		Если ВключатьКолонкиЕНВД Тогда
			Описание.Вставить("ТФОМСЕНВД", НСтр("ru = 'ТФОМС (ЕНВД)';
												|en = 'TCMIF (UTII)'"));
		КонецЕсли;
	КонецЕсли;
	
	Описание.Вставить("ФСС", НСтр("ru = 'ФСС';
									|en = 'SSF'"));
	Если ВключатьКолонкиЕНВД Тогда
		Описание.Вставить("ФССЕНВД", НСтр("ru = 'ФСС (ЕНВД)';
											|en = 'SSF (UTII)'"));
	КонецЕсли;
	Описание.Вставить("ФССНесчастныеСлучаи", НСтр("ru = 'ФСС (несч. случ.)';
													|en = 'SSF (accident)'"));
	
	Если Месяц < ДатаОбъединенияСтраховойИНакопительнойЧастейВзносовПФР Тогда
		Описание.Вставить("ПФРНакопительная", НСтр("ru = 'ПФР (накоп.)';
													|en = 'PF (fund.)'"));
		Описание.Вставить("ПФРСтраховая", НСтр("ru = 'ПФР (страх.)';
												|en = 'PF (insur.)'"));
		Если ВключатьКолонкиЕНВД Тогда
			Описание.Вставить("ПФРНакопительнаяЕНВД", НСтр("ru = 'ПФР (накоп. ЕНВД)';
															|en = 'PF (fund. UTII)'"));
			Описание.Вставить("ПФРСтраховаяЕНВД", НСтр("ru = 'ПФР (страх. ЕНВД)';
														|en = 'PF (insur. UTII)'"));
		КонецЕсли;
	ИначеЕсли Месяц < ДатаВыделенияВзносовНаОПСсСуммПревышенияПредельнойВеличины Тогда
		Описание.Вставить("ПФРПоСуммарномуТарифу", НСтр("ru = 'ПФР (на ОПС)';
														|en = 'PF (for MPI)'"));
		Если ВключатьКолонкиЕНВД Тогда
			Описание.Вставить("ПФРПоСуммарномуТарифуЕНВД", НСтр("ru = 'ПФР (на ОПС ЕНВД)';
																|en = 'PF (for MPI UTII)'"));
		КонецЕсли;
	Иначе
		Описание.Вставить("ПФРДоПредельнойВеличины", НСтр("ru = 'ПФР (на ОПС, до предела)';
															|en = 'PF (for MPI, to excess)'"));
		Если ВключатьКолонкиЕНВД Тогда
			Описание.Вставить("ПФРДоПредельнойВеличиныЕНВД", НСтр("ru = 'ПФР (на ОПС, до предела ЕНВД)';
																	|en = 'PF (for MPI, to UTII limit)'"));
		КонецЕсли;
		Описание.Вставить("ПФРСПревышения", НСтр("ru = 'ПФР (на ОПС, с превышения)';
												|en = 'PF (for MPI, from excess)'"));
		Если ВключатьКолонкиЕНВД Тогда
			Описание.Вставить("ПФРСПревышенияЕНВД", НСтр("ru = 'ПФР (на ОПС, с превышения ЕНВД)';
														|en = 'PF (for MPI, from UTII excess)'"));
		КонецЕсли;
	КонецЕсли;
	
	Если ВключатьДопТарифы Тогда
		Описание.Вставить("ПФРНаДоплатуЛетчикам", НСтр("ru = 'ПФР (летн. экипаж)';
														|en = 'PF (flight crew)'"));
		Если Не ЗначениеЗаполнено(Месяц) Или Месяц >= ДатаПоявленияВзносовНаДоплатуШахтерам Тогда
			Описание.Вставить("ПФРНаДоплатуШахтерам", НСтр("ru = 'ПФР (шахтеры)';
															|en = 'PF (miners)'"));
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Месяц) Или Месяц >= ДатаПоявленияВзносовЗаЗанятыхНаРаботахСДосрочнойПенсией Тогда
			Если Не ЗначениеЗаполнено(Месяц) Или Месяц >= ДатаОбъединенияСтраховойИНакопительнойЧастейВзносовПФР Тогда
				Описание.Вставить("ПФРЗаЗанятыхНаПодземныхИВредныхРаботах", НСтр("ru = 'ПФР (подз. раб., без оценки)';
																				|en = 'PF (late works, no assessment)'"));
				Описание.Вставить("ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах", НСтр("ru = 'ПФР (тяж. раб., без оценки)';
																				|en = 'PF (hard works, no assessment)'"));
				Для каждого ИмяПоля Из СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ВзносыПоКодамСпециальнойОценки()) Цикл
					Описание.Вставить(ИмяПоля, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'ПФР (%1. раб., кл.%2)';
																											|en = 'PF (%1. work, class %2)'"),
							?(Найти(ИмяПоля,"Подземн") > 0, "подз", "тяж"), 
							?(Прав(ИмяПоля,1) = "й", "О4", "В3" + Прав(ИмяПоля,1))));
				КонецЦикла;
			Иначе
				Описание.Вставить("ПФРЗаЗанятыхНаПодземныхИВредныхРаботах", НСтр("ru = 'ПФР (подземные работы)';
																				|en = 'PF (underground work)'"));
				Описание.Вставить("ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах", НСтр("ru = 'ПФР (тяжелые работы)';
																				|en = 'PF (arduous works)'"));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Описание
	
КонецФункции

/////////////////////////////////////////////////////////////////////////////////////////

Функция ПредставлениеСамостоятельнойКлассификационнойЕдиницы(Знач СтруктурнаяЕдиница, Знач СамостоятельнаяКлассификационнаяЕдиница) Экспорт
	
	Представление = "";
	Если ЗначениеЗаполнено(СамостоятельнаяКлассификационнаяЕдиница) Тогда
		ДанныеСамостоятельнойКлассификационнойЕдиницы = 
		ЗарплатаКадрыВызовСервера.ДанныеСамостоятельнойКлассификационнойЕдиницы(СтруктурнаяЕдиница, СамостоятельнаяКлассификационнаяЕдиница);
		
		Представление = СтрШаблон(НСтр("ru = '%1 ОКВЭД: %2, КПР: %3, Ставка НС и ПЗ: %4.';
										|en = '%1 RNCEA: %2, CPR: %3, Rate of insurance against industrial accidents and occupational diseases: %4.'"),
			ДанныеСамостоятельнойКлассификационнойЕдиницы.Наименование,
			ДанныеСамостоятельнойКлассификационнойЕдиницы.КодОКВЭД2,
			ДанныеСамостоятельнойКлассификационнойЕдиницы.КлассПрофессиональногоРиска,
			?(ДанныеСамостоятельнойКлассификационнойЕдиницы.Ставка<>Неопределено, 
			ДанныеСамостоятельнойКлассификационнойЕдиницы.Ставка, НСтр("ru = '<не заполнена>';
																		|en = '<not filled in>'")));
		
		Если ЗначениеЗаполнено(ДанныеСамостоятельнойКлассификационнойЕдиницы.Период) Тогда
			Представление = Представление + СтрШаблон(НСтр("ru = 'Действует с %1';
															|en = 'Valid from %1'"),
				Формат(ДанныеСамостоятельнойКлассификационнойЕдиницы.Период, "ДЛФ=DD"));
		КонецЕсли; 
	КонецЕсли;
	
	Если ПустаяСтрока(Представление) Тогда
		Представление = НСтр("ru = '<не заполнена>';
							|en = '<not filled in>'");
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

#Область УчетОтчитывающихсяПоВзносамОрганизаций

Функция СсылкаНаОрганизациюОтправляемогоДокумента(ДокументСсылка, Форма) Экспорт
	
	ОперативныеДокументы = Новый Соответствие;
	ОперативныеДокументы.Вставить(Тип("ДокументСсылка.ПачкаДокументовАДВ_1"), Истина);
	ОперативныеДокументы.Вставить(Тип("ДокументСсылка.ПачкаДокументовАДВ_2"), Истина);
	ОперативныеДокументы.Вставить(Тип("ДокументСсылка.ПачкаДокументовАДВ_3"), Истина);
	ОперативныеДокументы.Вставить(Тип("ДокументСсылка.ПачкаДокументовСЗВ_К"), Истина);
	ОперативныеДокументы.Вставить(Тип("ДокументСсылка.РеестрДСВ_3"), Истина);
	ОперативныеДокументы.Вставить(Тип("ДокументСсылка.СведенияОЗаработкеСтажеЗастрахованныхЛицСЗВ_ИСХ"), Истина);
	ОперативныеДокументы.Вставить(Тип("ДокументСсылка.РеестрСведенийНеобходимыхДляНазначенияИВыплатыПособий"), Истина);
	ОперативныеДокументы.Вставить(Тип("ДокументСсылка.РеестрДанныхЭЛНЗаполняемыхРаботодателем"), Истина);
	
	ОтчетныеДокументы = Новый Соответствие;
	ОтчетныеДокументы.Вставить(Тип("ДокументСсылка.СведенияОЗастрахованныхЛицахСЗВ_М"), Истина);
	ОтчетныеДокументы.Вставить(Тип("ДокументСсылка.СведенияОСтраховомСтажеЗастрахованныхЛицСЗВ_СТАЖ"), Истина);
	ОтчетныеДокументы.Вставить(Тип("ДокументСсылка.ОписьОДВ_1"), Истина);
	ОтчетныеДокументы.Вставить(Тип("ДокументСсылка.ДанныеОКорректировкеСведенийЗастрахованныхЛицСЗВ_КОРР"), Истина);
	ОтчетныеДокументы.Вставить(Тип("ДокументСсылка.СведенияОТрудовойДеятельностиРаботниковСЗВ_ТД"), Истина);
	
	ТипДокумента = ТипЗнч(ДокументСсылка);
	Если ОперативныеДокументы[ТипДокумента] <> Неопределено Тогда
		ДатаАктуальности = ОбщегоНазначенияБЗККлиентСервер.ЗначениеСвойства(Форма, "Дата");
		Если ТипДокумента <> Тип("ДокументСсылка.СведенияОЗаработкеСтажеЗастрахованныхЛицСЗВ_ИСХ") Тогда
			ОтчетныйПериод = ОбщегоНазначенияБЗККлиентСервер.ЗначениеСвойства(Форма, "ОтчетныйПериод");
			Если ЗначениеЗаполнено(ОтчетныйПериод) Тогда
				 ДатаАктуальности = ОтчетныйПериод
			КонецЕсли;
		КонецЕсли;
		Возврат УчетСтраховыхВзносовВызовСервера.ОтчитывающаясяПоВзносамОрганизация(ОбщегоНазначенияБЗККлиентСервер.ЗначениеСвойства(Форма, "Организация"), ДатаАктуальности);
	ИначеЕсли ОтчетныеДокументы[ТипДокумента] <> Неопределено Тогда
		Возврат ОбщегоНазначенияБЗККлиентСервер.ЗначениеСвойства(Форма, "Организация");
	Иначе
		Возврат Неопределено	
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует из любой даты каноническую дату получения дохода с точки зрения страховых взносов.
//      	 
// Параметры:
//		Дата - дата -
//
// Возвращаемое значение:
//		Дата
//
Функция ДатаПолученияДохода(Дата) Экспорт
	Возврат НачалоДня(КонецМесяца(Дата));	
КонецФункции	

#Область СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев

Процедура ОбновитьПолеСтавкаВзносаНаСтрахованиеОтНесчастныхСлучаевПериод(Форма, ВедущийОбъект) Экспорт
	
	РедактированиеПериодическихСведенийКлиентСервер.ОбновитьОтображениеПолейВвода(Форма, "СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев", ВедущийОбъект);
	
	// Не обязательно заполнение поля Период если данные по умолчанию.
	Если Форма.СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Период = НачалоМесяца(ЗарплатаКадрыКлиентСервер.ДатаОтсчетаПериодическихСведенийСПериодомМесяц()) Тогда
		
		Форма.Элементы.СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаевПериодСтрокой.АвтоОтметкаНезаполненного = Ложь;
		Форма.Элементы.СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаевПериодСтрокой.ОтметкаНезаполненного = Ложь;
		
	КонецЕсли;
	
	Если Форма.СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Период = НачалоМесяца(ЗарплатаКадрыКлиентСервер.ДатаОтсчетаПериодическихСведенийСПериодомМесяц()) Тогда
		Форма.СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаевПериод = '00010101';
	Иначе
		Форма.СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаевПериод = Форма.СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.Период;
	КонецЕсли;
	
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(Форма, "СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаевПериод", "СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаевПериодСтрокой");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
