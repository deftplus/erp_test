#Область ПрограммныйИнтерфейс

#Область ВидыБюджетов

Функция ЭтоБДДС(ВидБюджета) Экспорт
	Возврат (ВидБюджета = ПредопределенноеЗначение("Перечисление.ПредназначенияЭлементовСтруктурыОтчета.БюджетДвиженияДенежныхСредств")
			ИЛИ ВидБюджета = ПредопределенноеЗначение("ПланВидовХарактеристик.ВидыБюджетов.БюджетДвиженияДенежныхСредств")
			ИЛИ ВидБюджета = "БюджетДвиженияДенежныхСредств"
			ИЛИ ВидБюджета = "БДДС");
КонецФункции

Функция ЭтоБДР(ВидБюджета) Экспорт
	Возврат (ВидБюджета = ПредопределенноеЗначение("Перечисление.ПредназначенияЭлементовСтруктурыОтчета.БюджетДоходовИРасходов")
			ИЛИ ВидБюджета = ПредопределенноеЗначение("ПланВидовХарактеристик.ВидыБюджетов.БюджетДоходовИРасходов")	
			ИЛИ ВидБюджета = "БюджетДоходовИРасходов"
			ИЛИ ВидБюджета = "БДР");
КонецФункции

Функция ЭтоБюджетДвиженияРесурсов(ВидБюджета) Экспорт
	Возврат (ВидБюджета = ПредопределенноеЗначение("Перечисление.ПредназначенияЭлементовСтруктурыОтчета.БюджетДвиженияРесурсов")
			ИЛИ ВидБюджета = ПредопределенноеЗначение("ПланВидовХарактеристик.ВидыБюджетов.БюджетДвиженияРесурсов")	
			ИЛИ ВидБюджета = "БюджетДвиженияРесурсов"
			ИЛИ ВидБюджета = "Ресурсы");
КонецФункции

Функция ПараметрыБюджета(ВидБюджета) Экспорт
	
	ПараметрыБюджета=Новый Структура;
	
	Если ЭтоБДР(ВидБюджета) Тогда
		
		ПараметрыБюджета.Вставить("КолонкаСтатья",	"СтатьяДоходовИРасходов");
		ПараметрыБюджета.Вставить("КодБюджета",		"БДР");
		ПараметрыБюджета.Вставить("ТаблицаБюджета",	"БюджетДоходовИРасходов");
		ПараметрыБюджета.Вставить("ВидБюджета",		ПредопределенноеЗначение("Перечисление.ПредназначенияЭлементовСтруктурыОтчета.БюджетДоходовИРасходов"));
		ПараметрыБюджета.Вставить("СправочникСтатьи","СтатьиДоходовИРасходов");
		ПараметрыБюджета.Вставить("ЗаголовокКолонкиСтатья", НСтр("ru = 'Статья доходов и расходов'"));
		
	ИначеЕсли ЭтоБДДС(ВидБюджета) Тогда
				
		ПараметрыБюджета.Вставить("КолонкаСтатья",	"СтатьяДвиженияДенежныхСредств");
		ПараметрыБюджета.Вставить("КодБюджета",		"БДДС");
		ПараметрыБюджета.Вставить("ТаблицаБюджета",	"БюджетДвиженияДенежныхСредств");
		ПараметрыБюджета.Вставить("ВидБюджета",		ПредопределенноеЗначение("Перечисление.ПредназначенияЭлементовСтруктурыОтчета.БюджетДвиженияДенежныхСредств"));
		ПараметрыБюджета.Вставить("СправочникСтатьи","СтатьиДвиженияДенежныхСредств");
		ПараметрыБюджета.Вставить("ЗаголовокКолонкиСтатья", НСтр("ru = 'Статья движения денежных средств'"));
		
	ИначеЕсли ЭтоБюджетДвиженияРесурсов(ВидБюджета) Тогда
		
		ПараметрыБюджета.Вставить("КолонкаСтатья",	"СтатьяДвиженияРесурсов");
		ПараметрыБюджета.Вставить("КодБюджета",		"Ресурсы");
		ПараметрыБюджета.Вставить("ТаблицаБюджета",	"БюджетДвиженияРесурсов");
		ПараметрыБюджета.Вставить("ВидБюджета",		ПредопределенноеЗначение("Перечисление.ПредназначенияЭлементовСтруктурыОтчета.БюджетДвиженияРесурсов"));
		ПараметрыБюджета.Вставить("СправочникСтатьи","СтатьиДвиженияРесурсов");
		ПараметрыБюджета.Вставить("ЗаголовокКолонкиСтатья", НСтр("ru = 'Статья бюджета закупок'"));
		
	КонецЕсли;
	
	Возврат ПараметрыБюджета;
	
КонецФункции // ПолучитьПараметрыБюджета

Функция КодВидаБюджета(ВидБюджета) Экспорт
	ПараметрыБюджета = ПараметрыБюджета(ВидБюджета);
	Если ПараметрыБюджета.Свойство("КодБюджета") Тогда
		Возврат ПараметрыБюджета.КодБюджета;
	Иначе 
		Возврат "";		
	КонецЕсли;
	
КонецФункции

Функция ИмяКолонкиСтатья(ВидБюджета) Экспорт
	
	Возврат ПараметрыБюджета(ВидБюджета).КолонкаСтатья;
	
КонецФункции

Функция ИмяТаблицыБюджета(ВидБюджета) Экспорт	
	
	Возврат ПараметрыБюджета(ВидБюджета).ТаблицаБюджета;
	
КонецФункции

#КонецОбласти

#Область ТипыОперацийБюджетирование

Функция ЭтоПланированиеЗакупок(Документ) Экспорт
	
	Возврат Документ.ВидБюджета = ПредопределенноеЗначение("Перечисление.ПредназначенияЭлементовСтруктурыОтчета.БюджетДвиженияРесурсов");
	
КонецФункции

Функция ДелатьРегистрациюПотребностей(Документ) Экспорт
	
	Возврат Документ.ВидБюджета = ПредопределенноеЗначение("Перечисление.ПредназначенияЭлементовСтруктурыОтчета.БюджетДвиженияРесурсов") 
		И  (Документ.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийОперативныйПлан.Резервирование")
		//ИЛИ Документ.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийОперативныйПлан.УточнениеРезервирования")
		);
		
КонецФункции

#КонецОбласти

#Область УсловныеЕдиницы

Функция СтруктураПараметровРасчетаКурса() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("СпособОпределенияКурсаПлатежа");
	Результат.Вставить("КурсПлатежа");
	Результат.Вставить("КратностьПлатежа");
	Результат.Вставить("ДатаПлатежа");
	Результат.Вставить("ДатаФиксацииКурсаПлатежа");
	Результат.Вставить("СдвигДатыФиксацииКурсаПлатежа");
	Результат.Вставить("ВалютаДокумента");
	Результат.Вставить("ВалютаОплаты");
	Результат.Вставить("КурсПлатежаНеБолее");
	Результат.Вставить("КурсПлатежаНеМенее");
	Результат.Вставить("КурсПлатежаНеМенееВВалютеОплаты");
	Результат.Вставить("КурсПлатежаНеБолееВВалютеОплаты");
	
	Возврат Результат;
	
КонецФункции
	
#КонецОбласти

#Область ГруппыАналитик

#КонецОбласти

#КонецОбласти
