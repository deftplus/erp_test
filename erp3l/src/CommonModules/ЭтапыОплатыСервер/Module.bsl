
////////////////////////////////////////////////////////////////////////////////
// Модуль "ЭтапыОплатыСервер" содержит процедуры и функции для 
// работы с механизмом этапов оплаты,
// в первую очередь заполнение этапов оплаты различных объектов.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ЗаполнениеЭтаповОплаты

// Возвращает структуру с параметрами выбора реквизитов оплаты для передачи во внешние формы.
//
// Параметры:
// 		ОбъектМетаданных - ОбъектМетаданных - объект метаданных, по которому нужно определить параметры.
//
// Возвращаемое значение:
// 		Структура - использует в качестве ключа имя реквизита оплаты, а в значении содержит фиксированный массив с параметрами выбора.
//
Функция ПараметрыВыбораРеквизитовОплаты(ОбъектМетаданных) Экспорт
	
	РеквизитыОбъекта = ОбъектМетаданных.Реквизиты;
	ПараметрыВыбораРеквизитов = Новый Структура;
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("ФормаОплаты", ОбъектМетаданных) Тогда
		ПараметрыВыбораРеквизитов.Вставить("ФормаОплаты", РеквизитыОбъекта.ФормаОплаты.ПараметрыВыбора);
	КонецЕсли;
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Касса", ОбъектМетаданных) Тогда
		ПараметрыВыбораРеквизитов.Вставить("Касса", РеквизитыОбъекта.Касса.ПараметрыВыбора);
	КонецЕсли;
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("БанковскийСчет", ОбъектМетаданных) Тогда
		ПараметрыВыбораРеквизитов.Вставить("БанковскийСчет", РеквизитыОбъекта.БанковскийСчет.ПараметрыВыбора);
	ИначеЕсли ОбщегоНазначения.ЕстьРеквизитОбъекта("БанковскийСчетОрганизации", ОбъектМетаданных) Тогда
		ПараметрыВыбораРеквизитов.Вставить("БанковскийСчет", РеквизитыОбъекта.БанковскийСчетОрганизации.ПараметрыВыбора);
	КонецЕсли;
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("ГрафикОплаты", ОбъектМетаданных) Тогда
		ПараметрыВыбораРеквизитов.Вставить("ГрафикОплаты", РеквизитыОбъекта.ГрафикОплаты.ПараметрыВыбора);
	КонецЕсли;
	
	Возврат ПараметрыВыбораРеквизитов;
	
КонецФункции

//Возращает минимальную дату неоплаченного этапа графика исполнения договора.
//
// Параметры:
//
// 		ГрафикИсполненияДоговора - ДокументСсылка.ГрафикИсполненияДоговора - график договора.
// 		Дата - Дата - дата документа.
//
// Возвращаемое значение:
// 		Дата - Минимальная дата неоплаченного этапа графика.
Функция ДатаПервогоНеоплаченногоЭтапаГрафика(ГрафикИсполненияДоговора, Дата) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") И ЗначениеЗаполнено(ГрафикИсполненияДоговора) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ 
		|	МИНИМУМ(ПлановыеОплаты.ДатаПлановогоПогашения) КАК ДатаПлановогоПогашения
		|ИЗ
		|	(ВЫБРАТЬ
		|		РасчетыСКлиентамиПланОплатОстатки.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения
		|	ИЗ
		|		РегистрНакопления.РасчетыСКлиентамиПланОплат.Остатки(,
		|			ДокументПлан = &ДокументПлан
		|			И ДатаПлановогоПогашения >= &ДатаДокумента) КАК РасчетыСКлиентамиПланОплатОстатки
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		РасчетыСПоставщикамиПланОплат.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения
		|	ИЗ
		|		РегистрНакопления.РасчетыСПоставщикамиПланОплат.Остатки(,
		|			ДокументПлан = &ДокументПлан
		|			И ДатаПлановогоПогашения >= &ДатаДокумента) КАК РасчетыСПоставщикамиПланОплат
		|	) КАК ПлановыеОплаты";
		Запрос.УстановитьПараметр("ДокументПлан", ГрафикИсполненияДоговора);
		Запрос.УстановитьПараметр("ДатаДокумента", Дата);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Возврат Выборка.ДатаПлановогоПогашения;
		Иначе
			Возврат Дата;
		КонецЕсли;
	Иначе
		Возврат Дата;
	КонецЕсли;
	
КонецФункции

// Возвращает структуру с параметрами заполнения этапов оплаты по заказам.
//
// Возвращаемое значение:
// 		Структура - Структура параметров со значениями по умолчанию.
//
Функция ПараметрыЗаполненияЭтаповОплатыПоЗаказам() Экспорт
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Дата",                          Дата(1,1,1));
	СтруктураПараметров.Вставить("ТабличнаяЧасть",                Неопределено);
	СтруктураПараметров.Вставить("ЭтапыГрафикаОплаты",            Неопределено);
	СтруктураПараметров.Вставить("ТипРасчетов",                   Неопределено);
	СтруктураПараметров.Вставить("ПорядокРасчетов",               Неопределено);
	СтруктураПараметров.Вставить("Соглашение",                    Неопределено);
	СтруктураПараметров.Вставить("СуммаОбязательнойПредоплаты",   0);
	СтруктураПараметров.Вставить("УпрощеннаяСхема",               Ложь);
	СтруктураПараметров.Вставить("ЗаказКакСчет",                  Ложь);
	
	Возврат СтруктураПараметров;
	
КонецФункции

// Процедура заполняет ТЧ ЭтапыГрафикаОплаты по переданной структуре параметров.
Процедура ЗаполнитьЭтапыОплатыДокументаПоЗаказам(Параметры) Экспорт
	
	ЭтапыГрафикаОплаты = Параметры.ЭтапыГрафикаОплаты;
	Если НЕ ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭтапыГрафикаОплаты, "Колонки") Тогда
		Этапы = ЭтапыГрафикаОплаты.Выгрузить();
	Иначе
		Этапы = ЭтапыГрафикаОплаты;
	КонецЕсли;
	ДатаОтгрузки = ?(Параметры.Дата=Дата(1,1,1), ТекущаяДатаСеанса(), Параметры.Дата);
	ЕстьПредоплата = Параметры.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоЗаказамНакладным
			ИЛИ Параметры.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамНакладным;
	
	Если ЕстьПредоплата Тогда
		СуммаОбязательнойПредоплаты = Параметры.СуммаОбязательнойПредоплаты;
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ *
	|ПОМЕСТИТЬ ВТДанныеДокумента
	|ИЗ &ТаблицаТовары КАК ТаблицаТовары
	|;
	|ВЫБРАТЬ
	|	ДанныеДокумента.Заказ КАК Заказ,
	|	ДанныеДокумента.СверхЗаказа КАК СверхЗаказа,
	|	СУММА(ДанныеДокумента.СуммаПлатежа) КАК СуммаПлатежа,
	|	СУММА(ДанныеДокумента.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов,
	|	СУММА(ДанныеДокумента.СуммаЗалогаЗаТару) КАК СуммаЗалогаЗаТару,
	|	СУММА(ДанныеДокумента.СуммаВзаиморасчетовПоТаре) КАК СуммаВзаиморасчетовПоТаре
	|ПОМЕСТИТЬ ВтЗаказы
	|ИЗ
	|	ВтДанныеДокумента КАК ДанныеДокумента
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Заказ,
	|	ДанныеДокумента.СверхЗаказа
	|;";
	ТекстВыборкаПолей = "
	|ВЫБРАТЬ
	|	Заказы.Ссылка КАК Ссылка,
	|	Заказы.Сдвиг КАК Сдвиг,
	|	Заказы.ВариантОплаты КАК ВариантОплаты,
	|	Заказы.ДатаПлатежа КАК ДатаПлатежа,
	|	Заказы.СуммаПлатежа КАК СуммаПлатежа,
	|	Заказы.ПроцентПлатежа КАК ПроцентПлатежа,
	|	Заказы.СуммаЗалогаЗаТару КАК СуммаЗалогаЗаТару,
	|	Заказы.ПроцентЗалогаЗаТару КАК ПроцентЗалогаЗаТару
	|";
	
	Если Параметры.ТипРасчетов = ПредопределенноеЗначение("Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом") Тогда
		ВариантОплатыСдвиг = Перечисления.ВариантыОплатыКлиентом.КредитСдвиг;
		ВариантОплатыКредит = Перечисления.ВариантыОплатыКлиентом.КредитПослеОтгрузки;
		ВариантОплатыПредоплата = Перечисления.ВариантыОплатыКлиентом.ПредоплатаДоОтгрузки;
	Иначе
		ВариантОплатыСдвиг = Перечисления.ВариантыОплатыПоставщику.КредитСдвиг;
		ВариантОплатыКредит = Перечисления.ВариантыОплатыПоставщику.КредитПослеПоступления;
		ВариантОплатыПредоплата = Перечисления.ВариантыОплатыПоставщику.ПредоплатаДоПоступления;
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + ТекстВыборкаПолей + ",";
	
	Если ЗначениеЗаполнено(Параметры.Соглашение) Тогда
		Если Параметры.ТипРасчетов = ПредопределенноеЗначение("Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом") Тогда
			ТекстЗапроса = ТекстЗапроса + "
											|	ЕСТЬNULL(Заказы.Ссылка.Соглашение.ГрафикОплаты.Календарь,
											|		ЕСТЬNULL(Заказы.Ссылка.Соглашение.Календарь, 
											|			ЗНАЧЕНИЕ(Справочник.ПроизводственныеКалендари.ПустаяСсылка))) КАК Календарь";
		Иначе
			ТекстЗапроса = ТекстЗапроса + "
											|ЕСТЬNULL(Заказы.Ссылка.Соглашение.Календарь,
											|		ЗНАЧЕНИЕ(Справочник.ПроизводственныеКалендари.ПустаяСсылка)) КАК Календарь";
		КонецЕсли;
	Иначе
		ТекстЗапроса = ТекстЗапроса + "ЗНАЧЕНИЕ(Справочник.ПроизводственныеКалендари.ПустаяСсылка) КАК Календарь";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|
	|ПОМЕСТИТЬ ВтЭтапы
	|ИЗ (";
	
	СписокТипов = Этапы.Колонки.Заказ.ТипЗначения.Типы();
	
	Для Каждого Тип Из СписокТипов Цикл
		
		ТекстЗапроса = ТекстЗапроса + ТекстВыборкаПолей +"
		|ИЗ
		|	Документ.%ИмяТаблицы%.ЭтапыГрафикаОплаты КАК Заказы
		|ГДЕ
		|	Заказы.Ссылка В (&СписокЗаказов)
		|";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ИмяТаблицы%", Метаданные.НайтиПоТипу(Тип).Имя);
		
		Если Тип <> СписокТипов[СписокТипов.Количество()-1] Тогда
			
			ТекстЗапроса = ТекстЗапроса + "
			|ОБЪЕДИНИТЬ ВСЕ
			|"
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТекстЗапроса = ТекстЗапроса + ") КАК Заказы
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|ВЫБРАТЬ
	|	Этапы.Ссылка КАК Ссылка,
	|	СУММА(Этапы.СуммаПлатежа) КАК СуммаПлатежа,
	|	СУММА(Этапы.СуммаЗалогаЗаТару) КАК СуммаЗалогаЗаТару
	|ПОМЕСТИТЬ ВтСуммаЭтапов
	|ИЗ ВтЭтапы КАК Этапы
	|СГРУППИРОВАТЬ ПО
	|	Этапы.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Заказы.Заказ                                                                   КАК Заказ,
	|	Заказы.СверхЗаказа                                                             КАК СверхЗаказа,
	|
	|	ЕСТЬNULL(
	|		ВЫБОР КОГДА СуммаЭтапов.СуммаПлатежа = Заказы.СуммаПлатежа
	|			ТОГДА Этапы.СуммаПлатежа
	|			ИНАЧЕ Заказы.СуммаПлатежа * Этапы.ПроцентПлатежа/ 100
	|		КОНЕЦ ,0)                                                                  КАК СуммаПлатежа,
	|	ЕСТЬNULL(ВЫБОР КОГДА СуммаЭтапов.СуммаПлатежа = Заказы.СуммаВзаиморасчетов
	|			ТОГДА Этапы.СуммаПлатежа
	|			ИНАЧЕ Заказы.СуммаВзаиморасчетов * Этапы.ПроцентПлатежа / 100
	|		КОНЕЦ,0)                                                                   КАК СуммаВзаиморасчетов,
	|	ЕСТЬNULL(ВЫБОР КОГДА СуммаЭтапов.СуммаЗалогаЗаТару = Заказы.СуммаЗалогаЗаТару
	|			ТОГДА Этапы.СуммаЗалогаЗаТару
	|			ИНАЧЕ Заказы.СуммаЗалогаЗаТару * Этапы.СуммаЗалогаЗаТару/ 100
	|		КОНЕЦ,0)                                                                   КАК СуммаЗалогаЗаТару,
	|	ЕСТЬNULL(ВЫБОР КОГДА СуммаЭтапов.СуммаЗалогаЗаТару = Заказы.СуммаВзаиморасчетовПоТаре
	|			ТОГДА Этапы.СуммаЗалогаЗаТару
	|			ИНАЧЕ Заказы.СуммаВзаиморасчетовПоТаре * Этапы.ПроцентЗалогаЗаТару / 100
	|		КОНЕЦ,0)                                                                   КАК СуммаВзаиморасчетовПоТаре,
	|
	|	ЕСТЬNULL(Этапы.Сдвиг,0)                                                        КАК Сдвиг,
	|	ЕСТЬNULL(ВЫБОР КОГДА Этапы.ВариантОплаты = ЗНАЧЕНИЕ(Перечисление.ВариантыОплатыКлиентом.КредитСдвиг)
	|								ИЛИ Этапы.ВариантОплаты = ЗНАЧЕНИЕ(Перечисление.ВариантыОплатыПоставщику.КредитСдвиг)
	|			ТОГДА ДОБАВИТЬКДАТЕ(&ДатаОтгрузки, ДЕНЬ, Этапы.Сдвиг)
	|		ИНАЧЕ ДАТАВРЕМЯ(1,1,1)
	|	КОНЕЦ, &ДатаОтгрузки)                                              КАК ДатаПлатежаСдвиг,
	|	ЕСТЬNULL(ВЫБОР КОГДА Этапы.ВариантОплаты = ЗНАЧЕНИЕ(Перечисление.ВариантыОплатыКлиентом.КредитПослеОтгрузки)
	|								ИЛИ Этапы.ВариантОплаты = ЗНАЧЕНИЕ(Перечисление.ВариантыОплатыПоставщику.КредитПослеПоступления)
	|			ТОГДА ВЫБОР КОГДА Этапы.ДатаПлатежа < &ДатаОтгрузки ТОГДА &ДатаОтгрузки ИНАЧЕ Этапы.ДатаПлатежа КОНЕЦ
	|		ИНАЧЕ ДАТАВРЕМЯ(1,1,1)
	|	КОНЕЦ, &ДатаОтгрузки)                                              КАК ДатаПлатежа,
	|	ЕСТЬNULL(Этапы.Календарь, Неопределено)                            КАК Календарь
	|
	|ИЗ ВТЗаказы КАК Заказы
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтЭтапы КАК Этапы
	|		ПО Этапы.Ссылка = Заказы.Заказ
	|			И (Этапы.СуммаПлатежа > 0 И Заказы.СуммаПлатежа > 0 
	|				ИЛИ Этапы.СуммаЗалогаЗаТару > 0 И Заказы.СуммаЗалогаЗаТару > 0)
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтСуммаЭтапов КАК СуммаЭтапов
	|		ПО Заказы.Заказ = СуммаЭтапов.Ссылка
	|ГДЕ
	|	Заказы.СуммаПлатежа <> 0 ИЛИ Заказы.СуммаЗалогаЗаТару <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	*
	|ИЗ ВтЗаказы КАК Заказы
	|УПОРЯДОЧИТЬ ПО
	|	Заказы.Заказ,
	|	Заказы.СверхЗаказа
	|
	|";
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ТаблицаТовары", Параметры.ТабличнаяЧасть);
	Запрос.УстановитьПараметр("СписокЗаказов", Параметры.ТабличнаяЧасть.ВыгрузитьКолонку("Заказ"));
	Запрос.УстановитьПараметр("ДатаОтгрузки", ДатаОтгрузки);
	
	УстановитьПривилегированныйРежим(Истина);
	Результаты = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	СтрокиЭтапов = Результаты[Результаты.Количество()-2].Выгрузить();
	Заказы = Результаты[Результаты.Количество()-1].Выгрузить();
	Заказы.Индексы.Добавить("Заказ");
	
	Этапы.Очистить();
	
	Для Каждого СтрокаЗаказа Из Заказы Цикл
		СтрокиЭтаповЗаказа = СтрокиЭтапов.НайтиСтроки(Новый Структура("Заказ,СверхЗаказа",СтрокаЗаказа.Заказ,СтрокаЗаказа.СверхЗаказа));
		
		СуммаПлатежаКРаспределению = СтрокаЗаказа.СуммаПлатежа;
		СуммаЗалогаКРаспределению = СтрокаЗаказа.СуммаЗалогаЗаТару;
		СуммаВзаиморасчетовКРаспределению = СтрокаЗаказа.СуммаВзаиморасчетов;
		СуммаВзаиморасчетовПоТареКРаспределению = СтрокаЗаказа.СуммаВзаиморасчетовПоТаре;
		
		Для Каждого СтрокаЭтаповЗаказов Из СтрокиЭтаповЗаказа Цикл
			
			НовСтр = Этапы.Добавить();
			
			Если СтрокаЭтаповЗаказов = СтрокиЭтаповЗаказа[СтрокиЭтаповЗаказа.Количество()-1] Тогда
				НовСтр.СуммаПлатежа = СуммаПлатежаКРаспределению;
				НовСтр.СуммаЗалогаЗаТару = СуммаЗалогаКРаспределению;
				НовСтр.СуммаВзаиморасчетов = СуммаВзаиморасчетовКРаспределению;
				НовСтр.СуммаВзаиморасчетовПоТаре = СуммаВзаиморасчетовПоТареКРаспределению;
			Иначе
				НовСтр.СуммаПлатежа = Мин(СуммаПлатежаКРаспределению,СтрокаЭтаповЗаказов.СуммаПлатежа);
				НовСтр.СуммаЗалогаЗаТару = Мин(СуммаЗалогаКРаспределению,СтрокаЭтаповЗаказов.СуммаЗалогаЗаТару);
				НовСтр.СуммаВзаиморасчетов = Мин(СуммаВзаиморасчетовКРаспределению,СтрокаЭтаповЗаказов.СуммаВзаиморасчетов);
				НовСтр.СуммаВзаиморасчетовПоТаре = Мин(СуммаВзаиморасчетовПоТареКРаспределению,СтрокаЭтаповЗаказов.СуммаВзаиморасчетовПоТаре);
			КонецЕсли;
			
			СуммаПлатежаКРаспределению = СуммаПлатежаКРаспределению - НовСтр.СуммаПлатежа;
			СуммаЗалогаКРаспределению = СуммаЗалогаКРаспределению - НовСтр.СуммаЗалогаЗаТару;
			СуммаВзаиморасчетовКРаспределению = СуммаВзаиморасчетовКРаспределению - НовСтр.СуммаВзаиморасчетов;
			СуммаВзаиморасчетовПоТареКРаспределению = СуммаВзаиморасчетовПоТареКРаспределению - НовСтр.СуммаВзаиморасчетовПоТаре;
			
			Если НЕ ЕстьПредоплата Тогда
				НовСтр.Заказ = СтрокаЭтаповЗаказов.Заказ;
				НовСтр.СверхЗаказа = СтрокаЭтаповЗаказов.СверхЗаказа;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаЭтаповЗаказов.ДатаПлатежаСдвиг) Тогда
				Если ЗначениеЗаполнено(СтрокаЭтаповЗаказов.Календарь) Тогда
					НовСтр.ДатаПлатежа = КалендарныеГрафики.ДатаПоКалендарю(СтрокаЭтаповЗаказов.Календарь, ДатаОтгрузки, СтрокаЭтаповЗаказов.Сдвиг);
				Иначе
					НовСтр.ДатаПлатежа = СтрокаЭтаповЗаказов.ДатаПлатежаСдвиг;
				КонецЕсли;
			ИначеЕсли ЗначениеЗаполнено(СтрокаЭтаповЗаказов.ДатаПлатежа) Тогда
				НовСтр.ДатаПлатежа = СтрокаЭтаповЗаказов.ДатаПлатежа;
			КонецЕсли;
			
			Если НовСтр.ДатаПлатежа = Дата(1,1,1) И ЕстьПредоплата Тогда
				НовСтр.ВариантОплаты = ВариантОплатыПредоплата;
				СуммаОбязательнойПредоплаты = СуммаОбязательнойПредоплаты - НовСтр.СуммаПлатежа - НовСтр.СуммаЗалогаЗаТару;
			ИначеЕсли СтрокаЭтаповЗаказов.Сдвиг > 0 Тогда
				НовСтр.ВариантОплаты = ВариантОплатыСдвиг;
				НовСтр.Сдвиг = СтрокаЭтаповЗаказов.Сдвиг;
			Иначе
				НовСтр.ВариантОплаты = ВариантОплатыКредит;
			КонецЕсли;
			
			Если НовСтр.ДатаПлатежа < НачалоДня(Параметры.Дата) Тогда
				НовСтр.ДатаПлатежа = Параметры.Дата;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если ЕстьПредоплата Тогда
		Этапы.Свернуть("ВариантОплаты,ДатаПлатежа,Сдвиг",
			"СуммаПлатежа,СуммаЗалогаЗаТару,СуммаВзаиморасчетов,СуммаВзаиморасчетовПоТаре");
		Если СуммаОбязательнойПредоплаты > 0 Тогда
			ПроверитьСкорректироватьСуммуОбязательнойПредоплаты(СуммаОбязательнойПредоплаты,Этапы, ВариантОплатыПредоплата);
		КонецЕсли;
	Иначе
		Этапы.Свернуть("Заказ,СверхЗаказа,ВариантОплаты,ДатаПлатежа,Сдвиг",
			"СуммаПлатежа,СуммаЗалогаЗаТару,СуммаВзаиморасчетов,СуммаВзаиморасчетовПоТаре");
	КонецЕсли;
	
	Этапы.Сортировать("ДатаПлатежа Возр");
	
	//При упрощенной и заказе как счет сворачиваем до 1 даты, т.к. ни заказов ни предоплаты там быть не может.
	Если Параметры.УпрощеннаяСхема Тогда
		
		МаксКредит = Дата(1,1,1);
		МаксПредоплата = Дата(1,1,1);
		Для Каждого Стр Из Этапы Цикл
			Если Стр.ВариантОплаты = ВариантОплатыСдвиг ИЛИ Стр.ВариантОплаты = ВариантОплатыКредит Тогда
				МаксКредит = Макс(Стр.ДатаПлатежа, МаксКредит);
			Иначе
				МаксПредоплата = Макс(Стр.ДатаПлатежа, МаксПредоплата);
			КонецЕсли;
		КонецЦикла;
		
		Если Параметры.ЗаказКакСчет Тогда
			
			Для Каждого Стр Из Этапы Цикл
				Стр.ВариантОплаты = ВариантОплатыКредит;
				Стр.ДатаПлатежа = МаксКредит;
			КонецЦикла;
			Этапы.Свернуть("ВариантОплаты,ДатаПлатежа",
				"СуммаПлатежа,СуммаЗалогаЗаТару,СуммаВзаиморасчетов,СуммаВзаиморасчетовПоТаре");
		//Накладная и упрощенный режим
		ИначеЕсли Параметры.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоЗаказам Тогда
			
			Для Каждого Стр Из Этапы Цикл
				Если Стр.ВариантОплаты = ВариантОплатыСдвиг ИЛИ Стр.ВариантОплаты = ВариантОплатыКредит Тогда
					Стр.ВариантОплаты = ВариантОплатыКредит;
					Стр.ДатаПлатежа = МаксКредит;
				Иначе
					Стр.ВариантОплаты = ВариантОплатыПредоплата;
					Стр.ДатаПлатежа = МаксПредоплата;
				КонецЕсли;
			КонецЦикла;
			
			Этапы.Свернуть("Заказ,ВариантОплаты,ДатаПлатежа",
				"СуммаПлатежа,СуммаЗалогаЗаТару,СуммаВзаиморасчетов,СуммаВзаиморасчетовПоТаре");
		ИначеЕсли Параметры.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоЗаказамНакладным Тогда
			
			Для Каждого Стр Из Этапы Цикл
				Если Стр.ВариантОплаты = ВариантОплатыСдвиг ИЛИ Стр.ВариантОплаты = ВариантОплатыКредит Тогда
					Стр.ВариантОплаты = ВариантОплатыКредит;
					Стр.ДатаПлатежа = МаксКредит;
				Иначе
					Стр.ВариантОплаты = ВариантОплатыПредоплата;
					Стр.ДатаПлатежа = МаксПредоплата;
				КонецЕсли;
			КонецЦикла;
			
			Этапы.Свернуть("ВариантОплаты,ДатаПлатежа",
				"СуммаПлатежа,СуммаЗалогаЗаТару,СуммаВзаиморасчетов,СуммаВзаиморасчетовПоТаре");
		КонецЕсли;
			
	КонецЕсли;
	
	ЭтапыГрафикаОплаты.Загрузить(Этапы);
	
	ЗаполнитьПроцентыПоСуммам(ЭтапыГрафикаОплаты);
	
КонецПроцедуры

//Распределяет суммы документа по процентам графика
// 
// Параметры:
// 	Параметры - Структура - Параметры распределения:
//		* ЭтапыГрафикаОплаты          - ТаблицаЗначений - Этапы графика.
Процедура РаспределитьСуммыЭтаповОплатыДокументаПоЗаказам(Параметры) Экспорт
	
	Запрос   = Новый Запрос;
	Менеджер = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = Менеджер;
	
	Суммы = Параметры.ТабличнаяЧасть;
	ТребуетсяПерезаполнение = Ложь;
	
	ЭтапыГрафикаОплаты = Параметры.ЭтапыГрафикаОплаты;
	Если НЕ ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭтапыГрафикаОплаты, "Колонки") Тогда
		Этапы = ЭтапыГрафикаОплаты.Выгрузить();
	Иначе
		Этапы = ЭтапыГрафикаОплаты;
	КонецЕсли;
	
	ЕстьТара = Этапы.Колонки.Найти("СуммаЗалогаЗаТару") <> Неопределено;
	
	ТребуетсяПерезаполнение = Ложь;
	Для Каждого СтрокаЭтапов Из Параметры.ЭтапыГрафикаОплаты Цикл
		СтрокиСумм = Суммы.НайтиСтроки(Новый Структура("Заказ, СверхЗаказа", СтрокаЭтапов.Заказ, СтрокаЭтапов.СверхЗаказа));
		Если СтрокиСумм.Количество() = 0 Тогда
			ТребуетсяПерезаполнение = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаСумм Из Суммы Цикл
		СтрокиГрафика = Параметры.ЭтапыГрафикаОплаты.НайтиСтроки(Новый Структура("Заказ, СверхЗаказа", СтрокаСумм.Заказ, СтрокаСумм.СверхЗаказа));
		Если СтрокиГрафика.Количество() = 0 Тогда
			ТребуетсяПерезаполнение = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ТребуетсяПерезаполнение Тогда
		ЗаполнитьЭтапыОплатыДокументаПоЗаказам(Параметры);
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаСумм Из Суммы Цикл
		
		СтрокиГрафика = Параметры.ЭтапыГрафикаОплаты.НайтиСтроки(Новый Структура("Заказ, СверхЗаказа", СтрокаСумм.Заказ, СтрокаСумм.СверхЗаказа));
		
		СуммаПлатежа              = СтрокаСумм.СуммаПлатежа;
		СуммаВзаиморасчетов       = СтрокаСумм.СуммаВзаиморасчетов;
		СуммаЗалогаЗаТару         = СтрокаСумм.СуммаЗалогаЗаТару;
		СуммаВзаиморасчетовПоТаре = СтрокаСумм.СуммаВзаиморасчетовПоТаре;
		
		ВалютыСовпадают = СуммаПлатежа = СуммаВзаиморасчетов;
		
		СуммаПроцентовПлатежа = 0;
		СуммаПроцентовЗалога = 0;
		Для Каждого СтрокаГрафика Из СтрокиГрафика Цикл
			СуммаПроцентовПлатежа = СуммаПроцентовПлатежа + СтрокаГрафика.ПроцентПлатежа;
			Если ЕстьТара Тогда
				СуммаПроцентовЗалога = СуммаПроцентовЗалога + СтрокаГрафика.ПроцентЗалогаЗаТару;
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого СтрокаГрафика Из СтрокиГрафика Цикл
			
			ЭтоПоследняяСтрока = СтрокаГрафика = СтрокиГрафика[СтрокиГрафика.Количество()-1];
			
			Если СуммаПлатежа <> 0 Тогда
				Если ЭтоПоследняяСтрока Тогда
					СтрокаГрафика.СуммаПлатежа = СуммаПлатежа;
				Иначе
					СтрокаГрафика.СуммаПлатежа = СтрокаГрафика.ПроцентПлатежа / СуммаПроцентовПлатежа * СтрокаСумм.СуммаПлатежа;
				КонецЕсли;
				СуммаПлатежа = СуммаПлатежа - СтрокаГрафика.СуммаПлатежа;
			Иначе
				СтрокаГрафика.СуммаПлатежа = 0;
			КонецЕсли;
			
			Если СуммаВзаиморасчетов <> 0 Тогда
				Если ВалютыСовпадают Тогда
					СтрокаГрафика.СуммаВзаиморасчетов = СтрокаГрафика.СуммаПлатежа;
				ИначеЕсли ЭтоПоследняяСтрока Тогда
					СтрокаГрафика.СуммаВзаиморасчетов = СуммаВзаиморасчетов;
				Иначе
					СтрокаГрафика.СуммаВзаиморасчетов = СтрокаГрафика.СуммаПлатежа / СтрокаСумм.СуммаПлатежа * СуммаВзаиморасчетов;
				КонецЕсли;
				СуммаВзаиморасчетов = СуммаВзаиморасчетов - СтрокаГрафика.СуммаВзаиморасчетов;
			Иначе
				СтрокаГрафика.СуммаВзаиморасчетов = 0;
			КонецЕсли;
			
			Если СуммаЗалогаЗаТару <> 0 Тогда
				Если ЭтоПоследняяСтрока Тогда
					СтрокаГрафика.СуммаЗалогаЗаТару = СуммаЗалогаЗаТару;
				Иначе
					СтрокаГрафика.СуммаЗалогаЗаТару = СтрокаГрафика.ПроцентЗалогаЗаТару / СуммаПроцентовЗалога * СуммаЗалогаЗаТару;
				КонецЕсли;
				СуммаЗалогаЗаТару = СуммаЗалогаЗаТару - СтрокаГрафика.СуммаЗалогаЗаТару;
			ИначеЕсли ЕстьТара Тогда
				СтрокаГрафика.СуммаЗалогаЗаТару = 0;
			КонецЕсли;
			
			Если СуммаВзаиморасчетовПоТаре <> 0 Тогда
				Если ВалютыСовпадают Тогда
					СтрокаГрафика.СуммаВзаиморасчетовПоТаре = СтрокаГрафика.СуммаЗалогаЗаТару;
				ИначеЕсли ЭтоПоследняяСтрока Тогда
					СтрокаГрафика.СуммаВзаиморасчетовПоТаре = СуммаВзаиморасчетовПоТаре;
				Иначе
					СтрокаГрафика.СуммаВзаиморасчетовПоТаре = СтрокаГрафика.СуммаЗалогаЗаТару / СтрокаСумм.СуммаЗалогаЗаТару * СуммаВзаиморасчетовПоТаре;
				КонецЕсли;
				СуммаВзаиморасчетовПоТаре = СуммаВзаиморасчетовПоТаре - СтрокаГрафика.СуммаВзаиморасчетовПоТаре;
			ИначеЕсли ЕстьТара Тогда
				СтрокаГрафика.СуммаВзаиморасчетовПоТаре = 0;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

//Сворачивает табличную часть ЭтапыГрафикаОплаты по датам
Процедура СвернутьЭтапыОплаты(ЭтапыГрафикаОплаты, ЕстьПредоплата = Ложь) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ЭтапыГрафикаОплаты.ДатаПлатежа КАК ДатаПлатежа,
	|	ЭтапыГрафикаОплаты.Сдвиг КАК Сдвиг,
	|	ЭтапыГрафикаОплаты.ВариантОплаты КАК ВариантОплаты,
	|	ЭтапыГрафикаОплаты.СуммаПлатежа КАК СуммаПлатежа,
	|	ЭтапыГрафикаОплаты.ПроцентПлатежа КАК ПроцентПлатежа,
	|	ЭтапыГрафикаОплаты.СуммаЗалогаЗаТару КАК СуммаЗалогаЗаТару,
	|	ЭтапыГрафикаОплаты.ПроцентЗалогаЗаТару КАК ПроцентЗалогаЗаТару,
	|	ЭтапыГрафикаОплаты.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ЭтапыГрафикаОплаты.СуммаВзаиморасчетовПоТаре КАК СуммаВзаиморасчетовПоТаре
	|ПОМЕСТИТЬ
	|	ЭтапыГрафикаОплаты
	|ИЗ
	|	&ЭтапыГрафикаОплаты КАК ЭтапыГрафикаОплаты
	|;
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА &ЕстьПредоплата И ЭтапыГрафикаОплаты.ВариантОплаты В (ЗНАЧЕНИЕ(Перечисление.ВариантыОплатыКлиентом.АвансДоОбеспечения),
	|																	ЗНАЧЕНИЕ(Перечисление.ВариантыОплатыКлиентом.ПредоплатаДоОтгрузки))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыОплатыКлиентом.ПредоплатаДоОтгрузки)
	|		КОГДА &ЕстьПредоплата И ЭтапыГрафикаОплаты.ВариантОплаты В (ЗНАЧЕНИЕ(Перечисление.ВариантыОплатыПоставщику.АвансДоПодтверждения),
	|																	ЗНАЧЕНИЕ(Перечисление.ВариантыОплатыПоставщику.ПредоплатаДоПоступления))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыОплатыПоставщику.ПредоплатаДоПоступления)
	|		ИНАЧЕ ЭтапыГрафикаОплаты.ВариантОплаты
	|	КОНЕЦ КАК ВариантОплаты,
	|	МАКСИМУМ(ЭтапыГрафикаОплаты.ДатаПлатежа) КАК ДатаПлатежа,
	|	МАКСИМУМ(ЭтапыГрафикаОплаты.Сдвиг) КАК Сдвиг,
	|	СУММА(ЭтапыГрафикаОплаты.СуммаПлатежа) КАК СуммаПлатежа,
	|	СУММА(ЭтапыГрафикаОплаты.ПроцентПлатежа) КАК ПроцентПлатежа,
	|	СУММА(ЭтапыГрафикаОплаты.СуммаЗалогаЗаТару) КАК СуммаЗалогаЗаТару,
	|	СУММА(ЭтапыГрафикаОплаты.ПроцентЗалогаЗаТару) КАК ПроцентЗалогаЗаТару,
	|	СУММА(ЭтапыГрафикаОплаты.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов,
	|	СУММА(ЭтапыГрафикаОплаты.СуммаВзаиморасчетовПоТаре) КАК СуммаВзаиморасчетовПоТаре
	|ИЗ
	|	ЭтапыГрафикаОплаты КАК ЭтапыГрафикаОплаты
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР КОГДА &ЕстьПредоплата И ЭтапыГрафикаОплаты.ВариантОплаты В (ЗНАЧЕНИЕ(Перечисление.ВариантыОплатыКлиентом.АвансДоОбеспечения),
	|																	ЗНАЧЕНИЕ(Перечисление.ВариантыОплатыКлиентом.ПредоплатаДоОтгрузки))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыОплатыКлиентом.ПредоплатаДоОтгрузки)
	|		КОГДА &ЕстьПредоплата И ЭтапыГрафикаОплаты.ВариантОплаты В (ЗНАЧЕНИЕ(Перечисление.ВариантыОплатыПоставщику.АвансДоПодтверждения),
	|																	ЗНАЧЕНИЕ(Перечисление.ВариантыОплатыПоставщику.ПредоплатаДоПоступления))
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыОплатыПоставщику.ПредоплатаДоПоступления)
	|		ИНАЧЕ ЭтапыГрафикаОплаты.ВариантОплаты
	|	КОНЕЦ
	|");
	
	Запрос.УстановитьПараметр("ЭтапыГрафикаОплаты", ?(ТипЗнч(ЭтапыГрафикаОплаты) = Тип("ТаблицаЗначений"), ЭтапыГрафикаОплаты, ЭтапыГрафикаОплаты.Выгрузить()));
	Запрос.УстановитьПараметр("ЕстьПредоплата", ЕстьПредоплата);
	Выгрузка = Запрос.Выполнить().Выгрузить();
	ЭтапыГрафикаОплаты.Загрузить(Выгрузка);
	
КонецПроцедуры

// Процедура проверяет заполнение и корректность даты платежа в документе.
//
// Параметры:
//		ДатаПлатежа - Дата - дата платежа проверяемого документа
//		ДатаДокумента - Дата - дата проверяемого документа 
//		Отказ - Булево - Признак отказа от продолжения работы.
//
Процедура ПроверитьЗаполнениеКорректностьДатыПлатежа(ДатаПлатежа, ДатаДокумента, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(ДатаПлатежа) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Поле ""Дата платежа"" не заполнено';
				|en = '""Payment date"" is required'"),
			,
			"ДатаПлатежа",
			"Объект",
			Отказ);
		
	ИначеЕсли ДатаПлатежа < НачалоДня(ДатаДокумента) Тогда
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Дата платежа должна быть не меньше даты документа %1';
				|en = 'Payment date must be not less than the %1 document date'"),
			Формат(ДатаДокумента, "ДЛФ=DD"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			,
			"ДатаПлатежа",
			"Объект",
			Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполняет проценты по уже рассчитанным суммам в таблице этапов графиков оплаты.
// 
// Параметры:
// 	ЭтапыГрафикаОплаты - ТаблицаЗначений,ДанныеФормыКоллекция - этапы, в которых надо заполнить проценты.
//
Процедура ЗаполнитьПроцентыПоСуммам(ЭтапыГрафикаОплаты) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭтапыГрафикаОплаты, "Колонки") Тогда
		ЕстьТара = ЭтапыГрафикаОплаты.Колонки.Найти("СуммаЗалогаЗаТару") <> Неопределено;
	Иначе
		ЕстьТара = ЭтапыГрафикаОплаты.Выгрузить().Колонки.Найти("СуммаЗалогаЗаТару") <> Неопределено;
	КонецЕсли;
	
	СуммаПлатежей            = ЭтапыГрафикаОплаты.Итог("СуммаПлатежа");
	СуммаПлатежейПоЗалогу    = ?(ЕстьТара, ЭтапыГрафикаОплаты.Итог("СуммаЗалогаЗаТару"),0);
	
	ПоследняяСтрокаЗалога      = -1;
	
	СуммаПроцентов             = 100;
	СуммаПроцентовПоЗалогу     = ?(ЕстьТара,100,0);
	
	Сч = 0;
	Пока Сч < ЭтапыГрафикаОплаты.Количество() Цикл
		
		Если СуммаПлатежей <> 0 Тогда
			ТекущийПроцент = ЭтапыГрафикаОплаты[Сч].СуммаПлатежа / СуммаПлатежей * 100;
			
			ЭтапыГрафикаОплаты[Сч].ПроцентПлатежа = ?(Сч = ЭтапыГрафикаОплаты.Количество()-1 ИЛИ СуммаПроцентов - ТекущийПроцент< 0,
																СуммаПроцентов,
																ТекущийПроцент);
			
			СуммаПроцентов = СуммаПроцентов - ЭтапыГрафикаОплаты[Сч].ПроцентПлатежа;
		КонецЕсли;
	
		Если ЕстьТара И ЭтапыГрафикаОплаты[Сч].СуммаЗалогаЗаТару <> 0 Тогда
			ЭтапыГрафикаОплаты[Сч].ПроцентЗалогаЗаТару = ЭтапыГрафикаОплаты[Сч].СуммаЗалогаЗаТару/СуммаПлатежейПоЗалогу*100;
			СуммаПроцентовПоЗалогу = СуммаПроцентовПоЗалогу - ЭтапыГрафикаОплаты[Сч].ПроцентЗалогаЗаТару;
			ПоследняяСтрокаЗалога = Сч;
		КонецЕсли;
		
		Сч = Сч + 1;
	КонецЦикла;
	
	Если ПоследняяСтрокаЗалога <> -1 Тогда
		ЭтапыГрафикаОплаты[ПоследняяСтрокаЗалога].ПроцентЗалогаЗаТару = 
			ЭтапыГрафикаОплаты[ПоследняяСтрокаЗалога].ПроцентЗалогаЗаТару + СуммаПроцентовПоЗалогу;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеДокументовПродажи

// Заполняет этапы графика оплаты или распределяет уже заполненную сумму в документах продажи.
//
// Параметры:
//
// 		Объект - ДокументОбъект - документ, для которого заполняются этапы графика оплаты
// 		ГрафикСоглашенияЗаполнен - Булево - флаг возможности заполнения по графику указанном в соглашении
// 		ГрафикЗаполнен - Булево - флаг возможности заполнения по шаблону графика оплаты, указанном в документе
// 		СуммаОплаты - Число - сумма, распределяющаяся по этапам графика оплаты (без залога за тару)
// 		СуммаЗалога - Число - сумма залога, распределяющаяся по этапам графика оплаты
// 		ЗаполнятьФормуОплаты - Булево - признак, указывающий на необходимость заполнения формы оплаты из соглашения или графика
// 		ДатаПлатежаПоУмолчанию - Дата - Дата платежа для добавления строки по умолчанию.
//
Процедура ЗаполнитьЭтапыОплатыДокументаПродажи(Объект, ГрафикСоглашенияЗаполнен, ГрафикЗаполнен, СуммаОплаты,
	СуммаЗалога = 0, ЗаполнятьФормуОплаты = Ложь, ДатаПлатежаПоУмолчанию = Неопределено) Экспорт
	
	Если ГрафикСоглашенияЗаполнен Тогда
		ЗаполнитьЭтапыОплатыДокументаПродажиПоСоглашению(
			Объект,
			СуммаОплаты,
			СуммаЗалога,
			ЗаполнятьФормуОплаты);
	ИначеЕсли ГрафикЗаполнен Тогда
		ЗаполнитьЭтапыОплатыДокументаПродажиПоГрафикуОплаты(
			Объект,
			СуммаОплаты,
			СуммаЗалога,
			ЗаполнятьФормуОплаты);
	ИначеЕсли Объект.ЭтапыГрафикаОплаты.Количество() > 0 Тогда
		ЭтапыОплатыКлиентСервер.РаспределитьСуммуПоЭтапамГрафикаОплаты(
			Объект.ЭтапыГрафикаОплаты,
			СуммаОплаты,
			СуммаЗалога);
	ИначеЕсли Не (ГрафикЗаполнен Или ГрафикСоглашенияЗаполнен) Тогда
		НовСтр = Объект.ЭтапыГрафикаОплаты.Добавить();
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(НовСтр, "ВариантОплаты") Тогда
			НовСтр.ВариантОплаты = Перечисления.ВариантыОплатыКлиентом.КредитСдвиг;
		КонецЕсли;
		НовСтр.ДатаПлатежа  = ДатаПлатежаПоУмолчанию;
		НовСтр.СуммаПлатежа = СуммаОплаты;
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(НовСтр, "СуммаЗалогаЗаТару") Тогда
			НовСтр.СуммаЗалогаЗаТару     = СуммаЗалога;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет этапы графика оплаты в документе продажи по графику указанном в соглашении.
//
// Параметры:
// 		Объект - ДокументОбъект - документ, в котором необходимо заполнить этапы оплаты
// 		СуммаКРаспределениюОплаты - Число - сумма платежей, распределяющаяся по этапам графика оплаты
// 		СуммаКРаспределениюЗалога - Число - сумма залога за тару, распределяющаяся по этапам графика оплаты
// 		ЗаполнятьФормуОплаты - Булево - признак, указывающий на необходимость заполнения формы оплаты
// 			в документе формой оплаты по графику, указанной в соглашении.
// 		ЗаполнятьСНулевойСуммой - Булево - Если передана нулевая сумма то в ТЧ будет добавлена строка.
//
Процедура ЗаполнитьЭтапыОплатыДокументаПродажиПоСоглашению(Объект,
	                                              Знач СуммаКРаспределениюОплаты,
	                                              Знач СуммаКРаспределениюЗалога = 0,
	                                              ЗаполнятьФормуОплаты = Ложь,
	                                              ЗаполнятьСНулевойСуммой = Ложь) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СоглашенияСКлиентамиЭтапыГрафикаОплаты.НомерСтроки КАК НомерСтроки,
	|	СоглашенияСКлиентамиЭтапыГрафикаОплаты.ВариантОплаты КАК ВариантОплаты,
	|	СоглашенияСКлиентамиЭтапыГрафикаОплаты.Сдвиг КАК Сдвиг,
	|	СоглашенияСКлиентамиЭтапыГрафикаОплаты.ПроцентПлатежа КАК ПроцентПлатежа,
	|	СоглашенияСКлиентамиЭтапыГрафикаОплаты.ПроцентЗалогаЗаТару КАК ПроцентЗалогаЗаТару,
	|	СоглашенияСКлиентамиЭтапыГрафикаОплаты.Ссылка.ФормаОплаты КАК ФормаОплаты,
	|	СоглашенияСКлиентамиЭтапыГрафикаОплаты.Ссылка.Календарь КАК Календарь
	|ИЗ
	|	Справочник.СоглашенияСКлиентами.ЭтапыГрафикаОплаты КАК СоглашенияСКлиентамиЭтапыГрафикаОплаты
	|ГДЕ
	|	СоглашенияСКлиентамиЭтапыГрафикаОплаты.Ссылка = &Соглашение
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки");
		
	Запрос.УстановитьПараметр("Соглашение", Объект.Соглашение);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выгрузка         = РезультатЗапроса.Выгрузить();
	
	Если (СуммаКРаспределениюОплаты = 0 И СуммаКРаспределениюЗалога = 0 И НЕ ЗаполнятьСНулевойСуммой)
		ИЛИ Выгрузка.Количество() = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ЗаполнятьФормуОплаты Тогда
		
		ФормаОплаты = Выгрузка[0].ФормаОплаты;
		
		Если ЗначениеЗаполнено(ФормаОплаты) И Объект.ФормаОплаты <> ФормаОплаты Тогда
			Объект.ФормаОплаты = ФормаОплаты;
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьЭтапыОплатыДокументаПродажиПоШаблону(Объект,
		СуммаКРаспределениюОплаты,
		СуммаКРаспределениюЗалога,
		Выгрузка,
		Выгрузка[0].Календарь);
	
КонецПроцедуры

// Заполняет авансовые этапы графика оплаты в документе ЗаказКлиента
//
// Параметры:
//
// 		Объект - ДокументОбъект - документ, в котором необходимо заполнить этапы оплаты
// 		СуммаКРаспределениюОплаты - Число - сумма документа, в котором необходимо осуществить проверку
// 		СуммаКРаспределениюЗалога - Число - сумма залога по документу, в котором необходимо осуществить проверку
// 		ЗаполнятьФормуОплаты - Булево - признак, указывающий на необходимость заполнения формы оплаты
// 			в документе формой оплаты по графику.
// 		ЗаполнятьСНулевойСуммой - Булево - если передана сумма равная нулю, то в ТЧ будет все равно добавлена строка.
//
Процедура ЗаполнитьЭтапыОплатыДокументаПродажиПоГрафикуОплаты(Объект,
	                                  Знач СуммаКРаспределениюОплаты,
	                                  Знач СуммаКРаспределениюЗалога = 0,
	                                  ЗаполнятьФормуОплаты = Ложь,
	                                  ЗаполнятьСНулевойСуммой = Ложь) Экспорт
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	ГрафикиОплатыЭтапы.НомерСтроки         КАК НомерСтроки,
		|	ГрафикиОплатыЭтапы.ВариантОплаты       КАК ВариантОплаты,
		|	ГрафикиОплатыЭтапы.Сдвиг               КАК Сдвиг,
		|	ГрафикиОплатыЭтапы.ПроцентПлатежа      КАК ПроцентПлатежа,
		|	ГрафикиОплатыЭтапы.ПроцентЗалогаЗаТару КАК ПроцентЗалогаЗаТару,
		|	ГрафикиОплатыЭтапы.Ссылка.Календарь    КАК Календарь,
		|	ГрафикиОплатыЭтапы.Ссылка.ФормаОплаты  КАК ФормаОплаты
		|ИЗ
		|	Справочник.ГрафикиОплаты.Этапы КАК ГрафикиОплатыЭтапы
		|ГДЕ
		|	ГрафикиОплатыЭтапы.Ссылка = &ГрафикОплаты
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки ВОЗР
		|");
		
	Запрос.УстановитьПараметр("ГрафикОплаты",Объект.ГрафикОплаты);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выгрузка         = РезультатЗапроса.Выгрузить();
	
	Если Выгрузка.Количество() = 0 Тогда
		
		Объект.ЭтапыГрафикаОплаты.Очистить();
		Возврат;
		
	КонецЕсли;
	
	Если ЗаполнятьФормуОплаты Тогда
		
		ФормаОплаты = Выгрузка[0].ФормаОплаты;
		
		Если ЗначениеЗаполнено(ФормаОплаты) И Объект.ФормаОплаты <> ФормаОплаты Тогда
			Объект.ФормаОплаты = ФормаОплаты;
		КонецЕсли;
		
	КонецЕсли;
	
	Если СуммаКРаспределениюОплаты = 0 И СуммаКРаспределениюЗалога = 0 И Не ЗаполнятьСНулевойСуммой Тогда
		
		Объект.ЭтапыГрафикаОплаты.Очистить();
		Возврат;
		
	КонецЕсли;
	
	ЗаполнитьЭтапыОплатыДокументаПродажиПоШаблону(
		Объект,
		СуммаКРаспределениюОплаты,
		СуммаКРаспределениюЗалога,
		Выгрузка,
		Выгрузка[0].Календарь);
	
КонецПроцедуры

// Заполняет этапы графика оплаты в документе продажи по таблице значений
//
// Параметры:
//		Объект - ДокументОбъект - документ,  в котором необходимо заполнить этапы оплаты
//		СуммаКРаспределениюОплаты - Число - сумма платежей, распределяющаяся по этапам графика оплаты
//		СуммаКРаспределениюЗалога - Число - сумма залога за тару, распределяющаяся по этапам графика оплаты
//		ШаблонГрафика - ТаблицаЗначений - ТЗ по которой необходимо заполнить этапы графика оплаты
//		Календарь - СправочникСсылка.Календари - по которому вычисляются даты.
//
Процедура ЗаполнитьЭтапыОплатыДокументаПродажиПоШаблону(Объект,
	                                           Знач СуммаКРаспределениюОплаты,
	                                           Знач СуммаКРаспределениюЗалога,
	                                           ШаблонГрафика,
	                                           Знач Календарь) Экспорт
	
	ЭтапыГрафикаОплаты = Новый ТаблицаЗначений();
	
	ЭтапыГрафикаОплаты.Колонки.Добавить("ВариантОплаты");
	ЭтапыГрафикаОплаты.Колонки.Добавить("ДатаПлатежа");
	ЭтапыГрафикаОплаты.Колонки.Добавить("ПроцентПлатежа");
	ЭтапыГрафикаОплаты.Колонки.Добавить("СуммаПлатежа");
	ЭтапыГрафикаОплаты.Колонки.Добавить("ПроцентЗалогаЗаТару");
	ЭтапыГрафикаОплаты.Колонки.Добавить("СуммаЗалогаЗаТару");
	ЭтапыГрафикаОплаты.Колонки.Добавить("Сдвиг");

	РаспределеннаяСуммаОплаты = 0;
	РаспределеннаяСуммаЗалога = 0;
	ТекущийЭтап               = 0;
	ОдинДень                  = 86400;
	
	КоличествоЭтапов = ШаблонГрафика.Количество();
	
	ДатаДокумента = ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДатаСеанса());
	ЖелаемаяДатаОтгрузки = ?(ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(Объект, "ЖелаемаяДатаОтгрузки"), 
		Объект.ЖелаемаяДатаОтгрузки, Неопределено);
	НеОтгружатьЧастями = ?(ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(Объект, "НеОтгружатьЧастями"),Объект.НеОтгружатьЧастями,Ложь);
	ЖелаемаяДатаОтгрузки = ?(ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(Объект, "ДатаОтгрузки") И НеОтгружатьЧастями, 
		Объект.ДатаОтгрузки, ЖелаемаяДатаОтгрузки);
	ДатаРеализации = ЖелаемаяДатаОтгрузки;
	
	// Определим календарную дату для каждого этапа графика оплаты
	УчитыватьКалендарь = Ложь;
	
	Если ЗначениеЗаполнено(Календарь) Тогда
		
		УчитыватьКалендарь = Истина;
		
		СдвигиАвансовыхЭтапов = Новый Массив();
		СдвигиКредитныхЭтапов = Новый Массив();
		МассивДатПоКалендарю  = Новый Массив();
		
		Для Каждого Этап Из ШаблонГрафика Цикл
			
			Если Этап.ВариантОплаты = Перечисления.ВариантыОплатыКлиентом.ПредоплатаДоОтгрузки Или
				Этап.ВариантОплаты = Перечисления.ВариантыОплатыКлиентом.АвансДоОбеспечения Тогда
				СдвигиАвансовыхЭтапов.Добавить(Этап.Сдвиг);
			Иначе
				СдвигиКредитныхЭтапов.Добавить(Этап.Сдвиг);
			КонецЕсли;
			
		КонецЦикла;
		
		Если СдвигиАвансовыхЭтапов.Количество() > 0 Тогда
			
			МассивДатПоКалендарюАвансовыхЭтапов = КалендарныеГрафики.ДатыПоКалендарю(Календарь, ДатаДокумента, СдвигиАвансовыхЭтапов);
			
			Для Каждого ДатаПоКалендарю Из МассивДатПоКалендарюАвансовыхЭтапов Цикл
				МассивДатПоКалендарю.Добавить(ДатаПоКалендарю);
			КонецЦикла;
			
		КонецЕсли;
		
		Если СдвигиКредитныхЭтапов.Количество() > 0 Тогда
			
			Если Не ЗначениеЗаполнено(ДатаРеализации) Тогда
				
				Если СдвигиАвансовыхЭтапов.Количество() > 0 Тогда
					Если МассивДатПоКалендарюАвансовыхЭтапов.Количество() > 0 Тогда
						ДатаРеализации = МассивДатПоКалендарюАвансовыхЭтапов[МассивДатПоКалендарюАвансовыхЭтапов.Количество()-1];
					КонецЕсли;
				Иначе
					ДатаРеализации = ДатаДокумента;
				КонецЕсли;
				
			КонецЕсли;
			
			МассивДатПоКалендарюКредитныхЭтапов = КалендарныеГрафики.ДатыПоКалендарю(Календарь, ДатаРеализации, СдвигиКредитныхЭтапов);
			
			Для Каждого ДатаПоКалендарю Из МассивДатПоКалендарюКредитныхЭтапов Цикл
				МассивДатПоКалендарю.Добавить(ДатаПоКалендарю);
			КонецЦикла;
			
		КонецЕсли;
		
	Иначе
		
		Если Не ЗначениеЗаполнено(ДатаРеализации) Тогда
			
			МаксСдвигАванса = 0;
		
			Для Каждого ТекЭтап Из ШаблонГрафика Цикл
				
				Если ТекЭтап.ВариантОплаты = Перечисления.ВариантыОплатыКлиентом.ПредоплатаДоОтгрузки Или
					ТекЭтап.ВариантОплаты = Перечисления.ВариантыОплатыКлиентом.АвансДоОбеспечения Тогда
					
					МаксСдвигАванса = Макс(МаксСдвигАванса, ТекЭтап.Сдвиг);
					
				КонецЕсли;
				
			КонецЦикла;
			
			ДатаРеализации = ДатаДокумента + МаксСдвигАванса * ОдинДень;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Определим последний незалоговый этап
	ПоследнийНезалоговыйЭтап = КоличествоЭтапов;
	Пока ПоследнийНезалоговыйЭтап <> 0 И ШаблонГрафика[ПоследнийНезалоговыйЭтап-1].ПроцентПлатежа = 0 Цикл
		ПоследнийНезалоговыйЭтап = ПоследнийНезалоговыйЭтап - 1;
	КонецЦикла;
	
	// Определим последний залоговый этап
	ПоследнийЗалоговыйЭтап = КоличествоЭтапов;
	Пока ПоследнийЗалоговыйЭтап <> 0 И ШаблонГрафика[ПоследнийЗалоговыйЭтап-1].ПроцентЗалогаЗаТару = 0 Цикл
		ПоследнийЗалоговыйЭтап = ПоследнийЗалоговыйЭтап - 1;
	КонецЦикла;
	
	// Заполним этапы в соответствии с графиком оплаты
	Объект.ЭтапыГрафикаОплаты.Очистить();
	Для Каждого Этап Из ШаблонГрафика Цикл
		
		ТекущийЭтап                     = ТекущийЭтап + 1;
		ЭтапГрафикаОплаты               = ЭтапыГрафикаОплаты.Добавить();
		ЭтапГрафикаОплаты.ВариантОплаты = Этап.ВариантОплаты;
		
		Если УчитыватьКалендарь Тогда
			ДатаПлатежа = МассивДатПоКалендарю[ТекущийЭтап-1];
		Иначе
			ДатаПлатежа = ?(ЭтапГрафикаОплаты.ВариантОплаты = Перечисления.ВариантыОплатыКлиентом.КредитПослеОтгрузки
							ИЛИ ЭтапГрафикаОплаты.ВариантОплаты = Перечисления.ВариантыОплатыКлиентом.КредитСдвиг, ДатаРеализации, ДатаДокумента) + Этап.Сдвиг * ОдинДень;
		КонецЕсли;
		
		Если (ЭтапГрафикаОплаты.ВариантОплаты = Перечисления.ВариантыОплатыКлиентом.ПредоплатаДоОтгрузки
			Или ЭтапГрафикаОплаты.ВариантОплаты = Перечисления.ВариантыОплатыКлиентом.АвансДоОбеспечения)
			И ЗначениеЗаполнено(ЖелаемаяДатаОтгрузки)
			И ДатаПлатежа > ЖелаемаяДатаОтгрузки Тогда
			ДатаПлатежа = ЖелаемаяДатаОтгрузки;
		КонецЕсли;
		
		Если ЭтапГрафикаОплаты.ВариантОплаты = Перечисления.ВариантыОплатыКлиентом.КредитСдвиг Тогда
			ЭтапГрафикаОплаты.Сдвиг           = Этап.Сдвиг;
		КонецЕсли;
		
		ЭтапГрафикаОплаты.ДатаПлатежа         = ДатаПлатежа;
		ЭтапГрафикаОплаты.ПроцентПлатежа      = Этап.ПроцентПлатежа;
		СуммаОплатыПоЭтапу                    = Окр(СуммаКРаспределениюОплаты * Этап.ПроцентПлатежа / 100, 2, РежимОкругления.Окр15как20);
		ЭтапГрафикаОплаты.СуммаПлатежа        = ?(ТекущийЭтап = ПоследнийНезалоговыйЭтап, СуммаКРаспределениюОплаты - РаспределеннаяСуммаОплаты, СуммаОплатыПоЭтапу);
		РаспределеннаяСуммаОплаты             = РаспределеннаяСуммаОплаты + ЭтапГрафикаОплаты.СуммаПлатежа;
		ЭтапГрафикаОплаты.ПроцентЗалогаЗаТару = Этап.ПроцентЗалогаЗаТару;
		СуммаЗалогаПоЭтапу                    = Окр(СуммаКРаспределениюЗалога * Этап.ПроцентЗалогаЗаТару / 100, 2, РежимОкругления.Окр15как20);
		ЭтапГрафикаОплаты.СуммаЗалогаЗаТару   = ?(ТекущийЭтап = ПоследнийЗалоговыйЭтап, СуммаКРаспределениюЗалога - РаспределеннаяСуммаЗалога, СуммаЗалогаПоЭтапу);
		РаспределеннаяСуммаЗалога             = РаспределеннаяСуммаЗалога + ЭтапГрафикаОплаты.СуммаЗалогаЗаТару;
		
	КонецЦикла;
	
	ЭтапыГрафикаОплаты.Сортировать("ДатаПлатежа");
	Объект.ЭтапыГрафикаОплаты.Загрузить(ЭтапыГрафикаОплаты);

КонецПроцедуры

// Заполняет дату платежа по условиям из соглашения или подставляет текущую дату сеанса.
//
// Параметры:
//		Объект - ДокументОбъект - документ, в котором необходимо заполнить дату платежа
//		ГрафикОплаты - СправочникСсылка.ГрафикиОплаты - график оплаты из условий продаж,
//                     если не задан, то определяется в функции по соглашению документа
//		ПерезаполнитьДату - Булево - заполнить дату, в независимости, заполнена ли она.
//
Процедура ЗаполнитьДатуПлатежаПоУмолчанию(Объект, ГрафикОплаты = Неопределено, ПерезаполнитьДату = Ложь) Экспорт
	
	ДатаПлатежаДоЗаполнения = Объект.ДатаПлатежа;
	
	Если (Не ЗначениеЗаполнено(Объект.ДатаПлатежа) Или ПерезаполнитьДату)
		И ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами") И ЗначениеЗаполнено(Объект.Соглашение) Тогда
		
		ГрафикСоглашенияЗаполнен = ПродажиВызовСервера.ГрафикСоглашенияЗаполнен(Объект.Соглашение);
		
		ИспользоватьГрафикиОплаты = ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиОплаты");
		Если ГрафикСоглашенияЗаполнен Или Не ИспользоватьГрафикиОплаты Тогда
			ГрафикОплаты = Неопределено;
		ИначеЕсли Не ЗначениеЗаполнено(ГрафикОплаты) Тогда
			ГрафикОплаты = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Соглашение, "ГрафикОплаты")
		КонецЕсли;
		
		Объект.ДатаПлатежа = ПродажиСервер.ПолучитьПоследнююДатуПоГрафику(
			Объект.Дата,
			ГрафикОплаты,
			Объект.Соглашение
		);
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ДатаПлатежа) Тогда
		Если ПерезаполнитьДату Тогда
			Объект.ДатаПлатежа = ДатаПлатежаДоЗаполнения;
		Иначе
			Объект.ДатаПлатежа = ТекущаяДатаСеанса();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура распределяет сумму отклонения мерных товаров по этапам графика оплаты.
Процедура РаспределитьСуммуОтклоненияПоЭтапамГрафикаОплаты(ЭтапыГрафикаОплаты, Знач СуммаОтклоненияКРаспределению) Экспорт
	
	РаспределеннаяСуммаОтклонения = 0;
	ТекущийЭтап                   = 0;
	КоличествоЭтапов              = ЭтапыГрафикаОплаты.Количество();
	СуммаОтклоненияКРаспределению = СуммаОтклоненияКРаспределению + ЭтапыГрафикаОплаты.Итог("СуммаОтклоненияМерныхТоваров");
	
	Если СуммаОтклоненияКРаспределению = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СуммаОплатыВсего              = ЭтапыГрафикаОплаты.Итог("СуммаПлатежа")+ЭтапыГрафикаОплаты.Итог("СуммаЗалогаЗаТару");
	
	Для Каждого ЭтапГрафикаОплаты Из ЭтапыГрафикаОплаты Цикл
		
		ТекущийЭтап = ТекущийЭтап + 1;
		
		КоэффициентОтОбщего    = (ЭтапГрафикаОплаты.СуммаПлатежа + ЭтапГрафикаОплаты.СуммаЗалогаЗаТару)/СуммаОплатыВсего;
		СуммаОтклоненияПоЭтапу = Окр(СуммаОтклоненияКРаспределению * КоэффициентОтОбщего, 2, РежимОкругления.Окр15как20);
		
		ЭтапГрафикаОплаты.СуммаОтклоненияМерныхТоваров  = ?(ТекущийЭтап = КоличествоЭтапов,
		                                                    СуммаОтклоненияКРаспределению - РаспределеннаяСуммаОтклонения,
		                                                    СуммаОтклоненияПоЭтапу);
		
		КоэффициентОтСтроки = ЭтапГрафикаОплаты.СуммаОтклоненияМерныхТоваров
		                      / (ЭтапГрафикаОплаты.СуммаПлатежа+ЭтапГрафикаОплаты.СуммаЗалогаЗаТару);
		
		ЧастьСуммыПлатежа   = Окр(ЭтапГрафикаОплаты.СуммаПлатежа * КоэффициентОтСтроки,2);
		ЭтапГрафикаОплаты.СуммаПлатежа      = ЭтапГрафикаОплаты.СуммаПлатежа - ЧастьСуммыПлатежа;
		
		ЭтапГрафикаОплаты.СуммаЗалогаЗаТару = ЭтапГрафикаОплаты.СуммаЗалогаЗаТару
		                                      - (ЭтапГрафикаОплаты.СуммаОтклоненияМерныхТоваров - ЧастьСуммыПлатежа);
		
		РаспределеннаяСуммаОтклонения = РаспределеннаяСуммаОтклонения + ЭтапГрафикаОплаты.СуммаОтклоненияМерныхТоваров;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеДокументовЗакупки

// Заполняет этапы графика оплаты или распределяет уже заполненную сумму в документах закупки.
//
// Параметры:
//
// 		Объект - ДокументОбъект - документ, для которого заполняются этапы графика оплаты
// 		ГрафикСоглашенияЗаполнен - Булево - флаг возможности заполнения по графику указанном в соглашении
// 		СуммаОплаты - Число - сумма, распределяющаяся по этапам графика оплаты
// 		СуммаЗалога - Число - сумма залога, распределяющаяся по этапам графика оплаты
// 		ДатаПлатежаПоУмолчанию - Дата - Дата платежа по умолчанию, например, дата документа или дата отгузки.
//
Процедура ЗаполнитьЭтапыОплатыДокументаЗакупки(Объект, ГрафикСоглашенияЗаполнен, СуммаОплаты,
	СуммаЗалога = 0, ДатаПлатежаПоУмолчанию = Неопределено) Экспорт
	
	Если ГрафикСоглашенияЗаполнен Тогда
		ЗаполнитьЭтапыОплатыДокументаЗакупкиПоСоглашению(
			Объект,
			СуммаОплаты,
			СуммаЗалога);
	ИначеЕсли Объект.ЭтапыГрафикаОплаты.Количество() > 0 Тогда
		ЭтапыОплатыКлиентСервер.РаспределитьСуммуПоЭтапамГрафикаОплаты(
			Объект.ЭтапыГрафикаОплаты,
			СуммаОплаты,
			СуммаЗалога);
	Иначе
		НовСтр = Объект.ЭтапыГрафикаОплаты.Добавить();
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(НовСтр, "ВариантОплаты") Тогда
			НовСтр.ВариантОплаты = Перечисления.ВариантыОплатыПоставщику.КредитСдвиг;
		КонецЕсли;
		НовСтр.ДатаПлатежа  = ДатаПлатежаПоУмолчанию;
		НовСтр.СуммаПлатежа = СуммаОплаты;
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(НовСтр, "СуммаЗалогаЗаТару") Тогда
			НовСтр.СуммаЗалогаЗаТару     = СуммаЗалога;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет этапы графика оплаты в документе закупки по графику соглашения
//
// Параметры:
// 		Объект - ДокументОбъект - документ, в котором необходимо заполнить авансовые этапы оплаты
// 		СуммаОплатыКРаспределению - Число - сумма, распределяющаяся по этапам графика оплаты
// 		СуммаЗалогаКРаспределению - Число - сумма залога, распределяющаяся по этапам графика оплаты
// 		ЗаполнятьФормуОплаты - Булево - признак, указывающий на необходимость заполнения формы оплаты
// 			в документе формой оплаты по графику, указанной в соглашении.
//
Процедура ЗаполнитьЭтапыОплатыДокументаЗакупкиПоСоглашению(Объект,
	                                              Знач СуммаОплатыКРаспределению,
	                                              Знач СуммаЗалогаКРаспределению = 0,
	                                              ЗаполнятьФормуОплаты = Ложь) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СоглашенияСПоставщикамиЭтапыГрафикаОплаты.НомерСтроки КАК НомерСтроки,
	|	СоглашенияСПоставщикамиЭтапыГрафикаОплаты.ВариантОплаты КАК ВариантОплаты,
	|	СоглашенияСПоставщикамиЭтапыГрафикаОплаты.Сдвиг КАК Сдвиг,
	|	СоглашенияСПоставщикамиЭтапыГрафикаОплаты.ПроцентПлатежа КАК ПроцентПлатежа,
	|	СоглашенияСПоставщикамиЭтапыГрафикаОплаты.ПроцентЗалогаЗаТару КАК ПроцентЗалогаЗаТару,
	|	СоглашенияСПоставщикамиЭтапыГрафикаОплаты.Ссылка.ФормаОплаты КАК ФормаОплаты,
	|	СоглашенияСПоставщикамиЭтапыГрафикаОплаты.Ссылка.Календарь КАК Календарь
	|ИЗ
	|	Справочник.СоглашенияСПоставщиками.ЭтапыГрафикаОплаты КАК СоглашенияСПоставщикамиЭтапыГрафикаОплаты
	|ГДЕ
	|	СоглашенияСПоставщикамиЭтапыГрафикаОплаты.Ссылка = &Соглашение
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки");
		
	Запрос.УстановитьПараметр("Соглашение", Объект.Соглашение);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выгрузка         = РезультатЗапроса.Выгрузить();
	
	Если (СуммаОплатыКРаспределению = 0 И СуммаЗалогаКРаспределению = 0) ИЛИ Выгрузка.Количество() = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ЗаполнятьФормуОплаты Тогда
		
		ФормаОплаты = Выгрузка[0].ФормаОплаты;
		
		Если ЗначениеЗаполнено(ФормаОплаты) И Объект.ФормаОплаты <> ФормаОплаты Тогда
			Объект.ФормаОплаты = ФормаОплаты;
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьЭтапыОплатыДокументаЗакупкиПоШаблону(
		Объект,
		СуммаОплатыКРаспределению,
		СуммаЗалогаКРаспределению,
		Выгрузка,
		Выгрузка[0].Календарь);
	
КонецПроцедуры

// Заполняет этапы графика оплаты в документе закупки по таблице значений
//
// Параметры:
// 		Объект - ДокументОбъект - документ, в котором необходимо заполнить авансовые этапы оплаты
// 		СуммаОплатыКРаспределению - Число - сумма, распределяющаяся по этапам графика оплаты
// 		СуммаЗалогаКРаспределению - Число - сумма залога, распределяющаяся по этапам графика оплаты
// 		ШаблонГрафика - ТаблицаЗначений - ТЗ по которой необходимо заполнить этапы графика оплаты
// 		Календарь - СправочникСсылка.Календари - по которому вычисляются даты.
//
Процедура ЗаполнитьЭтапыОплатыДокументаЗакупкиПоШаблону(Объект,
	                                  Знач СуммаОплатыКРаспределению,
	                                  Знач СуммаЗалогаКРаспределению,
	                                  ШаблонГрафика,
	                                  Знач Календарь) Экспорт
	
	ЭтапыГрафикаОплаты = Новый ТаблицаЗначений();
	
	ЭтапыГрафикаОплаты.Колонки.Добавить("ВариантОплаты");
	ЭтапыГрафикаОплаты.Колонки.Добавить("ДатаПлатежа");
	ЭтапыГрафикаОплаты.Колонки.Добавить("ПроцентПлатежа");
	ЭтапыГрафикаОплаты.Колонки.Добавить("СуммаПлатежа");
	ЭтапыГрафикаОплаты.Колонки.Добавить("ПроцентЗалогаЗаТару");
	ЭтапыГрафикаОплаты.Колонки.Добавить("СуммаЗалогаЗаТару");
	ЭтапыГрафикаОплаты.Колонки.Добавить("Сдвиг");
	
	РаспределеннаяСуммаОплаты = 0;
	РаспределеннаяСуммаЗалога = 0;
	ТекущийЭтап               = 0;
	ОдинДень                  = 86400;
	
	КоличествоЭтапов = ШаблонГрафика.Количество();

	ДатаДокумента = ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДатаСеанса());
	ЖелаемаяДатаПоступления = ?(ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(Объект, "ЖелаемаяДатаПоступления"),
		Объект.ЖелаемаяДатаПоступления, Неопределено);
	ПоступлениеОднойДатой = ?(ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(Объект, "ПоступлениеОднойДатой"),Объект.ПоступлениеОднойДатой,Ложь);
	ЖелаемаяДатаПоступления = ?(ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(Объект, "ДатаПоступления") И ПоступлениеОднойДатой, 
		Объект.ДатаПоступления, ЖелаемаяДатаПоступления);
	ДатаПоступления = ЖелаемаяДатаПоступления;
	
	// Определим календарную дату для каждого этапа графика оплаты
	УчитыватьКалендарь = Ложь;
	
	Если ЗначениеЗаполнено(Календарь) Тогда
		
		УчитыватьКалендарь = Истина;
		
		СдвигиАвансовыхЭтапов = Новый Массив();
		СдвигиКредитныхЭтапов = Новый Массив();
		МассивДатПоКалендарю  = Новый Массив();
		
		Для Каждого Этап Из ШаблонГрафика Цикл
			
			Если Этап.ВариантОплаты = Перечисления.ВариантыОплатыПоставщику.ПредоплатаДоПоступления Или
				Этап.ВариантОплаты = Перечисления.ВариантыОплатыПоставщику.АвансДоПодтверждения Тогда
				СдвигиАвансовыхЭтапов.Добавить(Этап.Сдвиг);
			Иначе
				СдвигиКредитныхЭтапов.Добавить(Этап.Сдвиг);
			КонецЕсли;
			
		КонецЦикла;
		
		Если СдвигиАвансовыхЭтапов.Количество() > 0 Тогда
			
			МассивДатПоКалендарюАвансовыхЭтапов = КалендарныеГрафики.ДатыПоКалендарю(Календарь, ДатаДокумента, СдвигиАвансовыхЭтапов);
			
			Для Каждого ДатаПоКалендарю Из МассивДатПоКалендарюАвансовыхЭтапов Цикл
				МассивДатПоКалендарю.Добавить(ДатаПоКалендарю);
			КонецЦикла;
			
		КонецЕсли;
		
		Если СдвигиКредитныхЭтапов.Количество() > 0 Тогда
			
			Если Не ЗначениеЗаполнено(ДатаПоступления) Тогда
				
				Если СдвигиАвансовыхЭтапов.Количество() > 0 Тогда
					Если МассивДатПоКалендарюАвансовыхЭтапов.Количество() > 0 Тогда
						ДатаПоступления = МассивДатПоКалендарюАвансовыхЭтапов[МассивДатПоКалендарюАвансовыхЭтапов.Количество()-1];
					КонецЕсли;
				Иначе
					ДатаПоступления = ДатаДокумента;
				КонецЕсли;
				
			КонецЕсли;
			
			МассивДатПоКалендарюКредитныхЭтапов = КалендарныеГрафики.ДатыПоКалендарю(Календарь, ДатаПоступления, СдвигиКредитныхЭтапов);
			
			Для Каждого ДатаПоКалендарю Из МассивДатПоКалендарюКредитныхЭтапов Цикл
				МассивДатПоКалендарю.Добавить(ДатаПоКалендарю);
			КонецЦикла;
			
		КонецЕсли;
		
	Иначе
		
		Если Не ЗначениеЗаполнено(ДатаПоступления) Тогда
			
			МаксСдвигАванса = 0;
		
			Для Каждого ТекЭтап Из ШаблонГрафика Цикл
				
				Если ТекЭтап.ВариантОплаты = Перечисления.ВариантыОплатыПоставщику.ПредоплатаДоПоступления Или
					ТекЭтап.ВариантОплаты = Перечисления.ВариантыОплатыПоставщику.АвансДоПодтверждения Тогда
					
					МаксСдвигАванса = Макс(МаксСдвигАванса, ТекЭтап.Сдвиг);
					
				КонецЕсли;
				
			КонецЦикла;
			
			ДатаПоступления = ДатаДокумента + МаксСдвигАванса * ОдинДень;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Определим последний незалоговый этап
	ПоследнийНезалоговыйЭтап = КоличествоЭтапов;
	Пока ПоследнийНезалоговыйЭтап <> 0 И ШаблонГрафика[ПоследнийНезалоговыйЭтап-1].ПроцентПлатежа = 0 Цикл
		ПоследнийНезалоговыйЭтап = ПоследнийНезалоговыйЭтап - 1;
	КонецЦикла;
	
	// Определим последний залоговый этап
	ПоследнийЗалоговыйЭтап = КоличествоЭтапов;
	Пока ПоследнийЗалоговыйЭтап <> 0 И ШаблонГрафика[ПоследнийЗалоговыйЭтап-1].ПроцентЗалогаЗаТару = 0 Цикл
		ПоследнийЗалоговыйЭтап = ПоследнийЗалоговыйЭтап - 1;
	КонецЦикла;
	
	// Заполним этапы в соответствии с графиком оплаты	
	Объект.ЭтапыГрафикаОплаты.Очистить();
	Для Каждого Этап Из ШаблонГрафика Цикл
		
		ТекущийЭтап                     = ТекущийЭтап + 1;
		ЭтапГрафикаОплаты               = ЭтапыГрафикаОплаты.Добавить();
		ЭтапГрафикаОплаты.ВариантОплаты = Этап.ВариантОплаты;
		
		Если УчитыватьКалендарь Тогда
			ДатаПлатежа = МассивДатПоКалендарю[ТекущийЭтап-1];
		Иначе
			ДатаПлатежа = ?(ЭтапГрафикаОплаты.ВариантОплаты = Перечисления.ВариантыОплатыПоставщику.КредитПослеПоступления
				ИЛИ ЭтапГрафикаОплаты.ВариантОплаты = Перечисления.ВариантыОплатыПоставщику.КредитСдвиг, ДатаПоступления, ДатаДокумента) + Этап.Сдвиг * ОдинДень;
		КонецЕсли;
		
		Если (ЭтапГрафикаОплаты.ВариантОплаты = Перечисления.ВариантыОплатыПоставщику.АвансДоПодтверждения
			Или ЭтапГрафикаОплаты.ВариантОплаты = Перечисления.ВариантыОплатыПоставщику.ПредоплатаДоПоступления)
			И ЗначениеЗаполнено(ЖелаемаяДатаПоступления)
			И ДатаПлатежа > ЖелаемаяДатаПоступления Тогда
			ДатаПлатежа = ЖелаемаяДатаПоступления;
		КонецЕсли;
		
		Если ЭтапГрафикаОплаты.ВариантОплаты = Перечисления.ВариантыОплатыПоставщику.КредитСдвиг Тогда
			ЭтапГрафикаОплаты.Сдвиг           = Этап.Сдвиг;
		КонецЕсли;
		
		ЭтапГрафикаОплаты.ДатаПлатежа         = ДатаПлатежа;
		СуммаЭтапаОплаты                      = Окр(СуммаОплатыКРаспределению * Этап.ПроцентПлатежа / 100, 2, РежимОкругления.Окр15как20);
		ЭтапГрафикаОплаты.СуммаПлатежа        = ?(ТекущийЭтап = ПоследнийНезалоговыйЭтап, СуммаОплатыКРаспределению - РаспределеннаяСуммаОплаты, СуммаЭтапаОплаты);
		ЭтапГрафикаОплаты.ПроцентПлатежа      = ?(СуммаЭтапаОплаты <> 0, Этап.ПроцентПлатежа, 0);
		РаспределеннаяСуммаОплаты             = РаспределеннаяСуммаОплаты + ЭтапГрафикаОплаты.СуммаПлатежа;
		СуммаЭтапаЗалога                      = Окр(СуммаЗалогаКРаспределению * Этап.ПроцентЗалогаЗаТару / 100, 2, РежимОкругления.Окр15как20);
		ЭтапГрафикаОплаты.СуммаЗалогаЗаТару   = ?(ТекущийЭтап = ПоследнийЗалоговыйЭтап, СуммаЗалогаКРаспределению - РаспределеннаяСуммаЗалога, СуммаЭтапаЗалога);
		ЭтапГрафикаОплаты.ПроцентЗалогаЗаТару = ?(Этап.ПроцентЗалогаЗаТару > 0,Этап.ПроцентЗалогаЗаТару, 
			?(СуммаЗалогаКРаспределению<>0 И ЭтапГрафикаОплаты.СуммаЗалогаЗаТару <> 0,
			(Окр(ЭтапГрафикаОплаты.СуммаЗалогаЗаТару/СуммаЗалогаКРаспределению,2,РежимОкругления.Окр15как20))*100,
			0));
		РаспределеннаяСуммаЗалога             = РаспределеннаяСуммаЗалога + ЭтапГрафикаОплаты.СуммаЗалогаЗаТару;
		
	КонецЦикла;
	
	ЭтапыГрафикаОплаты.Сортировать("ДатаПлатежа");
	Объект.ЭтапыГрафикаОплаты.Загрузить(ЭтапыГрафикаОплаты);

КонецПроцедуры

// Заполняет дату платежа по условиям из соглашения или подставляет текущую дату сеанса.
//
// Параметры:
//		Объект - ДокументОбъект - документ, в котором необходимо заполнить дату платежа
//		ГрафикОплаты - СправочникСсылка.ГрафикиОплаты - график оплаты из условий продаж,
//			если не задан, то определяется в функции по соглашению документа
//		ПерезаполнитьДату - Булево - заполнить дату, в независимости, заполнена ли она.
//
Процедура ЗаполнитьДатуПлатежаВЗакупкахПоУмолчанию(Объект, ГрафикОплаты = Неопределено, ПерезаполнитьДату = Ложь) Экспорт
	
	ДатаПлатежаДоЗаполнения = Объект.ДатаПлатежа;
	
	Если (Не ЗначениеЗаполнено(Объект.ДатаПлатежа) Или ПерезаполнитьДату)
		И ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСПоставщиками") И ЗначениеЗаполнено(Объект.Соглашение) Тогда
		
		Объект.ДатаПлатежа = ЗакупкиСервер.ПолучитьПоследнююДатуПоГрафику(
			Объект.Дата,
			Объект.Соглашение);
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ДатаПлатежа) Тогда
		Если ПерезаполнитьДату Тогда
			Объект.ДатаПлатежа = ДатаПлатежаДоЗаполнения;
		Иначе
			Объект.ДатаПлатежа = ТекущаяДатаСеанса();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьСкорректироватьСуммуОбязательнойПредоплаты(СуммаОбязательнойПредоплаты,ЭтапыГрафикаОплаты,ВариантОплатыПредоплата)
	
	Если ЭтапыГрафикаОплаты[0].ВариантОплаты <> ВариантОплатыПредоплата Тогда
		НовСтр = ЭтапыГрафикаОплаты.Вставить(0);
		НовСтр.ВариантОплаты = ВариантОплатыПредоплата;
	КонецЕсли;
	СтрокаПредоплаты = ЭтапыГрафикаОплаты[0];
	сч = 0;
	Пока сч < ЭтапыГрафикаОплаты.Количество() Цикл
		Строка = ЭтапыГрафикаОплаты[0];
		
		Если СуммаОбязательнойПредоплаты = 0 Тогда
			Прервать;
		КонецЕсли;
		
		Если Строка.ВариантОплаты <> ВариантОплатыПредоплата Тогда
			Если Строка.СуммаПлатежа > СуммаОбязательнойПредоплаты Тогда
				Строка.СуммаПлатежа = Строка.СуммаПлатежа - СуммаОбязательнойПредоплаты;
				СтрокаПредоплаты.СуммаПлатежа = СтрокаПредоплаты.СуммаПлатежа + СуммаОбязательнойПредоплаты;
				СуммаОбязательнойПредоплаты = 0;
			Иначе
				СуммаОбязательнойПредоплаты = СуммаОбязательнойПредоплаты - Строка.СуммаПлатежа;
				СтрокаПредоплаты.СуммаПлатежа = СтрокаПредоплаты.СуммаПлатежа + Строка.СуммаПлатежа;
				Строка.СуммаПлатежа = 0;
			КонецЕсли;
			
			Если СуммаОбязательнойПредоплаты = 0 Тогда
				Прервать;
			КонецЕсли;
			
			Если Строка.СуммаЗалогаЗаТару > СуммаОбязательнойПредоплаты Тогда
				Строка.СуммаЗалогаЗаТару = Строка.СуммаЗалогаЗаТару - СуммаОбязательнойПредоплаты;
				СтрокаПредоплаты.СуммаЗалогаЗаТару = СтрокаПредоплаты.СуммаЗалогаЗаТару + СуммаОбязательнойПредоплаты;
				СуммаОбязательнойПредоплаты = 0;
			Иначе
				СуммаОбязательнойПредоплаты = СуммаОбязательнойПредоплаты - Строка.СуммаЗалогаЗаТару;
				СтрокаПредоплаты.СуммаЗалогаЗаТару = СтрокаПредоплаты.СуммаЗалогаЗаТару + Строка.СуммаЗалогаЗаТару;
				Строка.СуммаЗалогаЗаТару = 0;
			КонецЕсли;
		КонецЕсли;
		
		Если Строка.СуммаПлатежа = 0 И Строка.СуммаЗалогаЗаТару = 0 Тогда
			ЭтапыГрафикаОплаты.Удалить(сч);
		Иначе
			сч = сч + 1;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
