#Область ПрограммныйИнтерфейс

Процедура РежимНалогообложенияПриИзмененииСервер(Форма) Экспорт
	
	Объект = Форма.Объект;
	
	Объект.НалогообложениеНДСОпределяетсяВДокументе = ?(Форма.РежимНалогообложения = 1, Истина, Ложь);
	
	Если Объект.НалогообложениеНДСОпределяетсяВДокументе И ЗначениеЗаполнено(Объект.НалогообложениеНДС) Тогда
		Объект.НалогообложениеНДС = Неопределено;
		НалогообложениеНДСПриИзмененииСервер(Форма);
	КонецЕсли;
	
	НалогообложениеНДСПриИзмененииСервер(Форма);
	
КонецПроцедуры

Процедура НалогообложениеНДСПриИзмененииСервер(Форма) Экспорт
	
	Объект = Форма.Объект;
	
	Если Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС
		ИЛИ Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД
		ИЛИ Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДСУПокупателя
		ИЛИ Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.НалоговыйАгентПоНДС Тогда 
		Объект.СтавкаНДС = УчетНДСРФВызовСервера.СтавкаНДСПоЗначениюПеречисления(Перечисления.СтавкиНДС.БезНДС);
	КонецЕсли;
	
	Если Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаНаЭкспорт  Тогда 
		Объект.СтавкаНДС = УчетНДСРФВызовСервера.СтавкаНДСПоЗначениюПеречисления(Перечисления.СтавкиНДС.НДС0);
	КонецЕсли;
	
	Если Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.НалоговыйАгентПоНДС  Тогда 
		Объект.УчетАгентскогоНДС = Истина;
	Иначе
		Объект.УчетАгентскогоНДС = Ложь;
		Объект.ВидАгентскогоДоговора = Перечисления.ВидыАгентскихДоговоров.ПустаяСсылка();
	КонецЕсли;
	
	УстановитьСвойстваЭлементовНалогообложенияИРаздельногоУчетаНДС(Форма);
	
КонецПроцедуры

Процедура РежимРаздельногоУчетаНДСПриИзмененииСервер(Форма) Экспорт
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Объект.ЗакупкаПодДеятельностьОпределяетсяВДокументе = ?(Форма.РежимРаздельногоУчетаНДС = 1, Истина, Ложь);
	
	Если Объект.ЗакупкаПодДеятельностьОпределяетсяВДокументе 
		И ЗначениеЗаполнено(Объект.ЗакупкаПодДеятельность) Тогда
		Объект.ЗакупкаПодДеятельность = Неопределено;
	КонецЕсли;
	
	УстановитьСвойстваЭлементовНалогообложенияИРаздельногоУчетаНДС(Форма);

КонецПроцедуры

Процедура ВариантПлатежаГОЗПриИзмененииНаСервере(Форма) Экспорт
	
	Объект = Форма.Объект;
	
	Если Форма.ВариантПлатежаГОЗ = 1 Тогда
		Объект.ТипПлатежаФЗ275 = Справочники.ТипыПлатежейФЗ275.СписаниеНаОтдельныйСчет;
	Иначе
		Объект.ТипПлатежаФЗ275 = Неопределено;
	КонецЕсли;
	
	УправлениеЭлементамиГосКонтрактов(Форма);
	
КонецПроцедуры

Процедура УправлениеЭлементамиГосКонтрактов(Форма) Экспорт
	
	
КонецПроцедуры

Процедура ГосударственныйКонтрактПриИзмененииСервер(Форма) Экспорт
	
	Объект = Форма.Объект;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов
	|ГДЕ
	|	ГосударственныйКонтракт = &Госконтракт
	|	И ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СПокупателем)
	|	И НЕ ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Ссылка
	|ИЗ
	|	Справочник.ДоговорыМеждуОрганизациями
	|ГДЕ
	|	ГосударственныйКонтракт = &Госконтракт
	|	И НЕ ПометкаУдаления
	|";
	Запрос.УстановитьПараметр("Госконтракт", Объект.ГосударственныйКонтракт);
	
	Результат = Запрос.Выполнить().Выгрузить();
	Если Результат.Количество() = 1 Тогда
		Форма.КонтрактСЗаказчиком = Результат[0].Ссылка;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПлатежиПо275ФЗПриИзмененииНаСервере(Форма) Экспорт

	Объект = Форма.Объект;
	Объект.ДоговорСУчастникомГОЗ = Объект.ПлатежиПо275ФЗ;
	УправлениеЭлементамиГосКонтрактов(Форма);

КонецПроцедуры

Процедура ПриИзмененииНастроекПриемкиСервер(Форма) Экспорт
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Объект.ВариантПриемкиТоваров = ЗакупкиСервер.ПолучитьВариантовПриемкиПоНастройкам(Форма.ОформлениеПоступления, Форма.ПриемкаТоваров);
	
	Если Объект.ВариантОформленияЗакупок = Перечисления.ВариантыОформленияЗакупок.НеотфактурованныеПоставки
		И (Объект.ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.ПоДоговорамПослеНакладных
			Или Объект.ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.РазделенаТолькоПоНакладным) Тогда
		
		Если Объект.ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.ПоДоговорамПослеНакладных Тогда
			Объект.ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.ПоДоговорамПослеЗаказовИлиНакладных;
		ИначеЕсли Объект.ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.РазделенаТолькоПоНакладным Тогда
			Объект.ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.РазделенаПоЗаказамИНакладным;
		КонецЕсли;
		
		ЗакупкиСервер.ЗаполнитьНастройкиВариантовПриемки(Объект.ВариантПриемкиТоваров, Форма.ОформлениеПоступления, Форма.ПриемкаТоваров);
	КонецЕсли;
	
	ЗакупкиСервер.ЗаполнитьСписокВыбораВариантовПриемкиПоДоговорам(Элементы.ОформлениеПоступления, Элементы.ПриемкаТоваров, Объект.ВариантОформленияЗакупок);
	
	Если Справочники.ДоговорыКонтрагентов.ДоговорИспользуетсяПриПриемке(Объект.ВариантПриемкиТоваров)
		И Объект.СпособДоставки = Перечисления.СпособыДоставки.ОпределяетсяВРаспоряжении Тогда
		Объект.СпособДоставки = Перечисления.СпособыДоставки.ПустаяСсылка();
		ДоставкаТоваров.ПриЧтенииСозданииРаспоряженийНаСервере(Элементы, Объект);
	КонецЕсли;
	
	УстановитьДоступностьПриемки(Форма);
	УстановитьДоступностьДоставки(Форма);
	
КонецПроцедуры

Процедура ЗаполнитьСписокВыбораВариантовПриемки(Форма) Экспорт
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ЗакупкиСервер.ЗаполнитьНастройкиВариантовПриемки(Объект.ВариантПриемкиТоваров, Форма.ОформлениеОрдера, Форма.ПриемкаТоваров);
	ЗакупкиСервер.ЗаполнитьСписокВыбораВариантовПриемкиПоДоговорам(Элементы.ОформлениеОрдера, Элементы.ПриемкаТоваров, Объект.ВариантОформленияЗакупок);
	
КонецПроцедуры

Процедура УстановитьДоступностьПриемки(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	
	Если Форма.ОформлениеПоступления = "ПоДоговору" Тогда
		Элементы.ПриемкаТоваров.Доступность = Ложь;
		Форма.ПриемкаТоваров = "НеРазделена";
	Иначе
		Элементы.ПриемкаТоваров.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьДоступностьДоставки(Форма) Экспорт
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ЭтоДоговорРаспоряжениеНаПриемку = Справочники.ДоговорыКонтрагентов.ДоговорИспользуетсяПриПриемке(Объект.ВариантПриемкиТоваров);
	
	ДоступныеСпособыДоставки = ДоступныеСпособыДоставки(ЭтоДоговорРаспоряжениеНаПриемку);
	
	Элементы.СпособДоставки.СписокВыбора.ЗагрузитьЗначения(ДоступныеСпособыДоставки);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыВыбораПартнера(Форма) Экспорт
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	МассивПараметровВыбораПартнера = Новый Массив;
	
	ОперацииЗакупки = ЗакупкиСервер.ХозяйственныеОперацииПоОсновной(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика);
	ОперацииИмпорта = ЗакупкиСервер.ХозяйственныеОперацииПоОсновной(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту);
	ОперацииВСтранахЕАЭС = ЗакупкиСервер.ХозяйственныеОперацииПоОсновной(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС);
	
	Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияКлиенту
		Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПроизводствоИзДавальческогоСырья
		Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию 
		ИЛИ Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаХранениеСПравомПродажи Тогда
		
		МассивПараметровВыбораПартнера.Добавить(Новый ПараметрВыбора("Отбор.Клиент", Истина));
		
	ИначеЕсли ОперацииЗакупки.Найти(Объект.ХозяйственнаяОперация) <> Неопределено
		Или ОперацииИмпорта.Найти(Объект.ХозяйственнаяОперация) <> Неопределено
		Или ОперацииВСтранахЕАЭС.Найти(Объект.ХозяйственнаяОперация) <> Неопределено
		Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПриемНаКомиссию
		Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПроизводствоУПереработчика
		Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПроизводствоУПереработчика2_5	
		Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи Тогда
		
		МассивПараметровВыбораПартнера.Добавить(Новый ПараметрВыбора("Отбор.Поставщик", Истина));
		
	КонецЕсли;
	
	Элементы.Партнер.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбораПартнера);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьСвойстваЭлементовНалогообложенияИРаздельногоУчетаНДС(Форма)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ТекВидДоговора = Объект.ВидДоговораУХ;
	Элементы.ВидАгентскогоДоговора.Видимость = (ТекВидДоговора = Справочники.ВидыДоговоровКонтрагентовУХ.СПоставщиком);
	
	Элементы.НалогообложениеНДС.Доступность     = НЕ Объект.НалогообложениеНДСОпределяетсяВДокументе;
	Элементы.ЗакупкаПодДеятельность.Доступность = НЕ Объект.ЗакупкаПодДеятельностьОпределяетсяВДокументе;
	Элементы.ВидАгентскогоДоговора.Доступность = (Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.НалоговыйАгентПоНДС);
	Элементы.СтавкаНДС.Доступность = Объект.НалогообложениеНДСОпределяетсяВДокументе ИЛИ (Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС);
	
	Элементы.ГруппаРаздельныйУчетНДС.Видимость = 
		(ТекВидДоговора = Справочники.ВидыДоговоровКонтрагентовУХ.СПоставщиком
		ИЛИ ТекВидДоговора = Справочники.ВидыДоговоровКонтрагентовУХ.ВвозИзЕАЭС
		ИЛИ ТекВидДоговора = Справочники.ВидыДоговоровКонтрагентовУХ.Импорт
		ИЛИ ТекВидДоговора = Справочники.ВидыДоговоровКонтрагентовУХ.СПереработчиком
		ИЛИ ТекВидДоговора = Справочники.ВидыДоговоровКонтрагентовУХ.СПоклажедателем);
		
	Элементы.ГруппаНалогообложениеНДС.Видимость = 
		НЕ (ТекВидДоговора = Справочники.ВидыДоговоровКонтрагентовУХ.Импорт
			ИЛИ ТекВидДоговора = Справочники.ВидыДоговоровКонтрагентовУХ.ВвозИзЕАЭС
			ИЛИ ТекВидДоговора = Справочники.ВидыДоговоровКонтрагентовУХ.СПереработчиком);
	
КонецПроцедуры

Процедура УстановитьВидимостьОтчетовГОЗ(Форма)
	
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(Форма, Форма.Объект);
	
КонецПроцедуры

Функция ДоступныеСпособыДоставки(ЭтоДоговорРаспоряжениеНаПриемку)
	
	ВозвращаемыйМассив = Новый Массив();
	
	Если Не ЭтоДоговорРаспоряжениеНаПриемку Тогда
		ВозвращаемыйМассив.Добавить(Перечисления.СпособыДоставки.ОпределяетсяВРаспоряжении);
	КонецЕсли;
	
	ВозвращаемыйМассив.Добавить(Перечисления.СпособыДоставки.СиламиПоставщикаДоНашегоСклада);
	ВозвращаемыйМассив.Добавить(Перечисления.СпособыДоставки.СиламиПеревозчикаДоНашегоСклада);
	ВозвращаемыйМассив.Добавить(Перечисления.СпособыДоставки.СиламиПеревозчикаДоПунктаПередачи);
	ВозвращаемыйМассив.Добавить(Перечисления.СпособыДоставки.НашимиСиламиСАдресаОтправителя);
	ВозвращаемыйМассив.Добавить(Перечисления.СпособыДоставки.ОтОтправителяОпределяетСлужбаДоставки);
	
	Возврат ВозвращаемыйМассив;
	
КонецФункции

#КонецОбласти
