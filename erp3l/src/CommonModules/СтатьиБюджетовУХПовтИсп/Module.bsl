#Область ПрограммныйИнтерфейс

Функция СтатьяБюджетаПризнакВалютнаяПоУмолчанию(СтатьяБюджета) Экспорт
	
	ВидБюджета = ПолучитьВидБюджетаПоСтатье(СтатьяБюджета);
	Если Не ЗначениеЗаполнено(ВидБюджета) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Вариант = ВидБюджета.ПризнакВалютнаяПоУмолчанию;
	Если Вариант = Перечисления.ВариантыУстановкиПризнакаВалютнаяСтатьиБюджета.Да Тогда
		Возврат Истина;
	ИначеЕсли Вариант = Перечисления.ВариантыУстановкиПризнакаВалютнаяСтатьиБюджета.ПоНастройкамЛимитирования Тогда
		ПараметрыОП = ОперативноеПланированиеПовтИспУХ.ПолучитьПараметрыОперПланирования(ВидБюджета.Предназначение);
		Возврат ПараметрыОП.ИспользоватьЛимитирование;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьВидБюджетаПоСтатье(СтатьяБюджета)
	
	Результат = неопределено;
	
	ВидыБюджетов = ПланыВидовХарактеристик.ВидыБюджетов;
	
	ТипСтатьи = ТипЗнч(СтатьяБюджета);
	Если ВидыБюджетов.БюджетДвиженияДенежныхСредств.ТипЗначения.Типы().Найти(ТипСтатьи) <> неопределено Тогда
		Результат = ВидыБюджетов.БюджетДвиженияДенежныхСредств;
	ИначеЕсли ВидыБюджетов.БюджетДоходовИРасходов.ТипЗначения.Типы().Найти(ТипСтатьи) <> неопределено Тогда
		Результат = ВидыБюджетов.БюджетДоходовИРасходов;
	ИначеЕсли ВидыБюджетов.БюджетДвиженияРесурсов.ТипЗначения.Типы().Найти(ТипСтатьи) <> неопределено Тогда
		Результат = ВидыБюджетов.БюджетДвиженияРесурсов;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
 