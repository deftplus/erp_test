#Область ПрограммныйИнтерфейс

Функция ПолучитьГрафикНачисленияДоходаВексель(ПараметрыВекселя, СДаты, ПоДату = Неопределено) Экспорт
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Дата", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	Результат.Колонки.Добавить("Сумма", РаботаСКурсамиВалют.ОписаниеТипаДенежногоПоля());
	
	Если ПараметрыВекселя.ПредусмотренПроцентныйДоход Тогда
		
		Если ПараметрыВекселя.Номинал = 0 Тогда
			Возврат Результат;
		КонецЕсли;
		
		ДатаНачалаРасчетов = НачалоДня(СДаты+86400);
		ДатаОкончанияРасчетов = ?(ПоДату = Неопределено, ПараметрыВекселя.ДатаОкончанияНачисленияПроцентов, ПоДату);
		НачалоПериодаНачисления = Макс(НачалоДня(СДаты+86400), ПараметрыВекселя.ДатаНачалаНачисленияПроцентов);
		ОкончаниеПериоданачисления = Мин(НачалоДня(КонецМесяца(НачалоПериодаНачисления)), ДатаОкончанияРасчетов);
		
		Пока НачалоПериодаНачисления <= ДатаОкончанияРасчетов Цикл
			
			НовоеНачисление = Результат.Добавить();
			НовоеНачисление.Дата = ОкончаниеПериоданачисления;
			НовоеНачисление.Сумма = ФинансоваяМатематикаКлиентСервер.СуммаПроцентовЗаПериод(
									НачалоПериодаНачисления,
									ОкончаниеПериоданачисления,
									ПараметрыВекселя.Номинал,
									ПараметрыВекселя.СтавкаДоходности);
									
			НачалоПериодаНачисления = ОкончаниеПериоданачисления + 86400;
			ОкончаниеПериоданачисления = Мин(НачалоДня(КонецМесяца(НачалоПериодаНачисления)), ДатаОкончанияРасчетов);
			
		КонецЦикла;
	Иначе
		// По дисконтному векселю надо распределить разность между номиналом и суммой приобретения.
		СуммаДохода = (ПараметрыВекселя.Номинал - ПараметрыВекселя.СуммаОперации);
		Если СуммаДохода = 0 Тогда
			// Нечего распределять.
			Возврат Результат;
		КонецЕсли;
		ДатаНачалаРасчетов = НачалоДня(СДаты+86400);
		ДатаОкончанияРасчетов = ПараметрыВекселя.ДатаПлатежа;
		ЧислоДней = Цел((ДатаОкончанияРасчетов - ДатаНачалаРасчетов) / 86400) + 1;
		СуммаДоходаНаДень = СуммаДохода / ЧислоДней;
		
		НачалоПериодаНачисления = ДатаНачалаРасчетов;
		ОкончаниеПериодаНачисления = Мин(НачалоДня(КонецМесяца(НачалоПериодаНачисления)), ДатаОкончанияРасчетов);
		
		Пока НачалоПериодаНачисления <= ДатаОкончанияРасчетов Цикл
			
			НовоеНачисление = Результат.Добавить();
			НовоеНачисление.Дата = ОкончаниеПериоданачисления;
			СуммаПроцентовЗаПериод = Окр(СуммаДоходаНаДень * (Цел((ОкончаниеПериоданачисления - НачалоПериодаНачисления)/86400) +1),2);
			НовоеНачисление.Сумма = ?(ОкончаниеПериоданачисления = ДатаОкончанияРасчетов, СуммаДохода, СуммаПроцентовЗаПериод);
			СуммаДохода = СуммаДохода - СуммаПроцентовЗаПериод;
			НачалоПериодаНачисления = ОкончаниеПериоданачисления + 86400;
			ОкончаниеПериоданачисления = Мин(НачалоДня(КонецМесяца(НачалоПериодаНачисления)), ДатаОкончанияРасчетов);
			
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции


#КонецОбласти

#Область СлужебныеПроцедурыИФункции
//Код процедур и функций
#КонецОбласти
