
#Область ПрограммныйИнтерфейс

Процедура УправлениеФормой_ТипОрганизации(Объект) Экспорт
		
	Если Объект.ИспользоватьВРегламентированномУчете Тогда
		
		Объект.ИностранныйНалоговыйРезидент = Ложь;
		Объект.ЭлиминирующаяОрганизация = Ложь;		
		
		Если Не ИдентификацияПродуктаУХКлиентСервер.ЭтоЕХ()
			И Не Объект.ИностраннаяОрганизация 
			И Не Объект.СтранаРегистрации.Пустая() Тогда
			//в ЕРПУХ - СтранаРегистрации мб заполнена и для обычной организации
			Объект.СтранаРегистрации = ПредопределенноеЗначение("Справочник.СтраныМира.ПустаяСсылка");	
		КонецЕсли;
		
	ИначеЕсли Объект.ИностранныйНалоговыйРезидент Тогда				
		Объект.ЭлиминирующаяОрганизация = Ложь;						
	КонецЕсли;
	
КонецПроцедуры

Процедура УправлениеФормой_РеквизитыУХ(Форма) Экспорт

	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ЭтоУправлениеХолдингом = ИдентификацияПродуктаУХКлиентСервер.ЭтоУправлениеХолдингом();	
	ЭтоЦФО = Не Объект.ИспользоватьВРегламентированномУчете И Не Объект.ИностранныйНалоговыйРезидент;
	
	Элементы.ИностранныйНалоговыйРезидентУХ.Видимость	= НЕ Объект.ЭлиминирующаяОрганизация;
	Элементы.СтранаРегистрацииРезидентаУХ.Видимость 	= Объект.ИностранныйНалоговыйРезидент;
	Элементы.СтранаРегистрации.Видимость 				= Не Объект.ИностранныйНалоговыйРезидент;
	
	#Область Регламент
	Элементы.ГруппаКонтрагентов.Видимость 			= НЕ Объект.ЭлиминирующаяОрганизация;
	Элементы.ВидОрганизации.Видимость 				= НЕ Объект.ЭлиминирующаяОрганизация;
	Элементы.ЭлиминирующаяОрганизацияУХ.Видимость	= ЭтоЦФО;
	#КонецОбласти
	
	#Область Казначейство	
	Если ЭтоУправлениеХолдингом Тогда
		Элементы.КазначействоУХ.Видимость = НЕ Объект.ЭлиминирующаяОрганизация;
		Элементы.ГруппаАдресМеждународный.Видимость = НЕ Объект.ЭлиминирующаяОрганизация;
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ЦУЗ	
	Если ЭтоУправлениеХолдингом Тогда
		
		флРеальнаяОрганизация = Объект.ИспользоватьВРегламентированномУчете ИЛИ Объект.ИностранныйНалоговыйРезидент;
		
		Элементы.ГруппаЦУЗ_УХ.Видимость = флРеальнаяОрганизация;
		Если флРеальнаяОрганизация Тогда
			Элементы.ИКО.Видимость = Объект.ЗакупкаПоФЗ223;
		КонецЕсли;
		
	КонецЕсли;		
	#КонецОбласти
	
КонецПроцедуры

Процедура УправлениеФормой_УправлениеАктивами(Форма) Экспорт
	
	ИспользуетсяУправлениеАктивами = Ложь;
	Если ИдентификацияПродуктаУХКлиентСервер.ЭтоУправлениеХолдингом() Тогда
		ИмяИспользуетсяУправлениеАктивами = "ИспользуетсяУправлениеАктивами";
		#Если Сервер Или ВнешнееСоединение Тогда
			ИспользуетсяУправлениеАктивами = ПолучитьФункциональнуюОпцию(ИмяИспользуетсяУправлениеАктивами);
		#Иначе	
			ИспользуетсяУправлениеАктивами = ПолучитьФункциональнуюОпциюИнтерфейса(ИмяИспользуетсяУправлениеАктивами);	
		#КонецЕсли		
	КонецЕсли;
	
	Если Не ИспользуетсяУправлениеАктивами Тогда
		Возврат;
	КонецЕсли;
	
    Объект = Форма.Объект;
    Элементы = Форма.Элементы;
	
	ФормаУчастияВКапиталеАкции = ОрганизацииКлиентСерверУХ.ФормаУчастияВКапиталеАкции(Объект.КодОКОПФ, Объект.ИностраннаяОрганизация); 
	ЭтоЦФО = Не Объект.ИспользоватьВРегламентированномУчете И Не Объект.ИностранныйНалоговыйРезидент;
		
	Если ФормаУчастияВКапиталеАкции = Истина Тогда
	
		Элементы.ФормаУчастияВКапитале.Доступность = Ложь;
		Объект.ФормаУчастияВКапитале = Истина;
		
	ИначеЕсли ФормаУчастияВКапиталеАкции = Ложь Тогда
		
		Элементы.ФормаУчастияВКапитале.Доступность = Ложь;
		Объект.ФормаУчастияВКапитале = Ложь;
		
	Иначе 
		
		Элементы.ФормаУчастияВКапитале.Доступность = Истина;
	
	КонецЕсли;
	
	Элементы.ГруппаУставныйКапитал.Видимость 	= Не ЭтоЦФО;
    Элементы.УставныйКапитал.Видимость 			= Объект.ФормаУчастияВКапитале;

КонецПроцедуры

Процедура УправлениеФормой_КИК(Форма) Экспорт

	Объект = Форма.Объект;
    Элементы = Форма.Элементы;

	Элементы.ГруппаИностраннаяОрганизация.Видимость	= Объект.ИностранныйНалоговыйРезидент Или Объект.ИностраннаяОрганизация;		
	Элементы.ГруппаИностраннаяСтруктура.Видимость	= Объект.ИностранныйНалоговыйРезидент И Объект.ЮридическоеФизическоеЛицо <> ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо");
	Элементы.ВидИностраннойСтруктуры.Видимость		= Объект.ИностраннаяСтруктураБезОбразованияЮрЛица;
	Элементы.Аудитор.Видимость 						= (Форма.Объект.Аудит <> ПредопределенноеЗначение("Перечисление.ВидАудитаОрганизации.НеПроводится"));
	
КонецПроцедуры

#Область ТипОрганизации

Процедура ЗаполнитьТипОрганизацииПоРеквизитам(Форма, ИмяВидОрганизации = "ВидОрганизацииПредставление") Экспорт
	
	Объект = Форма.Объект;
	
	ВидИностраннойСтруктуры = ?(Объект.ВидИностраннойСтруктуры.Пустая(), "", "(" + Объект.ВидИностраннойСтруктуры + ")");
	
	Если (Форма.Элементы.Найти(ИмяВидОрганизации) = Неопределено) Или Форма.Элементы[ИмяВидОрганизации].ТолькоПросмотр Тогда
		ВидОрганизации = Форма[ИмяВидОрганизации];//БП
	Иначе	
				
		ЗначениеВида = Форма.Элементы[ИмяВидОрганизации].СписокВыбора.НайтиПоЗначению(Форма[ИмяВидОрганизации]);
		Если ЗначениеВида <> Неопределено Тогда //Обособленное подразделение
			ВидОрганизации = ЗначениеВида.Представление;//УП		
		КонецЕсли;
		
	КонецЕсли;	
	
	Форма.Элементы.ТипОрганизацииУХ.СписокВыбора.Очистить();	
	Форма.Элементы.ТипОрганизацииУХ.СписокВыбора.Добавить(0, СтрШаблон(НСтр("ru = 'Налоговый резидент Российской Федерации (%1)'"), ВидОрганизации));
	Форма.Элементы.ТипОрганизацииУХ.СписокВыбора.Добавить(1, СтрШаблон(НСтр("ru = 'Иностранный налоговый резидент (%1)'"), ВидОрганизации));
	Форма.Элементы.ТипОрганизацииУХ.СписокВыбора.Добавить(2, СтрШаблон(НСтр("ru = 'Иностранная структура без образования юр. лица %1'"), ВидИностраннойСтруктуры));
	Если ИдентификацияПродуктаУХКлиентСервер.ЭтоУправлениеХолдингом() Тогда
		Форма.Элементы.ТипОрганизацииУХ.СписокВыбора.Добавить(3, НСтр("ru = 'Элиминирующая организационная единица'"));
	КонецЕсли;	
	Форма.Элементы.ТипОрганизацииУХ.СписокВыбора.Добавить(4, НСтр("ru = 'ЦФО или консолидирующая организационная единица'"));
		
	Если Объект.ИспользоватьВРегламентированномУчете Тогда
		Форма.ТипОрганизацииУХ = 0;
	ИначеЕсли Объект.ИностранныйНалоговыйРезидент И Объект.ИностраннаяСтруктураБезОбразованияЮрЛица Тогда
		Форма.ТипОрганизацииУХ = 2;
	ИначеЕсли Объект.ИностранныйНалоговыйРезидент Тогда
		Форма.ТипОрганизацииУХ = 1;
	ИначеЕсли Объект.ЭлиминирующаяОрганизация Тогда
	    Форма.ТипОрганизацииУХ = 3;
	Иначе 
		Форма.ТипОрганизацииУХ = 4;
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьРеквизитыПоТипуОрганизации(Форма) Экспорт

	Объект = Форма.Объект;
	ТипОрганизацииУХ = Форма.ТипОрганизацииУХ;
	
	Объект.ИспользоватьВРегламентированномУчете = (ТипОрганизацииУХ = 0);
	Объект.ИностранныйНалоговыйРезидент = (ТипОрганизацииУХ = 1 Или ТипОрганизацииУХ = 2);
	Объект.ИностраннаяСтруктураБезОбразованияЮрЛица = (ТипОрганизацииУХ = 2);
	Объект.ЭлиминирующаяОрганизация = (ТипОрганизацииУХ = 3);

КонецПроцедуры

#КонецОбласти

Функция ФормаУчастияВКапиталеАкции(КодОКОПФ, ИностраннаяОрганизация = Ложь) Экспорт

	Если ИностраннаяОрганизация Тогда
	
		Возврат Неопределено;
		
	ИначеЕсли КодОКОПФ = "" Тогда 
		
		Возврат Неопределено;
		
	ИначеЕсли (КодОКОПФ = "12247") 
		Или (КодОКОПФ = "12267")
		Или (КодОКОПФ = "12200") Тогда //ОАО, ЗАО, АО
		
		Возврат Истина;
		
	ИначеЕсли (КодОКОПФ	= "12165") 
		Или (КодОКОПФ = "12166")
		Или (КодОКОПФ = "12100") Тогда //ООО, ОДО, ОДО или ООО
		
		Возврат Ложь;
		
	Иначе

		Возврат Неопределено;
		
	КонецЕсли;

КонецФункции

// Возвращает номер картинки в коллекции картинок Организации по типу организации 
// (Копия аналогичной модуля РаботаСДиалогами)
Функция ПолучитьКартинкуСтрокПоТипуОрганизации(ТипОрганизации, ПометкаУдаления = Ложь) Экспорт
	
	Если ТипОрганизации = ПредопределенноеЗначение("Перечисление.ТипыОрганизационныхЕдиниц.Консолидирующая") Тогда
		Результат = 0;
	ИначеЕсли ТипОрганизации = ПредопределенноеЗначение("Перечисление.ТипыОрганизационныхЕдиниц.Элиминирующая") Тогда
		Результат = 2;
	ИначеЕсли ТипОрганизации = ПредопределенноеЗначение("Перечисление.ТипыОрганизационныхЕдиниц.Обычная") Тогда
		Результат = 1;
	Иначе                    
		Результат = 10;
	КонецЕсли;
	
	Результат = Результат + ?(ПометкаУдаления = Истина, 3, 0);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
