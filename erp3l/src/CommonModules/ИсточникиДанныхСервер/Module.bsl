
#Область ПрограммныйИнтерфейс

#Область ПравилаПолученияФактическихДанных
	
// Возвращает имена показателей и соответствующих полей из схемы-источника
// 
// Параметры:
//	СхемаКомпоновкиДанных - СхемаКомпоновкиДанных - схема источник данных, которая содержит набор данных типа "Объединение" с именем "ОбъединенныйФакт"
//	Правило - СправочникСсылка.ПравилаПолученияФактаПоПоказателямБюджетов, СправочникСсылка.ПравилаПолученияФактаПоСтатьямБюджетов - правило получения фактических данных.
//
// Возвращаемое значение:
//	Структура - сопоставленные показатели факта и поля источника данных
//		* Ключ     - Строка - имя показателя фактических данных
//		* Значение - Строка - имя поля в схеме-источнике данных.
// 
Функция ИсточникиСуммыПравила(СхемаКомпоновкиДанных, Правило) Экспорт
	
	ИсточникиСуммы = Новый Структура;
	
	НаборДанных = СхемаКомпоновкиДанных.НаборыДанных.Найти("ОбъединенныйФакт");
	Если НаборДанных = Неопределено Тогда
		Если СхемаКомпоновкиДанных.НаборыДанных.Количество() > 0 Тогда
			НаборДанных = СхемаКомпоновкиДанных.НаборыДанных[0];
		Иначе 
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В СКД правила ""%1"" нет ни одного набора данных.';
					|en = 'There are no data sets in the ""%1"" rule DCS.'"), 
				Правило);
			ВызватьИсключение ТекстСообщения;
		КонецЕсли;
	КонецЕсли;
		
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Правило)) Тогда
		РеквизитыПравила = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Правило, 
			"ИмяМакетаИсточникаДанных, 
			|РазделИсточникаДанных, 
			|ИсточникСуммыОперации, 
			|ИсточникВалютный, 
			|ИсточникДанных, 
			|ТипИтога");
	Иначе
		РеквизитыПравила = Правило;
	КонецЕсли;
	
	ИмяНабораДанных = РеквизитыПравила.ИмяМакетаИсточникаДанных; // Имя набора данных соответствует регистру операции
	
	Если ТипЗнч(Правило) = Тип("СправочникСсылка.ПравилаПолученияФактаПоПоказателямБюджетов") Тогда
		ИмяСправочникаИсточника = "ПравилаПолученияФактаПоПоказателямБюджетов";
	ИначеЕсли ТипЗнч(Правило) = Тип("СправочникСсылка.ПравилаПолученияФактаПоСтатьямБюджетов") Тогда
		ИмяСправочникаИсточника = "ПравилаПолученияФактаПоСтатьямБюджетов";
	КонецЕсли;
	
	Если РеквизитыПравила.РазделИсточникаДанных = Перечисления.РазделыИсточниковДанныхБюджетирования.ОперативныйУчет Тогда
		ПоказателиРегистра = МеждународныйУчетСерверПовтИсп.Показатели(ИмяНабораДанных);
		РесурсыИсточникаСуммы = ПоказателиРегистра.Получить(РеквизитыПравила.ИсточникСуммыОперации).Ресурсы;
		Если РесурсыИсточникаСуммы = Неопределено Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для источника суммы %1 хозяйственной операции %2 не найдены соответствующие ресурсы в источнике данных.';
					|en = 'Corresponding resources are not found in the data source for the source of amount %1 of business transaction %2.'"), 
				РеквизитыПравила.ИсточникСуммыОперации, 
				РеквизитыПравила.ИсточникДанных);
			ВызватьИсключение ТекстСообщения;
		КонецЕсли;
		
		Для каждого Ресурс Из РесурсыИсточникаСуммы Цикл
			ПолеСуммы = Ресурс["Имя"];
			Если НаборДанных.Поля.Найти(ПолеСуммы) = Неопределено Тогда
				// Ресурс не выбирается в источнике
				Продолжить;
			КонецЕсли;
			Если Ресурс["ИсточникВалюты"] = "ВалютаУпр" Тогда
				ИсточникиСуммы.Вставить("Упр", ПолеСуммы);
			ИначеЕсли Ресурс["ИсточникВалюты"] = "ВалютаРегл" Тогда
				ИсточникиСуммы.Вставить("Регл", ПолеСуммы);
			Иначе
				ИсточникиСуммы.Вставить("Валюта", ПолеСуммы);
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли РеквизитыПравила.РазделИсточникаДанных = Перечисления.РазделыИсточниковДанныхБюджетирования.РегламентированныйУчет Тогда
		ИсточникиСуммы.Вставить("Регл", БюджетированиеСервер.ПолеРесурсаБухгалтерии("Сумма", РеквизитыПравила.ТипИтога));
		Если РеквизитыПравила.ИсточникВалютный Тогда
			ИсточникиСуммы.Вставить("Валюта", БюджетированиеСервер.ПолеРесурсаБухгалтерии("СуммаВВалюте", РеквизитыПравила.ТипИтога));
		КонецЕсли;
	ИначеЕсли РеквизитыПравила.РазделИсточникаДанных = Перечисления.РазделыИсточниковДанныхБюджетирования.МеждународныйУчет Тогда
		ИсточникиСуммы.Вставить("Международ", БюджетированиеСервер.ПолеРесурсаБухгалтерии("Сумма", РеквизитыПравила.ТипИтога));
		Если РеквизитыПравила.ИсточникВалютный Тогда
			ИсточникиСуммы.Вставить("Валюта", БюджетированиеСервер.ПолеРесурсаБухгалтерии("СуммаВВалюте", РеквизитыПравила.ТипИтога));
		КонецЕсли;
		
	ИначеЕсли РеквизитыПравила.РазделИсточникаДанных = Перечисления.РазделыИсточниковДанныхБюджетирования.ПроизвольныеДанные Тогда
		БюджетированиеСервер.ЗаполнитьИсточникиСуммыПоСхеме(ИсточникиСуммы, НаборДанных.Элементы[ИмяНабораДанных]);
	КонецЕсли;
	
	Возврат ИсточникиСуммы;
	
КонецФункции

// Делает свертку однотипно названных колонок таблицы значений в одну колонку с переносом данных.
// Например, позволяет несколько колонок Аналитика1..6 свернуть в одну колонку Аналитика.
// Имена сворачиваемых колонок должны состоять из двух (трех) частей - ИмяКолонкиИзмерения+(ПрефиксИдентификатора)+Идентификатор.
// 
// Параметры:
//	ТаблицаДанных         - ТаблицаЗначений - таблица, колонки которой будут свернуты
//		После свертки таблица будет содержать следующие колонки:
//			Колонки, не указанные в КолонкиИзмерения
//			Колонки, указанные в КолонкиИзмерения, но свернутые из нескольких в одну
//			Колонки, указанные в КолонкиАгрегаты с просуммированными данными.
//	КолонкиИзмерения      - Строка, Массив - имена колонок через запятую, по уникальным  значениям которых будет сворачиваться таблица. 
//		После свертки колонки будут удалены.
//	Идентификаторы        - Массив - строковые идентификаторы, по которым будут сворачиваться колонки и которые содержаться в именах колонок. 
//		Например, уникальный идентификатор или порядковый номер колонки.
//	ПрефиксИдентификатора - Строка - префикс, который будет использоваться как составная часть идентификатора
//	КолонкиАгрегаты       - Строка, Массив - мена колонок через запятую, значения которых будут суммироваться при свертке.
//	НечеткоеСоответствиеИдентификаторов - Булево - Признак нечеткого соответствия идентификаторов
//
Процедура СвернутьПоКолонкамИдентификаторам(ТаблицаДанных, КолонкиИзмерения, Идентификаторы, ПрефиксИдентификатора = "", КолонкиАгрегаты = Неопределено, НечеткоеСоответствиеИдентификаторов = Ложь) Экспорт
	
	#Область Инициализация
	
	ИсходныеКолонки = ТаблицаДанных.Колонки;
	НетрансформируемыеИзмерения = Новый Массив;
	
	СКДСвертки = КомпоновкаДанныхСервер.ПустаяСхема();
	ПоляИтога = СКДСвертки.ПоляИтога;
	
	#КонецОбласти 
	
	#Область ПроверкаТиповПараметров
	
	Если ТипЗнч(КолонкиИзмерения) = Тип("Строка") Тогда
		// Удалим пробелы и сформируем список имен
		ИменаКолонокИзмерений = СтрРазделить(СтрЗаменить(КолонкиИзмерения, " ", ""), ",");
	ИначеЕсли ТипЗнч(КолонкиИзмерения) = Тип("Массив") Тогда
		ИменаКолонокИзмерений = КолонкиИзмерения;
	Иначе
		ВызватьИсключение НСтр("ru = 'Некорректный тип параметра 2';
								|en = 'Incorrect type of parameter 2'") ;
	КонецЕсли;
	
	Если ТипЗнч(КолонкиАгрегаты) = Тип("Строка") Тогда
		// Удалим пробелы и сформируем список имен
		ИменаКолонокАгрегатов = СтрРазделить(СтрЗаменить(КолонкиАгрегаты, " ", ""), ",");
	ИначеЕсли ТипЗнч(КолонкиАгрегаты) = Тип("Массив") Тогда
		ИменаКолонокАгрегатов = КолонкиАгрегаты;
	ИначеЕсли ТипЗнч(КолонкиАгрегаты) = Тип("Неопределено") Тогда
		ИменаКолонокАгрегатов = Новый Массив;
	Иначе
		ВызватьИсключение НСтр("ru = 'Некорректный тип параметра 4';
								|en = 'Incorrect type of parameter 4'") ;
	КонецЕсли;
	
	Если Не ТипЗнч(Идентификаторы) = Тип("Массив") Тогда
		ВызватьИсключение НСтр("ru = 'Некорректный тип параметра 5';
								|en = 'Incorrect type of parameter 5'") ;
	КонецЕсли;
	
	#КонецОбласти

	НДТаблицаДанных = КомпоновкаДанныхСервер.ДобавитьПустойНаборДанных(СКДСвертки, 
		Тип("НаборДанныхОбъектСхемыКомпоновкиДанных"), "ТаблицаДанных");

	#Область ДобавлениеИсходныхПолейВНаборДанных
	
	Для Каждого ИсходнаяКолонка Из ИсходныеКолонки Цикл
		ИмяКолонки = ИсходнаяКолонка.Имя;
		ФинансоваяОтчетностьСервер.НовоеПолеНабора(НДТаблицаДанных, ИмяКолонки, ИмяКолонки, , ИсходнаяКолонка.ТипЗначения);
		
		НетрансформируемыеИзмерения.Добавить(ИмяКолонки);
	КонецЦикла;
	
	#КонецОбласти 

	#Область ДобавлениеНовыхПолейИзмерений
		
	ЭлементыВыраженияСверткиПоля = Новый Массив;
	
	Для Каждого ИмяКолонкиИзмерения Из ИменаКолонокИзмерений Цикл
		
		ЭлементыВыраженияСверткиПоля.Добавить("ВЫБОР"); //@Query-part
		
		Для Каждого УникальныйИдентификатор Из Идентификаторы Цикл 
			
			ИмяИсходногоПоля = ИмяКолонкиИзмерения + ПрефиксИдентификатора + УникальныйИдентификатор;
			
			Если НечеткоеСоответствиеИдентификаторов И ИсходныеКолонки.Найти(ИмяИсходногоПоля) = Неопределено Тогда // Идентификаторов может быть передано больше, чем есть колонок
				Продолжить;
			КонецЕсли;
			
			ЭлементыВыраженияСверткиПоля.Добавить("КОГДА " + "ЗначениеЗаполнено(" + ИмяИсходногоПоля + ")" + Символы.ПС
			+ "ТОГДА " + ИмяИсходногоПоля);
			
			ИндексПоля = НетрансформируемыеИзмерения.Найти(ИмяИсходногоПоля);
			Если Не ИндексПоля = Неопределено Тогда
				НетрансформируемыеИзмерения.Удалить(ИндексПоля);
			КонецЕсли;
		КонецЦикла;
		
		ЭлементыВыраженияСверткиПоля.Добавить("КОНЕЦ");
		
		ВыражениеСверткиПоля = СтрСоединить(ЭлементыВыраженияСверткиПоля, Символы.ПС);
		ФинансоваяОтчетностьСервер.НовоеВычисляемоеПоле(СКДСвертки, ИмяКолонкиИзмерения, ВыражениеСверткиПоля, , Истина);
		
		ЭлементыВыраженияСверткиПоля.Очистить();
		
	КонецЦикла;
	
	#КонецОбласти 
	
	#Область ДобавлениеНовыхПолейАгрегатов
	
	Для Каждого ИмяКолонкиАгрегата Из ИменаКолонокАгрегатов Цикл
		
		ВыражениеАгрегата = "ЕСТЬNULL(" 
		+ "ВЫБОР КОГДА ЗначениеЗаполнено(" + ИмяКолонкиАгрегата + ") ТОГДА " + ИмяКолонкиАгрегата + " ИНАЧЕ 0 КОНЕЦ" + ", 0)";
		
		ФинансоваяОтчетностьСервер.НовоеВычисляемоеПоле(СКДСвертки, ИмяКолонкиАгрегата, ВыражениеАгрегата, , Истина);
		
		Если ПоляИтога.Найти(ИмяКолонкиАгрегата) = Неопределено Тогда
			ФинансоваяОтчетностьСервер.НовыйРесурс(СКДСвертки, ИмяКолонкиАгрегата, "Сумма");
		КонецЕсли;
		
		ИндексПоля = НетрансформируемыеИзмерения.Найти(ИмяКолонкиАгрегата);
		Если Не ИндексПоля = Неопределено Тогда
			НетрансформируемыеИзмерения.Удалить(ИндексПоля);
		КонецЕсли;
		
	КонецЦикла;
	
	#КонецОбласти 
	
	#Область СозданиеНастроек
	
	// Создадим и инициализируем компоновщик
	НастройкиСвертки = БюджетированиеСервер.ИнициализированныеНастройкиПоУмолчанию(СКДСвертки);
	
	КомпоновкаДанныхКлиентСервер.ОтключитьВыводОбщихИтогов(НастройкиСвертки);
	
	КорневойЭлементСтруктуры = НастройкиСвертки.Структура;
	
	ГруппировкаДанных = ФинансоваяОтчетностьСервер.НоваяГруппировка(КорневойЭлементСтруктуры);
	
	Для Каждого ИмяКолонки Из НетрансформируемыеИзмерения Цикл 
		
		ФинансоваяОтчетностьСервер.НовоеПолеГруппировки(ГруппировкаДанных, ИмяКолонки);
		
	КонецЦикла;
	
	Для Каждого ИмяКолонки Из ИменаКолонокИзмерений Цикл
		
		ФинансоваяОтчетностьСервер.НовоеПолеГруппировки(ГруппировкаДанных, ИмяКолонки);
		
	КонецЦикла;
	
	Для Каждого ИмяКолонки Из ИменаКолонокАгрегатов Цикл
		
		ФинансоваяОтчетностьСервер.НовоеПолеВыбора(НастройкиСвертки, ИмяКолонки);
	
	КонецЦикла;
	
	#КонецОбласти 
	
	#Область ПолучениеСвернутойТаблицы
	
	// Инициализировать макет компоновки, который будет сохраняться в кэш и выполняться
	МакетПолученияДанных = ФинансоваяОтчетностьСервер.ПодготовитьМакетКомпоновкиДляВыгрузкиСКД(СКДСвертки, НастройкиСвертки);
	
	// Выполнить процессор вывода в коллекцию значений
	ВнешниеНаборыДанных = Новый Структура("ТаблицаДанных", ТаблицаДанных);
	ТаблицаДанных = ФинансоваяОтчетностьСервер.ВыгрузитьРезультатСКДПоМакету(МакетПолученияДанных, ВнешниеНаборыДанных);
	
	#КонецОбласти 
	
КонецПроцедуры

// Возвращает схему для правила получения фактических данных
// 
// Параметры:
//	ОписаниеПравила - СправочникСсылка.ПравилаПолученияФактаПоПоказателямБюджетов, 
//	                - СправочникСсылка.ПравилаПолученияФактаПоСтатьямБюджетов,
//	                - ВыборкаИзРезультатаЗапроса, СтрокаТаблицыЗначений, Структура - Правило получения факта по показателям бюджетов.
//	ОчищаемыеКоллекцииНастроек - Соответствие - имена коллекций, которые требуется гарантированно очистить. Если значение заполнено, будет получена копия СКД.
//	                           - Неопределено - Значение по умолчанию. Настройки не очищаются.
//	РежимПолученияДанных - ПеречислениеСсылка.РежимПолученияДанныхБюджетирования - 
//	                                  Все         - в этом случае в запросах схемы компоновки данных удаляется слово РАЗРЕШЕННЫЕ. Возвращена будет копия СКД.
//	                                  Разрешенные - в этом случае схема исполняется как есть.
//	                       Если значение заполнено, будет получена копия СКД.
//	                     - Неопределено - Значение по умолчанию. Текст запроса не меняется.
//	ПолучатьКопиюДляИзменения  - Булево - По умолчанию Ложь. Если Истина, то возвращена будет копия СКД.
// 
// Возвращаемое значение:
//	СхемаКомпоновкиДанных - СхемаКомпоновкиДанных - схема получения фактических данных, соответствующая источнику.
//
Функция СхемаКомпоновкиДанныхПравила(ОписаниеПравила, ОчищаемыеКоллекцииНастроек = Неопределено, РежимПолученияДанных = Неопределено, ПолучатьКопиюДляИзменения = Ложь) Экспорт
	
	// Получим схему правила
	ТипОписанияПравила = ТипЗнч(ОписаниеПравила);
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипОписанияПравила) Тогда
		РеквизитыПравила = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОписаниеПравила, "Ссылка, РазделИсточникаДанных, ИсточникДанных, ХешСхемыКомпоновкиДанных");
		РеквизитыПравила.Вставить("ТипОбъектаИсточникаДанных", ТипЗнч(РеквизитыПравила.Ссылка));
	ИначеЕсли ТипОписанияПравила = Тип("СтрокаТаблицыЗначений") Или ТипОписанияПравила = Тип("Структура") Тогда
		РеквизитыПравила = ОписаниеПравила;
	ИначеЕсли ТипОписанияПравила = Тип("ДанныеФормыСтруктура") 
		Или НЕ ОбщегоНазначения.ЭтоСсылка(ТипОписанияПравила) Тогда // Объект правила
		РеквизитыПравила = Новый Структура("Ссылка, РазделИсточникаДанных, ИсточникДанных, ХешСхемыКомпоновкиДанных");
		ЗаполнитьЗначенияСвойств(РеквизитыПравила, ОписаниеПравила);
		РеквизитыПравила.Вставить("ТипОбъектаИсточникаДанных", ТипЗнч(РеквизитыПравила.Ссылка));
	КонецЕсли;
	
	Если РеквизитыПравила.РазделИсточникаДанных = Перечисления.РазделыИсточниковДанныхБюджетирования.ПроизвольныеДанные Тогда
		ПроизвольныеДанныеВИсточнике = Истина;
	Иначе
		ПроизвольныеДанныеВИсточнике = Ложь;
	КонецЕсли;
	
	ПоддерживаемыеТипыОбъектов = ИсточникиДанныхПовтИсп.ПоддерживаемыеСправочникиИсточникиДанных();
	
	ИмяСправочникаИсточника = ПоддерживаемыеТипыОбъектов[РеквизитыПравила.ТипОбъектаИсточникаДанных];
	
	Если ИмяСправочникаИсточника = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Недопустимый тип справочника-источника получения данных.';
								|en = 'Invalid type of the catalog-data source.'");
	КонецЕсли;
		
	Если Не ПроизвольныеДанныеВИсточнике Тогда
		СхемаКомпоновкиДанныхПравила =
			ИсточникиДанныхПовтИсп.СхемаКомпоновкиДанныхПравила(
				ИмяСправочникаИсточника, 
				РеквизитыПравила.РазделИсточникаДанных,
				РеквизитыПравила.ИсточникДанных);
		
	Иначе // Произвольная схема в источнике данных 
		СхемаКомпоновкиДанныхПравила =
			ИсточникиДанныхПовтИсп.ПроизвольнаяСхемаКомпоновкиДанныхПравила(
				ИмяСправочникаИсточника, 
				РеквизитыПравила.РазделИсточникаДанных,
				РеквизитыПравила.ХешСхемыКомпоновкиДанных);
		
	КонецЕсли;
	
	// Если схема изменяется и нет цели записать в кэшируемый модуль измененную схему, то получаем копию.
	ВозвращатьКопиюСхемы = ПолучатьКопиюДляИзменения
		ИЛИ НЕ ОчищаемыеКоллекцииНастроек = Неопределено
		ИЛИ РежимПолученияДанных = Перечисления.РежимПолученияДанныхБюджетирования.Все;
	
	Если ВозвращатьКопиюСхемы Тогда
		СхемаКомпоновкиДанныхПравила = КомпоновкаДанныхСервер.СкопироватьСхемуКомпоновкиДанных(СхемаКомпоновкиДанныхПравила);
	КонецЕсли;
	
	Если СхемаКомпоновкиДанныхПравила = Неопределено Тогда
		Если РеквизитыПравила.РазделИсточникаДанных = Перечисления.РазделыИсточниковДанныхБюджетирования.ОперативныйУчет
			И ЗначениеЗаполнено(РеквизитыПравила.ИсточникДанных) Тогда
			
			ИменаСхемУмолчаний = ИсточникиДанныхПовтИсп.ИменаСхемУмолчанийДляИсточниковДанных();
			ИмяСхемыУмолчания = ИменаСхемУмолчаний[ИмяСправочникаИсточника];
			
			ТекстСообщенияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для правила ""%1"" и источника ""%2"" не определена схема получения данных.';
					|en = 'Data receipt scheme is not determined for the ""%1"" rule and the ""%2"" source.'"), 
				РеквизитыПравила.Ссылка,
				ОписаниеПравила.ИсточникДанных);
					
			Если Не ЗначениеЗаполнено(ИмяСхемыУмолчания) Тогда
				
				ВызватьИсключение ТекстСообщенияОбОшибке;
				
			КонецЕсли;
			
			СхемаКомпоновкиДанныхПравила = Справочники[ИмяСправочникаИсточника].ПолучитьМакет(ИмяСхемыУмолчания);
			
			Если СхемаКомпоновкиДанныхПравила = Неопределено Тогда
				
				ВызватьИсключение ТекстСообщенияОбОшибке;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ СхемаКомпоновкиДанныхПравила = Неопределено Тогда
	
		// Очистка коллекций настроек, которые переопределяются потребителем
		Если ОчищаемыеКоллекцииНастроек <> Неопределено Тогда
			Для Каждого ОчищаемаяКоллекция Из ОчищаемыеКоллекцииНастроек Цикл
				ОчищаемаяКоллекцияСхемы = СхемаКомпоновкиДанныхПравила[ОчищаемаяКоллекция.Ключ];
				
				Если ОчищаемаяКоллекция.Ключ = "Параметры" Тогда
					Для Каждого ЭлементКоллекции Из ОчищаемаяКоллекцияСхемы Цикл
						ЭлементКоллекции.Значение = Неопределено;
					КонецЦикла;
				Иначе
					ОчищаемаяКоллекцияСхемы.Очистить();
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
			
		Если РежимПолученияДанных = Перечисления.РежимПолученияДанныхБюджетирования.Все Тогда
			Для Каждого Набор Из СхемаКомпоновкиДанныхПравила.НаборыДанных Цикл
				Если ТипЗнч(Набор) = Тип("НаборДанныхЗапросСхемыКомпоновкиДанных")
					И ЗначениеЗаполнено(Набор.Запрос) Тогда
					Набор.Запрос = СхемыЗапросов.УстановитьВыборкуРазрешенныхЗаписейВоВсемЗапросе(Набор.Запрос, Ложь);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СхемаКомпоновкиДанныхПравила;
	
КонецФункции

// Возвращает параметры получения факта по умолчанию
//
// Параметры:
//   ЗначенияПоУмолчанию                    - Структура, Неопределено - переопределенные начальные значения параметров.
//   ПараметрыИдентификацииВыражений - Структура, Неопределено - состав используемых выражений.
//
// Возвращаемое значение:
//  Структура - Параметры получения факта по статьям и показателя бюджетов
//    * ВалютаМеждународ                 - СправочникСсылка.Валюты - Закешированное значение функциональной валюты международного учета (см. МеждународныйУчетОбщегоНазначения.УчетныеВалюты) 
//    * ВалютаОтчета                     - СправочникСсылка.Валюты - Валюта данных
//    * ВалютаРегл                       - СправочникСсылка.Валюты - Закешированное значение валюты регл. учета. См. Константы.ВалютаРегламентированногоУчета
//    * ВалютаУпр                        - СправочникСсылка.Валюты - Закешированное значение валюты упр. учета. См. Константы.ВалютаУправленческогоУчета
//    * ВидыАналитик                     - Массив - содержит значения типа
//      ПланВидовХарактеристикСсылка.АналитикиСтатейБюджетов, в разрезе которой необходимо получить факт. **  - значения
//      массива
//    * ПоПравиламПолученияДанных        - Булево - добавлять в таблицу данных колонку с примененным правилом
//    * ВозвращатьСтатьюПоказательБюджета- Булево - добавлять в таблицу данных колонку со статьей/показателем (источник данных)
//    * ВозвращатьУпрИРеглСуммы          - Булево - Вернуть суммы из регистров упр. и регл.
//    * ДополнительныеДанные             - Структура - дополнительные данные, которые будут присоединяться к фактическим
//      данным
//      ** Вид                   - ПеречислениеСсылка.ВидДополнительныхДанныхИсточниковДанных - описывает способ
//          дополнения фактических данных
//      ** ТаблицаДанных         - ТаблицаЗначений - таблица дополнительных данных, содержащая колонку с именем поля-идентификатора
//    * ДополнительныйОтбор              - НастройкиКомпоновкиДанных - дополнительный глобальный отбор, который
//                                                                     накладывается на данные источника.
//    * ДополнительныеФильтрыПоАналитикам- Структура - поля глобального отбора на аналитики 1..6, на основании которого
//    формируются настройки.
//      ** Ключ     - Строка - имя поля, по которому требуется применить доп. отбор
//      ** Значение - Произвольный - значение доп. отбора
//    * ДополнительныеФильтрыПоИзмерениям- Структура - поля глобального отбора на измерения, на основании которого
//    формируются настройки.
//      ** Ключ     - Строка - имя поля, по которому требуется применить доп. отбор
//      ** Значение - Произвольный - значение доп. отбора
//    * ИндексыТаблиц                    - Соответствие - имена таблиц значений параметров получения факта и массивы
//      включенных в них индексах.
//      ** Ключ - Строка - имя таблицы в параметрах получения факта
//      ** Значение - Массив - массив строк, описывающих примененные индексы
//    * ИсходныеВыраженияЗаполненияАналитик - Соответствие - исходные выражения заполнения аналитик на языке формул:
//      **Ключ - Строка - Исходное выражение заполнения аналитики.
//      **Значение - СправочникСсылка.СтатьиБюджетов, СправочникСсылка.ПоказателиБюджетов - Статья или показатель бюджета
//      		(для указания на конкретный объект в случае ошибки).
//    * ИспользуемыеНумерованныеАналитики- Структура - задействованные нумерованные аналитики. Принимает значения от
//    "Аналитика1" до "Аналитика6"
//    * МаксимальныйНомерАналитики       - Число - число задействованных аналитик. Принимает значения от 1 до 6
//    * ОстаткиТолькоНаНачалоПериода     - Булево - Получать факт только на начало периода (для остатков), в противном
//                                                  случае данные получаются на конец каждого из подпериодов.
//    * Период                           - СтандартныйПериод - Период выборки
//    * Периодичность                    - ПеречислениеСсылка.Периодичность, Неопределено - Периодичность детализации
//        данных за период. Неопределено - без детализации.
//    * Показатели                       - Структура - показатели, которые автоматически подбираются в выборку с учетом
//      настроек набора данных источника
//      ** Количество - Булево, Неопределено - присутствие свойства в структуре означает, что в выборку следует добавить
//                                             получение количества
//      ** Сумма      - Булево, Неопределено - присутствие свойства в структуре означает, что в выборку следует добавить
//                                             получение суммы
//    * ФактПоСтатьямВлияющимНаПоказателиБюджетовДополнятьХранимымФактом - Булево - Дополнять факт по статьям, влияющим на показатели хранимым фактом.
//                                                                                - По умолчанию Ложь. Ложь - если весь хранимый факт получается отдельно (с помощью кэшируемых скд).
//                                                                                - В бюджетных отчетах (и экземплярах бюджетов) хранимый факт получается отдельно (с помощью кэшируемых скд),
//                                                                                  в лимитах хранимый факт не используется (т.к. правила с типом исполнение бюджета не поддерживают хранимый факт).
//    * ПоВалютам                        - Булево - Развернуть по валютам (суммовые показатели).
//    * ПоЕдиницамИзмерения              - Булево - Развернуть по единицам измерения (количественные показатели).
//    * ПоОрганизациям                   - Булево - Развернуть по организациям.
//    * ПоПодразделениям                 - Булево - Развернуть по подразделениям.
//    * ПоРегистратору                   - Булево - Развернуть факт по регистратору (для оборотов).
//    * РежимПолученияДанных             - ПеречислениеСсылка.РежимПолученияДанныхБюджетирования - определяет режим
//        использования RLS при получении данных
//    * ТаблицаАналитик                  - ТаблицаЗначений - правила, используемые измерения, аналитики и мэппинг
//      аналитик (вид и выражение для получения). По умолчанию пустая таблица.
//      ** Правило                          - СправочникСсылка.ПравилаПолученияФактаПоСтатьямБюджетов - ссылка на правило
//      ** ПоОрганизациям                   - Булево - получать данные по соответствующему измерению
//      ** ПоПодразделениям                 - Булево - получать данные по соответствующему измерению
//      ** ВидАналитики1..6                 - ПланВидовХарактеристикСсылка.АналитикиСтатейБюджетов - вид аналитики
//      ** ВыражениеЗаполненияАналитики1..6 - Строка - выражение для получения данные из полей схемы-источника
//    * ТаблицаПериодов                  - ТаблицаЗначений - непрерывные периоды, за которые требуется получить данные.
//      По умолчанию пустая таблица.
//      ** ПравилоФакта  - СправочникСсылка.ПравилаПолученияФактаПоСтатьямБюджетов - ссылка на правило
//      ** НачалоПериода - Дата - дата начала периода
//      ** КонецПериода  - Дата - дата окончания периода
//    * ТаблицаПравилСтатей              - ТаблицаЗначений - правила получения факта и колонки с реквизитами. По
//        умолчанию пустая таблица. См. подробнее в Справочники.ПравилаПолученияФактаПоСтатьямБюджетов.Макеты.ПравилаПолученияФакта
//    * ТребуетсяПересчетПоКурсу         - Булево - пересчитать по курсам или добавить колонку "ПериодКурса".
//
Функция ШаблонПараметровПолученияФакта(ЗначенияПоУмолчанию = Неопределено, ПараметрыИдентификацииВыражений = Неопределено) Экспорт
	
	МаксимальноеКоличествоАналитик = БюджетированиеКлиентСервер.МаксимальноеКоличествоАналитик();
	Параметры = Новый Структура;
	
	Параметры.Вставить("Период",                            Новый СтандартныйПериод);
	Параметры.Вставить("Периодичность",                     Неопределено); // Без детализации по периодам
	Параметры.Вставить("ГраницаФактДанных",                 Дата(1, 1, 1));
	Параметры.Вставить("ДатаАктуальности",                  Дата(1, 1, 1));
	Параметры.Вставить("ОстаткиТолькоНаНачалоПериода",      Ложь);
	Параметры.Вставить("ВозвращатьСуммуВВалюте",            Истина);
	Параметры.Вставить("ВозвращатьУпрИРеглСуммы",           Ложь);
	Параметры.Вставить("ПоПравиламПолученияДанных",         Ложь);
	Параметры.Вставить("ВозвращатьСтатьюПоказательБюджета", Ложь);
	Параметры.Вставить("ТребуетсяПересчетПоКурсу",          Ложь);
	Параметры.Вставить("Сценарий",                          Справочники.Сценарии.ПустаяСсылка());
	
	// Показатели данных
	Параметры.Вставить("Показатели", Новый Структура("Количество, Сумма"));
	Параметры.Вставить("ФактПоСтатьямВлияющимНаПоказателиБюджетовДополнятьХранимымФактом", Ложь);
	
	// Разрезы данных
	Параметры.Вставить("ПоВалютам",                         Ложь);
	Параметры.Вставить("ПоЕдиницамИзмерения",               Ложь);
	Параметры.Вставить("ПоОрганизациям",                    Ложь);
	Параметры.Вставить("ПоПодразделениям",                  Ложь);
	Параметры.Вставить("ПоРегистратору",                    Ложь);
	
	Параметры.Вставить("ВидыАналитик",                      Новый Массив);
	Параметры.Вставить("МаксимальныйНомерАналитики",        0);
	Параметры.Вставить("ИспользуемыеНумерованныеАналитики", Новый Структура);
	Параметры.Вставить("ИсходныеВыраженияЗаполненияАналитик", Новый Соответствие);
	
	Параметры.Вставить("ДополнительныйОтбор");
	Параметры.Вставить("ДополнительныеФильтрыПоАналитикам", Новый Структура);
	Параметры.Вставить("ДополнительныеФильтрыПоИзмерениям", Новый Структура);
	Параметры.ДополнительныеФильтрыПоИзмерениям.Вставить("Организация");
	Параметры.ДополнительныеФильтрыПоИзмерениям.Вставить("Подразделение");
	Параметры.ДополнительныеФильтрыПоИзмерениям.Вставить("Сценарий");
	Параметры.ДополнительныеФильтрыПоИзмерениям.Вставить("ВалютаХранения");
	
	// Ограничения доступа к данным
	Параметры.Вставить("ОграниченияДоступа", Новый Структура);
	Параметры.ОграниченияДоступа.Вставить("ЕстьПривилегированныйРежим", Ложь);
	Параметры.ОграниченияДоступа.Вставить("ЕстьНеПривилегированныйРежим", Ложь);
	Параметры.ОграниченияДоступа.Вставить("РежимПолученияДанных", Перечисления.РежимПолученияДанныхБюджетирования.ПустаяСсылка());
		
	// Валюты
	// ++ НЕ УТКА
	УстановитьПривилегированныйРежим(Истина);
	РеквизитыПланаСчетов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Справочники.ПланыСчетовМеждународногоУчета.Международный,
		"ВалютаПредставления, УчетВФункциональнойВалюте");
	Если РеквизитыПланаСчетов.УчетВФункциональнойВалюте = Перечисления.ВидыУчетаВФункциональнойВалюте.ВВалютеРегл Тогда
		ФунциональнаяВалюта = Константы.ВалютаРегламентированногоУчета.Получить();
	Иначе
		ФунциональнаяВалюта = ЗначениеНастроекПовтИсп.ВалютаУправленческогоУчета();
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	Параметры.Вставить("ВалютаМеждународ", ФунциональнаяВалюта);
	//-- НЕ УТКА
	Параметры.Вставить("ВалютаОтчета",     Справочники.Валюты.ПустаяСсылка());
	Параметры.Вставить("ВалютаРегл",       Константы.ВалютаРегламентированногоУчета.Получить());
	Параметры.Вставить("ВалютаУпр",        Константы.ВалютаУправленческогоУчета.Получить());
	
	// Параметры пакетного получения данных с помощью правил получения данных
	ОписаниеТипаПравилаПолученияФакта = ИсточникиДанныхПовтИсп.ОписаниеТипаПравил();
	
	ОписаниеТипаБулево                = Новый ОписаниеТипов("Булево");
	ОписаниеТипаСтрока                = ОбщегоНазначения.ОписаниеТипаСтрока(0);
	ОписаниеТиповДата                 = ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата);
	ОписаниеТипаИД                    = ОбщегоНазначения.ОписаниеТипаЧисло(4,, ДопустимыйЗнак.Неотрицательный);
	
	Параметры.Вставить("ТаблицаПравилСтатей", Новый ТаблицаЗначений);
	
	Параметры.Вставить("ОбластиДанных", Новый Структура);
	
	ТаблицаОбластейДанных = Новый ТаблицаЗначений;
	ТаблицаОбластейДанных.Колонки.Добавить("ИдентификаторИсточникаДанных",    ОписаниеТипаСтрока);
	ТаблицаОбластейДанных.Колонки.Добавить("ПривилегированныйРежимИсточника", ОписаниеТипаБулево);
	ТаблицаОбластейДанных.Колонки.Добавить("Период",                          ОписаниеТиповДата);
	ТаблицаОбластейДанных.Колонки.Добавить("НачалоПериода",                   ОписаниеТиповДата);
	ТаблицаОбластейДанных.Колонки.Добавить("КонецПериода",                    ОписаниеТиповДата);
	ТаблицаОбластейДанных.Колонки.Добавить("ВыражениеПериод",                 ОписаниеТипаСтрока);
	ТаблицаОбластейДанных.Колонки.Добавить("ВыражениеПериодГраница",          ОписаниеТипаСтрока);
	ТаблицаОбластейДанных.Колонки.Добавить("ВыражениеНачалоПериода",          ОписаниеТипаСтрока);
	ТаблицаОбластейДанных.Колонки.Добавить("ВыражениеКонецПериода",           ОписаниеТипаСтрока);
	ТаблицаОбластейДанных.Колонки.Добавить("ИД_ВыражениеПериод",              ОписаниеТипаИД);
	ТаблицаОбластейДанных.Колонки.Добавить("ИД_ВыражениеНачалоПериода",       ОписаниеТипаИД);
	ТаблицаОбластейДанных.Колонки.Добавить("ИД_ВыражениеКонецПериода",        ОписаниеТипаИД);
	ТаблицаОбластейДанных.Колонки.Добавить("ПолучениеОстатковНаГраницуПериода", ОписаниеТипаБулево);
	
	Параметры.ОбластиДанных.Вставить("ТаблицаДанных", ТаблицаОбластейДанных);
	Параметры.ОбластиДанных.Вставить("КолонкиСвертки", 
		"ИдентификаторИсточникаДанных, 
		|ПривилегированныйРежимИсточника,
		|Период, НачалоПериода, КонецПериода,
		|ИД_ВыражениеНачалоПериода, ИД_ВыражениеКонецПериода, ИД_ВыражениеПериод,
		|ПолучениеОстатковНаГраницуПериода");
	
	Параметры.Вставить("УсловияВыражений", Новый Структура); 
	Если Не ПараметрыИдентификацииВыражений = Неопределено Тогда
		Для Каждого ЧастьВыражения Из ПараметрыИдентификацииВыражений.ЧастиВыражений Цикл 
			Параметры.УсловияВыражений.Вставить(ЧастьВыражения.Ключ);
		КонецЦикла;
	Иначе
		Параметры.УсловияВыражений.Вставить("ВыражениеНачалоПериода");
		Параметры.УсловияВыражений.Вставить("ВыражениеКонецПериода");
		Параметры.УсловияВыражений.Вставить("ВыражениеПериод");
		Параметры.УсловияВыражений.Вставить("ВыражениеПериодГраница");
		
		Для НомерАналитики = 1 По МаксимальноеКоличествоАналитик Цикл
			Параметры.УсловияВыражений.Вставить("ВыражениеЗаполненияАналитики" + НомерАналитики);
		КонецЦикла;
		Параметры.УсловияВыражений.Вставить("ВыражениеЗаполненияАналитикиВалюта");
		Параметры.УсловияВыражений.Вставить("ВыражениеЗаполненияАналитикиВалютаХранения");
		Параметры.УсловияВыражений.Вставить("ВыражениеЗаполненияПериодКурса");
		Параметры.УсловияВыражений.Вставить("ВыражениеЗаполненияАналитикиЕдиницаИзмерения");
		
		// Условия значений статей и показателей
		Параметры.УсловияВыражений.Вставить("ВыражениеПоказателяКоличествоНачальныйОстаток");
		Параметры.УсловияВыражений.Вставить("ВыражениеПоказателяСуммаВалютаНачальныйОстаток");
		Параметры.УсловияВыражений.Вставить("ВыражениеПоказателяСуммаВалютаУпрНачальныйОстаток");
		Параметры.УсловияВыражений.Вставить("ВыражениеПоказателяСуммаВалютаРеглНачальныйОстаток");
		
		Параметры.УсловияВыражений.Вставить("ВыражениеПоказателяКоличествоПриход");
		Параметры.УсловияВыражений.Вставить("ВыражениеПоказателяСуммаВалютаПриход");
		Параметры.УсловияВыражений.Вставить("ВыражениеПоказателяСуммаВалютаУпрПриход");
		Параметры.УсловияВыражений.Вставить("ВыражениеПоказателяСуммаВалютаРеглПриход");
		
		Параметры.УсловияВыражений.Вставить("ВыражениеПоказателяКоличествоРасход");
		Параметры.УсловияВыражений.Вставить("ВыражениеПоказателяСуммаВалютаРасход");
		Параметры.УсловияВыражений.Вставить("ВыражениеПоказателяСуммаВалютаУпрРасход");
		Параметры.УсловияВыражений.Вставить("ВыражениеПоказателяСуммаВалютаРеглРасход");
	КонецЕсли;
	
	ТаблицаАналитик = Новый ТаблицаЗначений;
	ТаблицаАналитик.Колонки.Добавить("Правило",          ОписаниеТипаПравилаПолученияФакта);
	ТаблицаАналитик.Колонки.Добавить("ПоОрганизациям",   ОписаниеТипаБулево);
	ТаблицаАналитик.Колонки.Добавить("ПоПодразделениям", ОписаниеТипаБулево);
	Для НомерАналитики = 1 По МаксимальноеКоличествоАналитик Цикл
		ТаблицаАналитик.Колонки.Добавить("ВидАналитики" + НомерАналитики);
		ТаблицаАналитик.Колонки.Добавить("ВыражениеЗаполненияАналитики" + НомерАналитики);
	КонецЦикла;
	
	Параметры.Вставить("ТаблицаАналитик", ТаблицаАналитик);
	
	ТаблицаПериодов = Планирование.ШаблонТаблицыПериодов();
	ТаблицаПериодов.Колонки["ДатаНачала"].Имя = "НачалоПериода";
	ТаблицаПериодов.Колонки["ДатаОкончания"].Имя = "КонецПериода";
	ТаблицаПериодов.Колонки.Добавить("ПравилоФакта", Новый ОписаниеТипов("СправочникСсылка.ПравилаПолученияФактаПоСтатьямБюджетов"));
	
	Параметры.Вставить("ТаблицаПериодов", ТаблицаПериодов);
	
	Параметры.Вставить("ДополнительныеДанные", Новый Структура);
	Параметры.ДополнительныеДанные.Вставить("ИмяПоляИдентификатора",    "");
	Параметры.ДополнительныеДанные.Вставить("Вид",                      Перечисления.ВидДополнительныхДанныхИсточниковДанных.ПустаяСсылка());
	Параметры.ДополнительныеДанные.Вставить("НумерацияИдентификаторов", Ложь);
	Параметры.ДополнительныеДанные.Вставить("ТаблицаДанных",            Новый ТаблицаЗначений);
	
	// Иерархические источники данных
	ТаблицаСопоставлений = Новый ТаблицаЗначений;
	ТаблицаСопоставлений.Колонки.Добавить("ИсточникДанных");
	ТаблицаСопоставлений.Колонки.Добавить("ИсточникДанныхИсходный");
	
	Параметры.Вставить("ИерархияИсточниковДанных", Новый Структура);
	Параметры.ИерархияИсточниковДанных.Вставить("ТаблицаСопоставлений", ТаблицаСопоставлений);
	Параметры.ИерархияИсточниковДанных.Вставить("СопоставлениеДляОтборов", Новый Соответствие);
	
	ТаблицаСопоставленийКорСчетов = Новый ТаблицаЗначений;
	ТаблицаСопоставленийКорСчетов.Колонки.Добавить("КорСчет");
	ТаблицаСопоставленийКорСчетов.Колонки.Добавить("КорСчетИсходный");
	Параметры.ИерархияИсточниковДанных.Вставить("ТаблицаСопоставленийКорСчетов", ТаблицаСопоставленийКорСчетов);
	
	Параметры.Вставить("ИндексыТаблиц", Новый Соответствие); // Ключ - название таблицы, значение - массив строк индексов
	
	Если ЗначенияПоУмолчанию <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Параметры, ЗначенияПоУмолчанию);
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

// Возвращает хранимые настройки из указанного справочника
//
// Параметры:
//  ИмяСправочникаИсточника  - Строка - например, "ПравилаПолученияФактаПоПоказателямБюджетов"
//  ХешНастроек - Строка - хеш-сумма настроенных отборов компоновки данных
//  ПроверкаУникальности - Булево - указывает на необходимость добавления в дополнительные свойства Ссылки.
//
// Возвращаемое значение:
//  КомпоновщикНастроекКомпоновкиДанных 
//
Функция ПолучитьХранимыеНастройкиСправочника(ИмяСправочникаИсточника, ХешНастроек, ПроверкаУникальности = Ложь) Экспорт
	
	ХранимыеНастройки = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИсточникДанныхПравила.Ссылка КАК Ссылка,
	|	ИсточникДанныхПравила.КомпоновщикНастроек КАК КомпоновщикНастроек
	|ИЗ
	|	&ТекстЗапросаТаблицаСправочника КАК ИсточникДанныхПравила
	|ГДЕ
	|	ИсточникДанныхПравила.ХешНастроек = &ХешНастроек";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаТаблицаСправочника", "Справочник." + ИмяСправочникаИсточника);
	Запрос.УстановитьПараметр("ХешНастроек", ХешНастроек);
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		ХранимыеНастройки = Выборка.КомпоновщикНастроек.Получить();
		Если Не ХранимыеНастройки = Неопределено И ПроверкаУникальности Тогда
			ХранимыеНастройки.ДополнительныеСвойства.Вставить("Ссылка", Выборка.Ссылка);
		КонецЕсли;
	КонецЕсли;

	Возврат ХранимыеНастройки;

КонецФункции

// Возвращает хранимый макет из указанного справочника
//
// Параметры:
//  ИмяСправочникаИсточника  - Строка - например, "ПравилаПолученияФактаПоПоказателямБюджетов"
//  ХешСхемыКомпоновкиДанных - Строка - хеш-сумма произвольной схемы компоновки данных
//  ПроверкаУникальности - Булево - указывает на необходимость добавления в дополнительные свойства Ссылки.
//
// Возвращаемое значение:
//  ТабличныйДокумент, ТекстовыйДокумент - объект, который может быть макетом. 
//
Функция ПолучитьХранимыйМакетСправочника(ИмяСправочникаИсточника, ХешСхемыКомпоновкиДанных, ПроверкаУникальности = Ложь) Экспорт
	
	ХранимыйМакет = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИсточникДанныхПравила.Ссылка КАК Ссылка,
	|	ИсточникДанныхПравила.СхемаИсточникаДанных КАК СхемаИсточникаДанных
	|ИЗ
	|	&ТекстЗапросаТаблицаСправочника КАК ИсточникДанныхПравила
	|ГДЕ
	|	ИсточникДанныхПравила.ХешСхемыКомпоновкиДанных = &ХешСхемы";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаТаблицаСправочника", "Справочник." + ИмяСправочникаИсточника);
	Запрос.УстановитьПараметр("ХешСхемы", ХешСхемыКомпоновкиДанных);
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		ХранимыйМакет = Выборка.СхемаИсточникаДанных.Получить();
		Если Не ХранимыйМакет = Неопределено И ПроверкаУникальности Тогда
			ХранимыйМакет.НастройкиПоУмолчанию.ДополнительныеСвойства.Вставить("Ссылка", Выборка.Ссылка);
		КонецЕсли;
	КонецЕсли;

	Возврат ХранимыйМакет;

КонецФункции

#КонецОбласти 

#Область ИндексыТаблицЗначений

// Возвращает строковые представления индексов
// 
// Параметры:
//	ТаблицаЗначений - ТаблицаЗначений - таблица, индексы которой требуется получить.
//
// Возвращаемое значение:
//	Массив - строковые представления индексов таблицы.
// 
Функция ИндексыТаблицыВМассив(ТаблицаЗначений) Экспорт
	ИндексыТаблицы = Новый Массив;
	
	Для Каждого ИндексТаблицы Из ТаблицаЗначений.Индексы Цикл
		ИндексыТаблицы.Добавить(Строка(ИндексТаблицы));
	КонецЦикла;
	
	Возврат ИндексыТаблицы;
	
КонецФункции

// Возвращает строковые представления индексов для нескольких таблиц
// 
// Параметры:
//	ТаблицыЗначений - Соответствие, Структура - таблицы, индексы которой требуется получить
//		* Ключ     - Строка - имя таблицы
//		* Значение - ТаблицаЗначений - таблица, индексы которой требуется получить.
//
// Возвращаемое значение:
//	Соответствие - индексы таблиц
//		* Ключ     - Строка - имя таблицы
//		* Значение - Массив - строковые представления индексов таблицы.
// 
Функция ИменаТаблицИИндексы(ТаблицыЗначений) Экспорт
	ИменаТаблицИИндексы = Новый Соответствие;
	
	Для Каждого ЭлементТаблицЗначений Из ТаблицыЗначений Цикл 
		ИменаТаблицИИндексы.Вставить(ЭлементТаблицЗначений.Ключ, ИндексыТаблицыВМассив(ЭлементТаблицЗначений.Значение));
	КонецЦикла;
	
	Возврат ИменаТаблицИИндексы;
КонецФункции

// Устанавливает или очищает индексы таблицы значений
// 
// Параметры:
//	ТаблицаЗначений - ТаблицаЗначений - таблица, индексы которой требуется установить
//	ИндексыТаблицы - Массив, Неопределено - строковые представления индексов таблицы.
//		Если передано Неопределено, то индексы таблицы будут очищены.
//
Процедура УстановитьИндексыИзМассива(ТаблицаЗначений, ИндексыТаблицы = Неопределено) Экспорт
	
	Если ИндексыТаблицы = Неопределено Тогда
		ТаблицаЗначений.Индексы.Очистить();
	Иначе
		
		Для Каждого ИндексТаблицы Из ИндексыТаблицы Цикл
			ТаблицаЗначений.Индексы.Добавить(ИндексТаблицы);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#КонецОбласти
