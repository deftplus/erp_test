
#Область ПрограммныйИнтерфейс

Процедура ПодготовитьТаблицуОпераций(ТаблицаОпераций, КэшируемыеЗначения, Организация) Экспорт
	
	Макет = Обработки.ЗакрытиеПериодаМСФО.ПолучитьМакет("СписокОпераций");
	
	ЭтоТК = Не КэшируемыеЗначения.ФормироватьПроводкиМСФО;
	ЭтоУХ = ИдентификацияПродуктаУХКлиентСервер.ЭтоУправлениеХолдингом();
	
	Замены = Новый Соответствие;
	Замены.Вставить("1", ЭтоТК);
	Замены.Вставить("2", Не ЭтоУХ);
	
	Для Счетчик = 2 По Макет.ВысотаТаблицы Цикл
		
		Попытка
			
			ТК 			= Булево(Макет.Область(Счетчик, 4, Счетчик, 4).Текст);
			ТУ			= Булево(Макет.Область(Счетчик, 5, Счетчик, 5).Текст);
			ТекстСп		= Макет.Область(Счетчик, 6, Счетчик, 6).Текст;
			Документ	= Макет.Область(Счетчик, 2, Счетчик, 2).Текст;
			
			Если ПустаяСтрока(ТекстСп) Тогда
				Справочно = Ложь;
			Иначе
				Замена = Замены.Получить(ТекстСп);
				Если Замена = Неопределено Тогда
					Справочно = Булево(Вычислить(ТекстСп));
				Иначе 
					Справочно = Замена;
				КонецЕсли;
				
			КонецЕсли;
			
		Исключение
			ОбщегоНазначенияУХ.СообщитьОбОшибке(НСтр("ru = 'Ошибка вычисления правил операции закрытия № '") + (Счетчик - 1));
			Продолжить;
		КонецПопытки;
		
		Если (НЕ ЭтоУХ) И (НЕ ТУ) Тогда
			Продолжить;
		ИначеЕсли Метаданные.НайтиПоПолномуИмени(Документ) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаТаблицы = ТаблицаОпераций.Добавить();
		СтрокаТаблицы.Документ		= Документ;
		СтрокаТаблицы.Представление	= Макет.Область(Счетчик, 1, Счетчик, 1).Текст;
		СтрокаТаблицы.ВидОперации	= Макет.Область(Счетчик, 3, Счетчик, 3).Текст;
		СтрокаТаблицы.Справочно 	= Справочно;
		СтрокаТаблицы.Требуется 	= (Не ЭтоТК И ТУ) ИЛИ (ЭтоТК И ТК);
		
	КонецЦикла;
	
	ЗакрытиеМесяцаМСФОПереопределяемый.ПодготовитьТаблицуОпераций(ТаблицаОпераций, КэшируемыеЗначения);
	
КонецПроцедуры

Функция ПодготовитьЗапросДанныхОпераций(ТаблицаОпераций, ОтборыЗапроса) Экспорт
	
	ЗапросВведенных = Новый Запрос;
	Счетчик = 1;
	
	Для каждого СтрокаТаблицы Из ТаблицаОпераций Цикл
		Счетчик = Счетчик + 1;
		ЗапросВведенных.Текст = ЗапросВведенных.Текст + ?(Счетчик = 2, "", Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ " + Символы.ПС) 
			+ СтрШаблон("ВЫБРАТЬ
		    			|	Таблица.Ссылка,
		                |	Таблица.Дата,
		                |	Таблица.Проведен,
		                |	Таблица.Ответственный,
		                |	""%1"" КАК Документ,
						|	" + ?(ПустаяСтрока(СтрокаТаблицы.ВидОперации), 
							"НЕОПРЕДЕЛЕНО", 
							"""" + СтрокаТаблицы.ВидОперации + """") + " КАК ВидОперации 
		                |ИЗ
		                |	%1 КАК Таблица
		                |ГДЕ
		                |	Таблица.Организация = &Организация
		                |	И Таблица.ПериодОтчета = &ПериодОтчета
		                |	И Таблица.Сценарий = &Сценарий" 
							+ ?(ПустаяСтрока(СтрокаТаблицы.ВидОперации), "", " И %2.ВидОперации = ЗНАЧЕНИЕ(Перечисление." 
							+ СтрокаТаблицы.ВидОперации + ")"), СтрокаТаблицы.Документ);

	КонецЦикла;	

	ЗапросВведенных.УстановитьПараметр("ПериодОтчета", ОтборыЗапроса.ПериодРегистрации);
	ЗапросВведенных.УстановитьПараметр("Организация", ОтборыЗапроса.Организация);
	ЗапросВведенных.УстановитьПараметр("Сценарий", ОтборыЗапроса.Сценарий);
	
	ЗапросВведенных = ЗакрытиеМесяцаМСФОПереопределяемый.ПодготовитьЗапросДанныхОпераций(ЗапросВведенных, ТаблицаОпераций, ОтборыЗапроса);
	
	Возврат ЗапросВведенных;
	
КонецФункции

#КонецОбласти