////////////////////////////////////////////////////////////////////////////////
// Работа со связанными файлами в ДО и в текущей ИБ
//

#Область ПрограммныйИнтерфейс


// Возвращает массив структур, описывающих файлы владельца независимо от места хранения
//
// Параметры:
//   Владелец - ЛюбаяСсылка - объект-владелец связанных файлов
//   ДокументID - идентификатор связанного объекта ДО (если не передан, будет определен автоматически)
//   ДокументТип - тип связанного объекта ДО (если не передан, будет определен автоматически)
//   ВключатьПомеченныеНаУдаление - Булево - Истина, если требуется получить и помеченные на удаление.
//
// Возвращаемое значение:
//   Массив - структуры, описывающие реквизиты связанных файлов
//
Функция СвязанныеФайлыПоВладельцу(Владелец, ДокументID = "", ДокументТип = "", 
				ВключатьПомеченныеНаУдаление = Ложь) Экспорт
	
	Возврат ПолучитьОбщийМодульСвязанныхФайловПоТипуХранения()
				.СвязанныеФайлыПоВладельцу(
					Владелец, ДокументID, ДокументТип,
					ВключатьПомеченныеНаУдаление);
КонецФункции

// Возвращает структуру, описывающую файл, найденный по идентификатору
// независимо от места хранения (1С:Документооборот или Справочник.Файлы).
//
// Параметры:
//   ДокументID - идентификатор связанного объекта ДО или УИД Справочник.Файлы
//
// Возвращаемое значение:
//  Структура, описывающая реквизиты найденного файла, если файл найден. Содержит следующие поля:
//        - ИндексКартинки - Число.
//        - Наименование - Строка, имя без расширения.
//        - Расширение - расширение без точки.
//        - СвязанныйФайл - Строка, СправочникСсылка.Фалы - идентификатор в документообороте или ссылка на справочник.
//        - Идентификатор - Строка, идентификатор в документообороте или строка уникального идентификатора элемента справочника.
//        - ДатаТекущейВерсии - Дата.
//        - Размер - Число, размер в байтах.
//        - ПометкаУдаления - Булево, помечен на удаление.
//        - Комментарий - Строка, описание файла.
//	Неопределено, если файл не найден.
//
Функция ПолучитьФайлПоИдентификатору(ИдентификаторФайла) Экспорт
	Возврат ПолучитьОбщийМодульСвязанныхФайловПоТипуХранения()
				.ПолучитьФайлПоИдентификатору(ИдентификаторФайла);
КонецФункции

// Возвращает структуру, описывающую файл, найденный по ссылке.
// Аналогична функции ПолучитьФайлПоИдентификатору.
//
Функция ПолучитьФайлПоСсылке(СсылкаНаФайл) Экспорт
	Возврат ПолучитьОбщийМодульСвязанныхФайловПоТипуХранения()
				.ПолучитьФайлПоСсылке(СсылкаНаФайл);
КонецФункции

// Возвращает файл, добавленный владельцу из временного хранилища и помещенный в ДО или в эту ИБ.
// Может приводить к автоматическому созданию связанного объекта.
//
// Параметры:
//   Владелец - ЛюбаяСсылка - объект-владелец связанных файлов
//   АдресВременногоХранилищаФайла - Строка - адрес временного хранилища, где размещен файл
//   Имя - Строка - имя помещаемого файла
//   Расширение - Строка - расширение помещаемого файла
//   Размер - Число - размер помещаемого файла
//   ВремяИзменения - Дата - дата и время файла на диске
//   ВремяИзмененияУниверсальное - Дата - дата и время UTC файла на диске 
//   ДокументID - идентификатор связанного объекта ДО (если не передан, будет определен автоматически)
//   ДокументТип - тип связанного объекта ДО (если не передан, будет определен автоматически)
//
// Возвращаемое значение:
//   СправочникСсылка.Файлы или Строка (идентификатор файла ДО)
//
Функция ДобавитьФайлИзВременногоХранилища(Владелец, АдресВременногоХранилищаФайла,
			Имя, Расширение, Размер, ВремяИзменения, ВремяИзмененияУниверсальное,
			ДокументID = "", ДокументТип = "") Экспорт
			
	Возврат ПолучитьОбщийМодульСвязанныхФайловПоТипуХранения()
				.ДобавитьФайлИзВременногоХранилища(
					Владелец, АдресВременногоХранилищаФайла, Имя, Расширение,
					Размер, ВремяИзменения, ВремяИзмененияУниверсальное,
					ДокументID, ДокументТип);
КонецФункции

// Возвращает файл, добавленный владельцу копированием шаблона и помещенный в ДО или в эту ИБ.
// Может приводить к автоматическому созданию связанного объекта.
//
// Параметры:
//   Владелец - ЛюбаяСсылка - объект-владелец связанных файлов или папка при хранении в этой ИБ
//   Шаблон - СправочникСсылка.Файлы - шаблон для копирования
//   ДокументID - идентификатор связанного объекта ДО (если не передан, будет определен автоматически)
//   ДокументТип - тип связанного объекта ДО (если не передан, будет определен автоматически)
//
// Возвращаемое значение:
//   СправочникСсылка.Файлы или Строка (идентификатор файла ДО)
//
Функция ДобавитьФайлИзШаблона(Владелец, Шаблон, ДокументID = "", ДокументТип = "") Экспорт
			
	Возврат ПолучитьОбщийМодульСвязанныхФайловПоТипуХранения()
				.ДобавитьФайлИзШаблона(Владелец, Шаблон, ДокументID, ДокументТип);			
КонецФункции

// Обновить файл, добавленный владельцу из временного хранилища и помещенный в ДО или в эту ИБ.
// Может приводить к автоматическому созданию связанного объекта.
//
// Параметры:
//   Владелец - ЛюбаяСсылка - объект-владелец связанных файлов
//   АдресВременногоХранилищаФайла - Строка - адрес временного хранилища, где размещен файл
//   Имя - Строка - имя помещаемого файла
//   Расширение - Строка - расширение помещаемого файла
//   Размер - Число - размер помещаемого файла
//   ВремяИзменения - Дата - дата и время файла на диске
//   ВремяИзмененияУниверсальное - Дата - дата и время UTC файла на диске 
//   ДокументID - идентификатор связанного объекта ДО (если не передан, будет определен автоматически)
//   ДокументТип - тип связанного объекта ДО (если не передан, будет определен автоматически)
//
// Возвращаемое значение:
//   Булево - Истина, если файл обновлен успешно.
//
Функция ОбновитьФайлИзВременногоХранилища(ИдентификаторФайла, Владелец,
				АдресВременногоХранилищаФайла, Имя, Расширение, Размер,
				ВремяИзменения, ВремяИзмененияУниверсальное) Экспорт
	Возврат ПолучитьОбщийМодульСвязанныхФайловПоТипуХранения()
				.ОбновитьФайлИзВременногоХранилища(ИдентификаторФайла,
					Владелец, АдресВременногоХранилищаФайла, Имя,
					Расширение, Размер,	ВремяИзменения,
					ВремяИзмененияУниверсальное);
КонецФункции

// Возвращает массив структур, описывающих версии файла независимо от места хранения
//
// Параметры:
//   Файл - СправочникСсылка.Файла, Строка - файл здесь или в ДО
//   ВключатьПомеченныеНаУдаление - Булево - Истина, если требуется получить и помеченные на удаление.
//
// Возвращаемое значение:
//   Массив - структуры, описывающие реквизиты версий файла
//
Функция ВерсииФайла(Файл, ВключатьПомеченныеНаУдаление = Ложь) Экспорт
	Возврат ПолучитьОбщийМодульСвязанныхФайловПоТипуХранения()
				.ВерсииФайла(Файл, ВключатьПомеченныеНаУдаление);
КонецФункции

// Помечает на удаление или снимает пометку с указанного файла.
//
// Параметры:
//   ИдентификаторФайла - СправочникСсылка.Файла, Строка - файл здесь или в ДО
//
Процедура ПометитьНаУдалениеСнятьПометку(ИдентификаторФайла) Экспорт
	ПолучитьОбщийМодульСвязанныхФайловПоТипуХранения()
		.ПометитьНаУдалениеСнятьПометку(ИдентификаторФайла);
КонецПроцедуры

// Изменить пометку на удаление для файлов принадлежащих Владелец
//
// Параметры:
//	ПометкаНаУдаление - Булево, Истина - установить, Ложь - снять пометку на удаление.
//	Владелец - ЛюбаяСсылка - объект-владелец связанных файлов.
//	ДокументID - идентификатор связанного объекта ДО (если не передан, будет определен автоматически).
//	ДокументТип - тип связанного объекта ДО (если не передан, будет определен автоматически).
//
Процедура ПометитьНаУдалениеПоВладельцу(ПометкаНаУдаление, Владелец,
				ДокументID = "", ДокументТип = "") Экспорт
	ПолучитьОбщийМодульСвязанныхФайловПоТипуХранения()
			.ПометитьНаУдалениеПоВладельцу(ПометкаНаУдаление,
						Владелец, ДокументID, ДокументТип);
КонецПроцедуры

// Получить ссылку на файл по его идентификатору. Для документооборота возвращает переданный идентификатор.
// Для внутреннего хранения определяет ссылку на справочник файлы.
// Необходима для унифицированной обработки идентификатора файла, с целью последующей передачи в другие функции.
//
// Параметры:
//   ИдентификаторФайла - СправочникСсылка.Файла, Строка - файл здесь или в ДО.
//
// Возвращает:
//	Строка, если используется документооборот, то возвращает переданный идентификатор файла.
//	СправочникСсылка.Файлы - если используется внутреннее хранение файлов.
Функция ПолучитьСсылкуНаФайл(ИдентификаторФайла) Экспорт
	Возврат ПолучитьОбщийМодульСвязанныхФайловПоТипуХранения()
				.ПолучитьСсылкуНаФайл(ИдентификаторФайла);
КонецФункции

// Получить идентификатор файла в 1С:Документооборот, либо строку уникального идентификатора элемента справочника Файлы
// в зависимости от способа хранения файлов.
//
// Параметры:
//	СсылкаНаФайл - СпрвочникСсылка.Файлы, Строка - обобщенная ссылка на файл, помещеенный в базу.
//
// Возвращает:
//	Строка - либо идентификатор в 1С:Документооборот, либо строку уникального идентификатора элемента справочника Файлы,
//		в зависимости от способа хранения файлов.
//
Функция ПолучитьИдентификаторФайлаПоСсылке(СсылкаНаФайл) Экспорт
	Возврат ПолучитьОбщийМодульСвязанныхФайловПоТипуХранения()
				.ПолучитьИдентификаторФайлаПоСсылке(СсылкаНаФайл);
КонецФункции

// Получить данные для открытия функцией СвязанныеФайлыКлиент.ОткрытьФайлДляПросмотра(ДанныеФайла)
Функция ПолучитьДанныеФайлаДляОткрытия(ИдентификаторФайла, УникальныйИдентификаторФормы) Экспорт
	Возврат ПолучитьОбщийМодульСвязанныхФайловПоТипуХранения()
				.ПолучитьДанныеФайлаДляОткрытия(
						ИдентификаторФайла, УникальныйИдентификаторФормы);
КонецФункции

// В массиве версий ищет 
Функция НайтиПоследнююВерсиюФайла(МассивВерсийФайла) Экспорт
	МаксДата = '00010101';
	ИдВерсии = Неопределено;
	
	Если ЗначениеЗаполнено(МассивВерсийФайла) Тогда
		Для Каждого Версия Из МассивВерсийФайла Цикл
			Если Версия.ДатаСоздания > МаксДата Тогда
				МаксДата = Версия.ДатаСоздания;
				ИдВерсии = Версия.Идентификатор;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ИдВерсии;
КонецФункции


#КонецОбласти


#Область ВспомогательныеПроцедурыФункции

// Позволяет определить, используется-ли 1С Документооборот для хранения файлов.
//
Функция ХранениеВДокументообороте() Экспорт
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюС1СДокументооборот");
КонецФункции

// Возвращает ссылку на общий модуль для вызова функций работы с файлами.
// Читает функциональную опцию "ИспользоватьИнтеграциюС1СДокументооборот".
// Возвращаемое значение:
//	ОбщийМодуль - ссылка на общий модуль для вызова функций.
//	Если не найден, то вызывает исключение.
//
Функция ПолучитьОбщийМодульСвязанныхФайловПоТипуХранения() Экспорт
	Если ХранениеВДокументообороте() Тогда
		ИмяОбщегоМодуля = "СвязанныеФайлыВызовСервераДО";
	Иначе
		ИмяОбщегоМодуля = "СвязанныеФайлыВызовСервераБСП";
	КонецЕсли;
	
	Возврат ОбщегоНазначения.ОбщийМодуль(ИмяОбщегоМодуля);
КонецФункции

// Возвращает структуру с описанием файла.
//
// Возвращает:
// 	Структура - описываюет реквизиты найденного файла.
//		Содержит следующие поля:
//        - ИндексКартинки - Число, индекс в картинке КоллекцияПиктограммФайлов.
//        - Наименование - Строка, имя без расширения.
//        - Расширение - Строка, расширение без точки.
//        - СвязанныйФайл - Строка, СправочникСсылка.Фалы - идентификатор в документообороте или ссылка на справочник.
//        - Идентификатор - Строка, идентификатор в документообороте или строка уникального идентификатора элемента справочника.
//        - ДатаТекущейВерсии - Дата.
//        - Размер - Число, размер в байтах.
//        - ПометкаУдаления - Булево, помечен на удаление.
//        - Комментарий - Строка, описание файла.
Функция ПолучитьШаблонОписанияФайла() Экспорт
	ОписаниеФайла = Новый Структура;
	ОписаниеФайла.Вставить("ИндексКартинки", -1);
	ОписаниеФайла.Вставить("Наименование", "");
	ОписаниеФайла.Вставить("Расширение", "");
	ОписаниеФайла.Вставить("СвязанныйФайл", "");
	ОписаниеФайла.Вставить("Идентификатор", "");
	ОписаниеФайла.Вставить("ДатаТекущейВерсии", '00010101');
	ОписаниеФайла.Вставить("Размер", 0);
	ОписаниеФайла.Вставить("ПометкаУдаления", Ложь);
	ОписаниеФайла.Вставить("Комментарий", "");
	
	Возврат ОписаниеФайла;
КонецФункции


#КонецОбласти
