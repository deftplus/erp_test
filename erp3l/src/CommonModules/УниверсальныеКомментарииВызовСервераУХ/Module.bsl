/////////////////////////////////////////////////////////////////////
// Вспомогательные функции различных шаблонов адреса
/////////////////////////////////////////////////////////////////////

// Создаем таблицу с описанием расхождения ВГО
Функция СоздатьТаблицуРасхожденияВГО(ПериодСверки, Сценарий, Этап, Отправитель, Получатель, РазделВГО, Валюта, ДопАналитика) Экспорт
	Перем ТаблицаОписанияПредмета;
	
	ТаблицаОписанияПредмета = УниверсальныеКомментарииУХ.СоздатьТаблицуПредметаКомментирования();
	
	НоваяСтрока = ТаблицаОписанияПредмета.Добавить();
	НоваяСтрока.НомерСтроки = 1;
	НоваяСтрока.ТипРеквизита = ПланыВидовХарактеристик.ТипыРеквизитовКомментариев.ПериодСценария;
	НоваяСтрока.Значение = ПериодСверки;
	
	НоваяСтрока = ТаблицаОписанияПредмета.Добавить();
	НоваяСтрока.НомерСтроки = 2;
	НоваяСтрока.ТипРеквизита = ПланыВидовХарактеристик.ТипыРеквизитовКомментариев.Сценарий;
	НоваяСтрока.Значение = Сценарий;
	
	НоваяСтрока = ТаблицаОписанияПредмета.Добавить();
	НоваяСтрока.НомерСтроки = 3;
	НоваяСтрока.ТипРеквизита = ПланыВидовХарактеристик.ТипыРеквизитовКомментариев.Этап;
	НоваяСтрока.Значение = Этап;
	
	НоваяСтрока = ТаблицаОписанияПредмета.Добавить();
	НоваяСтрока.НомерСтроки = 4;
	НоваяСтрока.ТипРеквизита = ПланыВидовХарактеристик.ТипыРеквизитовКомментариев.Отправитель;
	НоваяСтрока.Значение = Отправитель;
	
	НоваяСтрока = ТаблицаОписанияПредмета.Добавить();
	НоваяСтрока.НомерСтроки = 5;
	НоваяСтрока.ТипРеквизита = ПланыВидовХарактеристик.ТипыРеквизитовКомментариев.Получатель;
	НоваяСтрока.Значение = Получатель;
	
	НоваяСтрока = ТаблицаОписанияПредмета.Добавить();
	НоваяСтрока.НомерСтроки = 6;
	НоваяСтрока.ТипРеквизита = ПланыВидовХарактеристик.ТипыРеквизитовКомментариев.РазделВГО;
	НоваяСтрока.Значение = РазделВГО;
	
	НоваяСтрока = ТаблицаОписанияПредмета.Добавить();
	НоваяСтрока.НомерСтроки = 7;
	НоваяСтрока.ТипРеквизита = ПланыВидовХарактеристик.ТипыРеквизитовКомментариев.Валюты;
	НоваяСтрока.Значение = Валюта;
	
	НоваяСтрока = ТаблицаОписанияПредмета.Добавить();
	НоваяСтрока.НомерСтроки = 8;
	НоваяСтрока.ТипРеквизита = ПланыВидовХарактеристик.ТипыРеквизитовКомментариев.ДопАналитикаВГО;
	НоваяСтрока.Значение = ДопАналитика;
	
	Возврат ТаблицаОписанияПредмета;
КонецФункции

// Создаем таблицу с описанием расхождения ВГО и кладем ее в хранилище
Функция СоздатьТаблицуРасхожденияВГОВХранилище(ПериодСверки, Сценарий, Этап, Отправитель, Получатель, РазделВГО, Валюта, ДопАналитика) Экспорт
	ТаблицаОписанияПредмета = СоздатьТаблицуРасхожденияВГО(ПериодСверки, Сценарий, Этап, Отправитель, Получатель, РазделВГО, Валюта, ДопАналитика);
	ХранилищеТаблицы = Новый ХранилищеЗначения(ТаблицаОписанияПредмета);
	Возврат ХранилищеТаблицы;
КонецФункции

/////////////////////////////////////////////////////////////////////
// Процедуры и функции работы с универсальными комментариями
//////////////////////////////////////////////////////////////

// Возвращает ссылку на справочник ПредметыКомментариев для переаднных реквизитов.
// Поиск осуществляется по регистру сведений ЗначенияРеквизитовПредметаКомментирования.
// Если адрес не найден, то он созадется в справочнике и регистре сведений.
// Параметры:
//		РазделКомментариев - одно из предопределенных значений перечисления РазделыКомментариев.
//			Комментарии обрабатываются только в пределах одного раздела.
//		ОписаниеПредметаКомменитрования - хранилище значений, хранит таблицу значений следующего формата:
//			ТаблицаПредметаКомменитрования - таблица значений описывающая предмет комментирования. Колонки:
//				НомерСтроки - номер реквизита адреса по порядку использования,
//				ТипРеквизита - значение ПланВидовХарактеристикСсылка 
//					ТипыРеквизитовКомментариев, описывает тип реквизита,
//				Значение - значение реквизита.
//		ТекстОшибки - если возникла ошибка, то в переменную возвращается ее описание.
// Возвращает:
// 		Неопределено - возникли ошибки, в ТекстОшибки записывает ее описание.
//  	Ссылка на справочник ПредметыКомментирования.
//
Функция ПолучитьПредметКомментирования(РазделКомментариев, ОписаниеПредметаКомменитрования, ТекстОшибки) Экспорт
	Перем ТаблицаАдреса, Ссылка, ТекстЗапроса, ЧислоРеквизитов;
	
	ТаблицаПредметаКомментирования = ОписаниеПредметаКомменитрования.Получить();
	Если ТипЗнч(ТаблицаПредметаКомментирования) <> Тип("ТаблицаЗначений") Тогда
		ТекстОшибки = Нстр("ru = 'Передана не таблица адреса!'");
	КонецЕсли;
		
	Возврат УниверсальныеКомментарииУХ.ПолучитьПредметКомментированияДляТаблицы(РазделКомментариев, ТаблицаПредметаКомментирования, ТекстОшибки);
КонецФункции

// Записываем комментарий и пакет файлов в регистр сведений УниверсальныеКомментарии
// Создаем сопуствующие записи в справочник ПапкиФайлов и Файлы/ВерсииФайлов.
// Параметры:
//		ПредметКомментирования - ссылка на справочник ПредметыКомментирования.
//		Автор - ссылка на пользователя оставившего комментарий.
//		Комментарий - строка с комментарием.
//		ДатаКомментария - дата и время на которую будет сохранен комментария.
//		мХранимыхФайлов - массив объектов ОписаниеПередаваемогоФайла.
//		ТекстОшибки - если возникла ошибка, то в переменную возвращается ее описание.
// Возвращает:
// 		РегистрСведенийКлючЗаписи - операция прошла успешно, возвращаем ссылку на запись регистра комментария.
//		Неопределено - возникли ошибки, в ТекстОшибки записывает ее описание.
//
Функция ЗаписатьКомментарий(ПредметКомментирования, Комментарий, ДатаКомментария, Автор=Неопределено, мХранимыхФайлов=Неопределено, ТекстОшибки) Экспорт
	Перем Автор_, ПакетФайлов, Период, СписокФайлов;
	
	Если ПредметКомментирования = Неопределено Тогда
		ТекстОшибки = НСтр("ru = 'Не указан адрес комментария.'");
		Возврат Неопределено;
	КонецЕсли;
	
	Автор_ = ?(Автор = Неопределено, ОбщегоНазначенияУХ.ПолучитьЗначениеПеременной("глТекущийПользователь"), Автор);
	Если Автор_ = Неопределено Тогда
		ТекстОшибки = НСтр("ru = 'Нет возможности определить автора комментария.'");
		Возврат Неопределено;
	КонецЕсли;
	Период = ДатаКомментария;
	
	ПакетФайлов = Справочники.ПапкиФайлов.ПустаяСсылка();
	КлючЗаписи = Неопределено;
	
	НачатьТранзакцию();
	Попытка
		// обрабатываем список файлов
		Если мХранимыхФайлов <> Неопределено Тогда
			// сохраним файлы в пакет файлов
			
			Если мХранимыхФайлов.Количество() > 0 Тогда
				ТекстНаименование = НСтр("ru = 'Пакет файлов (Комментарии) %Период%'");
				ТекстНаименование = СтрЗаменить(ТекстНаименование, "%Период%", Формат(Период, ""));
				ПакетФайлов = Справочники.ПапкиФайлов.СоздатьЭлемент();
				ПакетФайлов.Наименование = ТекстНаименование;
				ПакетФайлов.Записать();
				ПакетФайлов = ПакетФайлов.Ссылка;	
			КонецЕсли;
			
			Для каждого Стр Из мХранимыхФайлов Цикл
				
				СоставИмениФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(Стр.Имя);
				
				СведенияОФайле = РаботаСФайламиКлиентСервер.СведенияОФайле("ФайлСВерсией");
				СведенияОФайле.АдресВременногоХранилищаФайла = Стр.Хранение;
				СведенияОФайле.АдресВременногоХранилищаТекста = "";
			    СведенияОФайле.Автор = Автор_;
				СведенияОФайле.Комментарий = НСтр("ru = '[Создан подсистемой комментариев]'");
				
				СведенияОФайле.ИмяБезРасширения = СоставИмениФайла.ИмяБезРасширения;
				СведенияОФайле.РасширениеБезТочки = ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(СоставИмениФайла.Расширение);
		
				РаботаСФайламиСлужебныйВызовСервера.СоздатьФайлСВерсией(ПакетФайлов, СведенияОФайле);
			КонецЦикла;
		КонецЕсли;
	
		// Записываем комментарий и пакет файлов в РС.УниверсальныеКомментарии
		КомментарийЗапись = РегистрыСведений.УниверсальныеКомментарии.СоздатьМенеджерЗаписи();
		КомментарийЗапись.Период = Период;
		КомментарийЗапись.ПредметКомментирования = ПредметКомментирования;
		КомментарийЗапись.Автор = Автор_;
		КомментарийЗапись.Комментарий = Комментарий;
		КомментарийЗапись.ПакетФайлов = ПакетФайлов;
		КомментарийЗапись.Записать();
		
		Отбор = Новый Структура;
		Отбор.Вставить("Период", Период);
		Отбор.Вставить("ПредметКомментирования", ПредметКомментирования);
		Отбор.Вставить("Автор", Автор_);
		КлючЗаписи = РегистрыСведений.УниверсальныеКомментарии.СоздатьКлючЗаписи(Отбор); 
		
	ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстОшибки = ОписаниеОшибки();
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат КлючЗаписи;
КонецФункции

// Возвращаем структуру с описанием предмета комментирования.
// 
//	Параметры
//			ПредметКомментирования - ссылка на справочник предмет комментирования, если незаполнено, создается новый предмет комментирования.
//			Реквизиты - структура с полями описывающими предмет сверки:
//				ПериодСценария, Сценарий, Этап, Отправитель, Получатель, РазделВГО, ВалютаВзаиморасчетов, ДопАналитикаРасхождения, Пользователь, Сумма - описание значения сверки.
//			ТекстОшибки - в переменную помещается описание ошибки.
//
//	Возвращает
//		ОписаниеПредметаКомменитрования - структура следующего формата:
//			РазделКомментариев - одно из предопределенных значений перечисления РазделыКомментариев.
//			ПредметКомментирования - ссылка на справочник предмет комментирования.
//			ПочтовыеАдреса - массив заполненный строками с адресами электронной почты, для рассылки уведомлений о комментариях. Необязательный.
//			ПрефиксКомментариев - произвольная строка для добавления перед текстом комментария.
//			Реквизиты - список с описанием реквизитов предмета комментирования. Используется при отправке оповещений по почте.
//			Отбор - структура, отбор по регистру сведений УниверсальныеКомментарииУХ. Необязательный.
//
Функция ПодготовитьОписаниеПредметаКомментированияВГО(ПредметКомментирования, Реквизиты, ТекстОшибки) Экспорт
	
	Перем ОписаниеПредметаКомментирования;
	
	ОписаниеПредметаКомментирования = Новый Структура("РазделКомментариев,ПредметКомментирования,ПрефиксКомментариев");
	ОписаниеПредметаКомментирования.ПрефиксКомментариев = СтрШаблон(Нстр("ru = 'Сумма: %1. '", ОбщегоНазначения.КодОсновногоЯзыка()), Реквизиты.Сумма);
	ОписаниеПредметаКомментирования.РазделКомментариев = Перечисления.РазделыКомментариев.РасхожденияВГО;
	Если НЕ ЗначениеЗаполнено(ПредметКомментирования) Тогда
		ТаблицаОписанияРасхожденияВГО = СоздатьТаблицуРасхожденияВГО(Реквизиты.ПериодСценария, Реквизиты.Сценарий, 
			Реквизиты.Этап, Реквизиты.Отправитель, Реквизиты.Получатель, Реквизиты.РазделВГО, Реквизиты.ВалютаВзаиморасчетов, Реквизиты.ДопАналитикаРасхождения);
		ОписаниеПредметаКомментирования.ПредметКомментирования = 
			УниверсальныеКомментарииУХ.ПолучитьПредметКомментированияДляТаблицы(Перечисления.РазделыКомментариев.РасхожденияВГО, ТаблицаОписанияРасхожденияВГО, ТекстОшибки);
	Иначе
		ОписаниеПредметаКомментирования.ПредметКомментирования = ПредметКомментирования;
	КонецЕсли; 
	
	// запишем в регистр сведений ИнформациОРасхожденияхВГО предмет комментирования
	ОтборРегистра = Новый Структура("ПериодСценария, Сценарий, Этап, Отправитель, Получатель, РазделВГО, ВалютаВзаиморасчетов, ДопАналитикаРасхождения");
	ЗаполнитьЗначенияСвойств(ОтборРегистра, Реквизиты);
	ДанныеПредметаКомментирования = Новый Структура("ПредметКомментирования", ОписаниеПредметаКомментирования.ПредметКомментирования);
	СверкаВГОУХ.ИзменитьСостояниеСверки(ОтборРегистра, ДанныеПредметаКомментирования, ТекущаяУниверсальнаяДата(), ТекстОшибки);
	
	Если Константы.ОповеститьОКомментарииРасхожденияВГО.Получить() Тогда
		СтруктураРеквизитов = Новый Соответствие;
		СтруктураРеквизитов.Вставить(Нстр("ru = 'Период сверки'", ОбщегоНазначения.КодОсновногоЯзыка()),Реквизиты.ПериодСценария);
		СтруктураРеквизитов.Вставить(Нстр("ru = 'Сценарий'", ОбщегоНазначения.КодОсновногоЯзыка()),Реквизиты.Сценарий);
		СтруктураРеквизитов.Вставить(Нстр("ru = 'Этап'", ОбщегоНазначения.КодОсновногоЯзыка()),Реквизиты.Этап);
		СтруктураРеквизитов.Вставить(Нстр("ru = 'Отправитель'", ОбщегоНазначения.КодОсновногоЯзыка()),Реквизиты.Отправитель);
		СтруктураРеквизитов.Вставить(Нстр("ru = 'Получатель'", ОбщегоНазначения.КодОсновногоЯзыка()),Реквизиты.Получатель);
		СтруктураРеквизитов.Вставить(Нстр("ru = 'Раздел сверки ВГО'", ОбщегоНазначения.КодОсновногоЯзыка()),Реквизиты.РазделВГО);
		СтруктураРеквизитов.Вставить(Нстр("ru = 'Валюта взаиморасчетов'", ОбщегоНазначения.КодОсновногоЯзыка()),Реквизиты.ВалютаВзаиморасчетов);
		СтруктураРеквизитов.Вставить(Нстр("ru = 'Доп. аналитика'", ОбщегоНазначения.КодОсновногоЯзыка()),Реквизиты.ДопАналитикаРасхождения);
		СтруктураРеквизитов.Вставить(Нстр("ru = 'Сумма расхождения в валюте взаиморасчетов'", ОбщегоНазначения.КодОсновногоЯзыка()),Реквизиты.Сумма);
		ОписаниеПредметаКомментирования.Вставить("Реквизиты", СтруктураРеквизитов);
		
		мОрганизаций = Новый Массив;
		мОрганизаций.Добавить(Реквизиты.Отправитель);
		мОрганизаций.Добавить(Реквизиты.Получатель);
		ТаблицаАдресов = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(мОрганизаций,, Справочники.ВидыКонтактнойИнформации.EmailОрганизации); //СверкаВГОВызовСервераУХ.ПочтаОтветственныхЛицОрганизации(Реквизиты.Сценарий, Реквизиты.ПериодСценария, Реквизиты.Этап, мОрганизаций);
		
		//ИскСтр_ = ТаблицаАдресов.Найти(Реквизиты.Пользователь, "Пользователь");
		//Если ИскСтр_ <> неопределено Тогда
		//	ТаблицаАдресов.Удалить(ИскСтр_);			
		//КонецЕсли; 
		ТаблицаАдресов.Свернуть("Представление");
		мАдресов = ТаблицаАдресов.ВыгрузитьКолонку("Представление");
		Если мАдресов.Количество() > 0 Тогда
			ОписаниеПредметаКомментирования.Вставить("ПочтовыеАдреса", мАдресов);
		КонецЕсли;
		ОписаниеПредметаКомментирования.Вставить("ИсточникПисьма", Реквизиты.Этап);
	КонецЕсли;
	возврат ОписаниеПредметаКомментирования;
КонецФункции

// Возвращаем структуру с описанием предмета комментирования.
// 
//	Параметры
//		Ссылка - ссылка на комментируемый документа.
//		ТекстОшибки - в переменную помещается описание ошибки.
//
//	Возвращает
//		ОписаниеПредметаКомменитрования - структура следующего формата:
//			РазделКомментариев - одно из предопределенных значений перечисления РазделыКомментариев.
//			ПредметКомментирования - ссылка на справочник предмет комментирования.
//			ПочтовыеАдреса - массив заполненный строками с адресами электронной почты, для рассылки уведомлений о комментариях. Необязательный.
//			ПрефиксКомментариев - произвольная строка для добавления перед текстом комментария.
//			Реквизиты - список с описанием реквизитов предмета комментирования. Используется при отправке оповещений по почте.
//			Отбор - структура, отбор по регистру сведений УниверсальныеКомментарииУХ. Необязательный.
//
Функция ПодготовитьОписаниеДокумента(Ссылка, ТекстОшибки) Экспорт
	Перем ОписаниеПредметаКомментирования, ТаблицаОписанияДокумента;
	ОписаниеПредметаКомментирования = Новый Структура("РазделКомментариев,ПредметКомментирования,ПрефиксКомментариев");
	ОписаниеПредметаКомментирования.ПрефиксКомментариев = "";
	ОписаниеПредметаКомментирования.РазделКомментариев = Перечисления.РазделыКомментариев.Документы;
	ТаблицаОписанияДокумента = УниверсальныеКомментарииУХ.СоздатьТаблицуПредметаКомментирования();
	НоваяСтрока = ТаблицаОписанияДокумента.Добавить();
	НоваяСтрока.НомерСтроки = 1;
	НоваяСтрока.ТипРеквизита = ПланыВидовХарактеристик.ТипыРеквизитовКомментариев.СсылкаДокумент;
	НоваяСтрока.Значение = Ссылка;
	
	ОписаниеПредметаКомментирования.ПредметКомментирования = 
			УниверсальныеКомментарииУХ.ПолучитьПредметКомментированияДляТаблицы(Перечисления.РазделыКомментариев.Документы, ТаблицаОписанияДокумента, ТекстОшибки);
	
	возврат ОписаниеПредметаКомментирования;
КонецФункции

// Возвращаем структуру с описанием предмета комментирования.
// 
//	Параметры
//		Ссылка - ссылка на комментируемый справочник.
//		ТекстОшибки - в переменную помещается описание ошибки.
//
//	Возвращает
//		ОписаниеПредметаКомменитрования - структура следующего формата:
//			РазделКомментариев - одно из предопределенных значений перечисления РазделыКомментариев.
//			ПредметКомментирования - ссылка на справочник предмет комментирования.
//			ПочтовыеАдреса - массив заполненный строками с адресами электронной почты, для рассылки уведомлений о комментариях. Необязательный.
//			ПрефиксКомментариев - произвольная строка для добавления перед текстом комментария.
//			Реквизиты - список с описанием реквизитов предмета комментирования. Используется при отправке оповещений по почте.
//			Отбор - структура, отбор по регистру сведений УниверсальныеКомментарииУХ. Необязательный.
//
Функция ПодготовитьОписаниеСправочника(Ссылка, ТекстОшибки) Экспорт
	Перем ОписаниеПредметаКомментирования, ТаблицаОписания;
	ОписаниеПредметаКомментирования = Новый Структура("РазделКомментариев,ПредметКомментирования,ПрефиксКомментариев");
	ОписаниеПредметаКомментирования.ПрефиксКомментариев = "";
	ОписаниеПредметаКомментирования.РазделКомментариев = Перечисления.РазделыКомментариев.Справочники;
	ТаблицаОписания = УниверсальныеКомментарииУХ.СоздатьТаблицуПредметаКомментирования();
	НоваяСтрока = ТаблицаОписания.Добавить();
	НоваяСтрока.НомерСтроки = 1;
	НоваяСтрока.ТипРеквизита = ПланыВидовХарактеристик.ТипыРеквизитовКомментариев.СсылкаСправочник;
	НоваяСтрока.Значение = Ссылка;
	
	ОписаниеПредметаКомментирования.ПредметКомментирования = 
			УниверсальныеКомментарииУХ.ПолучитьПредметКомментированияДляТаблицы(ОписаниеПредметаКомментирования.РазделКомментариев, ТаблицаОписания, ТекстОшибки);
	
	возврат ОписаниеПредметаКомментирования;
КонецФункции