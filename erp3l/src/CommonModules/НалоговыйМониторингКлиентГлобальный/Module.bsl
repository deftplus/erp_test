
// См. ОбщегоНазначенияКлиентПереопределяемый.ПриНачалеРаботыСистемы
Процедура ЗаписатьДействияПользователя_Подключаемый() Экспорт
	
	//ПодключитьОбработчикОжидания("ЗаписатьДействияПользователя_Подключаемый", 3, Ложь);
	РабочиеОкна = ПолучитьОкна();
	ДатаДействий = ТекущаяДата();
	
	СписокОкон = Новый СписокЗначений;
	
	Исключения = Новый Массив;
	Исключения.Добавить("Лог действий пользователя");
	Исключения.Добавить("Мои задачи и оповещения");
	
	ПроверкаУникальности = Новый Соответствие;
	
	Для Каждого ТекущееОкно Из РабочиеОкна Цикл
		
		Если ТекущееОкно.Основное Или ТекущееОкно.НачальнаяСтраница Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущийЗаголовокОкна = Лев(ТекущееОкно.Заголовок, 300);
		Если СтрНачинаетсяС(ТекущийЗаголовокОкна, "Управление холдингом") Или Исключения.Найти(ТекущийЗаголовокОкна) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекущееОкно.Содержимое.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущаяФорма = ТекущееОкно.Содержимое[0];
		
		ТекущиеДанные = Неопределено;
		Если СтрНачинаетсяС(ТекущаяФорма.ИмяФормы, "Отчет.") Тогда
			ТекущиеДанные = Лев(ПолучитьОписаниеНастроекСКД(ТекущаяФорма), 300);
		КонецЕсли;
				
		Если ТекущиеДанные = Неопределено Тогда
			ТекущиеДанные = ТекущаяФорма.КлючУникальности;
		КонецЕсли;
		
		ОкнаСТекущимЗаголовком = ПроверкаУникальности.Получить(ТекущийЗаголовокОкна);
		Если ОкнаСТекущимЗаголовком = Неопределено Тогда
			ОкнаСТекущимЗаголовком = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТекущиеДанные);
		ИначеЕсли ОкнаСТекущимЗаголовком.Найти(ТекущиеДанные) = Неопределено Тогда
			ОкнаСТекущимЗаголовком.Добавить(ТекущиеДанные);
		Иначе 
			Продолжить;// окно с таким заголовком и ключом уже попало в СписокОкон(например, открыто два отчета с одинаковыми отборами)
		КонецЕсли;
		
		СписокОкон.Добавить(ТекущиеДанные, ТекущийЗаголовокОкна);
		ПроверкаУникальности.Вставить(ТекущийЗаголовокОкна, ОкнаСТекущимЗаголовком);
		
	КонецЦикла;
	
	НалоговыйМониторингВызовСервера.ЗаписатьДействияПользователя(СписокОкон, ДатаДействий);
	
КонецПроцедуры

Функция ПолучитьОписаниеНастроекСКД(ТекущаяФорма)

	Результат = Новый СписокЗначений;
	
	Попытка
		
		Если ТипЗнч(ТекущаяФорма.КлючУникальности) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
			Возврат Неопределено;	
		КонецЕсли;
		
		КомпоновщикСКД = ТекущаяФорма.Отчет.КомпоновщикНастроек;				
		//считаем что все параметры устанавливаются в пользовательских настройках
		Для каждого ТекущийЭлемент Из КомпоновщикСКД.Настройки.Отбор.Элементы Цикл
			ДобавитьЭлементНастройкиСКД(Результат, ТекущийЭлемент);
		КонецЦикла;		
		
		Для каждого ЭлементСКД Из КомпоновщикСКД.ПользовательскиеНастройки.Элементы Цикл			
			Если ТипЗнч(ЭлементСКД) = Тип("ОтборКомпоновкиДанных") Тогда
				Для каждого ТекущийЭлемент Из ЭлементСКД.Элементы Цикл
					ДобавитьЭлементНастройкиСКД(Результат, ТекущийЭлемент);
				КонецЦикла;
			КонецЕсли;
			ДобавитьЭлементНастройкиСКД(Результат, ЭлементСКД);
		КонецЦикла;
		
		Результат.СортироватьПоЗначению();		
		Результат = СтрСоединить(Результат.ВыгрузитьЗначения(), ",");
		
	Исключение
		
		//считаем что отчет не на СКД, такие отчеты логируем только по заголовку
		ТекущаяОшибка = ОписаниеОшибки();
		Возврат Неопределено;
		
	КонецПопытки;
		
	Возврат Результат;
	
КонецФункции

Процедура ДобавитьЭлементНастройкиСКД(Результат, ЭлементСКД)
	
	Если (ТипЗнч(ЭлементСКД) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных")) И ЭлементСКД.Использование И ЗначениеЗаполнено(ЭлементСКД.Параметр) Тогда
		Результат.Добавить(СтрШаблон("%1=%2", ЭлементСКД.Параметр, ЭлементСКД.Значение));
	ИначеЕсли (ТипЗнч(ЭлементСКД) = Тип("ЭлементОтбораКомпоновкиДанных")) И ЭлементСКД.Использование И ЗначениеЗаполнено(ЭлементСКД.ЛевоеЗначение) Тогда
		
		Если ЭлементСКД.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда							
			Результат.Добавить(СтрШаблон("%1=%2", ЭлементСКД.ЛевоеЗначение, ЭлементСКД.ПравоеЗначение));
		Иначе 
			
			ПравоеЗначение = ЭлементСКД.ПравоеЗначение;
			Если ТипЗнч(ПравоеЗначение) = Тип("СписокЗначений") Тогда
				ПравоеЗначение = СтрШаблон("(%1)", СтрСоединить(ПравоеЗначение.ВыгрузитьЗначения(), ","));
			КонецЕсли;							
			Результат.Добавить(СтрШаблон("%1 %2 %3", ЭлементСКД.ЛевоеЗначение, ЭлементСКД.ВидСравнения, ПравоеЗначение));
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры




