
#Область ПрограммныйИнтерфейс

// Возвращает структуру с параметрами расчета курса в платежных позициях
Функция СтруктураПараметровРасчетаКурса() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("СпособОпределенияКурсаПлатежа");
	Результат.Вставить("КурсПлатежа");
	Результат.Вставить("КратностьПлатежа");
	Результат.Вставить("ДатаПлатежа");
	Результат.Вставить("ДатаФиксацииКурсаПлатежа");
	Результат.Вставить("СдвигДатыФиксацииКурсаПлатежа");
	Результат.Вставить("ВалютаДокумента");
	Результат.Вставить("ВалютаОплаты");
	Результат.Вставить("КурсПлатежаНеБолее");
	Результат.Вставить("КурсПлатежаНеМенее");
	Результат.Вставить("КурсПлатежаНеМенееВВалютеОплаты");
	Результат.Вставить("КурсПлатежаНеБолееВВалютеОплаты");
	
	Возврат Результат;
	
КонецФункции

// Функция возвращает представление для платежной позиции
// Замена функции: Обработки.РаботаСЗаявкамиНаОперации.ПредставлениеПлатежнойПозиции(Форма.ПлатежнаяПозиция)
// Возвращает: форматированную строку с описанием платежной позиции
Функция ПредставлениеПлатежнойПозиции(ПлатежнаяПозиция) Экспорт
	
	ШаблонТекста = НСтр("ru = '%1: <b>%2 %3</b> (%4)'");
	ФорматнаяСтрокаДатыИсполнения = НСтр("ru = 'ДФ=dd.MM.yy; ДП=""<Дата не указана>""'");
	
	ОписанияПозиций = Новый Массив;
	Если ПлатежнаяПозиция.Количество() > 0 Тогда
		СтруктураПоиска = Новый Структура("ПриходРасход", ПлатежнаяПозиция[0].ПриходРасход);
		Для Каждого ТекСтрока Из ПлатежнаяПозиция.НайтиСтроки(СтруктураПоиска) Цикл
			Текст = СтрШаблон(ШаблонТекста, ТекСтрока.СостояниеИсполнения, ТекСтрока.Сумма, ТекСтрока.ВалютаОплаты, 
				Формат(ТекСтрока.ДатаИсполнения, ФорматнаяСтрокаДатыИсполнения));
			ОписанияПозиций.Добавить(Текст);
		КонецЦикла;
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ФорматированнаяСтрока(СтрСоединить(ОписанияПозиций, ";    "));
	
КонецФункции

// Процедура обновляет представление платежной позиции на форме документа
Процедура ОбновитьПредставлениеПлатежнойПозиции(Форма, ПослеЗаписиДокумента = Ложь) Экспорт
	
	Если Форма.Элементы.Найти("СтрокаПлатежнаяПозиция") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	
	Форма.СтрокаПлатежнаяПозиция = ПредставлениеПлатежнойПозиции(Форма.ПлатежнаяПозиция);
	
	//
	ГиперСсылка = Форма.Элементы.СтрокаПлатежнаяПозиция;
	ИзмененаНеЗаписана = НЕ ПослеЗаписиДокумента И ЗначениеЗаполнено(Объект.Ссылка) И Форма.Модифицированность = Истина;
	Если ИзмененаНеЗаписана = Истина Тогда
		ГиперСсылка.Подсказка = НСтр("ru = 'Позиция изменена и будет записана вместе с документом.'");
		ГиперСсылка.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
	Иначе
		ГиперСсылка.Подсказка = "";
		//ГиперСсылка.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
		ГиперСсылка.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
	КонецЕсли;
	
КонецПроцедуры 
#КонецОбласти
