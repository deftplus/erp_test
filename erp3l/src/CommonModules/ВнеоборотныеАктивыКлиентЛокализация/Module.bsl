////////////////////////////////////////////////////////////////////////////////
// Процедуры подсистемы "Внеоборотные активы", предназначенные для локализации.
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Описание
// 
// Параметры:
// 	Команда - КомандаФормы - 
// 	Форма - ФормаКлиентскогоПриложения - 
Процедура ПриВыполненииКоманды(Команда, Форма) Экспорт

	ДополнительныеПараметры = Неопределено;
	ТребуетсяВызовСервера = Ложь;
	ПродолжитьВыполнениеКоманды = Истина;
	
	//++ Локализация
	
	Элементы = Форма.Элементы;

	Если Команда.Имя = Элементы.ФормаПроводкиРеглУчета.ИмяКоманды Тогда
		ВыполнитьКомандуОткрытьПроводкиРегламентированногоУчета(Форма, ДополнительныеПараметры);
	КонецЕсли; 
	
	//-- Локализация
	
	Если ПродолжитьВыполнениеКоманды Тогда
		
		ОбщегоНазначенияУТКлиент.ПродолжитьВыполнениеКоманды(
			Форма, 
			Команда.Имя, 
			ТребуетсяВызовСервера,
			ДополнительныеПараметры);
			
	КонецЕсли; 
	
КонецПроцедуры

Процедура ВыполнитьДействиеПослеРасчета(Форма, ДействиеПослеРасчета) Экспорт

	//++ Локализация
	
	Если ДействиеПослеРасчета = "ОткрытьПроводкиРегламентированногоУчета" Тогда
		ОткрытьПроводкиРегламентированногоУчета(Форма.Объект.Ссылка);
	КонецЕсли; 
	
	//-- Локализация

КонецПроцедуры

Функция ПриИзмененииПорядкаУчетаУУ_ОС(Объект, СлужебныеПараметрыФормы) Экспорт

	ИзмененныеРеквизиты = "";
	
	//++ Локализация
	
	Если Объект.ПорядокУчетаУУ <> ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.АмортизацияНачислена") 
		И Объект.ПорядокУчетаБУ = ПредопределенноеЗначение("Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена") Тогда
			
		Объект.ПорядокУчетаБУ = ПредопределенноеЗначение("Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеАмортизации");
		ИзмененныеРеквизиты = ПриИзмененииПорядкаУчетаБУ_ОС(Объект, СлужебныеПараметрыФормы.ПрименяетсяУСНДоходыМинусРасходы) + "," + ИзмененныеРеквизиты;
		
	ИначеЕсли Объект.ПорядокУчетаУУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.АмортизацияНачислена") 
		И Объект.ПорядокУчетаБУ <> ПредопределенноеЗначение("Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена") Тогда
			
		Объект.ПорядокУчетаБУ = ПредопределенноеЗначение("Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена");
		ИзмененныеРеквизиты = ПриИзмененииПорядкаУчетаБУ_ОС(Объект, СлужебныеПараметрыФормы.ПрименяетсяУСНДоходыМинусРасходы) + "," + ИзмененныеРеквизиты;
		
	КонецЕсли;
	
	//-- Локализация
	
	Возврат ИзмененныеРеквизиты;
	
КонецФункции

Функция ИмяФормыДокументыПоОсновномуСредству() Экспорт

	ИмяФормы = Неопределено;
	
	//++ Локализация
	Если ВнеоборотныеАктивыВызовСервера.ИспользуетсяУправлениеВНА_2_4() Тогда
		ИмяФормы = "Обработка.ЖурналДокументовОС2_4.Форма";
	Иначе
		ИмяФормы = "Обработка.ЖурналДокументовОС.Форма";
	КонецЕсли;
	//-- Локализация
	
	Возврат ИмяФормы;
	
КонецФункции

Функция ИмяФормыДокументыПоНематериальномуАктиву() Экспорт

	ИмяФормы = Неопределено;
	
	//++ Локализация
	Если ВнеоборотныеАктивыВызовСервера.ИспользуетсяУправлениеВНА_2_4() Тогда
		ИмяФормы = "Обработка.ЖурналДокументовНМА2_4.Форма";
	Иначе
		ИмяФормы = "Обработка.ЖурналДокументовНМА.Форма";
	КонецЕсли;
	//-- Локализация
	
	Возврат ИмяФормы;
	
КонецФункции

Процедура ПриИзмененииРеквизитаПринятияКУчетуОС(Элемент, Форма, ДополнительныеПараметры, ТребуетсяВызовСервера) Экспорт

	//++ Локализация
	
	Элементы = Форма.Элементы;
	
	Если Элемент.Имя = Элементы.ВариантРаздельногоУчетаНДСИзДокумента.Имя
		ИЛИ Элемент.Имя = Элементы.ВариантРаздельногоУчетаНДСРаспределяется.Имя
		ИЛИ Элемент.Имя = Элементы.ПорядокУчетаНУ.Имя Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Выполнить_НастроитьЗависимыеЭлементыФормы", Элемент.Имя);
		
	ИначеЕсли Элемент.Имя = Элементы.МетодНачисленияАмортизацииБУ.Имя Тогда
		
		ТребуетсяВызовСервера = Истина;
		
	ИначеЕсли Элемент.Имя = Элементы.ПорядокУчетаБУ.Имя Тогда
		
		ПорядокУчетаБУПриИзменении(Форма, ДополнительныеПараметры);
		
	ИначеЕсли Элемент.Имя = Элементы.СрокИспользованияБУ.Имя Тогда
		
		СрокИспользованияБУПриИзменении(ДополнительныеПараметры);
		
	ИначеЕсли Элемент.Имя = Элементы.СрокИспользованияНУ.Имя Тогда
		
		СрокИспользованияНУПриИзменении(ДополнительныеПараметры);
		
	ИначеЕсли Элемент.Имя = Элементы.СтатьяРасходовБУ.Имя
		ИЛИ Элемент.Имя = Элементы.СтатьяРасходовНалог.Имя
		ИЛИ Элемент.Имя = Элементы.СтатьяРасходовАмортизационнойПремии.Имя
		ИЛИ Элемент.Имя = Элементы.СтатьяДоходовЦФ.Имя
		ИЛИ Элемент.Имя = Элементы.СтатьяДоходовПоИнвентаризации.Имя Тогда
		
		ДоходыИРасходыКлиентСервер.СтатьяПриИзменении(Форма, Элемент);
		
	ИначеЕсли Элемент.Имя = Элементы.СтатьяРасходовНУ.Имя Тогда
		
		ДоходыИРасходыКлиентСервер.СтатьяПриИзменении(Форма, Элемент);
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Выполнить_НастроитьЗависимыеЭлементыФормы", Элемент.Имя);
		
	ИначеЕсли Элемент.Имя = Элементы.ПередаватьРасходыВДругуюОрганизацию.Имя Тогда
		
		ПередаватьРасходыВДругуюОрганизациюПриИзменении(Форма, ДополнительныеПараметры);
		
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

Процедура ПриИзмененииРеквизитаОбъединениеОС(Элемент, Форма, ДополнительныеПараметры, ТребуетсяВызовСервера) Экспорт

	//++ Локализация
	
	Элементы = Форма.Элементы;
	
	Если Элемент.Имя = Элементы.ВариантРаздельногоУчетаНДСИзДокумента.Имя
		ИЛИ Элемент.Имя = Элементы.ВариантРаздельногоУчетаНДСРаспределяется.Имя
		ИЛИ Элемент.Имя = Элементы.ПорядокУчетаНУ.Имя Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Выполнить_НастроитьЗависимыеЭлементыФормы", Элемент.Имя);
		
	ИначеЕсли Элемент.Имя = Элементы.МетодНачисленияАмортизацииБУ.Имя Тогда
		
		ТребуетсяВызовСервера = Истина;
		
	ИначеЕсли Элемент.Имя = Элементы.ПорядокУчетаБУ.Имя Тогда
		
		ПорядокУчетаБУПриИзменении(Форма, ДополнительныеПараметры);
		
	ИначеЕсли Элемент.Имя = Элементы.СрокИспользованияБУ.Имя Тогда
		
		СрокИспользованияБУПриИзменении(ДополнительныеПараметры);
		
	ИначеЕсли Элемент.Имя = Элементы.СрокИспользованияОстаточныйБУ.Имя
		ИЛИ Элемент.Имя = Элементы.СрокИспользованияОстаточныйБУ_ОбъединениеОднотипныхОС.Имя Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Выполнить_НастроитьЗависимыеЭлементыФормы", "СрокИспользованияОстаточныйБУ");
		
	ИначеЕсли Элемент.Имя = Элементы.СрокИспользованияНУ.Имя Тогда
		
		СрокИспользованияНУПриИзменении(ДополнительныеПараметры);
		
	ИначеЕсли Элемент.Имя = Элементы.СтатьяРасходовБУ.Имя
		ИЛИ Элемент.Имя = Элементы.СтатьяРасходовНалог.Имя
		ИЛИ Элемент.Имя = Элементы.СтатьяДоходов.Имя Тогда
		
		ДоходыИРасходыКлиентСервер.СтатьяПриИзменении(Форма, Элемент);
		
	ИначеЕсли Элемент.Имя = Элементы.ПередаватьРасходыВДругуюОрганизацию.Имя Тогда
		
		ПередаватьРасходыВДругуюОрганизациюПриИзменении(Форма, ДополнительныеПараметры);
		
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

#Область ПанельСправочниковВнеоборотныеАктивы

Процедура ПриВыполненииКоманды_ПанельСправочниковВнеоборотныеАктивы(Команда, Форма) Экспорт

	//++ Локализация
	
	Элементы = Форма.Элементы;
	
	Если Команда.Имя = Элементы.ОткрытьГодовыеГрафикиАмортизацииОС.ИмяКоманды Тогда
		ОткрытьФорму("Справочник.ГодовыеГрафикиАмортизацииОС.ФормаСписка", , Форма);
	ИначеЕсли Команда.Имя = Элементы.ОткрытьКлассификаторОКОФ.ИмяКоманды Тогда
		ОткрытьФорму("Справочник.ОбщероссийскийКлассификаторОсновныхФондов.ФормаСписка", , Форма);
	ИначеЕсли Команда.Имя = Элементы.ОткрытьКлассификаторЕНАОФ.ИмяКоманды Тогда
		ОткрытьФорму("Справочник.ЕдиныеНормыАмортизационныхОтчисленийОсновныхФондов.ФормаСписка", , Форма);
	ИначеЕсли Команда.Имя = Элементы.ОткрытьКатегорииЭксплуатации.ИмяКоманды Тогда
		ОткрытьФорму("Справочник.КатегорииЭксплуатации.ФормаСписка", , Форма);
	ИначеЕсли Команда.Имя = Элементы.ОткрытьСтавкиТранспортногоНалога.Имя Тогда
		ОткрытьФорму("РегистрСведений.СтавкиТранспортногоНалога.ФормаСписка", , Форма);
	//++ НЕ УТКА
	ИначеЕсли Команда.Имя = Элементы.ОткрытьГруппыОС.Имя Тогда
		ОткрытьФорму("Справочник.ГруппыОСМеждународныйУчет.ФормаСписка", , Форма);
	ИначеЕсли Команда.Имя = Элементы.ОткрытьГруппыНМА.Имя Тогда
		ОткрытьФорму("Справочник.ГруппыНМАМеждународныйУчет.ФормаСписка", , Форма);
	ИначеЕсли Команда.Имя = Элементы.ОткрытьГруппыОС.Имя Тогда
		ОткрытьФорму("Справочник.ГруппыОСМеждународныйУчет.ФормаСписка", , Форма);
	ИначеЕсли Команда.Имя = Элементы.ОткрытьГруппыНМА.Имя Тогда
		ОткрытьФорму("Справочник.ГруппыНМАМеждународныйУчет.ФормаСписка", , Форма);
	//-- НЕ УТКА
	КонецЕсли; 
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

//++ Локализация

Функция ПриИзмененииПорядкаУчетаБУ_ОС(Объект, ПрименяетсяУСНДоходыМинусРасходы = Ложь, ЕстьПорядокУчетаНУ = Истина) Экспорт

	ИзмененныеРеквизиты = "ПорядокУчетаБУ";
	
	Если Объект.ПорядокУчетаБУ = ПредопределенноеЗначение("Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеАмортизации") Тогда
		
		Если НЕ ЗначениеЗаполнено(Объект.МетодНачисленияАмортизацииБУ) Тогда
			Объект.МетодНачисленияАмортизацииБУ = ПредопределенноеЗначение("Перечисление.СпособыНачисленияАмортизацииОС.Линейный");
			ИзмененныеРеквизиты = ИзмененныеРеквизиты + ",МетодНачисленияАмортизацииБУ";
		КонецЕсли; 
		
		Если ЕстьПорядокУчетаНУ Тогда
			Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации");
			ИзмененныеРеквизиты = ИзмененныеРеквизиты + ",ПорядокУчетаНУ";
		КонецЕсли;
		
	ИначеЕсли Объект.ПорядокУчетаБУ = ПредопределенноеЗначение("Перечисление.ПорядокПогашенияСтоимостиОС.СтоимостьНеПогашается")
		И ЕстьПорядокУчетаНУ Тогда
		
		Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.СтоимостьНеВключаетсяВРасходы");
		ИзмененныеРеквизиты = ИзмененныеРеквизиты + ",ПорядокУчетаНУ";
		
	ИначеЕсли Объект.ПорядокУчетаБУ = ПредопределенноеЗначение("Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена")
		И ЕстьПорядокУчетаНУ Тогда
		
		Если ПрименяетсяУСНДоходыМинусРасходы Тогда
			Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключитьВСоставАмортизируемогоИмущества");
		Иначе
			Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.АмортизацияНачислена");
		КонецЕсли; 
		ИзмененныеРеквизиты = ИзмененныеРеквизиты + ",ПорядокУчетаНУ";
		
	ИначеЕсли Объект.ПорядокУчетаБУ = ПредопределенноеЗначение("Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ")
		И ЕстьПорядокУчетаНУ Тогда
		
		Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.СтоимостьНеВключаетсяВРасходы");
		Объект.ПрименениеЦелевогоФинансирования = Ложь;
		ИзмененныеРеквизиты = ИзмененныеРеквизиты + ",ПорядокУчетаНУ,ПрименениеЦелевогоФинансирования";
		
	ИначеЕсли Объект.ПорядокУчетаБУ = ПредопределенноеЗначение("Перечисление.ПорядокПогашенияСтоимостиОС.НачислитьАмортизациюПриПринятииКУчету")
		И ЕстьПорядокУчетаНУ Тогда
		
		Если Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.АмортизацияНачислена") Тогда
			Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации");
			ИзмененныеРеквизиты = ИзмененныеРеквизиты + ",ПорядокУчетаНУ";
		КонецЕсли; 
		
	КонецЕсли; 
	
	Если Объект.ПорядокУчетаБУ <> ПредопределенноеЗначение("Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена") 
		И Объект.ПорядокУчетаУУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.АмортизацияНачислена") Тогда
			
		Объект.ПорядокУчетаУУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислятьАмортизацию");
		ИзмененныеРеквизиты = ИзмененныеРеквизиты + ",ПорядокУчетаУУ";
	
	ИначеЕсли Объект.ПорядокУчетаБУ = ПредопределенноеЗначение("Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена") 
		И Объект.ПорядокУчетаУУ <> ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.АмортизацияНачислена") Тогда
			
		Объект.ПорядокУчетаУУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.АмортизацияНачислена");
		ИзмененныеРеквизиты = ИзмененныеРеквизиты + ",ПорядокУчетаУУ";
	КонецЕсли;
	
	Возврат ИзмененныеРеквизиты;
	
КонецФункции

//  Функция возвращает ОС выбранное в текущей строке таблицы ОС документа для дальнейшей передачи
// в функцию ДозаполнитьТабличнуюЧастьОсновнымиСредствамиПоНаименованию. В случае невозможности
// определения ОС выдает сообщение об ошибке.
//
// Параметры
//  ПараметрыФормы   - Структура с параметрами заполнения, ключи структуры:
//  	Форма             - форма заполняемого документа
//  	Объект            - Значение основного реквизита формы - документа для заполнения
//  	ИмяТабличнойЧасти - Имя табличной части основных средств документа, значение по умолчанию "ОС".
//
// Возвращаемое значение:
//   СправочникСсылка.ОбъектыЭксплуатации, Неопределено - В случае невозможности определения ОС функция
//   	возвращает Неопределено, в противном случае функция возвращает значение ОС.
//
Функция ПолучитьОСДляЗаполнениеПоНаименованию(Параметры) Экспорт
	
	ОчиститьСообщения();
	
	Форма = Параметры.Форма;
	Объект = Параметры.Объект;
	Если Параметры.Свойство("ИмяТабличнойЧасти") Тогда
		ИмяТабличнойЧасти = Параметры.ИмяТабличнойЧасти;
	Иначе
		ИмяТабличнойЧасти = "ОС";
	КонецЕсли;
	
	Если Форма.Элементы[ИмяТабличнойЧасти].ТекущаяСтрока = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Данные для заполнения отсутствуют.';
								|en = 'Data for population is missing.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Объект." + ИмяТабличнойЧасти);
		Возврат Неопределено;
	КонецЕсли;
	
	ОсновноеСредство = Форма.Элементы[ИмяТабличнойЧасти].ТекущиеДанные.ОсновноеСредство;
	
	Если НЕ ЗначениеЗаполнено(ОсновноеСредство) Тогда
		ТекстСообщения = НСтр("ru = 'Не выбрано основное средство';
								|en = 'Fixed asset is not selected'");
		ИндексСтроки = Формат(Объект[ИмяТабличнойЧасти].Индекс(Форма.Элементы[ИмяТабличнойЧасти].ТекущиеДанные), "ЧН=0; ЧГ=");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Объект." + ИмяТабличнойЧасти + "[" + ИндексСтроки + "].ОсновноеСредство");
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ОсновноеСредство;
	
КонецФункции // ПолучитьОСДляЗаполнениеПоНаименованию()

//-- Локализация

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОбработкаДействияПодробнееФормыЗакрытияМесяцаЭтапаНачислениеАмортизацииОС(Параметры) Экспорт

	ОбработкаВыполнена = Ложь;
	
	//++ Локализация
	Если Параметры.ДополнительныеПараметры.ИспользоватьВнеоборотныеАктивы2_4
		И Параметры.Период < Параметры.ДополнительныеПараметры.ДатаНачалаУчета Тогда
		
		ОбработкаВыполнена = Истина;
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Период", Параметры.Период);
		ПараметрыФормы.Вставить("МассивОрганизаций", Параметры.МассивОрганизаций);
		ОткрытьФорму("Документ.АмортизацияОС.ФормаСписка", ПараметрыФормы);
	КонецЕсли;
	//-- Локализация
	
	Возврат ОбработкаВыполнена;
	
КонецФункции

Функция ОбработкаДействияПодробнееФормыЗакрытияМесяцаЭтапаНачислениеАмортизацииНМА(Параметры) Экспорт

	ОбработкаВыполнена = Ложь;
	
	//++ Локализация
	Если Параметры.ДополнительныеПараметры.ИспользоватьВнеоборотныеАктивы2_4
		И Параметры.Период < Параметры.ДополнительныеПараметры.ДатаНачалаУчета Тогда
		
		ОбработкаВыполнена = Истина;
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Период", Параметры.Период);
		ПараметрыФормы.Вставить("МассивОрганизаций", Параметры.МассивОрганизаций);
		ОткрытьФорму("Документ.АмортизацияНМА.ФормаСписка", ПараметрыФормы);
	КонецЕсли;
	//-- Локализация
	
	Возврат ОбработкаВыполнена;
	
КонецФункции

//++ Локализация

Процедура ВыполнитьКомандуОткрытьПроводкиРегламентированногоУчета(Форма, ДополнительныеПараметры)
	
	Если Форма.Модифицированность ИЛИ НЕ ЗначениеЗаполнено(Форма.Объект.Ссылка) Тогда
		
		ДополнительныеПараметры = Новый Структура;
		
		ПараметрыДействия = Новый Структура;
		ПараметрыДействия.Вставить("ЗаголовокДействия", НСтр("ru = 'Проводки регламентированного учета';
															|en = 'Local accounting postings'"));
		ПараметрыДействия.Вставить("ДействиеПослеРасчета", "ОткрытьПроводкиРегламентированногоУчета");
		ДополнительныеПараметры.Вставить("Выполнить_ЗаписатьИВыполнитьДействие", ПараметрыДействия);
		
	Иначе
		ОткрытьПроводкиРегламентированногоУчета(Форма.Объект.Ссылка);
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОткрытьПроводкиРегламентированногоУчета(Ссылка)

	Отбор = Новый Структура("Регистратор", Ссылка);
	ПараметрыФормы = Новый Структура("Отбор", Отбор);
	ОткрытьФорму("Обработка.ОтражениеДокументовВРеглУчете.Форма.ПроводкиРегламентированногоУчета", ПараметрыФормы,, Ссылка);

КонецПроцедуры

Процедура ПорядокУчетаБУПриИзменении(Форма, ДополнительныеПараметры)
	
	ИзмененныеРеквизиты = ПриИзмененииПорядкаУчетаБУ_ОС(
								Форма.Объект, Форма.СлужебныеПараметрыФормы.ПрименяетсяУСНДоходыМинусРасходы);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Выполнить_НастроитьЗависимыеЭлементыФормы", ИзмененныеРеквизиты);
	
КонецПроцедуры

Процедура СрокИспользованияБУПриИзменении(ДополнительныеПараметры)
	
	ДополнительныеПараметры = Новый Структура;
	
	ПараметрыДействия = Новый Структура("ИмяРеквизита,ОбновитьЕслиСовпадают", "СрокИспользованияБУ", Истина);
	ДополнительныеПараметры.Вставить("Выполнить_ПриИзмененииСрокаИспользования", ПараметрыДействия);
	
КонецПроцедуры

Процедура СрокИспользованияНУПриИзменении(ДополнительныеПараметры)
	
	ДополнительныеПараметры = Новый Структура;
	
	ПараметрыДействия = Новый Структура("ИмяРеквизита,ОбновитьЕслиСовпадают", "СрокИспользованияНУ", Ложь);
	ДополнительныеПараметры.Вставить("Выполнить_ПриИзмененииСрокаИспользования", ПараметрыДействия);
	
КонецПроцедуры

Процедура ПередаватьРасходыВДругуюОрганизациюПриИзменении(Форма, ДополнительныеПараметры)
	
	Объект = Форма.Объект;
	
	Если Не Объект.ПередаватьРасходыВДругуюОрганизацию Тогда
		Объект.ОрганизацияПолучательРасходов = Неопределено;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Выполнить_НастроитьЗависимыеЭлементыФормы", "ПередаватьРасходыВДругуюОрганизацию");
	
КонецПроцедуры

//-- Локализация

#КонецОбласти
