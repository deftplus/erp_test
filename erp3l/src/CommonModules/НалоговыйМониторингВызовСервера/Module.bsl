
Процедура ЗаписатьДействияПользователя(СписокОкон, ДатаДействий, ИдентификаторПользовательИБ = Неопределено) Экспорт
	
	Если ИдентификаторПользовательИБ = Неопределено Тогда
		ТекущийПользователь = Пользователи.АвторизованныйПользователь();
	Иначе
		
		ТекПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ИдентификаторПользовательИБ);
		Если ТекПользовательИБ <> Неопределено Тогда
			ТекущийПользователь = Пользователи.НайтиПоИмени(ТекПользовательИБ.Имя);		
		КонецЕсли;
		
	КонецЕсли;
		
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	т.ЗаголовокОкна КАК ЗаголовокОкна,
	|	т.Данные КАК Данные,
	|	т.Начало КАК Начало,
	|	т.Описание КАК Описание
	|ИЗ
	|	РегистрСведений.ДействияПользователяНалоговогоМониторинга КАК т
	|ГДЕ
	|	т.Окончание = ДАТАВРЕМЯ(1, 1, 1)
	|	И т.Пользователь = &Пользователь");
	Запрос.УстановитьПараметр("Пользователь", ТекущийПользователь);
	
	Выборка = Запрос.Выполнить().Выбрать();	
		
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.Данные) Тогда //поиск по ссылочному реквизиту
			ЗначениеПоСсылке = СписокОкон.НайтиПоЗначению(Выборка.Данные);
		ИначеЕсли ЗначениеЗаполнено(Выборка.Описание) Тогда //поиск отбору СКД
			
			ЗначениеПоСсылке = СписокОкон.НайтиПоЗначению(Выборка.Описание);
			Если (ЗначениеПоСсылке <> Неопределено) И (ЗначениеПоСсылке.Представление <> Выборка.ЗаголовокОкна) Тогда 
				//отбор найден, но для другого объекта (например, открыта форма настроек СКД того же отчета) - ищем полным перебором
				ЗначениеПоСсылке = Неопределено;
				Для каждого ЭлементОкно Из СписокОкон Цикл
					Если (ЭлементОкно.Значение = Выборка.Описание) И (ЭлементОкно.Представление = Выборка.ЗаголовокОкна) Тогда
						ЗначениеПоСсылке = ЭлементОкно;
						Прервать;					
					КонецЕсли;				
				КонецЦикла;
				
			КонецЕсли;
			
		Иначе //поиск по заголовку окна
			
			ЗначениеПоСсылке = Неопределено;
			Для каждого ЭлементОкно Из СписокОкон Цикл
				Если ЭлементОкно.Представление = Выборка.ЗаголовокОкна Тогда
					ЗначениеПоСсылке = ЭлементОкно;
					Прервать;
				КонецЕсли;						
			КонецЦикла;
			
		КонецЕсли;
 				
		Если ЗначениеПоСсылке <> Неопределено Тогда
			ЗначениеПоСсылке.Пометка = Истина; // окно еще открыто
			Продолжить;
		КонецЕсли;
		
		//окно закрыто - запишем дату окончания		
		мз = РегистрыСведений.ДействияПользователяНалоговогоМониторинга.СоздатьМенеджерЗаписи();		
		ЗаполнитьЗначенияСвойств(мз, Выборка);		
		мз.Пользователь = ТекущийПользователь;		
		мз.Прочитать();
		
		Если мз.Выбран() Тогда
			мз.Окончание = ДатаДействий;
			мз.Записать();
		КонецЕсли;
								
	КонецЦикла;
	
	//запишем новые открытые окна
	нз = РегистрыСведений.ДействияПользователяНалоговогоМониторинга.СоздатьНаборЗаписей();	
	нз.Отбор.Начало.Установить(ДатаДействий);
	нз.Отбор.Пользователь.Установить(ТекущийПользователь);
	
	ПроверкаУникальности = Новый Соответствие;
		
	Для каждого НовоеОкно Из СписокОкон Цикл
	
		Если НовоеОкно.Пометка Тогда
			Продолжить;
		КонецЕсли;
		
		ТипТекущегоЗначения = ТипЗнч(НовоеОкно.Значение);
		Если ТипТекущегоЗначения = Тип("Строка") Тогда //отбор отчета на СКД
			ТекущееОписание = НовоеОкно.Значение;
		ИначеЕсли ОбщегоНазначения.ЭтоСсылка(ТипТекущегоЗначения) Тогда
			ТекущееОписание = НовоеОкно.Значение.УникальныйИдентификатор();
		Иначе 
			ТекущееОписание = "";
		КонецЕсли;
		
		Запись 					= нз.Добавить();
		
		Запись.Начало 			= ДатаДействий;
		Запись.Пользователь 	= ТекущийПользователь;
		Запись.ЗаголовокОкна 	= НовоеОкно.Представление;
		Запись.Данные			= НовоеОкно.Значение;// запишется только если ссылка		
		Запись.Описание 		= ТекущееОписание;
							
	КонецЦикла;
	
	нз.Записать();
	
КонецПроцедуры

Процедура ФормаРегламентированнойОтчетности_ПриСозданииНаСервере(Форма) Экспорт

	Если Не РольДоступна("НалоговыйМониторинг") Тогда
		Возврат; 
	КонецЕсли;
	
	ТекущийЭлемент = Форма.Элементы.Найти("Записать"); // сохранение не используется ддля роли налоговый инспектор
	Если ТекущийЭлемент = Неопределено Тогда		
		ТекущийЭлемент = Форма.Элементы.Найти("Сохранить"); // сохранение не используется ддля роли налоговый инспектор
		Если ТекущийЭлемент = Неопределено Тогда
			Возврат;	
		КонецЕсли;		
	КонецЕсли;
	
	ТекущийЭлемент.Заголовок = "Запрос налогового мониторинга";
	ТекущийЭлемент.Картинка = БиблиотекаКартинок.НалоговыйОрган;
	ТекущийЭлемент.Отображение = ОтображениеКнопки.КартинкаИТекст;
	
	Если Форма.Команды.Найти("РольНалоговыйИнспектор") = Неопределено Тогда
		
		Команда = Форма.Команды.Добавить("РольНалоговыйИнспектор");
		Команда.Заголовок = ТекущийЭлемент.Заголовок;
		Команда.Действие = "Обновить";
				
	КонецЕсли;	
	
	ТекущийЭлемент = Форма.Элементы.Найти("Заполнить");
	Если ТекущийЭлемент <> Неопределено Тогда
		ТекущийЭлемент.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

//обработчик регламентного задания: ЗавершениеСеансовИнспектора
Процедура ЗавершениеСеансовИнспектора() Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	т.Пользователь.ИдентификаторПользователяИБ КАК ИдентификаторПользователяИБ
	|ИЗ
	|	РегистрСведений.ДействияПользователяНалоговогоМониторинга КАК т
	|ГДЕ
	|	т.Окончание = ДАТАВРЕМЯ(1, 1, 1)");
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда		
		Возврат;
	КонецЕсли;
		
	ПользователиДляЗавершения = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("ИдентификаторПользователяИБ");
	
	СоединенияИнформационнойБазы = ПолучитьСоединенияИнформационнойБазы();
		
	Для каждого ТекущееСоединение Из СоединенияИнформационнойБазы Цикл
		
		НомерПользователя = ПользователиДляЗавершения.Найти(ТекущееСоединение.Пользователь.УникальныйИдентификатор);
		Если НомерПользователя <> Неопределено Тогда
			ПользователиДляЗавершения.Удалить(НомерПользователя);//инспектор еще в программе
			Если ПользователиДляЗавершения.Количество() = 0 Тогда
				Прервать;	
			КонецЕсли;
		КонецЕсли;
	
	КонецЦикла;
	
	Для каждого ИнспекторДляЗавершения Из ПользователиДляЗавершения Цикл
		ЗаписатьДействияПользователя(Новый СписокЗначений, ТекущаяДата(), ИнспекторДляЗавершения);
	КонецЦикла;
	
КонецПроцедуры

