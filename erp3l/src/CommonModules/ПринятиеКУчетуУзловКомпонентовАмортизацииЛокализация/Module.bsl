////////////////////////////////////////////////////////////////////////////////
// Процедуры, используемые для локализации документа "Принятие к учету узлов и компонентов амортизации".
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация
	МеханизмыДокумента.Добавить("ИмущественныеНалоги");
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	//-- Локализация
	
КонецПроцедуры

// Добавляет в текст запроса реквизиты шапки документа.
//  Реквизиты добавляются вместо параметра "&ПринятиеКУчетуОС_РеквизитыДокумента".
//
// Параметры:
//  ТекстЗапроса - Строка				 - Исходный текст запроса.
//  ИмяТаблицы	 - Строка, Неопределено	 - Синоним таблицы документа в запросе.
//  	Для получения пустых значений нужно передать "Неопределено".
//
Процедура ДобавитьВТекстЗапросаРеквизитыДокумента(ТекстЗапроса, ИмяТаблицы = Неопределено) Экспорт
	
	ПоляЛокализации = "NULL";
	
	//++ Локализация
	
	ПоляЛокализации =
	"
	|	ДанныеДокумента.ОтражатьВРеглУчете                                КАК ОтражатьВБУ,
	|	
	|	ДанныеДокумента.ПорядокУчетаБУ                                    КАК ПорядокУчетаБУ,
	|	ДанныеДокумента.НачислятьАмортизациюБУ                            КАК НачислятьАмортизациюБУ,
	|	
	|	ВЫБОР КОГДА ДанныеДокумента.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислитьАмортизациюПриПринятииКУчету)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный)
	|		ИНАЧЕ ДанныеДокумента.МетодНачисленияАмортизацииБУ
	|	КОНЕЦ                                                             КАК МетодНачисленияАмортизацииБУ,
	|
	|	ДанныеДокумента.СрокИспользованияБУ                               КАК СрокИспользованияБУ,
	|	ДанныеДокумента.ГрафикАмортизации                                 КАК ГрафикАмортизации,
	|	ДанныеДокумента.ГодоваяНормаАмортизацииБУ                         КАК ГодоваяНормаАмортизацииБУ,
	|	ДанныеДокумента.КоэффициентУскоренияБУ                            КАК КоэффициентУскоренияБУ";

	ПоляЛокализации = СтрЗаменить(ПоляЛокализации, "ДанныеДокумента", ИмяТаблицы);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПринятиеКУчетуОС_РеквизитыДокумента", ПоляЛокализации);
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ПроводкиРегУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	ТекстЗапроса = "";
	
	//++ Локализация
	
	СписокЗапросов = Новый Массив;
	
	ВнеоборотныеАктивыЛокализация.ТекстОтраженияВРеглУчетеНачисленияАмортизацииОС(СписокЗапросов);
	
	#Область ПринятиеКУчетуОС_Дт_СчетУчета__Кт_08_04_2
	
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Принятие к учету ОС (Дт СчетУчета:: Кт 08.04.2)
	|//
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СтоимостьРегл КАК Сумма,
	|	Строки.Стоимость КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА) КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СтоимостьНУ КАК СуммаНУДт,
	|	Строки.СтоимостьПР КАК СуммаПРДт,
	|	Строки.СтоимостьВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	Строки.КорСтатьяРасходов КАК АналитикаУчетаКт,
	|	Строки.КорПодразделение КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.КорПодразделение КАК ПодразделениеКт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	Строки.КорСтатьяРасходов КАК СубконтоКт1,
	|	Строки.КорАналитикаРасходов КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СтоимостьРегл - Строки.КорПостояннаяРазница - Строки.КорВременнаяРазница КАК СуммаНУКт,
	|	Строки.КорПостояннаяРазница КАК СуммаПРКт,
	|	Строки.КорВременнаяРазница КАК СуммаВРКт,
	|	""Принятие к учету узлов и компонентов амортизации"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуУзловКомпонентовАмортизации КАК Операция
	|	ПО Операция.Ссылка = ДокументыКОтражению.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК Строки
	|	ПО Строки.Регистратор = ДокументыКОтражению.Ссылка
	|	
	|ГДЕ
	|	Строки.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуУзловКомпонентовАмортизации)
	|	И Строки.РасчетСтоимости
	|	И (Строки.Стоимость <> 0
	|		ИЛИ Строки.СтоимостьРегл <> 0
	|		ИЛИ Строки.СтоимостьНУ <> 0
	|		ИЛИ Строки.СтоимостьПР <> 0
	|		ИЛИ Строки.СтоимостьВР <> 0)";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область СписаниеНаРасходыНеПринимаяКУчету_Дт_СчетЗатрат__Кт_08_04_2
	
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Включение в расходы стоимости ОС (Дт СчетЗатрат:: Кт 08.04.2)
	|//
	|ВЫБРАТЬ
	|	
	|	СтрокиДт.Регистратор КАК Ссылка,
	|	СтрокиДт.Период КАК Период,
	|	СтрокиДт.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	СтрокиДт.СуммаРегл КАК Сумма,
	|	СтрокиДт.СуммаУпр КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	СтрокиДт.СтатьяРасходов КАК АналитикаУчетаДт,
	|	СтрокиДт.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	СтрокиДт.Подразделение КАК ПодразделениеДт,
	|	СтрокиДт.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	СтрокиДт.СтатьяРасходов КАК СубконтоДт1,
	|	СтрокиДт.АналитикаРасходов КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	СтрокиДт.СуммаРегл - СтрокиДт.ПостояннаяРазница - СтрокиДт.ВременнаяРазница КАК СуммаНУДт,
	|	СтрокиДт.ПостояннаяРазница КАК СуммаПРДт,
	|	СтрокиДт.ВременнаяРазница КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	СтрокиКт.СтатьяРасходов КАК АналитикаУчетаКт,
	|	СтрокиКт.Подразделение КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	СтрокиКт.Подразделение КАК ПодразделениеКт,
	|	СтрокиКт.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	СтрокиКт.СтатьяРасходов КАК СубконтоКт1,
	|	СтрокиКт.АналитикаРасходов КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	СтрокиКт.СуммаРегл - СтрокиКт.ПостояннаяРазница - СтрокиКт.ВременнаяРазница КАК СуммаНУКт,
	|	СтрокиКт.ПостояннаяРазница КАК СуммаПРКт,
	|	СтрокиКт.ВременнаяРазница КАК СуммаВРКт,
	|
	|	""Включение в расходы стоимости ОС"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы КАК СтрокиДт
	|		ПО СтрокиДт.Регистратор = ДокументыКОтражению.Ссылка
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы КАК СтрокиКт
	|		ПО СтрокиКт.Регистратор = СтрокиДт.Регистратор
	|			И СтрокиКт.НастройкаХозяйственнойОперации = СтрокиДт.НастройкаХозяйственнойОперации
	|			И СтрокиКт.ИдентификаторФинЗаписи = СтрокиДт.ИдентификаторФинЗаписи
	|			И СтрокиКт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			И (СтрокиКт.СуммаРегл <> 0
	|				ИЛИ СтрокиКт.ПостояннаяРазница <> 0
	|				ИЛИ СтрокиКт.ВременнаяРазница <> 0)
	|
	|ГДЕ
	|	СтрокиДт.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНаРасходыСтоимостиОСНеПринимаяКУчету)
	|	И СтрокиДт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
		
	//-- Локализация
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц, необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	ТекстЗапроса = "";
	
	//++ Локализация

	СписокЗапросов = Новый Массив;
	
	ВнеоборотныеАктивыЛокализация.ТекстЗапросаВТНачислениеАмортизацииОС(СписокЗапросов);
	
	СписокЗапросов.Добавить(Символы.ПС);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());

	//-- Локализация
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область Проведение

Процедура ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты) Экспорт

	//++ Локализация
	
	ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитикПовтИсп.ДействующиеПараметрыНалоговУчетныхПолитик(
		"НастройкиУчетаНалогаНаПрибыль",
		Реквизиты.Организация,
		Реквизиты.Период);
		
	Запрос.УстановитьПараметр("ВключатьВСоставНалоговыхРасходовАрендныеПлатежи", 
		ПараметрыУчетнойПолитики <> Неопределено И ПараметрыУчетнойПолитики.ВключатьВСоставНалоговыхРасходовАрендныеПлатежи);
		
	Запрос.УстановитьПараметр("ПредельнаяСтоимостьСписанияНаРасходы", ВнеоборотныеАктивыЛокализация.ПредельнаяСтоимостьСписанияНаРасходы());
	//-- Локализация
	
КонецПроцедуры

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация
	
	ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаСобытияОСОрганизаций(ТекстыЗапроса, Регистры);
	
	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры);
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаПроверкиЗаполнения

// Процедура дополняет проверку ОС-владельцев.
//
// Параметры:
//  Запрос - Запрос - запрос для проверки.
//  Отказ - Булево - флаг отказа от завершения транзакции.
//
Процедура ПроверитьОсновныеСредства(Запрос, Отказ) Экспорт
	
	//++ Локализация

	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(МестонахождениеОС.ОсновноеСредство) КАК ОсновноеСредство,
		|	ЕСТЬNULL(ПорядокУчетаОСБУ.СостояниеБУ, ЗНАЧЕНИЕ(Перечисление.СостоянияОС.НеПринятоКУчету)) КАК Состояние
		|ИЗ
		|	МестонахождениеОС КАК МестонахождениеОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(&Период, (ОсновноеСредство, Организация) В
		|			(ВЫБРАТЬ
		|				МестонахождениеОС.ОсновноеСредство,
		|				МестонахождениеОС.Организация
		|			ИЗ
		|				МестонахождениеОС КАК МестонахождениеОС)) КАК ПорядокУчетаОСБУ
		|		ПО МестонахождениеОС.ОсновноеСредство = ПорядокУчетаОСБУ.ОсновноеСредство
		|		И МестонахождениеОС.Организация = ПорядокУчетаОСБУ.Организация";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.Состояние = Перечисления.СостоянияОС.НеПринятоКУчету Тогда
			
			ОбщегоНазначения.СообщитьПользователю(СтрШаблон(НСтр("ru = 'Основное средство ""%1"" не принято к учету в регламентированном учете.';
																|en = 'The ""%1"" fixed asset is not recognized in local accounting.'"),
				Выборка.ОсновноеСредство),,,, Отказ);
			
		ИначеЕсли Выборка.Состояние = Перечисления.СостоянияОС.СнятоСУчета Тогда
			
			ОбщегоНазначения.СообщитьПользователю(СтрШаблон(НСтр("ru = 'Основное средство ""%1"" снято с учета в регламентированном учете.';
																|en = 'The ""%1"" fixed asset is derecognized in local management accounting.'"),
				Выборка.ОсновноеСредство),,,, Отказ);
			
		КонецЕсли; 
	КонецЦикла;
	
	//-- Локализация

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

//++ Локализация

Процедура ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПорядокУчетаОСБУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыЛокализация.ТекстЗапросаТаблицаВтПорядокУчетаОСБУ(ТекстыЗапроса, "Документ.ПринятиеКУчетуУзловКомпонентовАмортизации");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                 КАК Период,
	|	&Организация                            КАК Организация,
	|	ТаблицаКомпонентовАмортизации.КомпонентАмортизации КАК ОсновноеСредство,
	|
	|	ВЫБОР
	|		КОГДА НЕ &ОтражатьВРеглУчете
	|			ТОГДА втПорядокУчетаОСБУ.СостояниеБУ
	|		КОГДА &ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|	КОНЕЦ КАК СостояниеБУ,
	|
	|	втПорядокУчетаОСБУ.СостояниеНУ КАК СостояниеНУ,
	|
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА &НачислятьАмортизациюБУ
	|		ИНАЧЕ втПорядокУчетаОСБУ.НачислятьАмортизациюБУ
	|	КОНЕЦ КАК НачислятьАмортизациюБУ,
	|
	|	втПорядокУчетаОСБУ.НачислятьАмортизациюНУ КАК НачислятьАмортизациюНУ,
	|	втПорядокУчетаОСБУ.СтатьяРасходовБУ КАК СтатьяРасходовБУ,
	|	втПорядокУчетаОСБУ.АналитикаРасходовБУ КАК АналитикаРасходовБУ,
	|	втПорядокУчетаОСБУ.СтатьяРасходовНУ КАК СтатьяРасходовНУ,
	|	втПорядокУчетаОСБУ.АналитикаРасходовНУ КАК АналитикаРасходовНУ,
	|	втПорядокУчетаОСБУ.СтатьяРасходовАмортизационнойПремии КАК СтатьяРасходовАмортизационнойПремии,
	|	втПорядокУчетаОСБУ.АналитикаРасходовАмортизационнойПремии КАК АналитикаРасходовАмортизационнойПремии,
	|	втПорядокУчетаОСБУ.АмортизационнаяГруппа                  КАК АмортизационнаяГруппа,
	|	втПорядокУчетаОСБУ.НедвижимоеИмущество                    КАК НедвижимоеИмущество,
	|	втПорядокУчетаОСБУ.НалогообложениеНДС                     КАК НалогообложениеНДС,
	|
	|	ЛОЖЬ                                    КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК ОрганизацияПолучательРасходов,
	|	&ГрафикАмортизации                      КАК ГрафикАмортизации
	|ИЗ
	|	Документ.ПринятиеКУчетуУзловКомпонентовАмортизации.КомпонентыАмортизации КАК ТаблицаКомпонентовАмортизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаОСБУ КАК втПорядокУчетаОСБУ
	|		ПО втПорядокУчетаОСБУ.ОсновноеСредство = ТаблицаКомпонентовАмортизации.ОсновноеСредство
	|ГДЕ
	|	ТаблицаКомпонентовАмортизации.Ссылка = &Ссылка
	|	И &ОтражатьВРеглУчете
	|	И &ВедетсяРегламентированныйУчетВНА";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыАмортизацииОСБУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтРасчетСтоимостиСгруппированная(Запрос, ТекстыЗапроса);
	ВнеоборотныеАктивыЛокализация.ТекстЗапросаТаблицаВтПараметрыАмортизацииОСБУ(ТекстыЗапроса, "Документ.ПринятиеКУчетуУзловКомпонентовАмортизации");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                      КАК Период,
	|	&Организация                 КАК Организация,
	|	ТаблицаКомпонентовАмортизации.КомпонентАмортизации КАК ОсновноеСредство,
	|
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА &Период
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.ДатаПоследнегоИзменения
	|	КОНЕЦ КАК ДатаПоследнегоИзменения,
	|
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА &СрокИспользованияБУ
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ
	|	КОНЕЦ КАК СрокПолезногоИспользованияБУ,
	|
	|	втПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияНУ КАК СрокПолезногоИспользованияНУ,
	|
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА &ГодоваяНормаАмортизацииБУ
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.КоэффициентАмортизацииБУ
	|	КОНЕЦ КАК КоэффициентАмортизацииБУ,
	|
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА &КоэффициентУскоренияБУ
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.КоэффициентУскорения
	|	КОНЕЦ КАК КоэффициентУскорения,
	|
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА &ГрафикАмортизации
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.ГрафикАмортизации
	|	КОНЕЦ КАК ГрафикАмортизации,
	|
	|	втПараметрыАмортизацииОСБУ.СпециальныйКоэффициент КАК СпециальныйКоэффициент,
	|
	|	ВЫБОР
	|		КОГДА &МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|				ИЛИ &МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПоЕНАОФНа1000кмПробега)
	|			ТОГДА ТаблицаКомпонентовАмортизации.ОбъемНаработки
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.ОбъемПродукцииРаботДляВычисленияАмортизации
	|	КОНЕЦ КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ЕСТЬNULL(РасчетСтоимости.СтоимостьПР, 0)
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.ПРДляВычисленияАмортизации
	|	КОНЕЦ КАК ПРДляВычисленияАмортизации,
	|
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА &СрокИспользованияБУ
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации
	|	КОНЕЦ КАК СрокИспользованияДляВычисленияАмортизации,
	|
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ЕСТЬNULL(РасчетСтоимости.СтоимостьРегл, ТаблицаКомпонентовАмортизации.СтоимостьБУ)
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.СтоимостьДляВычисленияАмортизации
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизации,
	|
	|	0 КАК СтоимостьДляВычисленияАмортизацииЦФ,
	|
	|	ВЫБОР
	|		КОГДА &ОтражатьВРеглУчете
	|			ТОГДА ТаблицаКомпонентовАмортизации.ЛиквидационнаяСтоимостьРегл
	|		ИНАЧЕ втПараметрыАмортизацииОСБУ.ЛиквидационнаяСтоимость
	|	КОНЕЦ КАК ЛиквидационнаяСтоимость
	|
	|ИЗ
	|	Документ.ПринятиеКУчетуУзловКомпонентовАмортизации.КомпонентыАмортизации КАК ТаблицаКомпонентовАмортизации
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПараметрыАмортизацииОСБУ КАК втПараметрыАмортизацииОСБУ
	|		ПО втПараметрыАмортизацииОСБУ.ОсновноеСредство = ТаблицаКомпонентовАмортизации.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтРасчетСтоимостиСгруппированная КАК РасчетСтоимости
	|		ПО РасчетСтоимости.ОбъектУчета = ТаблицаКомпонентовАмортизации.КомпонентАмортизации
	|ГДЕ
	|	ТаблицаКомпонентовАмортизации.Ссылка = &Ссылка
	|	И &ОтражатьВРеглУчете
	|	И &ВедетсяРегламентированныйУчетВНА";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаСобытияОСОрганизаций(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СобытияОСОрганизаций";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                     КАК Период,
	|	&Организация                КАК Организация,
	|	&СобытиеОС                  КАК Событие,
	|	ТаблицаКомпонентовАмортизации.КомпонентАмортизации КАК ОсновноеСредство,
	|	&НазваниеДокумента          КАК НазваниеДокумента,
	|	&Номер                      КАК НомерДокумента
	|ИЗ
	|	Документ.ПринятиеКУчетуУзловКомпонентовАмортизации.КомпонентыАмортизации КАК ТаблицаКомпонентовАмортизации
	|ГДЕ
	|	ТаблицаКомпонентовАмортизации.Ссылка = &Ссылка
	|	И (&ОтражатьВБУ
	|			И &ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы))
	|	И &ВедетсяРегламентированныйУчетВНА";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Ссылка КАК Документ,
	|	&Период КАК Период,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете) КАК Статус";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

//-- Локализация

Функция ТекстЗапросаТаблицаВтРасчетСтоимостиСгруппированная(Запрос, ТекстыЗапроса) Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	
	ИмяТаблицы = "ВтРасчетСтоимостиСгруппированная";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПустаяТаблицаСтоимости(ТекстыЗапроса, Запрос);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РасчетСтоимости.ОбъектУчета               КАК ОбъектУчета,
	|	СУММА(РасчетСтоимости.Стоимость)          КАК Стоимость,
	|	СУММА(РасчетСтоимости.СтоимостьРегл)      КАК СтоимостьРегл,
	|	СУММА(РасчетСтоимости.СтоимостьНУ)        КАК СтоимостьНУ,
	|	СУММА(РасчетСтоимости.СтоимостьПР)        КАК СтоимостьПР,
	|	СУММА(РасчетСтоимости.СтоимостьВР)        КАК СтоимостьВР,
	|	СУММА(РасчетСтоимости.СтоимостьЦФ)        КАК СтоимостьЦФ,
	|	СУММА(РасчетСтоимости.СтоимостьНУЦФ)      КАК СтоимостьНУЦФ,
	|	СУММА(РасчетСтоимости.СтоимостьПРЦФ)      КАК СтоимостьПРЦФ,
	|	СУММА(РасчетСтоимости.СтоимостьВРЦФ)      КАК СтоимостьВРЦФ,
	|	СУММА(РасчетСтоимости.НДС)                КАК НДС,
	|	СУММА(РасчетСтоимости.НДСРегл)            КАК НДСРегл
	|ПОМЕСТИТЬ ВтРасчетСтоимостиСгруппированная 
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаКомпонентовАмортизации.КомпонентАмортизации КАК ОбъектУчета,
	|		РасчетСтоимости.Стоимость          КАК Стоимость,
	|		РасчетСтоимости.СтоимостьРегл      КАК СтоимостьРегл,
	|		РасчетСтоимости.СтоимостьНУ        КАК СтоимостьНУ,
	|		РасчетСтоимости.СтоимостьПР        КАК СтоимостьПР,
	|		РасчетСтоимости.СтоимостьВР        КАК СтоимостьВР,
	|		РасчетСтоимости.СтоимостьЦФ        КАК СтоимостьЦФ,
	|		РасчетСтоимости.СтоимостьНУЦФ      КАК СтоимостьНУЦФ,
	|		РасчетСтоимости.СтоимостьПРЦФ      КАК СтоимостьПРЦФ,
	|		РасчетСтоимости.СтоимостьВРЦФ      КАК СтоимостьВРЦФ,
	|		0                                  КАК НДС,
	|		0                                  КАК НДСРегл
	|	ИЗ
	|		ВтРасчетСтоимости КАК РасчетСтоимости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуУзловКомпонентовАмортизации.КомпонентыАмортизации КАК ТаблицаКомпонентовАмортизации
	|		ПО ТаблицаКомпонентовАмортизации.НомерСтроки = РасчетСтоимости.НомерСтроки
	|			И ТаблицаКомпонентовАмортизации.Ссылка = &Ссылка
	|	ГДЕ
	|		РасчетСтоимости.Ссылка = &Ссылка
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		СтоимостьОС.ОсновноеСредство       КАК ОбъектУчета,
	|		0                                  КАК Стоимость,
	|		0                                  КАК СтоимостьРегл,
	|		0                                  КАК СтоимостьНУ,
	|		0                                  КАК СтоимостьПР,
	|		0                                  КАК СтоимостьВР,
	|		0                                  КАК СтоимостьЦФ,
	|		0                                  КАК СтоимостьНУЦФ,
	|		0                                  КАК СтоимостьПРЦФ,
	|		0                                  КАК СтоимостьВРЦФ,
	|		СтоимостьОС.Стоимость              КАК НДС,
	|		СтоимостьОС.СтоимостьРегл          КАК НДСРегл
	|	ИЗ
	|		РегистрНакопления.СтоимостьОС КАК СтоимостьОС
	|	ГДЕ
	|		СтоимостьОС.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
	|		И СтоимостьОС.Регистратор ССЫЛКА Документ.РаспределениеНДС
	|		И &ОтражатьВРеглУчете
	|		И СтоимостьОС.ОсновноеСредство В (
	|			ВЫБРАТЬ 
	|				РасчетСтоимости.ОбъектУчета 
	|			ИЗ 
	|				ВтРасчетСтоимости КАК РасчетСтоимости
	|			ГДЕ
	|				РасчетСтоимости.Ссылка = &Ссылка)
	|
	|	) КАК РасчетСтоимости
	|СГРУППИРОВАТЬ ПО
	|	РасчетСтоимости.ОбъектУчета
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПорядокУчетаОСУУ() Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                 КАК Период,
	|	&Организация                            КАК Организация,
	|	ТаблицаКомпонентовАмортизации.КомпонентАмортизации КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК Состояние,
	|	&НачислятьАмортизациюУУ                 КАК НачислятьАмортизациюУУ,
	|	СведенияВладельца.СтатьяРасходовУУ КАК СтатьяРасходов,
	|	СведенияВладельца.АналитикаРасходовУУ КАК АналитикаРасходов,
	|	ЛОЖЬ                                    КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК ОрганизацияПолучательРасходов
	|ИЗ
	|	Документ.ПринятиеКУчетуУзловКомпонентовАмортизации.КомпонентыАмортизации КАК ТаблицаКомпонентовАмортизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаСведенийДляКомпонентовАмортизации КАК СведенияВладельца
	|		ПО ТаблицаКомпонентовАмортизации.ИдентификаторСтроки = СведенияВладельца.ИдентификаторСтроки
	|ГДЕ
	|	ТаблицаКомпонентовАмортизации.Ссылка = &Ссылка
	|	И &ОтражатьВУпрУчете";
	//-- Локализация
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаПорядокУчетаОС() Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                 КАК Период,
	|	&Организация                            КАК Организация,
	|	ТаблицаКомпонентовАмортизации.КомпонентАмортизации КАК ОсновноеСредство,
	|	СведенияВладельца.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&ГруппаФинансовогоУчета                 КАК ГруппаФинансовогоУчета,
	|	ТаблицаКомпонентовАмортизации.ПоказательНаработки КАК ПоказательНаработки,
	|	ТаблицаКомпонентовАмортизации.ОбъемНаработки КАК ОбъемНаработки,
	|	&ОтражатьВРеглУчете                     КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                      КАК ОтражатьВУпрУчете
	|ИЗ
	|	Документ.ПринятиеКУчетуУзловКомпонентовАмортизации.КомпонентыАмортизации КАК ТаблицаКомпонентовАмортизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаСведенийДляКомпонентовАмортизации КАК СведенияВладельца
	|		ПО ТаблицаКомпонентовАмортизации.ИдентификаторСтроки = СведенияВладельца.ИдентификаторСтроки
	|ГДЕ
	|	ТаблицаКомпонентовАмортизации.Ссылка = &Ссылка
	|	И (&ОтражатьВБУ
	|			И &ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|		ИЛИ &ОтражатьВУпрУчете)";
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПервоначальныеСведенияОС(Запрос, ТекстыЗапроса) Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	
	ТекстЗапросаТаблицаВтРасчетСтоимостиСгруппированная(Запрос, ТекстыЗапроса);
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПервоначальныеСведенияОС(ТекстыЗапроса, "Документ.ПринятиеКУчетуУзловКомпонентовАмортизации");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                         КАК Период,
	|	ТаблицаКомпонентовАмортизации.КомпонентАмортизации КАК ОсновноеСредство,
	|	&Организация                    КАК Организация,
	|	ПервоначальныеСведенияОСВладельца.СпособПоступления КАК СпособПоступления,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВБУ И &ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА &Период 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ
	|	КОНЕЦ                           КАК ДатаВводаВЭксплуатациюБУ,
	|
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюНУ КАК ДатаВводаВЭксплуатациюНУ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА &Период 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ
	|	КОНЕЦ                           КАК ДатаВводаВЭксплуатациюУУ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВБУ И &ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы) 
	|			ТОГДА &Ссылка 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюБУ
	|	КОНЕЦ                           КАК ДокументВводаВЭксплуатациюБУ,
	|
	|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюНУ КАК ДокументВводаВЭксплуатациюНУ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА &Ссылка 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ
	|	КОНЕЦ                           КАК ДокументВводаВЭксплуатациюУУ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВБУ 
	|			ТОГДА &МетодНачисленияАмортизацииБУ 
	|		ИНАЧЕ ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ
	|	КОНЕЦ                           КАК МетодНачисленияАмортизацииБУ,
	|
	|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииНУ КАК МетодНачисленияАмортизацииНУ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВБУ 
	|			ТОГДА &ПорядокУчетаБУ 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПорядокУчетаБУ
	|	КОНЕЦ                           КАК ПорядокУчетаБУ,
	|
	|	ПервоначальныеСведенияОС.ПорядокУчетаНУ КАК ПорядокУчетаНУ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА &ПорядокУчетаУУ 
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПорядокУчетаУУ
	|	КОНЕЦ                           КАК ПорядокУчетаУУ,
	|
	|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВБУ И &ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы) 
	|			ТОГДА ЕСТЬNULL(РасчетСтоимости.СтоимостьРегл, ТаблицаКомпонентовАмортизации.СтоимостьБУ)
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьБУ
	|	КОНЕЦ                           КАК ПервоначальнаяСтоимостьБУ,
	|
	|	ВЫБОР 
	|		КОГДА &ОтражатьВУпрУчете 
	|			ТОГДА ЕСТЬNULL(РасчетСтоимости.Стоимость, ТаблицаКомпонентовАмортизации.СтоимостьУУ)
	|		ИНАЧЕ ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьУУ
	|	КОНЕЦ                           КАК ПервоначальнаяСтоимостьУУ
	|
	|ИЗ
	|	Документ.ПринятиеКУчетуУзловКомпонентовАмортизации.КомпонентыАмортизации КАК ТаблицаКомпонентовАмортизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО ПервоначальныеСведенияОС.ОсновноеСредство = ТаблицаКомпонентовАмортизации.КомпонентАмортизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
	|				&Период,
	|				Организация = &Организация
	|					И ОсновноеСредство В
	|						(ВЫБРАТЬ
	|							Т.ОсновноеСредство
	|						ИЗ
	|							Документ.ПринятиеКУчетуУзловКомпонентовАмортизации.КомпонентыАмортизации КАК Т
	|						ГДЕ
	|							Т.Ссылка = &Ссылка)) КАК ПервоначальныеСведенияОСВладельца
	|		ПО ПервоначальныеСведенияОСВладельца.ОсновноеСредство = ТаблицаКомпонентовАмортизации.ОсновноеСредство
	|			И ПервоначальныеСведенияОСВладельца.Организация = &Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтРасчетСтоимостиСгруппированная КАК РасчетСтоимости
	|		ПО РасчетСтоимости.ОбъектУчета = ТаблицаКомпонентовАмортизации.ОсновноеСредство
	|ГДЕ
	|	ТаблицаКомпонентовАмортизации.Ссылка = &Ссылка
	|	И (&ОтражатьВБУ
	|			И &ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|		ИЛИ &ОтражатьВУпрУчете)";

	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаСтоимостьОС(ТекстыЗапроса) Экспорт

	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаСведенийДляКомпонентовАмортизации(ТекстыЗапроса, "ПринятиеКУчетуУзловКомпонентовАмортизации");
	
	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)       КАК ВидДвижения,
	|	&Период                                      КАК Период,
	|	&Организация                                 КАК Организация,
	|	СведенияВладельца.Местонахождение КАК Подразделение,
	|	&ГруппаФинансовогоУчета                      КАК ГруппаФинансовогоУчета,
	|	СведенияВладельца.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВЫБОР 
	|		КОГДА &ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)
	|				ИЛИ &ПорядокУчетаУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.АмортизацияНачислена)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуСамортизированногоОС)
	|		ИНАЧЕ &ХозяйственнаяОперация 
	|	КОНЕЦ                                        КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.ПустаяСсылка) КАК ВариантПримененияЦелевогоФинансирования,
	|	&ОтражатьВРеглУчете                          КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                           КАК ОтражатьВУпрУчете,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыАналитикиКапитализацииРасходов.ОсновныеСредства) КАК ВидАналитикиКапитализацииРасходов,
	|	ТаблицаКомпонентовАмортизации.КомпонентАмортизации КАК ОсновноеСредство,
	|	СведенияВладельца.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ТаблицаКомпонентовАмортизации.ОсновноеСредство КАК АналитикаКапитализацииРасходов,
	|
	|	ЕСТЬNULL(РасчетСтоимости.КорПодразделение, НЕОПРЕДЕЛЕНО)                КАК КорПодразделение,
	|	ЕСТЬNULL(РасчетСтоимости.КорНаправлениеДеятельности, НЕОПРЕДЕЛЕНО)      КАК КорНаправлениеДеятельности,
	|	ЕСТЬNULL(РасчетСтоимости.КорСтатьяРасходов, НЕОПРЕДЕЛЕНО)               КАК КорСтатьяРасходов,
	|	ЕСТЬNULL(РасчетСтоимости.КорАналитикаРасходов, НЕОПРЕДЕЛЕНО)            КАК КорАналитикаРасходов,
	|	НЕ РасчетСтоимости.ОбъектУчета ЕСТЬ NULL                                КАК РасчетСтоимости,
	|
	|	ВЫБОР
	|		КОГДА &ПорядокУчетаУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(РасчетСтоимости.Стоимость, ТаблицаКомпонентовАмортизации.СтоимостьУУ)
	|	КОНЕЦ КАК Стоимость,
	|
	|	ВЫБОР
	|		КОГДА НЕ &ОтражатьВБУ
	|				ИЛИ &ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(РасчетСтоимости.СтоимостьРегл, ТаблицаКомпонентовАмортизации.СтоимостьБУ)
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	|	0 КАК СтоимостьНУ,
	|
	|	ВЫБОР
	|		КОГДА НЕ &ОтражатьВБУ
	|				ИЛИ &ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(РасчетСтоимости.СтоимостьПР, 0)
	|	КОНЕЦ КАК СтоимостьПР,
	|
	|	ВЫБОР
	|		КОГДА НЕ &ОтражатьВБУ
	|				ИЛИ &ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(РасчетСтоимости.СтоимостьРегл, ТаблицаКомпонентовАмортизации.СтоимостьБУ)
	|	КОНЕЦ
	|		- ВЫБОР
	|			КОГДА НЕ &ОтражатьВБУ
	|					ИЛИ &ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|				ТОГДА 0
	|			ИНАЧЕ ЕСТЬNULL(РасчетСтоимости.СтоимостьПР, 0)
	|		КОНЕЦ КАК СтоимостьВР,
	|
	|	0 КАК СтоимостьЦФ,
	|
	|	0 КАК СтоимостьНУЦФ,
	|	0 КАК СтоимостьПРЦФ,
	|	0 КАК СтоимостьВРЦФ,
	|
	|	0 КАК АмортизационнаяПремия,
	|
	|	0 КАК ПредварительнаяСтоимость,
	|	0 КАК ПредварительнаяСтоимостьРегл,
	|
	|	ВЫБОР
	|		КОГДА &ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(РасчетСтоимости.КорПостояннаяРазница, 0)
	|	КОНЕЦ КАК КорПостояннаяРазница,
	|
	|	ВЫБОР
	|		КОГДА НЕ &ОтражатьВБУ
	|				ИЛИ &ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(РасчетСтоимости.СтоимостьРегл, ТаблицаКомпонентовАмортизации.СтоимостьБУ)
	|	КОНЕЦ 
	|		- ВЫБОР
	|			КОГДА &ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|				ТОГДА 0
	|			ИНАЧЕ ЕСТЬNULL(РасчетСтоимости.КорПостояннаяРазница, 0)
	|		КОНЕЦ КАК КорВременнаяРазница,
	|
	|	ЕСТЬNULL(РасчетСтоимости.НеУчитываемаяСтоимостьНУ, 0) КАК НеУчитываемаяСтоимостьНУ,
	|	РасчетСтоимости.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	Документ.ПринятиеКУчетуУзловКомпонентовАмортизации.КомпонентыАмортизации КАК ТаблицаКомпонентовАмортизации
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтРасчетСтоимости КАК РасчетСтоимости
	|		ПО РасчетСтоимости.ОбъектУчета = ТаблицаКомпонентовАмортизации.ОсновноеСредство
	|			И РасчетСтоимости.НомерСтроки = ТаблицаКомпонентовАмортизации.НомерСтроки
	|			И РасчетСтоимости.Ссылка = &Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаСведенийДляКомпонентовАмортизации КАК СведенияВладельца
	|		ПО ТаблицаКомпонентовАмортизации.ИдентификаторСтроки = СведенияВладельца.ИдентификаторСтроки
	|
	|ГДЕ
	|	ТаблицаКомпонентовАмортизации.Ссылка = &Ссылка
	|	И (&ОтражатьВУпрУчете
	|			И &ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|		ИЛИ &ОтражатьВБУ
	|			И &ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Нужно отдельно записать предварительную стоимость для расчета амортизации
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)       КАК ВидДвижения,
	|	&Период                                      КАК Период,
	|	&Организация                                 КАК Организация,
	|	СведенияВладельца.Местонахождение            КАК Подразделение,
	|	&ГруппаФинансовогоУчета                      КАК ГруппаФинансовогоУчета,
	|	СведенияВладельца.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|	ВЫБОР 
	|		КОГДА &ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)
	|				ИЛИ &ПорядокУчетаУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.АмортизацияНачислена)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуСамортизированногоОС)
	|		ИНАЧЕ &ХозяйственнаяОперация 
	|	КОНЕЦ                                        КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияЦелевогоФинансирования.ПустаяСсылка) КАК ВариантПримененияЦелевогоФинансирования,
	|	&ОтражатьВРеглУчете                          КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                           КАК ОтражатьВУпрУчете,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыАналитикиКапитализацииРасходов.ОсновныеСредства) КАК ВидАналитикиКапитализацииРасходов,
	|	ТаблицаКомпонентовАмортизации.КомпонентАмортизации КАК ОсновноеСредство,
	|	СведенияВладельца.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ТаблицаКомпонентовАмортизации.ОсновноеСредство КАК АналитикаКапитализацииРасходов,
	|
	|	НЕОПРЕДЕЛЕНО                                 КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО                                 КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                 КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                                 КАК КорАналитикаРасходов,
	|	ЛОЖЬ                                         КАК РасчетСтоимости,
	|
	|	0                                            КАК Стоимость,
	|	0                                            КАК СтоимостьРегл,
	|	0                                            КАК СтоимостьНУ,
	|	0                                            КАК СтоимостьПР,
	|	0                                            КАК СтоимостьВР,
	|	0                                            КАК СтоимостьЦФ,
	|	0                                            КАК СтоимостьНУЦФ,
	|	0                                            КАК СтоимостьПРЦФ,
	|	0                                            КАК СтоимостьВРЦФ,
	|	0                                            КАК АмортизационнаяПремия,
	|
	|	ТаблицаКомпонентовАмортизации.СтоимостьУУ    КАК ПредварительнаяСтоимость,
	|	ТаблицаКомпонентовАмортизации.СтоимостьБУ    КАК ПредварительнаяСтоимостьРегл,
	|
	|	0                                            КАК КорПостояннаяРазница,
	|	0                                            КАК КорВременнаяРазница,
	|	0                                            КАК НеУчитываемаяСтоимостьНУ,
	|	&ИдентификаторНеиспользуемойФинЗаписи        КАК ИдентификаторФинЗаписи,
	|	НЕОПРЕДЕЛЕНО                                 КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	Документ.ПринятиеКУчетуУзловКомпонентовАмортизации.КомпонентыАмортизации КАК ТаблицаКомпонентовАмортизации
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаСведенийДляКомпонентовАмортизации КАК СведенияВладельца
	|		ПО ТаблицаКомпонентовАмортизации.ИдентификаторСтроки = СведенияВладельца.ИдентификаторСтроки
	|ГДЕ
	|	ТаблицаКомпонентовАмортизации.Ссылка = &Ссылка
	|	И &ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|	И (ТаблицаКомпонентовАмортизации.СтоимостьБУ <> 0 И НЕ &ВедетсяРегламентированныйУчетВНА
	|		ИЛИ ТаблицаКомпонентовАмортизации.СтоимостьУУ <> 0)";
	
	//-- Локализация
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса) Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПустаяВтРасчетРасходов(ТекстыЗапроса, Запрос);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПустаяТаблицаСтоимости(ТекстыЗапроса, Запрос);

	ЗапросыРегистра = Новый Массив;
	
	ЗапросыРегистра.Добавить(РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы());
	
	#Область ПринятиеКУчету // Дт 01 - Кт 08
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                      КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)       КАК ВидДвижения,
	|	&Организация                                 КАК Организация,
	|   РасчетРасходов.Подразделение                 КАК Подразделение,
	|	РасчетРасходов.СтатьяРасходов                КАК СтатьяРасходов,
	|	РасчетРасходов.АналитикаРасходов             КАК АналитикаРасходов,
	|	РасчетРасходов.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                 КАК ВидДеятельностиНДС,
	|
	|	ВЫБОР
	|		КОГДА &ПорядокУчетаУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		ИНАЧЕ РасчетРасходов.Сумма
	|	КОНЕЦ КАК СуммаСНДС,
	|
	|	ВЫБОР
	|		КОГДА &ПорядокУчетаУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		ИНАЧЕ РасчетРасходов.СуммаБезНДС
	|	КОНЕЦ КАК СуммаБезНДС,
	|
	|	ВЫБОР
	|		КОГДА &ПорядокУчетаУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		ИНАЧЕ РасчетРасходов.СуммаУпр
	|	КОНЕЦ КАК СуммаБезНДСУпр,
	|
	|	ВЫБОР
	|		КОГДА &ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		ИНАЧЕ РасчетРасходов.СуммаРегл
	|	КОНЕЦ КАК СуммаСНДСРегл,
	|
	|	ВЫБОР
	|		КОГДА &ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		ИНАЧЕ РасчетРасходов.СуммаРегл
	|	КОНЕЦ КАК СуммаБезНДСРегл,
	|
	|	ВЫБОР
	|		КОГДА &ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		ИНАЧЕ РасчетРасходов.ПостояннаяРазница
	|	КОНЕЦ КАК ПостояннаяРазница,
	|
	|	ВЫБОР
	|		КОГДА &ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА 0
	|		ИНАЧЕ РасчетРасходов.СуммаРегл
	|	КОНЕЦ
	|		- ВЫБОР
	|			КОГДА &ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|				ТОГДА 0
	|			ИНАЧЕ РасчетРасходов.ПостояннаяРазница
	|		КОНЕЦ КАК ВременнаяРазница,
	|
	|	&ХозяйственнаяОперация                       КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                 КАК АналитикаУчетаНоменклатуры,
	|	                                                                             
	|	РасчетРасходов.ИдентификаторСтроки           КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперации              КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтРасчетРасходов КАК РасчетРасходов
	|ГДЕ
	|	(&ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|		ИЛИ &ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы))
	|	И РасчетРасходов.Ссылка = &Ссылка
	|";
	ЗапросыРегистра.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область НеПриниматьКУчетуВключитьВРасходы // Дт 91 - Кт 08
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                      КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)       КАК ВидДвижения,
	|	&Организация                                 КАК Организация,
	|   РасчетРасходов.Подразделение                 КАК Подразделение,
	|	РасчетРасходов.СтатьяРасходов                КАК СтатьяРасходов,
	|	РасчетРасходов.АналитикаРасходов             КАК АналитикаРасходов,
	|	РасчетРасходов.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                 КАК ВидДеятельностиНДС,
	|
	|	ВЫБОР
	|		КОГДА &ПорядокУчетаУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА РасчетРасходов.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаСНДС,
	|
	|	ВЫБОР
	|		КОГДА &ПорядокУчетаУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА РасчетРасходов.СуммаБезНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБезНДС,
	|
	|	ВЫБОР
	|		КОГДА &ПорядокУчетаУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА РасчетРасходов.СуммаУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБезНДСУпр,
	|
	|	ВЫБОР
	|		КОГДА &ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА РасчетРасходов.СуммаРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаСНДСРегл,
	|
	|	ВЫБОР
	|		КОГДА &ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА РасчетРасходов.СуммаРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБезНДСРегл,
	|
	|	ВЫБОР
	|		КОГДА &ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА РасчетРасходов.ПостояннаяРазница
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПостояннаяРазница,
	|
	|	ВЫБОР
	|		КОГДА &ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА РасчетРасходов.СуммаРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ
	|		- ВЫБОР
	|			КОГДА &ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|				ТОГДА РасчетРасходов.ПостояннаяРазница
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ВременнаяРазница,
	|
	|	&ХО_СписаниеНаРасходыСтоимостиОСНеПринимаяКУчету КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                 КАК АналитикаУчетаНоменклатуры,
	|	                                                                             
	|	РасчетРасходов.ИдентификаторСтроки           КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_СписаниеНаРасходыСтоимостиОСНеПринимаяКУчету КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтРасчетРасходов КАК РасчетРасходов
	|
	|ГДЕ
	|	(&ПорядокУчетаУУ = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|		ИЛИ &ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы))
	|	И РасчетРасходов.Ссылка = &Ссылка
	|";
	ЗапросыРегистра.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(ЗапросыРегистра, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти
