#Область ПрограммныйИнтерфейс

// Этап подготовки к расчету
//
// Параметры:
//	ПараметрыРасчета - Структура - параметры расчета себестоимости
//
Процедура ПодготовкаИсходныхДанныхКРасчету(ПараметрыРасчета) Экспорт
	
	РасчетСебестоимостиПодготовкаДанныхЛокализация.ПодготовкаИсходныхДанныхКРасчету(ПараметрыРасчета);
	
	ПерепровестиДокументыПоРегистрам(ПараметрыРасчета);
	
	// Исправим проблемы в движениях и в самих документах в рассчитываемом периоде.
	ИсправитьПроблемыВДвиженияхИДокументах(ПараметрыРасчета);
	
	ОчиститьДанныеУпрРегистров(ПараметрыРасчета);
	
	// Проверим корректность исходных данных для расчета.
	РасчетСебестоимостиПрикладныеАлгоритмы.ПроверитьКорректностьИсходныхДанныхДоРасчета(ПараметрыРасчета);
	
	// Обновим кэши регистров, данные которых были изменены в этой процедуре.
	РасчетСебестоимостиПрикладныеАлгоритмы.ОбновитьРасчетныеКэшиРегистров(ПараметрыРасчета, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПерепровестиДокументыПоРегистрам(ПараметрыРасчета)
	
	// Перепроведение документов выполняется в автотестах.
	Если НЕ ПараметрыРасчета.АвтоматическоеТестирование
	 ИЛИ НЕ РасчетСебестоимостиПовтИсп.ЗначенияТехнологическихПараметров().ПерепроводитьДокументыПоРегистрам Тогда
		Возврат;
	КонецЕсли;
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "ПерепровестиДокументыПоРегистрам");
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	#Область СебестоимостьТоваров_ВыручкаИСебестоимостьПродаж
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	ДД.Регистратор           КАК Ссылка,
	|	ДД.Организация           КАК Организация,
	|	1						 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК ДД
	|ГДЕ
	|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДД.Организация В (&МассивОрганизаций)
	|	И НЕ ДД.РасчетПартий
	|	И НЕ ДД.РасчетСебестоимости
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ВыручкаИСебестоимостьПродаж""	         КАК ИмяРегистра,
	|	ДД.Регистратор                           КАК Ссылка,
	|	ДД.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	2							             КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ДД
	|ГДЕ
	|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДД.АналитикаУчетаПоПартнерам.Организация В (&МассивОрганизаций)
	|	И НЕ ДД.РасчетПартий
	|	И НЕ ДД.РасчетСебестоимости
	|";
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'Перепроведение документов по регистру ""Себестоимость товаров"" при автотестировании';
											|en = 'Repost documents in the ""Inventory cost"" register upon automatic testing'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(2, НСтр("ru = 'Перепроведение документов по регистру ""Выручка и себестоимость продаж"" при автотестировании';
											|en = 'Repost documents in the ""Revenue and cost of goods sold"" register upon automatic testing'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'перепроведение документов по регистрам при автотестировании';
			|en = 'repost documents in registers upon automatic testing'", ОбщегоНазначения.КодОсновногоЯзыка()),
		Неопределено, // Дополнительные поля
		Истина // Только перепроведение
		);
		
	#КонецОбласти
	
	#Область ПрочиеРасходы
	
	Запрос.УстановитьПараметр("ТипыДокументовОСиНМА", РасчетСебестоимостиПрикладныеАлгоритмы.ТипыДокументовОСиНМА());
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Регистратор КАК Ссылка,
	|	ДД.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК ДД
	|ГДЕ
	|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДД.Организация В (&МассивОрганизаций)
	|	И НЕ ДД.РасчетПартий
	|	И НЕ ДД.РасчетСебестоимости
	|	И НЕ ТИПЗНАЧЕНИЯ(ДД.Регистратор) В (&ТипыДокументовОСиНМА)
	|";
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	ОписаниеРегистра = ПараметрыРасчета.Движения[Метаданные.РегистрыНакопления.ПрочиеРасходы.Имя];
	ОписаниеРегистра.НадоОбновитьРасчетныйКэш = Истина;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВосстановитьДвиженияДокументовПоРегиструНакопления(
		ПараметрыРасчета, 
		ВыборкаДокументов, 
		Метаданные.РегистрыНакопления.ПрочиеРасходы.Имя);
		
	#КонецОбласти
	
	#Область ПартииПрочихРасходов
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Регистратор КАК Ссылка,
	|	ДД.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК ДД
	|ГДЕ
	|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ДД.Организация В (&МассивОрганизаций)
	|	И НЕ ДД.РасчетПартий
	|	И НЕ ТИПЗНАЧЕНИЯ(ДД.Регистратор) В (&ТипыДокументовОСиНМА)
	|";
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	ОписаниеРегистра = ПараметрыРасчета.Движения[Метаданные.РегистрыНакопления.ПартииПрочихРасходов.Имя];
	ОписаниеРегистра.НадоОбновитьРасчетныйКэш = Истина;
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ВосстановитьДвиженияДокументовПоРегиструНакопления(
		ПараметрыРасчета, 
		ВыборкаДокументов, 
		Метаданные.РегистрыНакопления.ПартииПрочихРасходов.Имя);
		
	#КонецОбласти
	
КонецПроцедуры

// Механизм, аналогичный по своему назначению механизму обновления ИБ.
// Выполняется при расчете конкретного периода и исправляет документы только этого периода.
//
// Исправляет проблемы в движениях документов и в самих документах в рассчитываемом периоде.
//
// Есть ошибки, которые "портят" исходные данные для расчета,
// но для исправления их последствий не пишется обработчик обновления.
// Например, из-за какой-то старой ошибки в каких-то случаях движения документа были некорректны.
// - если для исправления этой ошибки написать обработчик обновления,
// то он изменит данные прошлых, "закрытых" периодов, из-за чего эти периоды перестанут быть "закрытыми".
// Придется повторно закрывать эти периоды, из-за чего в них может измениться себестоимость и, как следствие,
// регламентированная отчетность. Это плохо, т.к. по этим периодам отчетность уже может быть сдана.
// - с другой стороны, в тех периодах, которые пользователь будет пересчитывать, эту ошибку в движениях надо бы исправить,
// чтобы в этих пересчитанных периодах себестоимость основывалась на правильных исходных данных.
//
// Для исправления таких ошибок и предназначена данная процедура.
// По сути, она является аналогом обработчика обновления, исправляющего движения документов в периоде, указанном пользователем.
//
// Также эта процедура используется для корректировки движений периода при его перерасчете в партионном учете версии 2.2.
//
Процедура ИсправитьПроблемыВДвиженияхИДокументах(ПараметрыРасчета)
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "ИсправитьПроблемыВДвиженияхИДокументах");
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	СуществующиеВТДоЭтапа = РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьПереченьСуществующихВременныхТаблиц(Запрос);
	
	#Область ПоляПартионногоУчетаВерсии22
	
	// Проверим в первичных движениях документов корректность заполнения полей:
	// Партия, КорПартия, ТипЗаписи, аналитика учета партий, вид деятельности НДС.
	// Если поля заполнены некорректно, то, видимо, документ был проведен до перехода на партионный учет версии 2.2 -
	// надо перепровести документ по регистру, содержащему некорректные движения.
	// Если после перепроведения документа поля все равно заполнены некорректно, значит ошибка кроется
	// в процедурах формирования движений документа или в макете описания движений (для регистра себестоимости товаров).
	
	// Подготовим вспомогательные данные.
	ОписаниеЦепочек 		= РасчетСебестоимостиЗаполнениеПартий.ОписаниеЦепочекПартийТоваров(ПараметрыРасчета);
	ИспользуемыеТипыЗаписей = РасчетСебестоимостиПрикладныеАлгоритмы.ИспользуемыеТипыЗаписейВЦепочках(ОписаниеЦепочек);
	
	Запрос.УстановитьПараметр("ИспользуемыеТипыЗаписей", ИспользуемыеТипыЗаписей);
	//++ НЕ УТ
	Запрос.УстановитьПараметр("АналитическийУчетПоГруппамПродукции", ПолучитьФункциональнуюОпцию("АналитическийУчетПоГруппамПродукции"));
	//-- НЕ УТ
	
	#Область ТекстЗапроса
	
	// Подготовим запрос для формирования ВТРегистраторыСНекорректнымиДвижениями.
	ТекстЗапроса = "
	//++ НЕ УТ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор				КАК Регистратор,
	|	Т.Организация				КАК Организация,
	|	Т.ПартияПроизводства		КАК ПартияПроизводства,
	|	Т.УдалитьПартияПроизводства	КАК УдалитьПартияПроизводства
	|ПОМЕСТИТЬ ВТДвиженияПрочиеРасходыНезавершенногоПроизводства
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходыНезавершенногоПроизводства КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (ТИП(Документ.РасчетСебестоимостиТоваров),
	|									ТИП(Документ.КорректировкаРегистров))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	//-- НЕ УТ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор 			 КАК Регистратор,
	|	Т.Организация 			 КАК Организация,
	|	Т.АналитикаУчетаПартий	 КАК АналитикаУчетаПартий,
	|	Т.ВидДеятельностиНДС	 КАК ВидДеятельностиНДС
	|ПОМЕСТИТЬ ВТДвиженияПартииПрочихРасходов
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И НЕ Т.РасчетПартий
	//++ Локализация
	|	И НЕ Т.Регистратор ССЫЛКА Документ.РаспределениеНДС
	//-- Локализация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор КАК Регистратор,
	|	Т.Организация КАК Организация,
	|	Т.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|ПОМЕСТИТЬ ВозвратыТоваровНаДругойСклад
	|ИЗ
	|	ВТДвиженияСебестоимостьТоваров КАК Т
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Т.Регистратор) В (ТИП(Документ.ВозвратТоваровОтКлиента), ТИП(Документ.ВозвратТоваровМеждуОрганизациями))
	|	И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ВозвратНаДругойСклад)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.ИмяРегистра КАК ИмяРегистра,
	|	Т.Ссылка 	  КАК Ссылка,
	|	Т.Организация КАК Организация,
	|	Т.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	Т.КодОшибки   КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ (
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"" КАК ИмяРегистра,
	|		Т.Регистратор КАК Ссылка,
	|		Т.Организация КАК Организация,
	|		Т.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|		ВЫБОР
	|			КОГДА Т.Организация В (&ОрганизацииСФИФОСкользящая) И Т.Партия = НЕОПРЕДЕЛЕНО
	|				ТОГДА 4
	|			КОГДА НЕ (Т.Организация В (&ОрганизацииСФИФОСкользящая)) И Т.Партия <> НЕОПРЕДЕЛЕНО
	|			  И Т.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)
	|			  И Т.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|			  И Т.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	//++ НЕ УТ
	|			  И НЕ (Т.РазделУчета В(ЗНАЧЕНИЕ (Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку),
	|											ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение))
	|					И НЕ Т.СтатьяКалькуляции ЕСТЬ NULL)
	//-- НЕ УТ
	|				ТОГДА 5
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК КодОшибки
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		(Т.ТипЗаписи В (&ТипыЗаписейПервичныхПартий)
	|		ИЛИ Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаПриобретения)
	|		ИЛИ Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыВПути)
	|			И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности))
	|		И НЕ Т.Сторно
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		7
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		НЕ (Т.ТипЗаписи В (&ИспользуемыеТипыЗаписей))
	|		И НЕ (Т.ТипЗаписи В (&НепересчитываемыеТипыЗаписей))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		8
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК Аналитики
	|			ПО Т.АналитикаУчетаПартий = Аналитики.Ссылка
	|	ГДЕ
	|		Аналитики.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		9
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК Аналитики
	|			ПО Т.КорАналитикаУчетаПартий = Аналитики.Ссылка
	|	ГДЕ
	|		Аналитики.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""ПартииПрочихРасходов"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		8
	|	ИЗ
	|		ВТДвиженияПартииПрочихРасходов КАК Т
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК Аналитики
	|			ПО Т.АналитикаУчетаПартий = Аналитики.Ссылка
	|	ГДЕ
	|		Аналитики.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""ПартииПрочихРасходов"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		10
	|	ИЗ
	|		ВТДвиженияПартииПрочихРасходов КАК Т
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК Аналитики
	|			ПО Т.АналитикаУчетаПартий = Аналитики.Ссылка
	|	ГДЕ
	|		Т.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|
	//++ НЕ УТ
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		11
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК Аналитики
	|			ПО Т.АналитикаУчетаПартий = Аналитики.Ссылка
	|
	|	ГДЕ
	|		Т.Организация В (&ОрганизацииСФИФОСкользящая)
	|		И Т.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции),
	|									ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад),
	|									ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение))
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) В (ТИП(Документ.ПроизводствоБезЗаказа),
	//++ НЕ УТКА
	|										ТИП(Документ.ЭтапПроизводства2_2),
	//-- НЕ УТКА
	|										ТИП(Документ.ОтчетПереработчика))
	|		И ЕСТЬNULL(Аналитики.КодСтроки, 0) = 0
	//-- НЕ УТ
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		12
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Т.КорАналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Возвраты.Регистратор,
	|		Возвраты.Организация,
	|		Возвраты.ХозяйственнаяОперация,
	|		13
	|	ИЗ
	|		ВозвратыТоваровНаДругойСклад КАК Возвраты
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТДвиженияСебестоимостьТоваров КАК Т
	|			ПО Т.Регистратор = Возвраты.Регистратор
	|			И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СторноВозвратНаДругойСклад)
	|	ГДЕ
	|		Т.Регистратор ЕСТЬ NULL
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		14
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)
	|		И НЕ Т.РазделУчета В (
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи))
	|
	//++ НЕ УТ
	//++ Устарело_Производство21
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		ДД.Регистратор,
	|		ДД.Организация,
	|		ДД.ХозяйственнаяОперация,
	|		15
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК ДД
	|	ГДЕ
	|		НЕ ДД.ПоложительноеКоличество
	|		И ДД.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
	|		И ДД.Регистратор Ссылка Документ.ВыпускПродукции
	//-- Устарело_Производство21
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		ДД.Регистратор,
	|		ДД.Организация,
	|		ДД.ХозяйственнаяОперация,
	|		16
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК ДД
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО СпрНоменклатура.Ссылка = ДД.АналитикаУчетаНоменклатуры.Номенклатура
	|
	|	ГДЕ
	|		ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И ДД.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|		И (ДД.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)
	|			И (&АналитическийУчетПоГруппамПродукции
	|				И НЕ СпрНоменклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|				И ДД.КорАналитикаФинансовогоУчета = НЕОПРЕДЕЛЕНО
	|				ИЛИ ДД.КорАналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка))
	|			ИЛИ ДД.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|				И НЕ ДД.КорАналитикаФинансовогоУчета = НЕОПРЕДЕЛЕНО)
	//-- НЕ УТ
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		17
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ДополнительныеРасходыСПартией)
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыСПартией)
	|		И Т.ДокументИсточник = НЕОПРЕДЕЛЕНО
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		20
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		НЕ Т.Организация В (&ОрганизацииСФИФОСкользящая)
	|		И Т.КорАналитикаУчетаПартий = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)
	|		И Т.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И (Т.СтоимостьРегл <> 0 ИЛИ Т.НДСРегл <> 0)
	|		И Т.ТипЗаписи В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаПриобретения))
	|		И Т.РазделУчета В (
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты))
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		21
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиентаПрошлыхПериодов)
	|		И Т.ПериодПродажи = ДАТАВРЕМЯ(1,1,1)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		22
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет)
	|		И Т.КорОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	//++ НЕ УТ
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		23
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Ссылка = Т.Партия
	|
	|	ГДЕ
	|		СпрПартииПроизводства.ИспользуетсяДляПерехода
	|		И НЕ ЕСТЬNULL(Т.АналитикаУчетаНоменклатуры.СтатьяКалькуляции,
	|			ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)) =
	|				ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		23
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Ссылка = Т.КорПартия
	|
	|	ГДЕ
	|		СпрПартииПроизводства.ИспользуетсяДляПерехода
	|		И НЕ ЕСТЬNULL(Т.КорАналитикаУчетаНоменклатуры.СтатьяКалькуляции,
	|			ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)) =
	|				ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""ПрочиеРасходыНезавершенногоПроизводства"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		23
	|	ИЗ
	|		ВТДвиженияПрочиеРасходыНезавершенногоПроизводства КАК Т
	|
	|	ГДЕ
	|		ЕСТЬNULL(Т.ПартияПроизводства.ИспользуетсяДляПерехода, ЛОЖЬ)
	//-- НЕ УТ
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		24
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
	|			ПО ВыручкаИСебестоимостьПродаж.Регистратор = Т.Регистратор
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Т.Организация = ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|		И Т.АналитикаУчетаПоПартнерам = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)
	|		И Т.ТипЗаписи <> ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ВозвратНаДругойСклад)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		25
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		(Т.КорАналитикаУчетаПартий = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)
	|			ИЛИ Т.КорАналитикаУчетаПартий = Т.АналитикаУчетаПартий) // По ошибке перезаполнился реквизит КорАналитикаУчетаПартий
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) <> ТИП(Документ.КорректировкаПриобретения)
	|		И Т.ТипЗаписи В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыБезПартии),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыСПартией))
	|
	//++ НЕ УТ
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		26
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ОтчетПереработчика)
	|		И Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)
	|		И НЕ Т.ГруппаПродукции = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|	
	//++ Устарело_Производство21
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		27
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ВыпускПродукции)
	|		И Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		И Т.ГруппаПродукции <> Т.АналитикаФинансовогоУчета
	|		И (
	|			Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|			И ТИПЗНАЧЕНИЯ(Т.АналитикаФинансовогоУчета) = Тип(Справочник.ГруппыАналитическогоУчетаНоменклатуры)
	|			И Т.АналитикаФинансовогоУчета <> ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ИЛИ
	|			Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции)
	|			И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)
	|			И Т.ГруппаПродукции <> ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			)
	//-- Устарело_Производство21
	//-- НЕ УТ
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""ВыручкаИСебестоимостьПродаж"",
	|		Т.Регистратор,
	|		Аналитика.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		28
	|	ИЗ
	|		РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
	|			ПО Аналитика.Ссылка = Т.АналитикаУчетаПоПартнерам
	|	ГДЕ
	|		Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Аналитика.Организация В (&МассивОрганизаций)
	|		И Т.Активность
	|		И НЕ Т.РасчетПартий
	|		И НЕ Т.РасчетСебестоимости
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ОтчетПоКомиссииМеждуОрганизациями)
	|		И Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		30
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТаможеннаяДекларацияИмпорт КАК Декларация
	|			ПО Декларация.Ссылка = Т.Регистратор
	|	ГДЕ
	|		Т.КорВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		31
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		&УчитыватьСебестоимостьТоваровПоНазначениям
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) В (
	//++ НЕ УТ
	|			ТИП(Документ.ПроизводствоБезЗаказа),
	//-- НЕ УТ
	|			ТИП(Документ.СборкаТоваров))

	|		И ЕСТЬNULL(Т.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС, ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка))
	|			<> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|		И ЕСТЬNULL(Т.АналитикаУчетаНоменклатуры.Назначение.ВидДеятельностиНДС, ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка))
	|			<> Т.ВидДеятельностиНДС
	|	
	//++ НЕ УТ
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		32
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа КАК ПроизводствоБезЗаказа
	|			ПО ПроизводствоБезЗаказа.Ссылка = Т.Регистратор
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции)
	|		И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)
	|		И Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение)
	|		И Т.КорАналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|		И Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|
	//++ НЕ УТКА
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		33
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства
	|			ПО ЭтапПроизводства.Ссылка = Т.Регистратор
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Т.АналитикаУчетаНоменклатуры = Т.КорАналитикаУчетаНоменклатуры
	|		И Т.РазделУчета = Т.КорРазделУчета
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаВПроизводство)
	//-- НЕ УТКА
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		34
	|	ИЗ
	|		РегистрНакопления.СебестоимостьТоваров КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов КАК Реквизиты
	|			ПО Реквизиты.Ссылка = Т.Регистратор
	|	ГДЕ
	|		Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Т.Активность
	|		И Т.Организация В(&МассивОрганизаций)
	|		И НЕ Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)
	|		И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)
	|		И Т.Количество < 0
	//-- НЕ УТ
	//++ НЕ УТКА
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// Заполнение ДокументаДвижения для возвратных отходов этапов производства (баланс)
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		35
	|	ИЗ
	|		РегистрНакопления.СебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Т.Организация В(&МассивОрганизаций)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ЭтапПроизводства2_2)
	|		И НЕ Т.СтоимостьРегл = 0
	|		И Т.ДокументДвижения = НЕОПРЕДЕЛЕНО
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// Заполнение ДокументаДвижения для возвратных отходов этапов производства (забаланс)
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		35
	|	ИЗ
	|		РегистрНакопления.СебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Т.Организация В(&МассивОрганизаций)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ЭтапПроизводства2_2)
	|		И НЕ Т.СтоимостьЗабалансоваяРегл = 0
	|		И Т.ДокументДвижения = НЕОПРЕДЕЛЕНО
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	//-- НЕ УТКА
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		36
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения КАК КорректировкаПриобретения
	|			ПО КорректировкаПриобретения.Ссылка = Т.Регистратор
	|	ГДЕ
	|		Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаПриобретенияПрошлогоПериода)
	|		И Т.Стоимость <> 0
	|		ИЛИ
	|		Т.ТипЗаписи В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаПриобретения))
	|		И Т.Количество = 0
	|		ИЛИ
	|		Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаПриобретенияПрошлогоПериода)
	|		И Т.Количество = 0
	|		И Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути)
	|		И Т.НастройкаХозяйственнойОперации <> ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ЗакупкаУПоставщика)
	|	
	//++ НЕ УТ
	// После изменения флажка "Цеховая кладовая" в складе могут остаться движения с неправильным разделом учета.
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		37
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
	|		И Т.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ЦеховаяКладовая
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""ВыручкаИСебестоимостьПродаж"",
	|		Т.Регистратор,
	|		Аналитика.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		37
	|	ИЗ
	|		РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
	|			ПО Аналитика.Ссылка = Т.АналитикаУчетаПоПартнерам
	|	ГДЕ
	|		Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Аналитика.Организация В (&МассивОрганизаций)
	|		И Т.Активность
	|		И НЕ Т.РасчетПартий
	|		И НЕ Т.РасчетСебестоимости
	|		И Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
	|		И Т.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ЦеховаяКладовая
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		38
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
	|		И НЕ Т.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ЦеховаяКладовая
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""ВыручкаИСебестоимостьПродаж"",
	|		Т.Регистратор,
	|		Аналитика.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		38
	|	ИЗ
	|		РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
	|			ПО Аналитика.Ссылка = Т.АналитикаУчетаПоПартнерам
	|	ГДЕ
	|		Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Аналитика.Организация В (&МассивОрганизаций)
	|		И Т.Активность
	|		И НЕ Т.РасчетПартий
	|		И НЕ Т.РасчетСебестоимости
	|		И Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
	|		И НЕ Т.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ЦеховаяКладовая
	//-- НЕ УТ
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""ПартииПрочихРасходов"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		Т.ХозяйственнаяОперация,
	|		39
	|	ИЗ
	|		РегистрНакопления.ПартииПрочихРасходов КАК Т
	|	ГДЕ
	|		Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Т.Организация В (&МассивОрганизаций)
	|		И Т.Активность
	|		И НЕ Т.РасчетПартий
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ПрочиеДоходыРасходы)
	|		И Т.ДокументПоступленияРасходов = НЕОПРЕДЕЛЕНО
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		40
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		И Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		41
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.КорректировкаНазначенияТоваров)
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Т.АналитикаУчетаНоменклатуры = Т.КорАналитикаУчетаНоменклатуры
	|		И Т.ВидЗапасов = Т.КорВидЗапасов
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		42
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.КорСтоимость <> 0
	|		И Т.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровПоставщику),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровМеждуОрганизациями),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаТоваровСПереоценкой),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваровСПереоценкой),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияПереданнойВозвратнойТары),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноПереданнойТары),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноПереданнойТарыВозвратНаДругойСклад),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПересортицаПартийТоваров))
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		43
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПорчаТоваровСПереоценкой)
	|		И Т.РазделУчета В (
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
	|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты))
	|		И Т.СтатьяДоходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)
	|	
	//++ НЕ УТ
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		44
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК ПартииПроизводства
	|			ПО ПартииПроизводства.Ссылка = Т.Партия
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) В (
	|			ТИП(Документ.ПроизводствоБезЗаказа),
	//++ НЕ УТКА
	|			ТИП(Документ.ЭтапПроизводства2_2),
	//-- НЕ УТКА
	|			ТИП(Документ.РаспределениеПроизводственныхЗатрат))
	|		И ПартииПроизводства.ВидДеятельностиНДС	<> Т.ВидДеятельностиНДС
	//-- НЕ УТ
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		45
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи)
	|		И Т.ВидЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|
	//++ НЕ УТКА
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		46
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВидыЗапасовМатериалы КАК ТабличнаяЧасть
	|			ПО ТабличнаяЧасть.Ссылка = Т.Регистратор
	|			И ТабличнаяЧасть.ИдентификаторСтроки = Т.ИдентификаторСтроки
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ЭтапПроизводства2_2)
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|		И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Дополнение)
	//-- НЕ УТКА
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		47
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ПриобретениеТоваровУслуг)
	|		И Т.ИдентификаторСтроки = """"
	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		48
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ИсправлениеРазвернутогоСальдоТоваровОрганизаций)
	|		И Т.ВидЗапасов = Т.КорВидЗапасов
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		49
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыВПути)
	|		И Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|
	//++ НЕ УТ
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		50
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Дополнение)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ОтчетПереработчика)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		51
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.РаспределениеПроизводственныхЗатрат)
	|		И Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|		И (Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			ИЛИ Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|			И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер))
	//-- НЕ УТ
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		52
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию)
	|		И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ЕСТЬNULL(Т.КорАналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры, НЕОПРЕДЕЛЕНО)
	|			= ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		И Т.КорРазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка)
	|		И Т.КорРазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|
	//++ НЕ УТ
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		53
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа.ВидыЗапасовПобочныеИзделия КАК ВидыЗапасов
	|			ПО ВидыЗапасов.Ссылка = Т.Регистратор
	|			И ВидыЗапасов.ИдентификаторСтроки = Т.ИдентификаторСтроки
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|		И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Дополнение)
	//-- НЕ УТ
	//++ НЕ УТКА
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		54
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВидыЗапасовИзделия КАК ВидыЗапасов
	|			ПО ВидыЗапасов.Ссылка = Т.Регистратор
	|			И ВидыЗапасов.ИдентификаторСтроки = Т.ИдентификаторСтроки
	|			И ВидыЗапасов.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|		И Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Дополнение)
	//-- НЕ УТКА
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		55
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути)
	|		И Т.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		56
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноРеализации)
	|		И Т.ПериодПродажи < &НачалоПериода
	|		И Т.ПериодПродажи <> ДАТАВРЕМЯ(1,1,1)
	|
	//++ НЕ УТ
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		58
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК ПартииПроизводства
	|			ПО ПартииПроизводства.Ссылка = Т.Партия
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                           
	|		И Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства)
	|		И &АналитическийУчетПоГруппамПродукции
	|		И ПартииПроизводства.ГруппаПродукции <> ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|		И ПартииПроизводства.ГруппаПродукции <> Т.АналитикаФинансовогоУчета
	//-- НЕ УТ
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		59
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДвиженияСебестоимостьТоваров КАК ДвиженияПриход
	|			ПО ДвиженияПриход.Регистратор = Т.Регистратор
	|			И ДвиженияПриход.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			И ДвиженияПриход.АналитикаУчетаНоменклатуры = Т.КорАналитикаУчетаНоменклатуры
	|			И ДвиженияПриход.РазделУчета = Т.РазделУчета
	|			И ДвиженияПриход.ВидДеятельностиНДС = Т.КорВидДеятельностиНДС
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиентуРеглУчет)
	|		И ДвиженияПриход.Регистратор ЕСТЬ NULL
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		""СебестоимостьТоваров"",
	|		Т.Регистратор,
	|		Т.Организация,
	|		НЕОПРЕДЕЛЕНО,
	|		60
	|	ИЗ
	|		ВТДвиженияСебестоимостьТоваров КАК Т
	|	ГДЕ
	|		Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтчетКомиссионера)
	|		И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ОтчетКомиссионера)
	|		И Т.ИдентификаторСтроки = """"
	|	) КАК Т
	|
	|ГДЕ
	|	Т.КодОшибки <> 0
	|";
	
	Запрос.Текст = ТекстЗапросаВТДвиженияСебестоимостьТоваров() + ТекстЗапроса;
	
	#КонецОбласти
	
	// Сформируем описание возможных кодов ошибок в движениях.
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(4, НСтр("ru = 'Для организаций с методом оценки ""ФИФО (скользящая)"" не заполнена первичная партия';
											|en = 'Primary lot for companies with the ""FIFO (rolling valuation)"" evaluation method is not filled in'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(5, НСтр("ru = 'Для организаций с методом оценки, отличным от ""ФИФО (скользящая)"", заполнена первичная партия или кор. партия';
											|en = 'Primary lot or corr. lot is filled in for companies with evaluation method other than ""FIFO (rolling valuation)""'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(7, НСтр("ru = 'Тип записи, указанный в движении, не используется при распределении партий';
											|en = 'Record type specified in the movement is not used in lot allocation'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(8, НСтр("ru = 'Аналитика учета партий не содержит вида ценности';
											|en = 'Lot: vendor attributes do not contain an asset tax class'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(9, НСтр("ru = 'Кор. аналитика учета партий не содержит вида ценности';
											|en = 'Lot: vendor attributes (corr.) do not contain asset tax class'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(10, НСтр("ru = 'Не заполнен вид деятельности НДС при заполненной аналитике учета партий';
											|en = 'VAT activity type is not specified while lot: vendor attributes are filled in'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(11, НСтр("ru = 'Для аналитики учета партий выпуска изделий не заполнен код строки (продукции)';
											|en = 'Line (product) code is not specified for lot: vendor attributes of product release'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(12, НСтр("ru = 'Не заполнена кор. аналитика учета номенклатуры для операции ""Реализация (товары в пути)""';
											|en = 'Cor. inventory location attributes are not filled in for ""Customer invoice — Goods-in-transit""'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(13, НСтр("ru = 'Для документов возврата товаров от клиента/организации на склад, отличный от склада реализации, используются неправильные хозяйственные операции и типы записей';
											|en = 'Incorrect business transactions and record types are used for the documents for goods return from customer/company to the warehouse other than the sales warehouse'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(14, НСтр("ru = 'Не заполнен вид деятельности НДС у приходных движений с типом записи ""Партия""';
											|en = 'VAT activity type is not specified for receipt movements with the ""Lot"" record type'", ОбщегоНазначения.КодОсновногоЯзыка()));
	//++ НЕ УТ
	РасшифровкаКодовОшибок.Вставить(15, НСтр("ru = 'Для документов выпуска обнаружены отрицательные движения по возвратным отходам при производстве давальческой продукции';
											|en = 'Negative records of recyclable waste on production of material provider products are found for release documents.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(16, НСтр("ru = 'Неправильно заполнена аналитика учета выпуска возвратных отходов (кор. аналитика учета номенклатуры, кор. аналитика финансового учета).';
											|en = 'Accounting dimension of recyclable waste release is filled in incorrectly (corr. inventory location attributes, corr. controlling attributes).'", ОбщегоНазначения.КодОсновногоЯзыка()));
	//-- НЕ УТ
	РасшифровкаКодовОшибок.Вставить(17, НСтр("ru = 'Не заполнен документ - источник для операции ""Дополнительные расходы с указанием документа партии"".';
											|en = 'Source document is required for the ""Additional expenses with lot document specification"" operation.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(20, НСтр("ru = 'Не заполнена кор. аналитика учета партий приходных движений с типом записи ""Партия"" или ""Корректировка приобретения"" для организаций с методом оценки, отличным от ""ФИФО (скользящая)""';
											|en = 'Lot: vendor attributes (corr.) of receipt movements with the ""Batch"" or ""Debit note"" record type are not filled in for companies using an evaluation method different form ""FIFO (rolling valuation)""'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(21, НСтр("ru = 'Не заполнен период продажи для операции ""Возврат товаров от клиента прошлых периодов""';
											|en = 'Sale period is required for the ""Goods return from customer of previous periods"" operation'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(22, НСтр("ru = 'Не заполнены кор. реквизиты для операции ""Закупка по регл. учету""';
											|en = 'Cor. attributes are required for the ""Purchase by local accounting"" operation'", ОбщегоНазначения.КодОсновногоЯзыка()));
	//++ НЕ УТ
	РасшифровкаКодовОшибок.Вставить(23, НСтр("ru = 'В незавершенном производстве в качестве партии производства используется ссылка на служебный элемент справочника ""Партии производства""';
											|en = 'In unfinished production, a link to a service item of the ""Production lots"" catalog is used as a production lot'", ОбщегоНазначения.КодОсновногоЯзыка()));
	//-- НЕ УТ
	РасшифровкаКодовОшибок.Вставить(24, НСтр("ru = 'Не заполнена аналитика учета по партнерам в движениях документов продажи';
											|en = 'Partner accounting dimension is required for sales document register records'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(25, НСтр("ru = 'Не заполнена кор. аналитика учета партий в приходных движениях с типом записей ""Доп. расходы (не указана партия)"" и ""Доп. расходы (указана партия)""';
											|en = 'Lot: vendor attributes (corr.) are required for receipt movements with the ""Add. expenses (lot is not specified)"" and ""Add. expenses (lot is specified)"" record types'", ОбщегоНазначения.КодОсновногоЯзыка()));
	//++ НЕ УТ
	РасшифровкаКодовОшибок.Вставить(26, НСтр("ru = 'В движениях по распределению материалов в отчете переработчика заполнен реквизит ""Группа продукции"".';
											|en = '""Group products"" attribute is filled in material allocation movements in subcontractor report.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(27, НСтр("ru = 'В движениях по выпуску продукции отличается заполнение реквизитов ""Аналитика финансового учета"" и ""Группа продукции"".';
											|en = 'In product release movements, the ""Controlling attributes"" and ""Group products"" attributes are filled in differently.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	//-- НЕ УТ
	РасшифровкаКодовОшибок.Вставить(28, НСтр("ru = 'Не корректно заполнен раздел учета для операции ""Отчет по комиссии между организациями""';
											|en = 'Accounting section for the ""Commission sales report — Intercompany"" operation is filled in incorrectly'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(30, НСтр("ru = 'Не корректно заполнен кор. вид деятельности НДС для документа ""Таможенная декларация на импорт"".';
											|en = 'The cor VAT activity kind is filled incorrectly for the ""Customs declaration for import"" document.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(31, НСтр("ru = 'Не корректно заполнен вид деятельности НДС в приходном движении документа.';
											|en = 'The VAT activity type in receipt document register record is filled in incorrectly.'"));
	РасшифровкаКодовОшибок.Вставить(32, НСтр("ru = 'Не корректно заполнен тип записи в приходном движении для документа ""Производство без заказа"".';
											|en = 'The entry type in receipt movements for the document ""Backflush production"" is filled in incorrectly.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(33, НСтр("ru = 'Не корректные движения по изменению назначения работ для документа ""Этап производства"".';
											|en = 'Incorrect records to change the job assignments for the ""Production stage"" document.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(34, НСтр("ru = 'Некорректные движения по разделу учета ""Производственные затраты"".';
											|en = 'Incorrect movements on the ""Production costs"" accounting section.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	//++ НЕ УТКА
	РасшифровкаКодовОшибок.Вставить(35, НСтр("ru = 'Не заполнен реквизит ""Документ движения"" в движениях продукции по фиксированной стоимости.';
											|en = 'Attribute ""Transfer record"" is required for fixed-cost product transfer records.'"));
	//-- НЕ УТКА
	РасшифровкаКодовОшибок.Вставить(36, НСтр("ru = 'Некорректные движения документа ""Корректировка приобретения"".';
											|en = 'Invalid records in the ""Debit note"" document.'"));
	//++ НЕ УТ
	РасшифровкаКодовОшибок.Вставить(37, НСтр("ru = 'Неправильный раздел учета для движений в цеховой кладовой.';
											|en = 'Invalid accounting section for transfer in the shop floor stockrooms.'"));
	РасшифровкаКодовОшибок.Вставить(38, НСтр("ru = 'Неправильный раздел учета для движений по складу, который не является цеховой кладовой.';
											|en = 'Invalid accounting section for transfer in the warehouse which is not a shop floor stockroom.'"));
	//-- НЕ УТ
	РасшифровкаКодовОшибок.Вставить(39, НСтр("ru = 'Не заполнен документ поступления расходов в движениях документа ""Отражение прочих доходов и расходов"".';
											|en = 'The document of expenses in the document records ""Posting of non-operating income and expenses"" is not filled.'"));
	РасшифровкаКодовОшибок.Вставить(40, НСтр("ru = 'Неправильный раздел учета для товаров на хранении с правом продажи.';
											|en = 'Incorrect branch of accounting for goods at storage with sale right.'"));
	РасшифровкаКодовОшибок.Вставить(41, НСтр("ru = 'Некорректные движения документа ""Корректировка назначения товаров"" при отключенном учете себестоимости по назначениям.';
											|en = 'Incorrect records in the ""Inventory assignment adjustment"" document with disabled cost accounting by destinations.'"));
	РасшифровкаКодовОшибок.Вставить(42, НСтр("ru = 'В движениях заполнен реквизит ""Кор стоимость"".';
											|en = 'The ""Cor cost"" attribute is filled in the register records.'"));
	РасшифровкаКодовОшибок.Вставить(43, НСтр("ru = 'Не заполнена статья доходов в движениях документа ""Порча товаров"".';
											|en = 'The income item in the ""Goods damage"" document records cannot be empty.'"));
	//++ НЕ УТ
	РасшифровкаКодовОшибок.Вставить(44, НСтр("ru = 'Вид деятельности НДС в приходном движении не соответствует партии производства.';
											|en = 'VAT activity in receipt record does not match production lot.'"));
	//-- НЕ УТ
	РасшифровкаКодовОшибок.Вставить(45, НСтр("ru = 'Неправильный раздел учета для собственных товаров.';
											|en = 'Incorrect accounting section for own goods items.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(46, НСтр("ru = 'Неправильный тип записи в движениях списания неизрасходованных материалов на расходы.';
											|en = 'Incorrect record type in transactions to write off unspent materials to expenses.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(47, НСтр("ru = 'Не заполнен идентификатор строки в движениях документа ""Приобретение товаров услуг"".';
											|en = 'The line ID is missing in the ""Purchase of goods and services"" document records.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(48, НСтр("ru = 'Лишние движения документа ""Исправление развернутого сальдо товаров организаций"".';
											|en = 'Extra records of the document ""Adjustment of the expanded balance of company goods"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(49, НСтр("ru = 'Неправильный раздел учета ""Товары в пути (отгрузка без перехода права собственности)"" для комиссионных товаров.';
											|en = 'Incorrect branch of accounting ""Goods in transit (shipment without transfer of title to goods)"" for consignment goods.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	//++ НЕ УТ
	РасшифровкаКодовОшибок.Вставить(50, НСтр("ru = 'Неправильный тип записи в движениях выпуска продукции отчетом переработчика.';
											|en = 'Invalid record type in manufactured products release register records by subcontractor report.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(51, НСтр("ru = 'Не правильный раздел учета в движениях списания комиссионного товара.';
											|en = 'Incorrect branch of accounting in the consigned goods write-off records.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	//-- НЕ УТ
	РасшифровкаКодовОшибок.Вставить(52, НСтр("ru = 'Неправильный кор. раздел учета при реализации работ между организациями.';
											|en = 'Incorrect corr. accounting section on implementation of works between companies.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	//++ НЕ УТ
	РасшифровкаКодовОшибок.Вставить(53, НСтр("ru = 'Неправильная хозяйственная операция в движениях выпуска побочных изделий на расходы.';
											|en = 'Incorrect business transaction in records of the by-products release for expenses.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	//-- НЕ УТ
	//++ НЕ УТКА
	РасшифровкаКодовОшибок.Вставить(54, НСтр("ru = 'Неправильная хозяйственная операция в движениях выпуска побочных изделий на расходы.';
											|en = 'Incorrect business transaction in records of the by-products release for expenses.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	//-- НЕ УТКА
	РасшифровкаКодовОшибок.Вставить(55, НСтр("ru = 'Неправильный раздел учета для товаров в пути от поставщиков.';
											|en = 'Incorrect branch of accounting for the goods in transit from vendors.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(56, НСтр("ru = 'Неправильная хозяйственная операция для возврата от клиента товаров, проданных в прошлом периоде.';
											|en = 'Incorrect business transaction for the return of goods sold in the previous period from the customer.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	//++ НЕ УТ
	РасшифровкаКодовОшибок.Вставить(58, НСтр("ru = 'Аналитика финансового учета в приходном движении не соответствует партии производства.';
											|en = 'Controlling attributes in the receipt document register record does not correspond to the production lot.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	//-- НЕ УТ
	РасшифровкаКодовОшибок.Вставить(59, НСтр("ru = 'Не корректно заполнен кор. вид деятельности НДС в приходном движении ""Реализация (только регл. учет)"".';
											|en = 'The cor. VAT taxation in the ""Sales (only local accounting)"" receipt record is filled in incorrectly.'"));
	РасшифровкаКодовОшибок.Вставить(60, НСтр("ru = 'Не заполнен идентификатор строки в движениях документа ""Отчет комиссионера"".';
											|en = 'Не заполнен идентификатор строки в движениях документа ""Отчет комиссионера"".'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	ДополнительныеПоля = Новый Структура("ХозяйственнаяОперация", НСтр("ru = 'операция';
																		|en = 'transaction'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'переход на партионный учет версии 2.2';
			|en = 'migration to lot accounting 2.2'", ОбщегоНазначения.КодОсновногоЯзыка()),
		ДополнительныеПоля);
	
	#КонецОбласти
	
//++ НЕ УТ
	#Область СебестоимостьТоваровСуммыВОтчетеПереработчика
	
	// Исправление некорректных движений в регистре "Себестоимость товаров".
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'Упр. стоимость в движениях по регистру ""Себестоимость товаров"" не приведена к валюте упр. учета';
											|en = 'Manag. cost of records in the ""Inventory cost"" register is not reduced to the manag. accounting currency'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.УстановитьПараметр("ВалютаУпр", Константы.ВалютаУправленческогоУчета.Получить());
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров""	КАК ИмяРегистра,
	|	Т.Регистратор				КАК Ссылка,
	|	Т.Организация				КАК Организация,
	|	1							КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика КАК ОтчетыПереработчика
	|	ПО Т.Регистратор = ОтчетыПереработчика.Ссылка
	|
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ОтчетПереработчика)
	|	И НЕ &ВалютаУпр = Т.Организация.ВалютаРегламентированногоУчета
	|	И Т.Стоимость = Т.СтоимостьРегл
	|	И НЕ Т.Стоимость = 0
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'приведение стоимостей в отчете переработчика к валюте упр. учета';
			|en = 'reduce the subcontractor report costs to the man. accounting currency'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти
//-- НЕ УТ

	#Область ВыручкаИСебестоимостьПродаж
	
	// Исправление некорректных движений в регистре "Выручка и себестоимость продаж".
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'Дублирование расчетной себестоимости в первичных движениях';
											|en = 'Duplication of estimated cost in primary records'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	СуществующиеВТ = РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьПереченьСуществующихВременныхТаблиц(Запрос);
	
	Запрос.Текст = ТекстЗапросаДокументыСДублямиДвиженийПоВыручкеИСебестоимостиПродаж();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'ошибка 00-00071689';
			|en = 'error 00-00071689'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	НовыеВТ = РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьПереченьСуществующихВременныхТаблиц(Запрос, СуществующиеВТ);
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета, НовыеВТ);
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'Пропавшие первичные движения по корректировке выручки';
											|en = 'Missing primary records on revenue adjustment'"));
	РасшифровкаКодовОшибок.Вставить(2, НСтр("ru = 'При обновлении базы очистилось измерение ""Заказ клиента""';
											|en = 'The ""Sales order"" dimension was cleared when updating the infobase'"));
	
	СуществующиеВТ = РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьПереченьСуществующихВременныхТаблиц(Запрос);
	
	Запрос.Текст = ТекстЗапросаДокументыСПропавшимиДвижениямиПоВыручкеИСебестоимостиПродаж();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'исправление движений по регистру ""Выручка и себестоимость продаж""';
			|en = 'correction of records in the ""Revenue and COGS"" register'"));
	
	НовыеВТ = РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьПереченьСуществующихВременныхТаблиц(Запрос, СуществующиеВТ);
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(ПараметрыРасчета, НовыеВТ);
	
	#КонецОбласти

	#Область НекорректнаяАналитикаФинансовогоУчета
	
	// Исправление некорректной пустой аналитики финансового учета в регистре "Себестоимость товаров".
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'Пустая аналитика финансового учет в первичных движениях имеет значение отличное от НЕОПРЕДЕЛЕНО';
											|en = 'Empty dimension of financial accounting in primary records has a value different from UNDEFINED'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(2, НСтр("ru = 'Пустая кор аналитика финансового учет в первичных движениях имеет значение отличное от НЕОПРЕДЕЛЕНО';
											|en = 'Empty adjustment dimension of financial accounting in primary records has a value different from UNDEFINED'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(3, НСтр("ru = 'Пустая аналитика финансового учет в первичных движениях отличается от кор аналитики финансового учета';
											|en = 'Пустая аналитика финансового учет в первичных движениях отличается от кор аналитики финансового учета'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	1 						 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И Т.АналитикаФинансовогоУчета В
	|		(ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка),
	|		 ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка),
	|		 ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка),
	|		 ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	2 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И Т.КорАналитикаФинансовогоУчета В
	|		(ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка),
	|		 ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка),
	|		 ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка),
	|		 ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка))
	|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) В (
	|		ТИП(Документ.РеализацияТоваровУслуг),
	|		ТИП(Документ.ПеремещениеТоваров))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	3 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Движения
	|		ПО Движения.Период = Т.Период
	|		И Движения.Регистратор = Т.Период
	|		И Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И НЕ Движения.РасчетПартий
	|		И НЕ Движения.РасчетСебестоимости
	|		И Движения.КорАналитикаУчетаНоменклатуры = Т.АналитикаУчетаНоменклатуры
	|		И Движения.КорВидЗапасов = Т.ВидЗапасов
	|		И Движения.КорАналитикаФинансовогоУчета = Т.АналитикаФинансовогоУчета
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И Т.КорАналитикаФинансовогоУчета  <> НЕОПРЕДЕЛЕНО
	|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ПередачаТоваровМеждуОрганизациями)
	|	И Движения.Регистратор ЕСТЬ NULL
	|";

	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'исправление аналитики финансового учета в движениях';
			|en = 'исправление аналитики финансового учета в движениях'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти

	#Область СуммыВДвижениях
	
	// Проверка соответствия заполненности суммы упр./регл. значениям функциональных опций.
	РасшифровкаКодовОшибок = Новый Соответствие;
	
	Шаблон1 = НСтр("ru = 'Заполнение упр. сумм в движениях не соответствует значению функциональной опции ""%Парам1%""';
					|en = 'Filling of the amounts, ENT in register records does not correspond to the value of the functional option ""%Парам1%""'", ОбщегоНазначения.КодОсновногоЯзыка());
	Шаблон1 = СтрЗаменить(Шаблон1, "%Парам1%", "ВестиУправленческийУчетОрганизаций");
	РасшифровкаКодовОшибок.Вставить(1,Шаблон1 );
	
	Шаблон1 = НСтр("ru = 'Заполнение регл. сумм в движениях не соответствует значению функциональной опции ""%Парам1%""';
					|en = 'Filling of the local amounts in register records does not correspond to the value of the functional option ""%Парам1%""'", ОбщегоНазначения.КодОсновногоЯзыка());
	Шаблон1 = СтрЗаменить(Шаблон1, "%Парам1%", "ИспользоватьУчетПрочихДоходовРасходовРегл");
	РасшифровкаКодовОшибок.Вставить(2,Шаблон1 );

	РасшифровкаКодовОшибок.Вставить(3, НСтр("ru = 'Заполнение суммы без НДС в движениях не соответствует варианту распределения статьи расходов';
											|en = 'Filling of amounts excluding VAT in the records does not match the expense item distribution option'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(4, НСтр("ru = 'Движения по регистру ""Прочие доходы"" сформированы без признака ""Расчет себестоимости"".';
											|en = 'Records of the ""Income"" register are created without the ""Cost calculation"" flag.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#Область ИсключаемыеТипыДокументов
	
	Запрос.УстановитьПараметр("ТипыДокументовОСиНМА", РасчетСебестоимостиПрикладныеАлгоритмы.ТипыДокументовОСиНМА());
	
	ТипыРегистраторов = Новый Массив;
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.РаспределениеДоходовПоНаправлениямДеятельности"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.РаспределениеПрочихЗатрат"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.РасчетКурсовыхРазниц"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.РегистраторРасчетов"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.ПрочиеДоходыРасходы"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.ДвижениеПрочихАктивовПассивов"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.КорректировкаПриобретения"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.КорректировкаРегистров"));
	//++ НЕ УТ
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.НачислениеСписаниеРезервовПоСомнительнымДолгам"));
	//-- НЕ УТ
	
	РасчетСебестоимостиПодготовкаДанныхЛокализация.ДополнитьТипыРегистраторовСНестандартнойЛогикойФормированияДвижений(ТипыРегистраторов);
	
	Запрос.УстановитьПараметр("ТипыРегламентныхДокументов", ТипыРегистраторов);
	
	#КонецОбласти
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	1 						 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И (Т.ТипЗаписи В (&НепересчитываемыеТипыЗаписей)
	|		ИЛИ Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление)
	|		  И Т.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию))
	|	И (Т.Стоимость <> 0 ИЛИ Т.КорСтоимость <> 0 ИЛИ Т.ДопРасходы <> 0 ИЛИ Т.Трудозатраты <> 0
	|		ИЛИ Т.ПостатейныеПостоянныеСНДС <> 0 ИЛИ Т.ПостатейныеПеременныеСНДС <> 0
	|		ИЛИ Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию))
	|	И НЕ Т.РазделУчета В (
	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию),
	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку),
	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи))
	|	И ((&УправленческийУчетОрганизаций И (Т.СтоимостьБезНДС <> 0 ИЛИ Т.КорСтоимость <> 0
	|			ИЛИ Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию)
	|			 И Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		И Т.СтоимостьУпр = 0 И Т.ДопРасходыУпр = 0 И Т.ТрудозатратыУпр = 0
	|		И Т.ПостатейныеПостоянныеУпр = 0 И Т.ПостатейныеПеременныеУпр = 0)
	|	ИЛИ (НЕ &УправленческийУчетОрганизаций
	|		И (Т.СтоимостьУпр <> 0 ИЛИ Т.ДопРасходыУпр <> 0 ИЛИ Т.ТрудозатратыУпр <> 0
	|		ИЛИ Т.ПостатейныеПостоянныеУпр <> 0 ИЛИ Т.ПостатейныеПеременныеУпр <> 0)))
	|	И НЕ (Т.СтоимостьБезНДС = 0	И Т.ДопРасходы <> 0 И Т.ДопРасходыУпр = 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПрочиеДоходы"" 		 КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	ВЫБОР
	|		КОГДА Т.Сумма <> 0
	|			И Т.СуммаРегл = 0
	|			И &ИспользоватьУчетПрочихДоходовРасходовРегл
	|			И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ					 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ПрочиеДоходы КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютОрганизаций КАК Курсы
	|		ПО Т.Организация = Курсы.Организация
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетСебестоимости
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
	|	И ((&УправленческийУчетОрганизаций
	|			И Т.СуммаУпр = 0
	|			И Т.Сумма <> 0
	|			И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА))
	|		ИЛИ (НЕ &УправленческийУчетОрганизаций
	|			И Т.СуммаУпр <> 0)
	|		ИЛИ (Т.Сумма <> 0
	|			И Т.СуммаРегл = 0 И ВЫРАЗИТЬ(Т.Сумма * Курсы.КоэффициентВалютыРегл КАК ЧИСЛО(31,2)) <> 0
	|			И &ИспользоватьУчетПрочихДоходовРасходовРегл
	|			И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)))
	|
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПрочиеДоходы"" 		 КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	4						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ПрочиеДоходы КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетСебестоимости
	|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ВозвратТоваровОтКлиента)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПрочиеРасходы"" 		 КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	1 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
	|	И ((&УправленческийУчетОрганизаций И Т.СуммаУпр = 0 И Т.Сумма <> 0 И Т.СуммаРегл <> 0
	|			И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА))
	|		ИЛИ (НЕ &УправленческийУчетОрганизаций И Т.СуммаУпр <> 0
	|			И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПартииПрочихРасходов"" КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	1 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалютОрганизаций КАК Курсы
	|		ПО Т.Организация = Курсы.Организация
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
	|	И ((&УправленческийУчетОрганизаций И Т.Стоимость <> 0 И Т.НДСРегл <> 0 И Т.НДСУпр = 0
	|			И ВЫРАЗИТЬ(Т.НДСРегл * Курсы.КоэффициентВалютыУпр КАК ЧИСЛО(31,2)) <> 0
	|			И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА))
	|		ИЛИ (НЕ &УправленческийУчетОрганизаций И Т.НДСУпр <> 0))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ВыручкаИСебестоимостьПродаж"" КАК ИмяРегистра,
	|	Т.Регистратор 			        КАК Ссылка,
	|	Аналитика.Организация 			КАК Организация,
	|	1 						        КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
	|		ПО Аналитика.Ссылка = Т.АналитикаУчетаПоПартнерам
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Аналитика.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
	|	И ((&УправленческийУчетОрганизаций И Т.Стоимость <> 0 И Т.СтоимостьУпр = 0)
	|		ИЛИ (НЕ &УправленческийУчетОрганизаций И Т.СтоимостьУпр <> 0))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Проверка заполнения суммы без НДС в движениях.
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПрочиеРасходы"" 		 КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	3 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)
	//++ Локализация
	|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) <> ТИП(Документ.СчетФактураНалоговыйАгент)
	|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) <> ТИП(Документ.СчетФактураПолученныйНалоговыйАгент)
	//-- Локализация
	// Сумма без НДС может быть не заполнена, если в в документе указана сумма НДС равная сумме документа.
	// В этом случае сумма без НДС и сумма регл будут равны 0. 
	|	И Т.Сумма <> 0 И Т.СуммаБезНДС = 0 И Т.СуммаРегл <> 0
	|	И НЕ Т.СтатьяРасходов.ВариантРаспределенияРасходовУпр В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы))
	|	И НЕ Т.Сторно
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'некорректное заполнение сумм упр. учета';
			|en = 'amounts, ENT are filled in incorrectly'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти

//++ НЕ УТ
	#Область ВыпускПродукции
	
	// Исправление движений по регистру Выпуск продукции
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'Требуется сформировать движения по регистру ""Выпуск продукции"" для отчетов переработчиков';
											|en = 'Generate records in the ""Product release"" register for subcontractor reports'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(2, НСтр("ru = 'Требуется удалить движения по регистру ""Выпуск продукции"" для поступлений от переработчиков';
											|en = 'Delete records in the ""Product release"" register for subcontractor receipts'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ВыпускПродукции"" 	 КАК ИмяРегистра,
	|	Реквизиты.Ссылка 		 КАК Ссылка,
	|	Реквизиты.Организация 	 КАК Организация,
	|	1 						 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	Документ.ОтчетПереработчика КАК Реквизиты
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВыпускПродукции КАК Т
	|	ПО Реквизиты.Ссылка = Т.Регистратор
	|ГДЕ
	|	Реквизиты.Проведен
	|	И Реквизиты.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Реквизиты.Организация В (&МассивОрганизаций)
	|	И Т.Регистратор ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ВыпускПродукции"" 	 КАК ИмяРегистра,
	|	Т.Регистратор 			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	2 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ВыпускПродукции КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И Т.Регистратор Ссылка Документ.ПоступлениеОтПереработчика";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'некорректные движения по регистру ""Выпуск продукции""';
			|en = 'incorrect records in the ""Product release"" register'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти

	
//++ НЕ УТКА
#Область БалансоваяСтоимостьПоПродукцииСОформлениемОтчетаДавальца
	
	// Исправление движений по регистру Выпуск продукции
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'Продукция давальца, по которой необходимо отчитаться, не должна иметь плановую стоимость.';
											|en = 'Manufactured products of material provider for which it is necessary to report should not have a standard cost.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор,
	|	Т.Организация,
	|   (Т.Стоимость = 0
	|		И Т.СтоимостьБезНДС = 0
	|		И Т.СтоимостьРегл = 0
	|		И Т.СтоимостьУпр = 0) КАК ПустыеСуммы
	|ПОМЕСТИТЬ ПредварительныеРегистраторы
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДвижениеПродукцииИМатериалов КАК ТоварыДвижения
	|		ПО Т.Регистратор = ТоварыДвижения.Ссылка
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И Т.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
	|	И Т.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства),
	|								ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПродукцииИзКладовой))
	|	И НЕ Т.РасчетСебестоимости
	|	И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров""	КАК ИмяРегистра,
	|	Регистраторы.Регистратор	КАК Ссылка,
	|	Регистраторы.Организация	КАК Организация,
	|	1 						 	КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	ПредварительныеРегистраторы КАК Регистраторы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДвижениеПродукцииИМатериалов.Товары КАК ТаблицаТовары
	|		ПО Регистраторы.Регистратор = ТаблицаТовары.Ссылка
	|				
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ИзделияЭтапа
	|		ПО ТаблицаТовары.Распоряжение = ИзделияЭтапа.Ссылка
	|			И ТаблицаТовары.КодСтроки = ИзделияЭтапа.КодСтроки
	|ГДЕ
	|	Регистраторы.ПустыеСуммы
	|		И НЕ ИзделияЭтапа.ОформитьОтчетДавальцу
	|		И НЕ ТаблицаТовары.Сумма = 0
	|	ИЛИ НЕ Регистраторы.ПустыеСуммы
	|		И ИзделияЭтапа.ОформитьОтчетДавальцу
	|;
	|УНИЧТОЖИТЬ ПредварительныеРегистраторы";
			
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'некорректные движения по регистру ""Себестоимость товаров""';
			|en = 'incorrect records in the ""Inventory cost"" register'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
#КонецОбласти
//-- НЕ УТКА
	
	#Область ТрудозатратыНЗП
	
	// Подготовим запрос для формирования ВТРегистраторыСНекорректнымиДвижениями.
	// Исключаем документы "ОтражениеЗарплатыВФинансовомУчете", так как они должны быть проведены в самом конце.
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ТрудозатратыНезавершенногоПроизводства""	КАК ИмяРегистра,
	|	Т.Регистратор								КАК Ссылка,
	|	Т.Организация								КАК Организация,
	|	1											КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.ТрудозатратыНезавершенногоПроизводства КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И НЕ Т.РасчетПартий
	|	И ЕСТЬNULL(Т.ПартияПроизводства.ИспользуетсяДляПерехода, ЛОЖЬ)
	//++ Локализация
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ОтражениеЗарплатыВФинансовомУчете)
	//-- Локализация
	|";
	
	// Сформируем описание возможных кодов ошибок в движениях.
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'В незавершенном производстве в качестве партии производства используется ссылка на документ партии, а не на справочник ""Партии производства""';
											|en = 'In unfinished production, a reference to lot document is used instead of the reference to the ""Production lots"" catalog'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'переход на новую партию производства';
			|en = 'transfer to new production lot'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	// Подготовим запрос для формирования ВТРегистраторыСНекорректнымиДвижениями.
	// Выбираем только документы "ОтражениеЗарплатыВФинансовомУчете", так как все остальные уже перепроведены.
	Запрос.Текст = " 
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ТрудозатратыНезавершенногоПроизводства""	КАК ИмяРегистра,
	|	Т.Регистратор								КАК Ссылка,
	|	Т.Организация								КАК Организация,
	|	1											КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.ТрудозатратыНезавершенногоПроизводства КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И НЕ Т.РасчетПартий
	|	И ЕСТЬNULL(Т.ПартияПроизводства.ИспользуетсяДляПерехода, ЛОЖЬ)
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'некорректные движения ""Трудозатраты НЗП""';
			|en = 'incorrect records ""Labor costs of unfinished production""'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти

	#Область ДвиженияНоменклатураНоменклатура
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'Не сформированы количественные движения по регистру ""Движения Номенклатура - Номенклатура"".';
											|en = 'Quantitative register records ""Items - Items"" are not generated.'"));
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор				КАК Ссылка,
	|	Т.Организация				КАК Организация
	|ПОМЕСТИТЬ ВТДвиженияСебестоимостиСписаниеВПроизводстве
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства)
	|	И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И Т.КорАналитикаУчетаНоменклатуры <> Т.АналитикаУчетаНоменклатуры
	|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) В (
	//++ НЕ УТКА
	|		ТИП(Документ.ЭтапПроизводства2_2),
	//-- НЕ УТКА
	|		ТИП(Документ.ПроизводствоБезЗаказа))
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор КАК Ссылка
	|ПОМЕСТИТЬ ВТДвиженияНоменклатураСписаниеВПроизводстве
	|ИЗ
	|	РегистрНакопления.ДвиженияНоменклатураНоменклатура КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И НЕ Т.РасчетСебестоимости
	|	И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства)
	|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) В (
	//++ НЕ УТКА
	|		ТИП(Документ.ЭтапПроизводства2_2),
	//-- НЕ УТКА
	|		ТИП(Документ.ПроизводствоБезЗаказа))
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ДвиженияНоменклатураНоменклатура""	КАК ИмяРегистра,
	|	Себестоимость.Ссылка					КАК Ссылка,
	|	Себестоимость.Организация				КАК Организация,
	|	1 										КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	ВТДвиженияСебестоимостиСписаниеВПроизводстве КАК Себестоимость
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТДвиженияНоменклатураСписаниеВПроизводстве КАК Движения
	|		ПО Движения.Ссылка = Себестоимость.Ссылка
	|ГДЕ
	|	Движения.Ссылка ЕСТЬ NULL
	|";

	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'исправление движений документов производства по регистру ""Движения номенклатура-номенклатура""';
			|en = 'correction of production document records on the register ""Items - Items""'"));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(
		ПараметрыРасчета,
		"ВТДвиженияСебестоимостиСписаниеВПроизводстве, ВТДвиженияНоменклатураСписаниеВПроизводстве");
	
	#КонецОбласти
//-- НЕ УТ

	#Область ДвиженияНоменклатураДоходыРасходы
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'Сформированы некорректные движения по регистру ""Движения Номенклатура - Доходы/Расходы"".';
											|en = 'Invalid records were generated for the ""Items - Income/Expenses"" register.'"));
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор				КАК Ссылка,
	|	Т.Период					КАК Период,
	|	Т.Организация				КАК Организация
	|ПОМЕСТИТЬ ВТДвиженияСебестоимостиСписаниеВПроизводстве
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноСписанияНаРасходы)
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Период
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор 				КАК Ссылка,
	|	Т.Период	  				КАК Период
	|ПОМЕСТИТЬ ВТДвиженияНоменклатураСписаниеВПроизводстве
	|ИЗ
	|	РегистрНакопления.ДвиженияНоменклатураДоходыРасходы КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И НЕ Т.РасчетСебестоимости
	|	И Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноСписанияНаРасходы)
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Период
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ДвиженияНоменклатураДоходыРасходы""	КАК ИмяРегистра,
	|	Себестоимость.Ссылка					КАК Ссылка,
	|	Себестоимость.Организация				КАК Организация,
	|	1 										КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	ВТДвиженияСебестоимостиСписаниеВПроизводстве КАК Себестоимость
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТДвиженияНоменклатураСписаниеВПроизводстве КАК Движения
	|		ПО Движения.Ссылка = Себестоимость.Ссылка
	|		 И Движения.Период = Себестоимость.Период
	|ГДЕ
	|	Движения.Ссылка ЕСТЬ NULL
	|";

	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'исправление движений документов по регистру ""Движения номенклатура доходы-расходы""';
			|en = 'correction of document records on the register ""Items - Income/Expenses""'"));
	
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(
		ПараметрыРасчета,
		"ВТДвиженияСебестоимостиСписаниеВПроизводстве, ВТДвиженияНоменклатураСписаниеВПроизводстве");
	
	#КонецОбласти

	#Область НаправлениеДеятельностиВПрочихРасходах
		
	// В регистрах "Прочие расходы" и "Партии прочих расходов" направление деятельности заполняется только при включенном
	// учете затрат по направлению деятельности. 
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'Заполнено направление деятельности с выключенным учетом затрат';
											|en = 'The line of business has been filled in with expenses accounting disabled'"));
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПрочиеРасходы""		 КАК ИмяРегистра,
	|	Т.Регистратор			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	1 						 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|	И Т.НаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	И НЕ Т.НаправлениеДеятельности.УчетЗатрат
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПартииПрочихРасходов"" КАК ИмяРегистра,
	|	Т.Регистратор			 КАК Ссылка,
	|	Т.Организация 			 КАК Организация,
	|	1 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК Т
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетПартий
	|	И Т.НаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	И НЕ Т.НаправлениеДеятельности.УчетЗатрат
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыРегламентныхДокументов)
	|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (&ТипыДокументовОСиНМА)
	|";
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'некорректное заполнение направления деятельности в прочих расходах';
			|en = 'incorrect filling of the line of business in other expenses'"));
	
	#КонецОбласти

	#Область ПартнерВКлючеАналитикиУчетаНоменклатуры
	
	// Замена ключей аналитики учета номенклатуры с партнером на ключи с договором контрагента.
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'В ключе аналитики учета номенклатуры в регистре ""Себестоимость товаров"" указан партнер, а не договор контрагента.';
											|en = 'In the inventory location attribute key, in the ""Inventory cost"" register, a partner is specified instead of a counterparty contract.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(2, НСтр("ru = 'В ключе аналитики учета номенклатуры в регистре ""Закупки"" указан партнер, а не договор контрагента.';
											|en = 'In the inventory location attribute key, in the ""Purchases"" register, a partner is specified instead of a counterparty contract.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	РасшифровкаКодовОшибок.Вставить(3, НСтр("ru = 'В ключе аналитики учета номенклатуры в регистре ""Движения Номенклатура - Номенклатура"" указан партнер, а не договор контрагента.';
											|en = 'In the inventory location attribute key, in the ""Items - Items"" register, a partner is specified instead of a counterparty contract.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров""	КАК ИмяРегистра,
	|	Т.Регистратор				КАК Ссылка,
	|	Т.Организация				КАК Организация,
	|	1							КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И Т.РазделУчета В (
	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути),
	|		ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки))
	|	И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""Закупки""	  КАК ИмяРегистра,
	|	Т.Регистратор КАК Ссылка,
	|	Т.Организация КАК Организация,
	|	2			  КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.Закупки КАК Т
	|
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И Т.ТипЗапасов В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке))
	|	И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
	|	И НЕ Т.РасчетСебестоимости
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ДвиженияНоменклатураНоменклатура"" КАК ИмяРегистра,
	|	Т.Регистратор						 КАК Ссылка,
	|	Т.Организация						 КАК Организация,
	|	3									 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.ДвиженияНоменклатураНоменклатура КАК Т
	|
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В(&МассивОрганизаций)
	|	И Т.ТипЗапасов В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке))
	|	И Т.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
	|	И НЕ Т.РасчетСебестоимости
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'исправление ключей аналитики учета номенклатуры в регистрах (замена партнера на договор)';
			|en = 'correction of the inventory location attribute keys in registers (replacement of a partner for a contract)'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти
	
	#Область НетТаможеннойПошлиныВДвиженияхДокументаТаможеннаяДекларацияИмпорт
	
	// Исправление движений по регистру "Себестоимость товаров" для документов "Таможенная декларация на импорт".
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'В движения таможенной декларации на импорт по регистру ""Себестоимость товаров"" отсутствует сумма пошлины. Требуется перепровести документ по регистру ""Себестоимость товаров""';
											|en = 'Duty amount is missing in records of the customs declaration for import by the ""Inventory cost"" register. Repost the document for the ""Inventory cost"" register'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	Декларация.Ссылка 		 КАК Ссылка,
	|	Декларация.Организация 	 КАК Организация,
	|	1 						 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	Документ.ТаможеннаяДекларацияИмпорт КАК Декларация
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Движения
	|		ПО Декларация.Ссылка = Движения.Регистратор
	|		И Движения.ТипЗаписи В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыСПартией),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыБезПартии))
	|ГДЕ
	|	Декларация.Проведен
	|	И Декларация.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Декларация.Организация В (&МассивОрганизаций)
	|	И Декларация.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыТаможенныхДеклараций.ТаможенноеОформление)
	|	И (Декларация.СуммаДокумента - Декларация.ТаможенныйСбор - Декларация.ТаможенныйШтраф) <> 0
	|	И Движения.Регистратор ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ ПЕРВЫЕ 0
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	Движения.Регистратор	 КАК Ссылка,
	|	Движения.Организация 	 КАК Организация,
	|	1 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Движения
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'не отражена таможенная пошлина в движениях по регистру ""Себестоимость товаров""';
			|en = 'customs duty is not recorded in records for the ""Inventory cost"" register'", ОбщегоНазначения.КодОсновногоЯзыка()));

	
	#КонецОбласти
	
	#Область АналитикаОтгрузкиВВозвратеТоваровМеждуОрганизациями
	
	// Заполнение нового реквизита табличной части ВидыЗапасов
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'Не заполнена аналитика номенклатуры отгрузки в документе возврата товаров между организациями.';
											|en = 'Shipped products dimension is required in the ""Credit note  — Intercompany return delivery"" document.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Ссылка				КАК Ссылка,
	|	Т.Ссылка.Организация	КАК Организация,
	|	1						КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыДляПерепроведения
	|ИЗ
	|	Документ.ВозвратТоваровМеждуОрганизациями.ВидыЗапасов КАК Т
	|
	|ГДЕ
	|	Т.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Ссылка.Организация В(&МассивОрганизаций)
	|	И Т.Ссылка.Проведен
	|	И Т.СпособОпределенияСебестоимости = ЗНАЧЕНИЕ(Перечисление.СпособыОпределенияСебестоимости.ИзДокументаПередачи)
	|	И Т.АналитикаУчетаНоменклатурыОтгрузки = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументы(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'исправление ключей аналитики учета номенклатуры отгрузки в документе возврата товаров между организациями';
			|en = 'correction of shipped inventory dimension keys in the ""Credit note  — Intercompany return delivery"" document'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти

	#Область ВключениеУчетаСебестоимостиПоНазначениям
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'Включение учета себестоимости по назначениям';
											|en = 'enabling cost accounting by assignments'", ОбщегоНазначения.КодОсновногоЯзыка()));

	Запрос.УстановитьПараметр("ТипДокументаИмпорта", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ТаможеннаяДекларацияИмпорт"));
	
	Запрос.Текст = ТекстЗапросаВключениеУчетаСебестоимостиПоНазначениям();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'включение учета себестоимости по назначениям';
			|en = 'enabling cost accounting by assignments'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
	Запрос.Текст = ТекстЗапросаВключениеУчетаВыручкиИСебестоимостиПродажПоНазначениям();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'включение учета выручки и себестоимости продаж по назначениям';
			|en = 'enabling accounting of revenue and COGS by assignments'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти
	
	#Область ВыключениеУчетаСебестоимостиПоНазначениям
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'Выключение учета себестоимости по назначениям';
											|en = 'disabling cost accounting by assignments'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.Текст = ТекстЗапросаВыключениеУчетаСебестоимостиПоНазначениям();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'выключение учета себестоимости по назначениям';
			|en = 'disabling cost accounting by assignments'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
	Запрос.Текст = ТекстЗапросаВыключениеУчетаВыручкиИСебестоимостиПродажПоНазначениям();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'выключение учета выручки и себестоимости продаж по назначениям';
			|en = 'disabling accounting of revenue and COGS by assignments'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
	#КонецОбласти
		
	#Область ВключениеУчетаСебестоимостиПоВидамЗапасов
	
	РасшифровкаКодовОшибок = Новый Соответствие;	
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'Включение учета себестоимости по видам запасов';
											|en = 'enabling cost accounting by inventory owner attributes'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.Текст = ТекстЗапросаВключениеУчетаСебестоимостиПоВидамЗапасов();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'включение учета себестоимости по видам запасов';
			|en = 'enabling cost accounting by inventory owner attributes'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
	Запрос.Текст = ТекстЗапросаВключениеУчетаВыручкиИСебестоимостиПродажПоВидамЗапасов();
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'включение учета выручки и себестоимости продаж по видам запасов';
			|en = 'enabling accounting of revenue and COGS by inventory owner attributes'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти
	
//++ НЕ УТКА
	#Область СоответствиеТоваровОрганизацийИСебестоимости
	
	// Исправление движений по регистру Выпуск продукции
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'Движения этапа производства по регистру ""Себестоимость товаров"" не соответствуют регистру ""Товары организаций"". Требуется перепровести документ по регистру ""Себестоимость товаров""';
											|en = 'Movements of production stage in the ""Inventory cost"" register do not correspond to the ""Company goods"" register. Repost the document for the ""Inventory cost"" register'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТоварыОрганизаций.Регистратор КАК Регистратор,
	|	(ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|			ТОГДА ТоварыОрганизаций.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ АналитикаБезНазначения.КлючАналитики
	|	КОНЕЦ) КАК АналитикаУчетаНоменклатуры,
	|	ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасов,
	|	ТоварыОрганизаций.Количество КАК Количество,
	|	ТоварыОрганизаций.Организация КАК Организация
	|ПОМЕСТИТЬ ВтТоварыОрганизацийИСебестоимость
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|	ПО АналитикаБезНазначения.Номенклатура = ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Номенклатура
	|	И АналитикаБезНазначения.Характеристика = ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Характеристика
	|	И АналитикаБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	И АналитикаБезНазначения.Серия = ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Серия
	|	И АналитикаБезНазначения.МестоХранения = ТоварыОрганизаций.АналитикаУчетаНоменклатуры.МестоХранения
	|	И АналитикаБезНазначения.СтатьяКалькуляции = ТоварыОрганизаций.АналитикаУчетаНоменклатуры.СтатьяКалькуляции
	|
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ТоварыОрганизаций.Регистратор) = ТИП(Документ.ЭтапПроизводства2_2)
	|	И ТоварыОрганизаций.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ТоварыОрганизаций.Организация В (&МассивОрганизаций)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СебестоимостьТоваров.Регистратор,
	|	СебестоимостьТоваров.АналитикаУчетаНоменклатуры,
	|	СебестоимостьТоваров.ВидЗапасов КАК ВидЗапасов,
	|	-СебестоимостьТоваров.Количество,
	|	СебестоимостьТоваров.Организация
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ВыходныеИзделия
	|	ПО СебестоимостьТоваров.Регистратор = ВыходныеИзделия.Ссылка
	|	И СебестоимостьТоваров.ИдентификаторСтроки = ВыходныеИзделия.ИдентификаторСтроки
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ПобочныеИзделия КАК ПобочныеИзделия
	|	ПО СебестоимостьТоваров.Регистратор = ПобочныеИзделия.Ссылка
	|	И СебестоимостьТоваров.ИдентификаторСтроки = ПобочныеИзделия.ИдентификаторСтроки
	|
	|ГДЕ
	// Исключаем движения в незавершенном производстве.
	|	ТИПЗНАЧЕНИЯ(СебестоимостьТоваров.Регистратор) = ТИП(Документ.ЭтапПроизводства2_2)
	|	И СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И СебестоимостьТоваров.Организация В (&МассивОрганизаций)
	|	И СебестоимостьТоваров.АналитикаУчетаНоменклатуры.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
	|	И НЕ СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	// Исключаем движения к оформлению отчетов давальцу.
	|	И НЕ (
	|		СебестоимостьТоваров.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)
	|		И СебестоимостьТоваров.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость))
	|		И СебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		И НЕ СебестоимостьТоваров.КорАналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка))
	// Исключаем товары, выкупленные у комитента.
	| 	И НЕ (
	|		СебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|		И СебестоимостьТоваров.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер))
	// Исключаем движения по себестоимости для продукции, списываемой на расходы, т.к. аналогичных движений в товарах организаций нет.
	|	И НЕ ЕСТЬNULL(ВыходныеИзделия.СписатьНаРасходы, ЛОЖЬ)
	|	И НЕ ЕСТЬNULL(ПобочныеИзделия.СписатьНаРасходы, ЛОЖЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	ВтДвижения.Регистратор КАК Ссылка,
	|	ВтДвижения.Организация КАК Организация,
	|	1 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	ВтТоварыОрганизацийИСебестоимость КАК ВтДвижения
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтДвижения.Регистратор,
	|	ВтДвижения.АналитикаУчетаНоменклатуры,
	|	ВтДвижения.ВидЗапасов,
	|	ВтДвижения.Организация
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВтДвижения.Количество) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВтТоварыОрганизацийИСебестоимость
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'некорректные движения по регистру ""Себестоимость товаров""';
			|en = 'incorrect records in the ""Inventory cost"" register'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти
//-- НЕ УТКА
	
	#Область КорректировкаПриобретения
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'Неправильный вид движения для документа ""Корректировка приобретения"".';
											|en = 'Invalid record type for the ""Debit note"" document.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	// Условия проверок соответствуют функции ТекстЗапросаВтВозвратыПоставщикам() в модуле менеджера документа "КорректировкаПриобретения".
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.Ссылка КАК Ссылка,
	|	ТаблицаРасхождения.Ссылка.Дата КАК Дата,
	|	ТаблицаРасхождения.Ссылка.Организация КАК Организация,
	|	ТаблицаРасхождения.Ссылка.ДокументОснование КАК ДокументОснование,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры,
	|	(ВЫБОР
	|		КОГДА НЕ &УчитыватьСебестоимостьТоваровПоНазначениям ТОГДА АналитикаБезНазначения.КлючАналитики
	|		ИНАЧЕ ТаблицаРасхождения.АналитикаУчетаНоменклатуры КОНЕЦ) КАК АналитикаУчетаСебестоимости,
	|	ТаблицаРасхождения.ВидЗапасов,
	|	(ВЫБОР
	|		КОГДА ТаблицаРасхождения.Ссылка.Организация В (&ОрганизацииСФИФОСкользящая)
	|			ТОГДА Приобретение.Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ) КАК Партия,
	|	СУММА(ТаблицаРасхождения.Количество) КАК Количество
	|ПОМЕСТИТЬ ВтКорректировкиПриобретения
	|ИЗ
	|	Документ.КорректировкаПриобретения.Расхождения КАК ТаблицаРасхождения
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК Приобретение
	|		ПО Приобретение.Ссылка = ТаблицаРасхождения.Ссылка.ДокументОснование
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО Аналитика.Ссылка = ТаблицаРасхождения.АналитикаУчетаНоменклатуры
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|		ПО АналитикаБезНазначения.Номенклатура = Аналитика.Номенклатура
	|		И АналитикаБезНазначения.Характеристика = Аналитика.Характеристика
	|		И АналитикаБезНазначения.Серия = Аналитика.Серия
	|		И АналитикаБезНазначения.МестоХранения = Аналитика.МестоХранения
	|		И АналитикаБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	//++ НЕ УТ
	|		И АналитикаБезНазначения.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
	//-- НЕ УТ
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка.Проведен
	|	И ТаблицаРасхождения.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ТаблицаРасхождения.Ссылка.Организация В (&МассивОрганизаций)
	|	И (Приобретение.Дата < &НачалоПериода
	|		ИЛИ Приобретение.Дата > &КонецПериода
	|		ИЛИ НЕ Приобретение.Проведен)
	|	И ТаблицаРасхождения.ВариантОтражения В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУменьшитьСкладскиеОстатки),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУчестьПриИнвентаризации))
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРасхождения.Ссылка,
	|	ТаблицаРасхождения.Ссылка.Организация,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры,
	|	(ВЫБОР
	|		КОГДА НЕ &УчитыватьСебестоимостьТоваровПоНазначениям ТОГДА АналитикаБезНазначения.КлючАналитики
	|		ИНАЧЕ ТаблицаРасхождения.АналитикаУчетаНоменклатуры КОНЕЦ),
	|	(ВЫБОР
	|		КОГДА ТаблицаРасхождения.Ссылка.Организация В (&ОрганизацииСФИФОСкользящая)
	|			ТОГДА Приобретение.Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ),
	|	ТаблицаРасхождения.ВидЗапасов
	|;
	|ВЫБРАТЬ
	|	ОстаткиТоваров.АналитикаУчетаНоменклатуры,
	|	ОстаткиТоваров.ВидЗапасов,
	|	ОстаткиТоваров.КоличествоОстаток КАК Количество
	|ПОМЕСТИТЬ ВтНеНулевыеОстаткиТоваров
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.Остатки(&ГраницаНачалоПериода,
	|		НЕ Организация В (&ОрганизацииСФИФОСкользящая)
	|		И (АналитикаУчетаНоменклатуры, Организация, ВидЗапасов) В (
	|			ВЫБРАТЬ
	|				Корректировки.АналитикаУчетаНоменклатуры,
	|				Корректировки.Организация,
	|               Корректировки.ВидЗапасов
	|			ИЗ
	|				ВтКорректировкиПриобретения КАК Корректировки
	|		)
	|	) КАК ОстаткиТоваров
	|;
	|ВЫБРАТЬ
	|	ОстаткиТоваров.АналитикаУчетаНоменклатуры,
	|	ОстаткиТоваров.ВидЗапасов,
	|	ОстаткиТоваров.Партия,
	|	ОстаткиТоваров.КоличествоОстаток КАК Количество
	|ПОМЕСТИТЬ ВтНеНулевыеОстаткиСебестоимости
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров.Остатки(&ГраницаНачалоПериода,
	|		Организация В (&ОрганизацииСФИФОСкользящая)
	|		И (АналитикаУчетаНоменклатуры, ВидЗапасов, Организация, Партия) В (
	|			ВЫБРАТЬ
	|				Корректировки.АналитикаУчетаСебестоимости,
	|               Корректировки.ВидЗапасов,
	|				Корректировки.Организация,
	|				Корректировки.Партия
	|			ИЗ
	|				ВтКорректировкиПриобретения КАК Корректировки
	|		)
	|	) КАК ОстаткиТоваров
	|;
	|ВЫБРАТЬ
	|	Движения.АналитикаУчетаНоменклатуры,
	|	Движения.ВидЗапасов,
	|	Корректировки.Ссылка КАК Ссылка,
	|	СУММА(Движения.Количество) КАК Количество
	|ПОМЕСТИТЬ ВтПрошлыеКорректировки
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Движения
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения КАК КорректировкаПриобретения
	|		ПО КорректировкаПриобретения.Ссылка = Движения.Регистратор
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтКорректировкиПриобретения КАК Корректировки
	|		ПО Корректировки.Организация = Движения.Организация
	|		И Корректировки.ВидЗапасов = Движения.ВидЗапасов
	|		И Корректировки.АналитикаУчетаСебестоимости = Движения.АналитикаУчетаНоменклатуры
	|		И Корректировки.ДокументОснование = КорректировкаПриобретения.ДокументОснование
	|ГДЕ
	|	Движения.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Движения.Период <= Корректировки.Дата
	|	И Движения.Регистратор <> Корректировки.Ссылка
	|	И Движения.Активность
	|	И Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|СГРУППИРОВАТЬ ПО
	|	Движения.АналитикаУчетаНоменклатуры,
	|	Движения.ВидЗапасов,
	|	Корректировки.Ссылка
	|;
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Себестоимость.Регистратор,
	|	Корректировки.АналитикаУчетаНоменклатуры,
	|	Себестоимость.ВидЗапасов
	|ПОМЕСТИТЬ ВтВозвратыПоставщикам
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Себестоимость
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтКорректировкиПриобретения КАК Корректировки
	|		ПО Корректировки.Ссылка = Себестоимость.Регистратор
	|		И Корректировки.ВидЗапасов = Себестоимость.ВидЗапасов
	|		И Корректировки.АналитикаУчетаСебестоимости = Себестоимость.АналитикаУчетаНоменклатуры
	|ГДЕ
	|	Себестоимость.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Себестоимость.Активность
	|	И Себестоимость.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И Себестоимость.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровПоставщику)
	|;
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""СебестоимостьТоваров""  КАК ИмяРегистра,
	|	Корректировки.Ссылка 	  КАК Ссылка,
	|	Корректировки.Организация КАК Организация,
	|	1 						  КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	ВтКорректировкиПриобретения КАК Корректировки
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтВозвратыПоставщикам КАК Возвраты
	|		ПО Возвраты.Регистратор = Корректировки.Ссылка
	|		И Возвраты.АналитикаУчетаНоменклатуры = Корректировки.АналитикаУчетаНоменклатуры
	|		И Возвраты.ВидЗапасов = Корректировки.ВидЗапасов
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтНеНулевыеОстаткиТоваров КАК ОстаткиТоваров
	|		ПО ОстаткиТоваров.АналитикаУчетаНоменклатуры = Корректировки.АналитикаУчетаНоменклатуры
	|		И ОстаткиТоваров.ВидЗапасов = Корректировки.ВидЗапасов
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтНеНулевыеОстаткиСебестоимости КАК ОстаткиСебестоимости
	|		ПО ОстаткиСебестоимости.АналитикаУчетаНоменклатуры = Корректировки.АналитикаУчетаСебестоимости
	|		И ОстаткиСебестоимости.ВидЗапасов = Корректировки.ВидЗапасов
	|		И ОстаткиСебестоимости.Партия = Корректировки.Партия
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтПрошлыеКорректировки КАК ПрошлыеКорректировки
	|		ПО ПрошлыеКорректировки.АналитикаУчетаНоменклатуры = Корректировки.АналитикаУчетаСебестоимости
	|		И ПрошлыеКорректировки.ВидЗапасов = Корректировки.ВидЗапасов
	|		И ПрошлыеКорректировки.Ссылка = Корректировки.Ссылка
	|ГДЕ
	|	Возвраты.Регистратор ЕСТЬ NULL
	|	И (НЕ Корректировки.Организация В (&ОрганизацииСФИФОСкользящая) И ЕСТЬNULL(ОстаткиТоваров.Количество, 0)
	|		<= (-Корректировки.Количество - ЕСТЬNULL(ПрошлыеКорректировки.Количество, 0))
	|	ИЛИ Корректировки.Организация В (&ОрганизацииСФИФОСкользящая) И ЕСТЬNULL(ОстаткиСебестоимости.Количество, 0)
	|		<= (-Корректировки.Количество - ЕСТЬNULL(ПрошлыеКорректировки.Количество, 0)))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ ПЕРВЫЕ 0
	|	""СебестоимостьТоваров"" КАК ИмяРегистра,
	|	Движения.Регистратор	 КАК Ссылка,
	|	Движения.Организация 	 КАК Организация,
	|	1 						 КАК КодОшибки
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Движения
	|";

	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'исправление движений документа ""Корректировка приобретения""';
			|en = 'corrected records in the ""Debit note"" document'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти
	
	#Область ПрочаяВыручка
	
	// Документы, у которых ранее были движения по регистру "Прочие расходы", теперь должны формировать движения по регистру "Прочая выручка":
	// - "Реализация услуг и прочих активов"
	// - "Корректировка реализации", у которой документ - основание "Реализация услуг и прочих активов"
	// - "Начисления по кредитам и депозитам"
	// - "Отражение зарплаты в финансовом учете"  
	
	РасшифровкаКодовОшибок = Новый Соответствие;
	РасшифровкаКодовОшибок.Вставить(1, НСтр("ru = 'Восстановление движений по регистру ""Прочая выручка"".';
											|en = 'Restore records through ""Revenue — Services and Assets"" register.'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Регистратор     КАК Регистратор,
	|	Т.Организация     КАК Организация
	|ПОМЕСТИТЬ ВТРегистраторыСПрочимиДоходами
	|ИЗ
	|	РегистрНакопления.ПрочиеДоходы КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК Корректировка
	|		ПО Корректировка.Ссылка = Т.Регистратор
	|ГДЕ
	|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Т.Организация В (&МассивОрганизаций)
	|	И Т.Активность
	|	И НЕ Т.РасчетСебестоимости
	|	И (ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.РеализацияУслугПрочихАктивов)
	|		ИЛИ ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.КорректировкаРеализации)
	|			И ТИПЗНАЧЕНИЯ(Корректировка.ДокументОснование) = ТИП(Документ.РеализацияУслугПрочихАктивов)
	|		ИЛИ ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.НачисленияКредитовИДепозитов)
	//++ Локализация
	//++ НЕ УТ
	|		ИЛИ ТИПЗНАЧЕНИЯ(Т.Регистратор) = ТИП(Документ.ОтражениеЗарплатыВФинансовомУчете)
	//-- НЕ УТ
	//-- Локализация
	|		)
	|;
	|ВЫБРАТЬ
	|	""ПрочаяВыручка"" КАК ИмяРегистра,
	|	Т.Регистратор     КАК Ссылка,
	|	Т.Организация     КАК Организация,
	|	1                 КАК КодОшибки
	|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
	|ИЗ
	|	ВТРегистраторыСПрочимиДоходами КАК Т
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ПрочиеДоходы""  КАК ИмяРегистра,
	|	Т.Регистратор     КАК Ссылка,
	|	Т.Организация     КАК Организация,
	|	1                 КАК КодОшибки
	|ИЗ
	|	ВТРегистраторыСПрочимиДоходами КАК Т
	|;
	|УНИЧТОЖИТЬ ВТРегистраторыСПрочимиДоходами
	|";
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПерепровестиДокументыПоОтдельнымРегистрамНакопления(
		ПараметрыРасчета,
		Запрос,
		РасшифровкаКодовОшибок,
		НСтр("ru = 'восстановление движений по регистру ""Прочая выручка""';
			|en = 'restore records through ""Revenue — Services and Assets"" register'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	#КонецОбласти
	
	РасчетСебестоимостиПодготовкаДанныхЛокализация.ИсправитьПроблемыВДвиженияхИДокументах(ПараметрыРасчета);
	
	ВТЭтапа = РасчетСебестоимостиПрикладныеАлгоритмы.ПолучитьПереченьСуществующихВременныхТаблиц(Запрос, СуществующиеВТДоЭтапа);
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(Запрос, ВТЭтапа);
	
КонецПроцедуры

// Возвращает текст запроса для формирования ВТДвиженияСебестоимостьТоваров
//
// Возвращаемое значение:
//	Строка - текст запроса
//
Функция ТекстЗапросаВТДвиженияСебестоимостьТоваров() Экспорт
	Возврат
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.ВидДвижения				 КАК ВидДвижения,
		|	Т.Регистратор 				 КАК Регистратор,
		|	Т.Организация 				 КАК Организация,
		|	Т.КорОрганизация 			 КАК КорОрганизация,
		|	Т.Партия 	  				 КАК Партия,
		|	Т.КорПартия 	  			 КАК КорПартия,
		|	Т.ХозяйственнаяОперация 	 КАК ХозяйственнаяОперация,
		|	Т.ТипЗаписи					 КАК ТипЗаписи,
		|	Т.АналитикаУчетаПартий		 КАК АналитикаУчетаПартий,
		|	Т.КорАналитикаУчетаПартий	 КАК КорАналитикаУчетаПартий,
		//++ НЕ УТ
		|	Статьи.Ссылка				 КАК СтатьяКалькуляции, 
		|	КорСтатьи.Ссылка			 КАК КорСтатьяКалькуляции, 
		//-- НЕ УТ
		|	Т.РазделУчета				 КАК РазделУчета,
		|	Т.ВидЗапасов				 КАК ВидЗапасов,
		|	Т.КорВидЗапасов				 КАК КорВидЗапасов,
		|	Т.КорРазделУчета			 КАК КорРазделУчета,
		|	Т.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
		|	Т.КорАналитикаУчетаНоменклатуры	КАК КорАналитикаУчетаНоменклатуры,
		|	Т.АналитикаФинансовогоУчета	КАК АналитикаФинансовогоУчета,
		|	Т.КорАналитикаФинансовогоУчета	КАК КорАналитикаФинансовогоУчета,
		|	Т.ВидДеятельностиНДС		 КАК ВидДеятельностиНДС,
		|	Т.КорВидДеятельностиНДС		 КАК КорВидДеятельностиНДС,
		|	Т.АналитикаУчетаПоПартнерам	 КАК АналитикаУчетаПоПартнерам,
		|	Т.ГруппаПродукции			 КАК ГруппаПродукции,
		|	Т.ДокументИсточник			 КАК ДокументИсточник,
		|	Т.ПериодПродажи				 КАК ПериодПродажи,
		|	Т.Сторно					 КАК Сторно,
		|	Т.ИдентификаторСтроки		 КАК ИдентификаторСтроки,
		|	Т.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
		|	ВЫБОР КОГДА Т.Количество > 0
		|		ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ 						 КАК ПоложительноеКоличество,
		|	Т.ДокументДвижения			 КАК ДокументДвижения,
		|	Т.КорСтоимость				 КАК КорСтоимость,
		|	Т.СтатьяДоходов				 КАК СтатьяДоходов,
		|	Т.Количество				 КАК Количество,
		|	Т.Стоимость					 КАК Стоимость,
		|	Т.СтоимостьРегл				 КАК СтоимостьРегл,
		|	Т.НДСРегл					 КАК НДСРегл,
		|	Т.СтоимостьЗабалансоваяРегл	 КАК СтоимостьЗабалансоваяРегл
		|ПОМЕСТИТЬ ВТДвиженияСебестоимостьТоваров
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК Т
		//++ НЕ УТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаНоменклатуры
		|			ПО Т.АналитикаУчетаНоменклатуры = АналитикаНоменклатуры.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтатьиКалькуляции КАК Статьи
		|			ПО АналитикаНоменклатуры.СтатьяКалькуляции = Статьи.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КорАналитикаНоменклатуры
		|			ПО Т.КорАналитикаУчетаНоменклатуры = КорАналитикаНоменклатуры.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтатьиКалькуляции КАК КорСтатьи
		|			ПО КорАналитикаНоменклатуры.СтатьяКалькуляции = КорСтатьи.Ссылка
		//-- НЕ УТ
		|ГДЕ
		|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Т.Организация В(&МассивОрганизаций)
		|	И НЕ Т.РасчетПартий
		|	И НЕ Т.РасчетСебестоимости
		|	И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (ТИП(Документ.РасчетСебестоимостиТоваров),
		//++ НЕ УТ
		|									ТИП(Документ.РасчетРезервовПодОбесценениеЗапасов),
		//-- НЕ УТ
		|									ТИП(Документ.КорректировкаРегистров))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|";
КонецФункции

// Возвращает текст запроса, позволяющего получить список документов
// с некорректными (задублированными) движениями по выручке и себестоимости продаж,
// появившимися в результате ошибки 00-00071689.
//
// Возвращаемое значение:
//	Строка - текст запроса
//
Функция ТекстЗапросаДокументыСДублямиДвиженийПоВыручкеИСебестоимостиПродаж() Экспорт
	Возврат
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ВыручкаИСебестоимостьПродаж"" КАК ИмяРегистра,
		|	Т.Регистратор 					КАК Ссылка,
		|	Аналитики.Организация			КАК Организация,
		|	1 								КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитики
		|		ПО Т.АналитикаУчетаПоПартнерам = Аналитики.Ссылка
		|ГДЕ
		|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Аналитики.Организация В(&МассивОрганизаций)
		|	И Т.Количество = 0
		|	И Т.Стоимость <> 0
		|	И НЕ Т.РасчетСебестоимости
		|	И НЕ Т.Регистратор ССЫЛКА Документ.КорректировкаРегистров
		|	И НЕ Т.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
		//++ НЕ УТ
		|	И НЕ Т.Регистратор ССЫЛКА Документ.РасчетРезервовПодОбесценениеЗапасов
		//-- НЕ УТ
		|	И НЕ Т.Регистратор ССЫЛКА Документ.КорректировкаРеализации";
КонецФункции

// Возвращает текст запроса, позволяющего получить список документов
// с пропавшими движениями по корректировке выручки и себестоимости продаж,
// появившимися в результате ошибки 00-00302463.
//
Функция ТекстЗапросаДокументыСПропавшимиДвижениямиПоВыручкеИСебестоимостиПродаж()
	Возврат
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	""ВыручкаИСебестоимостьПродаж"" 		КАК ИмяРегистра,
		|	Т.Регистратор							КАК Ссылка,
		|	Т.АналитикаУчетаПоПартнерам.Организация	КАК Организация,
		|	1 										КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ВыручкаИСебестоимостьПродаж"" КАК ИмяРегистра,
		|	Т.Ссылка 						КАК Ссылка,
		|	Т.Ссылка.Организация			КАК Организация,
		|	1 								КАК КодОшибки
		|ИЗ
		|	Документ.КорректировкаРеализации.ВидыЗапасовКорректировкаВыручки КАК Т
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Выручка
		|		ПО Т.Ссылка = Выручка.Регистратор
		|		И Т.АналитикаУчетаНоменклатуры.Номенклатура = Выручка.АналитикаУчетаНоменклатуры.Номенклатура
		|		И Т.АналитикаУчетаНоменклатуры.Характеристика = Выручка.АналитикаУчетаНоменклатуры.Характеристика
		|		И Т.АналитикаУчетаНоменклатуры.Серия = Выручка.АналитикаУчетаНоменклатуры.Серия
		|		И Выручка.Количество = 0
		|		И НЕ Выручка.РасчетСебестоимости
		|ГДЕ
		|	Т.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Т.Ссылка.Организация В(&МассивОрганизаций)
		|	И (Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|		ИЛИ (Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|			И ВЫБОР КОГДА Т.Ссылка.ДокументОснование ССЫЛКА Документ.РеализацияТоваровУслуг
		|				ТОГДА ВЫРАЗИТЬ(Т.Ссылка.ДокументОснование КАК Документ.РеализацияТоваровУслуг).ВернутьМногооборотнуюТару
		|				ИНАЧЕ ЛОЖЬ
		|			  КОНЕЦ = ЛОЖЬ)
		|		ИЛИ Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры ЕСТЬ NULL)
		|	И Выручка.Регистратор ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// При обновлении информационной базы из-за ошибки 00-00328204 в движениях очистилось измерение "Заказ клиента".
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ВыручкаИСебестоимостьПродаж"" КАК ИмяРегистра,
		|	Т.Регистратор					КАК Ссылка,
		|	Аналитика.Организация			КАК Организация,
		|	2 								КАК КодОшибки
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
		|		ПО Аналитика.Ссылка = Т.АналитикаУчетаПоПартнерам
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Себестоимость
		|		ПО Себестоимость.Регистратор = Т.Регистратор
		|		И Себестоимость.ЗаказКлиента = Т.ЗаказКлиента
		|ГДЕ
		|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Аналитика.Организация В (&МассивОрганизаций)
		|	И Т.Количество <> 0
		|	И Т.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
		|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) <> ТИП(Документ.ВводОстатковОПродажахЗаПрошлыеПериоды)
		|	И ТИПЗНАЧЕНИЯ(Т.Регистратор) <> ТИП(Документ.ВводОстатков)
		|	И Т.Активность
		|	И НЕ Т.РасчетПартий
		|	И НЕ Т.РасчетСебестоимости
		|	И Себестоимость.Регистратор ЕСТЬ NULL
		|";
КонецФункции

Процедура ОчиститьДанныеУпрРегистров(ПараметрыРасчета)
	
	РасчетСебестоимостиПротоколРасчета.НачалоЭтапаРасчета(ПараметрыРасчета, "ОчиститьДанныеУправленческихРегистров");
	
	Запрос = Новый Запрос;
	РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьСвойстваЗапроса(Запрос, ПараметрыРасчета);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Регистратор
		|ИЗ
		|	РегистрНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы КАК Т
		|ГДЕ
		|	Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Т.Организация В(&МассивОрганизаций)
		|	И Т.Регистратор ССЫЛКА Документ.РаспределениеПрочихЗатрат";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		
		ПараметрыЗаписи = РасчетСебестоимостиПрикладныеАлгоритмы.ИнициализироватьПараметрыЗаписи(
			ПараметрыРасчета.РасчетныйПериод.НачалоПериода,
			ПараметрыРасчета.ОграниченияВыборки.КоличествоЗаписейВНЗ,
			ПараметрыРасчета.НомерЗаданияДоРасчета);
	
		Попытка
			РасчетСебестоимостиПрикладныеАлгоритмы.ЗаписатьДвиженияПоРегистру(
				Выборка,
				РегистрыНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы,
				ПараметрыЗаписи);
		Исключение
			
			// Информацию об ошибке добавим в протокол расчета, в механизме закрытия месяца ошибки уже должны быть зарегистрированы.
			ТекстДляПротокола = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			РасчетСебестоимостиПротоколРасчета.ЗафиксироватьОшибкуРасчета(
				ПараметрыРасчета,
				Перечисления.ТипыОшибокПартионногоУчетаИСебестоимости.ОшибкаЗаписиДвиженийПоРегистрам,
				ТекстДляПротокола);
				
			ВызватьИсключение ТекстДляПротокола;
			
		КонецПопытки;
		
		РасчетСебестоимостиПротоколРасчета.ДополнительнаяИнформация(
			ПараметрыРасчета,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Очищено движений по регистру %1: %2';
					|en = 'Movements by %1 register cleared:%2'", ОбщегоНазначения.КодОсновногоЯзыка()),
				Метаданные.РегистрыНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы.ПолноеИмя(),
				РасчетСебестоимостиПротоколРасчета.ПредставлениеЗначения(Выборка.Количество())));
		
		
	КонецЕсли;
	
КонецПроцедуры

#Область ВключениеУчетаСебестоимостиПоВидамЗапасов

// Выбираем документы, в движениях которых не заполнен вид запасов, кроме исключений.
// Дополнительно выбираем данные из документов "Исправление развернутого сальдо товаров организаций",
// если у них не было движений по регистру "Себестоимость товаров" и отличаются виды запасов списания и оприходования.
//
// Движения, для которых не заполняется вид запасов в учете себестоимости товаров:
// - в аналитике учета номенклатуры указана номенклатура с типом номенклатуры "Работа"
// - товары, выкупленные у комитентов
//		раздел учета "Товары на складах", в аналитике учета номенклатуры тип места хранения "Партнер" или "Организация"
// - продукция, отраженная отчетами переработчика
//		раздел учета "Производственные затраты", в аналитике учета номенклатуры тип места хранения "Партнер"
// - работы к оформлению отчетов давальцу
//		движения "Приход" по разделу учета "Производственные затраты" с заполненным реквизитом "Кор. аналитика учета номенклатуры", 
//		хозяйственной операцией "Выпуск продукции", и типом записи "Партия"
// - продукция, реализованная документом "Отчет давальцу"
//		движения с хозяйственной операцией "Отчет давальцу"
//
// Дополнительные исключения для выручки и себестоимости продаж:
// - в типе запасов указано значение "Агентская услуга"
//	(в аналитике учета номенклатуры указана номенклатура с типом номенклатуры "Услуга. Выполняется партнером")
//
// Исключения для документов производства 2.1:	
// - вид запасов в движениях заполняется оффлайн: раздел учета "Производственные затраты" и тип записи "Потребление (производство)"
// - для документа "Перемещение материалов в производстве" вид запасов в движениях заполняется оффлайн
//
// Необходимо исправить движения в регистрах:
//	- Себестоимость товаров
//	- Выручка и себестоимость продаж
//
// Возвращаемое значение:
//	Строка - текст запроса
//
Функция ТекстЗапросаВключениеУчетаСебестоимостиПоВидамЗапасов() Экспорт
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""СебестоимостьТоваров""			КАК ИмяРегистра,
		|	СебестоимостьТоваров.Регистратор	КАК Ссылка,
		|	СебестоимостьТоваров.Организация	КАК Организация,
		|	1									КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|ГДЕ
		|	СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СебестоимостьТоваров.Организация В(&МассивОрганизаций)
		|	И СебестоимостьТоваров.ВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
		|	И СебестоимостьТоваров.Количество <> 0
		|	И СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		// Товары, выкупленные у комитентов, или отраженные отчетами переработчика
		|	И НЕ (
		|		СебестоимостьТоваров.РазделУчета В (
		|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
		|			ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты))
		|		И СебестоимостьТоваров.АналитикаУчетаНоменклатуры.ТипМестаХранения В (
		|			ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Организация)))
		//++ НЕ УТ
		// Возвратные отходы, отраженные отчетом переработчика.
		|	И НЕ (
		|		СебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)
		|		И СебестоимостьТоваров.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Дополнение)
		|		И СебестоимостьТоваров.ХозяйственнаяОперация В
		|			(ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции),
		|			 ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость))
		|		И СебестоимостьТоваров.Количество < 0
		|		И ТИПЗНАЧЕНИЯ(СебестоимостьТоваров.Регистратор) = ТИП(Документ.ОтчетПереработчика))
		//-- НЕ УТ
		//++ НЕ УТКА
		// Работы к оформлению отчетов давальцу
		|	И НЕ (
		|		СебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
		|		И СебестоимостьТоваров.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)
		|		И СебестоимостьТоваров.ХозяйственнаяОперация В
		|			(ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции),
		|			 ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость))
		|		И СебестоимостьТоваров.КорАналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка))
		|	И ТИПЗНАЧЕНИЯ(СебестоимостьТоваров.Регистратор) <> ТИП(Документ.ОтчетДавальцу)
		|	И НЕ (
		|		ТИПЗНАЧЕНИЯ(СебестоимостьТоваров.Регистратор) = ТИП(Документ.Сторно)
		|		И ТИПЗНАЧЕНИЯ(СебестоимостьТоваров.ДокументИсточник) = ТИП(Документ.ОтчетДавальцу))
		//-- НЕ УТКА
		//++ Устарело_Производство21
		//++ НЕ УТ
		// Для документов производства 2.1 вид запасов в движениях заполняется оффлайн:
		// раздел учета "Производственные затраты" и тип записи "Потребление (производство)"
		|	И НЕ (
		|		СебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
		|		И СебестоимостьТоваров.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ПотреблениеПроизводство))
		// Для документа "Перемещение материалов в производстве" вид запасов в движениях заполняется оффлайн.
		|	И ТИПЗНАЧЕНИЯ(СебестоимостьТоваров.Регистратор) <> ТИП(Документ.ПеремещениеМатериаловВПроизводстве)
		//-- НЕ УТ
		//-- Устарело_Производство21
		|	И НЕ СебестоимостьТоваров.РасчетСебестоимости
		|	И НЕ СебестоимостьТоваров.РасчетПартий
		|	И НЕ СебестоимостьТоваров.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
		//++ НЕ УТ
		|	И НЕ СебестоимостьТоваров.Регистратор ССЫЛКА Документ.РасчетРезервовПодОбесценениеЗапасов
		//-- НЕ УТ
		|	И НЕ СебестоимостьТоваров.Сторно
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// Дополнительно выбираем данные из документов "Исправление развернутого сальдо товаров организаций",
		// если у них не было движений по регистру "Себестоимость товаров" и отличаются виды запасов списания и оприходования.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""СебестоимостьТоваров""		КАК ИмяРегистра,
		|	Исправления.Ссылка				КАК Ссылка,
		|	Исправления.Ссылка.Организация	КАК Организация,
		|	1								КАК КодОшибки
		|ИЗ
		|	Документ.ИсправлениеРазвернутогоСальдоТоваровОрганизаций.Исправления КАК Исправления
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|		ПО СебестоимостьТоваров.Период = Исправления.Ссылка.Дата
		|		И СебестоимостьТоваров.Регистратор = Исправления.Ссылка
		|ГДЕ
		|	Исправления.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Исправления.Ссылка.Организация В(&МассивОрганизаций)
		|	И Исправления.ВидЗапасовСписания <> Исправления.ВидЗапасовОприходования
		|	И СебестоимостьТоваров.Регистратор ЕСТЬ NULL
		|";
КонецФункции

// Выбираем документы, в движениях которых не заполнен вид запасов, кроме исключений.
//
// Возвращаемое значение:
//	Строка - текст запроса
//
Функция ТекстЗапросаВключениеУчетаВыручкиИСебестоимостиПродажПоВидамЗапасов() Экспорт
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ВыручкаИСебестоимостьПродаж""			КАК ИмяРегистра,
		|	ВыручкаИСебестоимостьПродаж.Регистратор	КАК Ссылка,
		|	Аналитики.Организация					КАК Организация,
		|	1										КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитики
		|		ПО ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам = Аналитики.Ссылка
		|ГДЕ
		|	ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Аналитики.Организация В(&МассивОрганизаций)
		|	И ВыручкаИСебестоимостьПродаж.ВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
		|	И ВыручкаИСебестоимостьПродаж.Количество <> 0
		|	И НЕ ВыручкаИСебестоимостьПродаж.ТипЗапасов В (
		|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга),
		|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.АгентскаяУслуга))
		|	И ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		//++ НЕ УТКА
		|	И ТИПЗНАЧЕНИЯ(ВыручкаИСебестоимостьПродаж.Регистратор) <> ТИП(Документ.ОтчетДавальцу)	
		//-- НЕ УТКА
		|	И НЕ ВыручкаИСебестоимостьПродаж.РасчетСебестоимости
		|	И НЕ ВыручкаИСебестоимостьПродаж.РасчетПартий
		|	И НЕ ВыручкаИСебестоимостьПродаж.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
		//++ НЕ УТ
		|	И НЕ ВыручкаИСебестоимостьПродаж.Регистратор ССЫЛКА Документ.РасчетРезервовПодОбесценениеЗапасов
		//-- НЕ УТ
		|	И НЕ ВыручкаИСебестоимостьПродаж.Регистратор ССЫЛКА Документ.ВводОстатков
		|	И НЕ ВыручкаИСебестоимостьПродаж.Регистратор ССЫЛКА Документ.ВводОстатковОПродажахЗаПрошлыеПериоды
		|";
КонецФункции

#КонецОбласти

#Область ВыключениеУчетаСебестоимостиПоНазначениям

// Выбираем документы, у которых в движениях в аналитике учета номенклатуры указано назначение.
// Необходимо исправить движения в регистрах:
//	- Себестоимость товаров
//	- Выручка и себестоимость продаж
//
// Возвращаемое значение:
//	Строка - текст запроса
//
Функция ТекстЗапросаВыключениеУчетаСебестоимостиПоНазначениям() Экспорт
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""СебестоимостьТоваров""			КАК ИмяРегистра,
		|	СебестоимостьТоваров.Регистратор	КАК Ссылка,
		|	СебестоимостьТоваров.Организация	КАК Организация,
		|	1									КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|ГДЕ
		|	НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
		|	И СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СебестоимостьТоваров.Организация В(&МассивОрганизаций)
		|	И СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	И НЕ СебестоимостьТоваров.РасчетСебестоимости
		|	И НЕ СебестоимостьТоваров.РасчетПартий
		|	И НЕ СебестоимостьТоваров.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
		//++ НЕ УТ
		|	И НЕ СебестоимостьТоваров.Регистратор ССЫЛКА Документ.РасчетРезервовПодОбесценениеЗапасов
		//-- НЕ УТ
		|";
КонецФункции

// Выбираем документы, у которых в движениях в аналитике учета номенклатуры указано назначение.
//
// Возвращаемое значение:
//	Строка - текст запроса
//
Функция ТекстЗапросаВыключениеУчетаВыручкиИСебестоимостиПродажПоНазначениям() Экспорт
	Возврат "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ВыручкаИСебестоимостьПродаж""			КАК ИмяРегистра,
		|	ВыручкаИСебестоимостьПродаж.Регистратор	КАК Ссылка,
		|	Аналитики.Организация					КАК Организация,
		|	1										КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитики
		|		ПО ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам = Аналитики.Ссылка
		|ГДЕ
		|	НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
		|	И ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Аналитики.Организация В(&МассивОрганизаций)
		|	И ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	И НЕ ВыручкаИСебестоимостьПродаж.РасчетСебестоимости
		|	И НЕ ВыручкаИСебестоимостьПродаж.РасчетПартий
		|	И НЕ ВыручкаИСебестоимостьПродаж.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
		//++ НЕ УТ
		|	И НЕ ВыручкаИСебестоимостьПродаж.Регистратор ССЫЛКА Документ.РасчетРезервовПодОбесценениеЗапасов
		//-- НЕ УТ
		|";
КонецФункции

#КонецОбласти

#Область ВключениеУчетаСебестоимостиПоНазначениям

// Выбираем документы, у которых в движениях по оперативным регистрам указано назначение,
// а в движении учета себестоимости не указано назначение.
// Используются данные следующих оперативных регистров:
//	- Товары организаций
//		кроме таможенных деклараций с нулевыми суммами пошлины и НДС
//		кроме перемещений товаров в статусе "Отгружено"
//		кроме сборок товаров в статусе "В работе"
//	- Товары организаций к передаче
//	- Товары переданные переработчику
//	- Товары полученные от переработчика
//	- Товары к оформлению таможенных деклараций (движения "Приход)
//	- Материалы и работы в производстве (учитываются работы при партионном учете версии 2.2,
//		при партионном учете 2.1 не используется одновременно с онлайн движениями по регистру "Себестоимость товаров")
//		исключаются движения у документов "Маршрутный лист производства", "Списание затрат на выпуск", "Распределение материалов и работ",
//		используемых для производства 2.1, т.к. эти документы не делают онлайн движений по регистру "Себестоимость товаров" 
//		одновременно с регистром "Материалы и работы в производстве".
// Дополнительно к оперативным регистрам выбираем данные непосредственно из документов (у этих документов нет оперативных регистров):
//	- Отчет давальцу
//	- Заявление о ввозе товаров из ЕАЭС с заполненной суммой НДС
//	- Ввод остатков с выключенным флажком "ОУ"
// Исключения:
//	- По товарам, переданным на комиссию, учет по назначениям не ведется
//	- По товаром к оформлению отчетов комитенту учет по назначениям не ведется
//	- По товарам, отгруженным без перехода права собственности, учет по назначениям не ведется
//	- Для движений себестоимости по разделу учета "Незавершенное производство" нет соответствующих оперативных регистров.
//		Но этот раздел учета всегда корреспондирует с другими разделами, которые имеют соответствующие оперативные регистры.
//		Поэтому движения по разделу "Незавершенное производстве" не проверяем. Они будут переформированы одновременно с другими движениями.
//	- Движения документов "Заявление о ввозе товаров из ЕАЭС" по себестоимости не имеют соответствующих оперативных регистров. 
//		Проверяем данные в табличной части документов
//	- Движения документов "Распределение материалов и работ" при распределении материалов по правилу
//	- Движения документов "Исправление развернутого сальдо товаров организаций" если в движениях не меняется вид запасов.
// 		У таких документов не будет движений по регистру "Себестоимость товаров".
//	- Документы корректировки регистров. У таких документов могут быть движения только по оперативным регистрам.
//	- Документы ввода начальных остатков, у которых снят флажок "Себестоимость" (у таких документов нет движений по учету себестоимости)
//
// Возвращаемое значение:
//	Строка - текст запроса
//
Функция ТекстЗапросаВключениеУчетаСебестоимостиПоНазначениям() Экспорт
	Возврат "
		|ВЫБРАТЬ
		|	Ключи.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ АналитикаСНазначениями
		|ИЗ
		|	Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И &ИспользоватьУчетСебестоимости
		|	И Ключи.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		//++ НЕ УТ
		|	И Ключи.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
		//-- НЕ УТ
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		// Выбираем таможенные декларации на импорт, в которых указаны нулевые суммы таможенной пошлины и НДС.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Ссылка,
		|	Строки.АналитикаУчетаНоменклатуры
		|ПОМЕСТИТЬ ДекларацииСНулевымиСуммами
		|ИЗ
		|	Документ.ТаможеннаяДекларацияИмпорт КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТаможеннаяДекларацияИмпорт.Товары КАК Строки
		|		ПО Строки.Ссылка = ДД.Ссылка
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|		ПО АналитикаСНазначениями.Ссылка = Строки.АналитикаУчетаНоменклатуры
		|ГДЕ
		|	ДД.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДД.Организация В (&МассивОрганизаций)
		|	И ДД.Проведен
		|	И Строки.СуммаПошлины = 0
		|	И Строки.СуммаНДС = 0
		|;
		// Выбираем документы, у которых в движениях по оперативным регистрам указано назначение.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Товары.Регистратор КАК Регистратор,
		|	Товары.Организация КАК Организация
		|ПОМЕСТИТЬ ДокументыСНазначениями
		|ИЗ (
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыОрганизаций.Регистратор КАК Регистратор,
		|		ТоварыОрганизаций.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ТоварыОрганизаций.АналитикаУчетаНоменклатуры
		// Исключаем таможенные декларации с нулевыми суммами пошлины и НДС
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДекларацииСНулевымиСуммами КАК Декларации
		|			ПО Декларации.Ссылка = ТоварыОрганизаций.Регистратор
		|			И Декларации.АналитикаУчетаНоменклатуры = ТоварыОрганизаций.АналитикаУчетаНоменклатуры
		|			И ТИПЗНАЧЕНИЯ(ТоварыОрганизаций.Регистратор) = ТИП(Документ.ТаможеннаяДекларацияИмпорт)
		// Исключаем перемещения товаров в статусе "Отгружено"
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеТоваров КАК Перемещение
		|			ПО Перемещение.Ссылка = ТоварыОрганизаций.Регистратор
		|			И Перемещение.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПеремещенийТоваров.Отгружено)
		// Исключаем сборки товаров в статусе "В работе"
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СборкаТоваров КАК Сборка
		|			ПО Сборка.Ссылка = ТоварыОрганизаций.Регистратор
		|			И Сборка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСборокТоваров.ВРаботе)
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ТоварыОрганизаций.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыОрганизаций.Организация В(&МассивОрганизаций)
		|		И ТИПЗНАЧЕНИЯ(ТоварыОрганизаций.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
		// Исключаем движения документов производства 2.2, для которых нет онлайн движений по регистру "Себестоимость товаров"
		|		И ТоварыОрганизаций.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РаспределениеРасходовНаПартииПроизводства)
		// Исключаем движения документов "Исправление развернутого сальдо товаров организаций" если в движениях не меняется вид запасов.
		// У таких документов не будет движений по регистру "Себестоимость товаров".
		|		И НЕ (
		|			ТоварыОрганизаций.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаОбособленногоУчета)
		|			И ТоварыОрганизаций.ВидЗапасов = ТоварыОрганизаций.КорВидЗапасов
		|			И ТИПЗНАЧЕНИЯ(ТоварыОрганизаций.Регистратор) <> ТИП(Документ.КорректировкаНазначенияТоваров))
		// Исключаем таможенные декларации с нулевыми суммами пошлины и НДС
		|		И Декларации.Ссылка ЕСТЬ NULL
		// Исключаем вводы остатков с выключенным учетом себестоимости
		|		И ЕСТЬNULL(ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ВводОстатков).ОтражатьСебестоимость, ИСТИНА)
		|		И ЕСТЬNULL(ВЫРАЗИТЬ(ТоварыОрганизаций.Регистратор КАК Документ.ВводОстатковТоваров).ОтражатьСебестоимость, ИСТИНА)
		// Исключаем перемещения товаров в статусе "Отгружено"
		|		И Перемещение.Ссылка ЕСТЬ NULL
		// Исключаем сборки товаров в статусе "В работе"
		|		И Сборка.Ссылка ЕСТЬ NULL
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыОрганизацийКПередаче.Регистратор КАК Регистратор,
		|		ТоварыОрганизацийКПередаче.ВидЗапасовПродавца.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыОрганизацийКПередаче КАК ТоварыОрганизацийКПередаче
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ТоварыОрганизацийКПередаче.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ТоварыОрганизацийКПередаче.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыОрганизацийКПередаче.ВидЗапасовПродавца.Организация В(&МассивОрганизаций)
		|		И ТИПЗНАЧЕНИЯ(ТоварыОрганизацийКПередаче.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
		|
		//++ НЕ УТ
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыПереданныеПереработчику.Регистратор КАК Регистратор,
		|		ТоварыПереданныеПереработчику.ВидЗапасов.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыПереданныеПереработчику КАК ТоварыПереданныеПереработчику
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ТоварыПереданныеПереработчику.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ТоварыПереданныеПереработчику.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыПереданныеПереработчику.ВидЗапасов.Организация В(&МассивОрганизаций)
		|		И ТИПЗНАЧЕНИЯ(ТоварыПереданныеПереработчику.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
		// Исключаем вводы остатков с выключенным учетом себестоимости
		|		И ЕСТЬNULL(ВЫРАЗИТЬ(ТоварыПереданныеПереработчику.Регистратор КАК Документ.ВводОстатков).ОтражатьСебестоимость, ИСТИНА)
		|		И ЕСТЬNULL(ВЫРАЗИТЬ(ТоварыПереданныеПереработчику.Регистратор КАК Документ.ВводОстатковТоваров).ОтражатьСебестоимость, ИСТИНА)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыПолученныеОтПереработчика.Регистратор КАК Регистратор,
		|		ТоварыПолученныеОтПереработчика.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыПолученныеОтПереработчика КАК ТоварыПолученныеОтПереработчика
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ТоварыПолученныеОтПереработчика.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ТоварыПолученныеОтПереработчика.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыПолученныеОтПереработчика.Организация В(&МассивОрганизаций)
		|		И ТИПЗНАЧЕНИЯ(ТоварыПолученныеОтПереработчика.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
		//-- НЕ УТ
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыКОформлениюДокументовИмпорта.Регистратор КАК Регистратор,
		|		ТоварыКОформлениюДокументовИмпорта.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыКОформлениюДокументовИмпорта КАК ТоварыКОформлениюДокументовИмпорта
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ТоварыКОформлениюДокументовИмпорта.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ТоварыКОформлениюДокументовИмпорта.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыКОформлениюДокументовИмпорта.Организация В(&МассивОрганизаций)
		|		И ТоварыКОформлениюДокументовИмпорта.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		И ТИПЗНАЧЕНИЯ(ТоварыКОформлениюДокументовИмпорта.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
		|		И ТоварыКОформлениюДокументовИмпорта.ТипДокументаИмпорта = &ТипДокументаИмпорта
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		МатериалыИРаботыВПроизводстве.Регистратор КАК Регистратор,
		|		МатериалыИРаботыВПроизводстве.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.МатериалыИРаботыВПроизводстве КАК МатериалыИРаботыВПроизводстве
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И МатериалыИРаботыВПроизводстве.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И МатериалыИРаботыВПроизводстве.Организация В(&МассивОрганизаций)
		|		И МатериалыИРаботыВПроизводстве.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		И МатериалыИРаботыВПроизводстве.Количество <> 0
		|		И НЕ ТИПЗНАЧЕНИЯ(МатериалыИРаботыВПроизводстве.Регистратор) В (
		|			ТИП(Документ.КорректировкаРегистров),
		|			ТИП(Документ.РасчетСебестоимостиТоваров))
		// Исключаем вводы остатков с выключенным учетом себестоимости
		|		И ЕСТЬNULL(ВЫРАЗИТЬ(МатериалыИРаботыВПроизводстве.Регистратор КАК Документ.ВводОстатков).ОтражатьСебестоимость, ИСТИНА)
		|		И ЕСТЬNULL(ВЫРАЗИТЬ(МатериалыИРаботыВПроизводстве.Регистратор КАК Документ.ВводОстатковТоваров).ОтражатьСебестоимость, ИСТИНА)
		//++ НЕ УТ
		// Кроме документов "Распределение материалов и работ". Они делают движения по регистру "Материалы и работы в производстве",
		// но не всегда имеют движения по регистру "Себестоимость товаров".
		|		И ТИПЗНАЧЕНИЯ(МатериалыИРаботыВПроизводстве.Регистратор) <> ТИП(Документ.РаспределениеПроизводственныхЗатрат)
		//++ Устарело_Производство21
		// При использовании производства 2.1 документы "Маршрутный лист производства", "Списание затрат на выпуск" и "Распределение материалов и работ"
		// не имеют онлайн движений по регистру "Себестоимость товаров".
		|		И ТИПЗНАЧЕНИЯ(МатериалыИРаботыВПроизводстве.Регистратор) <> ТИП(Документ.СписаниеЗатратНаВыпуск)
		//++ НЕ УТКА
		|		И ТИПЗНАЧЕНИЯ(МатериалыИРаботыВПроизводстве.Регистратор) <> ТИП(Документ.МаршрутныйЛистПроизводства)
		//-- НЕ УТКА
		//-- Устарело_Производство21
		//-- НЕ УТ
		|
		//++ НЕ УТКА
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ОтчетДавальцу.Ссылка КАК Регистратор,
		|		ОтчетДавальцу.Организация КАК Организация
		|	ИЗ
		|		Документ.ОтчетДавальцу КАК ОтчетДавальцу
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ОтчетДавальцу.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ОтчетДавальцу.Организация В(&МассивОрганизаций)
		|		И ОтчетДавальцу.Проведен
		//-- НЕ УТКА
		|
		//++ Локализация
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ЗаявлениеОВвозеТовары.Ссылка КАК Регистратор,
		|		ЗаявлениеОВвозеТовары.Ссылка.Организация КАК Организация
		|	ИЗ
		|		Документ.ЗаявлениеОВвозеТоваров.Товары КАК ЗаявлениеОВвозеТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ЗаявлениеОВвозеТовары.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ЗаявлениеОВвозеТовары.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ЗаявлениеОВвозеТовары.Ссылка.Организация В(&МассивОрганизаций)
		|		И ЗаявлениеОВвозеТовары.Ссылка.Проведен
		|		И ЗаявлениеОВвозеТовары.СуммаНДС <> 0
		//-- Локализация
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ВводОстатков.Ссылка КАК Регистратор,
		|		ВводОстатков.Ссылка.Организация КАК Организация
		|	ИЗ
		|		Документ.ВводОстатков.Товары КАК ВводОстатков
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ВводОстатков.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И ВводОстатков.Ссылка.ОтражатьСебестоимость
		|		И НЕ ВводОстатков.Ссылка.ОтражатьВОперативномУчете
		|		И ВводОстатков.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ВводОстатков.Ссылка.Организация В(&МассивОрганизаций)
		|		И ВводОстатков.Ссылка.Проведен
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ВводОстатков.Ссылка КАК Регистратор,
		|		ВводОстатков.Ссылка.Организация КАК Организация
		|	ИЗ
		|		Документ.ВводОстатковТоваров.Товары КАК ВводОстатков
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ВводОстатков.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И ВводОстатков.Ссылка.ОтражатьСебестоимость
		|		И НЕ ВводОстатков.Ссылка.ОтражатьВОперативномУчете
		|		И ВводОстатков.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ВводОстатков.Ссылка.Организация В(&МассивОрганизаций)
		|		И ВводОстатков.Ссылка.Проведен
		|	) КАК Товары
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	Организация
		|;
		// Выбираем документы, у которых в движениях по себестоимости указано назначение.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СебестоимостьТоваров.Регистратор КАК Регистратор,
		|	СебестоимостьТоваров.Организация КАК Организация
		|
		|ПОМЕСТИТЬ ДвиженияСебестоимостиСНазначениями
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыСНазначениями КАК ДокументыСНазначениями
		|		ПО ДокументыСНазначениями.Регистратор = СебестоимостьТоваров.Регистратор
		|		И ДокументыСНазначениями.Организация = СебестоимостьТоваров.Организация
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|		ПО АналитикаСНазначениями.Ссылка = СебестоимостьТоваров.АналитикаУчетаНоменклатуры
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СебестоимостьТоваров.Организация В(&МассивОрганизаций)
		|	И НЕ СебестоимостьТоваров.РасчетСебестоимости
		|	И НЕ СебестоимостьТоваров.РасчетПартий
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	Организация
		|;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СебестоимостьТоваров.Регистратор КАК Регистратор,
		|	СебестоимостьТоваров.Организация КАК Организация
		|
		|ПОМЕСТИТЬ ЕстьДвиженияСебестоимости
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыСНазначениями КАК ДокументыСНазначениями
		|		ПО ДокументыСНазначениями.Регистратор = СебестоимостьТоваров.Регистратор
		|		И ДокументыСНазначениями.Организация = СебестоимостьТоваров.Организация
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СебестоимостьТоваров.Организация В(&МассивОрганизаций)
		|	И НЕ СебестоимостьТоваров.РасчетСебестоимости
		|	И НЕ СебестоимостьТоваров.РасчетПартий
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	Организация
		|;
		// Выбираем документы, у которых в движениях по оперативным регистрам указано назначение,
		// а в движениях учета себестоимости не указано назначение (нет движений с указанным назначением).
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""СебестоимостьТоваров""			КАК ИмяРегистра,
		|	ДокументыСНазначениями.Регистратор	КАК Ссылка,
		|	ДокументыСНазначениями.Организация	КАК Организация,
		|	1									КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	ДокументыСНазначениями КАК ДокументыСНазначениями
		// Выполняем проверерку только при наличии движений по себестоимости товаров. 
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЕстьДвиженияСебестоимости КАК ЕстьДвиженияСебестоимости
		|		ПО ЕстьДвиженияСебестоимости.Регистратор = ДокументыСНазначениями.Регистратор
		|		И ЕстьДвиженияСебестоимости.Организация = ДокументыСНазначениями.Организация
		|	ЛЕВОЕ СОЕДИНЕНИЕ ДвиженияСебестоимостиСНазначениями КАК СебестоимостьТоваров
		|		ПО СебестоимостьТоваров.Регистратор = ДокументыСНазначениями.Регистратор
		|		И СебестоимостьТоваров.Организация = ДокументыСНазначениями.Организация
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И СебестоимостьТоваров.Регистратор ЕСТЬ NULL
		|;
		|УНИЧТОЖИТЬ АналитикаСНазначениями;
		|УНИЧТОЖИТЬ ДокументыСНазначениями;
		|УНИЧТОЖИТЬ ДвиженияСебестоимостиСНазначениями
		|";
КонецФункции

// Выбираем документы, у которых в движениях в аналитике учета номенклатуры не указано назначение.
//
// Возвращаемое значение:
//	Строка - текст запроса
//
Функция ТекстЗапросаВключениеУчетаВыручкиИСебестоимостиПродажПоНазначениям() Экспорт
	Возврат "
		|ВЫБРАТЬ
		|	Ключи.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ АналитикаСНазначениями
		|ИЗ
		|	Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И &ИспользоватьУчетСебестоимости
		|	И Ключи.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		//++ НЕ УТ
		|	И Ключи.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
		//-- НЕ УТ
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВыручкаИСебестоимостьПродаж.Регистратор КАК Регистратор,
		|	Аналитики.Организация КАК Организация
		|ПОМЕСТИТЬ ДокументыПродаж
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитики
		|		ПО ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам = Аналитики.Ссылка
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК ДокументРеализации
		|		ПО ДокументРеализации.Ссылка = ВыручкаИСебестоимостьПродаж.Регистратор
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И &ИспользоватьУчетСебестоимости
		|	И ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Аналитики.Организация В (&МассивОрганизаций)
		|	И ЕСТЬNULL(ДокументРеализации.ХозяйственнаяОперация, НЕОПРЕДЕЛЕНО)
		|		<> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
		|;
		// Выбираем документы продаж, у которых в движениях по оперативным регистрам указано назначение.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Товары.Регистратор КАК Регистратор,
		|	Товары.Организация КАК Организация
		|ПОМЕСТИТЬ ДокументыСНазначениями
		|ИЗ (
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыОрганизаций.Регистратор КАК Регистратор,
		|		ТоварыОрганизаций.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыПродаж КАК ДокументыПродаж
		|			ПО ДокументыПродаж.Регистратор = ТоварыОрганизаций.Регистратор
		|			И ДокументыПродаж.Организация = ТоварыОрганизаций.Организация
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ТоварыОрганизаций.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ТоварыОрганизаций.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыОрганизаций.Организация В(&МассивОрганизаций)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТоварыОрганизацийКПередаче.Регистратор КАК Регистратор,
		|		ТоварыОрганизацийКПередаче.ВидЗапасовПродавца.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.ТоварыОрганизацийКПередаче КАК ТоварыОрганизацийКПередаче
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыПродаж КАК ДокументыПродаж
		|			ПО ДокументыПродаж.Регистратор = ТоварыОрганизацийКПередаче.Регистратор
		|			И ДокументыПродаж.Организация = ТоварыОрганизацийКПередаче.ВидЗапасовПродавца.Организация
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|			ПО АналитикаСНазначениями.Ссылка = ТоварыОрганизацийКПередаче.АналитикаУчетаНоменклатуры
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ТоварыОрганизацийКПередаче.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ТоварыОрганизацийКПередаче.ВидЗапасовПродавца.Организация В(&МассивОрганизаций)
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		МатериалыИРаботыВПроизводстве.Регистратор КАК Регистратор,
		|		МатериалыИРаботыВПроизводстве.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.МатериалыИРаботыВПроизводстве КАК МатериалыИРаботыВПроизводстве
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыПродаж КАК ДокументыПродаж
		|			ПО ДокументыПродаж.Регистратор = МатериалыИРаботыВПроизводстве.Регистратор
		|			И ДокументыПродаж.Организация = МатериалыИРаботыВПроизводстве.Организация
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И МатериалыИРаботыВПроизводстве.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|		И МатериалыИРаботыВПроизводстве.Организация В(&МассивОрганизаций)
		|		И МатериалыИРаботыВПроизводстве.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|
		//++ НЕ УТКА
		|	ОБЪЕДИНИТЬ ВСЕ
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ОтчетДавальцу.Ссылка КАК Регистратор,
		|		ОтчетДавальцу.Организация КАК Организация
		|	ИЗ
		|		Документ.ОтчетДавальцу КАК ОтчетДавальцу
		|	ГДЕ
		|		&УчитыватьСебестоимостьТоваровПоНазначениям
		|		И &ИспользоватьУчетСебестоимости
		|		И ОтчетДавальцу.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ОтчетДавальцу.Организация В(&МассивОрганизаций)
		|		И ОтчетДавальцу.Проведен
		//-- НЕ УТКА
		|	) КАК Товары
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	Организация
		|;
		// Выбираем документы, у которых в движениях по выручке и себестоимости продаж указано назначение.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВыручкаИСебестоимостьПродаж.Регистратор КАК Регистратор,
		|	Аналитики.Организация КАК Организация
		|
		|ПОМЕСТИТЬ ДвиженияСебестоимостиСНазначениями
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитики
		|		ПО ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам = Аналитики.Ссылка
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыСНазначениями КАК ДокументыСНазначениями
		|		ПО ДокументыСНазначениями.Регистратор = ВыручкаИСебестоимостьПродаж.Регистратор
		|		И ДокументыСНазначениями.Организация = Аналитики.Организация
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
		|		ПО АналитикаСНазначениями.Ссылка = ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Аналитики.Организация В(&МассивОрганизаций)
		|	И ВыручкаИСебестоимостьПродаж.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор,
		|	Организация
		|;
		// Выбираем документы, у которых в движениях по оперативным регистрам указано назначение,
		// а в движении выручки и себестоимости продаж не указано назначение (нет движений с указанным назначением).
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	""ВыручкаИСебестоимостьПродаж""		КАК ИмяРегистра,
		|	ДокументыСНазначениями.Регистратор	КАК Ссылка,
		|	ДокументыСНазначениями.Организация	КАК Организация,
		|	1									КАК КодОшибки
		|ПОМЕСТИТЬ ВТРегистраторыСНекорректнымиДвижениями
		|ИЗ
		|	ДокументыСНазначениями КАК ДокументыСНазначениями
		|	ЛЕВОЕ СОЕДИНЕНИЕ ДвиженияСебестоимостиСНазначениями КАК СебестоимостьТоваров
		|		ПО СебестоимостьТоваров.Регистратор = ДокументыСНазначениями.Регистратор
		|		И СебестоимостьТоваров.Организация = ДокументыСНазначениями.Организация
		|ГДЕ
		|	&УчитыватьСебестоимостьТоваровПоНазначениям
		|	И СебестоимостьТоваров.Регистратор ЕСТЬ NULL
		|;
		|УНИЧТОЖИТЬ АналитикаСНазначениями;
		|УНИЧТОЖИТЬ ДокументыПродаж;
		|УНИЧТОЖИТЬ ДокументыСНазначениями;
		|УНИЧТОЖИТЬ ДвиженияСебестоимостиСНазначениями
		|";
КонецФункции

#КонецОбласти

#КонецОбласти
