////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ РАБОТЫ С МАКЕТОМ
//

&НаСервере
Функция ПолучитьСтруктуруСсылокНаОБъекты(МассивОбъектов, ВидОтчета, ТипЭлемента) Экспорт
	
	Запрос = Новый Запрос;
	
	Если ТипЭлемента = "Строка" Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтрокиОтчетов.Ссылка,
		|	СтрокиОтчетов.Код
		|ИЗ
		|	Справочник.СтрокиОтчетов КАК СтрокиОтчетов
		|ГДЕ
		|	СтрокиОтчетов.Владелец = &ВидОтчета
		|	И СтрокиОтчетов.Код В(&СписокКодов)";
		
	ИначеЕсли ТипЭлемента = "Колонка" Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	КолонкиОтчетов.Ссылка,
		|	КолонкиОтчетов.Код
		|ИЗ
		|	Справочник.КолонкиОтчетов КАК КолонкиОтчетов
		|ГДЕ
		|	КолонкиОтчетов.Владелец = &ВидОтчета
		|	И КолонкиОтчетов.Код В(&СписокКодов)";
	Иначе
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоказателиОтчетов.Ссылка,
		|	ПоказателиОтчетов.Код
		|ИЗ
		|	Справочник.ПоказателиОтчетов КАК ПоказателиОтчетов
		|ГДЕ
		|	ПоказателиОтчетов.Владелец = &ВидОтчета
		|	И ПоказателиОтчетов.Код В(&СписокКодов)";
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("СписокКодов", МассивОбъектов);
	Запрос.УстановитьПараметр("ВидОтчета"  , ВидОтчета);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() = 1 Тогда
		
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
		
	Иначе
		
		СоответствиеОбъектовИОбластей = Новый Соответствие;
		
		Пока Выборка.Следующий() Цикл
			
			Если ТипЭлемента = "Показатель" Тогда
				СоответствиеОбъектовИОбластей.Вставить(Выборка.Ссылка, СокрЛП(Выборка.Код));
			Иначе
				СоответствиеОбъектовИОбластей.Вставить(Выборка.Ссылка, ТипЭлемента + "_" + СокрЛП(Выборка.Код));
			КонецЕсли;
			
		КонецЦикла;
		
		Возврат Новый ФиксированноеСоответствие(СоответствиеОбъектовИОбластей);
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ПолучитьМассивКодовРедактируемыхОбъектов(ТабДок, ТипЭлемента) Экспорт
	
	МассивКодовРедактируемыхОбъектов = Новый Массив;
	ТипЭлемента      = Неопределено;
	
	Для Каждого Область Из ТабДок.ВыделенныеОбласти Цикл
		Для ИндС = Область.Верх По Область.Низ Цикл
			Для ИндК = Область.Лево По Область.Право Цикл
				
				ТекОбласть = ТабДок.Область(ИндС, ИндК);
				Если ТипЗнч(ТекОбласть) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда
					
					ПозСтрока  = СтрНайти(ТекОбласть.Имя, "Строка_");
					ПозКолонка = СтрНайти(ТекОбласть.Имя, "Колонка_");
					
					Если ПозСтрока <> 0 Тогда
						
						Если ТипЭлемента = Неопределено Тогда
							ТипЭлемента = "Строка";
						Иначе 
							
							Если ТипЭлемента <> "Строка" Тогда
								Сообщить("Групповая обработка доступна только для однородных элементов");
								Возврат Неопределено;
							КонецЕсли;
							
						КонецЕсли;
						
						ТекКод = СокрЛП(Сред(ТекОбласть.Имя, ПозСтрока + 7));
						МассивКодовРедактируемыхОбъектов.Добавить(ТекКод);
						
					ИначеЕсли ПозКолонка <> 0 Тогда
						
						Если ТипЭлемента = Неопределено Тогда
							ТипЭлемента = "Колонка";
						Иначе
							
							Если ТипЭлемента <> "Колонка" Тогда
								Сообщить("Групповая обработка доступна только для однородных элементов");
								Возврат Неопределено;
							КонецЕсли;
							
						КонецЕсли;
						
						ТекКод = СокрЛП(Сред(ТекОбласть.Имя, ПозКолонка + 8));
						МассивКодовРедактируемыхОбъектов.Добавить(ТекКод);
						
					Иначе
						
						Если ТипЭлемента = Неопределено Тогда
							ТипЭлемента = "Показатель";
						Иначе
							
							Если ТипЭлемента <> "Показатель" Тогда
								Сообщить("Групповая обработка доступна только для однородных элементов");
								Возврат Неопределено;
							КонецЕсли;
												
						КонецЕсли;
						
						МассивКодовРедактируемыхОбъектов.Добавить(СокрЛП(ТекОБласть.Имя));
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
	КонецЦикла;
	
	Возврат МассивКодовРедактируемыхОбъектов;
	
КонецФункции
