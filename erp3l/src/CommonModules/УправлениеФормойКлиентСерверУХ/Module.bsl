
Функция УпаковатьДанныеФормыДерево(ДанныеФормыДерево, Колонки, ЭлементФормы = Неопределено) Экспорт
	
	Строки = Новый Массив;
	
	Для Каждого Элемент Из ДанныеФормыДерево.ПолучитьЭлементы() Цикл
		
		Строка = Новый Структура();
		
		Строки.Добавить(Строка);
		
		Значение = Новый Структура(Колонки);
		ЗаполнитьЗначенияСвойств(Значение, Элемент);
		
		Строка.Вставить("Значение", Значение);
		
		#Если НаКлиенте Тогда
		
		Если НЕ ЭлементФормы = Неопределено Тогда
			
			Если ЭлементФормы.Развернут(Элемент.ПолучитьИдентификатор()) Тогда
				Строка.Вставить("Развернута", Истина);
			КонецЕсли;
			
			Если ЭлементФормы.ТекущаяСтрока = Элемент.ПолучитьИдентификатор() Тогда
				Строка.Вставить("Текущая", Истина);
			КонецЕсли;
			
			Если НЕ ЭлементФормы.ВыделенныеСтроки.Найти(Элемент.ПолучитьИдентификатор()) = Неопределено Тогда
				Строка.Вставить("Выделена", Истина);
			КонецЕсли;
			
		КонецЕсли;
		
		#КонецЕсли
		
		Строка.Вставить("Строки", УпаковатьДанныеФормыДерево(Элемент, Колонки, ЭлементФормы));
		
	КонецЦикла;
	
	Возврат Строки;
	
Конецфункции

Процедура РаспаковатьДанныеФормыДерево(УпакованныеДанные, ДанныеФормыДерево, ЭлементФормы = Неопределено) Экспорт
	
	ДанныеФормыДерево.ПолучитьЭлементы().Очистить();
	
	Если НЕ ЭлементФормы = Неопределено Тогда
		ЭлементФормы.ВыделенныеСтроки.Очистить();
	КонецЕсли;
	
	РаспаковатьДанныеФормыКоллекцияЭлементовДерева(УпакованныеДанные, ДанныеФормыДерево, ЭлементФормы)
	
КонецПроцедуры

Процедура РаспаковатьДанныеФормыКоллекцияЭлементовДерева(УпакованныеДанные, ДанныеФормыДерево, ЭлементФормы)
	
	Элементы = ДанныеФормыДерево.ПолучитьЭлементы();
	
	Для Каждого Строка Из УпакованныеДанные Цикл
		
		Элемент = Элементы.Добавить();
		ЗаполнитьЗначенияСвойств(Элемент, Строка.Значение);
		
		РаспаковатьДанныеФормыКоллекцияЭлементовДерева(Строка.Строки, Элемент, ЭлементФормы);
		
		#Если НаКлиенте Тогда
			
		Если НЕ ЭлементФормы = Неопределено Тогда
			
			Если Строка.Свойство("Текущая") Тогда
				ВыделенныеСтроки = Новый Массив;
				Для Каждого Значение Из ЭлементФормы.ВыделенныеСтроки Цикл
					ВыделенныеСтроки.Добавить(Значение);
				КонецЦикла;
				Если НЕ ЭлементФормы.ТекущаяСтрока = Элемент.ПолучитьИдентификатор() Тогда
					ЭлементФормы.ТекущаяСтрока = Элемент.ПолучитьИдентификатор();
				КонецЕсли;
				ЭлементФормы.ВыделенныеСтроки.Очистить();
				Для Каждого Значение Из ВыделенныеСтроки Цикл
					ЭлементФормы.ВыделенныеСтроки.Добавить(Значение);
				КонецЦикла;
			КонецЕсли;
			
			Если Строка.Свойство("Развернута") Тогда
				ЭлементФормы.Развернуть(Элемент.ПолучитьИдентификатор());
			Иначе
				ЭлементФормы.Свернуть(Элемент.ПолучитьИдентификатор());
			КонецЕсли;
			
			Если Строка.Свойство("Выделена") Тогда
				ЭлементФормы.ВыделенныеСтроки.Добавить(Элемент.ПолучитьИдентификатор());
			КонецЕсли;
			
		КонецЕсли;
				
		#КонецЕсли
	
	КонецЦикла;
	
КонецПроцедуры

