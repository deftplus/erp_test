// Процедуры и функции для открытия окна со списком комментариев
//////////////////////////////////////////////////////////////////

// Открываем окно с комментариями, для переданного адреса.
// Параметры:
//		ОписаниеПредметаКомменитрования - структура следующего формата:
//			РазделКомментариев - одно из предопределенных значений перечисления РазделыКомментариев.
//				Комментарии обрабатываются только в пределах одного шаблона адреса.
//			ПредметКомментирования - ссылка на справочник предмет комментирования.
//			ПочтовыеАдреса - список значений заполненный строками с адресами электронной почты, для рассылки уведомлений о комментариях,
//			ПрефиксКомментариев - произвольная строка для добавления перед текстом комментария.
//			Реквизиты - структура с описанием реквизитов предмета комментирования.
//			Отбор - структура, отбор по регистру сведений УниверсальныеКомментарииУХ.
//		ВладелецФормы - форма или элемент управления другой формы.
//		Модально - не используется.
//		ТекстОшибки - Строка для помещения текста описания ошибки, если она возникла в процессе работы функции.
//
// Возвращает:
// 		Неопределено - возникли ошибки, в ТекстОшибки записывает ее описание.
//  	Ссылка на справочник ПредметыКомментариев.
//
Функция ОткрытьОкноКомментирования(ОписаниеПредметаКомменитрования, ОписаниеОповещенияОЗакрытии=Неопределено, ВладелецФормы=Неопределено, Окно =  Неопределено, Модально =  Ложь, ТекстОшибки = "") Экспорт
	ЧтоКомментируем = ОписаниеПредметаКомменитрования.ПредметКомментирования;
	Если ЧтоКомментируем = Неопределено Тогда
		ТекстОшибки = НСтр("ru = ' Не найден предмет комментирования.'") + ТекстОшибки;
		Возврат Неопределено;
	КонецЕсли;
	
	ОписаниеПредметаКомменитрования.Вставить("ПоказыватьПредметКомментирования", Истина);
	ОписаниеПредметаКомменитрования.Вставить("ЗакрыватьПриВыборе", Ложь);
	
	ОткрытьФорму("РегистрСведений.УниверсальныеКомментарии.Форма.ФормаСпискаКомментариев",
			ОписаниеПредметаКомменитрования, ВладелецФормы,, Окно,, ОписаниеОповещенияОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	Возврат ОписаниеПредметаКомменитрования.ПредметКомментирования;
КонецФункции

// Открыть окно с комментариями документа
// Параметры:
//		Ссылка - ссылка на документ.
//		ВладелецФормы - необязательный, ссылка на форму или ее элемент.
//
&НаКлиенте
Процедура ОткрытьОкноКомментариевДокумента(Ссылка, ВладелецФормы=Неопределено, Окно=Неопределено) Экспорт
		ТекстОшибки = "";
		
		ОписаниеПредметаКомментирования = УниверсальныеКомментарииВызовСервераУХ.ПодготовитьОписаниеДокумента(Ссылка, ТекстОшибки);
		
		Если ОписаниеПредметаКомментирования <> Неопределено Тогда
			ОткрытьОкноКомментирования(ОписаниеПредметаКомментирования,,ВладелецФормы, Окно, Ложь, ТекстОшибки);
		КонецЕсли;
		
		Если ТекстОшибки <> "" Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ТекстОшибки;
			Сообщение.Сообщить();
		КонецЕсли;
	КонецПроцедуры	
	
// Открыть окно с комментариями к элементу справочника.
// Параметры:
//		Ссылка - ссылка на элемент справочника.
//		ВладелецФормы - необязательный, ссылка на форму или ее элемент.
//
&НаКлиенте
Процедура ОткрытьОкноКомментариевСправочника(Ссылка, ВладелецФормы=Неопределено, Окно=Неопределено) Экспорт
	ТекстОшибки = "";
	
	ОписаниеПредметаКомментирования = УниверсальныеКомментарииВызовСервераУХ.ПодготовитьОписаниеСправочника(Ссылка, ТекстОшибки);
	
	Если ОписаниеПредметаКомментирования <> Неопределено Тогда
		ОткрытьОкноКомментирования(ОписаниеПредметаКомментирования,, ВладелецФормы, Окно, Ложь, ТекстОшибки);
	КонецЕсли;
	
	Если ТекстОшибки <> "" Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстОшибки;
		Сообщение.Сообщить();
	КонецЕсли;
КонецПроцедуры