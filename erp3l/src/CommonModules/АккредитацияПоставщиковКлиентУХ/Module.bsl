Процедура ОтправитьНаСогласование(Форма, ИмяРеквизитаОбъекта="Объект", ИмяРеквизитаДаты="ДатаОтправкиНаСогласование",
		ИмяФункцииДопЗаполнения="") Экспорт
		
	Объект = Форма[ИмяРеквизитаОбъекта];
	
	Если НЕ Форма.Модифицированность И ЗначениеЗаполнено(Объект[ИмяРеквизитаДаты]) Тогда
		Возврат
	КонецЕсли;
	
	Если Форма.ПроверитьЗаполнение() Тогда
		Попытка
			Объект[ИмяРеквизитаДаты] = ТекущаяДата();
			Если НЕ ПустаяСтрока(ИмяФункцииДопЗаполнения) Тогда
				ПроверенноеИмяФункции = СтрЗаменить(ИмяФункцииДопЗаполнения, ".", "");
				ПроверенноеИмяФункции = СтрЗаменить(ПроверенноеИмяФункции, ";", "");
				ПроверенноеИмяФункции = СтрЗаменить(ПроверенноеИмяФункции, "(", "");
				ПроверенноеИмяФункции = СтрЗаменить(ПроверенноеИмяФункции, ")", "");
				Выполнить("Форма." + ПроверенноеИмяФункции + "();");
			КонецЕсли;
			
		 	Форма.Записать(Новый Структура("РежимЗаписи,РежимПроведения", РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный));
		Исключение
			Объект[ИмяРеквизитаДаты] = '00010101';
			ПоказатьИнформациюОбОшибке(ИнформацияОбОшибке());
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

// Редактировать многострочный текст в таблице формы
Процедура ТабличнаяЧастьОткрытьРедакторМногострочногоТекста(Форма, ПолноеИмяОбъектаТЧ, ИмяЭлементаТЧ, ИмяКолонки, Заголовок="") Экспорт
	ОбъектТЧ = ОбщегоНазначенияКлиентУХ.ПолучитьЗнчениеРеквизитаПоПолномуПути(Форма, ПолноеИмяОбъектаТЧ);
	ЭлементТЧ = Форма.Элементы[ИмяЭлементаТЧ];
	
	ТекСтрока = ОбъектТЧ.НайтиПоИдентификатору(ЭлементТЧ.ТекущаяСтрока);
	ИндексСтроки = ОбъектТЧ.Индекс(ТекСтрока);
	
	ТекДанные = ЭлементТЧ.ТекущиеДанные;
	ИсходныйТекст = "";
	ИмяРеквизита = ПолноеИмяОбъектаТЧ + "[" + Строка(ИндексСтроки) + "]." + ИмяКолонки;
	
	Если ТекДанные <> Неопределено Тогда
		ИсходныйТекст = Строка(ТекДанные[ИмяКолонки]);
	КонецЕсли;
	
	ПередаемЗаголовок = ?(ПустаяСтрока(Заголовок), НСтр("ru = 'Редактирование текста'"), Заголовок);
	
	ОбщегоНазначенияКлиентУХ.ОткрытьФормуРедактированияМногострочногоТекста(
		Форма, ИсходныйТекст, ИмяРеквизита, Форма.Модифицированность, ПередаемЗаголовок);
КонецПроцедуры
	
// Вернуть массив сообщений об ошибках по строкам у которых не установлена положительное решение комиссии
Функция ПроверитьУстановленностьРешенийПоСтрокам(Объект, ИмяТаблицы, ИмяКолонкиРешения) Экспорт
	мОшибок = Новый Массив;
	ТабличнаяЧастьПроверки = Объект[ИмяТаблицы];
	Для Каждого Строка_ Из ТабличнаяЧастьПроверки Цикл
		Если НЕ Строка_[ИмяКолонкиРешения] Тогда
			ИндексСтроки = ТабличнаяЧастьПроверки.Индекс(Строка_);
			ТекстСообщения = НСтр("ru = 'Не установлено решение в строке №%НомерСтроки%'");
			ТекстСообщения = СТрЗаменить(ТекстСообщения, "%НомерСтроки%", Строка(Строка_.НомерСтроки));
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ТекстСообщения;
			Сообщение.Поле = ИмяТаблицы + "[" + ИндексСтроки + "]." + ИмяКолонкиРешения;
			Сообщение.КлючДанных = Объект.Ссылка;
	    	Сообщение.ПутьКДанным = "Объект";
			мОшибок.Добавить(Сообщение);
		КонецЕсли;
	КонецЦикла;
	Возврат мОшибок;
КонецФункции

