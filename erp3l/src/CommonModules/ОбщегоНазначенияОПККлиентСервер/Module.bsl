
#Область СлужебныйПрограммныйИнтерфейс

//Устарела. Следует использовать ФормыУХКлиентСервер.УстановитьАвтоОтметкуЗаполненияЭлемента
Процедура УстановитьАвтоОтметкуЗаполненияЭлемента(Элемент, ОбязательноеЗаполнение) Экспорт
	ФормыУХКлиентСервер.УстановитьАвтоОтметкуЗаполненияЭлемента(Элемент, ОбязательноеЗаполнение);
КонецПроцедуры

// Получает значение реквизита формы.
// Параметры:
//		Форма
//		ПутьРеквизита - строка, путь к данным, например: "Объект.МесяцНачисления".
Функция ПолучитьРеквизитФормыПоПути(Форма, ПутьРеквизита) Экспорт
	
	МассивИмен = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПутьРеквизита, ".");
	
	Объект        = Форма;
	ПоследнееПоле = МассивИмен[МассивИмен.Количество()-1];
	
	Для Сч = 0 По МассивИмен.Количество()-2 Цикл
		Объект = Объект[МассивИмен[Сч]]
	КонецЦикла;
	
	Возврат Объект[ПоследнееПоле];
	
КонецФункции

Процедура ЗаменитьУдалитьПараметрВыбора(Элемент, Имя, НовыйПараметрВыбора = Неопределено) Экспорт

	МассивПараметров = Новый Массив;
	Для Каждого СвязьПараметровВыбора Из Элемент.ПараметрыВыбора Цикл
		Если ВРег(СвязьПараметровВыбора.Имя) <> ВРег(Имя) Тогда
			МассивПараметров.Добавить(СвязьПараметровВыбора);
		КонецЕсли;
	КонецЦикла;

	Если НовыйПараметрВыбора <> Неопределено Тогда
		МассивПараметров.Добавить(НовыйПараметрВыбора);
	КонецЕсли;
	
	Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);

КонецПроцедуры

// Процедура устанавливает отбор по функциональным направлениям ЦФО
Процедура УстановитьОтборПоФункциональнымНаправлениям(Знач ЦФО, ЭлементыФормы) Экспорт
	
	ФункциональныеНаправления = ВстраиваниеОПКВызовСервераПереопределяемый.ФункциональныеНаправленияЦФО(ЦФО);
	ИмяПараметраВыбора = "Отбор.ФункциональноеНаправление";
	ПараметрВыбора = Новый ПараметрВыбора(ИмяПараметраВыбора, Новый ФиксированныйМассив(ФункциональныеНаправления));
	
	Для Каждого ТекЭлемент Из ЭлементыФормы Цикл
		ОбщегоНазначенияОПККлиентСервер.ЗаменитьУдалитьПараметрВыбора(ТекЭлемент, ИмяПараметраВыбора, ПараметрВыбора);
	КонецЦикла;
	
КонецПроцедуры

// Перенесена из ОперативноеПланированиеФормыУХКлиентСервер
Процедура ПересчитатьСуммуНДС(СтрокаРасшифровки) Экспорт
	
	Если СтрокаРасшифровки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ЗначениеСтавкиНДС = ВстраиваниеОПКВызовСервераПереопределяемый.ПолучитьСтавкуНДС(СтрокаРасшифровки.СтавкаНДС);
	СтрокаРасшифровки.СуммаНДС = СтрокаРасшифровки.Сумма * ЗначениеСтавкиНДС / (100 + ЗначениеСтавкиНДС);
	
КонецПроцедуры

#КонецОбласти
