
#Область ПрограммныйИнтерфейс
	
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Функция возвращает унифицировнную структуру реквизитов договора
Функция СтруктураРеквизитовДоговора() экспорт
	
	СписокРеквизитовДоговора = 
		"Контрагент,
		|Организация,
		|ВалютаВзаиморасчетов,
		|ВалютаПлатежа,
		|ЗапретПлатежаВПрочихВалютах,
		|СпособОпределенияКурсаПлатежа,
		|ДатаФиксацииКурсаПлатежа,
		|СдвигДатыФиксацииКурсаПлатежа,
		|КурсПлатежа,
		|КратностьПлатежа,
		|КурсПлатежаНеМенее,
		|КурсПлатежаНеБолее,
		|КурсПлатежаНеМенееВВалютеОплаты,
		|КурсПлатежаНеБолееВВалютеОплаты,
		|БанковскийСчет,
		|БанковскийСчетКонтрагента,
		|ФиксированныйСчетОрганизации,
		|ФиксированныйСчетКонтрагента,
		|ИдентификаторПлатежа,
		|СтатьяДвиженияДенежныхСредств,
		|ЦФО,
		|Проект,
		|СтатьяИсполнение";
	
	Результат = Новый Структура(СписокРеквизитовДоговора);
	
	Для Сч = 1 По АналитикиСтатейБюджетовУХКлиентСервер.КоличествоАналитикСтатьи() Цикл
		Результат.Вставить("Аналитика"+Сч);
		Результат.Вставить("АналитикаИсполнение"+Сч);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Функция возвращает набор реквизитов договора, унифицированный между решениями 
Функция ПолучитьРеквизитыДоговора(ДоговорСсылка, ВидОперацииУХ = Неопределено) экспорт
	
	//
	СтруктураРеквизитов = СтруктураРеквизитовДоговора();
	ДоговорыСКонтрагентамиВстраивание.СтруктураРеквизитовДоговораРешения(СтруктураРеквизитов, ТипЗнч(ДоговорСсылка), ВидОперацииУХ);
	
	// Получаем реквизиты договора
	РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДоговорСсылка, СтруктураРеквизитов);
	
	// 
	ОбработатьРеквизитыДоговора(РеквизитыДоговора, СтруктураРеквизитов);
	ДоговорыСКонтрагентамиВстраивание.ОбработатьРеквизитыДоговораРешения(РеквизитыДоговора, СтруктураРеквизитов);
	
	Возврат РеквизитыДоговора;
	
КонецФункции

Функция ПолучитьДанныеБюджетирования(Договор, Предназначение, РеквизитыДоговора = неопределено) Экспорт
	
	//
	ДанныеБюджетирования = Новый Структура("СтатьяБюджета, ЦФО, Проект");
	Для Сч = 1 По АналитикиСтатейБюджетовУХКлиентСервер.КоличествоАналитикСтатьи() Цикл
		ДанныеБюджетирования.Вставить("Аналитика"+Сч);
	КонецЦикла;
	
	//
	ТипДоговор = ТипЗнч(Договор);
	ЭтоСтандартныйДоговор = Метаданные.ОпределяемыеТипы.Договоры.Тип.СодержитТип(ТипДоговор);
	Если НЕ ЭтоСтандартныйДоговор Тогда
		ДоговорыСКонтрагентамиВстраивание.ЗаполнитьДанныеБюджетированияДоговораРешения(Договор, Предназначение, ДанныеБюджетирования);
		Возврат ДанныеБюджетирования;
	КонецЕсли;
	
	//
	Если РеквизитыДоговора = неопределено Тогда
		РеквизитыДоговора = ПолучитьРеквизитыДоговора(Договор);
	КонецЕсли;
	
	ДанныеБюджетирования.ЦФО = РеквизитыДоговора.ЦФО;
	ДанныеБюджетирования.Проект = РеквизитыДоговора.Проект;
	
	Если Предназначение = Перечисления.ПредназначенияЭлементовСтруктурыОтчета.БюджетДвиженияДенежныхСредств Тогда
		
		Если ЗначениеЗаполнено(РеквизитыДоговора.СтатьяДвиженияДенежныхСредств) Тогда
			ДанныеБюджетирования.СтатьяБюджета = РеквизитыДоговора.СтатьяДвиженияДенежныхСредств;
			Для Сч = 1 По АналитикиСтатейБюджетовУХКлиентСервер.КоличествоАналитикСтатьи() Цикл
				ДанныеБюджетирования["Аналитика" + Сч] = РеквизитыДоговора["Аналитика" + Сч];
			КонецЦикла;	
		КонецЕсли;
		
	ИначеЕсли Предназначение = Перечисления.ПредназначенияЭлементовСтруктурыОтчета.БюджетДоходовИРасходов Тогда
		
		Если ТипЗнч(РеквизитыДоговора.СтатьяИсполнение) = Тип("СправочникСсылка.СтатьиДоходовИРасходов")	
			И ЗначениеЗаполнено(РеквизитыДоговора.СтатьяИсполнение) Тогда
			
			ДанныеБюджетирования.СтатьяБюджета = РеквизитыДоговора.СтатьяИсполнение;
			Для Сч = 1 По АналитикиСтатейБюджетовУХКлиентСервер.КоличествоАналитикСтатьи() Цикл
				ДанныеБюджетирования["Аналитика" + Сч] = РеквизитыДоговора["АналитикаИсполнение" + Сч];
			КонецЦикла;	
		КонецЕсли;
			
	ИначеЕсли Предназначение = Перечисления.ПредназначенияЭлементовСтруктурыОтчета.БюджетДвиженияРесурсов Тогда
		
		Если ТипЗнч(РеквизитыДоговора.СтатьяИсполнение) = Тип("СправочникСсылка.СтатьиДвиженияРесурсов") 
			И ЗначениеЗаполнено(РеквизитыДоговора.СтатьяИсполнение) Тогда
			
			ДанныеБюджетирования.СтатьяБюджета = РеквизитыДоговора.СтатьяИсполнение;
			Для Сч = 1 По АналитикиСтатейБюджетовУХКлиентСервер.КоличествоАналитикСтатьи() Цикл
				ДанныеБюджетирования["Аналитика" + Сч] = РеквизитыДоговора["АналитикаИсполнение" + Сч];
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДанныеБюджетирования;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура рассчитывает некоторые реквизиты договора
Процедура ОбработатьРеквизитыДоговора(РеквизитыДоговора, СтруктураРеквизитов)
	
КонецПроцедуры

#КонецОбласти

