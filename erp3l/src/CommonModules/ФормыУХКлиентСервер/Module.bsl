
#Область ПрограммныйИнтерфейс

// Добавляет параметр выбора для элемента
Процедура ДобавитьПараметрВыбора(Элемент, Отбор, Значение) экспорт
	
	Если Элемент.ПараметрыВыбора = неопределено Тогда
		Массив = Новый Массив;
	Иначе
		Массив = Новый Массив(Элемент.ПараметрыВыбора);
	КонецЕсли;
	
	НовыйПараметр = Новый ПараметрВыбора(Отбор, Значение);
	
	Индекс = НайтиПараметрПоИмени(Массив, Отбор);
	Если Индекс = неопределено Тогда
		Массив.Добавить(НовыйПараметр);
	Иначе
		Массив[Индекс] = НовыйПараметр;
	КонецЕсли;
	
	Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(Массив);
	
КонецПроцедуры

// Удалить параметр выбора для элемента
Процедура УдалитьПараметрВыбора(Элемент, Отбор) экспорт
	
	Массив = Новый Массив(Элемент.ПараметрыВыбора);
	
	Поз = 0;
	Пока Поз < Массив.Количество() Цикл
		Параметр = Массив[Поз];
		
		Если Параметр.Имя = Отбор Тогда
			Массив.Удалить(Поз);
		Иначе
			Поз = Поз + 1;
		КонецЕсли;
		
	КонецЦикла;
	
	Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(Массив);
	
КонецПроцедуры

// Добавляет связь параметров выбора для элемента
Процедура ДобавитьСвязьПараметровВыбора(Элемент, Отбор, ПутьКДанным) экспорт
	
	Если Элемент.СвязиПараметровВыбора = неопределено Тогда
		Массив = Новый Массив;
	Иначе
		Массив = Новый Массив(Элемент.СвязиПараметровВыбора);
	КонецЕсли;
	
	НоваяСвязь = Новый СвязьПараметраВыбора(Отбор, ПутьКДанным);
	
	Индекс = НайтиПараметрПоИмени(Массив, Отбор);
	Если Индекс = неопределено Тогда
		Массив.Добавить(НоваяСвязь);
	Иначе
		Массив[Индекс] = НоваяСвязь;
	КонецЕсли;
	
	Элемент.СвязиПараметровВыбора = Новый ФиксированныйМассив(Массив);
	
КонецПроцедуры

// Удалить связь параметров выбора для элемента
Процедура УдалитьСвязьПараметровВыбора(Элемент, Отбор, ПутьКДанным = неопределено) экспорт
	
	Массив = Новый Массив(Элемент.СвязиПараметровВыбора);
	
	Поз = 0;
	Пока Поз < Массив.Количество() Цикл
		Связь = Массив[Поз];
		
		Если Связь.Имя = Отбор И (ПутьКДанным = неопределено ИЛИ Связь.ПутьКДанным = ПутьКДанным) Тогда
			Массив.Удалить(Поз);
		Иначе
			Поз = Поз + 1;
		КонецЕсли;
		
	КонецЦикла;
	
	Элемент.СвязиПараметровВыбора = Новый ФиксированныйМассив(Массив);
	
КонецПроцедуры

//  
Процедура ЗаменитьУдалитьПараметрВыбора(Элемент, Отбор, НовыйПараметрВыбора = Неопределено) Экспорт

	МассивПараметров = Новый Массив;
	Для Каждого ПараметрВыбора Из Элемент.ПараметрыВыбора Цикл
		Если ВРег(ПараметрВыбора.Имя) <> ВРег(Отбор) Тогда
			МассивПараметров.Добавить(ПараметрВыбора);
		КонецЕсли;
	КонецЦикла;

	Если НовыйПараметрВыбора <> Неопределено Тогда
		МассивПараметров.Добавить(НовыйПараметрВыбора);
	КонецЕсли;
	
	Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);

КонецПроцедуры

// Процедура корректно снимает АвтоОтметкуНезаполненного для элемента формы
Процедура УстановитьАвтоОтметкуЗаполненияЭлемента(Элемент, ОбязательноеЗаполнение) Экспорт
	
	СтараяАвтоОтметкаНезаполненного = Элемент.АвтоОтметкаНезаполненного;
	
	Элемент.АвтоОтметкаНезаполненного = ОбязательноеЗаполнение;
	
	// Выключение свойства АвтоОтметкаНезаполненного, т.е. перевод его значения из Истина в Ложь, 
	// не приводит к изменению текущего значения свойства ОтметкаНезаполненного.
	Если СтараяАвтоОтметкаНезаполненного = Истина И Элемент.АвтоОтметкаНезаполненного <> Истина Тогда
		Элемент.ОтметкаНезаполненного = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НайтиПараметрПоИмени(Массив, Имя)
	
	Поз = 0;
	Пока Поз < Массив.Количество() Цикл
		Параметр = Массив[Поз];
		Если Параметр.Имя = Имя Тогда
			Возврат Поз;
		КонецЕсли;
		Поз = Поз + 1;
	КонецЦикла;
	
	Возврат неопределено;
	
КонецФункции

#КонецОбласти
 