#Область ПрограммныйИнтерфейс
Функция ДатаАвансаНакладной(ДоговорЗаказ, ДатаНакладной) Экспорт
	
	ВерсияСоглашения = РаботаСДоговорамиКонтрагентовУХ.ВерсияСоглашенияОбъекта(ДоговорЗаказ);
	ПроизводственныйКалендарь = РаботаСДоговорамиКонтрагентовУХ.ПроизводственныйКалендарьОбъекта(ВерсияСоглашения);
	
	Соглашение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорЗаказ, "Соглашение");
	ПараметрыАванса = РасчетГрафиковОперацийЕХ.ПараметрыАвансаПоСоглашению(Соглашение);
	Если ПараметрыАванса = Неопределено Тогда
		Возврат Дата(1, 1, 1);
	КонецЕсли;
			
	ДатаАванса =  РасчетГрафиковОперацийУХ.ДатаОтБазовойДатыСоСдвигом(ДатаНакладной, 
		-ПараметрыАванса.Срок, ПараметрыАванса.ТипСрока, ПроизводственныйКалендарь);
		
	Возврат ДатаАванса;	
		
КонецФункции

Функция ОпорныйГрафикПоНакладной(ОбъектРасчетов, ОбщаяСумма, ДатаОтсчета) Экспорт
	
	Соглашение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектРасчетов, "Соглашение");
	ОпорныйГрафик = РасчетГрафиковОперацийЕХ.ПолучитьГрафикЭтаповОплатыПоСоглашению(ОбщаяСумма, ДатаОтсчета, Соглашение);
	
	Возврат ОпорныйГрафик;
	
КонецФункции

Функция ЭтоДокументПоступления(Документ) Экспорт
	Возврат ТипЗнч(Документ) = Тип("ДокументСсылка.ПоступлениеТоваровНаСклад");
КонецФункции

Функция ТребуетсяАктуализацияГрафикаРасчетов(ДокументГрафика, РеквизитыДокументаГрафика = Неопределено) Экспорт
	
	Если НЕ Метаданные.ОпределяемыеТипы.ВерсияСоглашения.Тип.СодержитТип(ТипЗнч(ДокументГрафика)) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	РеквизитыДокументаГрафика = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументГрафика, "ПорядокРасчетов");
	РежимАктуализацииГрафика = РаботаСДоговорамиКонтрагентовУХ.РежимАктуализацииГрафика(ДокументГрафика);
	РеквизитыДокументаГрафика.Вставить("РежимАктуализацииГрафика", РежимАктуализацииГрафика);
	
	Если НЕ ЗначениеЗаполнено(РежимАктуализацииГрафика) 
		ИЛИ РежимАктуализацииГрафика = Перечисления.РежимыАктуализацииГрафикаПриИзмененииПервичныхДокументов.НеАктуализировать Тогда
		
		// оставляем текущий график
		Возврат Ложь;	
	КонецЕсли;
	
	// для других вариантов графики актуализируются самой ЕРП
	Возврат (РеквизитыДокументаГрафика.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов);

КонецФункции
#КонецОбласти