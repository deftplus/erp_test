#Область ПрограммныйИнтерфейс

Функция СтруктураПараметровРасчетаПроцентов() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("ДатаНачала");
	Результат.Вставить("ДатаОкончания");
	Результат.Вставить("ПравилоПереноса");
	Результат.Вставить("ИзменятьПроцентныйПериод");
	Результат.Вставить("ПроизводственныйКалендарь");
	Результат.Вставить("ПериодичностьУплаты");
	Результат.Вставить("ПериодичностьНачисленияПроцентов");
	Результат.Вставить("ДатаОтсчетаПроцентныхПериодов");
	Результат.Вставить("ДатаПервойВыборки");
	Результат.Вставить("ДатаНачалаДействия");
	Результат.Вставить("ДатаПервогоПогашения");
	Результат.Вставить("ТочкаОтсчетаСдвигаДатыУплаты", ПредопределенноеЗначение("Перечисление.СпособыОпределенияДатыОперацииПоПериоду.ОкончаниеПериода"));
	Результат.Вставить("СдвигДатыУплаты", 0);
	Результат.Вставить("ВидДнейСдвигаУплаты", ПредопределенноеЗначение("Перечисление.СпособыРасчетаКоличестваДнейВПериоде.ПоРабочимДням"));
	Результат.Вставить("ТипПроцентнойСтавки");
	Результат.Вставить("ТочкаОтсчетаДатыФиксацииСтавки");
	Результат.Вставить("СдвигДатыФиксацииСтавки");
	Результат.Вставить("ПроцентнаяСтавка");
	Результат.Вставить("ИндикативнаяСтавка");
	Результат.Вставить("ПлавающаяСтавкаМинимум");
	Результат.Вставить("ПлавающаяСтавкаМаксимум");
	Результат.Вставить("РучноеУправлениеИзменениямиСтавки");
	Результат.Вставить("ПроцентныеСтавки");
	Результат.Вставить("ВыплачиватьПроцентыПериодически");
	Результат.Вставить("ВыплачиватьПроцентыВДатыПогашенияТела");
	Результат.Вставить("ОперацииИзмененияБазы");
	Результат.Вставить("ИмяКолонкиПриход");
	Результат.Вставить("ИмяКолонкиРасход");
	Результат.Вставить("БазаДляРасчетаПроцентов");
	Результат.Вставить("ГраницаФактическихДанных", Дата(1,1,1));
	Результат.Вставить("ДатаПоследнегоНачисления", Дата(1,1,1));
	Результат.Вставить("СуммаНакопленнойЗадолженности", 0);
	Результат.Вставить("МетодСписанияЗадолженности");
	Результат.Вставить("НачислениеПроцентовНаКрайниеДаты", ПредопределенноеЗначение("Перечисление.СпособыНачисленияПроцентовНаГраницыПериодов.НачислятьПроцентыНаПоследнийДень"));
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеЗаполненияОсновногоДолга(Знач ДатаНачалаДействия, Знач ДатаОкончанияДействия, Знач Сумма) Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("ДатаНачалаДействия", ДатаНачалаДействия);
	Результат.Вставить("ДатаОкончанияДействия", ДатаОкончанияДействия);
	Результат.Вставить("Сумма", Сумма);
	
	Возврат Результат;
	
КонецФункции

Функция СуммаПроцентовЗаПериод(ДатаНачала, ДатаОкончания, БазоваяСумма, Ставка, БазаДляРасчета = Неопределено) Экспорт
	
	Возврат БазоваяСумма * Ставка * КоэффициентДлительностиПериода(ДатаНачала, ДатаОкончания, БазаДляРасчета) / 100;
	
КонецФункции

Функция НайтиСекциюГрафикаПоИмениКолонки(ОписаниеГрафика, ИмяКолонки) Экспорт
	
	Результат = Неопределено;
	
	Для Каждого ТекСекцияГрафика Из ОписаниеГрафика Цикл
		
		ДанныеСекции = ТекСекцияГрафика.Значение;
		Если ДанныеСекции.КолонкаПриход = ИмяКолонки ИЛИ ДанныеСекции.КолонкаРасход = ИмяКолонки Тогда
			Результат = ДанныеСекции;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции // НайтиСекциюГрафикаПоИмениКолонки()

// Функция определяет ставку доходности за период в процентах годовых.
Функция СтавкаДоходностиЗаПериод(ДатаНачала, ДатаОкончания, БазоваяСумма, СуммаНаКонецПериода, БазаДляРасчета = Неопределено) Экспорт
	
	Если БазоваяСумма = 0 Тогда
		Возврат 0;
	КонецЕсли;
	КоэффициентДлительностиПериода = КоэффициентДлительностиПериода(ДатаНачала, ДатаОкончания, БазаДляРасчета);
	Если КоэффициентДлительностиПериода = 0 Тогда
		Возврат 0;
	КонецЕсли;
	Возврат (СуммаНаКонецПериода - БазоваяСумма) * 100 / (БазоваяСумма * КоэффициентДлительностиПериода)
	
КонецФункции

//Расчет аналогично функции MS EXCEL - IRR
//Потоки должны быть отсортированы по дате
Функция ПолучитьЭффективнуюСтавкуПроцента(ДенежныйПоток, ТочкиДенежногоПотока, Оценка = 0, ИспользоватьПараметрыРасчетаЭСП = Истина) Экспорт
	
	ЧислоОперацийПотока = ДенежныйПоток.Количество();
	
	ЭСП = Оценка;
	
	Если ИспользоватьПараметрыРасчетаЭСП Тогда
		ПараметрыЭСП = ФинансоваяМатематикаВызовСервера.ПолучитьПараметрыРасчетаЭСП();
	Иначе 
		ПараметрыЭСП = Новый Структура("МаксимальноеЧислоИтерацийРасчетаЭСП,ПорогТочностиРасчетаЭСП", 0, 0);
	КонецЕсли;
		
	ЧислоИтерацийУточнения = ПараметрыЭСП.МаксимальноеЧислоИтерацийРасчетаЭСП;
	ЧислоИтерацийУточнения = ?(ЧислоИтерацийУточнения = 0, 100, ЧислоИтерацийУточнения);
	
	РазрядПорогаТочности = ПараметрыЭСП.ПорогТочностиРасчетаЭСП;
	ПорогТочности = ?(РазрядПорогаТочности = 0, 0.0000001, Pow(10, -РазрядПорогаТочности) );
	
	ИндексСтарта = ИндексСтарта(ДенежныйПоток);
		
	Если ИндексСтарта < 0 Тогда
		Возврат ЭСП;	
	КонецЕсли;
	
	Если ЧислоОперацийПотока <> ТочкиДенежногоПотока.Количество() Тогда
		Возврат ЭСП;
	ИначеЕсли ЧислоОперацийПотока = 0 Тогда
		Возврат ЭСП;
	КонецЕсли;
	
	Если ИндексСтарта = 0 Тогда
		Если Оценка <= - 1 Тогда
			Возврат ЭСП;
		Иначе
			Показатель = Оценка;
		КонецЕсли;
	Иначе	
		Показатель = ЭСП;
	КонецЕсли; 
	
	Див	= ПолучитьЧистуюПриведеннуюСтоимость(Показатель, ДенежныйПоток, ТочкиДенежногоПотока, , Истина);
	Если Див = 0 Тогда
		Возврат ЭСП;	
	КонецЕсли; 
	
	ЧПС = ПолучитьЧистуюПриведеннуюСтоимость(Показатель, ДенежныйПоток, ТочкиДенежногоПотока);
		
	Для НомерОперации = 1 По ЧислоИтерацийУточнения Цикл
		
		ЭСП	= ЭСП - ЧПС / Див;
		
		ЧПС = ПолучитьЧистуюПриведеннуюСтоимость(ЭСП, ДенежныйПоток, ТочкиДенежногоПотока);
		
		Если Макс(ЧПС, -ЧПС) < ПорогТочности Тогда
			Возврат ЭСП;
		КонецЕсли;
		
	КонецЦикла; 
	
	Возврат ЭСП;
		
КонецФункции

//Расчет аналогично функции MS EXCEL - NPV
//Потоки должны быть отсортированы по дате
Функция ПолучитьЧистуюПриведеннуюСтоимость(Ставка, ДенежныйПоток, ТочкиДенежногоПотока, НаКонецПериода = Истина, Производная = Ложь) Экспорт
	
	Если Ставка <= -1 Тогда
		Возврат 0;	
	КонецЕсли;
	
	ЧислоОпераций = ДенежныйПоток.Количество();
	
	Если ЧислоОпераций = 0 Тогда 
		Возврат 0;	
	ИначеЕсли ЧислоОпераций <> ТочкиДенежногоПотока.Количество() Тогда
		Возврат 0;	
	КонецЕсли;
	
	ЧислоОперацийПотока	= ДенежныйПоток.Количество();
	НачальнаяДата		= ТочкиДенежногоПотока[0];
	
	ЧПС = 0;
	
	Для НомерОперации = 1 По ЧислоОперацийПотока - 1 Цикл
		
		ЧислоДней = РазностьДатВДнях(ТочкиДенежногоПотока[ЧислоОперацийПотока - НомерОперации], НачальнаяДата);
		ТекущийДенежныйПоток = ДенежныйПоток[ЧислоОперацийПотока - НомерОперации];
		Если Производная Тогда
			ЧПС	= ЧПС + ТекущийДенежныйПоток / Pow((1 + Ставка), ЧислоДней / 365+1) * ( - ЧислоДней / 365);			
		Иначе 
			ЧПС	= ЧПС + ТекущийДенежныйПоток / Pow((1 + Ставка), ЧислоДней / 365);						
		КонецЕсли;
		
	КонецЦикла; 
	
	ЧПС	= ЧПС + ?(Производная, 0, ДенежныйПоток[0]);
	
	Возврат ЧПС;
		
КонецФункции

Функция РазностьДатВДнях(ДатаУменьшаемое, ДатаВычитаемое)
	Возврат (ДатаУменьшаемое - ДатаВычитаемое) / 86400; // 86400 = 24*60*60 - число секунд в дне
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КоэффициентДлительностиПериода(ДатаНачала, ДатаОкончания, БазаДляРасчета = Неопределено)
	
	Коэффициент = 0;
	
	Если Не ЗначениеЗаполнено(БазаДляРасчета)
		ИЛИ БазаДляРасчета = ПредопределенноеЗначение("Справочник.СпособыРасчетаДнейПериодическихОпераций.ГодФактМесяцФакт") Тогда
		
		ГодНачала = Год(ДатаНачала);
		ГодОкончания = Год(ДатаОкончания);
		
		Если ГодНачала = ГодОкончания Тогда
			
			Коэффициент = ((ДатаОкончания - ДатаНачала)/86400 + 1) / ДеньГода(КонецГода(ДатаНачала));
			
		Иначе
			
			Для ТекГод = ГодНачала По ГодОкончания Цикл
				НачалоИнтервала = Макс(ДатаНачала, Дата(ТекГод,1,1));
				ОкончаниеИнтервала = Мин(ДатаОкончания, Дата(ТекГод,12,31));
				
				Коэффициент = Коэффициент + ((ОкончаниеИнтервала - НачалоИнтервала)/86400 + 1) / ДеньГода(КонецГода(НачалоИнтервала));
				
			КонецЦикла;
			
		КонецЕсли;
		
	ИначеЕсли БазаДляРасчета = ПредопределенноеЗначение("Справочник.СпособыРасчетаДнейПериодическихОпераций.Год360МесяцФакт") Тогда
		
		Коэффициент = (ДатаОкончания - ДатаНачала)/86400 / 360;
		
	ИначеЕсли БазаДляРасчета = ПредопределенноеЗначение("Справочник.СпособыРасчетаДнейПериодическихОпераций.Год360Месяц30") Тогда
		
		Коэффициент = (360 * (Год(ДатаОкончания) - Год(ДатаНачала)) + 30*(Месяц(ДатаОкончания) - Месяц(ДатаНачала)) + (День(ДатаОкончания) - День(ДатаНачала)))/360
		
	КонецЕсли;
	
	Возврат Коэффициент;
	
КонецФункции

Функция ИндексСтарта(ДенежныйМассив)
	
	Сч = ДенежныйМассив.Количество() - 1;
	
	Пока Сч >= 0 И ДенежныйМассив[Сч] >= 0 Цикл
		Сч = Сч - 1;		
	КонецЦикла; 
	
	Сч	= Сч + 1;
	
	Если Сч > 0 Тогда
		
		Показатель = 0;
		Счетчик = 0;
		
		Пока Счетчик < Сч Цикл
			
			Показатель	= Показатель + ДенежныйМассив[Счетчик];
			Если Показатель >= 0 Тогда
				Сч = 0;
				Прервать;
			КонецЕсли; 
			Счетчик	= Счетчик + 1;
			
		КонецЦикла; 
		
	КонецЕсли; 
	
	Возврат Сч;
	
КонецФункции

#КонецОбласти
