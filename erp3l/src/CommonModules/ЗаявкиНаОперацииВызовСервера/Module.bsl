
#Область ПрограммныйИнтерфейс

Функция ВидБюджетаЗаявки(МетаданныеИлиСсылка) Экспорт
	
	Если МетаданныеИлиСсылка = Метаданные.Документы.ЗаявкаНаРасходованиеДенежныхСредств
		Или МетаданныеИлиСсылка = Метаданные.Документы.ОжидаемоеПоступлениеДенежныхСредств
		Или ТипЗнч(МетаданныеИлиСсылка) = Тип("ДокументСсылка.ЗаявкаНаРасходованиеДенежныхСредств") 
		Или ТипЗнч(МетаданныеИлиСсылка) = Тип("ДокументСсылка.ОжидаемоеПоступлениеДенежныхСредств") Тогда
		
		Возврат Перечисления.ПредназначенияЭлементовСтруктурыОтчета.БюджетДвиженияДенежныхСредств;
		
	ИначеЕсли МетаданныеИлиСсылка = Метаданные.Документы.ЗаявкаНаРасход
		Или МетаданныеИлиСсылка = Метаданные.Документы.ПланируемыйДоход
		Или ТипЗнч(МетаданныеИлиСсылка) = Тип("ДокументСсылка.ЗаявкаНаРасход") 
		Или ТипЗнч(МетаданныеИлиСсылка) = Тип("ДокументСсылка.ПланируемыйДоход") Тогда
		
		Возврат Перечисления.ПредназначенияЭлементовСтруктурыОтчета.БюджетДоходовИРасходов;
		
	Иначе
		
		Возврат Неопределено
		
	КонецЕсли;
	
КонецФункции
	
Функция ПоляДанныхПредставления(ИмяДокумента) Экспорт
	Возврат ЗаявкиНаОперацииПовтИсп.ПоляДанныхПредставления(ИмяДокумента);
КонецФункции

Функция СтатьяДДСПоЭлементуСтруктурыЗадолженности(ДоговорКонтрагента, ЭлементСтруктурыЗадолженности, НаправлениеДвижения) Экспорт
	
	Если Не ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ГруппаОперацийГрафиков = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "ВидДоговораУХ.ГруппаОперацийГрафика"); 
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НастройкиФинансовыхИнструментов.СтатьяБюджета КАК СтатьяБюджета
	|ИЗ
	|	РегистрСведений.НастройкиФинансовыхИнструментов КАК НастройкиФинансовыхИнструментов
	|ГДЕ
	|	НастройкиФинансовыхИнструментов.Операция.ЭлементСтруктурыЗадолженности = &ЭлементСтруктурыЗадолженности
	|	И НастройкиФинансовыхИнструментов.Операция.ВидБюджета = &ВидБюджета
	|	И НастройкиФинансовыхИнструментов.Операция.НаправлениеДвижения = &НаправлениеДвижения
	|	И НастройкиФинансовыхИнструментов.Операция В ИЕРАРХИИ(&ГруппаОперацийГрафика)"
	);
	Запрос.УстановитьПараметр("ЭлементСтруктурыЗадолженности", ЭлементСтруктурыЗадолженности);
	Запрос.УстановитьПараметр("ВидБюджета", ПланыВидовХарактеристик.ВидыБюджетов.БюджетДвиженияДенежныхСредств);
	Запрос.УстановитьПараметр("НаправлениеДвижения", НаправлениеДвижения);
	Запрос.УстановитьПараметр("ГруппаОперацийГрафика", ГруппаОперацийГрафиков);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.СтатьяБюджета;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция СтатьиДДСПоДоговору(ДоговорКонтрагента, НаправлениеДвижения) Экспорт
		
	ГруппаОперацийГрафиков = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "ВидДоговораУХ.ГруппаОперацийГрафика"); 
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НастройкиФинансовыхИнструментов.СтатьяБюджета КАК СтатьяБюджета,
	|	НастройкиФинансовыхИнструментов.Операция.ЭлементСтруктурыЗадолженности КАК ЭлементСтруктурыЗадолженности	
	|ИЗ
	|	РегистрСведений.НастройкиФинансовыхИнструментов КАК НастройкиФинансовыхИнструментов
	|ГДЕ
	|	НастройкиФинансовыхИнструментов.Операция.ВидБюджета = &ВидБюджета
	|	И НастройкиФинансовыхИнструментов.Операция.НаправлениеДвижения = &НаправлениеДвижения
	|	И НастройкиФинансовыхИнструментов.Операция В ИЕРАРХИИ(&ГруппаОперацийГрафика)"
	);
	Запрос.УстановитьПараметр("ВидБюджета", ПланыВидовХарактеристик.ВидыБюджетов.БюджетДвиженияДенежныхСредств);
	Запрос.УстановитьПараметр("НаправлениеДвижения", НаправлениеДвижения);
	Запрос.УстановитьПараметр("ГруппаОперацийГрафика", ГруппаОперацийГрафиков);
	
	Возврат Запрос.Выполнить().Выгрузить();
			
КонецФункции

Функция ЭлементыСтруктурыЗадолженностиПоДоговору(ДоговорКонтрагента, Ссылка) Экспорт
	
	ЭлементыПоУмолчанию = Новый Массив;
	ЭлементыПоУмолчанию.Добавить(Перечисления.ЭлементыСтруктурыЗадолженности.ОсновнойДолг);
	
	Если НЕ ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		 Возврат ЭлементыПоУмолчанию;		 
	КонецЕсли;
	
	ГруппаОпераций = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "ВидДоговораУХ.ГруппаОперацийГрафика");
	Если НЕ ЗначениеЗаполнено(ГруппаОпераций) Тогда
		Возврат ЭлементыПоУмолчанию;
	КонецЕсли;            
	
	НаправлениеДвижения = ЗаявкиНаОперацииКлиентСервер.НаправлениеДвиженияДокумента(Ссылка);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОперацииГрафиковДоговоров.ЭлементСтруктурыЗадолженности КАК ЭлементСтруктурыЗадолженности
	|ИЗ
	|	Справочник.ОперацииГрафиковДоговоров КАК ОперацииГрафиковДоговоров
	|ГДЕ
	|	ОперацииГрафиковДоговоров.НаправлениеДвижения = &НаправлениеДвижения
	|	И ОперацииГрафиковДоговоров.ВидБюджета = &ВидБюджета
	|	И ОперацииГрафиковДоговоров.Ссылка В ИЕРАРХИИ (&ГруппаОпераций)
	|	И НЕ ОперацииГрафиковДоговоров.ЭтоГруппа"
	);
	Запрос.УстановитьПараметр("НаправлениеДвижения", НаправлениеДвижения);
	Запрос.УстановитьПараметр("ВидБюджета", ПланыВидовХарактеристик.ВидыБюджетов.БюджетДвиженияДенежныхСредств);
	Запрос.УстановитьПараметр("ГруппаОпераций", ГруппаОпераций); 
	
	МассивЭлементов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ЭлементСтруктурыЗадолженности");
	
	Если МассивЭлементов.Количество() = 0 Тогда
		 МассивЭлементов = ЭлементыПоУмолчанию;
	КонецЕсли;
	
	
	Возврат МассивЭлементов;
		
КонецФункции

Функция ОсновнойЭлементСтруктурыЗадолженности(ДоговорКонтрагента, Ссылка) Экспорт

	ЭлементыСтруктурыЗадолженности = ЭлементыСтруктурыЗадолженностиПоДоговору(ДоговорКонтрагента, Ссылка);
	
	Если ЭлементыСтруктурыЗадолженности.Найти(Перечисления.ЭлементыСтруктурыЗадолженности.ОсновнойДолг) <> Неопределено Тогда
		ОсновнойЭлемент = Перечисления.ЭлементыСтруктурыЗадолженности.ОсновнойДолг;
	Иначе 
		ОсновнойЭлемент = ЭлементыСтруктурыЗадолженности[0];
	КонецЕсли;
	
	Возврат ОсновнойЭлемент;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
	
#КонецОбласти
 