#Область ПрограммныйИнтерфейс


// Проверяет, что анкета поставщика - участвует в закрытой закупке.
//
// Параметры:
//  ЗакупочнаяПроцедура - СправочникСсылка.ЗакупочныеПроцедуры - 
//		закупка для которой нужно проверить участие поставщика.
//	АнкетаПоставщика - СправоченикСсылка.АнкетыПоставщиков -
//		анкета поставщика, которого нужно проверить на участие.
// 
// Возвращаемое значение:
//   - Булево - это анкета участника закрытой закупочной процедуры.
//
Функция ЭтоАнкетаУчастникаЗакрытойЗакупки(ЗакупочнаяПроцедура,
										 АнкетаПоставщика) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	1 КАК Поле1
		|ИЗ
		|	РегистрСведений.УчастникиЗакупок КАК УчастникиЗакупок
		|ГДЕ
		|	УчастникиЗакупок.ЗакупочнаяПроцедура = &ЗакупочнаяПроцедура
		|	И УчастникиЗакупок.Участник = &АнкетаПоставщика
		|	И УчастникиЗакупок.ЗакупочнаяПроцедура.ЗакрытаяЗакупка
		|	И НЕ УчастникиЗакупок.ЗакупочнаяПроцедура.ТребуетсяКвалификационныйОтбор";
	Запрос.УстановитьПараметр("ЗакупочнаяПроцедура", ЗакупочнаяПроцедура);
	Запрос.УстановитьПараметр("АнкетаПоставщика", АнкетаПоставщика);
	РезультатЗапроса = Запрос.Выполнить();
	Возврат НЕ РезультатЗапроса.Пустой();
КонецФункции

// Проверяет, что контрагент участвует в закрытой закупке.
//
// Параметры:
//  ЗакупочнаяПроцедура - СправочникСсылка.ЗакупочныеПроцедуры - 
//		закупка для которой нужно проверить участие поставщика.
//	Контрагент - СправоченикСсылка.Контрагенты -
//		контрагент, которого нужно проверить на участие.
// 
// Возвращаемое значение:
//   - Булево - это контрагент участвуеи в закрытой закупочной процедуре.
//
Функция ЭтоКонтрагентУчастникЗакупки(ЗакупочнаяПроцедура,
										 	 Контрагент) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	1 КАК Поле1
		|ИЗ
		|	РегистрСведений.УчастникиЗакупок КАК УчастникиЗакупок
		|ГДЕ
		|	УчастникиЗакупок.ЗакупочнаяПроцедура = &ЗакупочнаяПроцедура
		|	И (УчастникиЗакупок.Участник.Контрагент = &Контрагент ИЛИ УчастникиЗакупок.Участник = &Контрагент)";
	Запрос.УстановитьПараметр("ЗакупочнаяПроцедура", ЗакупочнаяПроцедура);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	РезультатЗапроса = Запрос.Выполнить();
	Возврат НЕ РезультатЗапроса.Пустой();
КонецФункции

// Возвращает массив контрагентов допущенных к участию
// в закрытой закупочной процедуре.
//
Функция ПолучитьКонтрагентовЗакупок(ЗакупочнаяПроцедура) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(УчастникиЗакупок.Участник) = ТИП(Справочник.Контрагенты)
		|			ТОГДА УчастникиЗакупок.Участник
		|		ИНАЧЕ УчастникиЗакупок.Участник.Контрагент
		|	КОНЕЦ КАК УчастникКонтрагент
		|ИЗ
		|	РегистрСведений.УчастникиЗакупок КАК УчастникиЗакупок
		|ГДЕ
		|	УчастникиЗакупок.ЗакупочнаяПроцедура = &ЗакупочнаяПроцедура
		|	И ВЫБОР
		|			КОГДА ТИПЗНАЧЕНИЯ(УчастникиЗакупок.Участник) = ТИП(Справочник.АнкетыПоставщиков)
		|				ТОГДА УчастникиЗакупок.Участник.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ";
	Запрос.УстановитьПараметр("ЗакупочнаяПроцедура", ЗакупочнаяПроцедура);
	РезультатЗапроса = Запрос.Выполнить();
	Возврат РезультатЗапроса
		.Выгрузить(ОбходРезультатаЗапроса.Прямой)
		.ВыгрузитьКолонку(0);
КонецФункции


#КонецОбласти