////////////////////////////////////////////////////////////////////////////////
// Отражение зарплаты в бухгалтерском учете.
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает структуру с настройкой бухучета сотрудника на указанную дату.
//
// Параметры:
//  Сотрудник - Тип СправочникСсылка.Сотрудник
//  ДатаАктуальности - Тип Дата, дата на которую получаем способ отражения.
//
// Возвращаемое значение: Структура, ключи СпособОтраженияЗарплатыВБухучете, ОтношениеКЕНВД, Период
//		СпособОтраженияЗарплатыВБухучете, тип Справочник.СпособыОтраженияЗарплатыВБухУчете
//		ОтношениеКЕНВД, тип Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.
//		Период, дата на которую установлено значение.
//
Функция НастройкаБухучетаЗарплатыСотрудника(Сотрудник, ДатаАктуальности) Экспорт
	
	БухучетСотрудника = Новый Структура("СтатьяФинансирования, СтатьяРасходов, СпособОтраженияЗарплатыВБухучете, ОтношениеКЕНВД, Период");
	БухучетСотрудника.СтатьяФинансирования = Справочники.СтатьиФинансированияЗарплата.ПустаяСсылка();
	БухучетСотрудника.СтатьяРасходов = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	БухучетСотрудника.СпособОтраженияЗарплатыВБухучете = Справочники.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка();
	БухучетСотрудника.ОтношениеКЕНВД = Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка();
	БухучетСотрудника.Период = Дата(1,1,1);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаСреза", ДатаАктуальности);
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БухучетЗарплатыСотрудников.Период,
	|	ВЫБОР
	|		КОГДА БухучетЗарплатыСотрудников.ДействуетДо <> ДАТАВРЕМЯ(1, 1, 1)
	|				И БухучетЗарплатыСотрудников.ДействуетДо <= &ДатаСреза
	|			ТОГДА БухучетЗарплатыСотрудников.СтатьяФинансированияПоОкончании
	|		ИНАЧЕ БухучетЗарплатыСотрудников.СтатьяФинансирования
	|	КОНЕЦ КАК СтатьяФинансирования,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА БухучетЗарплатыСотрудников.ДействуетДо <> ДАТАВРЕМЯ(1, 1, 1)
	|				И БухучетЗарплатыСотрудников.ДействуетДо <= &ДатаСреза
	|			ТОГДА БухучетЗарплатыСотрудников.СпособОтраженияЗарплатыВБухучетеПоОкончании
	|		ИНАЧЕ БухучетЗарплатыСотрудников.СпособОтраженияЗарплатыВБухучете
	|	КОНЕЦ КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА БухучетЗарплатыСотрудников.ДействуетДо <> ДАТАВРЕМЯ(1, 1, 1)
	|				И БухучетЗарплатыСотрудников.ДействуетДо <= &ДатаСреза
	|			ТОГДА БухучетЗарплатыСотрудников.ОтношениеКЕНВДПоОкончании
	|		ИНАЧЕ БухучетЗарплатыСотрудников.ОтношениеКЕНВД
	|	КОНЕЦ КАК ОтношениеКЕНВД
	|ИЗ
	|	РегистрСведений.БухучетЗарплатыСотрудников.СрезПоследних(&ДатаСреза, Сотрудник = &Сотрудник) КАК БухучетЗарплатыСотрудников";
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(БухучетСотрудника, Выборка);
	КонецЕсли;
	
	Возврат БухучетСотрудника;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область Свойства

// См. УправлениеСвойствамиПереопределяемый.ПриПолученииПредопределенныхНаборовСвойств.
Процедура ПриПолученииПредопределенныхНаборовСвойств(Наборы) Экспорт
	
	УправлениеСвойствамиБЗК.ЗарегистрироватьНаборСвойств(Наборы, "d42dbf2d-9802-11e9-80cd-4cedfb43b11a", Метаданные.Документы.БухучетНачисленийСотрудников);
	УправлениеСвойствамиБЗК.ЗарегистрироватьНаборСвойств(Наборы, "d42dbeee-9802-11e9-80cd-4cedfb43b11a", Метаданные.Документы.ПереносЗатратНаПерсоналМеждуСтатьями);
	УправлениеСвойствамиБЗК.ЗарегистрироватьНаборСвойств(Наборы, "d42dbfa7-9802-11e9-80cd-4cedfb43b11a", Метаданные.Документы.РаспределениеОсновногоЗаработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаНачисленийУдержанийПередЗаписью

Процедура УдержаниеПередЗаписью(ВидРасчетаОбъект) Экспорт
	
	УстановитьОчередностьОтраженияВУчете(ВидРасчетаОбъект);
	
	// Уточнение стратегии отражения в бухучете.
	СтратегияОтраженияВУчетеПоБазе = ВидРасчетаОбъект.КатегорияУдержания = Перечисления.КатегорииУдержаний.ИсполнительныйЛист
		Или ВидРасчетаОбъект.КатегорияУдержания = Перечисления.КатегорииУдержаний.ВознаграждениеПлатежногоАгента
		Или РасчетЗарплатыРасширенный.ЕстьПоказательВКоллекции(ВидРасчетаОбъект.Показатели, "РасчетнаяБаза");
		
	Если СтратегияОтраженияВУчетеПоБазе И ВидРасчетаОбъект.СтратегияОтраженияВУчете <> Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам Тогда
		ВидРасчетаОбъект.СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам;
	ИначеЕсли Не СтратегияОтраженияВУчетеПоБазе И ВидРасчетаОбъект.СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам Тогда
		ВидРасчетаОбъект.СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоФактическимНачислениям;
	КонецЕсли;
	
	ВидРасчетаОбъект.ДоступнаСтратегияОтраженияКакЗаданоВидуРасчета = Не СтратегияОтраженияВУчетеПоБазе;
	
	// Уточнение значения свойства ЯвляетсяОснованиемОформленияКассовогоЧека.
	Если ВидРасчетаОбъект.ЯвляетсяОснованиемОформленияКассовогоЧека Тогда
		МожетЯвляетсяОснованием = ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.УдержаниеМожетЯвляетсяОснованиемОформленияКассовогоЧека(ВидРасчетаОбъект.КатегорияУдержания, ВидРасчетаОбъект.ВидОперацииПоЗарплате);
		Если Не МожетЯвляетсяОснованием Тогда
			ВидРасчетаОбъект.ЯвляетсяОснованиемОформленияКассовогоЧека = Ложь;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Заполняет реквизит ОчередностьОтраженияВУчете для начислений и удержаний.
//
Процедура УстановитьОчередностьОтраженияВУчете(ВидРасчетаОбъект) Экспорт 

	Если ВидРасчетаОбъект.СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам Тогда 
		ВидРасчетаОбъект.ОчередностьОтраженияВУчете = ВидРасчетаОбъект.ОчередностьРасчета;
	Иначе 
		ВидРасчетаОбъект.ОчередностьОтраженияВУчете = 1;
	КонецЕсли;
	
КонецПроцедуры

Процедура УточнитьСтратегиюОтраженияВУчетеНачисления(ВидРасчетаОбъект) Экспорт

	ИспользоватьСтатьиФинансирования = ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата");
	РаботаВБюджетномУчреждении = ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении");
	ИспользуетсяЕНВД = ПолучитьФункциональнуюОпцию("ИспользуетсяЕНВД");
	
	Если РаботаВБюджетномУчреждении И ВидРасчетаОбъект.ОтношениеКЕНВД = Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ОпределяетсяЕжемесячноПроцентом Тогда
		ВидРасчетаОбъект.ОтношениеКЕНВД = Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка();
	КонецЕсли;
	
	// Для оплаты дней ухода игнорируем статью расходов при вычислении стратегии отражения в бухучете.
	ЭтоОплатаДнейУходаЗаДетьмиИнвалидами = ВидРасчетаОбъект.КатегорияНачисленияИлиНеоплаченногоВремени = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДнейУходаЗаДетьмиИнвалидами
						Или ВидРасчетаОбъект.КатегорияНачисленияИлиНеоплаченногоВремени = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ДенежноеСодержаниеОплатаДнейУходаЗаДетьмиИнвалидами;
	
	СтратегияОтраженияВУчете = ВидРасчетаОбъект.СтратегияОтраженияВУчете;
	СтатьяФинансирования = ВидРасчетаОбъект.СтатьяФинансирования;
	СтатьяРасходов = ВидРасчетаОбъект.СтатьяРасходов;
	СпособОтраженияЗарплатыВБухучете = ВидРасчетаОбъект.СпособОтраженияЗарплатыВБухучете;
	ОтношениеКЕНВД = ВидРасчетаОбъект.ОтношениеКЕНВД;
	
	СтатьяФинансированияЗаполнена = ИспользоватьСтатьиФинансирования И Не СтатьяФинансирования.Пустая();
	СтатьяРасходовЗаполнена = РаботаВБюджетномУчреждении И Не ЭтоОплатаДнейУходаЗаДетьмиИнвалидами И Не СтатьяРасходов.Пустая();
	СпособОтраженияЗаполнен = Не СпособОтраженияЗарплатыВБухучете.Пустая();
	ОтношениеКЕНВДЗаполнено = ИспользуетсяЕНВД И Не ОтношениеКЕНВД.Пустая();
	НастройкаЗаполнена = СпособОтраженияЗаполнен Или ОтношениеКЕНВДЗаполнено Или СтатьяФинансированияЗаполнена Или СтатьяРасходовЗаполнена;
	
	ЭтоПособиеЗаСчетФСС 	= УчетПособийСоциальногоСтрахованияРасширенный.НачислениеЯвляетсяПособиемЗаСчетФСС(ВидРасчетаОбъект);
	НачислениеБезОплаты 	= ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.КатегорияНачисленийБезОплаты(ВидРасчетаОбъект.КатегорияНачисленияИлиНеоплаченногоВремени);
	ОплатаПоСреднему    	= ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.НачислениеОплатаПоСреднемуОбщий(ВидРасчетаОбъект);
	ОплатаПоСохраняемомуДС 	= ВидРасчетаОбъект.Рассчитывается И ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.КатегорияНачисленийПоСохраняемомуДС(ВидРасчетаОбъект.КатегорияНачисленияИлиНеоплаченногоВремени);
	
	Если СтратегияОтраженияВУчете.Пустая() Тогда
		// Начальное заполнение стратегии.
		Если ЭтоПособиеЗаСчетФСС Тогда
			СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоДаннымОСотрудникеИЕгоПлановыхНачислениях;
		ИначеЕсли НачислениеБезОплаты Тогда
			СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоДаннымОСотрудникеИЕгоПлановыхНачислениях;
		ИначеЕсли НастройкаЗаполнена Тогда
			СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.КакЗаданоВидуРасчета;
		Иначе
			СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоДаннымОСотрудникеИЕгоПлановыхНачислениях;
		КонецЕсли;
	ИначеЕсли Не НастройкаЗаполнена И СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.КакЗаданоВидуРасчета Тогда
		СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоДаннымОСотрудникеИЕгоПлановыхНачислениях;
	ИначеЕсли НастройкаЗаполнена Тогда
		СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.КакЗаданоВидуРасчета;
	ИначеЕсли СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазеСреднегоЗаработка Тогда
		
		Если НЕ ЭтоПособиеЗаСчетФСС И Не ОплатаПоСреднему
				И НЕ ВидРасчетаОбъект.КатегорияНачисленияИлиНеоплаченногоВремени = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоЛистаЗаСчетРаботодателя Тогда
			СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоДаннымОСотрудникеИЕгоПлановыхНачислениях;
		КонецЕсли;	
		
	ИначеЕсли СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам Тогда	
		
		Если Не ОплатаПоСохраняемомуДС И ЭтоПособиеЗаСчетФСС Или ОплатаПоСреднему
				Или ВидРасчетаОбъект.КатегорияНачисленияИлиНеоплаченногоВремени = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоЛистаЗаСчетРаботодателя Тогда
			СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоДаннымОСотрудникеИЕгоПлановыхНачислениях;
		Иначе
			
			БазаДоступна = ОплатаПоСохраняемомуДС Или ВидРасчетаОбъект.ТребуетсяРасчетБазы
			И ВидРасчетаОбъект.СпособРасчета <> Перечисления.СпособыРасчетаНачислений.ДоплатаДоСреднегоЗаработка
			И ВидРасчетаОбъект.СпособРасчета <> Перечисления.СпособыРасчетаНачислений.ДоплатаДоСреднегоЗаработкаФСС
			И ВидРасчетаОбъект.СпособРасчета <> Перечисления.СпособыРасчетаНачислений.ДоплатаДоСохраняемогоДенежногоСодержанияЗаДниБолезни
			И ВидРасчетаОбъект.ПериодРасчетаБазовыхНачислений = Перечисления.ПериодыРасчетаБазовыхНачислений.ТекущийМесяц;
			
			Если Не БазаДоступна Тогда
				СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоДаннымОСотрудникеИЕгоПлановыхНачислениях;	
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НастройкаЗаполнена И СтратегияОтраженияВУчете <> Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.КакЗаданоВидуРасчета Тогда
		ВидРасчетаОбъект.СтатьяФинансирования 				= Справочники.СтатьиФинансированияЗарплата.ПустаяСсылка();
		ВидРасчетаОбъект.СтатьяРасходов 					= Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
		ВидРасчетаОбъект.СпособОтраженияЗарплатыВБухучете 	= Справочники.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка();
		ВидРасчетаОбъект.ОтношениеКЕНВД 					= Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка();
	КонецЕсли;
	
	ВидРасчетаОбъект.СтратегияОтраженияВУчете = СтратегияОтраженияВУчете;
	
КонецПроцедуры

Процедура УточнитьВидОперацииПоЗарплатеНачисления(ВидРасчетаОбъект) Экспорт
	
	Если ВидРасчетаОбъект.КатегорияНачисленияИлиНеоплаченногоВремени = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаОтпуска
		Или ВидРасчетаОбъект.КатегорияНачисленияИлиНеоплаченногоВремени = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ДенежноеСодержаниеНаПериодОтпуска Тогда
		
		ОтпускЯвляетсяЕжегодным = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидРасчетаОбъект.ВидОтпуска,"ОтпускЯвляетсяЕжегодным");
		Если ОтпускЯвляетсяЕжегодным <> Неопределено И ОтпускЯвляетсяЕжегодным И ВидРасчетаОбъект.ВидОперацииПоЗарплате <> Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпуск Тогда
			ВидРасчетаОбъект.ВидОперацииПоЗарплате = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпуск;
		КонецЕсли;
	ИначеЕсли ВидРасчетаОбъект.КатегорияНачисленияИлиНеоплаченногоВремени = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.КомпенсацияОтпуска
		Или ВидРасчетаОбъект.КатегорияНачисленияИлиНеоплаченногоВремени = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ДенежноеСодержаниеКомпенсацияОтпуска Тогда
		
		ОтпускЯвляетсяЕжегодным = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидРасчетаОбъект.ВидОтпуска,"ОтпускЯвляетсяЕжегодным");
		Если ОтпускЯвляетсяЕжегодным <> Неопределено И ОтпускЯвляетсяЕжегодным И ВидРасчетаОбъект.ВидОперацииПоЗарплате <> Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпуска Тогда
			ВидРасчетаОбъект.ВидОперацииПоЗарплате = Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпуска;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СведенияОБухучетеЗарплаты

// Формирует временную таблицу ВТСведенияОБухучетеЗарплатыСотрудников, список сотрудников и периодов,
// по которым необходимо получить данные, берутся из временной таблицы в менеджере временных
// таблиц, переданном в качестве параметра. Временная таблица обязательно должна содержать
// колонки Сотрудник и Период.
//		Поля "Сотрудник,Период" можно переопределить в параметре ИменаПолейВременнойТаблицы.
//
//	Структура таблицы ВТСведенияОБухучетеЗарплатыСотрудников.
//		Организация
//		Подразделение
//		Сотрудник
//		Период
//		СтатьяФинансирования
//		СтатьяРасходов
//		СпособОтраженияЗарплатыВБухучете
//		ОтношениеКЕНВД.
//
// Параметры:
//		МенеджерВременныхТаблиц - содержит временную таблицу с именем, указанным в параметре ИмяВременнойТаблицы.
//		Организация - если значение Неопределенно, то должно быть поле Организация в таблице ИмяВременнойТаблицы.
//		Подразделение - если значение Неопределенно, то должно быть поле Подразделение в таблице ИмяВременнойТаблицы.
//		ИмяВременнойТаблицы - временная таблица с полями Организация, Подразделение, Сотрудник, Период.
//			Поля Организация и Подразделение могут отсутствовать, если заполнены параметр Организация, Подразделение.
//			Имена полей Сотрудник и Период можно переопределить в параметре ИменаПолейВременнойТаблицы.
//		ИменаПолейВременнойТаблицы - строка, имена полей Сотрудник и Период временной таблицы ИмяВременнойТаблицы.
//
Процедура СоздатьВТСведенияОБухучетеЗарплатыСотрудников(МенеджерВременныхТаблиц, ИмяВременнойТаблицы, ИменаПолейВременнойТаблицы = "Сотрудник,Период", Организация = Неопределено, Подразделение = Неопределено, ТерриторияВыполненияРаботВОрганизации = Неопределено ) Экспорт
	
	МассивИменПолейОтбора = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИменаПолейВременнойТаблицы, ",");
	ИмяПоляСотрудник     = "Таблица." + МассивИменПолейОтбора[0];
	ИмяПоляПериод        = "Таблица." + МассивИменПолейОтбора[1];
	
	ИспользоватьСтатьиФинансирования = ЗарплатаКадры.ИспользоватьСтатьиФинансированияЗарплата();
	
	СтатьяОплатаТруда = СтатьяОплатаТруда();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ИспользоватьСтатьиФинансирования", ИспользоватьСтатьиФинансирования);
	Запрос.УстановитьПараметр("СтатьяОплатаТруда", СтатьяОплатаТруда);
	
	ОрганизацияВТаблице   = Организация = Неопределено;
	ПодразделениеВТаблице = Подразделение = Неопределено;
	ТерриторияВТаблице    = ТерриторияВыполненияРаботВОрганизации = Неопределено;
	
	УдалитьВТ = Новый Массив;
	
	Если ОрганизацияВТаблице Тогда
		
		Запрос.Текст =  
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Организация
		|ПОМЕСТИТЬ ВТОрганизацииИзТаблицы
		|ИЗ
		|	ИмяВременнойТаблицы КАК Таблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НастройкиЗарплатаКадрыРасширенная.Организация,
		|	НастройкиЗарплатаКадрыРасширенная.ИспользоватьОбособленныеТерритории КАК Использование
		|ПОМЕСТИТЬ ВТИспользованиеОбособленныхТерриторийВОрганизации
		|ИЗ
		|	РегистрСведений.НастройкиЗарплатаКадрыРасширенная КАК НастройкиЗарплатаКадрыРасширенная
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОрганизацииИзТаблицы КАК Организации
		|		ПО НастройкиЗарплатаКадрыРасширенная.Организация = Организации.Организация";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИмяВременнойТаблицы", ИмяВременнойТаблицы);
		Запрос.Выполнить();
		
		УдалитьВТ.Добавить("ВТОрганизацииИзТаблицы");
		
	Иначе
		
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	НастройкиЗарплатаКадрыРасширенная.Организация,
		|	НастройкиЗарплатаКадрыРасширенная.ИспользоватьОбособленныеТерритории КАК Использование
		|ПОМЕСТИТЬ ВТИспользованиеОбособленныхТерриторийВОрганизации
		|ИЗ
		|	РегистрСведений.НастройкиЗарплатаКадрыРасширенная КАК НастройкиЗарплатаКадрыРасширенная
		|ГДЕ
		|	НастройкиЗарплатаКадрыРасширенная.Организация = &Организация";
		Запрос.Выполнить();
		
	КонецЕсли; 
	УдалитьВТ.Добавить("ВТИспользованиеОбособленныхТерриторийВОрганизации");
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&ИмяПоляОрганизация КАК Организация,
	|	&ИмяПоляПериод КАК Период
	|ПОМЕСТИТЬ ВТОтборОрганизаций
	|ИЗ
	|	ИмяВременнойТаблицы КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаксимальныеПериоды.Организация КАК Организация,
	|	МаксимальныеПериоды.Период КАК Период,
	|	БухучетЗарплаты.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетЗарплаты.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетЗарплаты.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	ВЫБОР
	|		КОГДА БухучетЗарплаты.СтатьяФинансирования = ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|				И БухучетЗарплаты.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|				И БухучетЗарплаты.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьБухучетОрганизации
	|ПОМЕСТИТЬ ВТБухучетОрганизаций
	|ИЗ
	|	(ВЫБРАТЬ
	|		Организации.Организация КАК Организация,
	|		Организации.Период КАК Период,
	|		МАКСИМУМ(БухучетЗарплатыОрганизаций.Период) КАК ПериодРегистра
	|	ИЗ
	|		ВТОтборОрганизаций КАК Организации
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыОрганизаций КАК БухучетЗарплатыОрганизаций
	|			ПО Организации.Организация = БухучетЗарплатыОрганизаций.Организация
	|				И Организации.Период >= БухучетЗарплатыОрганизаций.Период
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Организации.Организация,
	|		Организации.Период) КАК МаксимальныеПериоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыОрганизаций КАК БухучетЗарплаты
	|		ПО МаксимальныеПериоды.Организация = БухучетЗарплаты.Организация
	|			И МаксимальныеПериоды.ПериодРегистра = БухучетЗарплаты.Период";
	
	УдалитьВТ.Добавить("ВТОтборОрганизаций");
	УдалитьВТ.Добавить("ВТБухучетОрганизаций");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяВременнойТаблицы", ИмяВременнойТаблицы);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляПериод", ИмяПоляПериод);
	Если ОрганизацияВТаблице Тогда
		ИмяПоляОрганизация = "Таблица.Организация";
	Иначе
		ИмяПоляОрганизация = "&Организация";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляОрганизация", ИмяПоляОрганизация);
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.Выполнить();
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&ИмяПоляПодразделение КАК Подразделение,
	|	&ИмяПоляПериод КАК Период
	|ПОМЕСТИТЬ ВТОтборПодразделений
	|ИЗ
	|	ИмяВременнойТаблицы КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаксимальныеПериоды.Подразделение КАК Подразделение,
	|	МаксимальныеПериоды.Период КАК Период,
	|	БухучетЗарплаты.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетЗарплаты.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетЗарплаты.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	ВЫБОР
	|		КОГДА БухучетЗарплаты.СтатьяФинансирования = ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|				И БухучетЗарплаты.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|				И БухучетЗарплаты.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьБухучетПодразделения
	|ПОМЕСТИТЬ ВТБухучетПодразделений
	|ИЗ
	|	(ВЫБРАТЬ
	|		МаксимальныеПериоды.Подразделение КАК Подразделение,
	|		МаксимальныеПериоды.Период КАК Период,
	|		МаксимальныеПериоды.ПериодРегистра КАК ПериодРегистра
	|	ИЗ
	|		(ВЫБРАТЬ
	|			Подразделения.Подразделение КАК Подразделение,
	|			Подразделения.Период КАК Период,
	|			МАКСИМУМ(БухучетЗарплатыПодразделений.Период) КАК ПериодРегистра
	|		ИЗ
	|			ВТОтборПодразделений КАК Подразделения
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыПодразделений КАК БухучетЗарплатыПодразделений
	|				ПО Подразделения.Подразделение = БухучетЗарплатыПодразделений.Подразделение
	|					И Подразделения.Период >= БухучетЗарплатыПодразделений.Период
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Подразделения.Подразделение,
	|			Подразделения.Период) КАК МаксимальныеПериоды
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыПодразделений КАК БухучетЗарплатыПодразделений
	|			ПО МаксимальныеПериоды.Подразделение = БухучетЗарплатыПодразделений.Подразделение
	|				И МаксимальныеПериоды.ПериодРегистра = БухучетЗарплатыПодразделений.Период
	|	
	|	СГРУППИРОВАТЬ ПО
	|		МаксимальныеПериоды.Подразделение,
	|		МаксимальныеПериоды.ПериодРегистра,
	|		МаксимальныеПериоды.Период
	|	
	|	ИМЕЮЩИЕ
	|		КОЛИЧЕСТВО(БухучетЗарплатыПодразделений.ИдентификаторСтроки) = 1) КАК МаксимальныеПериоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыПодразделений КАК БухучетЗарплаты
	|		ПО МаксимальныеПериоды.Подразделение = БухучетЗарплаты.Подразделение
	|			И МаксимальныеПериоды.ПериодРегистра = БухучетЗарплаты.Период";
	
	УдалитьВТ.Добавить("ВТОтборПодразделений");
	УдалитьВТ.Добавить("ВТБухучетПодразделений");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяВременнойТаблицы", ИмяВременнойТаблицы);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляПериод", ИмяПоляПериод);
	Если ПодразделениеВТаблице Тогда
		ИмяПоляПодразделение = "Таблица.Подразделение";
	Иначе
		Запрос.УстановитьПараметр("Подразделение", Подразделение);
		ИмяПоляПодразделение = "&Подразделение";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляПодразделение", ИмяПоляПодразделение);
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.Выполнить();

	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&ИмяПоляТерритория КАК ТерриторияВыполненияРаботВОрганизации,
	|	&ИмяПоляПериод КАК Период
	|ПОМЕСТИТЬ ВТОтборТерриторий
	|ИЗ
	|	ИмяВременнойТаблицы КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаксимальныеПериоды.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	МаксимальныеПериоды.Период КАК Период,
	|	БухучетЗарплаты.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетЗарплаты.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетЗарплаты.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	ВЫБОР
	|		КОГДА БухучетЗарплаты.СтатьяФинансирования = ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|				И БухучетЗарплаты.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|				И БухучетЗарплаты.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьБухучетТерритории
	|ПОМЕСТИТЬ ВТБухучетТерриторий
	|ИЗ
	|	(ВЫБРАТЬ
	|		МаксимальныеПериоды.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|		МаксимальныеПериоды.Период КАК Период,
	|		МаксимальныеПериоды.ПериодРегистра КАК ПериодРегистра
	|	ИЗ
	|		(ВЫБРАТЬ
	|			Территории.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|			Территории.Период КАК Период,
	|			МАКСИМУМ(БухучетЗарплатыТерриторий.Период) КАК ПериодРегистра
	|		ИЗ
	|			ВТОтборТерриторий КАК Территории
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыТерриторийВыполненияРабот КАК БухучетЗарплатыТерриторий
	|				ПО Территории.ТерриторияВыполненияРаботВОрганизации = БухучетЗарплатыТерриторий.ТерриторияВыполненияРабот
	|					И Территории.Период >= БухучетЗарплатыТерриторий.Период
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Территории.ТерриторияВыполненияРаботВОрганизации,
	|			Территории.Период) КАК МаксимальныеПериоды
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыТерриторийВыполненияРабот КАК БухучетЗарплатыТерриторий
	|			ПО МаксимальныеПериоды.ТерриторияВыполненияРаботВОрганизации = БухучетЗарплатыТерриторий.ТерриторияВыполненияРабот
	|				И МаксимальныеПериоды.ПериодРегистра = БухучетЗарплатыТерриторий.Период
	|	
	|	СГРУППИРОВАТЬ ПО
	|		МаксимальныеПериоды.ТерриторияВыполненияРаботВОрганизации,
	|		МаксимальныеПериоды.ПериодРегистра,
	|		МаксимальныеПериоды.Период
	|	
	|	ИМЕЮЩИЕ
	|		КОЛИЧЕСТВО(БухучетЗарплатыТерриторий.ИдентификаторСтроки) = 1) КАК МаксимальныеПериоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыТерриторийВыполненияРабот КАК БухучетЗарплаты
	|		ПО МаксимальныеПериоды.ТерриторияВыполненияРаботВОрганизации = БухучетЗарплаты.ТерриторияВыполненияРабот
	|			И МаксимальныеПериоды.ПериодРегистра = БухучетЗарплаты.Период";
	
	УдалитьВТ.Добавить("ВТОтборТерриторий");
	УдалитьВТ.Добавить("ВТБухучетТерриторий");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяВременнойТаблицы", ИмяВременнойТаблицы);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляПериод", ИмяПоляПериод);
	Если ТерриторияВТаблице Тогда
		ИмяПоляТерритория = "Таблица.ТерриторияВыполненияРаботВОрганизации";
	Иначе
		Запрос.УстановитьПараметр("Территория", ТерриторияВыполненияРаботВОрганизации);
		ИмяПоляТерритория = "&Территория";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляТерритория", ИмяПоляТерритория);
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.Выполнить();
	
	// В запросе используем условие
	// БухучетЗарплаты.ДействуетДо < МаксимальныеПериоды.Период.
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&ИмяПоляСотрудник КАК Сотрудник,
	|	&ИмяПоляПериод КАК Период
	|ПОМЕСТИТЬ ВТОтборСотрудников
	|ИЗ
	|	ИмяВременнойТаблицы КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаксимальныеПериоды.Сотрудник КАК Сотрудник,
	|	МаксимальныеПериоды.Период КАК Период,
	|	ВЫБОР
	|		КОГДА БухучетЗарплаты.ДействуетДо <> ДАТАВРЕМЯ(1, 1, 1)
	|				И БухучетЗарплаты.ДействуетДо < МаксимальныеПериоды.Период
	|			ТОГДА БухучетЗарплаты.СтатьяФинансированияПоОкончании
	|		ИНАЧЕ БухучетЗарплаты.СтатьяФинансирования
	|	КОНЕЦ КАК СтатьяФинансирования,
	|	ВЫБОР
	|		КОГДА БухучетЗарплаты.ДействуетДо <> ДАТАВРЕМЯ(1, 1, 1)
	|				И БухучетЗарплаты.ДействуетДо < МаксимальныеПериоды.Период
	|			ТОГДА БухучетЗарплаты.СпособОтраженияЗарплатыВБухучетеПоОкончании
	|		ИНАЧЕ БухучетЗарплаты.СпособОтраженияЗарплатыВБухучете
	|	КОНЕЦ КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА БухучетЗарплаты.ДействуетДо <> ДАТАВРЕМЯ(1, 1, 1)
	|				И БухучетЗарплаты.ДействуетДо <= МаксимальныеПериоды.Период
	|			ТОГДА БухучетЗарплаты.ОтношениеКЕНВДПоОкончании
	|		ИНАЧЕ БухучетЗарплаты.ОтношениеКЕНВД
	|	КОНЕЦ КАК ОтношениеКЕНВД,
	|	ВЫБОР
	|		КОГДА БухучетЗарплаты.ДействуетДо <> ДАТАВРЕМЯ(1, 1, 1)
	|				И БухучетЗарплаты.ДействуетДо < МаксимальныеПериоды.Период
	|			ТОГДА ВЫБОР
	|					КОГДА БухучетЗарплаты.СтатьяФинансированияПоОкончании = ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|							И БухучетЗарплаты.СпособОтраженияЗарплатыВБухучетеПоОкончании = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|							И БухучетЗарплаты.ОтношениеКЕНВДПоОкончании = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
	|						ТОГДА ЛОЖЬ
	|					ИНАЧЕ ИСТИНА
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА БухучетЗарплаты.СтатьяФинансирования = ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|						И БухучетЗарплаты.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|						И БухучетЗарплаты.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
	|					ТОГДА ЛОЖЬ
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ
	|	КОНЕЦ КАК ЕстьБухучетСотрудника
	|ПОМЕСТИТЬ ВТБухучетСотрудников
	|ИЗ
	|	(ВЫБРАТЬ
	|		Сотрудники.Сотрудник КАК Сотрудник,
	|		Сотрудники.Период КАК Период,
	|		МАКСИМУМ(БухучетЗарплатыСотрудников.Период) КАК ПериодРегистра
	|	ИЗ
	|		ВТОтборСотрудников КАК Сотрудники
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыСотрудников КАК БухучетЗарплатыСотрудников
	|			ПО Сотрудники.Сотрудник = БухучетЗарплатыСотрудников.Сотрудник
	|				И Сотрудники.Период >= БухучетЗарплатыСотрудников.Период
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Сотрудники.Сотрудник,
	|		Сотрудники.Период) КАК МаксимальныеПериоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыСотрудников КАК БухучетЗарплаты
	|		ПО МаксимальныеПериоды.Сотрудник = БухучетЗарплаты.Сотрудник
	|			И МаксимальныеПериоды.ПериодРегистра = БухучетЗарплаты.Период";
	
	УдалитьВТ.Добавить("ВТОтборСотрудников");
	УдалитьВТ.Добавить("ВТБухучетСотрудников");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяВременнойТаблицы", ИмяВременнойТаблицы);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляСотрудник", ИмяПоляСотрудник);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляПериод", ИмяПоляПериод);
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.Выполнить();
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&ИмяПоляОрганизация КАК Организация,
	|	&ИмяПоляПодразделение КАК Подразделение,
	|	&ИмяПоляТерритория КАК ТерриторияВыполненияРаботВОрганизации,
	|	&ИмяПоляСотрудник КАК Сотрудник,
	|	&ИмяПоляПериод КАК Период,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьСтатьиФинансирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|		КОГДА ЕСТЬNULL(БухучетСотрудников.СтатьяФинансирования, ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|			ТОГДА БухучетСотрудников.СтатьяФинансирования
	|		КОГДА ИспользованиеТерриторий.Использование
	|				И ЕСТЬNULL(БухучетТерриторий.СтатьяФинансирования, ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|			ТОГДА БухучетТерриторий.СтатьяФинансирования
	|		КОГДА ЕСТЬNULL(БухучетПодразделений.СтатьяФинансирования, ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|			ТОГДА БухучетПодразделений.СтатьяФинансирования
	|		ИНАЧЕ ЕСТЬNULL(БухучетОрганизаций.СтатьяФинансирования, ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка))
	|	КОНЕЦ КАК СтатьяФинансирования,
	|	&СтатьяОплатаТруда КАК СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(БухучетСотрудников.СпособОтраженияЗарплатыВБухучете, ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|			ТОГДА БухучетСотрудников.СпособОтраженияЗарплатыВБухучете
	|		КОГДА ИспользованиеТерриторий.Использование
	|				И ЕСТЬNULL(БухучетТерриторий.СпособОтраженияЗарплатыВБухучете, ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|			ТОГДА БухучетТерриторий.СпособОтраженияЗарплатыВБухучете
	|		КОГДА ЕСТЬNULL(БухучетПодразделений.СпособОтраженияЗарплатыВБухучете, ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|			ТОГДА БухучетПодразделений.СпособОтраженияЗарплатыВБухучете
	|		ИНАЧЕ ЕСТЬNULL(БухучетОрганизаций.СпособОтраженияЗарплатыВБухучете, ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка))
	|	КОНЕЦ КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(БухучетСотрудников.ОтношениеКЕНВД, ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
	|			ТОГДА БухучетСотрудников.ОтношениеКЕНВД
	|		КОГДА ИспользованиеТерриторий.Использование
	|				И ЕСТЬNULL(БухучетТерриторий.ОтношениеКЕНВД, ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
	|			ТОГДА БухучетТерриторий.ОтношениеКЕНВД
	|		КОГДА ЕСТЬNULL(БухучетПодразделений.ОтношениеКЕНВД, ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
	|			ТОГДА БухучетПодразделений.ОтношениеКЕНВД
	|		ИНАЧЕ ЕСТЬNULL(БухучетОрганизаций.ОтношениеКЕНВД, ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка))
	|	КОНЕЦ КАК ОтношениеКЕНВД,
	|	ЕСТЬNULL(БухучетОрганизаций.ЕстьБухучетОрганизации, ЛОЖЬ) КАК ЕстьБухучетОрганизации,
	|	ЕСТЬNULL(БухучетПодразделений.ЕстьБухучетПодразделения, ЛОЖЬ) КАК ЕстьБухучетПодразделения,
	|	ИспользованиеТерриторий.Использование
	|		И ЕСТЬNULL(БухучетТерриторий.ЕстьБухучетТерритории, ЛОЖЬ) КАК ЕстьБухучетТерритории,
	|	ЕСТЬNULL(БухучетСотрудников.ЕстьБухучетСотрудника, ЛОЖЬ) КАК ЕстьБухучетСотрудника
	|ПОМЕСТИТЬ ВТСведенияОБухучетеЗарплатыСотрудников
	|ИЗ
	|	ИмяВременнойТаблицы КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетСотрудников КАК БухучетСотрудников
	|		ПО (&ИмяПоляСотрудник = БухучетСотрудников.Сотрудник)
	|			И (&ИмяПоляПериод = БухучетСотрудников.Период)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетПодразделений КАК БухучетПодразделений
	|		ПО (&ИмяПоляПериод = БухучетПодразделений.Период)
	|			И Таблица.Подразделение = БухучетПодразделений.Подразделение
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетТерриторий КАК БухучетТерриторий
	|		ПО (&ИмяПоляПериод = БухучетТерриторий.Период)
	|			И Таблица.ТерриторияВыполненияРаботВОрганизации = БухучетТерриторий.ТерриторияВыполненияРаботВОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетОрганизаций КАК БухучетОрганизаций
	|		ПО (&ИмяПоляПериод = БухучетОрганизаций.Период)
	|			И Таблица.Организация = БухучетОрганизаций.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИспользованиеОбособленныхТерриторийВОрганизации КАК ИспользованиеТерриторий
	|		ПО Таблица.Организация = ИспользованиеТерриторий.Организация";
	
	Если Не ОрганизацияВТаблице Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И Таблица.Организация = БухучетОрганизаций.Организация", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПО Таблица.Организация = ИспользованиеТерриторий.Организация", "ПО (ИСТИНА)");
	КонецЕсли; 
	
	Если Не ПодразделениеВТаблице Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И Таблица.Подразделение = БухучетПодразделений.Подразделение", "");	
	КонецЕсли;
	
	Если Не ТерриторияВТаблице Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И Таблица.ТерриторияВыполненияРаботВОрганизации = БухучетТерриторий.ТерриторияВыполненияРаботВОрганизации", "");	
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяВременнойТаблицы", ИмяВременнойТаблицы);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляОрганизация", ИмяПоляОрганизация);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляПодразделение", ИмяПоляПодразделение);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляТерритория", ИмяПоляТерритория);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляСотрудник", ИмяПоляСотрудник);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляПериод", ИмяПоляПериод);
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.Выполнить();
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
КонецПроцедуры

// Формирует временную таблицу ВТСведенияОБухучетеНачислений
// получает настройки бухучета из ПВР Начисления, у которых задана стратегия отражения в учете КакЗаданоВидуРасчета.
// 
// Параметры:
//		МенеджерВременныхТаблиц - в менеджер помещается таблица ВТСведенияОБухучетеНачислений.
//
Процедура СоздатьВТСведенияОБухучетеНачислений(МенеджерВременныхТаблиц) Экспорт

	ИспользоватьСтатьиФинансирования = ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата");
	РаботаВБюджетномУчреждении = ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ИспользоватьСтатьиФинансирования", ИспользоватьСтатьиФинансирования);
	Запрос.УстановитьПараметр("РаботаВБюджетномУчреждении", РаботаВБюджетномУчреждении);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.Ссылка КАК Начисление,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьСтатьиФинансирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|		ИНАЧЕ Начисления.СтатьяФинансирования
	|	КОНЕЦ КАК СтатьяФинансирования,
	|	ВЫБОР
	|		КОГДА НЕ &РаботаВБюджетномУчреждении
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|		ИНАЧЕ Начисления.СтатьяРасходов
	|	КОНЕЦ КАК СтатьяРасходов,
	|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	Начисления.ОтношениеКЕНВД КАК ОтношениеКЕНВД
	|ПОМЕСТИТЬ ВТСведенияОБухучетеНачислений
	|ИЗ
	|	ПланВидовРасчета.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.КакЗаданоВидуРасчета)";
	
	Запрос.Выполнить();

КонецПроцедуры

// Формирует таблицу отражения в бухучете начислений - ВТБухучетНачислений.
//
//	МенеджерВременныхТаблиц должен содержать таблицу с именем ВТНачисленияДляРаспределения
//	Описание таблицы ВТНачисленияДляРаспределения
// 		ПериодРегистрации
// 		Организация
// 		Сотрудник
// 		ФизическоеЛицо
// 		Подразделение
// 		СтатьяРасходов - поле присутствует только в режиме
//							РаботаВХозрасчетнойОрганизации = Истина
//							И ИспользоватьСтатьиФинансированияЗарплатаРасширенный = Ложь
//		ТерриторияВыполненияРаботВОрганизации
// 		Начисление
// 		ВидОперации
// 		ДатаНачала
// 		ДатаОкончания
// 		Сумма
//		ДокументОснование
// 		ИдентификаторСтроки 
//
//	МенеджерВременныхТаблиц может содержать таблицу с именем ВТБухучетНачисленийИзФормы
//	Описание таблицы ВТБухучетНачисленийИзФормы
//		Организация
//		Сотрудник
//		ФизическоеЛицо
//		Подразделение
//		Территория
//		Начисление
//		ВидОперации
//		ДатаНачала
//		ДатаОкончания
//		ДокументОснование
//		СпособОтраженияЗарплатыВБухучете
//		СтатьяФинансирования
//		СтатьяРасходов
//		Сумма
//		ОблагаетсяЕНВД
//
// Поля таблицы ВТБухучетНачислений
// 		ПериодРегистрации
// 		ИдентификаторСтроки
//		ПериодПринятияРасходов
//		Организация
// 		ФизическоеЛицо
// 		Сотрудник
// 		Подразделение
// 		ПодразделениеУчетаЗатрат
//		ТерриторияВыполненияРаботВОрганизации
// 		Начисление
// 		ВидОперации
// 		ДатаНачала
//		ДатаОкончания
//		ДокументОснование
// 		Сумма
// 		СпособОтраженияЗарплатыВБухучете
//		СтатьяФинансирования
//		СтатьяРасходов
// 		ОблагаетсяЕНВД
// 		ВидНачисленияОплатыТрудаДляНУ
//
// Параметры:
//		ДополнительныеПараметры - Структура
//			* ДокументСсылка  - ссылка на документ, начисления которого сейчас отражаем в бухучете.
//			* БухучетПервичногоДокумента - таблица значений содержит сведения о настройках бухучета, заданных в документе
//			                               ДокументСсылка.
//			* КоэффициентыРаспределенияСреднегоЗаработка - таблица значений, содержит сведения о распределении среднего
//			                                               заработка по статьям финансирования.
//
Процедура СоздатьВТБухучетНачислений(Организация, ПериодРегистрации, ПроцентЕНВД, МенеджерВременныхТаблиц) Экспорт
	
	УдалитьВТ = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НачисленияДляРаспределения.ПериодРегистрации КАК ПериодРегистрации,
	|	НачисленияДляРаспределения.Организация КАК Организация,
	|	НачисленияДляРаспределения.Сотрудник КАК Сотрудник,
	|	НачисленияДляРаспределения.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НачисленияДляРаспределения.Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	НачисленияДляРаспределения.Начисление КАК Начисление,
	|	НачисленияДляРаспределения.ВидОперации КАК ВидОперации,
	|	НачисленияДляРаспределения.ДатаНачала КАК ДатаНачала,
	|	НачисленияДляРаспределения.ДатаОкончания КАК ДатаОкончания,
	|	НачисленияДляРаспределения.Сумма КАК Сумма,
	|	НачисленияДляРаспределения.ДокументОснование КАК ДокументОснование,
	|	НачисленияДляРаспределения.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НачисленияДляРаспределения.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации
	|ПОМЕСТИТЬ ВТНачисленияПоДоговорамГПХ
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК НачисленияДляРаспределения
	|ГДЕ
	|	НачисленияДляРаспределения.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ДоговорАвторскогоЗаказа), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ДоговорРаботыУслуги))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленияДляРаспределения.ПериодРегистрации КАК ПериодРегистрации,
	|	НачисленияДляРаспределения.Организация КАК Организация,
	|	НачисленияДляРаспределения.Сотрудник КАК Сотрудник,
	|	НачисленияДляРаспределения.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НачисленияДляРаспределения.Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	НачисленияДляРаспределения.ВидОперации КАК ВидОперации,
	|	НачисленияДляРаспределения.Начисление КАК Начисление,
	|	НачисленияДляРаспределения.ДатаНачала КАК ДатаНачала,
	|	НачисленияДляРаспределения.ДатаОкончания КАК ДатаОкончания,
	|	НачисленияДляРаспределения.Сумма КАК Сумма,
	|	НачисленияДляРаспределения.ДокументОснование КАК ДокументОснование,
	|	НачисленияДляРаспределения.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НачисленияДляРаспределения.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	ЛОЖЬ КАК РассчитыватьПоРазовымНачислениямДокумента
	|ПОМЕСТИТЬ ВТНачисленияБезДоговоровГПХ
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК НачисленияДляРаспределения
	|ГДЕ
	|	НЕ НачисленияДляРаспределения.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ДоговорАвторскогоЗаказа), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ДоговорРаботыУслуги))";
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный")
		И (МенеджерВременныхТаблиц.Таблицы["ВТНачисленияДляРаспределения"].Колонки.Найти("СтатьяРасходов") <> Неопределено) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СтатьяРасходов", "НачисленияДляРаспределения.СтатьяРасходов");
		
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СтатьяРасходов", "ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)");
	КонецЕсли;
	
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТНачисленияБезДоговоровГПХ");
	УдалитьВТ.Добавить("ВТНачисленияПоДоговорамГПХ");
	
	ОтражениеЗарплатыВБухучете.СоздатьВТНачислениеВидНачисленияОплатыТрудаДляНУ(МенеджерВременныхТаблиц);
	УдалитьВТ.Добавить("ВТНачислениеВидНачисленияОплатыТрудаДляНУ");
	
	ИсходныеДанные = ОписаниеИсходныхДанныхДляОтраженияНачисленийВБухучете();
	ИсходныеДанные.Организация    			  = Организация;
	ИсходныеДанные.МесяцНачисления 			  = ПериодРегистрации;
	ИсходныеДанные.МенеджерВременныхТаблиц    = МенеджерВременныхТаблиц;
	ИсходныеДанные.ПлательщикЕНВД             = ОтражениеЗарплатыВБухучете.ПлательщикЕНВД(Организация, ПериодРегистрации);
	ИсходныеДанные.ПроцентЕНВД    			  = ПроцентЕНВД;
	
	ИсходныеДанные.ИмяВТНачисления = "ВТНачисленияБезДоговоровГПХ";
	СоздатьВТБухучетНачисленийБезДоговоровГПХ(ИсходныеДанные, "ВТБухучетНачисленийБезДоговоровГПХ");
	УдалитьВТ.Добавить("ВТБухучетНачисленийБезДоговоровГПХ");
	
	ИсходныеДанные.ИмяВТНачисления = "ВТНачисленияПоДоговорамГПХ";
	СоздатьВТБухучетНачисленийПоДоговорам(ИсходныеДанные, "ВТБухучетНачисленийПоДоговорам");
	УдалитьВТ.Добавить("ВТБухучетНачисленийПоДоговорам");
		
	// ВТБухучетНачислений - объединение сведений о бухучете начислений.
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БухучетНачислений.ПериодРегистрации КАК ПериодРегистрации,
	|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	БухучетНачислений.ПериодПринятияРасходов КАК ПериодПринятияРасходов,
	|	БухучетНачислений.Организация КАК Организация,
	|	БухучетНачислений.Сотрудник КАК Сотрудник,
	|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
	|	БухучетНачислений.Подразделение КАК Подразделение,
	|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	БухучетНачислений.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	БухучетНачислений.Начисление КАК Начисление,
	|	БухучетНачислений.ВидОперации КАК ВидОперации,
	|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
	|	БухучетНачислений.ДатаОкончания КАК ДатаОкончания,
	|	БухучетНачислений.ДокументОснование КАК ДокументОснование,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	СУММА(БухучетНачислений.Сумма) КАК Сумма,
	|	БухучетНачислений.ВидНачисленияОплатыТрудаДляНУ КАК ВидНачисленияОплатыТрудаДляНУ,
	|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД
	|ПОМЕСТИТЬ ВТБухучетНачислений
	|ИЗ
	|	(ВЫБРАТЬ
	|		БухучетНачисления.ПериодРегистрации КАК ПериодРегистрации,
	|		БухучетНачисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|		БухучетНачисления.ПериодПринятияРасходов КАК ПериодПринятияРасходов,
	|		БухучетНачисления.Организация КАК Организация,
	|		БухучетНачисления.Сотрудник КАК Сотрудник,
	|		БухучетНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|		БухучетНачисления.Подразделение КАК Подразделение,
	|		БухучетНачисления.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|		БухучетНачисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|		БухучетНачисления.Начисление КАК Начисление,
	|		БухучетНачисления.ВидОперации КАК ВидОперации,
	|		БухучетНачисления.ДатаНачала КАК ДатаНачала,
	|		БухучетНачисления.ДатаОкончания КАК ДатаОкончания,
	|		БухучетНачисления.ДокументОснование КАК ДокументОснование,
	|		БухучетНачисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|		БухучетНачисления.СтатьяФинансирования КАК СтатьяФинансирования,
	|		БухучетНачисления.СтатьяРасходов КАК СтатьяРасходов,
	|		БухучетНачисления.Сумма КАК Сумма,
	|		БухучетНачисления.ВидНачисленияОплатыТрудаДляНУ КАК ВидНачисленияОплатыТрудаДляНУ,
	|		БухучетНачисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД
	|	ИЗ
	|		ВТБухучетНачисленийБезДоговоровГПХ КАК БухучетНачисления
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		БухучетНачисления.ПериодРегистрации,
	|		БухучетНачисления.ИдентификаторСтроки,
	|		БухучетНачисления.ПериодПринятияРасходов,
	|		БухучетНачисления.Организация,
	|		БухучетНачисления.Сотрудник,
	|		БухучетНачисления.ФизическоеЛицо,
	|		БухучетНачисления.Подразделение,
	|		БухучетНачисления.Подразделение,
	|		БухучетНачисления.ТерриторияВыполненияРаботВОрганизации,
	|		БухучетНачисления.Начисление,
	|		БухучетНачисления.ВидОперации,
	|		БухучетНачисления.ДатаНачала,
	|		БухучетНачисления.ДатаОкончания,
	|		БухучетНачисления.ДокументОснование,
	|		БухучетНачисления.СпособОтраженияЗарплатыВБухучете,
	|		БухучетНачисления.СтатьяФинансирования,
	|		БухучетНачисления.СтатьяРасходов,
	|		БухучетНачисления.Сумма,
	|		БухучетНачисления.ВидНачисленияОплатыТрудаДляНУ,
	|		БухучетНачисления.ОблагаетсяЕНВД
	|	ИЗ
	|		ВТБухучетНачисленийПоДоговорам КАК БухучетНачисления) КАК БухучетНачислений
	|
	|СГРУППИРОВАТЬ ПО
	|	БухучетНачислений.ВидНачисленияОплатыТрудаДляНУ,
	|	БухучетНачислений.ОблагаетсяЕНВД,
	|	БухучетНачислений.ДатаОкончания,
	|	БухучетНачислений.ИдентификаторСтроки,
	|	БухучетНачислений.ВидОперации,
	|	БухучетНачислений.ПериодПринятияРасходов,
	|	БухучетНачислений.Организация,
	|	БухучетНачислений.ДокументОснование,
	|	БухучетНачислений.ПериодРегистрации,
	|	БухучетНачислений.СтатьяРасходов,
	|	БухучетНачислений.Сотрудник,
	|	БухучетНачислений.СтатьяФинансирования,
	|	БухучетНачислений.ФизическоеЛицо,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.Начисление,
	|	БухучетНачислений.Подразделение,
	|	БухучетНачислений.ТерриторияВыполненияРаботВОрганизации,
	|	БухучетНачислений.ДатаНачала,
	|	БухучетНачислений.ПодразделениеУчетаЗатрат";
	
	Запрос.Выполнить();
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, УдалитьВТ);
	
КонецПроцедуры

// Выполняет бухучет начислений, кроме договоров ГПХ
//	результаты помещает во временную таблицу с именем ИмяВТБухучетНачислений.
//
Процедура СоздатьВТБухучетНачисленийБезДоговоровГПХ(ИсходныеДанные, ИмяВТБухучетНачислений) Экспорт
	
	Организация                 = ИсходныеДанные.Организация;
	ПериодРегистрации           = ИсходныеДанные.МесяцНачисления;
	МенеджерВременныхТаблиц     = ИсходныеДанные.МенеджерВременныхТаблиц;
	ИмяВТНачисленияИсходная     = ИсходныеДанные.ИмяВТНачисления;
	БухучетПервичногоДокумента  = ИсходныеДанные.БухучетПервичногоДокумента;
	ИсключаемыйРегистратор 		= ИсходныеДанные.ИсключаемыйРегистратор;
	ПроцентЕНВД                 = ИсходныеДанные.ПроцентЕНВД;
	ПлательщикЕНВД              = ИсходныеДанные.ПлательщикЕНВД;
	УчитыватьСторноЗаписи 		= ИсходныеДанные.УчитыватьСторноЗаписи;
	СтрокиКоэффициентыСреднегоЗаработка       = ИсходныеДанные.СтрокиКоэффициентыСреднегоЗаработка;
	КоэффициентыСреднегоЗаработкаДокумента    = ИсходныеДанные.КоэффициентыСреднегоЗаработкаДокумента;
	КоэффициентыСреднегоЗаработкаФССДокумента = ИсходныеДанные.КоэффициентыСреднегоЗаработкаФССДокумента;
	ВидыОперацийРасходыПоСтрахованию          = ИсходныеДанные.ВидыОперацийРасходыПоСтрахованию;
	ВидыОперацийРасходыФСС                    = ИсходныеДанные.ВидыОперацийРасходыФСС;
	СтрокиКоэффициентыСохраняемогоДС       		 = ИсходныеДанные.СтрокиКоэффициентыСохраняемогоДС;
	КоэффициентыРаспределенияДенежногоСодержания = ИсходныеДанные.КоэффициентыРаспределенияДенежногоСодержания;
	
	// Параметры, используемые при получении бухучета начислений сохраняемого денежного содержания
	// РаспределениеСохраняемогоДС - признак того, что выполняется получение бухучета начислений, входящих в расчет
	//                               сохраняемого ДС
	РаспределениеСохраняемогоДС = ИсходныеДанные.РаспределениеСохраняемогоДС;
	// СоответствиеСотрудников - ключ, это ссылка на "временного сотрудника", значение - ссылка на настоящего сотрудника
	СоответствиеСотрудников 	= ИсходныеДанные.СоответствиеСотрудников;
	// ДатаНачалаДляБухучета - настоящая дата, для получения бухучета
	ДатаНачалаДляБухучета = ИсходныеДанные.ДатаНачалаДляБухучета;
	
	РаботаВБюджетномУчреждении  	   = ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении");
	ИспользоватьСтатьиФинансирования   = ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата");
	ИспользоватьОбособленныеТерритории = ПолучитьФункциональнуюОпцию("ИспользоватьОбособленныеТерритории", Новый Структура("Организация", Организация));
	
	ПрямыеВыплатыПособий = ПрямыеВыплатыПособийСоциальногоСтрахования.ПособиеПлатитУчастникПилотногоПроекта(Организация, ПериодРегистрации);
	
	Если ПлательщикЕНВД = Неопределено Тогда
		ПлательщикЕНВД = ОтражениеЗарплатыВБухучете.ПлательщикЕНВД(Организация, ПериодРегистрации);
		ОтражениеЗарплатыВБухучете.ПроцентЕНВД(Организация, ПериодРегистрации);
	КонецЕсли;
	
	ПроцентЕНВД 		 = ?(ПроцентЕНВД = Неопределено, 0, ПроцентЕНВД);
	РассчитыватьДолюЕНВД = ?(ПроцентЕНВД = 0 Или ПроцентЕНВД = 100, Ложь, Истина);
	СтатьяРасходов213	 = СтатьяРасходов213();
	СтатьяРасходов211	 = СтатьяРасходов211();
	СтатьяРасчетыПоОплатеТруда = СтатьяОплатаТруда();
	
	// В массиве будем накапливать имена временных таблиц
	// которые необходимо уничтожить.
	УдалитьВТ = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если РаспределениеСохраняемогоДС Тогда
		
		ТаблицаСоответствия = Новый ТаблицаЗначений;
		ТаблицаСоответствия.Колонки.Добавить("ВременныйСотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
		ТаблицаСоответствия.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
		Для каждого ЭлементКоллекции Из СоответствиеСотрудников Цикл
			НоваяСтрока = ТаблицаСоответствия.Добавить();
			НоваяСтрока.ВременныйСотрудник = ЭлементКоллекции.Ключ;
			НоваяСтрока.Сотрудник = ЭлементКоллекции.Значение;
		КонецЦикла;
		Запрос.УстановитьПараметр("ТаблицаСоответствия", ТаблицаСоответствия);
		Запрос.УстановитьПараметр("ДатаНачалаДляБухучета", ДатаНачалаДляБухучета);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаСоответствия.Сотрудник КАК Сотрудник,
		|	ТаблицаСоответствия.ВременныйСотрудник КАК ВременныйСотрудник
		|ПОМЕСТИТЬ ВТТаблицаСоответствия
		|ИЗ
		|	&ТаблицаСоответствия КАК ТаблицаСоответствия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&ДатаНачалаДляБухучета КАК ДатаНачалаДляБухучета,
		|	НачисленияДляРаспределения.Сотрудник КАК Сотрудник,
		|	ЕСТЬNULL(ТаблицаСоответствия.Сотрудник, НачисленияДляРаспределения.Сотрудник) КАК ВременныйСотрудник,
		|	НачисленияДляРаспределения.*
		|ПОМЕСТИТЬ ВТНачисленияДляРаспределенияСохраняемогоДС
		|ИЗ
		|	ВТНачисленияДляРаспределения КАК НачисленияДляРаспределения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТТаблицаСоответствия КАК ТаблицаСоответствия
		|		ПО НачисленияДляРаспределения.Сотрудник = ТаблицаСоответствия.ВременныйСотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТТаблицаСоответствия";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
		Запрос.Выполнить();
		ИмяВТНачисленияИсходная = "ВТНачисленияДляРаспределенияСохраняемогоДС";
		УдалитьВТ.Добавить(ИмяВТНачисленияИсходная);
		
	КонецЕсли;
		
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("ИспользоватьСтатьиФинансирования", ИспользоватьСтатьиФинансирования);
	Запрос.УстановитьПараметр("РаботаВБюджетномУчреждении", РаботаВБюджетномУчреждении);
	Запрос.УстановитьПараметр("ЕстьЕНВД", ПлательщикЕНВД);
	Запрос.УстановитьПараметр("ПроцентЕНВД", ПроцентЕНВД);
	Запрос.УстановитьПараметр("РассчитыватьДолюЕНВД", РассчитыватьДолюЕНВД);
	Запрос.УстановитьПараметр("ИсключаемыйРегистратор", ИсключаемыйРегистратор);
	Запрос.УстановитьПараметр("БухучетПервичногоДокумента", БухучетПервичногоДокумента);
	Запрос.УстановитьПараметр("Статья213", СтатьяРасходов213);
	Запрос.УстановитьПараметр("Статья211", СтатьяРасходов211);
	Запрос.УстановитьПараметр("СтатьяРасчетыПоОплатеТруда", СтатьяРасчетыПоОплатеТруда);
	Запрос.УстановитьПараметр("РасходыПоСтрахованию", ВидыОперацийРасходыПоСтрахованию);
	Запрос.УстановитьПараметр("РасходыФСС", ВидыОперацийРасходыФСС);
	Запрос.УстановитьПараметр("ИспользоватьОбособленныеТерритории", ИспользоватьОбособленныеТерритории);
	Запрос.УстановитьПараметр("ПрямыеВыплатыПособий", ПрямыеВыплатыПособий);
	
	ПустыеЗначенияТерритории = ОтражениеЗарплатыВУчетеРасширенный.ПустыеЗначенияТерриторииДляПараметраЗапросов();
	Запрос.УстановитьПараметр("ПустыеТерритории", ПустыеЗначенияТерритории);
	
	ПараметрыРаспределенияПоСтатьям = Новый Структура;
	ПараметрыРаспределенияПоСтатьям.Вставить("АктуальныеСтатьиФинансирования", АктуальныеСтатьиФинансирования(Запрос));
	ПараметрыРаспределенияПоСтатьям.Вставить("АктуальныеСпособыОтражения", АктуальныеСпособыОтражения(Запрос));
	ПараметрыРаспределенияПоСтатьям.Вставить("СтатьиФинансированияЗамены", СтатьиФинансированияЗамены(Запрос));
	ПараметрыРаспределенияПоСтатьям.Вставить("СтатьяИспользуетсяВБазеСреднего", СтатьяИспользуетсяВБазеСреднего(Запрос));
	ПараметрыРаспределенияПоСтатьям.Вставить("РазрешенныеКатегорииНачисленийСтатьиФинансирования", РазрешенныеКатегорииНачисленийСтатьиФинансирования());
	
	// ВТНачислениеВидНачисленияОплатыТрудаДляНУ.
	// Таблица соответствия начисления и вида начисления для НУ.
	Если Не ЗарплатаКадры.ВТСуществует(МенеджерВременныхТаблиц, "ВТНачислениеВидНачисленияОплатыТрудаДляНУ") Тогда
		ОтражениеЗарплатыВБухучете.СоздатьВТНачислениеВидНачисленияОплатыТрудаДляНУ(МенеджерВременныхТаблиц);
		УдалитьВТ.Добавить("ВТНачислениеВидНачисленияОплатыТрудаДляНУ");	
	КонецЕсли;
	
	Если Не ЗарплатаКадры.ВТСуществует(МенеджерВременныхТаблиц, "ВТБухучетНачисленийИзФормы") Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Сотрудник,
		|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ФизическоеЛицо,
		|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.ТерриторииВыполненияРабот.ПустаяСсылка) КАК Территория,
		|	ЗНАЧЕНИЕ(ПланВидовРасчета.Начисления.ПустаяСсылка) КАК Начисление,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ПустаяСсылка) КАК ВидОперации,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаНачала,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаОкончания,
		|	НЕОПРЕДЕЛЕНО КАК ДокументОснование,
		|	ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка) КАК СпособОтраженияЗарплатыВБухучете,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
		|	0 КАК Сумма,
		|	ЛОЖЬ КАК ОблагаетсяЕНВД
		|ПОМЕСТИТЬ ВТБухучетНачисленийИзФормы";
		Запрос.Выполнить();
		УдалитьВТ.Добавить("ВТБухучетНачисленийИзФормы");
		
	КонецЕсли;
	
	Если Не ЗарплатаКадры.ВТСуществует(МенеджерВременныхТаблиц, "ВТБухучетНачисленийСторно") Тогда
		СоздатьПустуюВТБухучетНачислений(Запрос, "ВТБухучетНачисленийСторно");
		УдалитьВТ.Добавить("ВТБухучетНачисленийСторно");
	КонецЕсли;
	
	// ВТНастройкиНачислений
	// Таблица с настройками начислений.
	Если Не ЗарплатаКадры.ВТСуществует(МенеджерВременныхТаблиц, "ВТНастройкиНачислений") Тогда
		СоздатьВТНастройкиНачислений(Запрос);
		УдалитьВТ.Добавить("ВТНастройкиНачислений");
	КонецЕсли;
	
	// ВТНастройкиПоказателейНачислений
	// Таблица с настройками показателей начислений.
	СоздатьВТНастройкиПоказателейНачислений(Запрос);
	УдалитьВТ.Добавить("ВТНастройкиПоказателейНачислений");
	
	// ВТСтатьиРасходовНачисленийПоУмолчанию
	СоздатьВТСтатьиРасходовНачисленийПоУмолчанию(Запрос, РаботаВБюджетномУчреждении, ПериодРегистрации);
	УдалитьВТ.Добавить("ВТСтатьиРасходовНачисленийПоУмолчанию");
	
	// ВТНастройкиБухучетаСотрудников
	// настройки бухучета, указанные в начислении, сотруднике, территории, подразделении, организации
	// будет использоваться как настройки бухучета по умолчанию.
	СоздатьВТНастройкиБухучетаСотрудников(Запрос, ИспользоватьОбособленныеТерритории, ИмяВТНачисленияИсходная, РаспределениеСохраняемогоДС);
	УдалитьВТ.Добавить("ВТНастройкиБухучетаСотрудников");
	
	// Создаем временные таблицы с бухучетом
	// если имя таблицы начинается с "ВТБухучет...", это таблица с окончательными сведениями
	// если имя таблицы оканчивается на "...ДляБухучета", это таблица для дальнейшей обработки в части доли ЕНВД.
	
	// Во временной таблице ВТСтрокиКУдалению накапливаем строки
	// по которым уже выполнена обработка.
	
	// ВТБухучетЕдиновременныеПособия
	СоздатьВТБухучетЕдиновременныеПособия(Запрос, ИмяВТНачисленияИсходная);
	УдалитьВТ.Добавить("ВТБухучетЕдиновременныеПособия");
	
	// ВТБухучетКомпенсацийЗаЗадержкуЗарплаты
	СоздатьВТБухучетКомпенсацийЗаЗадержкуЗарплаты(Запрос, ИмяВТНачисленияИсходная);
	УдалитьВТ.Добавить("ВТБухучетКомпенсацийЗаЗадержкуЗарплаты");
	
	// Таблицы создаем в порядке убывания приоритета места указания настройки бухучета
	// сведения о бухучете, зарегистрированные в "сдельном наряде"
	// Первичный документ, регистрирующий начисление
	// Настройка, указанная для планового начисления сотрудника
	// Настройка, указанная в виде расчета: по базе среднего заработка, по базовым начислениям, по фактическим начислениям
	// или как указано виду расчета Начисления, для которых задано распределение на текущий месяц
	// Распределение, указанное в подразделении сотрудника.
	
	// ВТБухучетСдельногоЗаработка
	СоздатьВТБухучетСдельногоЗаработка(Запрос, ИмяВТНачисленияИсходная);
	УдалитьВТ.Добавить("ВТБухучетСдельногоЗаработка");
	
	// ВТБухучетПраздничныхСверхурочных
	СоздатьВТБухучетПраздничныхСверхурочных(Запрос, ИмяВТНачисленияИсходная, ИсходныеДанные.ИсточникДанныхОДатахНачислений);
	УдалитьВТ.Добавить("ВТБухучетПраздничныхСверхурочных");
	
	// ВТБухучетНачисленийПоПоказателям
	СоздатьВТБухучетНачисленийПоПоказателям(Запрос, ИмяВТНачисленияИсходная);
	УдалитьВТ.Добавить("ВТБухучетНачисленийПоПоказателям");
	
	// ВТНачисленияПоДокументамИПлановыеДляБухучет
	// содержит строки, у которых бухучет задан в первичном документе и в настройках бухучета плановых начислений.
	// Дополнительно будет создана таблица ВТСтатьиРасчетыПоОплатеТрудаПервичныхДокументов,
	// которая содержит статью расходов и используется, когда ведется учет по статьям и не бюджетная организация.
	СоздатьВТНачисленияПоДокументамИПлановыеДляБухучета(Запрос, ИмяВТНачисленияИсходная, РаспределениеСохраняемогоДС);
	УдалитьВТ.Добавить("ВТНачисленияПоДокументамИПлановыеДляБухучета");
	УдалитьВТ.Добавить("ВТСтатьиРасчетыПоОплатеТрудаПервичныхДокументов");
	
	// ВТБухучетНачисленийПоБазеСохраняемогоДС
	Если ПолучитьФункциональнуюОпцию("ИспользоватьРасчетСохраняемогоДенежногоСодержания") Тогда
		СоздатьВТБухучетНачисленийПоБазеСохраняемогоДС(Запрос, КоэффициентыРаспределенияДенежногоСодержания, СтрокиКоэффициентыСохраняемогоДС, ИмяВТНачисленияИсходная);
	Иначе
		СоздатьПустуюВТБухучетНачислений(Запрос, "ВТБухучетНачисленийПоБазеСохраняемогоДС");
	КонецЕсли;
	УдалитьВТ.Добавить("ВТБухучетНачисленийПоБазеСохраняемогоДС");
	
	// ВТБухучетНачисленийПоБазеСреднегоЗаработка
	ПараметрыКоэффициенты = Новый Структура("КоэффициентыСреднегоЗаработкаФССДокумента,КоэффициентыСреднегоЗаработкаДокумента,СтрокиКоэффициентыСреднегоЗаработка");
	ЗаполнитьЗначенияСвойств(ПараметрыКоэффициенты, ИсходныеДанные);
	СоздатьВТБухучетНачисленийПоБазеСреднегоЗаработка(Запрос, ИмяВТНачисленияИсходная, ПараметрыКоэффициенты, ПараметрыРаспределенияПоСтатьям);
	УдалитьВТ.Добавить("ВТБухучетНачисленийПоБазеСреднегоЗаработка");
	
	// ВТБухучетНачисленийПоФактическимНачислениям
	СоздатьВТБухучетНачисленийПоФактическимНачислениям(Запрос, ИмяВТНачисленияИсходная, ПараметрыРаспределенияПоСтатьям);
	УдалитьВТ.Добавить("ВТБухучетНачисленийПоФактическимНачислениям");
	
	// ВТНачисленияКакЗаданоВидуРасчетаДляБухучета
	СоздатьВТНачисленияКакЗаданоВидуРасчетаДляБухучета(Запрос, ИмяВТНачисленияИсходная);
	УдалитьВТ.Добавить("ВТНачисленияКакЗаданоВидуРасчетаДляБухучета");
	
	// ВТНачисленияПоБазе - отберем из таблицы ИмяВТНачисленияИсходная начисления со стратегией отражения ПоБазовымРасчетам.
	СоздатьВТНачисленияПоБазе(Запрос, УчитыватьСторноЗаписи, ИмяВТНачисленияИсходная);
	УдалитьВТ.Добавить("ВТНачисленияПоБазе");
	
	// ВТБухучетОсновногоЗаработка
	// начисления для которых задано распределение на текущий месяц.
	СоздатьВТБухучетОсновногоЗаработка(Запрос, ИмяВТНачисленияИсходная, РаспределениеСохраняемогоДС);
	УдалитьВТ.Добавить("ВТБухучетОсновногоЗаработка");
	
	// ВТБухучетОсновногоЗаработкаРаспределениеПоПодразделению
	// начисления для которых задано распределение на текущий месяц.
	СоздатьВТБухучетОсновногоЗаработкаРаспределениеПоПодразделению(Запрос, ИмяВТНачисленияИсходная);
	УдалитьВТ.Добавить("ВТБухучетОсновногоЗаработкаРаспределениеПоПодразделению");
	
	// ВТБухучетНачисленийБезБазы
	// бухучет всех оставшихся начислений, для которых применяется бухучет по умолчанию из таблицы ВТНастройкиБухучетаСотрудников
	// дополнительно в таблицу включаются данные из таблиц с постфиксом "...ДляБухучета".
	СоздатьВТБухучетНачисленийБезБазы(Запрос, ИмяВТНачисленияИсходная);
	УдалитьВТ.Добавить("ВТБухучетНачисленийБезБазы");
	
	// Объединим все результаты в таблице ВТБухучетНачисленийБезРаспределяемыхПоБазе
	// эта таблица будет источником данных для получения бухучета по базе начислений.
	ИменаТаблиц = Новый Массив; 
	ИменаТаблиц.Добавить("ВТБухучетНачисленийБезБазы");
	ИменаТаблиц.Добавить("ВТБухучетЕдиновременныеПособия");
	ИменаТаблиц.Добавить("ВТБухучетКомпенсацийЗаЗадержкуЗарплаты");
	ИменаТаблиц.Добавить("ВТБухучетНачисленийПоБазеСохраняемогоДС");
	ИменаТаблиц.Добавить("ВТБухучетНачисленийПоБазеСреднегоЗаработка");
	ИменаТаблиц.Добавить("ВТБухучетНачисленийПоФактическимНачислениям");
	ИменаТаблиц.Добавить("ВТБухучетОсновногоЗаработка");
	ИменаТаблиц.Добавить("ВТБухучетОсновногоЗаработкаРаспределениеПоПодразделению");
	ИменаТаблиц.Добавить("ВТБухучетСдельногоЗаработка");
	ИменаТаблиц.Добавить("ВТБухучетНачисленийПоПоказателям");
	ИменаТаблиц.Добавить("ВТБухучетПраздничныхСверхурочных");
	
	ШаблонТекстаЗапроса = 
	"ВЫБРАТЬ
	|	БухучетНачислений.ПериодРегистрации КАК ПериодРегистрации,
	|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	БухучетНачислений.Организация КАК Организация,
	|	БухучетНачислений.Сотрудник КАК Сотрудник,
	|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо, 
	|	БухучетНачислений.Подразделение КАК Подразделение,
	|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	БухучетНачислений.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	БухучетНачислений.Начисление КАК Начисление,
	|	БухучетНачислений.ВидОперации КАК ВидОперации,
	|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
	|	БухучетНачислений.ДатаОкончания КАК ДатаОкончания,
	|	БухучетНачислений.ДокументОснование КАК ДокументОснование,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачислений.Сумма КАК Сумма,
	|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД
	|ПОМЕСТИТЬ ВТБухучетНачисленийБезРаспределяемыхПоБазе
	|ИЗ
	|	#ИмяТаблицы КАК БухучетНачислений";
	
	ТекстЗапроса = "";
	Для Индекс = 0 По ИменаТаблиц.ВГраница() Цикл
		
		ИмяТаблицы = ИменаТаблиц[Индекс];
		
		Если Индекс > 0 Тогда
			ТекстЗапроса = ТекстЗапроса + " 
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|";
			ФрагментТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса,"ПОМЕСТИТЬ ВТБухучетНачисленийБезРаспределяемыхПоБазе","");
		Иначе
			ФрагментТекстаЗапроса = ШаблонТекстаЗапроса;
		КонецЕсли;
		
		ФрагментТекстаЗапроса = СтрЗаменить(ФрагментТекстаЗапроса, "#ИмяТаблицы", ИмяТаблицы);
		ТекстЗапроса = ТекстЗапроса + ФрагментТекстаЗапроса;
		
	КонецЦикла;
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТБухучетНачисленийБезРаспределяемыхПоБазе");
	
	// ВТБухучетНачисленияПоБазе
	СоздатьВТБухучетНачисленияПоБазе(Запрос, УчитыватьСторноЗаписи, ИсходныеДанные.МенеджерКадровогоУчета, ИсходныеДанные.МенеджерДанныхУчетаВремени);
	УдалитьВТ.Добавить("ВТБухучетНачисленияПоБазе");
	
	// ВТБухучетНачислений - объединение сведений о бухучете начислений.
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БухучетНачислений.ПериодРегистрации КАК ПериодРегистрации,
	|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	БухучетНачислений.ПериодПринятияРасходов КАК ПериодПринятияРасходов,
	|	БухучетНачислений.Организация КАК Организация,
	|	БухучетНачислений.Сотрудник КАК Сотрудник,
	|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
	|	БухучетНачислений.Подразделение КАК Подразделение,
	|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	БухучетНачислений.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	БухучетНачислений.Начисление КАК Начисление,
	|	БухучетНачислений.ВидОперации КАК ВидОперации,
	|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
	|	БухучетНачислений.ДатаОкончания КАК ДатаОкончания,
	|	БухучетНачислений.ДокументОснование КАК ДокументОснование,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачислений.ВидНачисленияОплатыТрудаДляНУ КАК ВидНачисленияОплатыТрудаДляНУ,
	|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	СУММА(БухучетНачислений.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВТБухучетНачисленийВыходнаяТаблица
	|ИЗ
	|	(ВЫБРАТЬ
	|		БухучетНачисления.ПериодРегистрации КАК ПериодРегистрации,
	|		БухучетНачисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|		ВЫБОР
	|			КОГДА БухучетНачисления.ДатаНачала = ДАТАВРЕМЯ(1, 1, 1)
	|					ИЛИ БухучетНачисления.ДатаНачала < БухучетНачисления.ПериодРегистрации
	|				ТОГДА БухучетНачисления.ПериодРегистрации
	|			ИНАЧЕ НАЧАЛОПЕРИОДА(БухучетНачисления.ДатаНачала, МЕСЯЦ)
	|		КОНЕЦ КАК ПериодПринятияРасходов,
	|		БухучетНачисления.Организация КАК Организация,
	|		БухучетНачисления.Сотрудник КАК Сотрудник,
	|		БухучетНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|		БухучетНачисления.Подразделение КАК Подразделение,
	|		ВЫБОР
	|			КОГДА БухучетНачисления.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|				ТОГДА БухучетНачисления.Подразделение
	|			ИНАЧЕ БухучетНачисления.ПодразделениеУчетаЗатрат
	|		КОНЕЦ КАК ПодразделениеУчетаЗатрат,
	|		БухучетНачисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|		БухучетНачисления.Начисление КАК Начисление,
	|		БухучетНачисления.ВидОперации КАК ВидОперации,
	|		БухучетНачисления.ДатаНачала КАК ДатаНачала,
	|		БухучетНачисления.ДатаОкончания КАК ДатаОкончания,
	|		БухучетНачисления.ДокументОснование КАК ДокументОснование,
	|		БухучетНачисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|		БухучетНачисления.СтатьяФинансирования КАК СтатьяФинансирования,
	|		&СтатьяРасходов КАК СтатьяРасходов,
	|		БухучетНачисления.Сумма КАК Сумма,
	|		НачислениеВидНачисленияОплатыТрудаДляНУ.ВидНачисленияОплатыТрудаДляНУ КАК ВидНачисленияОплатыТрудаДляНУ,
	|		БухучетНачисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД
	|	ИЗ
	|		ВТБухучетНачисленийБезРаспределяемыхПоБазе КАК БухучетНачисления
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТНачислениеВидНачисленияОплатыТрудаДляНУ КАК НачислениеВидНачисленияОплатыТрудаДляНУ
	|			ПО БухучетНачисления.Начисление = НачислениеВидНачисленияОплатыТрудаДляНУ.Начисление
	|				И (&ДополнительноеСоединение)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		БухучетНачисления.ПериодРегистрации,
	|		БухучетНачисления.ИдентификаторСтроки,
	|		ВЫБОР
	|			КОГДА БухучетНачисления.ДатаНачала = ДАТАВРЕМЯ(1, 1, 1)
	|					ИЛИ БухучетНачисления.ДатаНачала < БухучетНачисления.ПериодРегистрации
	|				ТОГДА БухучетНачисления.ПериодРегистрации
	|			ИНАЧЕ НАЧАЛОПЕРИОДА(БухучетНачисления.ДатаНачала, МЕСЯЦ)
	|		КОНЕЦ,
	|		БухучетНачисления.Организация,
	|		БухучетНачисления.Сотрудник,
	|		БухучетНачисления.ФизическоеЛицо,
	|		БухучетНачисления.Подразделение,
	|		ВЫБОР
	|			КОГДА БухучетНачисления.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|				ТОГДА БухучетНачисления.Подразделение
	|			ИНАЧЕ БухучетНачисления.ПодразделениеУчетаЗатрат
	|		КОНЕЦ,
	|		БухучетНачисления.ТерриторияВыполненияРаботВОрганизации,
	|		БухучетНачисления.Начисление,
	|		БухучетНачисления.ВидОперации,
	|		БухучетНачисления.ДатаНачала,
	|		БухучетНачисления.ДатаОкончания,
	|		БухучетНачисления.ДокументОснование,
	|		БухучетНачисления.СпособОтраженияЗарплатыВБухучете,
	|		БухучетНачисления.СтатьяФинансирования,
	|		&СтатьяРасходов,
	|		БухучетНачисления.Сумма,
	|		НачислениеВидНачисленияОплатыТрудаДляНУ.ВидНачисленияОплатыТрудаДляНУ,
	|		БухучетНачисления.ОблагаетсяЕНВД
	|	ИЗ
	|		ВТБухучетНачисленияПоБазе КАК БухучетНачисления
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТНачислениеВидНачисленияОплатыТрудаДляНУ КАК НачислениеВидНачисленияОплатыТрудаДляНУ
	|			ПО БухучетНачисления.Начисление = НачислениеВидНачисленияОплатыТрудаДляНУ.Начисление
	|				И (&ДополнительноеСоединение)) КАК БухучетНачислений
	|
	|СГРУППИРОВАТЬ ПО
	|	БухучетНачислений.ВидНачисленияОплатыТрудаДляНУ,
	|	БухучетНачислений.ОблагаетсяЕНВД,
	|	БухучетНачислений.ДатаОкончания,
	|	БухучетНачислений.ИдентификаторСтроки,
	|	БухучетНачислений.ВидОперации,
	|	БухучетНачислений.ПериодПринятияРасходов,
	|	БухучетНачислений.Организация,
	|	БухучетНачислений.ДокументОснование,
	|	БухучетНачислений.ПериодРегистрации,
	|	БухучетНачислений.СтатьяРасходов,
	|	БухучетНачислений.Сотрудник,
	|	БухучетНачислений.СтатьяФинансирования,
	|	БухучетНачислений.ФизическоеЛицо,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.Начисление,
	|	БухучетНачислений.Подразделение,
	|	БухучетНачислений.ТерриторияВыполненияРаботВОрганизации,
	|	БухучетНачислений.ДатаНачала,
	|	БухучетНачислений.ПодразделениеУчетаЗатрат";
	
	СтрокаЗаменыСоединения = "И (&ДополнительноеСоединение)";
	ОписаниеПоляСтатьяРасходы = "&СтатьяРасходов";
	
	Если Не ИспользоватьСтатьиФинансирования
		И (МенеджерВременныхТаблиц.Таблицы[ИмяВТНачисленияИсходная].Колонки.Найти("СтатьяРасходов") <> Неопределено) Тогда
		
		ТекстОписанияСоединения = "ЛЕВОЕ СОЕДИНЕНИЕ "+ ИмяВТНачисленияИсходная +" КАК НачисленияДляРаспределения
		|	ПО БухучетНачисления.ИдентификаторСтроки = НачисленияДляРаспределения.ИдентификаторСтроки";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ОписаниеПоляСтатьяРасходы, "НачисленияДляРаспределения.СтатьяРасходов");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, СтрокаЗаменыСоединения, ТекстОписанияСоединения);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ОписаниеПоляСтатьяРасходы, "БухучетНачисления.СтатьяРасходов");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, СтрокаЗаменыСоединения, "");
		
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТБухучетНачисленийВыходнаяТаблица", ИмяВТБухучетНачислений);
	Запрос.Выполнить();
	
	УдалитьВТ.Добавить("ВТСтрокиКУдалению");
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, УдалитьВТ);
	
КонецПроцедуры

Процедура ДополнитьСведенияОВзносахДаннымиБухучета(Движения, Организация, ПериодРегистрации, Ссылка, МенеджерВременныхТаблиц, ИмяВременнойТаблицы) Экспорт

	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата") Тогда
		СоздатьВТСтраховыеВзносыПоИсточникамФинансирования(Движения, Организация, ПериодРегистрации, Ссылка, МенеджерВременныхТаблиц);
		ИмяВременнойТаблицы = "ВТСтраховыеВзносыПоИсточникамФинансирования";
	КонецЕсли;
	
КонецПроцедуры

// Формирует таблицу значений с коэффициентами распределения сохраняемого денежного содержания
// Параметры
//		РаспределениеСохраняемыхНачислений - Коллекция, содержит результаты распределения начислений,
//											сохраняемых при расчете сохраняемого ДС
//			* Начисление
//			* РаспределениеПоСтатьям - ТаблицаЗначений, структура см НоваяТаблицаРаспределениеРезультатовНачислений
//			* Сотрудник
//		НачисленияДляРаспределения - ТаблицаЗначений, содержит начисления, учитываемые по фактическим начислениям, а также строки с 
//							с результатами расчета РК и СН
//			* Начисление
//			* Результат
//			* Сотрудник
//			* Подразделение
//		НазначениеРасчета
//		Организация
//		ПериодРасчета
//		ДатаНачалаСобытия
//
//	Возвращаемое значение - ТаблицаЗначений, описание см НоваяТаблицаКоэффициентыРаспределенияСохраняемогоДС
//
Функция ТаблицаКоэффициентовРаспределенияСохраняемогоДС(РаспределениеСохраняемыхНачислений, НачисленияДляРаспределения, НазначениеРасчета, Организация, ПериодРасчета, ДатаНачалаСобытия) Экспорт
	
	ТаблицаКоэффициентов = НоваяТаблицаКоэффициентыРаспределенияСохраняемогоДС();
	ТаблицаКоэффициентов.Колонки.Добавить("Начисление", Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Начисления"));
	ТаблицаКоэффициентов.Колонки.Коэффициент.Имя = "Результат";
	
	// Переносим в таблицу результаты распределения сохраняемых начислений.
	Для каждого СтрокаТЗ Из РаспределениеСохраняемыхНачислений Цикл
		
		Если СтрокаТЗ.РаспределениеПоСтатьям <> Неопределено Тогда
			
			Для каждого СтрокаРаспределения Из СтрокаТЗ.РаспределениеПоСтатьям Цикл
			
				НоваяСтрока = ТаблицаКоэффициентов.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРаспределения);
				НоваяСтрока.Сотрудник 			= СтрокаТЗ.Сотрудник;
				НоваяСтрока.Начисление 			= СтрокаТЗ.Начисление;
				НоваяСтрока.НазначениеРасчета 	= НазначениеРасчета;
			
			КонецЦикла;
			
		КонецЕсли;
	
	КонецЦикла;
	ОтражениеЗарплатыВУчете.СвернутьТаблицу(ТаблицаКоэффициентов);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// Заполним соответствие базовых в.р. для РК и СН, при условии, что стратегия отражения в бухучете - ПоБазовымРасчетам.
	БазовыеВидыРасчета = Новый Соответствие;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НачисленияБазовыеВидыРасчета.Ссылка КАК Ссылка,
	|	НачисленияБазовыеВидыРасчета.ВидРасчета КАК ВидРасчета
	|ИЗ
	|	ПланВидовРасчета.Начисления.БазовыеВидыРасчета КАК НачисленияБазовыеВидыРасчета
	|ГДЕ
	|	НачисленияБазовыеВидыРасчета.Ссылка.КатегорияНачисленияИлиНеоплаченногоВремени В (ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.РайонныйКоэффициент), ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.СевернаяНадбавка))
	|	И НачисленияБазовыеВидыРасчета.Ссылка.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Ссылка") Цикл
		ОсновнойВР = Выборка.Ссылка;
		БазовыеВР = Новый Массив;
		Пока Выборка.Следующий() Цикл
			БазовыеВР.Добавить(Выборка.ВидРасчета);
		КонецЦикла;
		БазовыеВидыРасчета.Вставить(ОсновнойВР, БазовыеВР);
	КонецЦикла;
	
	Начисления = ОтражениеЗарплатыВУчете.НоваяТаблицаНачислений();
	НачислениеУдержаниеВидОперации = ОтражениеЗарплатыВУчете.НачислениеУдержаниеВидОперации();
	
	ТаблицаРКиСН = НачисленияДляРаспределения.СкопироватьКолонки();
	
	ИдентификаторСтроки = 1;
	Для каждого СтрокаТЗ Из НачисленияДляРаспределения Цикл
		
		Если БазовыеВидыРасчета[СтрокаТЗ.Начисление] <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ТаблицаРКиСН.Добавить(), СтрокаТЗ);
			Продолжить;
		КонецЕсли;	
		
		НоваяСтрока = Начисления.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
		
		НоваяСтрока.ИдентификаторСтроки = ИдентификаторСтроки;
		НоваяСтрока.Организация 		= Организация;
		НоваяСтрока.ДатаНачала 			= ДатаНачалаСобытия;
		НоваяСтрока.ДатаОкончания 		= КонецМесяца(ДатаНачалаСобытия);
		НоваяСтрока.ВидОперации 		= НачислениеУдержаниеВидОперации[НоваяСтрока.Начисление];
		НоваяСтрока.Сумма 				= СтрокаТЗ.Результат;
		
		ИдентификаторСтроки = ИдентификаторСтроки + 1;
		
	КонецЦикла;
	
	Если Начисления.Количество() > 0 Тогда
		
		ИсходныеДанные = ОписаниеИсходныхДанныхДляОтраженияНачисленийВБухучете();
		Запрос.УстановитьПараметр("РасходыПоСтрахованию", ИсходныеДанные.ВидыОперацийРасходыПоСтрахованию);
		Запрос.УстановитьПараметр("РасходыФСС", ИсходныеДанные.ВидыОперацийРасходыФСС);
		
		// Получим настройки начислений, для начислений со стратегией бухучета ПоБазовымРасчетам и ПоБазеСреднегоЗаработка
		// устанавливаем пустое значение
		СоздатьВТНастройкиНачислений(Запрос, Истина);
		
		Запрос.УстановитьПараметр("Начисления", Начисления);
		Запрос.УстановитьПараметр("ПериодРегистрации", НачалоМесяца(ПериодРасчета));
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Начисления.Организация КАК Организация,
		|	&ПериодРегистрации КАК ПериодРегистрации,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Начисления.Подразделение КАК Подразделение,
		|	Начисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	Начисления.Начисление КАК Начисление,
		|	Начисления.ДатаНачала КАК ДатаНачала,
		|	Начисления.ДатаОкончания КАК ДатаОкончания,
		|	Начисления.Сумма КАК Сумма,
		|	Начисления.ДокументОснование КАК ДокументОснование,
		|	Начисления.ВидОперации КАК ВидОперации,
		|	ЛОЖЬ КАК РассчитыватьПоРазовымНачислениямДокумента
		|ПОМЕСТИТЬ ВТНачисленияТаблица
		|ИЗ
		|	&Начисления КАК Начисления";
		Запрос.Выполнить();
		
		ИсходныеДанные.МенеджерВременныхТаблиц    = Запрос.МенеджерВременныхТаблиц;
		ИсходныеДанные.Организация    			  = Организация;
		ИсходныеДанные.МесяцНачисления 			  = ПериодРасчета;
		ИсходныеДанные.ИмяВТБухучетНачислений     = "ВТБухучетНачислений";
		ИсходныеДанные.ИмяВТНачисления			  = "ВТНачисленияТаблица";
		ИсходныеДанные.ПроцентЕНВД    			  = ОтражениеЗарплатыВБухучете.ПроцентЕНВД(Организация, ПериодРасчета);
		ИсходныеДанные.ПлательщикЕНВД             = ОтражениеЗарплатыВБухучете.ПлательщикЕНВД(Организация, ПериодРасчета);
		
		СоздатьВТБухучетНачисленийБезДоговоровГПХ(ИсходныеДанные, ИсходныеДанные.ИмяВТБухучетНачислений);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачислений.Сотрудник КАК Сотрудник,
		|	БухучетНачислений.Начисление КАК Начисление,
		|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	СУММА(БухучетНачислений.Сумма) КАК Результат
		|ИЗ
		|	ВТБухучетНачислений КАК БухучетНачислений
		|
		|СГРУППИРОВАТЬ ПО
		|	БухучетНачислений.Сотрудник,
		|	БухучетНачислений.Начисление,
		|	БухучетНачислений.ПодразделениеУчетаЗатрат,
		|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачислений.СтатьяФинансирования,
		|	БухучетНачислений.СтатьяРасходов,
		|	БухучетНачислений.ОблагаетсяЕНВД";
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = ТаблицаКоэффициентов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.НазначениеРасчета 	= НазначениеРасчета;
		КонецЦикла;
		
	КонецЕсли;
	
	Для каждого СтрокаРКиСН Из ТаблицаРКиСН Цикл
		
		БазовыеВР = БазовыеВидыРасчета[СтрокаРКиСН.Начисление];
		ТаблицаРаспределения = ТаблицаКоэффициентов.СкопироватьКолонки();
		Для каждого СтрокаТЗ Из ТаблицаКоэффициентов Цикл
			Если ЗначениеЗаполнено(СтрокаТЗ.Начисление) И БазовыеВР.Найти(СтрокаТЗ.Начисление) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(ТаблицаРаспределения.Добавить(), СтрокаТЗ);
		КонецЦикла;
		
		ТаблицаРаспределения.Свернуть("ПодразделениеУчетаЗатрат,СтатьяФинансирования,СтатьяРасходов,СпособОтраженияЗарплатыВБухучете,ОблагаетсяЕНВД","Результат");
		Если ТаблицаРаспределения.Количество() > 0 Тогда
			ЗарплатаКадры.РазнестиСуммуПоБазе(СтрокаРКиСН.Результат, ТаблицаРаспределения, "Результат");
		КонецЕсли;
		
		Для каждого СтрокаТЗ Из ТаблицаРаспределения Цикл
			НоваяСтрока = ТаблицаКоэффициентов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
			НоваяСтрока.НазначениеРасчета = НазначениеРасчета;
		КонецЦикла;
		
	КонецЦикла;
	
	ТаблицаКоэффициентов.Колонки.Удалить("Начисление");
	ОтражениеЗарплатыВУчете.СвернутьТаблицу(ТаблицаКоэффициентов);
	ТаблицаКоэффициентов.Колонки.Результат.Имя = "Коэффициент";
	
	Возврат ТаблицаКоэффициентов;

КонецФункции

Функция ВидыОперацийПоЗарплатеБезСпособаОтражения() Экспорт

	РасходыБезСпособаОтражения = Новый Массив;
	РасходыБезСпособаОтражения.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФСС);
	РасходыБезСпособаОтражения.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФССНС);
	РасходыБезСпособаОтражения.Добавить(Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЗаЗадержкуЗарплаты);
	РасходыБезСпособаОтражения.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПособиеНаПогребение);
	
	Возврат РасходыБезСпособаОтражения;

КонецФункции

#КонецОбласти

#Область ФормированиеДвижений

Процедура СформироватьДвиженияБухучетНачисленияУдержанияПоКонтрагентамАкционерам(Движения, Отказ, Организация, ПериодРегистрации, Начисления, Удержания, РезультатыРасчетаНДФЛ, ЗаписыватьДвижения = Ложь) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата") Тогда
		Возврат;
	КонецЕсли;
	
	НачислениеУдержаниеВидОперации = ОтражениеЗарплатыВУчете.НачислениеУдержаниеВидОперации();
	
	Если Начисления <> НеОпределено Тогда
		
		ВидыДоходаИсполнительногоПроизводства = УчетНачисленнойЗарплаты.ВидыДоходовИсполнительногоПроизводстваНачислений();
		
		Для Каждого Строка Из Начисления Цикл
			
			НоваяСтрока = Движения.БухучетНачисленияУдержанияПоКонтрагентамАкционерам.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			НоваяСтрока.Период				= ПериодРегистрации;
			НоваяСтрока.НачислениеУдержание	= Строка.Начисление;
			НоваяСтрока.Организация			= Организация;
			НоваяСтрока.ВидОперации			= НачислениеУдержаниеВидОперации[НоваяСтрока.НачислениеУдержание];
			Если Не ЗначениеЗаполнено(НоваяСтрока.ТерриторияВыполненияРаботВОрганизации) Тогда
				НоваяСтрока.ТерриторияВыполненияРаботВОрганизации = НоваяСтрока.Подразделение;
			КонецЕсли;
			НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Начислено;
			НоваяСтрока.ВидДоходаИсполнительногоПроизводства = ВидыДоходаИсполнительногоПроизводства[Строка.Начисление];
			
		КонецЦикла;
		
		Движения.БухучетНачисленияУдержанияПоКонтрагентамАкционерам.Записывать = Истина;
		
	КонецЕсли;
	
	Если Удержания <> НеОпределено Тогда
		
		Для Каждого Строка Из Удержания Цикл
			
			НоваяСтрока = Движения.БухучетНачисленияУдержанияПоКонтрагентамАкционерам.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			ВидОперации = НачислениеУдержаниеВидОперации[НоваяСтрока.НачислениеУдержание];
			Если ВидОперации = Перечисления.ВидыОперацийПоЗарплате.АлиментыПрочиеИсполнительныеЛисты Тогда
				ВидОперации = Перечисления.ВидыОперацийПоЗарплате.АлиментыПрочиеИсполнительныеЛистыКонтрагенты;
			ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ВознаграждениеПлатежногоАгента Тогда
				ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ВознаграждениеПлатежногоАгентаКонтрагенты;
			КонецЕсли;
			
			НоваяСтрока.Период				= ПериодРегистрации;
			НоваяСтрока.Организация			= Организация;
			НоваяСтрока.ВидОперации			= ВидОперации;
			
			Если Строка.НачислениеУдержание = Перечисления.ВидыОсобыхНачисленийИУдержаний.МатериальнаяВыгодаПоЗаймам
					Или Строка.НачислениеУдержание = Перечисления.ВидыОсобыхНачисленийИУдержаний.НачисленоПроцентовПоЗайму Тогда
				НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Справочно;
			Иначе
				НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Удержано;
			КонецЕсли
			
		КонецЦикла;
		
		Движения.БухучетНачисленияУдержанияПоКонтрагентамАкционерам.Записывать = Истина;
		
	КонецЕсли;

	Если РезультатыРасчетаНДФЛ <> НеОпределено Тогда
		
		Для Каждого Строка Из РезультатыРасчетаНДФЛ Цикл
			
			НоваяСтрока = Движения.БухучетНачисленияУдержанияПоКонтрагентамАкционерам.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			НоваяСтрока.Период				= ПериодРегистрации;
			НоваяСтрока.Организация			= Организация;
			НоваяСтрока.ВидОперации			= НачислениеУдержаниеВидОперации[НоваяСтрока.НачислениеУдержание];
			НоваяСтрока.ТерриторияВыполненияРаботВОрганизации 	= Строка.Подразделение;
			НоваяСтрока.ГруппаНачисленияУдержанияВыплаты 		= Перечисления.ГруппыНачисленияУдержанияВыплаты.Удержано;
			НоваяСтрока.НачислениеУдержание 					= Строка.ВидУдержания;
			
		КонецЦикла;
		
		Движения.БухучетНачисленияУдержанияПоКонтрагентамАкционерам.Записывать = Истина;
		
	КонецЕсли;
	
	Если ЗаписыватьДвижения Тогда
		Движения.БухучетНачисленияУдержанияПоКонтрагентамАкционерам.Записать();
		Движения.БухучетНачисленияУдержанияПоКонтрагентамАкционерам.Записывать = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Добавляет в коллекцию движений запись о бухучете планового начисления.
// 
// Параметры:
//			Движения,
//			БухучетПлановыхНачислений - Коллекция записей c полями,
//				Обязательные:
//				- Период
//				- Сотрудник
//				- Организация
//				- Начисление
//				- Используется - булево
//				Необязательные:
//				- СпособОтраженияЗарплатыВБухучете - если не заполнено, то подбирается автоматически.
//				- СтатьяФинансирования - если не заполнено, то подбирается автоматически.
//				- СтатьяЗатрат - если не заполнено, то подбирается автоматически.
//				- ОтношениеКЕНВД - если не заполнено, то подбирается автоматически
//				- ДействуетДо.
//
Процедура СформироватьДвиженияБухучетПлановыхНачислений(Движения, БухучетПлановыхНачислений) Экспорт
		
	Для каждого ОтражениеНачисления Из БухучетПлановыхНачислений Цикл
		НоваяЗапись = Движения.БухучетПлановыхНачислений.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ОтражениеНачисления);
		Движения.БухучетПлановыхНачислений.Записывать = Истина;
	КонецЦикла;
	
	Если Движения.БухучетПлановыхНачислений.Записывать Тогда
		Движения.БухучетПлановыхНачислений.Записать();
		Движения.БухучетПлановыхНачислений.Записывать = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Добавляет запись о бухучете зарплаты по документу основанию.
//
Процедура ЗарегистрироватьБухучетЗарплатыПервичныхДокументов(БухучетЗарплаты) Экспорт
	
	НаборЗаписей = РегистрыСведений.БухучетЗарплатыПервичныхДокументов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ДокументОснование.Установить(БухучетЗарплаты.ДокументОснование);
	
	Для каждого СтрокаТаблицы Из БухучетЗарплаты.ТаблицаБухучетЗарплаты Цикл
		НоваяЗапись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаТаблицы);
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Добавляет в коллекцию движений запись о бухучете планового удержания.
// 
// Параметры:
//			Движения,
//			БухучетПлановыхУдержаний - Коллекция записей c полями.
//
Процедура СформироватьДвиженияБухучетПлановыхУдержаний(Движения, БухучетПлановыхУдержаний) Экспорт
		
	Для каждого Строка Из БухучетПлановыхУдержаний Цикл
		НоваяЗапись = Движения.БухучетПлановыхУдержаний.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, Строка);
		Движения.БухучетПлановыхУдержаний.Записывать = Истина;
	КонецЦикла;
	
	Если Движения.БухучетПлановыхУдержаний.Записывать Тогда
		Движения.БухучетПлановыхУдержаний.Записать();
		Движения.БухучетПлановыхУдержаний.Записывать = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет данные для проведения плановых удержаний для многосотрудникового документа.
//
// Параметры:
//		ДанныеДляПроведения - Структура, описанная в СоздатьДанныеДляРегистрацииПлановыхУдержаний.
//		Документ - Ссылка на документ.
//
Функция ДанныеДляРегистрацииБухучетаПлановыхУдержаний(ДокументСсылка) Экспорт
	
	МетаданныеДокумента = ДокументСсылка.Метаданные();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документ", ДокументСсылка);
	Запрос.УстановитьПараметр("ПустойДокументОснование", Документы[МетаданныеДокумента.Имя].ПустаяСсылка());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументУдержания.Ссылка.Организация КАК Организация,
	|	ДокументУдержания.ФизическоеЛицо,
	|	ДокументУдержания.Ссылка.ДатаНачала КАК Период,
	|	ВЫБОР
	|		КОГДА ДокументУдержания.Ссылка.ДатаОкончания > ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ДОБАВИТЬКДАТЕ(ДокументУдержания.Ссылка.ДатаОкончания, ДЕНЬ, 1)
	|		ИНАЧЕ ДокументУдержания.Ссылка.ДатаОкончания
	|	КОНЕЦ КАК ДействуетДо,
	|	ДокументУдержания.Ссылка.Удержание,
	|	ВЫБОР
	|		КОГДА ДокументУдержания.Ссылка.ДокументОснование = &ПустойДокументОснование
	|			ТОГДА ДокументУдержания.Ссылка
	|		ИНАЧЕ ДокументУдержания.Ссылка.ДокументОснование
	|	КОНЕЦ КАК ДокументОснование,
	|	ДокументУдержания.Ссылка.СтатьяФинансирования,
	|	ДокументУдержания.Ссылка.СтатьяРасходов
	|ПОМЕСТИТЬ ВТДанныеДокумента
	|ИЗ
	|	#ТаблицаУдержанийДокумента КАК ДокументУдержания
	|ГДЕ
	|	ДокументУдержания.Ссылка = &Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.ФизическоеЛицо,
	|	ДанныеДокумента.ДокументОснование,
	|	ДанныеДокумента.Период,
	|	МАКСИМУМ(БухучетПлановыхУдержаний.Период) КАК ПериодРегистра
	|ПОМЕСТИТЬ ВТМаксимальныеПериоды
	|ИЗ
	|	ВТДанныеДокумента КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетПлановыхУдержаний КАК БухучетПлановыхУдержаний
	|		ПО ДанныеДокумента.ДокументОснование = БухучетПлановыхУдержаний.ДокументОснование
	|			И ДанныеДокумента.ФизическоеЛицо = БухучетПлановыхУдержаний.ФизическоеЛицо
	|			И ДанныеДокумента.Период > БухучетПлановыхУдержаний.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.ФизическоеЛицо,
	|	ДанныеДокумента.ДокументОснование,
	|	ДанныеДокумента.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаксимальныеПериоды.ФизическоеЛицо,
	|	МаксимальныеПериоды.Период,
	|	ВЫБОР
	|		КОГДА БухучетПлановыхУдержаний.ДействуетДо <> ДАТАВРЕМЯ(1, 1, 1)
	|				И БухучетПлановыхУдержаний.ДействуетДо < МаксимальныеПериоды.Период
	|			ТОГДА БухучетПлановыхУдержаний.СтатьяФинансированияПоОкончании
	|		ИНАЧЕ БухучетПлановыхУдержаний.СтатьяФинансирования
	|	КОНЕЦ КАК СтатьяФинансирования,
	|	ВЫБОР
	|		КОГДА БухучетПлановыхУдержаний.ДействуетДо <> ДАТАВРЕМЯ(1, 1, 1)
	|				И БухучетПлановыхУдержаний.ДействуетДо < МаксимальныеПериоды.Период
	|			ТОГДА БухучетПлановыхУдержаний.СтатьяРасходовПоОкончании
	|		ИНАЧЕ БухучетПлановыхУдержаний.СтатьяРасходов
	|	КОНЕЦ КАК СтатьяРасходов
	|ПОМЕСТИТЬ ВТЗначенияПоОкончании
	|ИЗ
	|	ВТМаксимальныеПериоды КАК МаксимальныеПериоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетПлановыхУдержаний КАК БухучетПлановыхУдержаний
	|		ПО МаксимальныеПериоды.ФизическоеЛицо = БухучетПлановыхУдержаний.ФизическоеЛицо
	|			И МаксимальныеПериоды.ДокументОснование = БухучетПлановыхУдержаний.ДокументОснование
	|			И МаксимальныеПериоды.ПериодРегистра = БухучетПлановыхУдержаний.Период
	|ГДЕ
	|	ВЫБОР
	|			КОГДА БухучетПлановыхУдержаний.ДействуетДо <> ДАТАВРЕМЯ(1, 1, 1)
	|					И БухучетПлановыхУдержаний.ДействуетДо < МаксимальныеПериоды.Период
	|				ТОГДА БухучетПлановыхУдержаний.ИспользуетсяПоОкончании
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.ФизическоеЛицо,
	|	ДанныеДокумента.Период,
	|	ДанныеДокумента.ДействуетДо,
	|	ДанныеДокумента.Удержание,
	|	ДанныеДокумента.ДокументОснование,
	|	ДанныеДокумента.СтатьяФинансирования,
	|	ДанныеДокумента.СтатьяРасходов,
	|	ЗначенияПоОкончании.СтатьяФинансирования КАК СтатьяФинансированияПоОкончании,
	|	ЗначенияПоОкончании.СтатьяРасходов КАК СтатьяРасходовПоОкончании,
	|	ВЫБОР
	|		КОГДА ЗначенияПоОкончании.СтатьяФинансирования ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ИспользуетсяПоОкончании
	|ИЗ
	|	ВТДанныеДокумента КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗначенияПоОкончании КАК ЗначенияПоОкончании
	|		ПО ДанныеДокумента.ФизическоеЛицо = ЗначенияПоОкончании.ФизическоеЛицо
	|			И ДанныеДокумента.Период = ЗначенияПоОкончании.Период";
	
	ИмяДокумента = МетаданныеДокумента.ПолноеИмя();
	Если МетаданныеДокумента.ТабличныеЧасти.Найти("Удержания") = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДокументУдержания.Ссылка.", "ДокументУдержания.");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ТаблицаУдержанийДокумента", ИмяДокумента);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ТаблицаУдержанийДокумента", ИмяДокумента + ".Удержания");
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Для плановых начислений, которые прекращаются навсегда в регистре БухучетПлановыхНачислений регистрируется прекращение
// действия настроек бухучета для этого начисления.
//
Процедура ПлановыеНачисленияПриЗаписи(НаборЗаписей) Экспорт
	
	Если НаборЗаписей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПлановыеНачисления = НаборЗаписей.Выгрузить();
	ПлановыеНачисления = ПлановыеНачисления.Скопировать(Новый Структура("Используется,ДействуетДо",Ложь,Дата(1,1,1)));
	Если ПлановыеНачисления.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПлановыеНачисления", ПлановыеНачисления);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПлановыеНачисления.Период КАК Период,
	|	ПлановыеНачисления.Регистратор КАК Регистратор,
	|	ПлановыеНачисления.Сотрудник КАК Сотрудник,
	|	ПлановыеНачисления.Начисление КАК Начисление,
	|	ПлановыеНачисления.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ ВТПлановыеНачисления
	|ИЗ
	|	&ПлановыеНачисления КАК ПлановыеНачисления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПлановыеНачисления.Регистратор КАК Регистратор,
	|	НАЧАЛОПЕРИОДА(ПлановыеНачисления.Период, ДЕНЬ) КАК Период,
	|	БухучетПлановыхНачислений.Сотрудник КАК Сотрудник,
	|	БухучетПлановыхНачислений.Организация КАК Организация,
	|	БухучетПлановыхНачислений.Начисление КАК Начисление,
	|	БухучетПлановыхНачислений.ДокументОснование КАК ДокументОснование,
	|	МАКСИМУМ(БухучетПлановыхНачислений.Период) КАК ПериодРегистра
	|ПОМЕСТИТЬ ВТДатыПоследнихДвижений
	|ИЗ
	|	ВТПлановыеНачисления КАК ПлановыеНачисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетПлановыхНачислений КАК БухучетПлановыхНачислений
	|		ПО ПлановыеНачисления.Сотрудник = БухучетПлановыхНачислений.Сотрудник
	|			И ПлановыеНачисления.Начисление = БухучетПлановыхНачислений.Начисление
	|			И ПлановыеНачисления.ДокументОснование = БухучетПлановыхНачислений.ДокументОснование
	|			И (НАЧАЛОПЕРИОДА(ПлановыеНачисления.Период, ДЕНЬ) >= БухучетПлановыхНачислений.Период)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПлановыеНачисления.Регистратор,
	|	НАЧАЛОПЕРИОДА(ПлановыеНачисления.Период, ДЕНЬ),
	|	БухучетПлановыхНачислений.Сотрудник,
	|	БухучетПлановыхНачислений.Организация,
	|	БухучетПлановыхНачислений.Начисление,
	|	БухучетПлановыхНачислений.ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыПоследнихДвижений.Регистратор КАК Регистратор,
	|	ДатыПоследнихДвижений.Период КАК Период,
	|	БухучетПлановыхНачислений.Сотрудник КАК Сотрудник,
	|	БухучетПлановыхНачислений.Организация КАК Организация,
	|	БухучетПлановыхНачислений.Начисление КАК Начисление,
	|	БухучетПлановыхНачислений.ДокументОснование КАК ДокументОснование,
	|	ЛОЖЬ КАК Используется
	|ИЗ
	|	ВТДатыПоследнихДвижений КАК ДатыПоследнихДвижений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетПлановыхНачислений КАК БухучетПлановыхНачислений
	|		ПО ДатыПоследнихДвижений.ПериодРегистра = БухучетПлановыхНачислений.Период
	|			И (НЕ БухучетПлановыхНачислений.НеИзменятьПриОтменеНачисления)
	|			И ДатыПоследнихДвижений.Период <> БухучетПлановыхНачислений.Период
	|			И ДатыПоследнихДвижений.Сотрудник = БухучетПлановыхНачислений.Сотрудник
	|			И ДатыПоследнихДвижений.Организация = БухучетПлановыхНачислений.Организация
	|			И ДатыПоследнихДвижений.Начисление = БухучетПлановыхНачислений.Начисление
	|			И ДатыПоследнихДвижений.ДокументОснование = БухучетПлановыхНачислений.ДокументОснование
	|			И (БухучетПлановыхНачислений.Используется)
	|			И (БухучетПлановыхНачислений.ДействуетДо = ДАТАВРЕМЯ(1, 1, 1))";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	Выборка = Результат.Выбрать();
	
	БухучетПлановыхНачислений = РегистрыСведений.БухучетПлановыхНачислений.СоздатьНаборЗаписей();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(БухучетПлановыхНачислений.Добавить(), Выборка);
		Регистратор = Выборка.Регистратор;
	КонецЦикла;
	БухучетПлановыхНачислений.Отбор.Регистратор.Установить(Регистратор);
	БухучетПлановыхНачислений.Записать();

КонецПроцедуры

Процедура СформироватьДвиженияПоДокументу(Движения, Отказ, Организация, ПериодРегистрации, ДанныеДляПроведения, СтрокаСписокТаблиц) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрНайти(СтрокаСписокТаблиц, "НачисленныйНДФЛ") > 0 Тогда
		
		Если ДанныеДляПроведения.НачисленияУдержания = Неопределено Или ДанныеДляПроведения.НачисленияУдержания.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ВидыОсобыхНачисленийИУдержанийНДФЛ = ОтражениеЗарплатыВУчете.ВидыОсобыхНачисленийИУдержанийНДФЛ();
		НачислениеУдержаниеВидОперации = ОтражениеЗарплатыВУчете.НачислениеУдержаниеВидОперации();
		
		Для каждого СтрокаТЗ Из ДанныеДляПроведения.НачисленияУдержания Цикл
		
			Если ВидыОсобыхНачисленийИУдержанийНДФЛ.Найти(СтрокаТЗ.НачислениеУдержание) <> Неопределено Тогда
				 
				Движения.БухучетНачисленияУдержанияПоСотрудникам.Записывать = Истина;
				
				НоваяСтрока = Движения.БухучетНачисленияУдержанияПоСотрудникам.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
				НоваяСтрока.ВидОперации = НачислениеУдержаниеВидОперации[НоваяСтрока.НачислениеУдержание];
				
			КонецЕсли;
		
		КонецЦикла;
	
	КонецЕсли; 

КонецПроцедуры

Процедура ДанныеДляПроведенияСоздатьВТНачисленияСтраховыеВзносы(МенеджерВременныхТаблиц, РеквизитыДляПроведения, ИмяВТНачисленияСтраховыеВзносы) Экспорт

	Организация = РеквизитыДляПроведения.Организация;
	ПериодРегистрации = РеквизитыДляПроведения.МесяцНачисления;
	
	УдалитьВТ = Новый Массив;
	
	Если ПолучитьФункциональнуюОпцию("РаботаВХозрасчетнойОрганизации") Тогда
		ОписаниеСтатейРасходов = ЗарплатаКадры.СтатьиРасходовПоСпособамРасчетовСФизическимиЛицами();
		ОплатаТруда = ОписаниеСтатейРасходов[Перечисления.СпособыРасчетовСФизическимиЛицами.ОплатаТруда];
		РасчетыСКонтрагентами = ОписаниеСтатейРасходов[Перечисления.СпособыРасчетовСФизическимиЛицами.РасчетыСКонтрагентами];
	Иначе
		ОплатаТруда = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
		РасчетыСКонтрагентами = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МесяцНачисления", ПериодРегистрации);
	Запрос.УстановитьПараметр("ОплатаТруда", ОплатаТруда);
	Запрос.УстановитьПараметр("РасчетыСКонтрагентами", РасчетыСКонтрагентами);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НачисленийПоДоговорам.Договор КАК Договор,
	|	ЕСТЬNULL(ПлановыеНачисленияПоДоговорам.КодДоходаСтраховыеВзносы, ЕСТЬNULL(УсловияДоговораГПХ.КодДоходаСтраховыеВзносы, УсловияДоговораОпеки.КодДоходаСтраховыеВзносы)) КАК КодДоходаСтраховыеВзносы,
	|	ЕСТЬNULL(ПлановыеНачисленияПоДоговорам.ЗаключенСоСтудентомРаботающимВСтудотряде, УсловияДоговораГПХ.ЗаключенСоСтудентомРаботающимВСтудотряде) КАК ЗаключенСоСтудентомРаботающимВСтудотряде,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПлановыеНачисленияПоДоговорам.СтатьяРасходов, УсловияДоговораГПХ.СтатьяРасходов) = &РасчетыСКонтрагентами
	|			ТОГДА &РасчетыСКонтрагентами
	|		ИНАЧЕ &ОплатаТруда
	|	КОНЕЦ КАК СтатьяРасходов
	|ПОМЕСТИТЬ ВТДанныеДоговоров
	|ИЗ
	|	ВТЗаписиНачисленийПоДоговорам КАК НачисленийПоДоговорам
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияПоДоговорам КАК ПлановыеНачисленияПоДоговорам
	|		ПО НачисленийПоДоговорам.Договор = ПлановыеНачисленияПоДоговорам.ДоговорАкт
	|			И (ПлановыеНачисленияПоДоговорам.МесяцНачисления = &МесяцНачисления)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УсловияДоговораГПХ КАК УсловияДоговораГПХ
	|		ПО НачисленийПоДоговорам.Договор = УсловияДоговораГПХ.Договор
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УсловияДоговораОпеки КАК УсловияДоговораОпеки
	|		ПО НачисленийПоДоговорам.Договор = УсловияДоговораОпеки.Договор";	
	
	Запрос.Выполнить();
	
	ПрименяетсяЕНВД = ОтражениеЗарплатыВБухучете.ПлательщикЕНВД(Организация, ПериодРегистрации);
	Если Не ПрименяетсяЕНВД Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаписиНачисленийПоДоговорам.Сотрудник,
		|	ЗаписиНачисленийПоДоговорам.Подразделение,
		|	ЗаписиНачисленийПоДоговорам.Начисление,
		|	ЗаписиНачисленийПоДоговорам.Сумма КАК СуммаДохода,
		|	ЗаписиНачисленийПоДоговорам.СкидкаПоВзносам КАК СуммаВычетаВзносы,
		|	ЗаписиНачисленийПоДоговорам.ДатаНачала,
		|	ЛОЖЬ КАК ОблагаетсяЕНВД,
		|	ЕСТЬNULL(ДанныеДоговоров.КодДоходаСтраховыеВзносы, ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.ДоговорыГПХ)) КАК КодДоходаСтраховыеВзносы,
		|	ЕСТЬNULL(ДанныеДоговоров.ЗаключенСоСтудентомРаботающимВСтудотряде, ЛОЖЬ) КАК ЗаключенСоСтудентомРаботающимВСтудотряде
		|ПОМЕСТИТЬ ВТНачисленияСтраховыеВзносы
		|ИЗ
		|	ВТЗаписиНачисленийПоДоговорам КАК ЗаписиНачисленийПоДоговорам
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеДоговоров КАК ДанныеДоговоров
		|		ПО ЗаписиНачисленийПоДоговорам.Договор = ДанныеДоговоров.Договор";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияСтраховыеВзносы", ИмяВТНачисленияСтраховыеВзносы);
		Запрос.Выполнить();
		
	Иначе
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
			
			// Сведения о бухучете договоров есть во временной таблице ВТНачисленияПоДоговорамСРаспределением.
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	НачисленияПоДоговорамСРаспределением.Договор,
			|	НачисленияПоДоговорамСРаспределением.ОблагаетсяЕНВД,
			|	СУММА(НачисленияПоДоговорамСРаспределением.Сумма) КАК Сумма
			|ПОМЕСТИТЬ ВТНачисленияСЕНВД
			|ИЗ
			|	ВТНачисленияПоДоговорамСРаспределением КАК НачисленияПоДоговорамСРаспределением
			|
			|СГРУППИРОВАТЬ ПО
			|	НачисленияПоДоговорамСРаспределением.Договор,
			|	НачисленияПоДоговорамСРаспределением.ОблагаетсяЕНВД";
			Запрос.Выполнить();
			УдалитьВТ.Добавить("ВТНачисленияСЕНВД");
			
		Иначе
			
			ОтражениеЗарплатыВУчете.СоздатьВТНачислениеУдержаниеВидОперации(МенеджерВременныхТаблиц);
			// Поле ТерриторияВыполненияРаботВОрганизации имеет пустое значение, для вычисления доли ЕНВД это допустимо.
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЗаписиНачисленийПоДоговорам.Организация КАК Организация,
			|	ЗаписиНачисленийПоДоговорам.ПериодРегистрации КАК ПериодРегистрации,
			|	ЗаписиНачисленийПоДоговорам.ДокументСсылка КАК ДокументСсылка,
			|	АВТОНОМЕРЗАПИСИ() КАК ИдентификаторСтроки,
			|	ЗаписиНачисленийПоДоговорам.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ЗаписиНачисленийПоДоговорам.Сотрудник КАК Сотрудник,
			|	ЗаписиНачисленийПоДоговорам.Подразделение КАК Подразделение,
			|	ЗаписиНачисленийПоДоговорам.Начисление КАК Начисление,
			|	ЗаписиНачисленийПоДоговорам.Сумма КАК Сумма,
			|	ЗаписиНачисленийПоДоговорам.СкидкаПоВзносам КАК СкидкаПоВзносам,
			|	ЗаписиНачисленийПоДоговорам.КодДохода КАК КодДохода,
			|	ЗаписиНачисленийПоДоговорам.КодВычета КАК КодВычета,
			|	ЗаписиНачисленийПоДоговорам.Договор КАК Договор,
			|	ЗаписиНачисленийПоДоговорам.ДокументОснование КАК ДокументОснование,
			|	ЗаписиНачисленийПоДоговорам.ДатаНачала КАК ДатаНачала,
			|	ЗаписиНачисленийПоДоговорам.ДатаОкончания КАК ДатаОкончания,
			|	ЗаписиНачисленийПоДоговорам.ПланируемаяДатаВыплаты КАК ПланируемаяДатаВыплаты,
			|	ЗНАЧЕНИЕ(Справочник.ТерриторииВыполненияРабот.ПустаяСсылка) КАК ТерриторияВыполненияРаботВОрганизации,
			|	ЕСТЬNULL(НачисленияВидОперации.ВидОперации, ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ПустаяСсылка)) КАК ВидОперации
			|ПОМЕСТИТЬ ВТНачисленияПоДоговорамГПХ
			|ИЗ
			|	ВТЗаписиНачисленийПоДоговорам КАК ЗаписиНачисленийПоДоговорам
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачислениеУдержаниеВидОперации КАК НачисленияВидОперации
			|		ПО ЗаписиНачисленийПоДоговорам.Начисление = НачисленияВидОперации.НачислениеУдержание";
			Запрос.Выполнить();
			УдалитьВТ.Добавить("ВТНачисленияПоДоговорамГПХ");
			
			ИсходныеДанные = ОписаниеИсходныхДанныхДляОтраженияНачисленийВБухучете();
			ИсходныеДанные.МенеджерВременныхТаблиц 		      = МенеджерВременныхТаблиц;
			ИсходныеДанные.ИмяВТНачисления		 		      = "ВТНачисленияПоДоговорамГПХ";
			ИсходныеДанные.ПлательщикЕНВД                     = ПрименяетсяЕНВД;
			СоздатьВТБухучетНачисленийПоДоговорам(ИсходныеДанные, "ВТБухучетНачисленийПоДоговорам");
			УдалитьВТ.Добавить("ВТБухучетНачисленийПоДоговорам");
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	НачисленияПоДоговорамСРаспределением.ДокументОснование КАК Договор,
			|	НачисленияПоДоговорамСРаспределением.ОблагаетсяЕНВД,
			|	СУММА(НачисленияПоДоговорамСРаспределением.Сумма) КАК Сумма
			|ПОМЕСТИТЬ ВТНачисленияСЕНВД
			|ИЗ
			|	ВТБухучетНачисленийПоДоговорам КАК НачисленияПоДоговорамСРаспределением
			|
			|СГРУППИРОВАТЬ ПО
			|	НачисленияПоДоговорамСРаспределением.ДокументОснование,
			|	НачисленияПоДоговорамСРаспределением.ОблагаетсяЕНВД";
			Запрос.Выполнить();
			УдалитьВТ.Добавить("ВТНачисленияСЕНВД");
			
		КонецЕсли;
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаписиНачислений.Сотрудник,
		|	ЗаписиНачислений.Подразделение,
		|	ЗаписиНачислений.Начисление,
		|	ЗаписиНачислений.ДатаНачала,
		|	ЕСТЬNULL(НачисленияСЕНВД.ОблагаетсяЕНВД, ЛОЖЬ) КАК ОблагаетсяЕНВД,
		|	ЕСТЬNULL(НачисленияСЕНВД.Сумма, 0) КАК СуммаДохода,
		|	ВЫБОР
		|		КОГДА НачисленияСЕНВД.ОблагаетсяЕНВД ЕСТЬ NULL 
		|			ТОГДА 0
		|		КОГДА ЗаписиНачислений.Сумма = 0
		|			ТОГДА 0
		|		КОГДА ЗаписиНачислений.Сумма = НачисленияСЕНВД.Сумма
		|			ТОГДА ЗаписиНачислений.СкидкаПоВзносам
		|		КОГДА НачисленияСЕНВД.ОблагаетсяЕНВД
		|			ТОГДА ВЫРАЗИТЬ(ЗаписиНачислений.СкидкаПоВзносам * (ВЫРАЗИТЬ(НачисленияСЕНВД.Сумма / ЗаписиНачислений.Сумма КАК ЧИСЛО(25, 10))) КАК ЧИСЛО(15, 2))
		|		ИНАЧЕ ЗаписиНачислений.СкидкаПоВзносам - (ВЫРАЗИТЬ(ЗаписиНачислений.СкидкаПоВзносам * (ВЫРАЗИТЬ((ЗаписиНачислений.Сумма - НачисленияСЕНВД.Сумма) / ЗаписиНачислений.Сумма КАК ЧИСЛО(25, 10))) КАК ЧИСЛО(15, 2)))
		|	КОНЕЦ КАК СуммаВычетаВзносы,
		|	ЕСТЬNULL(ДанныеДоговоров.КодДоходаСтраховыеВзносы, ЗНАЧЕНИЕ(Справочник.ВидыДоходовПоСтраховымВзносам.ДоговорыГПХ)) КАК КодДоходаСтраховыеВзносы,
		|	ЕСТЬNULL(ДанныеДоговоров.ЗаключенСоСтудентомРаботающимВСтудотряде, ЛОЖЬ) КАК ЗаключенСоСтудентомРаботающимВСтудотряде
		|ПОМЕСТИТЬ ВТНачисленияСтраховыеВзносы
		|ИЗ
		|	ВТЗаписиНачисленийПоДоговорам КАК ЗаписиНачислений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияСЕНВД КАК НачисленияСЕНВД
		|		ПО ЗаписиНачислений.Договор = НачисленияСЕНВД.Договор
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеДоговоров КАК ДанныеДоговоров
		|		ПО ЗаписиНачислений.Договор = ДанныеДоговоров.Договор";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияСтраховыеВзносы", ИмяВТНачисленияСтраховыеВзносы);
		Запрос.Выполнить();
		
	КонецЕсли;
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
КонецПроцедуры

Процедура СформироватьДвиженияКоэффициентыРаспределенияСреднегоЗаработка(Движения, КоэффициентыРаспределенияСреднегоЗаработка) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата") Тогда
		Возврат;
	КонецЕсли;
	
	Если КоэффициентыРаспределенияСреднегоЗаработка.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаТЗ Из КоэффициентыРаспределенияСреднегоЗаработка Цикл
		НоваяЗапись = Движения.КоэффициентыРаспределенияСреднегоЗаработка.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаТЗ);
		Движения.КоэффициентыРаспределенияСреднегоЗаработка.Записывать = Истина;
	КонецЦикла;

КонецПроцедуры

// Формирует временные таблицы с данными бухучета начислений и взносов.
//
// Параметры:
//	Организация - СправочникСсылка.Организации
//	ПериодРегистрации - Дата
//	МенеджерВременныхТаблиц - помещает в менеджер таблицы
//			ВТБухучетНачислений и ВТБухучетСтраховыхВзносов.
//	Сотрудники - Неопределено или Массив.
//
Процедура СведенияОБухучетеНачисленийИВзносовПоСтатьямФинансирования(Организация, ПериодРегистрации, МенеджерВременныхТаблиц, Сотрудники = Неопределено) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОтложенноеПроведениеДокументов") Тогда 
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОтражениеДокументовВУчетеСтраховыхВзносов");
		Модуль.ОтразитьДокументыВУчетеСтраховыхВзносов(Организация);
	КонецЕсли;
	
	УдалитьВТ = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	
	ОтражениеЗарплатыВБухучете.СоздатьВТНачислениеВидНачисленияОплатыТрудаДляНУ(МенеджерВременныхТаблиц);
	УдалитьВТ.Добавить("ВТНачислениеВидНачисленияОплатыТрудаДляНУ");
	
	ОтражениеЗарплатыВУчете.СоздатьВТНачислениеУдержаниеВидОперации(МенеджерВременныхТаблиц);
	УдалитьВТ.Добавить("ВТНачислениеУдержаниеВидОперации");
	
	МассивФизическихЛиц = Неопределено;
	БухучетУсловиеПоСотрудникам = "ИСТИНА";
	ВзносыУсловиеПоСотрудникам  = "ИСТИНА";
	Если Сотрудники <> Неопределено Тогда
		БухучетУсловиеПоСотрудникам = "БухучетНачисления.Сотрудник В(&Сотрудники)";
		ВзносыУсловиеПоСотрудникам  = "СтраховыеВзносы.Сотрудник В(&Сотрудники)";
		Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
		ФизическиеЛицаСотрудников = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Сотрудники, "ФизическоеЛицо");
		МассивФизическихЛиц = Новый Массив;
		Для каждого ЭлементКоллекции Из ФизическиеЛицаСотрудников Цикл
			Если МассивФизическихЛиц.Найти(ЭлементКоллекции.Значение) = Неопределено Тогда
				МассивФизическихЛиц.Добавить(ЭлементКоллекции.Значение);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ПараметрыПолученияДанных = ПараметрыПолученияБухучетаНачисленийПоСтатьямФинансирования();
	ПараметрыПолученияДанных.Организация 				= Организация;
	ПараметрыПолученияДанных.ПериодРегистрации 			= ПериодРегистрации;
	ПараметрыПолученияДанных.МенеджерВременныхТаблиц 	= МенеджерВременныхТаблиц;
	ПараметрыПолученияДанных.ФизическиеЛица 			= МассивФизическихЛиц;
	СоздатьВТБухучетНачисленийПоСтатьямФинансирования(ПараметрыПолученияДанных);
	УдалитьВТ.Добавить("ВТБухучетНачисленийПоСтатьямФинансирования");
	ИмяТаблицыБухучетНачислений = "ВТБухучетНачисленийПоСтатьямФинансирования";
	
	Если ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		
		ИмяТаблицыБухучетНачислений = "ВТБухучетНачисленийПоСтатьямФинансированияВременная";
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	БухучетНачислений.Организация КАК Организация,
		|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БухучетНачислений.Сотрудник КАК Сотрудник,
		|	БухучетНачислений.Подразделение КАК Подразделение,
		|	БухучетНачислений.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	БухучетНачислений.НачислениеУдержание КАК НачислениеУдержание,
		|	ВЫБОР
		|		КОГДА БухучетНачислений.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускАвансом)
		|			ТОГДА НачислениеУдержаниеВидОперации.ВидОперации
		|		ИНАЧЕ БухучетНачислений.ВидОперации
		|	КОНЕЦ КАК ВидОперации,
		|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
		|	БухучетНачислений.ДатаОкончания КАК ДатаОкончания,
		|	БухучетНачислений.Сумма КАК Сумма,
		|	ВЫБОР
		|		КОГДА БухучетНачислений.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпускАвансом)
		|			ТОГДА БухучетНачислений.Сумма
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ОтпускАвансом,
		|	БухучетНачислений.ДокументОснование КАК ДокументОснование
		|ПОМЕСТИТЬ ВТБухучетНачисленийПоСтатьямФинансированияВременная
		|ИЗ
		|	ВТБухучетНачисленийПоСтатьямФинансирования КАК БухучетНачислений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачислениеУдержаниеВидОперации КАК НачислениеУдержаниеВидОперации
		|		ПО БухучетНачислений.НачислениеУдержание = НачислениеУдержаниеВидОперации.НачислениеУдержание";
		Запрос.Выполнить();
		
		УдалитьВТ.Добавить("ВТБухучетНачисленийПоСтатьямФинансированияВременная");
		
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&ПериодРегистрации КАК ПериодРегистрации,
	|	ВЫБОР
	|		КОГДА БухучетНачисления.ДатаНачала = ДАТАВРЕМЯ(1, 1, 1)
	|				ИЛИ БухучетНачисления.ДатаНачала < &ПериодРегистрации
	|			ТОГДА &ПериодРегистрации
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(БухучетНачисления.ДатаНачала, МЕСЯЦ)
	|	КОНЕЦ КАК ПериодПринятияРасходов,
	|	БухучетНачисления.Организация КАК Организация,
	|	БухучетНачисления.Сотрудник КАК Сотрудник,
	|	БухучетНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	БухучетНачисления.Подразделение КАК Подразделение,
	|	БухучетНачисления.НачислениеУдержание КАК Начисление,
	|	БухучетНачисления.ВидОперации КАК ВидОперации,
	|	БухучетНачисления.ДатаНачала КАК ДатаНачала,
	|	БухучетНачисления.ДатаОкончания КАК ДатаОкончания,
	|	БухучетНачисления.ДокументОснование КАК ДокументОснование,
	|	БухучетНачисления.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачисления.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачисления.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	НачислениеВидНачисленияОплатыТрудаДляНУ.ВидНачисленияОплатыТрудаДляНУ КАК ВидНачисленияОплатыТрудаДляНУ,
	|	СУММА(БухучетНачисления.Сумма) КАК Сумма,
	|	СУММА(БухучетНачисления.ОтпускАвансом) КАК ОтпускАвансом
	|ПОМЕСТИТЬ ВТБухучетНачислений
	|ИЗ
	|	ВТБухучетНачисленийПоСтатьямФинансирования КАК БухучетНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачислениеВидНачисленияОплатыТрудаДляНУ КАК НачислениеВидНачисленияОплатыТрудаДляНУ
	|		ПО БухучетНачисления.НачислениеУдержание = НачислениеВидНачисленияОплатыТрудаДляНУ.Начисление
	|ГДЕ
	|	&БухучетУсловиеПоСотрудникам
	|
	|СГРУППИРОВАТЬ ПО
	|	НачислениеВидНачисленияОплатыТрудаДляНУ.ВидНачисленияОплатыТрудаДляНУ,
	|	БухучетНачисления.ФизическоеЛицо,
	|	БухучетНачисления.Сотрудник,
	|	ВЫБОР
	|		КОГДА БухучетНачисления.ДатаНачала = ДАТАВРЕМЯ(1, 1, 1)
	|				ИЛИ БухучетНачисления.ДатаНачала < &ПериодРегистрации
	|			ТОГДА &ПериодРегистрации
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(БухучетНачисления.ДатаНачала, МЕСЯЦ)
	|	КОНЕЦ,
	|	БухучетНачисления.Подразделение,
	|	БухучетНачисления.СтатьяРасходов,
	|	БухучетНачисления.ОблагаетсяЕНВД,
	|	БухучетНачисления.СтатьяФинансирования,
	|	БухучетНачисления.ВидОперации,
	|	БухучетНачисления.ПодразделениеУчетаЗатрат,
	|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачисления.НачислениеУдержание,
	|	БухучетНачисления.ДатаНачала,
	|	БухучетНачисления.ДатаОкончания,
	|	БухучетНачисления.ДокументОснование,
	|	БухучетНачисления.Организация
	|
	|ИМЕЮЩИЕ
	|	СУММА(БухучетНачисления.Сумма) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СтраховыеВзносы.Сотрудник КАК Сотрудник,
	|	СтраховыеВзносы.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СтраховыеВзносы.Подразделение КАК Подразделение,
	|	СтраховыеВзносы.Начисление КАК Начисление,
	|	НачислениеУдержаниеВидОперации.ВидОперации КАК ВидОперации,
	|	СУММА(0) КАК СтраховыеВзносы,
	|	ВЫБОР
	|		КОГДА СтраховыеВзносы.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|			ТОГДА СтраховыеВзносы.Подразделение
	|		ИНАЧЕ СтраховыеВзносы.ПодразделениеУчетаЗатрат
	|	КОНЕЦ КАК ПодразделениеУчетаЗатрат,
	|	СтраховыеВзносы.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	СтраховыеВзносы.СтатьяФинансирования КАК СтатьяФинансирования,
	|	СтраховыеВзносы.СтатьяРасходов КАК СтатьяРасходов,
	|	СтраховыеВзносы.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА СтраховыеВзносы.ДатаНачала = ДАТАВРЕМЯ(1, 1, 1)
	|				ИЛИ СтраховыеВзносы.ДатаНачала < &ПериодРегистрации
	|			ТОГДА &ПериодРегистрации
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(СтраховыеВзносы.ДатаНачала, МЕСЯЦ)
	|	КОНЕЦ КАК ПериодПринятияРасходов,
	|	СтраховыеВзносы.ДатаНачала КАК ДатаНачала
	|ПОМЕСТИТЬ ВТБухучетСтраховыхВзносов
	|ИЗ
	|	РегистрНакопления.СтраховыеВзносыПоФизическимЛицам КАК СтраховыеВзносы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачислениеУдержаниеВидОперации КАК НачислениеУдержаниеВидОперации
	|		ПО СтраховыеВзносы.Начисление = НачислениеУдержаниеВидОперации.НачислениеУдержание
	|ГДЕ
	|	СтраховыеВзносы.Организация = &Организация
	|	И НАЧАЛОПЕРИОДА(СтраховыеВзносы.Период, МЕСЯЦ) = &ПериодРегистрации
	|	И &ВзносыУсловиеПоСотрудникам
	|
	|СГРУППИРОВАТЬ ПО
	|	СтраховыеВзносы.ФизическоеЛицо,
	|	НачислениеУдержаниеВидОперации.ВидОперации,
	|	СтраховыеВзносы.ОблагаетсяЕНВД,
	|	СтраховыеВзносы.СтатьяФинансирования,
	|	СтраховыеВзносы.СтатьяРасходов,
	|	СтраховыеВзносы.СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА СтраховыеВзносы.ДатаНачала = ДАТАВРЕМЯ(1, 1, 1)
	|				ИЛИ СтраховыеВзносы.ДатаНачала < &ПериодРегистрации
	|			ТОГДА &ПериодРегистрации
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(СтраховыеВзносы.ДатаНачала, МЕСЯЦ)
	|	КОНЕЦ,
	|	СтраховыеВзносы.Подразделение,
	|	СтраховыеВзносы.Начисление,
	|	СтраховыеВзносы.ДатаНачала,
	|	СтраховыеВзносы.Сотрудник,
	|	ВЫБОР
	|		КОГДА СтраховыеВзносы.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|			ТОГДА СтраховыеВзносы.Подразделение
	|		ИНАЧЕ СтраховыеВзносы.ПодразделениеУчетаЗатрат
	|	КОНЕЦ";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&БухучетУсловиеПоСотрудникам", БухучетУсловиеПоСотрудникам);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВзносыУсловиеПоСотрудникам", ВзносыУсловиеПоСотрудникам);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТБухучетНачисленийПоСтатьямФинансирования", ИмяТаблицыБухучетНачислений);
	Если Не ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СУММА(БухучетНачисления.ОтпускАвансом)", "0");
	КонецЕсли;
	
	ТекстПолейВзносов = "";
	Для каждого ИмяПоля Из СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(УчетСтраховыхВзносов.ОтражаемыеВУчетеВзносы(Истина)) Цикл
		ТекстПолейВзносов = ТекстПолейВзносов + "СУММА(СтраховыеВзносы." + ИмяПоля + ") КАК " + ИмяПоля + "," + Символы.ПС;
	КонецЦикла;
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "СУММА(0) КАК СтраховыеВзносы,", ТекстПолейВзносов);
	Запрос.Выполнить();
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, УдалитьВТ);
	
КонецПроцедуры

// Добавляет запись о бухучете зарплаты по документу основанию.
//
Процедура ОбновитьСведенияОБухучетеЗарплатыСотрудников(ДокументОбъект, НеРегистрироватьБухучет, ИмяТаблицы, ИмяПоляПериод, ИмяПоляДействуетДо, НетПоляЕНВД = Ложь) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.БухучетЗарплатыСотрудников.СоздатьНаборЗаписей();
	НаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
	НаборЗаписей.Отбор.ДокументОснование.Установить(ДокументОбъект.Ссылка);
		
	Если НеРегистрироватьБухучет Или Не ДокументОбъект.Проведен Тогда
		
		
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() > 0 Тогда
			НаборЗаписей.Очистить();
			НаборЗаписей.Записать();
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДокументОбъект.ИсправленныйДокумент) Тогда
			// "Включение" записей исправленного документа.
			НаборИсправленныхЗаписей = РегистрыСведений.БухучетЗарплатыСотрудниковИсправления.СоздатьНаборЗаписей();
			НаборИсправленныхЗаписей.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
			НаборИсправленныхЗаписей.Отбор.ДокументОснование.Установить(ДокументОбъект.ИсправленныйДокумент);
			НаборИсправленныхЗаписей.Прочитать();
			Если НаборИсправленныхЗаписей.Количество()>0 Тогда
				НаборЗаписей.Отбор.ДокументОснование.Установить(ДокументОбъект.ИсправленныйДокумент);
				Для каждого ЗаписьНабора Из НаборИсправленныхЗаписей Цикл
					НоваяЗапись = НаборЗаписей.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, ЗаписьНабора);
					НоваяЗапись.Период = ЗаписьНабора.ПериодИзмерение;
				КонецЦикла;
				НаборЗаписей.Записать();
				НаборИсправленныхЗаписей.Очистить();
				НаборИсправленныхЗаписей.Записать();
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
		Запрос.УстановитьПараметр("ЕстьЕНВД", ПолучитьФункциональнуюОпцию("ИспользуетсяЕНВДВБюджетномУчреждении"));
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Таблица.ДатаНачала КАК Период,
		|	Таблица.ДатаОкончания КАК ДействуетДо,
		|	Таблица.Сотрудник КАК Сотрудник,
		|	Таблица.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	Таблица.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	Таблица.СтатьяФинансирования КАК СтатьяФинансирования,
		|	&Ссылка КАК ДокументОснование
		|ПОМЕСТИТЬ ВТНастройкиБухучета
		|ИЗ
		|	#Таблица КАК Таблица
		|ГДЕ
		|	Таблица.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МаксимальныеПериоды.Сотрудник КАК Сотрудник,
		|	МаксимальныеПериоды.Период КАК Период,
		|	ВЫБОР
		|		КОГДА БухучетЗарплаты.ДействуетДо <> ДАТАВРЕМЯ(1, 1, 1)
		|				И БухучетЗарплаты.ДействуетДо <= МаксимальныеПериоды.Период
		|			ТОГДА БухучетЗарплаты.СтатьяФинансированияПоОкончании
		|		ИНАЧЕ БухучетЗарплаты.СтатьяФинансирования
		|	КОНЕЦ КАК СтатьяФинансирования,
		|	ВЫБОР
		|		КОГДА БухучетЗарплаты.ДействуетДо <> ДАТАВРЕМЯ(1, 1, 1)
		|				И БухучетЗарплаты.ДействуетДо <= МаксимальныеПериоды.Период
		|			ТОГДА БухучетЗарплаты.СпособОтраженияЗарплатыВБухучетеПоОкончании
		|		ИНАЧЕ БухучетЗарплаты.СпособОтраженияЗарплатыВБухучете
		|	КОНЕЦ КАК СпособОтраженияЗарплатыВБухучете,
		|	ВЫБОР
		|		КОГДА БухучетЗарплаты.ДействуетДо <> ДАТАВРЕМЯ(1, 1, 1)
		|				И БухучетЗарплаты.ДействуетДо <= МаксимальныеПериоды.Период
		|			ТОГДА БухучетЗарплаты.ОтношениеКЕНВДПоОкончании
		|		ИНАЧЕ БухучетЗарплаты.ОтношениеКЕНВД
		|	КОНЕЦ КАК ОтношениеКЕНВД
		|ПОМЕСТИТЬ ВТЗначенияПоОкончании
		|ИЗ
		|	(ВЫБРАТЬ
		|		НастройкиБухучета.Сотрудник КАК Сотрудник,
		|		НастройкиБухучета.Период КАК Период,
		|		МАКСИМУМ(БухучетЗарплатыСотрудников.Период) КАК ПериодРегистра
		|	ИЗ
		|		ВТНастройкиБухучета КАК НастройкиБухучета
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыСотрудников КАК БухучетЗарплатыСотрудников
		|			ПО НастройкиБухучета.Период > БухучетЗарплатыСотрудников.Период
		|				И НастройкиБухучета.Сотрудник = БухучетЗарплатыСотрудников.Сотрудник
		|				И (НастройкиБухучета.ДействуетДо <> ДАТАВРЕМЯ(1, 1, 1))
		|	
		|	СГРУППИРОВАТЬ ПО
		|		НастройкиБухучета.Сотрудник,
		|		НастройкиБухучета.Период) КАК МаксимальныеПериоды
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыСотрудников КАК БухучетЗарплаты
		|		ПО МаксимальныеПериоды.Сотрудник = БухучетЗарплаты.Сотрудник
		|			И МаксимальныеПериоды.ПериодРегистра = БухучетЗарплаты.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НастройкиБухучета.Период КАК Период,
		|	НастройкиБухучета.Сотрудник КАК Сотрудник,
		|	ВЫБОР
		|		КОГДА НЕ БухучетСотрудников.Период ЕСТЬ NULL
		|				И БухучетСотрудников.ДокументОснование = НЕОПРЕДЕЛЕНО
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК РучнаяКорректировка,
		|	ВЫБОР
		|		КОГДА НастройкиБухучета.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
		|				ИЛИ НастройкиБухучета.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
		|				ИЛИ &ЕстьЕНВД
		|					И НастройкиБухучета.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК БухучетВДокументеЗадан,
		|	БухучетСотрудников.ДокументОснование КАК ДокументОснованиеСтарый
		|ИЗ
		|	ВТНастройкиБухучета КАК НастройкиБухучета
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыСотрудников КАК БухучетСотрудников
		|		ПО НастройкиБухучета.Период = БухучетСотрудников.Период
		|			И НастройкиБухучета.Сотрудник = БухучетСотрудников.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НастройкиБухучета.Период КАК Период,
		|	НастройкиБухучета.Сотрудник КАК Сотрудник,
		|	НастройкиБухучета.ДокументОснование КАК ДокументОснование,
		|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
		|	НастройкиБухучета.ДействуетДо КАК ДействуетДо,
		|	ЗначенияПоОкончании.СтатьяФинансирования КАК СтатьяФинансированияПоОкончании,
		|	ЗначенияПоОкончании.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучетеПоОкончании,
		|	ЗначенияПоОкончании.ОтношениеКЕНВД КАК ОтношениеКЕНВДПоОкончании
		|ИЗ
		|	ВТНастройкиБухучета КАК НастройкиБухучета
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗначенияПоОкончании КАК ЗначенияПоОкончании
		|		ПО НастройкиБухучета.Период = ЗначенияПоОкончании.Период
		|			И НастройкиБухучета.Сотрудник = ЗначенияПоОкончании.Сотрудник
		|ГДЕ
		|	ВЫБОР
		|			КОГДА НастройкиБухучета.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
		|					ИЛИ НастройкиБухучета.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
		|					ИЛИ &ЕстьЕНВД
		|						И НастройкиБухучета.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"#Таблица",ИмяТаблицы);
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Таблица.ДатаНачала",ИмяПоляПериод);
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Таблица.ДатаОкончания",?(ИмяПоляДействуетДо = Неопределено,"ДАТАВРЕМЯ(1, 1, 1)",ИмяПоляДействуетДо));
		Если НетПоляЕНВД Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"Таблица.ОтношениеКЕНВД","ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД)");
		КонецЕсли;
		Результат = Запрос.ВыполнитьПакет();
		
		Выборка = Результат[Результат.ВГраница()-1].Выбрать();
		Пока Выборка.Следующий() Цикл
			Если НЕ Выборка.БухучетВДокументеЗадан И Выборка.ДокументОснованиеСтарый = ДокументОбъект.Ссылка Тогда
				// Очистка записей если в документе очистили бухучет.
				НаборОчищаемыхЗаписей = РегистрыСведений.БухучетЗарплатыСотрудников.СоздатьНаборЗаписей();
				НаборОчищаемыхЗаписей.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
				НаборОчищаемыхЗаписей.Отбор.ДокументОснование.Установить(ДокументОбъект.Ссылка);
				НаборОчищаемыхЗаписей.Записать();
			ИначеЕсли Выборка.БухучетВДокументеЗадан И Выборка.РучнаяКорректировка Тогда
				// Очистка записей если есть ручная корректировка.
				НаборОчищаемыхЗаписей = РегистрыСведений.БухучетЗарплатыСотрудников.СоздатьНаборЗаписей();
				НаборОчищаемыхЗаписей.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
				НаборОчищаемыхЗаписей.Отбор.Период.Установить(Выборка.Период);
				НаборОчищаемыхЗаписей.Отбор.Сотрудник.Установить(Выборка.Сотрудник);
				НаборОчищаемыхЗаписей.Записать();	
			ИначеЕсли ЗначениеЗаполнено(ДокументОбъект.ИсправленныйДокумент) И Выборка.ДокументОснованиеСтарый = ДокументОбъект.ИсправленныйДокумент Тогда
				// "Выключение" записей исправленного документа.
				НаборОчищаемыхЗаписей = РегистрыСведений.БухучетЗарплатыСотрудников.СоздатьНаборЗаписей();
				НаборОчищаемыхЗаписей.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
				НаборОчищаемыхЗаписей.Отбор.ДокументОснование.Установить(ДокументОбъект.ИсправленныйДокумент);
				НаборОчищаемыхЗаписей.Прочитать();
				Если НаборОчищаемыхЗаписей.Количество()>0 Тогда
					НаборИсправленныхЗаписей = РегистрыСведений.БухучетЗарплатыСотрудниковИсправления.СоздатьНаборЗаписей();
					НаборИсправленныхЗаписей.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
					НаборИсправленныхЗаписей.Отбор.ДокументОснование.Установить(ДокументОбъект.ИсправленныйДокумент);
					Для каждого ЗаписьНабора Из НаборОчищаемыхЗаписей Цикл
						НоваяЗапись = НаборИсправленныхЗаписей.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяЗапись, ЗаписьНабора);
						НоваяЗапись.ПериодИзмерение = ЗаписьНабора.Период;
					КонецЦикла;
					НаборИсправленныхЗаписей.Записать();
					НаборОчищаемыхЗаписей.Очистить();				
					НаборОчищаемыхЗаписей.Записать();	
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Выборка = Результат[Результат.ВГраница()].Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);	
		КонецЦикла;
		
		Если НаборЗаписей.Количество() > 0 Тогда
			НаборЗаписей.Записать();
		КонецЕсли
		
	КонецЕсли;
	
КонецПроцедуры

// Добавляет запись о бухучете зарплаты по документу основанию.
//
Процедура ОбновитьСведенияОБухучетеЗарплатыСотрудниковСписок(ДокументОбъект, Параметры) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.БухучетЗарплатыСотрудников.СоздатьНаборЗаписей();
	НаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
	НаборЗаписей.Отбор.ДокументОснование.Установить(ДокументОбъект.Ссылка);
		
	Если Параметры.НеРегистрироватьБухучет Или Не ДокументОбъект.Проведен Тогда
		
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() > 0 Тогда
			НаборЗаписей.Очистить();
			НаборЗаписей.Записать();
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДокументОбъект.ИсправленныйДокумент) Тогда
			// "Включение" записей исправленного документа.
			НаборИсправленныхЗаписей = РегистрыСведений.БухучетЗарплатыСотрудниковИсправления.СоздатьНаборЗаписей();
			НаборИсправленныхЗаписей.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
			НаборИсправленныхЗаписей.Отбор.ДокументОснование.Установить(ДокументОбъект.ИсправленныйДокумент);
			НаборИсправленныхЗаписей.Прочитать();
			Если НаборИсправленныхЗаписей.Количество()>0 Тогда
				НаборЗаписей.Отбор.ДокументОснование.Установить(ДокументОбъект.ИсправленныйДокумент);
				Для каждого ЗаписьНабора Из НаборИсправленныхЗаписей Цикл
					НоваяЗапись = НаборЗаписей.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, ЗаписьНабора);
					НоваяЗапись.Период = ЗаписьНабора.ПериодИзмерение;
				КонецЦикла;
				НаборЗаписей.Записать();
				НаборИсправленныхЗаписей.Очистить();
				НаборИсправленныхЗаписей.Записать();
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
		Запрос.УстановитьПараметр("ЕстьЕНВД", ПолучитьФункциональнуюОпцию("ИспользуетсяЕНВДВБюджетномУчреждении"));
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Таблица.Сотрудник КАК Сотрудник,
		|	Таблица.ДатаНачала КАК Период,
		|	Таблица.ДатаОкончания КАК ДействуетДо,
		|	Таблица.СтатьяФинансирования КАК СтатьяФинансирования,
		|	Таблица.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	Таблица.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	&Ссылка КАК ДокументОснование,
		|	Таблица.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1) КАК ЕстьОкончание
		|ПОМЕСТИТЬ ВТНастройкиБухучета
		|ИЗ
		|	#Таблица КАК Таблица
		|ГДЕ
		|	Таблица.Ссылка = &Ссылка
		|	И &РегистрироватьБухучет";
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"#Таблица", Параметры.ИмяТаблицы);
		Если ЗначениеЗаполнено(Параметры.ИмяПоляРегистрироватьБухучет) Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"&РегистрироватьБухучет", Параметры.ИмяПоляРегистрироватьБухучет);
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"&РегистрироватьБухучет", "ИСТИНА");
		КонецЕсли;
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Таблица.ДатаНачала", Параметры.ИмяПоляПериод);
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Таблица.ДатаОкончания",?(Параметры.ИмяПоляДействуетДо = Неопределено,"ДАТАВРЕМЯ(1, 1, 1)",Параметры.ИмяПоляДействуетДо));
		Если Параметры.НетПоляЕНВД Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"Таблица.ОтношениеКЕНВД","ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД)");
		КонецЕсли;
		Запрос.Выполнить();
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	МаксимальныеПериоды.Сотрудник КАК Сотрудник,
		|	МаксимальныеПериоды.Период КАК Период,
		|	ВЫБОР
		|		КОГДА БухучетЗарплаты.ДействуетДо <> ДАТАВРЕМЯ(1, 1, 1)
		|				И БухучетЗарплаты.ДействуетДо <= МаксимальныеПериоды.Период
		|			ТОГДА БухучетЗарплаты.СтатьяФинансированияПоОкончании
		|		ИНАЧЕ БухучетЗарплаты.СтатьяФинансирования
		|	КОНЕЦ КАК СтатьяФинансирования,
		|	ВЫБОР
		|		КОГДА БухучетЗарплаты.ДействуетДо <> ДАТАВРЕМЯ(1, 1, 1)
		|				И БухучетЗарплаты.ДействуетДо <= МаксимальныеПериоды.Период
		|			ТОГДА БухучетЗарплаты.СпособОтраженияЗарплатыВБухучетеПоОкончании
		|		ИНАЧЕ БухучетЗарплаты.СпособОтраженияЗарплатыВБухучете
		|	КОНЕЦ КАК СпособОтраженияЗарплатыВБухучете,
		|	ВЫБОР
		|		КОГДА БухучетЗарплаты.ДействуетДо <> ДАТАВРЕМЯ(1, 1, 1)
		|				И БухучетЗарплаты.ДействуетДо <= МаксимальныеПериоды.Период
		|			ТОГДА БухучетЗарплаты.ОтношениеКЕНВДПоОкончании
		|		ИНАЧЕ БухучетЗарплаты.ОтношениеКЕНВД
		|	КОНЕЦ КАК ОтношениеКЕНВД
		|ПОМЕСТИТЬ ВТЗначенияПоОкончании
		|ИЗ
		|	(ВЫБРАТЬ
		|		НастройкиБухучета.Сотрудник КАК Сотрудник,
		|		НастройкиБухучета.Период КАК Период,
		|		МАКСИМУМ(БухучетЗарплатыСотрудников.Период) КАК ПериодРегистра
		|	ИЗ
		|		ВТНастройкиБухучета КАК НастройкиБухучета
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыСотрудников КАК БухучетЗарплатыСотрудников
		|			ПО НастройкиБухучета.Период > БухучетЗарплатыСотрудников.Период
		|				И НастройкиБухучета.Сотрудник = БухучетЗарплатыСотрудников.Сотрудник
		|				И (НастройкиБухучета.ЕстьОкончание)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		НастройкиБухучета.Сотрудник,
		|		НастройкиБухучета.Период) КАК МаксимальныеПериоды
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыСотрудников КАК БухучетЗарплаты
		|		ПО МаксимальныеПериоды.Сотрудник = БухучетЗарплаты.Сотрудник
		|			И МаксимальныеПериоды.ПериодРегистра = БухучетЗарплаты.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НастройкиБухучета.Период КАК Период,
		|	НастройкиБухучета.Сотрудник КАК Сотрудник,
		|	ВЫБОР
		|		КОГДА НЕ БухучетСотрудников.Период ЕСТЬ NULL
		|				И БухучетСотрудников.ДокументОснование = НЕОПРЕДЕЛЕНО
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК РучнаяКорректировка,
		|	ВЫБОР
		|		КОГДА НастройкиБухучета.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
		|				ИЛИ НастройкиБухучета.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
		|				ИЛИ &ЕстьЕНВД
		|					И НастройкиБухучета.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК БухучетВДокументеЗадан,
		|	БухучетСотрудников.ДокументОснование КАК ДокументОснованиеСтарый
		|ИЗ
		|	ВТНастройкиБухучета КАК НастройкиБухучета
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыСотрудников КАК БухучетСотрудников
		|		ПО НастройкиБухучета.Период = БухучетСотрудников.Период
		|			И НастройкиБухучета.Сотрудник = БухучетСотрудников.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НастройкиБухучета.Период КАК Период,
		|	НастройкиБухучета.Сотрудник КАК Сотрудник,
		|	НастройкиБухучета.ДокументОснование КАК ДокументОснование,
		|	НастройкиБухучета.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	НастройкиБухучета.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	НастройкиБухучета.СтатьяФинансирования КАК СтатьяФинансирования,
		|	НастройкиБухучета.ДействуетДо КАК ДействуетДо,
		|	ЗначенияПоОкончании.СтатьяФинансирования КАК СтатьяФинансированияПоОкончании,
		|	ЗначенияПоОкончании.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучетеПоОкончании,
		|	ЗначенияПоОкончании.ОтношениеКЕНВД КАК ОтношениеКЕНВДПоОкончании
		|ИЗ
		|	ВТНастройкиБухучета КАК НастройкиБухучета
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗначенияПоОкончании КАК ЗначенияПоОкончании
		|		ПО НастройкиБухучета.Период = ЗначенияПоОкончании.Период
		|			И НастройкиБухучета.Сотрудник = ЗначенияПоОкончании.Сотрудник
		|ГДЕ
		|	ВЫБОР
		|			КОГДА НастройкиБухучета.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
		|					ИЛИ НастройкиБухучета.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
		|					ИЛИ &ЕстьЕНВД
		|						И НастройкиБухучета.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ";
		Результат = Запрос.ВыполнитьПакет();
		
		Выборка = Результат[Результат.ВГраница()-1].Выбрать();
		Пока Выборка.Следующий() Цикл
			Если НЕ Выборка.БухучетВДокументеЗадан И Выборка.ДокументОснованиеСтарый = ДокументОбъект.Ссылка Тогда
				// Очистка записей если в документе очистили бухучет.
				НаборОчищаемыхЗаписей = РегистрыСведений.БухучетЗарплатыСотрудников.СоздатьНаборЗаписей();
				НаборОчищаемыхЗаписей.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
				НаборОчищаемыхЗаписей.Отбор.ДокументОснование.Установить(ДокументОбъект.Ссылка);
				НаборОчищаемыхЗаписей.Отбор.Сотрудник.Установить(Выборка.Сотрудник);
				НаборОчищаемыхЗаписей.Записать();
			ИначеЕсли Выборка.БухучетВДокументеЗадан И Выборка.РучнаяКорректировка Тогда
				// Очистка записей если есть ручная корректировка.
				НаборОчищаемыхЗаписей = РегистрыСведений.БухучетЗарплатыСотрудников.СоздатьНаборЗаписей();
				НаборОчищаемыхЗаписей.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
				НаборОчищаемыхЗаписей.Отбор.Период.Установить(Выборка.Период);
				НаборОчищаемыхЗаписей.Отбор.Сотрудник.Установить(Выборка.Сотрудник);
				НаборОчищаемыхЗаписей.Записать();
			ИначеЕсли ЗначениеЗаполнено(ДокументОбъект.ИсправленныйДокумент) И Выборка.ДокументОснованиеСтарый = ДокументОбъект.ИсправленныйДокумент Тогда
				// "Выключение" записей исправленного документа.
				НаборОчищаемыхЗаписей = РегистрыСведений.БухучетЗарплатыСотрудников.СоздатьНаборЗаписей();
				НаборОчищаемыхЗаписей.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
				НаборОчищаемыхЗаписей.Отбор.ДокументОснование.Установить(ДокументОбъект.ИсправленныйДокумент);
				НаборОчищаемыхЗаписей.Отбор.Сотрудник.Установить(Выборка.Сотрудник);
				НаборОчищаемыхЗаписей.Прочитать();
				Если НаборОчищаемыхЗаписей.Количество()>0 Тогда
					НаборИсправленныхЗаписей = РегистрыСведений.БухучетЗарплатыСотрудниковИсправления.СоздатьНаборЗаписей();
					НаборИсправленныхЗаписей.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
					НаборИсправленныхЗаписей.Отбор.ДокументОснование.Установить(ДокументОбъект.ИсправленныйДокумент);
					НаборИсправленныхЗаписей.Отбор.Сотрудник.Установить(Выборка.Сотрудник);
					Для каждого ЗаписьНабора Из НаборОчищаемыхЗаписей Цикл
						НоваяЗапись = НаборИсправленныхЗаписей.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяЗапись, ЗаписьНабора);
						НоваяЗапись.ПериодИзмерение = ЗаписьНабора.Период;
					КонецЦикла;
					НаборИсправленныхЗаписей.Записать();
					НаборОчищаемыхЗаписей.Очистить();
					НаборОчищаемыхЗаписей.Записать();
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Выборка = Результат[Результат.ВГраница()].Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
		КонецЦикла;
		
		Если НаборЗаписей.Количество() > 0 Тогда
			НаборЗаписей.Записать();
		Иначе
			НаборЗаписей.Прочитать();
			Если НаборЗаписей.Количество() > 0 Тогда
				НаборЗаписей.Очистить();
				НаборЗаписей.Записать();
			КонецЕсли;
		КонецЕсли
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПараметрыРегистрацииБухучетаСотрудниковСписком() Экспорт

	ПараметрыОбновленияБухучета = Новый Структура("
	|НеРегистрироватьБухучет,
	|ИмяТаблицы,
	|ИмяПоляПериод,
	|ИмяПоляДействуетДо,
	|ИмяПоляРегистрироватьБухучет,
	|НетПоляЕНВД");
	
	ПараметрыОбновленияБухучета.НетПоляЕНВД = Истина;
	
	Возврат ПараметрыОбновленияБухучета;
	
КонецФункции

Процедура СформироватьДвиженияКоэффициентыРаспределенияДенежногоСодержания(Движения, КоэффициентыРаспределенияДенежногоСодержания) Экспорт

	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба.РасчетДенежногоСодержания") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("РасчетДенежногоСодержания");
		Модуль.СформироватьДвиженияКоэффициентыРаспределенияДенежногоСодержания(Движения, КоэффициентыРаспределенияДенежногоСодержания);
	КонецЕсли;
	
КонецПроцедуры

// Формирует движения по регистрам подсистемы.
//
// Параметры:
//		ДанныеДляПроведения
//		Начисления - регистрируемые начисления.
//		Удержания  - регистрируемые удержания.
//		РезультатыРасчетаНДФЛ - таблица с НДФЛ.
//		ЗаписыватьДвижения - тип Булево, признак того, что движения необходимо записать
//
Процедура ЗарегистрироватьНачисленияУдержания(ДанныеДляПроведения, Отказ, Начисления, Удержания, РезультатыРасчетаНДФЛ, ЗаписыватьДвижения = Ложь) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата") Тогда
		Возврат;
	КонецЕсли;
	
	Движения 			= ДанныеДляПроведения.Движения;
	Организация 		= ДанныеДляПроведения.Организация;
	ПериодРегистрации 	= ДанныеДляПроведения.ПериодРегистрации;
	ПорядокВыплаты 		= ДанныеДляПроведения.ПорядокВыплаты;
	ВыплатитьКакАванс 	= ДанныеДляПроведения.ВыплатитьКакАванс;
	ДниОтпускаАвансом 	= ДанныеДляПроведения.ДниОтпускаАвансом;
	
	ДанныеМежрасчетногоПериода = ?(ПорядокВыплаты = Неопределено, Ложь, РасчетЗарплатыРасширенный.ЭтоМежрасчетнаяВыплата(ПорядокВыплаты));
	
	НачислениеУдержаниеВидОперации = ОтражениеЗарплатыВУчете.НачислениеУдержаниеВидОперации();
	
	Если Начисления <> НеОпределено Тогда
		
		ВидыДоходаИсполнительногоПроизводства = УчетНачисленнойЗарплаты.ВидыДоходовИсполнительногоПроизводстваНачислений();
		
		ВыделятьДниОтпускаАвансом = ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") И ДниОтпускаАвансом <> Неопределено И ДниОтпускаАвансом.Количество() > 0;
		
		Если ВыделятьДниОтпускаАвансом Тогда
			ТаблицаНачислений = Начисления.Скопировать();
			ЗаполнитьВидОперацииВНачислениях(ТаблицаНачислений, НачислениеУдержаниеВидОперации, ДниОтпускаАвансом);
		Иначе
			ТаблицаНачислений = Начисления;
		КонецЕсли;
		
		Для Каждого Строка Из ТаблицаНачислений Цикл
			
			НоваяСтрока = Движения.БухучетНачисленияУдержанияПоСотрудникам.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			НоваяСтрока.Период				= ПериодРегистрации;
			НоваяСтрока.НачислениеУдержание	= Строка.Начисление;
			НоваяСтрока.Организация			= Организация;
			НоваяСтрока.ПериодДействия		= НачалоМесяца(НоваяСтрока.ДатаНачала);
			НоваяСтрока.ДанныеМежрасчетногоПериода  = ДанныеМежрасчетногоПериода;
			НоваяСтрока.УчитыватьВРаспределенииНДФЛ = ВыплатитьКакАванс;
			Если Не ВыделятьДниОтпускаАвансом Тогда
				НоваяСтрока.ВидОперации = НачислениеУдержаниеВидОперации[Строка.Начисление];
			КонецЕсли;
			
			НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Начислено;
			Если Не ЗначениеЗаполнено(НоваяСтрока.ПодразделениеУчетаЗатрат) Тогда
				НоваяСтрока.ПодразделениеУчетаЗатрат = НоваяСтрока.Подразделение;
			КонецЕсли;
			
			НоваяСтрока.ВидДоходаИсполнительногоПроизводства = ВидыДоходаИсполнительногоПроизводства[Строка.Начисление];
			
		КонецЦикла;
		
		Движения.БухучетНачисленияУдержанияПоСотрудникам.Записывать = Истина;
		
	КонецЕсли;
	
	Если Удержания <> НеОпределено Тогда
		
		Для Каждого Строка Из Удержания Цикл
			
			НоваяСтрока = Движения.БухучетНачисленияУдержанияПоСотрудникам.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			НоваяСтрока.Период				= ПериодРегистрации;
			НоваяСтрока.НачислениеУдержание = Строка.Удержание;
			НоваяСтрока.Организация			= Организация;
			НоваяСтрока.ПериодДействия		= ПериодРегистрации;
			НоваяСтрока.ВидОперации			= НачислениеУдержаниеВидОперации[НоваяСтрока.НачислениеУдержание];
			НоваяСтрока.ДанныеМежрасчетногоПериода = ДанныеМежрасчетногоПериода;
			
			Если Строка.Удержание = Перечисления.ВидыОсобыхНачисленийИУдержаний.МатериальнаяВыгодаПоЗаймам
					Или Строка.Удержание = Перечисления.ВидыОсобыхНачисленийИУдержаний.НачисленоПроцентовПоЗайму Тогда
				НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Справочно;
			Иначе
				НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Удержано;
			КонецЕсли
			
		КонецЦикла;
		
		Движения.БухучетНачисленияУдержанияПоСотрудникам.Записывать = Истина;
		
	КонецЕсли;

	Если РезультатыРасчетаНДФЛ <> НеОпределено Тогда
		
		Для Каждого Строка Из РезультатыРасчетаНДФЛ Цикл
			
			НоваяСтрока = Движения.БухучетНачисленияУдержанияПоСотрудникам.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			НоваяСтрока.Подразделение							= Строка.ПодразделениеСотрудника;
			НоваяСтрока.ТерриторияВыполненияРаботВОрганизации 	= Строка.Подразделение;
			
			НоваяСтрока.Период				= ПериодРегистрации;
			НоваяСтрока.ПериодДействия		= ПериодРегистрации;
			НоваяСтрока.НачислениеУдержание = Строка.ВидУдержания;
			НоваяСтрока.Организация			= Организация;
			НоваяСтрока.ВидОперации			= НачислениеУдержаниеВидОперации[НоваяСтрока.НачислениеУдержание];
			НоваяСтрока.ДанныеМежрасчетногоПериода = ДанныеМежрасчетногоПериода;
			НоваяСтрока.ДатаПолученияДохода = Строка.МесяцНалоговогоПериода;
			
			НоваяСтрока.ГруппаНачисленияУдержанияВыплаты = Перечисления.ГруппыНачисленияУдержанияВыплаты.Удержано;
			
		КонецЦикла;
		
		Движения.БухучетНачисленияУдержанияПоСотрудникам.Записывать = Истина;
		
	КонецЕсли;
	
	Если ЗаписыватьДвижения Тогда
		Движения.БухучетНачисленияУдержанияПоСотрудникам.Записать();
		Движения.БухучетНачисленияУдержанияПоСотрудникам.Записывать = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьВидОперацииВНачислениях(Начисления, НачислениеУдержаниеВидОперации, ДниОтпускаАвансом)

	Начисления.Колонки.Добавить("ВидОперации", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыОперацийПоЗарплате"));
	
	НачисленияОтпуска = Начисления.СкопироватьКолонки();
	ПрочиеНачисления  = Начисления.СкопироватьКолонки();
	
	Отбор = Новый Структура("Начисление,Сторно");
	
	Для каждого СтрокаТЗ Из Начисления Цикл
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаТЗ);
		
		НайденныеСтроки = ДниОтпускаАвансом.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(НачисленияОтпуска.Добавить(), СтрокаТЗ);
		Иначе
			НоваяСтрока = ПрочиеНачисления.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
			НоваяСтрока.ВидОперации = НачислениеУдержаниеВидОперации[СтрокаТЗ.Начисление];
		КонецЕсли;
		
	КонецЦикла;
	
	Начисления = ПрочиеНачисления;
	
	Если НачисленияОтпуска.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
		
	Отбор = Новый Структура("Начисление,Сторно");
	ОтборПоПериоду = Новый Структура("Начисление,Сторно,ДатаНачала");
	
	Для каждого СтрокаТЗ Из ДниОтпускаАвансом Цикл
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаТЗ);
		
		СтрокиОтпуска = НачисленияОтпуска.Скопировать(Отбор);
		СтрокиОтпуска.Свернуть("ДатаНачала,ОплаченоДней");
		СтрокиОтпуска.Сортировать("ДатаНачала Убыв");
		
		ОсталосьДнейАвансом = СтрокаТЗ.КоличествоДнейАвансом;
		Для каждого СтрокаОтпуска Из СтрокиОтпуска Цикл
			
			ОплаченоДней = ?(СтрокаТЗ.Сторно, -СтрокаОтпуска.ОплаченоДней, СтрокаОтпуска.ОплаченоДней);
			
			Если ОсталосьДнейАвансом = 0 Тогда
				ДоляАванса = 0;
			ИначеЕсли ОплаченоДней <= ОсталосьДнейАвансом Тогда
				ОсталосьДнейАвансом = ОсталосьДнейАвансом - ОплаченоДней;
				ДоляАванса = 1;
			Иначе
				ДоляАванса = ОсталосьДнейАвансом / ОплаченоДней;
				ОсталосьДнейАвансом = 0;
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(ОтборПоПериоду, СтрокаТЗ);
			ОтборПоПериоду.ДатаНачала = СтрокаОтпуска.ДатаНачала;
			
			СтрокиДляРаспределения = НачисленияОтпуска.Скопировать(ОтборПоПериоду);
			
			Для каждого СтрокаРаспределения Из СтрокиДляРаспределения Цикл
				
				НоваяСтрока = Начисления.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРаспределения);
				Если ДоляАванса = 0 Тогда
					НоваяСтрока.ВидОперации = НачислениеУдержаниеВидОперации[Отбор.Начисление];
				ИначеЕсли ДоляАванса = 1 Тогда
					НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускАвансом;
				Иначе
					
					НоваяСтрока.ВидОперации = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускАвансом;
					СуммаАвансом = Окр(НоваяСтрока.Сумма * ДоляАванса, 2);
					НоваяСтрока.Сумма = СуммаАвансом;
					
					НоваяСтрока = Начисления.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРаспределения);
					НоваяСтрока.ВидОперации = НачислениеУдержаниеВидОперации[Отбор.Начисление];
					НоваяСтрока.Сумма = НоваяСтрока.Сумма - СуммаАвансом;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
		
КонецПроцедуры

// Формирует сторно записи отменяющие движения исправленного документа по регистрам подсистемы.
//
// Параметры:
//  Движения				 - КоллекцияДвижений, Структура	 - Коллекция движений в которую будут добавлены сторно записи.
//  ИсправленныйДокумент	 - ДокументСсылка				 - Документ, записи которого необходимо сторнировать.
//  Записывать				 - Булево						 - Если Истина, то наборы будут записаны сразу, если Ложь, то наборам будет установлен признак Записывать = Истина.
//
Процедура СторнироватьДвиженияДокумента(Движения, ИсправленныйДокумент, Записывать = Ложь) Экспорт
	
	ТолькоИзолироватьНаборы = Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата");
	
	ИмяУчета = "ОтражениеЗарплатыВБухучете";
	МетаданныеРегистров = МетаданныеРегистровПодсистемы();
	
	ДвиженияВСтруктуре = ТипЗнч(Движения) = Тип("Структура");
	Набор = Неопределено;
	
	// Сторнирование универсальными алгоритмами.
	
	Для Каждого МетаданныеРегистра Из МетаданныеРегистров Цикл
		
		Если ДвиженияВСтруктуре Тогда 
			Движения.Свойство(МетаданныеРегистра.Имя, Набор);
		Иначе 
			Набор = Движения.Найти(МетаданныеРегистра.Имя);
		КонецЕсли;
		
		Если Набор = Неопределено Или Не ИсправлениеДокументовЗарплатаКадры.ИзолироватьУчетом(Набор, ИмяУчета) Или ТолькоИзолироватьНаборы Тогда
			Продолжить;
		КонецЕсли;
		
		Если ОбщегоНазначения.ЭтоРегистрНакопления(МетаданныеРегистра) Тогда
			ИсправлениеДокументовЗарплатаКадры.СторнироватьДвиженияВРегистреНакопления(Набор, ИсправленныйДокумент, МетаданныеРегистра, Записывать);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗарегистрироватьБухучетРабочегоВремениСотрудников(Движения, ПериодРегистрации, ДокументОснование, БухучетДанныхОВремени) Экспорт
	
	Если БухучетДанныхОВремени.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Строка Из БухучетДанныхОВремени Цикл
		
		Если Не ЗначениеЗаполнено(Строка.СпособОтраженияЗарплатыВБухучете)
			И Не ЗначениеЗаполнено(Строка.СтатьяФинансирования)
			И Не ЗначениеЗаполнено(Строка.СтатьяРасходов)
			И Не ЗначениеЗаполнено(Строка.ОтношениеКЕНВД) Тогда
			
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Движения.БухучетРабочегоВремениСотрудников.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.ПериодРегистрации 	= ПериодРегистрации;
		НоваяСтрока.ДокументОснование 	= ДокументОснование;
		НоваяСтрока.ВидУчетаВремени 	= Строка.ВидВремени;
		НоваяСтрока.Часы 				= Строка.Часов;
		
		Движения.БухучетРабочегоВремениСотрудников.Записывать = Истина;
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбслуживаниеМенеджераРасчетов

// Выполняет отражение начислений в бухучете, создает временную таблицу с именем указанным в ИсходныеДанные - ИмяВТБухучетНачислений.
//
// Параметры:
// 		ИсходныеДанные - структура, см ОписаниеИсходныхДанныхДляОтраженияНачисленийВБухучете
//			Организация
//			МесяцНачисления
//			МенеджерВременныхТаблиц 	- менеджер временных таблиц, содержит таблицу с начислениями, имя ВТ в значении ИмяВТНачисленияИсходная 
//			РаботаВБюджетномУчреждении 	- Булево, значение ФО РаботаВБюджетномУчреждении
//			ИспользоватьСтатьиФинансирования  - Булево, значение ФО ИспользоватьСтатьиФинансирования
//			ДокументСсылка
//			ПроцентЕНВД
//			ПлательщикЕНВД
//
//			ИмяВТБухучетНачислений - имя ВТ в которую будут помещены результаты отражения в бухучете 
//					ИдентификаторСтроки
//					Сотрудник
//					ФизическоеЛицо
//					Подразделение
//					ПодразделениеУчетаЗатрат
//					СпособОтраженияЗарплатыВБухучете
//					СтатьяФинансирования
//					СтатьяРасходов
//					Сумма
//					ОблагаетсяЕНВД
//					ДатаНачала
//					Начисление
//					Территория
//					КодСтатьиФинансирования
//					МестоПолученияДохода
//
//			ИмяВТНачисленияИсходная - имя ВТ с начислениями для получения бухучета, содержит поля
//					ПериодРегистрации
//					Организация
//					Сотрудник
//					ФизическоеЛицо
//					Подразделение
//					ТерриторияВыполненияРаботВОрганизации
//					ДатаНачала
//					ДатаОкончания
//					Начисление
//					ДокументОснование
//					ИдентификаторСтроки
//					Сумма
//					Сторно
//					ФиксСторно
//					ВидОперации
//					МестоПолученияДохода
//
//			СтрокиБухучетСторноНачислений - соответствие
//					Ключ	 - ИдентификаторСтроки
//					Значение - таблица значений бухучетом, описание см НоваяТаблицаРаспределениеРезультатовНачислений().
//
//			СтрокиКоэффициентыСреднегоЗаработка - соответствие
//					Ключ	 - ИдентификаторСтроки
//					Значение - таблица значений с коэффициентами распределения, описание см НоваяТаблицаКоэффициентыРаспределенияСреднегоЗаработка().
//
//			КоэффициентыСреднегоЗаработкаДокумента - Соответствие
//					Ключ	 - Перечисления.СпособыРасчетаНачислений
//					Значение - таблица значений с коэффициентами распределения, описание см НоваяТаблицаКоэффициентыРаспределенияСреднегоЗаработка().
//	
//			КоэффициентыСреднегоЗаработкаФССДокумента - таблица значений с коэффициентами распределения, описание см НоваяТаблицаКоэффициентыРаспределенияСреднегоЗаработка().
//
//			БухучетПервичногоДокумента - таблица значений, описание см НоваяТаблицаБухучетЗарплатыПервичныхДокументов().
//
// Возвращаемое значение: таблица значений с бухучетом начислений
//		описание таблицы см НоваяТаблицаРаспределениеРезультатовНачислений().
//
Функция ВыполнитьОтражениеНачисленийВБухучете(ИсходныеДанные) Экспорт
	
	// В случае если отражение в бухучете выполняется сразу при расчете
	// уточним отражение в бухучете для учтенных ранее начисленных сумм.
	РаспределятьРанееНачисленныеСуммы = (ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата")
												И ИсходныеДанные.МенеджерРасчетаЗарплаты <> Неопределено);
	
	УдалитьВТ = Новый Массив;
	
	ИмяВТБухучетНачислений 	= ИсходныеДанные.ИмяВТБухучетНачислений;
	МенеджерВременныхТаблиц = ИсходныеДанные.МенеджерВременныхТаблиц;
	ИмяВТНачисления			= ИсходныеДанные.ИмяВТНачисления;
	СтрокиБухучетСторноНачислений = ИсходныеДанные.СтрокиБухучетСторноНачислений;
	
	СоздатьВТБухучетНачисленийСторно(МенеджерВременныхТаблиц, ИмяВТНачисления, СтрокиБухучетСторноНачислений);
	УдалитьВТ.Добавить("ВТБухучетНачисленийСторно");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если РаспределятьРанееНачисленныеСуммы Тогда
		СоздатьВТБухучетКорректировкаРанееВыполненногоНачисления(ИсходныеДанные);
	Иначе
		СоздатьПустуюВТБухучетНачислений(Запрос, "ВТБухучетКорректировкаРанееВыполненногоНачисления");
	КонецЕсли;
	УдалитьВТ.Добавить("ВТБухучетКорректировкаРанееВыполненногоНачисления");
	
	Запрос.УстановитьПараметр("РаспределятьРанееНачисленныеСуммы", РаспределятьРанееНачисленныеСуммы);
	
	// Входная таблица ВТНачисленияДляОтраженияВБухучет содержит поля идентификаторов строк.
	// ИдентификаторСтроки - уникальный в пределах этой таблицы идентификатор строки.
	// ИдентификаторСтрокиБухучет - идентификатор строки таблицы начислений менеджера, используется для
	// помещения результатов распределения в таблицу начислений менеджера, может быть не уникальным
	// в пределах этой таблицы, когда ведется учет по территориям.
	// ИдентификаторСтрокиНачисления - идентификатор строки таблицы начислений менеджера, используемый
	// в алгоритмах расчета менеджера.
	// В выходной таблице ИмяВТБухучетНачислений поле ИдентификаторСтроки будет содержать данные из поля ИдентификаторСтрокиБухучет.
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НачисленияДляРаспределения.ПериодРегистрации КАК ПериодРегистрации,
	|	НачисленияДляРаспределения.Организация КАК Организация,
	|	НачисленияДляРаспределения.Сотрудник КАК Сотрудник,
	|	НачисленияДляРаспределения.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НачисленияДляРаспределения.Подразделение КАК Подразделение,
	|	НачисленияДляРаспределения.Начисление КАК Начисление,
	|	НачисленияДляРаспределения.ДатаНачала КАК ДатаНачала,
	|	НачисленияДляРаспределения.ДатаОкончания КАК ДатаОкончания,
	|	НачисленияДляРаспределения.РанееНачислено КАК РанееНачислено,
	|	НачисленияДляРаспределения.ДокументОснование КАК ДокументОснование,
	|	НачисленияДляРаспределения.ИдентификаторСтрокиБухучет КАК ИдентификаторСтрокиБухучет,
	|	НачисленияДляРаспределения.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НачисленияДляРаспределения.ИдентификаторСтрокиНачисления КАК ИдентификаторСтрокиНачисления,
	|	НачисленияДляРаспределения.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	НачисленияДляРаспределения.Сторно КАК Сторно,
	|	НачисленияДляРаспределения.ФиксСторно КАК ФиксСторно,
	|	НачисленияДляРаспределения.ВидОперации КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА &РаспределятьРанееНачисленныеСуммы
	|				И ЕСТЬNULL(Начисления.СтратегияОтраженияВУчете, ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам)
	|			ТОГДА НачисленияДляРаспределения.Сумма + НачисленияДляРаспределения.РанееНачислено
	|		ИНАЧЕ НачисленияДляРаспределения.Сумма
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА &РаспределятьРанееНачисленныеСуммы
	|				И ЕСТЬNULL(Начисления.СтратегияОтраженияВУчете, ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам)
	|				И НачисленияДляРаспределения.РанееНачислено <> 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК РанееНачисленныеДанные,
	|	НачисленияДляРаспределения.РассчитыватьПоРазовымНачислениямДокумента КАК РассчитыватьПоРазовымНачислениямДокумента
	|ПОМЕСТИТЬ ВТНачисленияДляРаспределения
	|ИЗ
	|	ВТНачисленияДляОтраженияВБухучете КАК НачисленияДляРаспределения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетНачисленийСторно КАК БухучетНачисленийСторно
	|		ПО НачисленияДляРаспределения.ИдентификаторСтроки = БухучетНачисленийСторно.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетКорректировкаРанееВыполненногоНачисления КАК БухучетКорректировка
	|		ПО НачисленияДляРаспределения.ИдентификаторСтроки = БухучетКорректировка.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК Начисления
	|		ПО НачисленияДляРаспределения.Начисление = Начисления.Ссылка
	|ГДЕ
	|	БухучетНачисленийСторно.ИдентификаторСтроки ЕСТЬ NULL
	|	И БухучетКорректировка.ИдентификаторСтроки ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленияДляРаспределения.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НачисленияДляРаспределения.ИдентификаторСтрокиНачисления КАК ИдентификаторСтрокиНачисления,
	|	НачисленияДляРаспределения.Сотрудник КАК Сотрудник,
	|	НачисленияДляРаспределения.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НачисленияДляРаспределения.Подразделение КАК Подразделение,
	|	НачисленияДляРаспределения.Начисление КАК Начисление,
	|	НачисленияДляРаспределения.ДатаНачала КАК ДатаНачала,
	|	СУММА(НачисленияДляРаспределения.РанееНачислено) КАК РанееНачислено,
	|	НачисленияДляРаспределения.ТерриторияВыполненияРаботВОрганизации КАК Территория
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК НачисленияДляРаспределения
	|ГДЕ
	|	НачисленияДляРаспределения.РанееНачисленныеДанные
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленияДляРаспределения.ИдентификаторСтроки,
	|	НачисленияДляРаспределения.ИдентификаторСтрокиНачисления,
	|	НачисленияДляРаспределения.Сотрудник,
	|	НачисленияДляРаспределения.ФизическоеЛицо,
	|	НачисленияДляРаспределения.Подразделение,
	|	НачисленияДляРаспределения.Начисление,
	|	НачисленияДляРаспределения.ДатаНачала,
	|	НачисленияДляРаспределения.ТерриторияВыполненияРаботВОрганизации";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляОтраженияВБухучете", ИмяВТНачисления);
	ТаблицаРанееНачислено = Запрос.Выполнить().Выгрузить();
	
	УдалитьВТ.Добавить("ВТНачисленияДляРаспределения");
	
	ИсходныеДанные.ИмяВТНачисления = "ВТНачисленияДляРаспределения";
	ИсходныеДанные.УчитыватьСторноЗаписи = Истина;
	СоздатьВТБухучетНачисленийБезДоговоровГПХ(ИсходныеДанные, "ВТБухучетНачисленийВыходнаяТаблица");
	УдалитьВТ.Добавить("ВТБухучетНачисленийВыходнаяТаблица");
	
	Если ТаблицаРанееНачислено.Количество() > 0 Тогда
		СоздатьВТБухучетНачисленийРанееНачислено(ТаблицаРанееНачислено, ИсходныеДанные);
	Иначе
		СоздатьПустуюВТБухучетНачислений(Запрос, "ВТБухучетНачисленийРанееНачислено");
	КонецЕсли;
	УдалитьВТ.Добавить("ВТБухучетНачисленийРанееНачислено");
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.ИдентификаторСтрокиБухучет КАК ИдентификаторСтроки,
	|	Начисления.МестоПолученияДохода КАК МестоПолученияДохода,
	|	Начисления.ТерриторияВыполненияРаботВОрганизации КАК Территория,
	|	Начисления.Сумма КАК ИсходнаяСумма,
	|	БухучетНачислений.Сотрудник КАК Сотрудник,
	|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
	|	БухучетНачислений.Подразделение КАК Подразделение,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	СУММА(БухучетНачислений.Сумма) КАК Сумма,
	|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
	|	БухучетНачислений.Начисление КАК Начисление,
	|	ЕСТЬNULL(СтатьиФинансированияЗарплата.Код, """") КАК КодСтатьиФинансирования
	|ПОМЕСТИТЬ ИмяВТБухучетНачислений
	|ИЗ
	|	(ВЫБРАТЬ
	|		БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|		БухучетНачислений.Сотрудник КАК Сотрудник,
	|		БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
	|		БухучетНачислений.Подразделение КАК Подразделение,
	|		БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|		БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|		БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|		БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|		БухучетНачислений.Сумма КАК Сумма,
	|		БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|		БухучетНачислений.ДатаНачала КАК ДатаНачала,
	|		БухучетНачислений.Начисление КАК Начисление
	|	ИЗ
	|		ВТБухучетНачисленийВыходнаяТаблица КАК БухучетНачислений
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		БухучетНачисленийСторно.ИдентификаторСтроки,
	|		БухучетНачисленийСторно.Сотрудник,
	|		БухучетНачисленийСторно.ФизическоеЛицо,
	|		БухучетНачисленийСторно.Подразделение,
	|		БухучетНачисленийСторно.Подразделение,
	|		БухучетНачисленийСторно.СпособОтраженияЗарплатыВБухучете,
	|		БухучетНачисленийСторно.СтатьяФинансирования,
	|		БухучетНачисленийСторно.СтатьяРасходов,
	|		БухучетНачисленийСторно.Сумма,
	|		БухучетНачисленийСторно.ОблагаетсяЕНВД,
	|		БухучетНачисленийСторно.ДатаНачала,
	|		БухучетНачисленийСторно.Начисление
	|	ИЗ
	|		ВТБухучетНачисленийСторно КАК БухучетНачисленийСторно
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		БухучетНачисленийСторно.ИдентификаторСтроки,
	|		БухучетНачисленийСторно.Сотрудник,
	|		БухучетНачисленийСторно.ФизическоеЛицо,
	|		БухучетНачисленийСторно.Подразделение,
	|		БухучетНачисленийСторно.Подразделение,
	|		БухучетНачисленийСторно.СпособОтраженияЗарплатыВБухучете,
	|		БухучетНачисленийСторно.СтатьяФинансирования,
	|		БухучетНачисленийСторно.СтатьяРасходов,
	|		БухучетНачисленийСторно.Сумма,
	|		БухучетНачисленийСторно.ОблагаетсяЕНВД,
	|		БухучетНачисленийСторно.ДатаНачала,
	|		БухучетНачисленийСторно.Начисление
	|	ИЗ
	|		ВТБухучетНачисленийРанееНачислено КАК БухучетНачисленийСторно
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		БухучетКорректировка.ИдентификаторСтроки,
	|		БухучетКорректировка.Сотрудник,
	|		БухучетКорректировка.ФизическоеЛицо,
	|		БухучетКорректировка.Подразделение,
	|		БухучетКорректировка.Подразделение,
	|		БухучетКорректировка.СпособОтраженияЗарплатыВБухучете,
	|		БухучетКорректировка.СтатьяФинансирования,
	|		БухучетКорректировка.СтатьяРасходов,
	|		БухучетКорректировка.Сумма,
	|		БухучетКорректировка.ОблагаетсяЕНВД,
	|		БухучетКорректировка.ДатаНачала,
	|		БухучетКорректировка.Начисление
	|	ИЗ
	|		ВТБухучетКорректировкаРанееВыполненногоНачисления КАК БухучетКорректировка) КАК БухучетНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтатьиФинансированияЗарплата КАК СтатьиФинансированияЗарплата
	|		ПО БухучетНачислений.СтатьяФинансирования = СтатьиФинансированияЗарплата.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияДляОтраженияВБухучете КАК Начисления
	|		ПО БухучетНачислений.ИдентификаторСтроки = Начисления.ИдентификаторСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	БухучетНачислений.Сотрудник,
	|	БухучетНачислений.ФизическоеЛицо,
	|	БухучетНачислений.ОблагаетсяЕНВД,
	|	БухучетНачислений.ПодразделениеУчетаЗатрат,
	|	БухучетНачислений.ДатаНачала,
	|	БухучетНачислений.Начисление,
	|	БухучетНачислений.Подразделение,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов,
	|	Начисления.ТерриторияВыполненияРаботВОрганизации,
	|	Начисления.МестоПолученияДохода,
	|	Начисления.Сумма,
	|	Начисления.ИдентификаторСтрокиБухучет,
	|	ЕСТЬNULL(СтатьиФинансированияЗарплата.Код, """")
	|
	|ИМЕЮЩИЕ
	|	(СУММА(БухучетНачислений.Сумма) <> 0
	|		ИЛИ СУММА(БухучетНачислений.Сумма) = 0
	|			И Начисления.Сумма = 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачислений.Сумма КАК Результат,
	|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	БухучетНачислений.Территория КАК Территория,
	|	БухучетНачислений.КодСтатьиФинансирования КАК КодСтатьиФинансирования,
	|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат
	|ИЗ
	|	ИмяВТБухучетНачислений КАК БухучетНачислений
	|
	|УПОРЯДОЧИТЬ ПО
	|	БухучетНачислений.ИдентификаторСтроки,
	|	БухучетНачислений.Территория,
	|	БухучетНачислений.СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.ОблагаетсяЕНВД";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИмяВТБухучетНачислений", ИмяВТБухучетНачислений);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляОтраженияВБухучете", ИмяВТНачисления);
	
	БухучетНачислений = Запрос.Выполнить().Выгрузить();
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, УдалитьВТ);
	
	Возврат БухучетНачислений;

КонецФункции

// Выполняет отражение начислений по договорам ГПХ в бухучете, создает временную таблицу с именем указанным в
// ИсходныеДанные - ИмяВТБухучетНачислений.
//
// Параметры:
// 		ИсходныеДанные - структура, см ОписаниеИсходныхДанныхДляОтраженияНачисленийВБухучете.
//
// Возвращаемое значение: таблица значений с бухучетом начислений
//		описание таблицы см НоваяТаблицаРаспределениеРезультатовНачислений().
//
Функция ВыполнитьОтражениеНачисленийПоДоговорамВБухучете(ИсходныеДанные) Экспорт

	ИмяВТБухучетНачислений  = ИсходныеДанные.ИмяВТБухучетНачислений;
	МенеджерВременныхТаблиц = ИсходныеДанные.МенеджерВременныхТаблиц;
	
	УдалитьВТ = Новый Массив;
	
	// Входная таблица ИсходныеДанные.ИмяВТНачисления содержит поля идентификаторов строк.
	// ИдентификаторСтроки - уникальный в пределах этой таблицы идентификатор строки.
	// ИдентификаторСтрокиБухучет - идентификатор строки таблицы начислений менеджера, используется для
	// 		помещения результатов распределения в таблицу начислений менеджера, может быть не уникальным
	// 		в пределах этой таблицы, когда ведется учет по территориям.
	// В выходной таблице ИмяВТБухучетНачислений поле ИдентификаторСтроки будет содержать данные из поля ИдентификаторСтрокиБухучет.
		
	СоздатьВТБухучетНачисленийПоДоговорам(ИсходныеДанные, "ВТБухучетНачисленийПоДоговорамВременная");
	УдалитьВТ.Добавить("ВТБухучетНачисленийПоДоговорамВременная");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.МестоПолученияДохода КАК МестоПолученияДохода,
	|	Начисления.ИдентификаторСтрокиБухучет КАК ИдентификаторСтроки,
	|	БухучетНачислений.Сотрудник КАК Сотрудник,
	|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
	|	БухучетНачислений.Подразделение КАК Подразделение,
	|	БухучетНачислений.Подразделение КАК ПодразделениеУчетаЗатрат,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачислений.Сумма КАК Сумма,
	|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
	|	БухучетНачислений.Начисление КАК Начисление,
	|	БухучетНачислений.ТерриторияВыполненияРаботВОрганизации КАК Территория,
	|	ЕСТЬNULL(СтатьиФинансированияЗарплата.Код, """") КАК КодСтатьиФинансирования
	|ПОМЕСТИТЬ ВТБухучетНачисленийВыходнаяТаблица
	|ИЗ
	|	ВТБухучетНачисленийПоДоговорамВременная КАК БухучетНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтатьиФинансированияЗарплата КАК СтатьиФинансированияЗарплата
	|		ПО БухучетНачислений.СтатьяФинансирования = СтатьиФинансированияЗарплата.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияДляОтраженияВБухучете КАК Начисления
	|		ПО БухучетНачислений.ИдентификаторСтроки = Начисления.ИдентификаторСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачислений.Сумма КАК Результат,
	|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	БухучетНачислений.Территория КАК Территория,
	|	БухучетНачислений.КодСтатьиФинансирования КАК КодСтатьиФинансирования
	|ИЗ
	|	ВТБухучетНачисленийВыходнаяТаблица КАК БухучетНачислений
	|
	|УПОРЯДОЧИТЬ ПО
	|	БухучетНачислений.ИдентификаторСтроки,
	|	БухучетНачислений.Территория,
	|	БухучетНачислений.СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.ОблагаетсяЕНВД";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТБухучетНачисленийВыходнаяТаблица", ИмяВТБухучетНачислений);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляОтраженияВБухучете", ИсходныеДанные.ИмяВТНачисления);
	
	БухучетНачисленийПоДоговорам = Запрос.Выполнить().Выгрузить();
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, УдалитьВТ);
	
	Возврат БухучетНачисленийПоДоговорам;

КонецФункции

// Выполняет отражение НДФЛ и КорректировокВыплаты в бухучете
//
// Параметры:
// 		ИсходныеДанные - структура, см ОписаниеИсходныхДанныхДляОтраженияУдержанийВБухучете
//				* МенеджерВременныхТаблиц
//				* ИмяВТБухучетНачислений
//				* ОкончательныйРасчет
//				* ПериодРегистрации
//				* Организация
//				* ИсключаемыйРегистратор
//				* РезультатРасчетаНДФЛ, описание см НоваяТаблицаРезультатРасчетаНДФЛ
//				* НДФЛКЗачету, описание см НоваяТаблицаКорректировкиВыплаты
//				* НДФЛЗачтено, описание см НоваяТаблицаКорректировкиВыплаты.
//
// Возвращаемое значение: структура с ключами
//		БухучетНДФЛ - Таблица значений, описание см в  ОтражениеЗарплатыВУчете.НоваяТаблицаРаспределениеРезультатовУдержаний()
//		БухучетКорректировкиВыплаты - Таблица значений, описание см в  ОтражениеЗарплатыВУчете.НоваяТаблицаРаспределениеРезультатовУдержаний().
//
Функция ВыполнитьОтражениеНДФЛИКорректировокВыплатыВБухучете(ИсходныеДанные) Экспорт

	БухучетНДФЛ 				 = ОтражениеЗарплатыВУчете.НоваяТаблицаРаспределениеРезультатовУдержаний();
	БухучетКорректировкиВыплаты  = ОтражениеЗарплатыВУчете.НоваяТаблицаРаспределениеРезультатовУдержаний();
	
	МенеджерВременныхТаблиц = ИсходныеДанные.МенеджерВременныхТаблиц;
	ИмяВТБухучетНачислений 	= ИсходныеДанные.ИмяВТБухучетНачислений;
	ОкончательныйРасчет		= ИсходныеДанные.ОкончательныйРасчет;
	ПериодРегистрации       = ИсходныеДанные.МесяцНачисления;
	Организация      		= ИсходныеДанные.Организация;
	ИсключаемыйРегистратор  = ИсходныеДанные.ИсключаемыйРегистратор;
	РегистраторыНДФЛОбновленияБухучета 	= ИсходныеДанные.РегистраторыУдержанийОбновленияБухучета;
	ОснованияУчтенныеПриРасчетеНДФЛ 	= ИсходныеДанные.ОснованияУчтенныеПриРасчетеНДФЛ;
	
	РезультатРасчетаНДФЛ 	= ИсходныеДанные.РезультатРасчетаНДФЛ;
	НДФЛКЗачету		 		= ИсходныеДанные.НДФЛКЗачету;
	НДФЛЗачтено		 		= ИсходныеДанные.НДФЛЗачтено;
	
	УдалитьВТ = Новый Массив;
	
	Если РезультатРасчетаНДФЛ.Количество() > 0 Или НДФЛКЗачету.Количество() > 0 Тогда
		
		// Если переданы основания, создаем временную таблицу ВТУсловияОтбораДляРаспределенияНДФЛ.
		Если ОснованияУчтенныеПриРасчетеНДФЛ <> Неопределено
			И ПериодРегистрации >= ОтражениеЗарплатыВУчете.ДатаНачалаИспользованияОснованийВРаспределенииНДФЛ() Тогда
			ОтражениеЗарплатыВУчете.СоздатьВТУсловияОтбораДляРаспределенияНДФЛ(МенеджерВременныхТаблиц, ОснованияУчтенныеПриРасчетеНДФЛ, ИсключаемыйРегистратор);
			УдалитьВТ.Добавить("ВТУсловияОтбораДляРаспределенияНДФЛ");
		КонецЕсли;
		
		ФизическиеЛицаМассив = ОбщегоНазначения.ВыгрузитьКолонку(РезультатРасчетаНДФЛ, "ФизическоеЛицо", Истина);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ФизическиеЛицаМассив, ОбщегоНазначения.ВыгрузитьКолонку(НДФЛКЗачету, "ФизическоеЛицо", Истина), Истина);
		
		УдержанияМассив = ОбщегоНазначения.ВыгрузитьКолонку(РезультатРасчетаНДФЛ, "ВидУдержания", Истина);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(УдержанияМассив, ОбщегоНазначения.ВыгрузитьКолонку(НДФЛКЗачету, "ВидУдержания", Истина), Истина);
		
		// Получение данных для распределения, ДанныеДляРаспределенияНДФЛ - структура
		// БазаВсеНачисления - таблица значений с базовыми начислениями
		// УжеУдержано - таблица значений, содержит данные об уже выполненных удержаниях по физическому лиц
		// в менеджере временных таблиц будет создана таблица ВТРаспределениеНачисленийДляБазыНДФЛ для получения базы НДФЛ.
		
		ПараметрыРаспределения = ОтражениеЗарплатыВУчете.НовоеОписаниеПараметровРаспределенияНДФЛ();
		ПараметрыРаспределения.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		ПараметрыРаспределения.Организация = Организация;
		ПараметрыРаспределения.ПериодРегистрации = ПериодРегистрации;
		ПараметрыРаспределения.МассивФизическихЛиц = ФизическиеЛицаМассив;
		ПараметрыРаспределения.МассивУдержаний = УдержанияМассив;
		ПараметрыРаспределения.ИсключаемыйРегистратор = ИсключаемыйРегистратор;
		ПараметрыРаспределения.ОкончательныйРасчет = ОкончательныйРасчет;
		ПараметрыРаспределения.РегистраторыНДФЛОбновленияБухучета = РегистраторыНДФЛОбновленияБухучета;
		ПараметрыРаспределения.ИмяВТДанныеТекущегоДокумента = ИмяВТБухучетНачислений;
		
		ДанныеДляРаспределенияНДФЛ = ОтражениеЗарплатыВУчете.ДанныеДляРаспределенияНДФЛ(ПараметрыРаспределения);
		УдалитьВТ.Добавить("ВТРаспределениеНачисленийДляБазыНДФЛ");
		
		БазаВсеНачисления = ДанныеДляРаспределенияНДФЛ.БазаВсеНачисления;
		СтрокиУжеУдержано = ДанныеДляРаспределенияНДФЛ.СтрокиУжеУдержано;
		
		СведенияОДоходахНДФЛ = Неопределено;
		Если ИсходныеДанные.ЗасчитыватьДанныеАвансовНДФЛ И ЗарплатаКадры.ВТСуществует(МенеджерВременныхТаблиц, "ВТРегистрНакопления_СведенияОДоходахНДФЛ") Тогда
				Запрос = Новый Запрос;
				Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	*
				|ИЗ
				|	ВТРегистрНакопления_СведенияОДоходахНДФЛ КАК СведенияОДоходах";
				СведенияОДоходахНДФЛ = Запрос.Выполнить().Выгрузить();
				СведенияОДоходахНДФЛ.Индексы.Добавить("ЗарегистрированоПриНачисленииАванса");
				СведенияОДоходахНДФЛБезАваносов = СведенияОДоходахНДФЛ.Скопировать(Новый Структура("ЗарегистрированоПриНачисленииАванса", Дата(1,1,1,0,0,0)));
				ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, "ВТРегистрНакопления_СведенияОДоходахНДФЛ");
				ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(МенеджерВременныхТаблиц, СведенияОДоходахНДФЛБезАваносов, "ВТРегистрНакопления_СведенияОДоходахНДФЛ");
				СведенияОДоходахНДФЛБезАваносов = Неопределено;
		КонецЕсли;
		БазаРасчетаНДФЛ = ОтражениеЗарплатыВУчете.БазаДляРаспределенияНДФЛ(ПараметрыРаспределения);
		Если СведенияОДоходахНДФЛ <> Неопределено Тогда
			ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, "ВТРегистрНакопления_СведенияОДоходахНДФЛ");
			ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(МенеджерВременныхТаблиц, СведенияОДоходахНДФЛ, "ВТРегистрНакопления_СведенияОДоходахНДФЛ");	
		КонецЕсли;
		
		// бухучет НДФЛ
		БухучетНДФЛ = ОтражениеЗарплатыВУчете.НДФЛПоРабочимМестамИСтатьям(РезультатРасчетаНДФЛ, БазаРасчетаНДФЛ, БазаВсеНачисления,
									СтрокиУжеУдержано, Организация, ПериодРегистрации);
									
		// бухучет НДФЛКЗачету
		БухучетНДФЛКЗачету = ОтражениеЗарплатыВУчете.КорректировкиВыплатыПоРабочимМестамИСтатьям(НДФЛКЗачету, БазаРасчетаНДФЛ, БазаВсеНачисления,
									Организация, ПериодРегистрации);
									
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(БухучетНДФЛКЗачету, БухучетКорректировкиВыплаты);
		
	КонецЕсли;
	
	Если НДФЛЗачтено.Количество()>0 Тогда
		
		ФизическиеЛицаМассив = ОбщегоНазначения.ВыгрузитьКолонку(НДФЛЗачтено, "ФизическоеЛицо", Истина);
		УдержанияМассив 	 = Новый Массив;
		
		// Получение данных для распределения, ДанныеДляРаспределения - структура
		// БазаВсеНачисления - таблица значений с базовыми начислениями
		// УжеУдержано - таблица значений, содержит данные об уже выполненных удержаниях по физическому лиц.
		ПараметрыПолученияДанных = ОтражениеЗарплатыВУчете.НовоеОписаниеПараметровПолученияДанныхДляРаспределенияУдержаний();
		ЗаполнитьЗначенияСвойств(ПараметрыПолученияДанных ,ИсходныеДанные, "МенеджерВременныхТаблиц,Организация,ОкончательныйРасчет");
		ПараметрыПолученияДанных.ПериодРегистрации = ПериодРегистрации;
		ПараметрыПолученияДанных.ИмяВТДанныеТекущегоДокумента = ИмяВТБухучетНачислений;
		
		ИсключаемыеРегистраторы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИсключаемыйРегистратор);
		Если РегистраторыНДФЛОбновленияБухучета <> Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ИсключаемыеРегистраторы, РегистраторыНДФЛОбновленияБухучета);
		КонецЕсли;

		ДанныеДляРаспределения = ОтражениеЗарплатыВУчете.ДанныеДляРаспределенияУдержаний(ФизическиеЛицаМассив, УдержанияМассив, ИсключаемыеРегистраторы, ПараметрыПолученияДанных);
		БазаВсеНачисления = ДанныеДляРаспределения.БазовыеНачисления;
		
		// КорректировкиВыплаты могут служить базой для распределения, нужно переименовать колонку "КорректировкаВыплаты".
		БазаРасчетаНДФЛ = ВзаиморасчетыССотрудниками.КорректировкиВыплаты(Организация, ПериодРегистрации, ФизическиеЛицаМассив, ИсключаемыйРегистратор);
		БазаРасчетаНДФЛ.Колонки.СуммаКорректировки.Имя = "Сумма";
		
		БухучетНДФЛЗачтено = ОтражениеЗарплатыВУчете.КорректировкиВыплатыПоРабочимМестамИСтатьям(НДФЛЗачтено, БазаРасчетаНДФЛ, БазаВсеНачисления,
									Организация, ПериодРегистрации);
									
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(БухучетНДФЛЗачтено, БухучетКорректировкиВыплаты);
		
	КонецЕсли;
	
	// сортировка результатов
	УпорядочитьТаблицуРаспределениеУдержаний(БухучетНДФЛ);
	УпорядочитьТаблицуРаспределениеУдержаний(БухучетКорректировкиВыплаты);
	
	// заполним колонку КодСтатьиФинансирования в таблицах
	КодыСтатейФинансирования = КодыСтатейФинансирования();
	ЗаполнитьКодСтатьиФинансирования(БухучетКорректировкиВыплаты, КодыСтатейФинансирования);
	ЗаполнитьКодСтатьиФинансирования(БухучетНДФЛ, КодыСтатейФинансирования);
	
	РезультатыОтраженияВБухучете = Новый Структура("БухучетНДФЛ,БухучетКорректировкиВыплаты");
	РезультатыОтраженияВБухучете.БухучетНДФЛ 				 = БухучетНДФЛ;
	РезультатыОтраженияВБухучете.БухучетКорректировкиВыплаты = БухучетКорректировкиВыплаты;
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, УдалитьВТ);
	
	Возврат РезультатыОтраженияВБухучете;
	
КонецФункции

// Выполняет отражение удержаний и займов в бухучете
//
// Параметры:
// 		ИсходныеДанные - структура, см ОписаниеИсходныхДанныхДляОтраженияУдержанийВБухучете
//				* МенеджерВременныхТаблиц
//				* ИмяВТБухучетНачислений
//				* ОкончательныйРасчет
//				* ПериодРегистрации
//				* Организация
//				* ИсключаемыйРегистратор
//				* ТаблицаУдержаний, описание см ОтражениеЗарплатыВУчете.НоваяТаблицаРезультатРасчетаУдержаний
//				* ТаблицаЗаймов, описание см ОтражениеЗарплатыВУчетеРасширенный.НоваяТаблицаРезультатПогашениеЗаймов.
//
// Возвращаемое значение: структура с ключами
//		БухучетУдержаний - Таблица значений, описание см в  ОтражениеЗарплатыВУчете.НоваяТаблицаРаспределениеРезультатовУдержаний()
//		БухучетЗаймов    - Таблица значений, описание см в  ОтражениеЗарплатыВУчете.НоваяТаблицаРаспределениеРезультатовУдержаний().
//
Функция ВыполнитьОтражениеУдержанийИЗаймовВБухучете(ИсходныеДанные) Экспорт

	БухучетУдержаний  = ОтражениеЗарплатыВУчете.НоваяТаблицаРаспределениеРезультатовУдержаний();
	БухучетЗаймов 	  = ОтражениеЗарплатыВУчете.НоваяТаблицаРаспределениеРезультатовУдержаний();
	
	МенеджерВременныхТаблиц = ИсходныеДанные.МенеджерВременныхТаблиц;
	ИмяВТБухучетНачислений 	= ИсходныеДанные.ИмяВТБухучетНачислений;
	ОкончательныйРасчет		= ИсходныеДанные.ОкончательныйРасчет;
	ПериодРегистрации       = ИсходныеДанные.МесяцНачисления;
	Организация      		= ИсходныеДанные.Организация;
	ИсключаемыйРегистратор  = ИсходныеДанные.ИсключаемыйРегистратор;
	РегистраторыУдержанийОбновленияБухучета = ИсходныеДанные.РегистраторыУдержанийОбновленияБухучета;
	
	ТаблицаУдержаний = ИсходныеДанные.ТаблицаУдержаний;
	ТаблицаЗаймов	 = ИсходныеДанные.ТаблицаЗаймов;
	
	// Получение данных учета для распределения
	// БазовыеНачисления - таблица значений с базовыми начислениями
	// УжеУдержано - таблица значений, содержит данные об уже выполненных удержаниях по физическому лицу.
	
	ФизическиеЛицаМассив = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ФизическиеЛицаМассив, ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаЗаймов, "ФизическоеЛицо", Истина), Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ФизическиеЛицаМассив, ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаУдержаний, "ФизическоеЛицо", Истина), Истина);
	
	УдержанияМассив 	 = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(УдержанияМассив, ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаУдержаний, "ВидУдержания", Истина), Истина);
	
	// Получение данных для распределения, ДанныеДляРаспределения - структура
	// БазаВсеНачисления - таблица значений с базовыми начислениями
	// УжеУдержано - таблица значений, содержит данные об уже выполненных удержаниях по физическому лиц.
	ПараметрыПолученияДанных = ОтражениеЗарплатыВУчете.НовоеОписаниеПараметровПолученияДанныхДляРаспределенияУдержаний();
	ЗаполнитьЗначенияСвойств(ПараметрыПолученияДанных ,ИсходныеДанные, "МенеджерВременныхТаблиц,Организация,ОкончательныйРасчет,РегистраторыУдержанийОбновленияБухучета");
	ПараметрыПолученияДанных.ПериодРегистрации = ПериодРегистрации;
	ПараметрыПолученияДанных.ИмяВТДанныеТекущегоДокумента = ИмяВТБухучетНачислений;
	
	ИсключаемыеРегистраторы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИсключаемыйРегистратор);
	
	ДанныеДляРаспределения = ОтражениеЗарплатыВУчете.ДанныеДляРаспределенияУдержаний(ФизическиеЛицаМассив, УдержанияМассив, ИсключаемыеРегистраторы, ПараметрыПолученияДанных);
	
	БазовыеНачисления = ДанныеДляРаспределения.БазовыеНачисления;
	БазовыеНачисления.Индексы.Добавить("ФизическоеЛицо");
	
	СтрокиУжеУдержано = ДанныеДляРаспределения.СтрокиУжеУдержано;
	
	// ТаблицаЗаймов
	ПараметрыРаспределения = ОтражениеЗарплатыВУчете.НовоеОписаниеПараметровРаспределенияУдержаний();
	ПараметрыРаспределения.Организация 		 = Организация;
	ПараметрыРаспределения.ПериодРегистрации = ПериодРегистрации;
	БухучетЗаймов = ОтражениеЗарплатыВУчете.РаспределениеУдержанийПоРабочимМестамИСтатьям(ТаблицаЗаймов, БазовыеНачисления, Новый Соответствие, ПараметрыРаспределения);
	
	Если ПолучитьФункциональнуюОпцию("РаботаВХозрасчетнойОрганизации") Тогда
		// Уточним статью расходов.
		Для каждого СтрокаТЗ Из БухучетЗаймов Цикл
			Если СтрокаТЗ.ВидУдержания = Перечисления.ВидыОсобыхНачисленийИУдержаний.НачисленоПроцентовПоЗайму Тогда
				СтрокаТЗ.СтатьяРасходов = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// ТаблицаУдержаний
	ДополнительныеПараметры = ОтражениеЗарплатыВУчетеРасширенный.ОписаниеИсходныхДанныхДляРаспределенияУдержаний();
	ДополнительныеПараметры.БазовыеНачисления = БазовыеНачисления;
	ДополнительныеПараметры.СтрокиУжеУдержаноПоФизическимЛицам = СтрокиУжеУдержано;
	ДополнительныеПараметры.ТаблицаУдержаний = ТаблицаУдержаний;
	ДополнительныеПараметры.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ДополнительныеПараметры.Организация = Организация;
	ДополнительныеПараметры.ПериодРегистрации = ПериодРегистрации;
	ДополнительныеПараметры.ВидыНачисленийДополненияРасчетнойБазы = ИсходныеДанные.ВидыНачисленийДополненияРасчетнойБазы;
	ДополнительныеПараметры.ОкончательныйРасчет = ОкончательныйРасчет;
	ОтражениеЗарплатыВУчетеРасширенный.ВыполнитьРаспределениеУдержаний(БухучетУдержаний, ДополнительныеПараметры);
	
	// сортировка результатов
	УпорядочитьТаблицуРаспределениеУдержаний(БухучетУдержаний);
	УпорядочитьТаблицуРаспределениеУдержаний(БухучетЗаймов);
	
	// заполним колонку КодСтатьиФинансирования в таблицах
	КодыСтатейФинансирования = КодыСтатейФинансирования();
	ЗаполнитьКодСтатьиФинансирования(БухучетУдержаний, КодыСтатейФинансирования);
	ЗаполнитьКодСтатьиФинансирования(БухучетЗаймов, КодыСтатейФинансирования);
	
	РезультатыОтраженияВБухучете = Новый Структура("БухучетУдержаний,БухучетЗаймов");
	РезультатыОтраженияВБухучете.БухучетУдержаний  = БухучетУдержаний;
	РезультатыОтраженияВБухучете.БухучетЗаймов 	   = БухучетЗаймов;
	
	Возврат РезультатыОтраженияВБухучете;

КонецФункции

// Создает временную таблицу с именем ИмяВТ помещая в нее содержимое таблицы значений
//	Параметры
//		МенеджерВременныхТаблиц - менеджер временных таблиц
//		БухучетНачислений - таблица значений, описание см НоваяТаблицаБухучетНачислений()
//		ИмяВТ - строка, имя создаваемой временной таблицы.
//
Процедура СоздатьВТБухучетНачисленийПоТаблицеБухучетНачислений(МенеджерВременныхТаблиц, БухучетНачислений, ИмяВТ) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если БухучетНачислений.Количество() = 0 Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	0 КАК ИдентификаторСтроки,
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Сотрудник,
		|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ФизическоеЛицо,
		|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеУчетаЗатрат,
		|	ЗНАЧЕНИЕ(Справочник.ТерриторииВыполненияРабот.ПустаяСсылка) КАК МестоПолученияДохода,
		|	ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка) КАК СпособОтраженияЗарплатыВБухучете,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыДоходовИсполнительногоПроизводства.ПустаяСсылка) КАК ВидДоходаИсполнительногоПроизводства,
		|	0 КАК Сумма,
		|	ЛОЖЬ КАК ОблагаетсяЕНВД,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаНачала,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаОкончания,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ПериодДействия,
		|	ЗНАЧЕНИЕ(ПланВидовРасчета.Начисления.ПустаяСсылка) КАК Начисление,
		|	ЗНАЧЕНИЕ(Справочник.ТерриторииВыполненияРабот.ПустаяСсылка) КАК Территория
		|ПОМЕСТИТЬ ВТБухучетНачисленийВыходнаяТаблица";
		
	Иначе
		
		ВидыДоходовИсполнительногоПроизводства = УчетНачисленнойЗарплаты.ВидыДоходовИсполнительногоПроизводстваНачислений();
		БухучетНачислений.Колонки.Добавить("ВидДохода",  Новый ОписаниеТипов("ПеречислениеСсылка.ВидыДоходовИсполнительногоПроизводства"));
		Для каждого СтрокаТЗ Из БухучетНачислений Цикл
			СтрокаТЗ.ВидДохода = ВидыДоходовИсполнительногоПроизводства[СтрокаТЗ.Начисление];
		КонецЦикла;
		
		Запрос.УстановитьПараметр("БухучетНачислений", БухучетНачислений);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	БухучетНачислений.Организация КАК Организация,
		|	БухучетНачислений.Сотрудник КАК Сотрудник,
		|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БухучетНачислений.Подразделение КАК Подразделение,
		|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачислений.МестоПолученияДохода КАК МестоПолученияДохода,
		|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачислений.ВидДохода КАК ВидДоходаИсполнительногоПроизводства,
		|	БухучетНачислений.Сумма КАК Сумма,
		|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
		|	БухучетНачислений.ДатаОкончания КАК ДатаОкончания,
		|	БухучетНачислений.ПериодДействия КАК ПериодДействия,
		|	БухучетНачислений.Начисление КАК Начисление,
		|	БухучетНачислений.Территория КАК Территория
		|ПОМЕСТИТЬ ВТБухучетНачисленийВыходнаяТаблица
		|ИЗ
		|	&БухучетНачислений КАК БухучетНачислений";
		
	КонецЕсли;

	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТБухучетНачисленийВыходнаяТаблица", ИмяВТ);
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьВТБухучетВсехНачисленийДляБухучетаУдержанийНДФЛ(МенеджерВременныхТаблиц, ПараметрыБухучета) Экспорт
	
	ВидыДоходовИсполнительногоПроизводства = УчетНачисленнойЗарплаты.ВидыДоходовИсполнительногоПроизводстваНачислений();
	
	МассивТиповНачисления = Новый Массив;
	МассивТиповНачисления.Добавить(Тип("ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний"));
	МассивТиповНачисления.Добавить(Тип("ПланВидовРасчетаСсылка.Начисления"));
	ОтражениеЗарплатыВУчетеРасширенный.ДополнитьМассивТиповНачисления(МассивТиповНачисления);

	ВидыДоходовНачислений = Новый ТаблицаЗначений;
	ВидыДоходовНачислений.Колонки.Добавить("Начисление", Новый ОписаниеТипов(МассивТиповНачисления));
	ВидыДоходовНачислений.Колонки.Добавить("ВидДохода",  Новый ОписаниеТипов("ПеречислениеСсылка.ВидыДоходовИсполнительногоПроизводства"));
	
	Для Каждого Элемент Из ВидыДоходовИсполнительногоПроизводства Цикл
		ВидДоходаНачисления = ВидыДоходовНачислений.Добавить();
		ВидДоходаНачисления.Начисление = Элемент.Ключ;
		ВидДоходаНачисления.ВидДохода  = Элемент.Значение;
	КонецЦикла;
	
	ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(МенеджерВременныхТаблиц, ВидыДоходовНачислений, "ВТВидыДоходовНачислений");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БухучетНачислений.Сотрудник КАК Сотрудник,
	|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
	|	БухучетНачислений.Подразделение КАК Подразделение,
	|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	БухучетНачислений.МестоПолученияДохода КАК МестоПолученияДохода,
	|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
	|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачислений.Сумма КАК Сумма,
	|	БухучетНачислений.Начисление КАК Начисление,
	|	БухучетНачислений.Территория КАК Территория,
	|	ВидыДоходовНачислений.ВидДохода КАК ВидДоходаИсполнительногоПроизводства
	|ПОМЕСТИТЬ ВТБухучетНачисленийВыходнаяТаблица
	|ИЗ
	|	ВТБухучетНачисленийВходнаяТаблица КАК БухучетНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыДоходовНачислений КАК ВидыДоходовНачислений
	|		ПО БухучетНачислений.Начисление = ВидыДоходовНачислений.Начисление
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	БухучетНачислений.Сотрудник,
	|	БухучетНачислений.ФизическоеЛицо,
	|	БухучетНачислений.Подразделение,
	|	БухучетНачислений.ПодразделениеУчетаЗатрат,
	|	БухучетНачислений.МестоПолученияДохода,
	|	БухучетНачислений.ДатаНачала,
	|	БухучетНачислений.СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов,
	|	БухучетНачислений.Сумма,
	|	БухучетНачислений.Начисление,
	|	БухучетНачислений.Территория,
	|	ВидыДоходовНачислений.ВидДохода
	|ИЗ
	|	ВТБухучетНачисленийПоДоговорамВходнаяТаблица КАК БухучетНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыДоходовНачислений КАК ВидыДоходовНачислений
	|		ПО БухучетНачислений.Начисление = ВидыДоходовНачислений.Начисление";
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТБухучетНачисленийВходнаяТаблица", ПараметрыБухучета.ИмяВТБухучетНачислений);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТБухучетНачисленийПоДоговорамВходнаяТаблица", ПараметрыБухучета.ИмяВТБухучетНачисленийПоДоговорам);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТБухучетНачисленийВыходнаяТаблица", ПараметрыБухучета.ИмяВТБухучетВсехНачислений);
	Запрос.Выполнить();
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц,"ВТВидыДоходовНачислений")
	
КонецПроцедуры

// Выбирает начисления у которых в настройках задана
// стратегия отражения в учете - "По базовым расчетам".
//
// Возвращаемое значение: массив
//
Функция НачисленияСоСтратегиейОтраженияПоБазовымРасчетам() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.Ссылка КАК Ссылка
	|ИЗ
	|	ПланВидовРасчета.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам)";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции 

#КонецОбласти

#Область ОпределениеДоступностиДанных

Функция ДоступноЧтениеБухучетаЗарплатыТерриторийВыполненияРабот() Экспорт
	
	Возврат ПравоДоступа("Чтение", Метаданные.РегистрыСведений.БухучетЗарплатыТерриторийВыполненияРабот);
	
КонецФункции

Функция ДоступноИзменениеБухучетаЗарплатыТерриторийВыполненияРабот() Экспорт
	
	Возврат ПравоДоступа("Изменение", Метаданные.РегистрыСведений.БухучетЗарплатыТерриторийВыполненияРабот);
	
КонецФункции

Функция ДоступноИзменениеБухучетаЗарплатыПодразделений() Экспорт
	
	Возврат ПравоДоступа("Изменение", Метаданные.РегистрыСведений.БухучетЗарплатыПодразделений);
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ЗаполнитьБухучетНовойОрганизацииПриЗаписи(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Константы.ИспользоватьСтатьиФинансированияЗарплата.Получить() Тогда
		Возврат;
	КонецЕсли;
	
	РаботаВБюджетномУчреждении = Константы.РаботаВБюджетномУчреждении.Получить();
	РаботаВХозрасчетнойОрганизации = Константы.РаботаВХозрасчетнойОрганизации.Получить();
	
	ЗаписьПоУмолчанию = Неопределено;
	Если РаботаВБюджетномУчреждении И ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.УчетБюджетныхУчреждений") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("УчетБюджетныхУчреждений");
		ЗаписьПоУмолчанию = Модуль.БухучетОрганизацииПоУмолчанию();
	ИначеЕсли РаботаВХозрасчетнойОрганизации И ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.УчетХозрасчетныхОрганизаций") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("УчетХозрасчетныхОрганизаций");
		ЗаписьПоУмолчанию = Модуль.БухучетОрганизацииПоУмолчанию();	
	КонецЕсли;
	
	Если ЗаписьПоУмолчанию = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	НаборЗаписей = РегистрыСведений.БухучетЗарплатыОрганизаций.СоздатьНаборЗаписей();
	НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗапретаИзменения");
	НаборЗаписей.Отбор.Организация.Установить(Источник.Ссылка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() = 0 Тогда
		
		НоваяЗапись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ЗаписьПоУмолчанию);
		НоваяЗапись.Организация = Источник.Ссылка;
		НаборЗаписей.Записать();
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура УстановитьВидОперацииПоЗарплатеЕжегодныйОтпускПриЗаписи(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидОтпуска", Источник.Ссылка);
	Запрос.УстановитьПараметр("ОтпускЯвляетсяЕжегодным", Источник.ОтпускЯвляетсяЕжегодным);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.Ссылка,
	|	ВЫБОР
	|		КОГДА Начисления.КатегорияНачисленияИлиНеоплаченногоВремени В (ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.КомпенсацияОтпуска), ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ДенежноеСодержаниеКомпенсацияОтпуска))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК КомпенсацияОтпуска
	|ИЗ
	|	ПланВидовРасчета.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.ВидОтпуска = &ВидОтпуска
	|	И (&ОтпускЯвляетсяЕжегодным
	|				И НЕ Начисления.ВидОперацииПоЗарплате В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпуск), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпуска))
	|			ИЛИ НЕ &ОтпускЯвляетсяЕжегодным
	|				И Начисления.ВидОперацииПоЗарплате В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ЕжегодныйОтпуск), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпуска)))";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Источник.ОтпускЯвляетсяЕжегодным Тогда
			ВидОперации = ?(Выборка.КомпенсацияОтпуска,Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпуска,Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпуск);
		Иначе
			ВидОперации = Перечисления.ВидыОперацийПоЗарплате.НачисленоДоход;
		КонецЕсли;
		ВидРасчетаОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ВидРасчетаОбъект.ВидОперацииПоЗарплате = ВидОперации;
		ВидРасчетаОбъект.ОбменДанными.Загрузка = Истина;
		ВидРасчетаОбъект.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура НастройкиСистемыНалогообложенияПриЗаписи(Источник, Отказ, Замещение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	УстановитьИспользованиеЕНВД();
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаРаспределенияПоИсточникамФинансирования

// Выполняет проверку заполнения полей в результатах распределения по статьям, если есть ошибки заполнения,
// значению параметра Отказ буден установлено Истина.
// Вызывается из обработчиков проверки заполнения объектов.
//
// Параметры
// 			ДокументОбъект - ДокументОбъект - документ, табличные части которого проверяются.
// 			ИменаТаблиц - Строка - имена табличных частей документа для проверки, например "Начисления,НачисленияПерерасчет,Удержания,НДФЛ".
// 			Отказ - Булево - если есть ошибки заполнение, параметру будет установлено значение Истина.
// 			ВидНачисленияВШапке - ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний, 
// 								ПланВидовРасчетаСсылка.Удержания, ПланВидовРасчетаСсылка.Начисления,
// 								СправочникСсылка.ВидыВыплатБывшимСотрудникам,
// 								СправочникСсылка.ВидыПрочихДоходовФизическихЛиц - необязательный.
//
Процедура ПроверитьРезультатыРаспределенияНачисленийУдержанийОбъекта(ДокументОбъект, ИменаТаблиц, Отказ, ВидНачисленияВШапке = Неопределено) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата") Тогда
		Возврат;
	КонецЕсли;
	
	НачислениеУдержаниеВидОперации = ОтражениеЗарплатыВУчете.НачислениеУдержаниеВидОперации();
	
	ТаблицаРезультатовПроверки = Новый ТаблицаЗначений;
	ТаблицаРезультатовПроверки.Колонки.Добавить("ИмяТаблицы",  Новый ОписаниеТипов("Строка"));
	ТаблицаРезультатовПроверки.Колонки.Добавить("НомерСтроки",  Новый ОписаниеТипов("Число"));
	ТаблицаРезультатовПроверки.Колонки.Добавить("ЕстьОшибкиЗаполнения",  Новый ОписаниеТипов("Булево"));
	ТаблицаРезультатовПроверки.Колонки.Добавить("ЕстьОшибкиРаспределения",  Новый ОписаниеТипов("Булево"));
	
	РаботаВБюджетномУчреждении = ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении");
	
	ИменаТаблицРаспределенияРезультатов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИменаТаблиц, ",", Истина, Истина);
	
	Для Каждого ИмяТаблицыНачисленийУдержаний Из ИменаТаблицРаспределенияРезультатов Цикл
		
		ИмяТаблицыРаспределениеРезультатовРасчета = ИмяТаблицыДляРаспределенияРезультата(ИмяТаблицыНачисленийУдержаний);
		ТаблицаРаспределенияРезультатовИзХранилища = ДокументОбъект[ИмяТаблицыРаспределениеРезультатовРасчета];
		
		ИмяРеквизитаИдентификаторСтроки = ИмяРеквизитаИдентификаторСтрокиПоИмениТаблицы(ИмяТаблицыНачисленийУдержаний);
		
		Для каждого СтрокаТЧ Из ДокументОбъект[ИмяТаблицыНачисленийУдержаний] Цикл
			
			Отбор = Новый Структура("ИдентификаторСтроки", СтрокаТЧ[ИмяРеквизитаИдентификаторСтроки]);
			РезультатыРаспределения = ТаблицаРаспределенияРезультатовИзХранилища.НайтиСтроки(Отбор);
			
			РезультатПроверкиСтроки = ПроверитьРезультатыРаспределенияНачисленийУдержанийВСтрокеОбъекта(
				СтрокаТЧ, РезультатыРаспределения, ИмяТаблицыНачисленийУдержаний, НачислениеУдержаниеВидОперации, РаботаВБюджетномУчреждении, ВидНачисленияВШапке);
			
			Если РезультатПроверкиСтроки.ЕстьОшибкиЗаполнения Или РезультатПроверкиСтроки.ЕстьОшибкиРаспределения Тогда
				
				НоваяСтрока = ТаблицаРезультатовПроверки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, РезультатПроверкиСтроки);
				НоваяСтрока.НомерСтроки = СтрокаТЧ.НомерСтроки;
				НоваяСтрока.ИмяТаблицы  = ИмяТаблицыНачисленийУдержаний;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если ТаблицаРезультатовПроверки.Количество() > 0 Тогда
		
		МаксимальноСообщений = 10;
		ВыводимыхСообщений = 0;
		НевыводимыхСообщений = 0;
		
		МетаданныеДокумента = ДокументОбъект.Ссылка.Метаданные();
		ПредставленияТаблиц = Новый Соответствие;
		
		Для каждого СтрокаТаблицы Из ТаблицаРезультатовПроверки Цикл
			
			Отказ = Истина;
			
			ПредставлениеТаблицы = ПредставленияТаблиц.Получить(СтрокаТаблицы.ИмяТаблицы);
			Если ПредставлениеТаблицы = Неопределено Тогда
				
				ПредставлениеТаблицы = МетаданныеДокумента.ТабличныеЧасти.Найти(СтрокаТаблицы.ИмяТаблицы).Синоним;
				ПредставленияТаблиц.Вставить(СтрокаТаблицы.ИмяТаблицы, ПредставлениеТаблицы);
				
			КонецЕсли;
			
			Если ВыводимыхСообщений > МаксимальноСообщений Тогда
				НевыводимыхСообщений = НевыводимыхСообщений + 1;
			Иначе
				
				ОбщегоНазначения.СообщитьПользователю(
					СтрШаблон(
						НСтр("ru = 'В строке %1 таблицы %2 обнаружены незаполненные поля при распределении результата';
							|en = 'In row %1 of table %2, unpopulated fields were found when allocating the result'"),
						СтрокаТаблицы.НомерСтроки,
						ПредставлениеТаблицы),, 
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
						СтрШаблон("Объект.%1", СтрокаТаблицы.ИмяТаблицы),
						СтрокаТаблицы.НомерСтроки,
						"КомандаРедактированияРаспределения"),, 
					Отказ);
				ВыводимыхСообщений = ВыводимыхСообщений + 1;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Область ЗначенияСтатейРасходовПоУмолчанию

// Возвращает ссылку на элемент справочника СтатьиРасходовЗарплата
//	соответствующий способу расчетов ОплатаТруда.
Функция СтатьяОплатаТруда() Экспорт 
	
	Если ПолучитьФункциональнуюОпцию("РаботаВХозрасчетнойОрганизации") Тогда
		ОписаниеСтатейРасходов = ЗарплатаКадры.СтатьиРасходовПоСпособамРасчетовСФизическимиЛицами();
		ОплатаТруда = ОписаниеСтатейРасходов[Перечисления.СпособыРасчетовСФизическимиЛицами.ОплатаТруда];
	Иначе
		ОплатаТруда = СтатьяРасходов211();
	КонецЕсли;
	
	Возврат ОплатаТруда;
	
КонецФункции

// Возвращает ссылку на элемент справочника СтатьиРасходовЗарплата с кодом 211.
Функция СтатьяРасходов211() Экспорт
	
	Статья211 = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	Если ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Статья211 = Справочники.СтатьиРасходовЗарплата.НайтиПоКоду("211");
	КонецЕсли;

	Возврат Статья211;
	
КонецФункции

// Возвращает ссылку на элемент справочника СтатьиРасходовЗарплата с кодом 212.
Функция СтатьяРасходов212() Экспорт
	
	Статья = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	Если ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Статья = Справочники.СтатьиРасходовЗарплата.НайтиПоКоду("212");
	КонецЕсли;

	Возврат Статья;
	
КонецФункции

// Возвращает ссылку на элемент справочника СтатьиРасходовЗарплата с кодом 213.
Функция СтатьяРасходов213() Экспорт
	
	Статья213 = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	Если ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Статья213 = Справочники.СтатьиРасходовЗарплата.НайтиПоКоду("213");
	КонецЕсли;
	
	Возврат Статья213;
	
КонецФункции 

// Возвращает ссылку на элемент справочника СтатьиРасходовЗарплата с кодом 266.
Функция СтатьяРасходов266() Экспорт
	
	Статья = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	Если ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Статья = Справочники.СтатьиРасходовЗарплата.НайтиПоКоду("266");
	КонецЕсли;

	Возврат Статья;
	
КонецФункции

// Возвращает ссылку на элемент справочника СтатьиРасходовЗарплата с кодом 264.
Функция СтатьяРасходов264() Экспорт
	
	Статья = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	Если ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Статья = Справочники.СтатьиРасходовЗарплата.НайтиПоКоду("264");
	КонецЕсли;

	Возврат Статья;
	
КонецФункции

// Возвращает статью расходов, которая используется по умолчанию для 
//	сохраняемого заработка на период трудоустройства.
//
// Параметры:
//  ПериодРегистрации - дата, на которую определяется статья.
//
// Возвращаемое значение:
//   Статья расходов - тип СправочникСсылка.СтатьиРасходовЗарплата.
//
Функция СтатьяРасходовДляСохраняемогоЗаработкаНаПериодТрудоустройства(ПериодРегистрации) Экспорт

	Если СтатьиРасходовПоПриказу209нПрименяются(ПериодРегистрации) Тогда
		Возврат СтатьяРасходов264();
	Иначе
		Возврат СтатьяРасходов211();
	КонецЕсли;
	
КонецФункции

// Возвращает статью расходов, которая используется по умолчанию для 
//	компенсации за задержку зарплаты в бюджетных учреждениях.
//
// Параметры:
//  ПериодРегистрации - дата, на которую определяется статья.
//
// Возвращаемое значение:
//   Статья расходов - тип СправочникСсылка.СтатьиРасходовЗарплата.
//
Функция СтатьяРасходовДляКомпенсацииЗаЗадержкуЗарплаты(ПериодРегистрации) Экспорт

	Если Не ЗначениеЗаполнено(ПериодРегистрации) Или ПериодРегистрации >= ДатаПриказа222н() Тогда
		Возврат СтатьяРасходов296();
	Иначе
		Возврат СтатьяРасходов290();
	КонецЕсли;
	
КонецФункции

// Возвращает статью расходов, которая используется по умолчанию для 
//	пособия на погребение, выплачиваемое стороннему лицу.
//
// Параметры:
//  ПериодРегистрации - дата, на которую определяется статья.
//
// Возвращаемое значение:
//   Статья расходов - тип СправочникСсылка.СтатьиРасходовЗарплата.
//
Функция СтатьяРасходовДляПособияНаПогребение(ПериодРегистрации) Экспорт

	Если Не ЗначениеЗаполнено(ПериодРегистрации) Или ПериодРегистрации >= ДатаПриказа222н() Тогда
		Возврат СтатьяРасходов265();
	Иначе
		Возврат СтатьяРасходов213();
	КонецЕсли;	

КонецФункции

#КонецОбласти

// Создает временную таблицу ВТФинансированиеПоДокументам.
// Используется при обновлении учета НДФЛ.
//
//	Параметры
//		МенеджерВременныхТаблиц, менеджер временных таблиц, содержит ВТ ВТДокументыКОбработке с полями
//			* Регистратор
//		ТипыДокументов, массив, содержит типы документов к обработке.
//
//	Временная таблица ВТФинансированиеПоДокументам содержит поля
//			* Регистратор
//			* СтатьяФинансирования
//			* СтатьяРасходов
//
Процедура СоздатьВТФинансированиеПоДокументам(МенеджерВременныхТаблиц, ТипыДокументов) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		Возврат;
	КонецЕсли;
	
	ТипыДокументовСФинансированием = Новый Массив;
	Для каждого ТипДокумента Из ТипыДокументов Цикл
		МдДокумент = Метаданные.НайтиПоТипу(ТипДокумента);
		Если МдДокумент.Реквизиты.Найти("СтатьяФинансирования") <> Неопределено
			И МдДокумент.Реквизиты.Найти("СтатьяРасходов") <> Неопределено Тогда
			ТипыДокументовСФинансированием.Добавить(ТипДокумента);
		КонецЕсли;
	КонецЦикла;
	
	Если ТипыДокументовСФинансированием.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТипыДокументов", ТипыДокументовСФинансированием);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДокументыКОбработке.Регистратор КАК Регистратор,
	|	ДокументыКОбработке.Регистратор.СтатьяФинансирования КАК СтатьяФинансирования,
	|	ДокументыКОбработке.Регистратор.СтатьяРасходов КАК СтатьяРасходов
	|ПОМЕСТИТЬ ВТФинансированиеПоДокументам
	|ИЗ
	|	ВТДокументыКОбработке КАК ДокументыКОбработке
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДокументыКОбработке.Регистратор) В (&ТипыДокументов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПодтверждениеВыплатыДоходов.Ссылка,
	|	ПодтверждениеВыплатыДоходов.Ведомость.СтатьяФинансирования,
	|	ПодтверждениеВыплатыДоходов.Ведомость.СтатьяРасходов
	|ИЗ
	|	Документ.ПодтверждениеВыплатыДоходов КАК ПодтверждениеВыплатыДоходов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыКОбработке КАК ДокументыКОбработке
	|		ПО ПодтверждениеВыплатыДоходов.Ссылка = ДокументыКОбработке.Регистратор";
	Запрос.Выполнить();

КонецПроцедуры

// Возвращает структуру с описанием исходных данных для отражения начислений контрагентов в бухучете.
//
// Возвращаемое значение:
//  Структура - Ключ содержит имя параметра.
//
Функция ОписаниеИсходныхДанныхДляОтраженияНачисленийКонтрагентовВБухучете() Экспорт

	Параметры = Новый Структура(
	"Организация,
	|МесяцНачисления,
	|Подразделение,
	|Начисление,
	|БухучетПервичногоДокумента,
	|КоэффициентыРаспределения,
	|ТаблицаНачислений");
	
	Параметры.БухучетПервичногоДокумента = НоваяТаблицаБухучетЗарплатыПервичныхДокументов();
	
	Возврат Параметры;
	
КонецФункции

// Выполняет отражение начислений контрагентов в бухучете.
//
// Параметры:
//   ИсходныеДанные - структура, см ОписаниеИсходныхДанныхДляОтраженияНачисленийКонтрагентовВБухучете
//
// Возвращаемое значение:
//  ТаблицаЗначений - таблица значений с бухучетом начислений,
//		описание таблицы см НоваяТаблицаРаспределениеРезультатовНачислений().
//
Функция ОтражениеНачисленийКонтрагентовВБухучете(ИсходныеДанные) Экспорт

	Организация					= ИсходныеДанные.Организация;
	МесяцНачисления				= ИсходныеДанные.МесяцНачисления;
	Подразделение				= ИсходныеДанные.Подразделение;
	Начисление 					= ИсходныеДанные.Начисление;
	БухучетПервичногоДокумента	= ИсходныеДанные.БухучетПервичногоДокумента;
	КоэффициентыРаспределения 	= ИсходныеДанные.КоэффициентыРаспределения;
	ТаблицаНачислений 			= ИсходныеДанные.ТаблицаНачислений;
	
	РаботаВХозрасчетнойОрганизации = ПолучитьФункциональнуюОпцию("РаботаВХозрасчетнойОрганизации");
	ИспользоватьСтатьиФинансирования = ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный");
	ЕстьЕНВД = ОтражениеЗарплатыВБухучете.ПлательщикЕНВД(Организация, МесяцНачисления);
	
	УдалитьВТ = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// Получение настроек по умолчанию.
	Запрос.УстановитьПараметр("МесяцНачисления", МесяцНачисления);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	&МесяцНачисления КАК Период,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Сотрудник,
	|	ЗНАЧЕНИЕ(Справочник.ТерриторииВыполненияРабот.ПустаяСсылка) КАК ТерриторияВыполненияРаботВОрганизации
	|ПОМЕСТИТЬ ВТНачисленияДляРаспределения";
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТНачисленияДляРаспределения");
	
	СоздатьВТСведенияОБухучетеЗарплатыСотрудников(Запрос.МенеджерВременныхТаблиц, "ВТНачисленияДляРаспределения");
	УдалитьВТ.Добавить("ВТСведенияОБухучетеЗарплатыСотрудников");
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СведенияОБухучетеЗарплатыСотрудников.СтатьяФинансирования КАК СтатьяФинансирования,
	|	СведенияОБухучетеЗарплатыСотрудников.СтатьяРасходов КАК СтатьяРасходов,
	|	СведенияОБухучетеЗарплатыСотрудников.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА СведенияОБухучетеЗарплатыСотрудников.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОблагаетсяЕНВД
	|ИЗ
	|	ВТСведенияОБухучетеЗарплатыСотрудников КАК СведенияОБухучетеЗарплатыСотрудников";
	НастройкиБухучетаПоУмолчанию = Запрос.Выполнить().Выгрузить();
	
	ПоляБухучета = "СпособОтраженияЗарплатыВБухучете,СтатьяРасходов";
	Если ИспользоватьСтатьиФинансирования Тогда
		ПоляБухучета = ПоляБухучета + ",СтатьяФинансирования";
	КонецЕсли;
	Если ЕстьЕНВД Тогда
		ПоляБухучета = ПоляБухучета + ",ОблагаетсяЕНВД";
	КонецЕсли;
	БухучетПоУмолчанию = Новый Структура(ПоляБухучета);
	Если НастройкиБухучетаПоУмолчанию.Количество() > 0 Тогда
		ЗаполнитьЗначенияСвойств(БухучетПоУмолчанию, НастройкиБухучетаПоУмолчанию[0]);
	КонецЕсли;
	
	// Статья расходов по умолчанию, если РаботаВХозрасчетнойОрганизации передается из документа иначе определяется видом начисления.
	СтатьяРасходовПоУмолчанию = Неопределено;
	Если Не РаботаВХозрасчетнойОрганизации Тогда
		
		ПрямыеВыплатыПособий = ПрямыеВыплатыПособийСоциальногоСтрахования.ПособиеПлатитУчастникПилотногоПроекта(Организация, МесяцНачисления);
		Запрос.УстановитьПараметр("ПрямыеВыплатыПособий", ПрямыеВыплатыПособий);
		СоздатьВТСтатьиРасходовНачисленийПоУмолчанию(Запрос, Истина, МесяцНачисления);
		УдалитьВТ.Добавить("ВТСтатьиРасходовНачисленийПоУмолчанию");
		Запрос.УстановитьПараметр("Начисление", Начисление);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтатьиРасходовНачисленийПоУмолчанию.Начисление КАК Начисление,
		|	СтатьиРасходовНачисленийПоУмолчанию.СтатьяРасходов КАК СтатьяРасходов
		|ИЗ
		|	ВТСтатьиРасходовНачисленийПоУмолчанию КАК СтатьиРасходовНачисленийПоУмолчанию
		|ГДЕ
		|	СтатьиРасходовНачисленийПоУмолчанию.Начисление = &Начисление";
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СтатьяРасходовПоУмолчанию = Выборка.СтатьяРасходов;
		КонецЕсли;
	КонецЕсли;
	
	// Настройки бухучета указанные в документе.
	КолонкиБухучетаДокумента = "";
	НастройкиБухучетДокумента = Новый Структура(ПоляБухучета);
	НайденныеСтроки = БухучетПервичногоДокумента.НайтиСтроки(Новый Структура("НачислениеУдержание", Начисление));
	Если НайденныеСтроки.Количество() > 0 Тогда
		
		КолонкиБухучет = Новый Массив;
		Если ИспользоватьСтатьиФинансирования И ЗначениеЗаполнено(НайденныеСтроки[0].СтатьяФинансирования) Тогда
			КолонкиБухучет.Добавить("СтатьяФинансирования");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НайденныеСтроки[0].СпособОтраженияЗарплатыВБухучете) Тогда
			КолонкиБухучет.Добавить("СпособОтраженияЗарплатыВБухучете");
		КонецЕсли;
		
		Если ЕстьЕНВД И ЗначениеЗаполнено(НайденныеСтроки[0].ОтношениеКЕНВД) Тогда
			НастройкиБухучетДокумента.ОблагаетсяЕНВД = (НайденныеСтроки[0].ОтношениеКЕНВД = Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НайденныеСтроки[0].СтатьяРасходов) Тогда
			Если РаботаВХозрасчетнойОрганизации Тогда
				СтатьяРасходовПоУмолчанию = НайденныеСтроки[0].СтатьяРасходов;
			Иначе
				КолонкиБухучет.Добавить("СтатьяРасходов");
			КонецЕсли;
		КонецЕсли;
		
		КолонкиБухучетаДокумента = СтрСоединить(КолонкиБухучет, ",");
		
		Если Не ПустаяСтрока(КолонкиБухучетаДокумента) Тогда
			ЗаполнитьЗначенияСвойств(НастройкиБухучетДокумента, НайденныеСтроки[0], КолонкиБухучетаДокумента);
		КонецЕсли;
		
	КонецЕсли;
	
	ТаблицаБухучетНачислений = НоваяТаблицаРаспределениеРезультатовНачислений();
	РаспределятьПоКоэффициентам = (КоэффициентыРаспределения <> Неопределено И КоэффициентыРаспределения.Количество() > 0);
	Если РаспределятьПоКоэффициентам Тогда
		ПоляКоэффициентов = ПоляБухучета + ",ФизическоеЛицо,ПодразделениеУчетаЗатрат";
		КоэффициентыРаспределения.Свернуть(ПоляКоэффициентов, "Коэффициент");
	КонецЕсли;
	
	ОтборКоэффициентов = Новый Структура("ФизическоеЛицо");
	
	Для каждого СтрокаТЗ Из ТаблицаНачислений Цикл
		
		НоваяСтрокаБухучета = ТаблицаБухучетНачислений.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаБухучета, СтрокаТЗ);
		НоваяСтрокаБухучета.Результат = СтрокаТЗ.Сумма;
		
		// Настройки бухучета по умолчанию.
		ЗаполнитьЗначенияСвойств(НоваяСтрокаБухучета, БухучетПоУмолчанию);
		
		Если ЗначениеЗаполнено(СтатьяРасходовПоУмолчанию) Тогда
			НоваяСтрокаБухучета.СтатьяРасходов = СтатьяРасходовПоУмолчанию;
		КонецЕсли;
		
		Если Не ПустаяСтрока(КолонкиБухучетаДокумента) Тогда
			// Задан бухучет в документе.
			ЗаполнитьЗначенияСвойств(НоваяСтрокаБухучета, НастройкиБухучетДокумента, КолонкиБухучетаДокумента);
			
		ИначеЕсли СтрокаТЗ.Сумма <> 0 И РаспределятьПоКоэффициентам Тогда
			
			ЗаполнитьЗначенияСвойств(ОтборКоэффициентов, СтрокаТЗ);
			СтрокиРаспределения = КоэффициентыРаспределения.НайтиСтроки(ОтборКоэффициентов);
			
			Коэффициенты = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиРаспределения,"Коэффициент");
			Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СтрокаТЗ.Сумма, Коэффициенты);
			
			Если Результаты <> Неопределено Тогда
				
				Индекс = 0;
				Для Каждого СтрокаРаспределения Из СтрокиРаспределения Цикл
					
					СтрокаБухучета = ТаблицаБухучетНачислений.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаБухучета, НоваяСтрокаБухучета);
					
					СтрокаБухучета.Результат = Результаты[Индекс];
					
					Если ИспользоватьСтатьиФинансирования И ЗначениеЗаполнено(СтрокаРаспределения.СтатьяФинансирования) Тогда
						СтрокаБухучета.СтатьяФинансирования = СтрокаРаспределения.СтатьяФинансирования;
					КонецЕсли;
					
					Если ЗначениеЗаполнено(СтрокаРаспределения.СпособОтраженияЗарплатыВБухучете) Тогда
						СтрокаБухучета.СпособОтраженияЗарплатыВБухучете = СтрокаРаспределения.СпособОтраженияЗарплатыВБухучете;
					КонецЕсли;
					
					Если Не РаботаВХозрасчетнойОрганизации И Не ЗначениеЗаполнено(СтатьяРасходовПоУмолчанию) И ЗначениеЗаполнено(СтрокаРаспределения.СтатьяРасходов) Тогда
						// Заменяем статью расходов если не задана статья по умолчанию.
						СтрокаБухучета.СтатьяРасходов = СтрокаРаспределения.СтатьяРасходов;
					КонецЕсли;
					
					Если ЕстьЕНВД  Тогда
						СтрокаБухучета.ОблагаетсяЕНВД = СтрокаРаспределения.ОблагаетсяЕНВД;
					КонецЕсли;
					
					СтрокаБухучета.ПодразделениеУчетаЗатрат = СтрокаРаспределения.ПодразделениеУчетаЗатрат;
					
					Индекс = Индекс + 1;
					
				КонецЦикла;
				
				ТаблицаБухучетНачислений.Удалить(НоваяСтрокаБухучета);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ИспользоватьСтатьиФинансирования Тогда
		КодыСтатейФинансирования = КодыСтатейФинансирования();
		Для каждого СтрокаТЗ Из ТаблицаБухучетНачислений Цикл
			СтрокаТЗ.КодСтатьиФинансирования = КодыСтатейФинансирования[СтрокаТЗ.СтатьяФинансирования];
		КонецЦикла;
	КонецЕсли;
	
	ОтражениеЗарплатыВУчете.СвернутьТаблицу(ТаблицаБухучетНачислений);
	
	Возврат ТаблицаБухучетНачислений;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область  СоздатьВТБухучетНачислений

Функция АктуальныеСтатьиФинансирования(Запрос)
	
	СоответствиеСтатьей = Новый Соответствие;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		Возврат СоответствиеСтатьей;
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатьиФинансированияЗарплата.Ссылка КАК СтатьяФинансирования,
	|	ЕСТЬNULL(АктуальнаяСтатья.СтатьяФинансирования, СтатьиФинансированияЗарплата.Ссылка) КАК АктуальнаяСтатьяФинансирования
	|ИЗ
	|	Справочник.СтатьиФинансированияЗарплата КАК СтатьиФинансированияЗарплата
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатьяФинансированияДляЗаменыУстаревшейАналитикиВБухучете.СрезПоследних(&ПериодРегистрации, Организация = &Организация) КАК АктуальнаяСтатья
	|		ПО (СтатьиФинансированияЗарплата.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1))
	|			И (СтатьиФинансированияЗарплата.ДатаОкончания < &ПериодРегистрации)";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СоответствиеСтатьей.Вставить(Выборка.СтатьяФинансирования, Выборка.АктуальнаяСтатьяФинансирования);
	КонецЦикла;
	
	Возврат СоответствиеСтатьей;

КонецФункции

Функция АктуальныеСпособыОтражения(Запрос)
	
	СоответствиеСпособовОтражения = Новый Соответствие;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		Возврат СоответствиеСпособовОтражения;
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СпособыОтражения.Ссылка КАК СпособОтраженияЗарплатыВБухучете,
	|	ЕСТЬNULL(АктуальныйСпособОтражения.СпособОтраженияЗарплатыВБухучете, СпособыОтражения.Ссылка) КАК АктуальныйСпособОтраженияЗарплатыВБухучете
	|ИЗ
	|	Справочник.СпособыОтраженияЗарплатыВБухУчете КАК СпособыОтражения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СпособОтраженияДляЗаменыУстаревшейАналитикиВБухучете.СрезПоследних(&ПериодРегистрации, Организация = &Организация) КАК АктуальныйСпособОтражения
	|		ПО (СпособыОтражения.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1))
	|			И (СпособыОтражения.ДатаОкончания < &ПериодРегистрации)";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СоответствиеСпособовОтражения.Вставить(Выборка.СпособОтраженияЗарплатыВБухучете, Выборка.АктуальныйСпособОтраженияЗарплатыВБухучете);
	КонецЦикла;
	
	Возврат СоответствиеСпособовОтражения;

КонецФункции

Функция РазрешенныеКатегорииНачисленийСтатьиФинансирования()

	РазрешенныеКатегории = Новый Соответствие;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КатегорииНачислений.Ссылка КАК Ссылка,
	|	КатегорииНачислений.КатегорияНачисления КАК КатегорияНачисления
	|ИЗ
	|	Справочник.СтатьиФинансированияЗарплата.КатегорииНачислений КАК КатегорииНачислений
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Ссылка") Цикл
		КатегорииНачислений = Новый Массив;
		Пока Выборка.Следующий() Цикл
			КатегорииНачислений.Добавить(Выборка.КатегорияНачисления);
		КонецЦикла;
		РазрешенныеКатегории.Вставить(Выборка.Ссылка, КатегорииНачислений);
	КонецЦикла;
	
	Возврат РазрешенныеКатегории;

КонецФункции

Функция СтатьиФинансированияЗамены(Запрос)

	СтатьиДляЗамены = Новый Соответствие;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатьиФинансирования.СтатьяФинансирования КАК СтатьяФинансирования,
	|	СтатьиФинансирования.СтатьяФинансированияДляЗамены КАК СтатьяФинансированияДляЗамены
	|ИЗ
	|	РегистрСведений.СтатьиФинансированияБазыСреднегоЗаработка.СрезПоследних(&ПериодРегистрации, ) КАК СтатьиФинансирования";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтатьиДляЗамены.Вставить(Выборка.СтатьяФинансирования, Выборка.СтатьяФинансированияДляЗамены);
	КонецЦикла;
	
	Возврат СтатьиДляЗамены;

КонецФункции

Функция СтатьяИспользуетсяВБазеСреднего(Запрос)

	НастройкиСтатей = Новый Соответствие;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатьиФинансирования.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА СтатьиФинансирования.ИспользованиеВБазеСреднегоЗаработка = ЗНАЧЕНИЕ(Перечисление.ИспользованиеСтатьиФинансированияВБазеСреднегоЗаработка.Используется)
	|				ИЛИ СтатьиФинансирования.ИспользованиеВБазеСреднегоЗаработка = ЗНАЧЕНИЕ(Перечисление.ИспользованиеСтатьиФинансированияВБазеСреднегоЗаработка.ПустаяСсылка)
	|				ИЛИ СтатьиФинансированияБазыСреднего.СтатьяФинансированияДляЗамены ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Используется
	|ИЗ
	|	Справочник.СтатьиФинансированияЗарплата КАК СтатьиФинансирования
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатьиФинансированияБазыСреднегоЗаработка.СрезПоследних(&ПериодРегистрации, ) КАК СтатьиФинансированияБазыСреднего
	|		ПО СтатьиФинансирования.Ссылка = СтатьиФинансированияБазыСреднего.СтатьяФинансирования";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НастройкиСтатей.Вставить(Выборка.Ссылка, Выборка.Используется);
	КонецЦикла;
	
	Возврат НастройкиСтатей;

КонецФункции

Процедура СоздатьПустуюВТБухучетНачислений(Запрос, ИмяВТ)
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 0
	|	0 КАК ИдентификаторСтроки,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ПериодРегистрации,
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Сотрудник,
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ФизическоеЛицо,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеУчетаЗатрат,
	|	ЗНАЧЕНИЕ(Справочник.ТерриторииВыполненияРабот.ПустаяСсылка) КАК ТерриторияВыполненияРаботВОрганизации,
	|	ЗНАЧЕНИЕ(Справочник.ТерриторииВыполненияРабот.ПустаяСсылка) КАК Территория,
	|	ЗНАЧЕНИЕ(ПланВидовРасчета.Начисления.ПустаяСсылка) КАК Начисление,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ПустаяСсылка) КАК ВидОперации,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаНачала,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаОкончания,
	|	НЕОПРЕДЕЛЕНО КАК ДокументОснование,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
	|	ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка) КАК СпособОтраженияЗарплатыВБухучете,
	|	ЛОЖЬ КАК ОблагаетсяЕНВД,
	|	0 КАК Сумма
	|ПОМЕСТИТЬ ВТБухучетНачисленийПустая";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТБухучетНачисленийПустая", ИмяВТ);
	Запрос.Выполнить();

КонецПроцедуры

Процедура СоздатьВТНастройкиБухучетаСотрудников(Запрос, ИспользоватьОбособленныеТерритории, ИмяВТНачисленияИсходная, РаспределениеСохраняемогоДС)

	Если РаспределениеСохраняемогоДС Тогда
		ИменаПолейВременнойТаблицы = "ВременныйСотрудник,ДатаНачалаДляБухучета";
	Иначе
		ИменаПолейВременнойТаблицы = "Сотрудник,ДатаНачала";
	КонецЕсли;
	
	СоздатьВТСведенияОБухучетеЗарплатыСотрудников(Запрос.МенеджерВременныхТаблиц, ИмяВТНачисленияИсходная, ИменаПолейВременнойТаблицы);
	СоздатьВТСведенияОБухучетеНачислений(Запрос.МенеджерВременныхТаблиц);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НастройкиНачислений.ЭтоРасходыФСС, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|		КОГДА БухучетНачислений.Начисление ЕСТЬ NULL
	|			ТОГДА БухучетСотрудников.СпособОтраженияЗарплатыВБухучете
	|		КОГДА БухучетНачислений.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка)
	|			ТОГДА БухучетСотрудников.СпособОтраженияЗарплатыВБухучете
	|		ИНАЧЕ БухучетНачислений.СпособОтраженияЗарплатыВБухучете
	|	КОНЕЦ КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьСтатьиФинансирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|		КОГДА БухучетНачислений.Начисление ЕСТЬ NULL
	|			ТОГДА БухучетСотрудников.СтатьяФинансирования
	|		КОГДА БухучетНачислений.СтатьяФинансирования = ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|			ТОГДА БухучетСотрудников.СтатьяФинансирования
	|		ИНАЧЕ БухучетНачислений.СтатьяФинансирования
	|	КОНЕЦ КАК СтатьяФинансирования,
	|	ВЫБОР
	|		КОГДА НЕ &РаботаВБюджетномУчреждении
	|			ТОГДА ЕСТЬNULL(НастройкиНачислений.СтатьяРасчетыПоОплатеТруда, &СтатьяРасчетыПоОплатеТруда)
	|		КОГДА ЕСТЬNULL(НастройкиНачислений.ЭтоОплатаДнейУходаЗаДетьмиИнвалидами, ЛОЖЬ)
	|			ТОГДА ЕСТЬNULL(СтатьиРасходовНачисленийПоУмолчанию.СтатьяРасходов, &Статья213)
	|		КОГДА ЕСТЬNULL(НастройкиНачислений.ЭтоРасходыФСС, ЛОЖЬ)
	|			ТОГДА &Статья213
	|		КОГДА БухучетНачислений.Начисление ЕСТЬ NULL
	|			ТОГДА ЕСТЬNULL(СтатьиРасходовНачисленийПоУмолчанию.СтатьяРасходов, БухучетСотрудников.СтатьяРасходов)
	|		КОГДА БухучетНачислений.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(СтатьиРасходовНачисленийПоУмолчанию.СтатьяРасходов, БухучетСотрудников.СтатьяРасходов)
	|		ИНАЧЕ БухучетНачислений.СтатьяРасходов
	|	КОНЕЦ КАК СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА &ЕстьЕНВД
	|			ТОГДА ВЫБОР
	|					КОГДА БухучетНачислений.Начисление ЕСТЬ NULL
	|						ТОГДА БухучетСотрудников.ОтношениеКЕНВД
	|					КОГДА БухучетНачислений.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
	|						ТОГДА БухучетСотрудников.ОтношениеКЕНВД
	|					ИНАЧЕ БухучетНачислений.ОтношениеКЕНВД
	|				КОНЕЦ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД)
	|	КОНЕЦ КАК ОтношениеКЕНВД,
	|	БухучетСотрудников.ЕстьБухучетОрганизации КАК ЕстьБухучетОрганизации,
	|	БухучетСотрудников.ЕстьБухучетПодразделения КАК ЕстьБухучетПодразделения,
	|	БухучетСотрудников.ЕстьБухучетТерритории КАК ЕстьБухучетТерритории,
	|	ВЫБОР
	|		КОГДА БухучетСотрудников.ЕстьБухучетСотрудника
	|			ТОГДА ИСТИНА
	|		КОГДА БухучетСотрудников.ЕстьБухучетТерритории
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьБухучетСотрудникаТерритории
	|ПОМЕСТИТЬ ВТНастройкиБухучетаСотрудников
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСведенияОБухучетеЗарплатыСотрудников КАК БухучетСотрудников
	|		ПО Начисления.Сотрудник = БухучетСотрудников.Сотрудник
	|			И Начисления.Подразделение = БухучетСотрудников.Подразделение
	|			И Начисления.ТерриторияВыполненияРаботВОрганизации = БухучетСотрудников.ТерриторияВыполненияРаботВОрганизации
	|			И Начисления.ДатаНачала = БухучетСотрудников.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСведенияОБухучетеНачислений КАК БухучетНачислений
	|		ПО Начисления.Начисление = БухучетНачислений.Начисление
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатьиРасходовНачисленийПоУмолчанию КАК СтатьиРасходовНачисленийПоУмолчанию
	|		ПО Начисления.Начисление = СтатьиРасходовНачисленийПоУмолчанию.Начисление
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|		ПО Начисления.Начисление = НастройкиНачислений.Начисление";
	Если НЕ ИспользоватьОбособленныеТерритории Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И Начисления.ТерриторияВыполненияРаботВОрганизации = БухучетСотрудников.ТерриторияВыполненияРаботВОрганизации", "");
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	Если РаспределениеСохраняемогоДС Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПО Начисления.Сотрудник = БухучетСотрудников.Сотрудник", "ПО Начисления.ВременныйСотрудник = БухучетСотрудников.Сотрудник");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И Начисления.ДатаНачала = БухучетСотрудников.Период", "И Начисления.ДатаНачалаДляБухучета = БухучетСотрудников.Период");
	КонецЕсли;
	Запрос.Выполнить();
	
	Запрос.Текст =
	"УНИЧТОЖИТЬ ВТСведенияОБухучетеЗарплатыСотрудников
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСведенияОБухучетеНачислений";
	Запрос.Выполнить();	

КонецПроцедуры

Процедура СоздатьВТСтатьиРасходовНачисленийПоУмолчанию(Запрос, РаботаВБюджетномУчреждении, ПериодРегистрации)

	Если Не РаботаВБюджетномУчреждении Или Не СтатьиРасходовПоПриказу209нПрименяются(ПериодРегистрации) Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	NULL КАК Начисление,
		|	NULL КАК СтатьяРасходов
		|ПОМЕСТИТЬ ВТСтатьиРасходовНачисленийПоУмолчанию";
		Запрос.Выполнить();
		
	Иначе
		
		СохраняемыйЗаработок = Новый Массив;
		СохраняемыйЗаработок.Добавить(ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыВыплатБывшимСотрудникам.СохраняемыйЗаработокНаВремяТрудоустройства"));
		СохраняемыйЗаработок.Добавить(ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыВыплатБывшимСотрудникам.СохраняемоеДенежноеСодержаниеНаПериодТрудоустройства"));
		
		Запрос.УстановитьПараметр("СохраняемыйЗаработок", СохраняемыйЗаработок);
		Запрос.УстановитьПараметр("Статья264", СтатьяРасходов264());
		Запрос.УстановитьПараметр("Статья266", СтатьяРасходов266());
		Запрос.УстановитьПараметр("Статья213", СтатьяРасходов213());
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВидыВыплатБывшимСотрудникам.Ссылка КАК Начисление,
		|	&Статья264 КАК СтатьяРасходов
		|ПОМЕСТИТЬ ВТСтатьиРасходовНачисленийПоУмолчанию
		|ИЗ
		|	Справочник.ВидыВыплатБывшимСотрудникам КАК ВидыВыплатБывшимСотрудникам
		|ГДЕ
		|	ВидыВыплатБывшимСотрудникам.Ссылка В(&СохраняемыйЗаработок)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Начисления.Ссылка,
		|	&Статья266
		|ИЗ
		|	ПланВидовРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.КатегорияНачисленияИлиНеоплаченногоВремени В (ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоЛистаЗаСчетРаботодателя), ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.КомпенсацияЗаНеотработанныеДниЧасыПриУвольнении), ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.КомпенсацияЗаНеотработанныеДниПриУвольненииГосслужащего), ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ВыходноеПособиеМесячноеДенежноеСодержание), ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ВыходноеПособие))
		|	И Начисления.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Начисления.Ссылка,
		|	&Статья266
		|ИЗ
		|	ПланВидовРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ПособиеПоУходуЗаРебенкомДоТрехЛет)
		|	И Начисления.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Начисления.Ссылка,
		|	ВЫБОР
		|		КОГДА НЕ &ПрямыеВыплатыПособий
		|				ИЛИ Начисления.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
		|			ТОГДА &Статья213
		|		ИНАЧЕ Начисления.СтатьяРасходов
		|	КОНЕЦ
		|ИЗ
		|	ПланВидовРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.КатегорияНачисленияИлиНеоплаченногоВремени В (ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДнейУходаЗаДетьмиИнвалидами), ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ДенежноеСодержаниеОплатаДнейУходаЗаДетьмиИнвалидами))";
		
		Запрос.Выполнить();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьВТНастройкиНачислений(Запрос, НастройкиФактическихНачисленийСохраняемогоДенежногоСодержания = Ложь)
	
	Если НастройкиФактическихНачисленийСохраняемогоДенежногоСодержания Тогда
		
		// Получим настройки начислений, для начислений со стратегией бухучета ПоБазовымРасчетам и ПоБазеСреднегоЗаработка
		// устанавливаем пустое значение
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Начисления.Ссылка КАК Начисление,
		|	ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаНачислений.ПустаяСсылка) КАК СпособРасчетаПоСреднемуЗаработку,
		|	ВЫБОР
		|		КОГДА Начисления.СтратегияОтраженияВУчете В (ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам), ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазеСреднегоЗаработка))
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПустаяСсылка)
		|		ИНАЧЕ Начисления.СтратегияОтраженияВУчете
		|	КОНЕЦ КАК СтратегияОтраженияВУчете,
		|	ВЫБОР
		|		КОГДА Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ПособиеПоУходуЗаРебенкомДоПолутораЛет)
		|			ТОГДА ИСТИНА
		|		КОГДА Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ПособиеПоУходуЗаРебенкомДоПолутораЛетВоеннослужащим)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоПособиеПоУходуЗаРебенкомДоПолутораЛет,
		|	ВЫБОР
		|		КОГДА Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДнейУходаЗаДетьмиИнвалидами)
		|			ТОГДА ИСТИНА
		|		КОГДА Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ДенежноеСодержаниеОплатаДнейУходаЗаДетьмиИнвалидами)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоОплатаДнейУходаЗаДетьмиИнвалидами,
		|	ВЫБОР
		|		КОГДА Начисления.ВидОперацииПоЗарплате = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюРаботодатель)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоРасходыПоСтрахованиюРаботодатель,
		|	ВЫБОР
		|		КОГДА Начисления.ВидОперацииПоЗарплате = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюБюджет)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоРасходыПоСтрахованиюБюджет,
		|	ВЫБОР
		|		КОГДА Начисления.ВидОперацииПоЗарплате В (&РасходыПоСтрахованию)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоРасходыПоСтрахованию,
		|	ВЫБОР
		|		КОГДА Начисления.ВидОперацииПоЗарплате В (&РасходыФСС)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоРасходыФСС,
		|	НЕОПРЕДЕЛЕНО КАК НазначениеРасчетаСохраняемогоДенежногоСодержания,
		|	Начисления.ОчередностьОтраженияВУчете КАК ОчередностьОтраженияВУчете,
		|	Начисления.ИспользуетСдельныйЗаработок КАК ИспользуетСдельныйЗаработок,
		|	Начисления.ИспользоватьОперативныеПоказателиВЦеломЗаМесяц КАК ИспользоватьОперативныеПоказателиВЦеломЗаМесяц,
		|	Начисления.ИспользуетСреднийЗаработокОбщий КАК ИспользуетСреднийЗаработокОбщий,
		|	Начисления.ИспользуетБухучетПоказателей КАК ИспользуетБухучетПоказателей,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасчетыПоОплатеТруда,
		|	Начисления.КатегорияНачисленияИлиНеоплаченногоВремени КАК КатегорияНачисления
		|ПОМЕСТИТЬ ВТНастройкиНачислений
		|ИЗ
		|	ПланВидовРасчета.Начисления КАК Начисления"; 
		Запрос.Выполнить();
		
	Иначе
		
		Если ПолучитьФункциональнуюОпцию("РаботаВХозрасчетнойОрганизации") Тогда
			УчетНачисленнойЗарплатыРасширенный.СоздатьВТСтатьиРасходовНачисленийПоСпособамРасчетов(Запрос.МенеджерВременныхТаблиц);
		Иначе
			Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 0
			|	NULL КАК Ссылка,
			|	NULL КАК СтатьяРасходов
			|ПОМЕСТИТЬ ВТСтатьиРасходовНачисленийПоСпособамРасчетов";
			Запрос.Выполнить();
		КонецЕсли;
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Начисления.Ссылка КАК Начисление,
		|	ВЫБОР
		|		КОГДА Начисления.ВидОтпуска.СпособРасчетаОтпуска В (ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОтпуска.ВКалендарныхДнях), ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОтпуска.ВКалендарныхИлиРабочихДняхВЗависимостиОтТрудовогоДоговора))
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаНачислений.ОплатаОтпускаПоКалендарнымДням)
		|		КОГДА Начисления.ВидОтпуска.СпособРасчетаОтпуска = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОтпуска.ВРабочихДнях)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаНачислений.ОплатаОтпускаПоШестидневке)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаНачислений.ПустаяСсылка)
		|	КОНЕЦ КАК СпособРасчетаПоСреднемуЗаработку,
		|	ВЫБОР
		|		КОГДА НЕ &ИспользоватьСтатьиФинансирования
		|				И Начисления.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоФактическимНачислениям)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПустаяСсылка)
		|		КОГДА НЕ &ИспользоватьСтатьиФинансирования
		|				И Начисления.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазеСреднегоЗаработка)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПустаяСсылка)
		|		ИНАЧЕ Начисления.СтратегияОтраженияВУчете
		|	КОНЕЦ КАК СтратегияОтраженияВУчете,
		|	ВЫБОР
		|		КОГДА Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДнейУходаЗаДетьмиИнвалидами)
		|			ТОГДА ИСТИНА
		|		КОГДА Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ДенежноеСодержаниеОплатаДнейУходаЗаДетьмиИнвалидами)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоОплатаДнейУходаЗаДетьмиИнвалидами,
		|	ВЫБОР
		|		КОГДА Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ПособиеПоУходуЗаРебенкомДоПолутораЛет)
		|			ТОГДА ИСТИНА
		|		КОГДА Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ПособиеПоУходуЗаРебенкомДоПолутораЛетВоеннослужащим)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоПособиеПоУходуЗаРебенкомДоПолутораЛет,
		|	ВЫБОР
		|		КОГДА Начисления.ВидОперацииПоЗарплате = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюРаботодатель)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоРасходыПоСтрахованиюРаботодатель,
		|	ВЫБОР
		|		КОГДА Начисления.ВидОперацииПоЗарплате = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюБюджет)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоРасходыПоСтрахованиюБюджет,
		|	ВЫБОР
		|		КОГДА Начисления.ВидОперацииПоЗарплате В (&РасходыПоСтрахованию)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоРасходыПоСтрахованию,
		|	ВЫБОР
		|		КОГДА Начисления.ВидОперацииПоЗарплате В (&РасходыФСС)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоРасходыФСС,
		|	&НазначениеРасчетаСохраняемогоДенежногоСодержания КАК НазначениеРасчетаСохраняемогоДенежногоСодержания,
		|	Начисления.ОчередностьОтраженияВУчете КАК ОчередностьОтраженияВУчете,
		|	Начисления.ИспользуетСдельныйЗаработок
		|		И Начисления.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоСдельномуЗаработку) КАК ИспользуетСдельныйЗаработок,
		|	Начисления.ИспользоватьОперативныеПоказателиВЦеломЗаМесяц КАК ИспользоватьОперативныеПоказателиВЦеломЗаМесяц,
		|	Начисления.ИспользуетСреднийЗаработокОбщий КАК ИспользуетСреднийЗаработокОбщий,
		|	Начисления.ИспользуетБухучетПоказателей КАК ИспользуетБухучетПоказателей,
		|	ЕСТЬNULL(СтатьиРасходовНачислений.СтатьяРасходов, ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)) КАК СтатьяРасчетыПоОплатеТруда,
		|	Начисления.КатегорияНачисленияИлиНеоплаченногоВремени КАК КатегорияНачисления
		|ПОМЕСТИТЬ ВТНастройкиНачислений
		|ИЗ
		|	ПланВидовРасчета.Начисления КАК Начисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатьиРасходовНачисленийПоСпособамРасчетов КАК СтатьиРасходовНачислений
		|		ПО Начисления.Ссылка = СтатьиРасходовНачислений.ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТСтатьиРасходовНачисленийПоСпособамРасчетов";
		
		ОписаниеПоля = "НЕОПРЕДЕЛЕНО";
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба.РасчетДенежногоСодержания") Тогда
			// Уточнение получения поля НазначениеРасчетаСохраняемогоДенежногоСодержания.
			Модуль = ОбщегоНазначения.ОбщийМодуль("РасчетДенежногоСодержания");
			Модуль.ОписаниеПоляНазначениеРасчетаСохраняемогоДенежногоСодержания(ОписаниеПоля);
		КонецЕсли;
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&НазначениеРасчетаСохраняемогоДенежногоСодержания", ОписаниеПоля);
		
		Запрос.Выполнить();
		
	КонецЕсли;

КонецПроцедуры

Процедура СоздатьВТНастройкиПоказателейНачислений(Запрос)

	Запрос.Текст =
	"ВЫБРАТЬ
	|	НачисленияПоказатели.Ссылка КАК Начисление,
	|	НачисленияПоказатели.Показатель КАК Показатель,
	|	НачисленияПоказатели.ЗадаетБухучет КАК ЗадаетБухучет
	|ПОМЕСТИТЬ ВТНастройкиПоказателейНачислений
	|ИЗ
	|	ПланВидовРасчета.Начисления.Показатели КАК НачисленияПоказатели";
	Запрос.Выполнить();

КонецПроцедуры

Процедура ДополнитьВТСтрокиКУдалению(Запрос, ИмяТаблицы)

	Если НЕ ЗарплатаКадры.ВТСуществует(Запрос.МенеджерВременныхТаблиц, "ВТСтрокиКУдалению") Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки
		|ПОМЕСТИТЬ ВТСтрокиКУдалению
		|ИЗ
		|	#Таблица КАК Таблица";
	Иначе
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВТСтрокиКУдалению.ИдентификаторСтроки КАК ИдентификаторСтроки
		|ПОМЕСТИТЬ ВТСтрокиКУдалениюВременная
		|ИЗ
		|	ВТСтрокиКУдалению КАК ВТСтрокиКУдалению
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТСтрокиКУдалению
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТСтрокиКУдалениюВременная.ИдентификаторСтроки КАК ИдентификаторСтроки
		|ПОМЕСТИТЬ ВТСтрокиКУдалению
		|ИЗ
		|	ВТСтрокиКУдалениюВременная КАК ВТСтрокиКУдалениюВременная
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.ИдентификаторСтроки
		|ИЗ
		|	#Таблица КАК Таблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТСтрокиКУдалениюВременная";
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "#Таблица", ИмяТаблицы);
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьВТБухучетЕдиновременныеПособия(Запрос, ИмяВТНачисленияИсходная)
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.ПериодРегистрации КАК ПериодРегистрации,
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Организация КАК Организация,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.Подразделение КАК Подразделение,
	|	Начисления.Подразделение КАК ПодразделениеУчетаЗатрат,
	|	Начисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.ВидОперации КАК ВидОперации,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.ДокументОснование КАК ДокументОснование,
	|	Начисления.Сумма КАК Сумма,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
	|	ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка) КАК СпособОтраженияЗарплатыВБухучете,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА НастройкиБухучетаСотрудников.ОтношениеКЕНВД ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		КОГДА НастройкиБухучетаСотрудников.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ОпределяетсяЕжемесячноПроцентом)
	|				И &ПроцентЕНВД = 100
	|			ТОГДА ИСТИНА
	|		КОГДА НастройкиБухучетаСотрудников.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОблагаетсяЕНВД
	|ПОМЕСТИТЬ ВТБухучетЕдиновременныеПособия
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаСотрудников КАК НастройкиБухучетаСотрудников
	|		ПО Начисления.ИдентификаторСтроки = НастройкиБухучетаСотрудников.ИдентификаторСтроки
	|ГДЕ
	|	Начисления.Начисление В (ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.ПособиеПриПостановкеНаУчетВРанниеСрокиБеременности), ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.ПособиеПриРожденииРебенка), ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.ПособиеНаПогребение), ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.ПособиеНаПогребениеСотруднику))";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	Запрос.Выполнить();
	// Зарегистрируем обработанные идентификаторы строк.
	ДополнитьВТСтрокиКУдалению(Запрос, "ВТБухучетЕдиновременныеПособия");

КонецПроцедуры

Процедура СоздатьВТБухучетКомпенсацийЗаЗадержкуЗарплаты(Запрос, ИмяВТНачисленияИсходная)
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.ПериодРегистрации КАК ПериодРегистрации,
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Организация КАК Организация,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.Подразделение КАК Подразделение,
	|	Начисления.Подразделение КАК ПодразделениеУчетаЗатрат,
	|	Начисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.ВидОперации КАК ВидОперации,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.ДокументОснование КАК ДокументОснование,
	|	Начисления.Сумма КАК Сумма,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
	|	ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка) КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА НастройкиБухучетаСотрудников.ОтношениеКЕНВД ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		КОГДА НастройкиБухучетаСотрудников.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ОпределяетсяЕжемесячноПроцентом)
	|				И &ПроцентЕНВД = 100
	|			ТОГДА ИСТИНА
	|		КОГДА НастройкиБухучетаСотрудников.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОблагаетсяЕНВД
	|ПОМЕСТИТЬ ВТБухучетКомпенсацийЗаЗадержкуЗарплаты
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаСотрудников КАК НастройкиБухучетаСотрудников
	|		ПО Начисления.ИдентификаторСтроки = НастройкиБухучетаСотрудников.ИдентификаторСтроки
	|ГДЕ
	|	Начисления.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.КомпенсацияЗаЗадержкуЗарплаты)";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	Запрос.Выполнить();
	
	// Зарегистрируем обработанные идентификаторы строк.
	ДополнитьВТСтрокиКУдалению(Запрос, "ВТБухучетКомпенсацийЗаЗадержкуЗарплаты");

КонецПроцедуры

Процедура СоздатьВТБухучетСдельногоЗаработка(Запрос, ИмяВТНачисленияИсходная)

	ПоказательСдельногоЗаработка = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.СдельныйЗаработок");
	Запрос.УстановитьПараметр("ПоказательСдельногоЗаработка", ПоказательСдельногоЗаработка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.ПериодРегистрации КАК ПериодРегистрации,
	|	Начисления.Организация КАК Организация,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.Подразделение КАК Подразделение,
	|	Начисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.ВидОперации КАК ВидОперации,
	|	Начисления.ДокументОснование КАК ДокументОснование,
	|	НастройкиБухучетаСотрудников.СтатьяФинансирования КАК СтатьяФинансирования,
	|	НастройкиБухучетаСотрудников.СтатьяРасходов КАК СтатьяРасходов,
	|	НастройкиБухучетаСотрудников.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	НастройкиБухучетаСотрудников.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	Начисления.Сумма КАК Сумма
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|		ПО Начисления.Начисление = НастройкиНачислений.Начисление
	|			И (НастройкиНачислений.ИспользуетСдельныйЗаработок)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаСотрудников КАК НастройкиБухучетаСотрудников
	|		ПО Начисления.ИдентификаторСтроки = НастройкиБухучетаСотрудников.ИдентификаторСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СУММА(ЗначенияПоказателей.Значение) КАК Коэффициент,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьСтатьиФинансирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|		ИНАЧЕ ЗначенияПоказателей.СтатьяФинансирования
	|	КОНЕЦ КАК СтатьяФинансирования,
	|	ЗначенияПоказателей.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА &ЕстьЕНВД
	|			ТОГДА ЗначенияПоказателей.ОтношениеКЕНВД
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД)
	|	КОНЕЦ КАК ОтношениеКЕНВД,
	|	ЗначенияПоказателей.Подразделение КАК ПодразделениеУчетаЗатрат
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК СписокНачислений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|		ПО СписокНачислений.Начисление = НастройкиНачислений.Начисление
	|			И (НастройкиНачислений.ИспользуетСдельныйЗаработок)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗначенияОперативныхПоказателейРасчетаЗарплатыСотрудников КАК ЗначенияПоказателей
	|		ПО СписокНачислений.Сотрудник = ЗначенияПоказателей.Сотрудник
	|			И (ЗначенияПоказателей.Показатель = &ПоказательСдельногоЗаработка)
	|			И (ЗначенияПоказателей.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка))
	|			И (НастройкиНачислений.ИспользоватьОперативныеПоказателиВЦеломЗаМесяц
	|					И НАЧАЛОПЕРИОДА(ЗначенияПоказателей.Период, МЕСЯЦ) = НАЧАЛОПЕРИОДА(СписокНачислений.ДатаНачала, МЕСЯЦ)
	|				ИЛИ ЗначенияПоказателей.Период МЕЖДУ СписокНачислений.ДатаНачала И СписокНачислений.ДатаОкончания)
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокНачислений.ИдентификаторСтроки,
	|	ЗначенияПоказателей.СтатьяФинансирования,
	|	ЗначенияПоказателей.СпособОтраженияЗарплатыВБухучете,
	|	ЗначенияПоказателей.ОтношениеКЕНВД,
	|	ЗначенияПоказателей.Подразделение";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Если РезультатЗапроса[0].Пустой() Тогда
		СоздатьПустуюВТБухучетНачислений(Запрос, "ВТБухучетСдельногоЗаработка");
	Иначе
		
		ТаблицаБухучетСдельногоЗаработка = НоваяТаблицаБухучетНачисленияУдержанияПоСотрудникам();
		
		ТаблицаКоэффициентов = РезультатЗапроса[1].Выгрузить();
		ТаблицаКоэффициентов.Индексы.Добавить("ИдентификаторСтроки");
		Отбор = Новый Структура("ИдентификаторСтроки");
		
		ВыборкаНачисления = РезультатЗапроса[0].Выбрать();
		Пока ВыборкаНачисления.Следующий() Цикл
			
			Отбор.ИдентификаторСтроки = ВыборкаНачисления.ИдентификаторСтроки;
			
			ЗаполнитьЗначенияСвойств(Отбор, ВыборкаНачисления);
			СтрокиОтражения = ТаблицаКоэффициентов.НайтиСтроки(Отбор);
			Если СтрокиОтражения.Количество() = 0 Тогда
				// пропускаем эту строку, т.к. бухучет сдельного заработка не задан
				Продолжить;
			КонецЕсли;
			
			Коэффициенты = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиОтражения,"Коэффициент");
			Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(ВыборкаНачисления.Сумма, Коэффициенты);
			
			Если Результаты = Неопределено Тогда
				
				СтрокаТаблицы = ТаблицаБухучетСдельногоЗаработка.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ВыборкаНачисления);
				
			Иначе
				
				Индекс = 0;
				Для Каждого СтрокаОтражения Из СтрокиОтражения Цикл
					
					СтрокаТаблицы = ТаблицаБухучетСдельногоЗаработка.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ВыборкаНачисления);
					СтрокаТаблицы.Сумма = Результаты[Индекс];
					СтрокаТаблицы.СтатьяФинансирования = СтрокаОтражения.СтатьяФинансирования;
					СтрокаТаблицы.СпособОтраженияЗарплатыВБухучете = СтрокаОтражения.СпособОтраженияЗарплатыВБухучете;
					СтрокаТаблицы.ПодразделениеУчетаЗатрат = СтрокаОтражения.ПодразделениеУчетаЗатрат;
					СтрокаТаблицы.ОблагаетсяЕНВД = ?(СтрокаОтражения.ОтношениеКЕНВД = Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД, Истина, Ложь);
					
					Индекс = Индекс + 1;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Запрос.УстановитьПараметр("ТаблицаБухучетСдельногоЗаработка", ТаблицаБухучетСдельногоЗаработка);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	БухучетНачислений.ПериодРегистрации КАК ПериодРегистрации,
		|	БухучетНачислений.Организация КАК Организация,
		|	БухучетНачислений.Сотрудник КАК Сотрудник,
		|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БухучетНачислений.Подразделение КАК Подразделение,
		|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачислений.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	БухучетНачислений.Начисление КАК Начисление,
		|	БухучетНачислений.ВидОперации КАК ВидОперации,
		|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
		|	БухучетНачислений.ДатаОкончания КАК ДатаОкончания,
		|	БухучетНачислений.ДокументОснование КАК ДокументОснование,
		|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачислений.Сумма КАК Сумма,
		|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД
		|ПОМЕСТИТЬ ВТБухучетСдельногоЗаработка
		|ИЗ
		|	&ТаблицаБухучетСдельногоЗаработка КАК БухучетНачислений";
		Запрос.Выполнить();
		// Зарегистрируем обработанные идентификаторы строк.
		ДополнитьВТСтрокиКУдалению(Запрос, "ВТБухучетСдельногоЗаработка");
		
	КонецЕсли;

КонецПроцедуры

Процедура СоздатьВТБухучетПраздничныхСверхурочных(Запрос, ИмяВТНачисленияИсходная, ИсточникДанныхОДатахНачислений)

	УдалитьВТ = Новый Массив;
	
	ВидыВремени = Новый Массив;
	ВидыВремени.Добавить(ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.Праздники"));
	ВидыВремени.Добавить(ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ПраздникиБезПовышеннойОплаты"));
	ВидыВремени.Добавить(ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.Сверхурочные"));
	ВидыВремени.Добавить(ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.СверхурочныеБезПовышеннойОплаты"));
	
	Запрос.УстановитьПараметр("ВидыВремениПраздничныхСверхурочных", ВидыВремени);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НачисленияВидыВремени.Ссылка КАК Ссылка,
	|	НачисленияВидыВремени.ВидВремени КАК ВидВремени
	|ПОМЕСТИТЬ ВТНачисленияПраздничныхСверхурочных
	|ИЗ
	|	ПланВидовРасчета.Начисления.ВидыВремени КАК НачисленияВидыВремени
	|ГДЕ
	|	НачисленияВидыВремени.Ссылка.СпособВыполненияНачисления = ЗНАЧЕНИЕ(Перечисление.СпособыВыполненияНачислений.ПоЗначениюВидаВремениПриОкончательномРасчете)
	|	И НачисленияВидыВремени.ВидВремени В(&ВидыВремениПраздничныхСверхурочных)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СписокНачислений.Начисление КАК Начисление,
	|	СписокНачислений.ПериодРегистрации КАК ПериодРегистрации,
	|	СписокНачислений.Сотрудник КАК Сотрудник,
	|	СписокНачислений.ДатаНачала КАК ДатаНачала,
	|	СписокНачислений.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.ВидВремени КАК ВидУчетаВремени
	|ПОМЕСТИТЬ ВТСтрокиКОбработке
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК СписокНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтрокиКУдалению КАК СтрокиКУдалению
	|		ПО СписокНачислений.ИдентификаторСтроки = СтрокиКУдалению.ИдентификаторСтроки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисленияПраздничныхСверхурочных КАК Начисления
	|		ПО СписокНачислений.Начисление = Начисления.Ссылка
	|ГДЕ
	|	СтрокиКУдалению.ИдентификаторСтроки ЕСТЬ NULL
	|	И СписокНачислений.Сумма <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтрокиКОбработке.ПериодРегистрации КАК ПериодРегистрации,
	|	СтрокиКОбработке.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СтрокиКОбработке.Сотрудник КАК Сотрудник,
	|	СтрокиКОбработке.Начисление КАК Начисление,
	|	НАЧАЛОПЕРИОДА(СтрокиКОбработке.ДатаНачала, МЕСЯЦ) КАК ПериодДействия,
	|	БухучетРабочегоВремениСотрудников.Дата КАК Дата,
	|	БухучетРабочегоВремениСотрудников.ВидУчетаВремени КАК ВидУчетаВремени,
	|	БухучетРабочегоВремениСотрудников.Часы КАК Часы,
	|	ВЫБОР
	|		КОГДА &ИспользоватьСтатьиФинансирования
	|			ТОГДА БухучетРабочегоВремениСотрудников.СтатьяФинансирования
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|	КОНЕЦ КАК СтатьяФинансирования,
	|	ВЫБОР
	|		КОГДА &РаботаВБюджетномУчреждении
	|			ТОГДА БухучетРабочегоВремениСотрудников.СтатьяРасходов
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|	КОНЕЦ КАК СтатьяРасходов,
	|	БухучетРабочегоВремениСотрудников.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетРабочегоВремениСотрудников.ОтношениеКЕНВД КАК ОтношениеКЕНВД
	|ИЗ
	|	ВТСтрокиКОбработке КАК СтрокиКОбработке
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетРабочегоВремениСотрудников КАК БухучетРабочегоВремениСотрудников
	|		ПО СтрокиКОбработке.Сотрудник = БухучетРабочегоВремениСотрудников.Сотрудник
	|			И СтрокиКОбработке.ВидУчетаВремени = БухучетРабочегоВремениСотрудников.ВидУчетаВремени
	|			И СтрокиКОбработке.ПериодРегистрации >= БухучетРабочегоВремениСотрудников.ПериодРегистрации
	|			И (БухучетРабочегоВремениСотрудников.Дата МЕЖДУ СтрокиКОбработке.ДатаНачала И СтрокиКОбработке.ДатаОкончания)
	|			И (БухучетРабочегоВремениСотрудников.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|				ИЛИ &ИспользоватьСтатьиФинансирования
	|					И БухучетРабочегоВремениСотрудников.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|				ИЛИ &РаботаВБюджетномУчреждении
	|					И БухучетРабочегоВремениСотрудников.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|				ИЛИ &ЕстьЕНВД
	|					И БухучетРабочегоВремениСотрудников.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка))";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	БухучетРабочегоВремени = Запрос.Выполнить().Выгрузить();
	УдалитьВТ.Добавить("ВТНачисленияПраздничныхСверхурочных");
	УдалитьВТ.Добавить("ВТСтрокиКОбработке");
	
	Если БухучетРабочегоВремени.Количество() = 0 Тогда
		// Бухучет не задан.
		СоздатьПустуюВТБухучетНачислений(Запрос, "ВТБухучетПраздничныхСверхурочных");
	Иначе

		// Получим фактически оплаченные дни по видам времени.
		ТаблицаЗапросаОплаченногоВремени = БухучетРабочегоВремени.Скопировать(,"ПериодРегистрации,ПериодДействия,Сотрудник,Начисление");
		ТаблицаЗапросаОплаченногоВремени.Свернуть("ПериодРегистрации,ПериодДействия,Сотрудник,Начисление");
		
		// Получим таблицу оплаченного времени - ТаблицаОплаченногоВремени.
		// колонки таблицы: Сотрудник, ПериодДействия, Начисление, Дата, ВидУчетаВремени.
		Если ИсточникДанныхОДатахНачислений = Неопределено Тогда
			ТаблицаОплаченногоВремени = РасчетЗарплатыРасширенный.ДатыНачисленийОплачивающихВидыВремени(ТаблицаЗапросаОплаченногоВремени);
		Иначе
			ТаблицаОплаченногоВремени = ИсточникДанныхОДатахНачислений.ДатыНачисленийОплачивающихВидыВремени(ТаблицаЗапросаОплаченногоВремени);
		КонецЕсли;
		
		ПроцентЕНВД = Запрос.Параметры.ПроцентЕНВД;
		ЕстьЕНВД 	= Запрос.Параметры.ЕстьЕНВД;
		ТаблицаБухучета = НоваяТаблицаБухучетНачисленияУдержанияПоСотрудникам();
		
		ИдентификаторыСтрок = ОбщегоНазначения.ВыгрузитьКолонку(БухучетРабочегоВремени, "ИдентификаторСтроки", Истина);
		Запрос.УстановитьПараметр("ОтборСтрок", ИдентификаторыСтрок);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(СписокНачислений.ДатаНачала, МЕСЯЦ) КАК ПериодДействия,
		|	СписокНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	СписокНачислений.Начисление КАК Начисление,
		|	СписокНачислений.ПериодРегистрации КАК ПериодРегистрации,
		|	СписокНачислений.Организация КАК Организация,
		|	СписокНачислений.Сотрудник КАК Сотрудник,
		|	СписокНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СписокНачислений.Подразделение КАК Подразделение,
		|	СписокНачислений.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	СписокНачислений.ДатаНачала КАК ДатаНачала,
		|	СписокНачислений.ДатаОкончания КАК ДатаОкончания,
		|	СписокНачислений.ВидОперации КАК ВидОперации,
		|	СписокНачислений.ДокументОснование КАК ДокументОснование,
		|	НастройкиБухучетаСотрудников.СтатьяФинансирования КАК СтатьяФинансирования,
		|	НастройкиБухучетаСотрудников.СтатьяРасходов КАК СтатьяРасходов,
		|	НастройкиБухучетаСотрудников.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	НастройкиБухучетаСотрудников.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	СписокНачислений.Сумма КАК Сумма
		|ИЗ
		|	ВТНачисленияДляРаспределения КАК СписокНачислений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаСотрудников КАК НастройкиБухучетаСотрудников
		|		ПО СписокНачислений.ИдентификаторСтроки = НастройкиБухучетаСотрудников.ИдентификаторСтроки
		|ГДЕ
		|	СписокНачислений.ИдентификаторСтроки В(&ОтборСтрок)";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
		ТаблицаНачислений = Запрос.Выполнить().Выгрузить();
		
		БухучетРабочегоВремени.Свернуть("ИдентификаторСтроки,Сотрудник,Начисление,Дата,ВидУчетаВремени,Часы,СтатьяФинансирования,СтатьяРасходов,СпособОтраженияЗарплатыВБухучете,ОтношениеКЕНВД");
		
		// В таблицу собираем строки начислений, для которых бухучет задан не для всех оплаченных дней.
		// Доля суммы, приходящаяся на такие дни будет отражена в бухучете по общим правилам.
		НеОбработанныеНачисления = ТаблицаНачислений.СкопироватьКолонки("ИдентификаторСтроки,ТерриторияВыполненияРаботВОрганизации,Сумма");
		// В таблицу собираем идентификаторы строк тех начислений, которые полностью распределили,
		// эти строки будут помещены в таблицу ВТСтрокиКУдалению.
		ОбработанныеНачисления = НеОбработанныеНачисления.СкопироватьКолонки("ИдентификаторСтроки");
		
		ТаблицаНачислений.Индексы.Добавить("ИдентификаторСтроки");
		ОтборНачислений = Новый Структура("ИдентификаторСтроки");
		
		БухучетРабочегоВремени.Индексы.Добавить("ИдентификаторСтроки,Дата,ВидУчетаВремени");
		ОтборБухучета = Новый Структура("ИдентификаторСтроки,Дата,ВидУчетаВремени");
		
		ТаблицаОплаченногоВремени.Индексы.Добавить("Сотрудник,Начисление,ПериодДействия");
		ОтборОплаченныхДней = Новый Структура("Сотрудник,Начисление,ПериодДействия");
		
		// Оплаченные дни, по которым зарегистрирован бухучет.
		ОплаченныеДниСБухучетом = Новый Массив;
		// Оплаченные дни, по которым не зарегистрирован бухучет.
		ОплаченныеДниБезБухучета = Новый Массив;
		// Строки с бухучетом рабочего времени.
		СтрокиБазы = Новый Массив;
		
		Для каждого ИдентификаторСтроки Из ИдентификаторыСтрок Цикл
			
			ОтборНачислений.ИдентификаторСтроки = ИдентификаторСтроки;
			ОтборБухучета.ИдентификаторСтроки   = ИдентификаторСтроки;
			
			СтрокиНачислений = ТаблицаНачислений.НайтиСтроки(ОтборНачислений);
			ДатаНачала 		= СтрокиНачислений[0].ДатаНачала;
			ДатаОкончания 	= СтрокиНачислений[0].ДатаОкончания;
			
			ОплаченныеДниСБухучетом.Очистить();
			ОплаченныеДниБезБухучета.Очистить();
			СтрокиБазы.Очистить();
			
			ВсегоЧасов = 0;
			РаспределятьПоЧасам = Истина;
			
			ЗаполнитьЗначенияСвойств(ОтборОплаченныхДней, СтрокиНачислений[0]);
			ОплаченныеДни = ТаблицаОплаченногоВремени.НайтиСтроки(ОтборОплаченныхДней);
			Для каждого СтрокаОплаченныйДень Из ОплаченныеДни Цикл
				
				Если СтрокаОплаченныйДень.Дата < ДатаНачала Или СтрокаОплаченныйДень.Дата > ДатаОкончания Тогда
					// день не в периоде начисления
					Продолжить;
				КонецЕсли;
				
				// Дополним отбор строк бухучета полями: Дата,ВидУчетаВремени.
				ЗаполнитьЗначенияСвойств(ОтборБухучета, СтрокаОплаченныйДень);
				СтрокиБухучет = БухучетРабочегоВремени.НайтиСтроки(ОтборБухучета);
				Если СтрокиБухучет.Количество() = 0 Тогда
					// Для оплаченного дня не задан бухучет по этому виду времени.
					ОплаченныеДниБезБухучета.Добавить(СтрокаОплаченныйДень.Дата);
				Иначе
					// Для оплаченного дня задан бухучет.
					ОплаченныеДниСБухучетом.Добавить(СтрокаОплаченныйДень.Дата);
					Для каждого СтрокаБухучета Из СтрокиБухучет Цикл
						СтрокиБазы.Добавить(СтрокаБухучета);
						ВсегоЧасов = ВсегоЧасов + СтрокаБухучета.Часы;
						Если СтрокаБухучета.Часы = 0 Тогда
							РаспределятьПоЧасам = Ложь;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
			КонецЦикла;
			
			ДнейОплаченоСБухучетом = ОплаченныеДниСБухучетом.Количество();
			Если ДнейОплаченоСБухучетом = 0 Тогда
				// У начисления нет оплаченных дней, для которых задан бухучет.
				Продолжить;
			КонецЕсли;
			
			// Распределение начислений.
			
			ДнейОплаченоБезБухучета = ОплаченныеДниБезБухучета.Количество();
			Для каждого СтрокаНачисления Из СтрокиНачислений Цикл
				
				Сумма = СтрокаНачисления.Сумма;
				Если ДнейОплаченоБезБухучета > 0 Тогда
					// Сумма по строке, которую необходимо отразить в бухучете по общим правилам.
					СуммаБезРаспределения = ОКР(ДнейОплаченоБезБухучета/(ДнейОплаченоБезБухучета+ДнейОплаченоСБухучетом)*Сумма,2);
					// Добавим в таблицу строку начисления, которую необходимо отразить в бухучете по общим правилам.
					НоваяСтрока = НеОбработанныеНачисления.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНачисления);
					НоваяСтрока.Сумма = СуммаБезРаспределения;
					// Сумма строки начисления, которую необходимо распределить.
					Сумма = Сумма - СуммаБезРаспределения;
				Иначе
					// Добавим в таблицу строку, которая полностью обработана, для помещения в таблицу ВТСтрокиКУдалению.
					НоваяОбработаннаяСтрока = ОбработанныеНачисления.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяОбработаннаяСтрока, СтрокаНачисления);
				КонецЕсли;
				
				РаспределеннаяСумма = 0;
				Если РаспределятьПоЧасам Тогда
					СтоимостьДняЧаса = Сумма/ВсегоЧасов;
				Иначе
					СтоимостьДняЧаса = Сумма/ДнейОплаченоСБухучетом;
				КонецЕсли;
			
				КоличествоСтрокБазы = СтрокиБазы.ВГраница();
				Для Индекс = 0 По КоличествоСтрокБазы Цикл
					
					СтрокаБазы = СтрокиБазы[Индекс];
					Если Индекс = КоличествоСтрокБазы Тогда
						// Это последняя строка.
						СуммаДня = Сумма - РаспределеннаяСумма;
					Иначе
						Если РаспределятьПоЧасам Тогда
							СуммаДня = ОКР(СтоимостьДняЧаса * СтрокаБазы.Часы,2);
						Иначе
							СуммаДня = ОКР(СтоимостьДняЧаса, 2);
						КонецЕсли;
					КонецЕсли;
					РаспределеннаяСумма = РаспределеннаяСумма + СуммаДня;
					
					НоваяСтрока = ТаблицаБухучета.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНачисления);
					
					Если ЗначениеЗаполнено(СтрокаБазы.СтатьяФинансирования) Тогда
						НоваяСтрока.СтатьяФинансирования = СтрокаБазы.СтатьяФинансирования;
					КонецЕсли;
					Если ЗначениеЗаполнено(СтрокаБазы.СтатьяРасходов) Тогда
						НоваяСтрока.СтатьяРасходов = СтрокаБазы.СтатьяРасходов;
					КонецЕсли;
					Если ЗначениеЗаполнено(СтрокаБазы.СпособОтраженияЗарплатыВБухучете) Тогда
						НоваяСтрока.СпособОтраженияЗарплатыВБухучете = СтрокаБазы.СпособОтраженияЗарплатыВБухучете;
					КонецЕсли;
					
					Если Не ЕстьЕНВД Тогда
						НоваяСтрока.Сумма = СуммаДня;
					Иначе
						
						ОтношениеКЕНВД = ?(ЗначениеЗаполнено(СтрокаБазы.ОтношениеКЕНВД),СтрокаБазы.ОтношениеКЕНВД,СтрокаНачисления.ОтношениеКЕНВД);
						Если ОтношениеКЕНВД = Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ОпределяетсяЕжемесячноПроцентом Тогда
							
							СоздаватьДополнительнуюСтроку = Ложь;
							
							ДоляЕНВД = ОКР(СуммаДня * ПроцентЕНВД / 100, 2);
							Если ДоляЕНВД <> 0 Тогда
								НоваяСтрока.ОблагаетсяЕНВД = Истина;
								НоваяСтрока.Сумма = ДоляЕНВД;
								СоздаватьДополнительнуюСтроку = Истина;
							КонецЕсли;
							
							ДоляНеЕНВД = СуммаДня - ДоляЕНВД;
							Если ДоляНеЕНВД <> 0 Тогда
								Если СоздаватьДополнительнуюСтроку Тогда
									ДополнительнаяСтрока = ТаблицаБухучета.Добавить();
									ЗаполнитьЗначенияСвойств(ДополнительнаяСтрока, НоваяСтрока);
								Иначе
									ДополнительнаяСтрока = НоваяСтрока;
								КонецЕсли;
								ДополнительнаяСтрока.ОблагаетсяЕНВД = Ложь;
								НоваяСтрока.Сумма = ДоляНеЕНВД;
							КонецЕсли;
							
						ИначеЕсли ОтношениеКЕНВД = Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД Тогда
							НоваяСтрока.ОблагаетсяЕНВД = Истина;
							НоваяСтрока.Сумма = СуммаДня;
						Иначе
							НоваяСтрока.Сумма = СуммаДня;
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЦикла;
			
			КонецЦикла;
		
		КонецЦикла;
		
		Если ТаблицаБухучета.Количество() = 0 Тогда
			СоздатьПустуюВТБухучетНачислений(Запрос, "ВТБухучетПраздничныхСверхурочных");
		Иначе
			
			ПоляГруппировки = "ИдентификаторСтроки,ПериодРегистрации,Организация,Сотрудник,ФизическоеЛицо,Подразделение,ПодразделениеУчетаЗатрат,
			|ТерриторияВыполненияРаботВОрганизации,Начисление,ВидОперации,ДатаНачала,ДатаОкончания,ДокументОснование,
			|СпособОтраженияЗарплатыВБухучете,СтатьяФинансирования,СтатьяРасходов,ОблагаетсяЕНВД,";
			ПоляСуммирования = "Сумма";
			ТаблицаБухучета.Свернуть(ПоляГруппировки, ПоляСуммирования);
			
			Запрос.УстановитьПараметр("ТаблицаБухучета", ТаблицаБухучета);
			Запрос.УстановитьПараметр("ОбработанныеНачисления", ОбработанныеНачисления);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	БухучетНачислений.ПериодРегистрации КАК ПериодРегистрации,
			|	БухучетНачислений.Организация КАК Организация,
			|	БухучетНачислений.Сотрудник КАК Сотрудник,
			|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
			|	БухучетНачислений.Подразделение КАК Подразделение,
			|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
			|	БухучетНачислений.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
			|	БухучетНачислений.Начисление КАК Начисление,
			|	БухучетНачислений.ВидОперации КАК ВидОперации,
			|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
			|	БухучетНачислений.ДатаОкончания КАК ДатаОкончания,
			|	БухучетНачислений.ДокументОснование КАК ДокументОснование,
			|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
			|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
			|	БухучетНачислений.Сумма КАК Сумма,
			|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД
			|ПОМЕСТИТЬ ВТБухучетПраздничныхСверхурочных
			|ИЗ
			|	&ТаблицаБухучета КАК БухучетНачислений
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ОбработанныеНачисления.ИдентификаторСтроки КАК ИдентификаторСтроки
			|ПОМЕСТИТЬ ВТБухучетПраздничныхСверхурочныхОбработанныеСтроки
			|ИЗ
			|	&ОбработанныеНачисления КАК ОбработанныеНачисления";
			Запрос.Выполнить();
			УдалитьВТ.Добавить("ВТБухучетПраздничныхСверхурочныхОбработанныеСтроки");
			// Зарегистрируем обработанные идентификаторы строк.
			ДополнитьВТСтрокиКУдалению(Запрос, "ВТБухучетПраздничныхСверхурочныхОбработанныеСтроки");
			
			Если НеОбработанныеНачисления.Количество() > 0 Тогда
				
				Запрос.УстановитьПараметр("НеОбработанныеНачисления", НеОбработанныеНачисления);
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	СписокНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
				|	СписокНачислений.Сумма КАК Сумма
				|ПОМЕСТИТЬ ВТНеОбработанныеНачисления
				|ИЗ
				|	&НеОбработанныеНачисления КАК СписокНачислений
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	СписокНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
				|	СписокНачислений.Сумма КАК Сумма,
				|	&ПоляТаблицы КАК ПоляТаблицы
				|ПОМЕСТИТЬ ВТНачисленияДляРаспределенияВременная
				|ИЗ
				|	ВТНачисленияДляРаспределения КАК СписокНачислений
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|УНИЧТОЖИТЬ ВТНачисленияДляРаспределения
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	НачисленияДляРаспределенияВременная.ИдентификаторСтроки КАК ИдентификаторСтроки,
				|	ЕСТЬNULL(НеОбработанныеНачисления.Сумма, НачисленияДляРаспределенияВременная.Сумма) КАК Сумма,
				|	&ПоляТаблицы КАК ПоляТаблицы
				|ПОМЕСТИТЬ ВТНачисленияДляРаспределения
				|ИЗ
				|	ВТНачисленияДляРаспределенияВременная КАК НачисленияДляРаспределенияВременная
				|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНеОбработанныеНачисления КАК НеОбработанныеНачисления
				|		ПО НачисленияДляРаспределенияВременная.ИдентификаторСтроки = НеОбработанныеНачисления.ИдентификаторСтроки";
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПоляТаблицы КАК ПоляТаблицы", "*");
				Запрос.Выполнить();
				УдалитьВТ.Добавить("ВТНеОбработанныеНачисления");
				
			КонецЕсли;
			
		КонецЕсли;

	КонецЕсли;
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
КонецПроцедуры

Процедура СоздатьВТБухучетНачисленийПоПоказателям(Запрос, ИмяВТНачисленияИсходная)
	
	УдалитьВТ = Новый Массив;
	
	ИмяТаблицыБухучетПоказателей = "";
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОбразовательныеУчреждения") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОбразовательныеУчреждения");
		ИмяТаблицыБухучетПоказателей = Модуль.ИмяТаблицыБухучетПоказателейДляЗапроса();
	КонецЕсли;
	
	Если ПустаяСтрока(ИмяТаблицыБухучетПоказателей) Тогда
		
		СоздатьПустуюВТБухучетНачислений(Запрос, "ВТБухучетНачисленийПоПоказателям");
		
	Иначе
		
		ТаблицаБухучетНачисленийПоПоказателям = НоваяТаблицаБухучетНачисленияУдержанияПоСотрудникам();
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Начисления.Организация КАК Организация,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.ДатаНачала КАК ДатаНачала,
		|	НастройкиПоказателейНачислений.Показатель КАК Показатель
		|ПОМЕСТИТЬ ВТПоказателиДляБухучета
		|ИЗ
		|	ВТНачисленияДляРаспределения КАК Начисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
		|		ПО Начисления.Начисление = НастройкиНачислений.Начисление
		|			И (НастройкиНачислений.ИспользуетБухучетПоказателей)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНастройкиПоказателейНачислений КАК НастройкиПоказателейНачислений
		|		ПО Начисления.Начисление = НастройкиПоказателейНачислений.Начисление
		|			И (НастройкиПоказателейНачислений.ЗадаетБухучет)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтрокиКУдалению КАК СтрокиКУдалению
		|		ПО Начисления.ИдентификаторСтроки = СтрокиКУдалению.ИдентификаторСтроки
		|ГДЕ
		|	СтрокиКУдалению.ИдентификаторСтроки ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоказателиДляБухучета.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ПоказателиДляБухучета.Организация КАК Организация,
		|	ПоказателиДляБухучета.Сотрудник КАК Сотрудник,
		|	ПоказателиДляБухучета.Показатель КАК Показатель,
		|	МАКСИМУМ(БухучетПоказателей.Период) КАК Период,
		|	ПоказателиДляБухучета.ДатаНачала КАК ДатаНачала
		|ПОМЕСТИТЬ ВТМаксимальныеПериоды
		|ИЗ
		|	ВТПоказателиДляБухучета КАК ПоказателиДляБухучета
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ТаблицаБухучетПоказателей КАК БухучетПоказателей
		|		ПО ПоказателиДляБухучета.Организация = БухучетПоказателей.Организация
		|			И ПоказателиДляБухучета.Сотрудник = БухучетПоказателей.Сотрудник
		|			И ПоказателиДляБухучета.Показатель = БухучетПоказателей.Показатель
		|			И ПоказателиДляБухучета.ДатаНачала >= БухучетПоказателей.Период
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоказателиДляБухучета.ИдентификаторСтроки,
		|	ПоказателиДляБухучета.Организация,
		|	ПоказателиДляБухучета.Сотрудник,
		|	ПоказателиДляБухучета.Показатель,
		|	ПоказателиДляБухучета.ДатаНачала
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Коэффициенты.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Коэффициенты.СтатьяФинансирования КАК СтатьяФинансирования,
		|	Коэффициенты.СтатьяРасходов КАК СтатьяРасходов,
		|	Коэффициенты.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	Коэффициенты.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	СУММА(Коэффициенты.Коэффициент) КАК Коэффициент
		|ПОМЕСТИТЬ ВТКоэффициенты
		|ИЗ
		|	(ВЫБРАТЬ
		|		МаксимальныеПериоды.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|		БухучетПоказателей.СтатьяФинансирования КАК СтатьяФинансирования,
		|		БухучетПоказателей.СтатьяРасходов КАК СтатьяРасходов,
		|		БухучетПоказателей.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|		БухучетПоказателей.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|		СУММА(БухучетПоказателей.Значение) КАК Коэффициент
		|	ИЗ
		|		ВТМаксимальныеПериоды КАК МаксимальныеПериоды
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ТаблицаБухучетПоказателей КАК БухучетПоказателей
		|			ПО МаксимальныеПериоды.Организация = БухучетПоказателей.Организация
		|				И МаксимальныеПериоды.Сотрудник = БухучетПоказателей.Сотрудник
		|				И МаксимальныеПериоды.Показатель = БухучетПоказателей.Показатель
		|				И МаксимальныеПериоды.Период = БухучетПоказателей.Период
		|				И МаксимальныеПериоды.ДатаНачала <= БухучетПоказателей.ДатаОкончания
		|				И (БухучетПоказателей.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1))
		|	
		|	СГРУППИРОВАТЬ ПО
		|		МаксимальныеПериоды.ИдентификаторСтроки,
		|		БухучетПоказателей.СтатьяФинансирования,
		|		БухучетПоказателей.СтатьяРасходов,
		|		БухучетПоказателей.СпособОтраженияЗарплатыВБухучете,
		|		БухучетПоказателей.ОтношениеКЕНВД
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		МаксимальныеПериоды.ИдентификаторСтроки,
		|		БухучетПоказателей.СтатьяФинансирования,
		|		БухучетПоказателей.СтатьяРасходов,
		|		БухучетПоказателей.СпособОтраженияЗарплатыВБухучете,
		|		БухучетПоказателей.ОтношениеКЕНВД,
		|		СУММА(БухучетПоказателей.Значение)
		|	ИЗ
		|		ВТМаксимальныеПериоды КАК МаксимальныеПериоды
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ТаблицаБухучетПоказателей КАК БухучетПоказателей
		|			ПО МаксимальныеПериоды.Организация = БухучетПоказателей.Организация
		|				И МаксимальныеПериоды.Сотрудник = БухучетПоказателей.Сотрудник
		|				И МаксимальныеПериоды.Показатель = БухучетПоказателей.Показатель
		|				И МаксимальныеПериоды.Период = БухучетПоказателей.Период
		|				И (БухучетПоказателей.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1))
		|	
		|	СГРУППИРОВАТЬ ПО
		|		МаксимальныеПериоды.ИдентификаторСтроки,
		|		БухучетПоказателей.СтатьяФинансирования,
		|		БухучетПоказателей.СтатьяРасходов,
		|		БухучетПоказателей.СпособОтраженияЗарплатыВБухучете,
		|		БухучетПоказателей.ОтношениеКЕНВД) КАК Коэффициенты
		|
		|СГРУППИРОВАТЬ ПО
		|	Коэффициенты.СтатьяФинансирования,
		|	Коэффициенты.ИдентификаторСтроки,
		|	Коэффициенты.ОтношениеКЕНВД,
		|	Коэффициенты.СпособОтраженияЗарплатыВБухучете,
		|	Коэффициенты.СтатьяРасходов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СтрокиКОбработке.ИдентификаторСтроки КАК ИдентификаторСтроки
		|ПОМЕСТИТЬ ВТСтрокиКОбработке
		|ИЗ
		|	ВТКоэффициенты КАК СтрокиКОбработке
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Коэффициенты.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Коэффициенты.СтатьяФинансирования КАК СтатьяФинансирования,
		|	Коэффициенты.СтатьяРасходов КАК СтатьяРасходов,
		|	Коэффициенты.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	Коэффициенты.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	Коэффициенты.Коэффициент КАК Коэффициент
		|ИЗ
		|	ВТКоэффициенты КАК Коэффициенты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Начисления.Начисление КАК Начисление,
		|	Начисления.ПериодРегистрации КАК ПериодРегистрации,
		|	Начисления.Организация КАК Организация,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Начисления.Подразделение КАК Подразделение,
		|	Начисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	Начисления.ДатаНачала КАК ДатаНачала,
		|	Начисления.ДатаОкончания КАК ДатаОкончания,
		|	Начисления.ВидОперации КАК ВидОперации,
		|	Начисления.ДокументОснование КАК ДокументОснование,
		|	НастройкиБухучетаСотрудников.СтатьяФинансирования КАК СтатьяФинансирования,
		|	НастройкиБухучетаСотрудников.СтатьяРасходов КАК СтатьяРасходов,
		|	НастройкиБухучетаСотрудников.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	НастройкиБухучетаСотрудников.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
		|	Начисления.Сумма КАК Сумма
		|ИЗ
		|	ВТНачисленияДляРаспределения КАК Начисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаСотрудников КАК НастройкиБухучетаСотрудников
		|		ПО Начисления.ИдентификаторСтроки = НастройкиБухучетаСотрудников.ИдентификаторСтроки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСтрокиКОбработке КАК СтрокиКОбработке
		|		ПО Начисления.ИдентификаторСтроки = СтрокиКОбработке.ИдентификаторСтроки";
		
		УдалитьВТ.Добавить("ВТПоказателиДляБухучета");
		УдалитьВТ.Добавить("ВТМаксимальныеПериоды");
		УдалитьВТ.Добавить("ВТКоэффициенты");
		УдалитьВТ.Добавить("ВТСтрокиКОбработке");
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ТаблицаБухучетПоказателей", ИмяТаблицыБухучетПоказателей);
		
		УстановитьПривилегированныйРежим(Истина); // Для чтения из РС #ТаблицаБухучетПоказателей.
		РезультатыЗапроса = Запрос.ВыполнитьПакет();
		УстановитьПривилегированныйРежим(Ложь);
		КоличествоРезультатов = РезультатыЗапроса.ВГраница();
		
		Если Не РезультатыЗапроса[КоличествоРезультатов].Пустой() Тогда
			
			ТаблицаКоэффициентов = РезультатыЗапроса[КоличествоРезультатов-1].Выгрузить();
			ТаблицаКоэффициентов.Индексы.Добавить("ИдентификаторСтроки");
			Отбор = Новый Структура("ИдентификаторСтроки");
			
			ВыборкаНачисления = РезультатыЗапроса[КоличествоРезультатов].Выбрать();
			Пока ВыборкаНачисления.Следующий() Цикл
				
				Отбор.ИдентификаторСтроки = ВыборкаНачисления.ИдентификаторСтроки;
				
				ЗаполнитьЗначенияСвойств(Отбор, ВыборкаНачисления);
				СтрокиОтражения = ТаблицаКоэффициентов.НайтиСтроки(Отбор);
				Если СтрокиОтражения.Количество() = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				Коэффициенты = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиОтражения,"Коэффициент");
				Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(ВыборкаНачисления.Сумма, Коэффициенты);
				
				Если Результаты = Неопределено Тогда
					
					СтрокаТаблицы = ТаблицаБухучетНачисленийПоПоказателям.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ВыборкаНачисления);
					
				Иначе
					
					Индекс = 0;
					Для Каждого СтрокаОтражения Из СтрокиОтражения Цикл
						
						СтрокаТаблицы = ТаблицаБухучетНачисленийПоПоказателям.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ВыборкаНачисления);
						СтрокаТаблицы.Сумма = Результаты[Индекс];
						
						Если ЗначениеЗаполнено(СтрокаОтражения.СтатьяФинансирования) Тогда
							СтрокаТаблицы.СтатьяФинансирования = СтрокаОтражения.СтатьяФинансирования;	
						КонецЕсли;
						Если ЗначениеЗаполнено(СтрокаОтражения.СпособОтраженияЗарплатыВБухучете) Тогда
							СтрокаТаблицы.СпособОтраженияЗарплатыВБухучете = СтрокаОтражения.СпособОтраженияЗарплатыВБухучете;	
						КонецЕсли;
						Если ЗначениеЗаполнено(СтрокаОтражения.СтатьяРасходов) Тогда
							СтрокаТаблицы.СтатьяРасходов = СтрокаОтражения.СтатьяРасходов;	
						КонецЕсли;
						СтрокаТаблицы.ОблагаетсяЕНВД = ?(СтрокаОтражения.ОтношениеКЕНВД = Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД, Истина, Ложь);
						
						Индекс = Индекс + 1;
						
					КонецЦикла;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Запрос.УстановитьПараметр("ТаблицаБухучетНачисленийПоПоказателям", ТаблицаБухучетНачисленийПоПоказателям);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	БухучетНачислений.ПериодРегистрации КАК ПериодРегистрации,
		|	БухучетНачислений.Организация КАК Организация,
		|	БухучетНачислений.Сотрудник КАК Сотрудник,
		|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БухучетНачислений.Подразделение КАК Подразделение,
		|	БухучетНачислений.Подразделение КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачислений.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	БухучетНачислений.Начисление КАК Начисление,
		|	БухучетНачислений.ВидОперации КАК ВидОперации,
		|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
		|	БухучетНачислений.ДатаОкончания КАК ДатаОкончания,
		|	БухучетНачислений.ДокументОснование КАК ДокументОснование,
		|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачислений.Сумма КАК Сумма,
		|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД
		|ПОМЕСТИТЬ ВТБухучетНачисленийПоПоказателям
		|ИЗ
		|	&ТаблицаБухучетНачисленийПоПоказателям КАК БухучетНачислений";
		Запрос.Выполнить();
		// Зарегистрируем обработанные идентификаторы строк.
		ДополнитьВТСтрокиКУдалению(Запрос, "ВТБухучетНачисленийПоПоказателям");
		
		ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьВТНачисленияПоДокументамИПлановыеДляБухучета(Запрос, ИмяВТНачисленияИсходная, РаспределениеСохраняемогоДС)
	
	// Дополнительно создается таблица ВТСтатьиРасчетыПоОплатеТрудаПервичныхДокументов.
	
	РаботаВБюджетномУчреждении = Запрос.Параметры.РаботаВБюджетномУчреждении;
	ИспользоватьСтатьиФинансирования = Запрос.Параметры.ИспользоватьСтатьиФинансирования;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	БухучетПервичныхДокументов.ДокументОснование КАК ДокументОснование,
	|	БухучетПервичныхДокументов.НачислениеУдержание КАК Начисление,
	|	БухучетПервичныхДокументов.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетПервичныхДокументов.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетПервичныхДокументов.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетПервичныхДокументов.ОтношениеКЕНВД КАК ОтношениеКЕНВД
	|ПОМЕСТИТЬ ВТБухучетПервичногоДокументаПараметр
	|ИЗ
	|	&БухучетПервичногоДокумента КАК БухучетПервичныхДокументов";
	Запрос.Выполнить();
	
	Если Не РаботаВБюджетномУчреждении И ИспользоватьСтатьиФинансирования Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	БухучетЗарплатыПервичныхДокументов.СтатьяРасходов КАК СтатьяРасходов
		|ПОМЕСТИТЬ ВТСтатьиРасчетыПоОплатеТрудаПервичныхДокументов
		|ИЗ
		|	ВТНачисленияДляРаспределения КАК Начисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыПервичныхДокументов КАК БухучетЗарплатыПервичныхДокументов
		|		ПО Начисления.Начисление = БухучетЗарплатыПервичныхДокументов.НачислениеУдержание
		|			И Начисления.ДокументОснование = БухучетЗарплатыПервичныхДокументов.ДокументОснование
		|			И (БухучетЗарплатыПервичныхДокументов.ДокументОснование <> &ИсключаемыйРегистратор)
		|			И (БухучетЗарплатыПервичныхДокументов.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Начисления.ИдентификаторСтроки,
		|	БухучетПервичногоДокумента.СтатьяРасходов
		|ИЗ
		|	ВТНачисленияДляРаспределения КАК Начисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучетПервичногоДокументаПараметр КАК БухучетПервичногоДокумента
		|		ПО Начисления.Начисление = БухучетПервичногоДокумента.Начисление
		|			И Начисления.ДокументОснование = БухучетПервичногоДокумента.ДокументОснование
		|			И (БухучетПервичногоДокумента.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка))";
		
	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	0 КАК ИдентификаторСтроки,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов
		|ПОМЕСТИТЬ ВТСтатьиРасчетыПоОплатеТрудаПервичныхДокументов";
		
	КонецЕсли;
	Запрос.Выполнить();
	
	ПустыеЗначенияДокументОснование = Новый Массив;
	ПустыеЗначенияДокументОснование.Добавить(Неопределено);
	ПустыеЗначенияДокументОснование.Добавить(Null);
	Для Каждого ТипДокументОснование Из Метаданные.ОпределяемыеТипы.ОснованиеНачисленияУдержания.Тип.Типы() Цикл
		ПустыеЗначенияДокументОснование.Добавить(ОбщегоНазначенияБЗК.МенеджерОбъектаПоТипу(ТипДокументОснование).ПустаяСсылка());
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ПустойДокументОснование", ПустыеЗначенияДокументОснование);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.Организация КАК Организация,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	ВЫБОР
	|		КОГДА Начисления.ДокументОснование В (&ПустойДокументОснование)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ Начисления.ДокументОснование
	|	КОНЕЦ КАК ДокументОснование,
	|	Начисления.ДокументОснование КАК ДокументОснованиеИсходный,
	|	Начисления.Начисление КАК Начисление
	|ПОМЕСТИТЬ ВТСтрокиКОбработке
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтрокиКУдалению КАК СтрокиКУдалению
	|		ПО Начисления.ИдентификаторСтроки = СтрокиКУдалению.ИдентификаторСтроки
	|ГДЕ
	|	СтрокиКУдалению.ИдентификаторСтроки ЕСТЬ NULL";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	Если РаспределениеСохраняемогоДС Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Начисления.Сотрудник КАК Сотрудник", "Начисления.ВременныйСотрудник КАК Сотрудник");
	КонецЕсли;
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Начисление КАК Начисление,
	|	БухучетЗарплатыПервичныхДокументов.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетЗарплатыПервичныхДокументов.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетЗарплатыПервичныхДокументов.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетЗарплатыПервичныхДокументов.ОтношениеКЕНВД КАК ОтношениеКЕНВД
	|ПОМЕСТИТЬ ВТНастройкиБухучетаПервичныхДокументов
	|ИЗ
	|	ВТСтрокиКОбработке КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыПервичныхДокументов КАК БухучетЗарплатыПервичныхДокументов
	|		ПО Начисления.Начисление = БухучетЗарплатыПервичныхДокументов.НачислениеУдержание
	|			И Начисления.ДокументОснованиеИсходный = БухучетЗарплатыПервичныхДокументов.ДокументОснование
	|			И (БухучетЗарплатыПервичныхДокументов.ДокументОснование <> &ИсключаемыйРегистратор)
	|			И (БухучетЗарплатыПервичныхДокументов.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|				ИЛИ &ИспользоватьСтатьиФинансирования
	|					И БухучетЗарплатыПервичныхДокументов.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|				ИЛИ &РаботаВБюджетномУчреждении
	|					И БухучетЗарплатыПервичныхДокументов.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|				ИЛИ &ЕстьЕНВД
	|					И БухучетЗарплатыПервичныхДокументов.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.ИдентификаторСтроки,
	|	Начисления.Начисление,
	|	БухучетПервичногоДокумента.СпособОтраженияЗарплатыВБухучете,
	|	БухучетПервичногоДокумента.СтатьяФинансирования,
	|	БухучетПервичногоДокумента.СтатьяРасходов,
	|	БухучетПервичногоДокумента.ОтношениеКЕНВД
	|ИЗ
	|	ВТСтрокиКОбработке КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучетПервичногоДокументаПараметр КАК БухучетПервичногоДокумента
	|		ПО Начисления.Начисление = БухучетПервичногоДокумента.Начисление
	|			И Начисления.ДокументОснованиеИсходный = БухучетПервичногоДокумента.ДокументОснование
	|			И (БухучетПервичногоДокумента.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|				ИЛИ &ИспользоватьСтатьиФинансирования
	|					И БухучетПервичногоДокумента.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|				ИЛИ &РаботаВБюджетномУчреждении
	|					И БухучетПервичногоДокумента.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|				ИЛИ &ЕстьЕНВД
	|					И БухучетПервичногоДокумента.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.Организация КАК Организация,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	БухучетПлановыхНачислений.ДокументОснование КАК ДокументОснование,
	|	МАКСИМУМ(БухучетПлановыхНачислений.Период) КАК Период
	|ПОМЕСТИТЬ ВТНастройкиДатыПоследнихДвижений
	|ИЗ
	|	ВТСтрокиКОбработке КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|		ПО Начисления.Начисление = НастройкиНачислений.Начисление
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетПлановыхНачислений КАК БухучетПлановыхНачислений
	|		ПО Начисления.Сотрудник = БухучетПлановыхНачислений.Сотрудник
	|			И Начисления.Организация = БухучетПлановыхНачислений.Организация
	|			И Начисления.Начисление = БухучетПлановыхНачислений.Начисление
	|			И Начисления.ДатаНачала >= БухучетПлановыхНачислений.Период
	|			И (Начисления.ДокументОснование = БухучетПлановыхНачислений.ДокументОснование
	|				ИЛИ НастройкиНачислений.ЭтоПособиеПоУходуЗаРебенкомДоПолутораЛет)
	|
	|СГРУППИРОВАТЬ ПО
	|	Начисления.ИдентификаторСтроки,
	|	Начисления.ДатаНачала,
	|	Начисления.Организация,
	|	Начисления.Начисление,
	|	Начисления.Сотрудник,
	|	БухучетПлановыхНачислений.ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыПоследнихДвижений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ДатыПоследнихДвижений.Начисление КАК Начисление,
	|	БухучетПлановыхНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетПлановыхНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетПлановыхНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетПлановыхНачислений.ОтношениеКЕНВД КАК ОтношениеКЕНВД
	|ПОМЕСТИТЬ ВТНастройкиБухучетаПлановыхНачислений
	|ИЗ
	|	ВТНастройкиДатыПоследнихДвижений КАК ДатыПоследнихДвижений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетПлановыхНачислений КАК БухучетПлановыхНачислений
	|		ПО ДатыПоследнихДвижений.Начисление = БухучетПлановыхНачислений.Начисление
	|			И ДатыПоследнихДвижений.Сотрудник = БухучетПлановыхНачислений.Сотрудник
	|			И ДатыПоследнихДвижений.Организация = БухучетПлановыхНачислений.Организация
	|			И ДатыПоследнихДвижений.Период = БухучетПлановыхНачислений.Период
	|			И ДатыПоследнихДвижений.ДокументОснование = БухучетПлановыхНачислений.ДокументОснование
	|			И (БухучетПлановыхНачислений.Используется)
	|			И (БухучетПлановыхНачислений.ДействуетДо = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ИЛИ БухучетПлановыхНачислений.ДействуетДо >= ДатыПоследнихДвижений.ДатаНачала)
	|			И (БухучетПлановыхНачислений.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|				ИЛИ &ИспользоватьСтатьиФинансирования
	|					И БухучетПлановыхНачислений.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|				ИЛИ &РаботаВБюджетномУчреждении
	|					И БухучетПлановыхНачислений.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|				ИЛИ &ЕстьЕНВД
	|					И БухучетПлановыхНачислений.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиБухучетаПервичныхДокументов.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НастройкиБухучетаСотрудников.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	НастройкиБухучетаПервичныхДокументов.Начисление КАК Начисление,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьСтатьиФинансирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|		КОГДА НастройкиБухучетаПервичныхДокументов.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|			ТОГДА НастройкиБухучетаПервичныхДокументов.СтатьяФинансирования
	|		КОГДА ЕСТЬNULL(НастройкиБухучетаПлановыхНачислений.СтатьяФинансирования, ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|			ТОГДА НастройкиБухучетаПлановыхНачислений.СтатьяФинансирования
	|		ИНАЧЕ НастройкиБухучетаСотрудников.СтатьяФинансирования
	|	КОНЕЦ КАК СтатьяФинансирования,
	|	ВЫБОР
	|		КОГДА НастройкиБухучетаПервичныхДокументов.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|			ТОГДА НастройкиБухучетаПервичныхДокументов.СтатьяРасходов
	|		КОГДА ЕСТЬNULL(НастройкиБухучетаПлановыхНачислений.СтатьяРасходов, ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|			ТОГДА НастройкиБухучетаПлановыхНачислений.СтатьяРасходов
	|		ИНАЧЕ НастройкиБухучетаСотрудников.СтатьяРасходов
	|	КОНЕЦ КАК СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА НастройкиБухучетаПервичныхДокументов.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|			ТОГДА НастройкиБухучетаПервичныхДокументов.СпособОтраженияЗарплатыВБухучете
	|		КОГДА ЕСТЬNULL(НастройкиБухучетаПлановыхНачислений.СпособОтраженияЗарплатыВБухучете, ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|			ТОГДА НастройкиБухучетаПлановыхНачислений.СпособОтраженияЗарплатыВБухучете
	|		ИНАЧЕ НастройкиБухучетаСотрудников.СпособОтраженияЗарплатыВБухучете
	|	КОНЕЦ КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА НЕ &ЕстьЕНВД
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД)
	|		КОГДА НастройкиБухучетаПервичныхДокументов.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
	|			ТОГДА НастройкиБухучетаПервичныхДокументов.ОтношениеКЕНВД
	|		КОГДА ЕСТЬNULL(НастройкиБухучетаПлановыхНачислений.ОтношениеКЕНВД, ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
	|			ТОГДА НастройкиБухучетаПлановыхНачислений.ОтношениеКЕНВД
	|		ИНАЧЕ НастройкиБухучетаСотрудников.ОтношениеКЕНВД
	|	КОНЕЦ КАК ОтношениеКЕНВД,
	|	ЕСТЬNULL(НастройкиНачислений.ЭтоРасходыФСС, ЛОЖЬ) КАК ЭтоРасходыФСС
	|ПОМЕСТИТЬ ВТБухучетНачисленийПоДокументамИПлановыеВременная
	|ИЗ
	|	ВТНастройкиБухучетаПервичныхДокументов КАК НастройкиБухучетаПервичныхДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаПлановыхНачислений КАК НастройкиБухучетаПлановыхНачислений
	|		ПО НастройкиБухучетаПервичныхДокументов.ИдентификаторСтроки = НастройкиБухучетаПлановыхНачислений.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|		ПО НастройкиБухучетаПервичныхДокументов.Начисление = НастройкиНачислений.Начисление
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаСотрудников КАК НастройкиБухучетаСотрудников
	|		ПО НастройкиБухучетаПервичныхДокументов.ИдентификаторСтроки = НастройкиБухучетаСотрудников.ИдентификаторСтроки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НастройкиБухучетаПлановыхНачислений.ИдентификаторСтроки,
	|	НастройкиБухучетаСотрудников.ТерриторияВыполненияРаботВОрганизации,
	|	НастройкиБухучетаПлановыхНачислений.Начисление,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьСтатьиФинансирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|		КОГДА НастройкиБухучетаПлановыхНачислений.СтатьяФинансирования <> ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|			ТОГДА НастройкиБухучетаПлановыхНачислений.СтатьяФинансирования
	|		ИНАЧЕ НастройкиБухучетаСотрудников.СтатьяФинансирования
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ &РаботаВБюджетномУчреждении
	|			ТОГДА НастройкиБухучетаСотрудников.СтатьяРасходов
	|		КОГДА НастройкиБухучетаПлановыхНачислений.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|			ТОГДА НастройкиБухучетаПлановыхНачислений.СтатьяРасходов
	|		ИНАЧЕ НастройкиБухучетаСотрудников.СтатьяРасходов
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НастройкиБухучетаПлановыхНачислений.СпособОтраженияЗарплатыВБухучете <> ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|			ТОГДА НастройкиБухучетаПлановыхНачислений.СпособОтраженияЗарплатыВБухучете
	|		ИНАЧЕ НастройкиБухучетаСотрудников.СпособОтраженияЗарплатыВБухучете
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ &ЕстьЕНВД
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД)
	|		КОГДА НастройкиБухучетаПлановыхНачислений.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
	|			ТОГДА НастройкиБухучетаПлановыхНачислений.ОтношениеКЕНВД
	|		ИНАЧЕ НастройкиБухучетаСотрудников.ОтношениеКЕНВД
	|	КОНЕЦ,
	|	ЕСТЬNULL(НастройкиНачислений.ЭтоРасходыФСС, ЛОЖЬ)
	|ИЗ
	|	ВТНастройкиБухучетаПлановыхНачислений КАК НастройкиБухучетаПлановыхНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаПервичныхДокументов КАК НастройкиБухучетаПервичныхДокументов
	|		ПО НастройкиБухучетаПлановыхНачислений.ИдентификаторСтроки = НастройкиБухучетаПервичныхДокументов.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|		ПО НастройкиБухучетаПлановыхНачислений.Начисление = НастройкиНачислений.Начисление
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаСотрудников КАК НастройкиБухучетаСотрудников
	|		ПО НастройкиБухучетаПлановыхНачислений.ИдентификаторСтроки = НастройкиБухучетаСотрудников.ИдентификаторСтроки
	|ГДЕ
	|	НастройкиБухучетаПервичныхДокументов.ИдентификаторСтроки ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	БухучетНачислений.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	БухучетНачислений.Начисление КАК Начисление,
	|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	ВЫБОР
	|		КОГДА БухучетНачислений.ЭтоРасходыФСС
	|			ТОГДА ВЫБОР
	|					КОГДА &РаботаВБюджетномУчреждении
	|						ТОГДА &Статья213
	|					ИНАЧЕ &СтатьяРасчетыПоОплатеТруда
	|				КОНЕЦ
	|		КОГДА &РаботаВБюджетномУчреждении
	|			ТОГДА БухучетНачислений.СтатьяРасходов
	|		ИНАЧЕ ЕСТЬNULL(СтатьиРасчетыПоОплатеТруда.СтатьяРасходов, &СтатьяРасчетыПоОплатеТруда)
	|	КОНЕЦ КАК СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА БухучетНачислений.ЭтоРасходыФСС
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|		ИНАЧЕ БухучетНачислений.СпособОтраженияЗарплатыВБухучете
	|	КОНЕЦ КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.ОтношениеКЕНВД КАК ОтношениеКЕНВД
	|ПОМЕСТИТЬ ВТНачисленияПоДокументамИПлановыеДляБухучета
	|ИЗ
	|	ВТБухучетНачисленийПоДокументамИПлановыеВременная КАК БухучетНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатьиРасчетыПоОплатеТрудаПервичныхДокументов КАК СтатьиРасчетыПоОплатеТруда
	|		ПО БухучетНачислений.ИдентификаторСтроки = СтатьиРасчетыПоОплатеТруда.ИдентификаторСтроки";
	Запрос.Выполнить();
	// Зарегистрируем обработанные идентификаторы строк.
	ДополнитьВТСтрокиКУдалению(Запрос, "ВТНачисленияПоДокументамИПлановыеДляБухучета");
	
	Запрос.Текст =
	"УНИЧТОЖИТЬ ВТНастройкиБухучетаПлановыхНачислений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТНастройкиДатыПоследнихДвижений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТБухучетПервичногоДокументаПараметр
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТБухучетНачисленийПоДокументамИПлановыеВременная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТНастройкиБухучетаПервичныхДокументов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСтрокиКОбработке";
	
	Запрос.Выполнить();

КонецПроцедуры

Процедура СоздатьВТБухучетНачисленийПоБазеСохраняемогоДС(Запрос, КоэффициентыРаспределенияДенежногоСодержания, СтрокиКоэффициентыСохраняемогоДС, ИмяВТНачисленияИсходная)

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.ПериодРегистрации КАК ПериодРегистрации,
	|	Начисления.Организация КАК Организация,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.Подразделение КАК Подразделение,
	|	Начисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.ВидОперации КАК ВидОперации,
	|	Начисления.ДокументОснование КАК ДокументОснование,
	|	НастройкиБухучетаСотрудников.СтатьяФинансирования КАК СтатьяФинансирования,
	|	НастройкиБухучетаСотрудников.СтатьяРасходов КАК СтатьяРасходов,
	|	НастройкиБухучетаСотрудников.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	НастройкиБухучетаСотрудников.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	НастройкиНачислений.ЭтоРасходыПоСтрахованиюРаботодатель КАК ЭтоРасходыПоСтрахованиюРаботодатель,
	|	НастройкиНачислений.ЭтоРасходыФСС КАК ЭтоРасходыФСС,
	|	НастройкиНачислений.НазначениеРасчетаСохраняемогоДенежногоСодержания КАК НазначениеРасчетаСохраняемогоДенежногоСодержания,
	|	Начисления.Сумма КАК Сумма
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтрокиКУдалению КАК СтрокиКУдалению
	|		ПО Начисления.ИдентификаторСтроки = СтрокиКУдалению.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаСотрудников КАК НастройкиБухучетаСотрудников
	|		ПО Начисления.ИдентификаторСтроки = НастройкиБухучетаСотрудников.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|		ПО Начисления.Начисление = НастройкиНачислений.Начисление
	|ГДЕ
	|	СтрокиКУдалению.ИдентификаторСтроки ЕСТЬ NULL
	|	И НастройкиНачислений.НазначениеРасчетаСохраняемогоДенежногоСодержания <> НЕОПРЕДЕЛЕНО";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		СоздатьПустуюВТБухучетНачислений(Запрос, "ВТБухучетНачисленийПоБазеСохраняемогоДС");
	Иначе
		
		ТаблицаБухучет = НоваяТаблицаБухучетНачисленияУдержанияПоСотрудникам();
		
		НачисленияПоБазе = РезультатЗапроса.Выгрузить();
		Отбор = Новый Структура("НазначениеРасчета,Сотрудник");
		
		Для каждого СтрокаНачисления Из НачисленияПоБазе Цикл
			
			Отбор.НазначениеРасчета = СтрокаНачисления.НазначениеРасчетаСохраняемогоДенежногоСодержания;
			Отбор.Сотрудник = СтрокаНачисления.Сотрудник;
			
			СтрокиОтражения = СтрокиКоэффициентыСохраняемогоДС[СтрокаНачисления.ИдентификаторСтроки];
			Если СтрокиОтражения = Неопределено Тогда
				// получим коэффициенты для всего документа
				СтрокиОтражения = КоэффициентыРаспределенияДенежногоСодержания;
			КонецЕсли;
			
			Если СтрокиОтражения = Неопределено Тогда
				Результаты = Неопределено;
			Иначе
				
				ИспользуемыеСтрокиОтражения = СтрокиОтражения.Скопировать(Отбор);
				Если ИспользуемыеСтрокиОтражения.Количество() > 0 Тогда
					Коэффициенты = ИспользуемыеСтрокиОтражения.ВыгрузитьКолонку("Коэффициент");
					Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СтрокаНачисления.Сумма, Коэффициенты);
				Иначе
					Результаты = Неопределено;
				КонецЕсли;
				
			КонецЕсли;
			
			Если Результаты = Неопределено Тогда
				СтрокаТаблицы = ТаблицаБухучет.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаНачисления);
				СтрокаТаблицы.ПодразделениеУчетаЗатрат = СтрокаНачисления.Подразделение;
				Продолжить;
			КонецЕсли;
			
			Индекс = 0;
			Для Каждого СтрокаОтражения Из ИспользуемыеСтрокиОтражения Цикл
				
				СтрокаТаблицы = ТаблицаБухучет.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаНачисления);
				СтрокаТаблицы.Сумма = Результаты[Индекс];
				СтрокаТаблицы.ОблагаетсяЕНВД = СтрокаОтражения.ОблагаетсяЕНВД;
				
				СтрокаТаблицы.ПодразделениеУчетаЗатрат = ?(ЗначениеЗаполнено(СтрокаОтражения.ПодразделениеУчетаЗатрат),СтрокаОтражения.ПодразделениеУчетаЗатрат,СтрокаНачисления.Подразделение);
				
				НоваяСтатьяФинансирования = ?(ЗначениеЗаполнено(СтрокаОтражения.СтатьяФинансирования),СтрокаОтражения.СтатьяФинансирования,СтрокаТаблицы.СтатьяФинансирования);
				НоваяСтатьяРасходов       = ?(ЗначениеЗаполнено(СтрокаОтражения.СтатьяРасходов),СтрокаОтражения.СтатьяРасходов,СтрокаТаблицы.СтатьяРасходов);
				НовыйСпособОтражения      = ?(ЗначениеЗаполнено(СтрокаОтражения.СпособОтраженияЗарплатыВБухучете),СтрокаОтражения.СпособОтраженияЗарплатыВБухучете,СтрокаТаблицы.СпособОтраженияЗарплатыВБухучете);
				
				СтрокаТаблицы.СтатьяФинансирования = НоваяСтатьяФинансирования;
				Если Не СтрокаНачисления.ЭтоРасходыФСС Тогда
					СтрокаТаблицы.СпособОтраженияЗарплатыВБухучете = НовыйСпособОтражения;
					Если Не СтрокаНачисления.ЭтоРасходыПоСтрахованиюРаботодатель Тогда
						СтрокаТаблицы.СтатьяРасходов = НоваяСтатьяРасходов;
					КонецЕсли;
				КонецЕсли;
				
				Индекс = Индекс + 1;
				
			КонецЦикла;
			
		КонецЦикла;	
		
		Запрос.УстановитьПараметр("ТаблицаБухучет", ТаблицаБухучет);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	БухучетНачислений.ПериодРегистрации КАК ПериодРегистрации,
		|	БухучетНачислений.Организация КАК Организация,
		|	БухучетНачислений.Сотрудник КАК Сотрудник,
		|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БухучетНачислений.Подразделение КАК Подразделение,
		|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачислений.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	БухучетНачислений.Начисление КАК Начисление,
		|	БухучетНачислений.ВидОперации КАК ВидОперации,
		|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
		|	БухучетНачислений.ДатаОкончания КАК ДатаОкончания,
		|	БухучетНачислений.ДокументОснование КАК ДокументОснование,
		|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачислений.Сумма КАК Сумма,
		|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД
		|ПОМЕСТИТЬ ВТБухучетНачисленийПоБазеСохраняемогоДС
		|ИЗ
		|	&ТаблицаБухучет КАК БухучетНачислений";
		Запрос.Выполнить();
		// Зарегистрируем обработанные идентификаторы строк.
		ДополнитьВТСтрокиКУдалению(Запрос, "ВТБухучетНачисленийПоБазеСохраняемогоДС");
		
	КонецЕсли;

КонецПроцедуры

Процедура СоздатьВТБухучетНачисленийПоБазеСреднегоЗаработка(Запрос, ИмяВТНачисленияИсходная, ПараметрыКоэффициенты, ПараметрыРаспределенияПоСтатьям)
	
	КоэффициентыСреднегоЗаработкаФССДокумента 	= ПараметрыКоэффициенты.КоэффициентыСреднегоЗаработкаФССДокумента;
	КоэффициентыСреднегоЗаработкаДокумента 		= ПараметрыКоэффициенты.КоэффициентыСреднегоЗаработкаДокумента;
	СтрокиКоэффициентыСреднегоЗаработка 		= ПараметрыКоэффициенты.СтрокиКоэффициентыСреднегоЗаработка;
	
	АктуальныеСтатьиФинансирования 	= ПараметрыРаспределенияПоСтатьям.АктуальныеСтатьиФинансирования;
	АктуальныеСпособыОтражения 		= ПараметрыРаспределенияПоСтатьям.АктуальныеСпособыОтражения;
	СтатьиФинансированияЗамены 		= ПараметрыРаспределенияПоСтатьям.СтатьиФинансированияЗамены;
	СтатьяИспользуетсяВБазеСреднего = ПараметрыРаспределенияПоСтатьям.СтатьяИспользуетсяВБазеСреднего;
	РазрешенныеКатегорииНачисленийСтатьиФинансирования = ПараметрыРаспределенияПоСтатьям.РазрешенныеКатегорииНачисленийСтатьиФинансирования;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.ПериодРегистрации КАК ПериодРегистрации,
	|	Начисления.Организация КАК Организация,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.Подразделение КАК Подразделение,
	|	Начисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.ВидОперации КАК ВидОперации,
	|	Начисления.ДокументОснование КАК ДокументОснование,
	|	НастройкиБухучетаСотрудников.СтатьяФинансирования КАК СтатьяФинансирования,
	|	НастройкиБухучетаСотрудников.СтатьяРасходов КАК СтатьяРасходов,
	|	НастройкиБухучетаСотрудников.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	НастройкиБухучетаСотрудников.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	НастройкиНачислений.ЭтоРасходыПоСтрахованиюРаботодатель КАК ЭтоРасходыПоСтрахованиюРаботодатель,
	|	НастройкиНачислений.СпособРасчетаПоСреднемуЗаработку КАК СпособРасчетаПоСреднемуЗаработку,
	|	НастройкиНачислений.ЭтоРасходыФСС КАК ЭтоРасходыФСС,
	|	НастройкиНачислений.ИспользуетСреднийЗаработокОбщий КАК ИспользуетСреднийЗаработокОбщий,
	|	НастройкиНачислений.КатегорияНачисления КАК КатегорияНачисления,
	|	Начисления.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА НЕ &РаботаВБюджетномУчреждении
	|			ТОГДА ЛОЖЬ
	|		КОГДА НастройкиНачислений.ЭтоРасходыПоСтрахованиюРаботодатель
	|			ТОГДА ЛОЖЬ
	|		КОГДА НЕ СтатьиРасходовНачисленийПоУмолчанию.СтатьяРасходов ЕСТЬ NULL
	|				И СтатьиРасходовНачисленийПоУмолчанию.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ИспользоватьНовуюСтатьюРасходов
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтрокиКУдалению КАК СтрокиКУдалению
	|		ПО Начисления.ИдентификаторСтроки = СтрокиКУдалению.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаСотрудников КАК НастройкиБухучетаСотрудников
	|		ПО Начисления.ИдентификаторСтроки = НастройкиБухучетаСотрудников.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|		ПО Начисления.Начисление = НастройкиНачислений.Начисление
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатьиРасходовНачисленийПоУмолчанию КАК СтатьиРасходовНачисленийПоУмолчанию
	|		ПО Начисления.Начисление = СтатьиРасходовНачисленийПоУмолчанию.Начисление
	|ГДЕ
	|	СтрокиКУдалению.ИдентификаторСтроки ЕСТЬ NULL
	|	И НастройкиНачислений.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазеСреднегоЗаработка)";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		СоздатьПустуюВТБухучетНачислений(Запрос, "ВТБухучетНачисленийПоБазеСреднегоЗаработка");
	Иначе
		
		ЕстьЕНВД = Запрос.Параметры.ЕстьЕНВД;
		ТаблицаБухучетПоСреднемуЗаработку = НоваяТаблицаБухучетНачисленияУдержанияПоСотрудникам();
		
		НачисленияПоБазе = РезультатЗапроса.Выгрузить();
		
		Для каждого СтрокаНачисления Из НачисленияПоБазе Цикл
			
			СтрокиОтражения = СтрокиКоэффициентыСреднегоЗаработка[СтрокаНачисления.ИдентификаторСтроки];
			Если СтрокиОтражения = Неопределено Тогда
				
				// получим коэффициенты для всего документа
				Если СтрокаНачисления.ИспользуетСреднийЗаработокОбщий Тогда
					КоэффициентыСреднегоЗаработкаПоСотруднику = КоэффициентыСреднегоЗаработкаДокумента[СтрокаНачисления.Сотрудник];
					Если КоэффициентыСреднегоЗаработкаПоСотруднику = Неопределено Тогда
						СтрокиОтражения = КоэффициентыСреднегоЗаработкаДокумента[СтрокаНачисления.СпособРасчетаПоСреднемуЗаработку];
					Иначе
						СтрокиОтражения = КоэффициентыСреднегоЗаработкаПоСотруднику[СтрокаНачисления.СпособРасчетаПоСреднемуЗаработку];
					КонецЕсли;
				Иначе
					СтрокиОтражения = КоэффициентыСреднегоЗаработкаФССДокумента;
				КонецЕсли;
				
			КонецЕсли;
			
			Если СтрокиОтражения = Неопределено Тогда
				Результаты = Неопределено;
			Иначе
				Коэффициенты = СтрокиОтражения.ВыгрузитьКолонку("Коэффициент");
				Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СтрокаНачисления.Сумма, Коэффициенты);	
			КонецЕсли;
			
			Если Результаты = Неопределено Тогда
				СтрокаТаблицы = ТаблицаБухучетПоСреднемуЗаработку.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаНачисления);
				Продолжить;
			КонецЕсли;
			
			Индекс = 0;
			Для Каждого СтрокаОтражения Из СтрокиОтражения Цикл
				
				СтрокаТаблицы = ТаблицаБухучетПоСреднемуЗаработку.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаНачисления);
				СтрокаТаблицы.Сумма = Результаты[Индекс];
				Если ЕстьЕНВД Тогда
					СтрокаТаблицы.ОблагаетсяЕНВД = СтрокаОтражения.ОблагаетсяЕНВД;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(СтрокаОтражения.СтатьяФинансирования) Тогда
					Если СтатьяИспользуетсяВБазеСреднего[СтрокаОтражения.СтатьяФинансирования] Тогда
						НоваяСтатьяФинансирования = АктуальныеСтатьиФинансирования[СтрокаОтражения.СтатьяФинансирования];
					Иначе
						РазрешенныеКатегорииНачислений = РазрешенныеКатегорииНачисленийСтатьиФинансирования[СтрокаОтражения.СтатьяФинансирования];
						Если ЗначениеЗаполнено(РазрешенныеКатегорииНачислений)
							И РазрешенныеКатегорииНачислений.Найти(СтрокаНачисления.КатегорияНачисления) <> Неопределено Тогда
							НоваяСтатьяФинансирования = АктуальныеСтатьиФинансирования[СтрокаОтражения.СтатьяФинансирования];
						Иначе
							НоваяСтатьяФинансирования = СтатьиФинансированияЗамены[СтрокаОтражения.СтатьяФинансирования];
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				НовыйСпособОтражения = СтрокаТаблицы.СпособОтраженияЗарплатыВБухучете;
				Если ЗначениеЗаполнено(СтрокаОтражения.СпособОтраженияЗарплатыВБухучете) Тогда
					НовыйСпособОтражения = АктуальныеСпособыОтражения[СтрокаОтражения.СпособОтраженияЗарплатыВБухучете];
				КонецЕсли;
				
				НоваяСтатьяРасходов = ?(ЗначениеЗаполнено(СтрокаОтражения.СтатьяРасходов),СтрокаОтражения.СтатьяРасходов,СтрокаТаблицы.СтатьяРасходов);
				
				СтрокаТаблицы.СтатьяФинансирования = НоваяСтатьяФинансирования;
				Если Не СтрокаНачисления.ЭтоРасходыФСС Тогда
					СтрокаТаблицы.СпособОтраженияЗарплатыВБухучете = НовыйСпособОтражения;
					Если СтрокаНачисления.ИспользоватьНовуюСтатьюРасходов Тогда
						СтрокаТаблицы.СтатьяРасходов = НоваяСтатьяРасходов;
					КонецЕсли;
				КонецЕсли;
				
				Индекс = Индекс + 1;
				
			КонецЦикла;
			
		КонецЦикла;	
		
		Запрос.УстановитьПараметр("ТаблицаБухучетПоСреднемуЗаработку", ТаблицаБухучетПоСреднемуЗаработку);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	БухучетНачислений.ПериодРегистрации КАК ПериодРегистрации,
		|	БухучетНачислений.Организация КАК Организация,
		|	БухучетНачислений.Сотрудник КАК Сотрудник,
		|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БухучетНачислений.Подразделение КАК Подразделение,
		|	БухучетНачислений.Подразделение КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачислений.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	БухучетНачислений.Начисление КАК Начисление,
		|	БухучетНачислений.ВидОперации КАК ВидОперации,
		|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
		|	БухучетНачислений.ДатаОкончания КАК ДатаОкончания,
		|	БухучетНачислений.ДокументОснование КАК ДокументОснование,
		|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачислений.Сумма КАК Сумма,
		|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД
		|ПОМЕСТИТЬ ВТБухучетНачисленийПоБазеСреднегоЗаработка
		|ИЗ
		|	&ТаблицаБухучетПоСреднемуЗаработку КАК БухучетНачислений";
		Запрос.Выполнить();
		// Зарегистрируем обработанные идентификаторы строк.
		ДополнитьВТСтрокиКУдалению(Запрос, "ВТБухучетНачисленийПоБазеСреднегоЗаработка");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьВТБухучетНачисленийПоФактическимНачислениям(Запрос, ИмяВТНачисленияИсходная, ПараметрыРаспределенияПоСтатьям)
	
	АктуальныеСтатьиФинансирования 	= ПараметрыРаспределенияПоСтатьям.АктуальныеСтатьиФинансирования;
	АктуальныеСпособыОтражения 		= ПараметрыРаспределенияПоСтатьям.АктуальныеСпособыОтражения;
	СтатьиФинансированияЗамены 		= ПараметрыРаспределенияПоСтатьям.СтатьиФинансированияЗамены;
	СтатьяИспользуетсяВБазеСреднего = ПараметрыРаспределенияПоСтатьям.СтатьяИспользуетсяВБазеСреднего;
	РазрешенныеКатегорииНачисленийСтатьиФинансирования = ПараметрыРаспределенияПоСтатьям.РазрешенныеКатегорииНачисленийСтатьиФинансирования;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.ПериодРегистрации КАК ПериодРегистрации,
	|	Начисления.Организация КАК Организация,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.Подразделение КАК Подразделение,
	|	Начисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.ВидОперации КАК ВидОперации,
	|	Начисления.ДокументОснование КАК ДокументОснование,
	|	НастройкиБухучетаСотрудников.СтатьяФинансирования КАК СтатьяФинансирования,
	|	НастройкиБухучетаСотрудников.СтатьяРасходов КАК СтатьяРасходов,
	|	НастройкиБухучетаСотрудников.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	НастройкиБухучетаСотрудников.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	НастройкиНачислений.ЭтоРасходыПоСтрахованиюРаботодатель КАК ЭтоРасходыПоСтрахованиюРаботодатель,
	|	НастройкиНачислений.КатегорияНачисления КАК КатегорияНачисления,
	|	Начисления.Сумма КАК Сумма
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтрокиКУдалению КАК СтрокиКУдалению
	|		ПО Начисления.ИдентификаторСтроки = СтрокиКУдалению.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаСотрудников КАК НастройкиБухучетаСотрудников
	|		ПО Начисления.ИдентификаторСтроки = НастройкиБухучетаСотрудников.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|		ПО Начисления.Начисление = НастройкиНачислений.Начисление
	|ГДЕ
	|	СтрокиКУдалению.ИдентификаторСтроки ЕСТЬ NULL
	|	И НастройкиНачислений.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоФактическимНачислениям)";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		СоздатьПустуюВТБухучетНачислений(Запрос, "ВТБухучетНачисленийПоФактическимНачислениям");
	Иначе
		
		ЕстьЕНВД = Запрос.Параметры.ЕстьЕНВД;
		ПериодРегистрации = Запрос.Параметры.ПериодРегистрации;
		
		ТаблицаБухучетПоФактическимНачислениям = НоваяТаблицаБухучетНачисленияУдержанияПоСотрудникам();
		
		Если Месяц(ПериодРегистрации) > 1 Тогда
			// для первого месяца нет смысла получать фактические начисления
			НачПериода = НачалоГода(ПериодРегистрации);
			КонПериода = КонецМесяца(ДобавитьМесяц(ПериодРегистрации, -1));
			
			НачисленияПоБазе = РезультатЗапроса.Выгрузить();
			ФизическиеЛицаДляОтбора = НачисленияПоБазе.ВыгрузитьКолонку("ФизическоеЛицо");
			Запрос.УстановитьПараметр("ФизическиеЛицаДляОтбора",ФизическиеЛицаДляОтбора);
			Запрос.УстановитьПараметр("НачПериода",НачПериода);
			Запрос.УстановитьПараметр("КонПериода",КонПериода);
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	БухучетНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
			|	БухучетНачисления.СтатьяФинансирования КАК СтатьяФинансирования,
			|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	БухучетНачисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
			|	СУММА(БухучетНачисления.Сумма) КАК Сумма
			|ИЗ
			|	РегистрНакопления.БухучетНачисленияУдержанияПоСотрудникам КАК БухучетНачисления
			|ГДЕ
			|	БухучетНачисления.Период МЕЖДУ &НачПериода И &КонПериода
			|	И БухучетНачисления.Организация = &Организация
			|	И БухучетНачисления.ФизическоеЛицо В(&ФизическиеЛицаДляОтбора)
			|	И БухучетНачисления.НачислениеУдержание.КодДоходаСтраховыеВзносы.ВходитВБазуФСС
			|	И БухучетНачисления.Регистратор <> &ИсключаемыйРегистратор
			|
			|СГРУППИРОВАТЬ ПО
			|	БухучетНачисления.СтатьяФинансирования,
			|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете,
			|	БухучетНачисления.ОблагаетсяЕНВД,
			|	БухучетНачисления.ФизическоеЛицо
			|
			|ИМЕЮЩИЕ
			|	СУММА(БухучетНачисления.Сумма) > 0";
			
			БазаДляРасходовРаботодателя = Запрос.Выполнить().Выгрузить();
			БазаДляРасходовФСС = БазаДляРасходовРаботодателя.Скопировать();
			БазаДляРасходовФСС.Свернуть("ФизическоеЛицо,СтатьяФинансирования,ОблагаетсяЕНВД","Сумма");
			
			БазаДляРасходовРаботодателя.Индексы.Добавить("ФизическоеЛицо");
			БазаДляРасходовФСС.Индексы.Добавить("ФизическоеЛицо");
			Отбор = Новый Структура("ФизическоеЛицо");
			
			Для каждого СтрокаНачисления Из НачисленияПоБазе Цикл
				
				Если СтрокаНачисления.ЭтоРасходыПоСтрахованиюРаботодатель Тогда
					БазаРаспределения = БазаДляРасходовРаботодателя;
				Иначе	
					БазаРаспределения = БазаДляРасходовФСС;
				КонецЕсли;	
				
				ЗаполнитьЗначенияСвойств(Отбор, СтрокаНачисления);
				СтрокиОтражения = БазаРаспределения.НайтиСтроки(Отбор);
				Если СтрокиОтражения.Количество() = 0 Тогда
					СтрокаТаблицы = ТаблицаБухучетПоФактическимНачислениям.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаНачисления);
					Продолжить;	
				КонецЕсли;	
				Коэффициенты = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиОтражения,"Сумма");
				Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СтрокаНачисления.Сумма, Коэффициенты);
				
				Если Результаты = Неопределено Тогда
					СтрокаТаблицы = ТаблицаБухучетПоФактическимНачислениям.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаНачисления);
					Продолжить;
				КонецЕсли;
				
				Индекс = 0;
				Для Каждого СтрокаОтражения Из СтрокиОтражения Цикл
					
					СтрокаТаблицы = ТаблицаБухучетПоФактическимНачислениям.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаНачисления);
					СтрокаТаблицы.Сумма = Результаты[Индекс];
					Если ЕстьЕНВД Тогда
						СтрокаТаблицы.ОблагаетсяЕНВД = СтрокаОтражения.ОблагаетсяЕНВД;
					КонецЕсли;
					
					Если ЗначениеЗаполнено(СтрокаОтражения.СтатьяФинансирования) Тогда
						Если СтатьяИспользуетсяВБазеСреднего[СтрокаОтражения.СтатьяФинансирования] Тогда
							СтрокаТаблицы.СтатьяФинансирования = АктуальныеСтатьиФинансирования[СтрокаОтражения.СтатьяФинансирования];
						Иначе
							РазрешенныеКатегорииНачислений = РазрешенныеКатегорииНачисленийСтатьиФинансирования[СтрокаОтражения.СтатьяФинансирования];
							Если ЗначениеЗаполнено(РазрешенныеКатегорииНачислений)
								И РазрешенныеКатегорииНачислений.Найти(СтрокаНачисления.КатегорияНачисления) <> Неопределено Тогда
								СтрокаТаблицы.СтатьяФинансирования = АктуальныеСтатьиФинансирования[СтрокаОтражения.СтатьяФинансирования];
							Иначе
								СтрокаТаблицы.СтатьяФинансирования = СтатьиФинансированияЗамены[СтрокаОтражения.СтатьяФинансирования];
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
					
					Если СтрокаНачисления.ЭтоРасходыПоСтрахованиюРаботодатель Тогда
						Если ЗначениеЗаполнено(СтрокаОтражения.СпособОтраженияЗарплатыВБухучете) Тогда
							СтрокаТаблицы.СпособОтраженияЗарплатыВБухучете = АктуальныеСпособыОтражения[СтрокаОтражения.СпособОтраженияЗарплатыВБухучете];
						КонецЕсли;
					КонецЕсли;	
					
					Индекс = Индекс + 1;
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Запрос.УстановитьПараметр("ТаблицаБухучетПоФактическимНачислениям", ТаблицаБухучетПоФактическимНачислениям);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	БухучетНачислений.ПериодРегистрации КАК ПериодРегистрации,
		|	БухучетНачислений.Организация КАК Организация,
		|	БухучетНачислений.Сотрудник КАК Сотрудник,
		|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БухучетНачислений.Подразделение КАК Подразделение,
		|	БухучетНачислений.Подразделение КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачислений.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	БухучетНачислений.Начисление КАК Начисление,
		|	БухучетНачислений.ВидОперации КАК ВидОперации,
		|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
		|	БухучетНачислений.ДатаОкончания КАК ДатаОкончания,
		|	БухучетНачислений.ДокументОснование КАК ДокументОснование,
		|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачислений.Сумма КАК Сумма,
		|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД
		|ПОМЕСТИТЬ ВТБухучетНачисленийПоФактическимНачислениям
		|ИЗ
		|	&ТаблицаБухучетПоФактическимНачислениям КАК БухучетНачислений";
		Запрос.Выполнить();
		// Зарегистрируем обработанные идентификаторы строк.
		ДополнитьВТСтрокиКУдалению(Запрос, "ВТБухучетНачисленийПоФактическимНачислениям");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьВТНачисленияКакЗаданоВидуРасчетаДляБухучета(Запрос, ИмяВТНачисленияИсходная)
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СписокНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СписокНачислений.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	СписокНачислений.Начисление КАК Начисление,
	|	НастройкиБухучетаСотрудников.СтатьяФинансирования КАК СтатьяФинансирования,
	|	НастройкиБухучетаСотрудников.СтатьяРасходов КАК СтатьяРасходов,
	|	НастройкиБухучетаСотрудников.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	НастройкиБухучетаСотрудников.ОтношениеКЕНВД КАК ОтношениеКЕНВД
	|ПОМЕСТИТЬ ВТНачисленияКакЗаданоВидуРасчетаДляБухучета
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК СписокНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтрокиКУдалению КАК СтрокиКУдалению
	|		ПО СписокНачислений.ИдентификаторСтроки = СтрокиКУдалению.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаСотрудников КАК НастройкиБухучетаСотрудников
	|		ПО СписокНачислений.ИдентификаторСтроки = НастройкиБухучетаСотрудников.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|		ПО СписокНачислений.Начисление = НастройкиНачислений.Начисление
	|ГДЕ
	|	СтрокиКУдалению.ИдентификаторСтроки ЕСТЬ NULL
	|	И НастройкиНачислений.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.КакЗаданоВидуРасчета)";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	Запрос.Выполнить();
	// Зарегистрируем обработанные идентификаторы строк.
	ДополнитьВТСтрокиКУдалению(Запрос, "ВТНачисленияКакЗаданоВидуРасчетаДляБухучета");
	
КонецПроцедуры

Процедура СоздатьВТНачисленияПоБазе(Запрос, УчитыватьСторноЗаписи, ИмяВТНачисленияИсходная)
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокНачислений.ПериодРегистрации КАК ПериодРегистрации,
	|	СписокНачислений.Организация КАК Организация,
	|	СписокНачислений.Сотрудник КАК Сотрудник,
	|	СписокНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СписокНачислений.Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА НЕ СписокНачислений.ТерриторияВыполненияРаботВОрганизации В (&ПустыеТерритории)
	|			ТОГДА СписокНачислений.ТерриторияВыполненияРаботВОрганизации
	|		ИНАЧЕ СписокНачислений.Подразделение
	|	КОНЕЦ КАК ТерриторияВыполненияРаботВОрганизации,
	|	СписокНачислений.Начисление КАК Начисление,
	|	СписокНачислений.ВидОперации КАК ВидОперации,
	|	СписокНачислений.ДатаНачала КАК ДатаНачала,
	|	СписокНачислений.ДатаОкончания КАК ДатаОкончания,
	|	СписокНачислений.Сумма КАК Сумма,
	|	СписокНачислений.ДокументОснование КАК ДокументОснование,
	|	СписокНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СписокНачислений.Сторно КАК Сторно,
	|	СписокНачислений.ФиксСторно КАК ФиксСторно,
	|	Начисления.ОчередностьОтраженияВУчете КАК ОчередностьОтраженияВУчете,
	|	НастройкиБухучетаСотрудников.СтатьяФинансирования КАК СтатьяФинансирования,
	|	НастройкиБухучетаСотрудников.СтатьяРасходов КАК СтатьяРасходов,
	|	НастройкиБухучетаСотрудников.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА НастройкиБухучетаСотрудников.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ОпределяетсяЕжемесячноПроцентом)
	|				ИЛИ НастройкиБухучетаСотрудников.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОблагаетсяЕНВД,
	|	СписокНачислений.РассчитыватьПоРазовымНачислениямДокумента КАК РассчитыватьПоРазовымНачислениямДокумента
	|ПОМЕСТИТЬ ВТНачисленияПоБазе
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК СписокНачислений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК Начисления
	|		ПО (Начисления.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам))
	|			И СписокНачислений.Начисление = Начисления.Начисление
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтрокиКУдалению КАК СтрокиКУдалению
	|		ПО СписокНачислений.ИдентификаторСтроки = СтрокиКУдалению.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаСотрудников КАК НастройкиБухучетаСотрудников
	|		ПО СписокНачислений.ИдентификаторСтроки = НастройкиБухучетаСотрудников.ИдентификаторСтроки
	|ГДЕ
	|	СтрокиКУдалению.ИдентификаторСтроки ЕСТЬ NULL";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	Если Не УчитыватьСторноЗаписи Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "СписокНачислений.Сторно КАК Сторно,", "ЛОЖЬ КАК Сторно,");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "СписокНачислений.ФиксСторно КАК ФиксСторно,", "ЛОЖЬ КАК ФиксСторно,");
	КонецЕсли;
	Запрос.Выполнить();
	// Зарегистрируем обработанные идентификаторы строк.
	ДополнитьВТСтрокиКУдалению(Запрос, "ВТНачисленияПоБазе");
	
КонецПроцедуры

Процедура СоздатьВТБухучетОсновногоЗаработка(Запрос, ИмяВТНачисленияИсходная, РаспределениеСохраняемогоДС)

	УдалитьВТ = Новый Массив;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Организация КАК Организация,
	|	Начисления.Сотрудник КАК СотрудникДляСоединения,
	|	ЕСТЬNULL(НастройкиНачислений.ЭтоРасходыФСС, ЛОЖЬ) КАК ЭтоРасходыФСС,
	|	ВЫБОР
	|		КОГДА НЕ &РаботаВБюджетномУчреждении
	|			ТОГДА ЕСТЬNULL(ЕСТЬNULL(СтатьиРасчетыПоОплатеТруда.СтатьяРасходов, НастройкиНачислений.СтатьяРасчетыПоОплатеТруда), &СтатьяРасчетыПоОплатеТруда)
	|		КОГДА ЕСТЬNULL(НастройкиНачислений.ЭтоОплатаДнейУходаЗаДетьмиИнвалидами, ЛОЖЬ)
	|			ТОГДА ЕСТЬNULL(СтатьиРасходовНачисленийПоУмолчанию.СтатьяРасходов, &Статья213)
	|		КОГДА ЕСТЬNULL(НастройкиНачислений.ЭтоРасходыФСС, ЛОЖЬ)
	|			ТОГДА &Статья213
	|		КОГДА НЕ СтатьиРасходовНачисленийПоУмолчанию.СтатьяРасходов ЕСТЬ NULL
	|				И СтатьиРасходовНачисленийПоУмолчанию.СтатьяРасходов <> ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|			ТОГДА СтатьиРасходовНачисленийПоУмолчанию.СтатьяРасходов
	|		ИНАЧЕ &Статья211
	|	КОНЕЦ КАК СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА Начисления.ДатаНачала > КОНЕЦПЕРИОДА(Начисления.ПериодРегистрации, МЕСЯЦ)
	|			ТОГДА Начисления.ПериодРегистрации
	|		ИНАЧЕ Начисления.ДатаНачала
	|	КОНЕЦ КАК ДатаНачала
	|ПОМЕСТИТЬ ВТСтрокиКОбработке
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтрокиКУдалению КАК СтрокиКУдалению
	|		ПО Начисления.ИдентификаторСтроки = СтрокиКУдалению.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|		ПО Начисления.Начисление = НастройкиНачислений.Начисление
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатьиРасходовНачисленийПоУмолчанию КАК СтатьиРасходовНачисленийПоУмолчанию
	|		ПО Начисления.Начисление = СтатьиРасходовНачисленийПоУмолчанию.Начисление
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатьиРасчетыПоОплатеТрудаПервичныхДокументов КАК СтатьиРасчетыПоОплатеТруда
	|		ПО Начисления.ИдентификаторСтроки = СтатьиРасчетыПоОплатеТруда.ИдентификаторСтроки
	|ГДЕ
	|	СтрокиКУдалению.ИдентификаторСтроки ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	РаспределениеОсновногоЗаработка.СтатьяФинансирования КАК СтатьяФинансирования,
	|	Начисления.СтатьяРасходов КАК СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА Начисления.ЭтоРасходыФСС
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка)
	|		ИНАЧЕ РаспределениеОсновногоЗаработка.СпособОтраженияЗарплатыВБухучете
	|	КОНЕЦ КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА НЕ &ЕстьЕНВД
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ РаспределениеОсновногоЗаработка.ОблагаетсяЕНВД
	|	КОНЕЦ КАК ОблагаетсяЕНВД,
	|	РаспределениеОсновногоЗаработка.ДоляРаспределения КАК ДоляРаспределения,
	|	РаспределениеОсновногоЗаработка.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат
	|ПОМЕСТИТЬ ВТСтрокиСРаспределением
	|ИЗ
	|	ВТСтрокиКОбработке КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетРаспределениеОсновногоЗаработка КАК РаспределениеОсновногоЗаработка
	|		ПО Начисления.СотрудникДляСоединения = РаспределениеОсновногоЗаработка.Сотрудник
	|			И Начисления.Организация = РаспределениеОсновногоЗаработка.Организация
	|			И (Начисления.ДатаНачала МЕЖДУ РаспределениеОсновногоЗаработка.ПериодРегистрации И КОНЕЦПЕРИОДА(РаспределениеОсновногоЗаработка.ПериодРегистрации, МЕСЯЦ))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтрокиСРаспределением.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТСтрокиОтражения
	|ИЗ
	|	ВТСтрокиСРаспределением КАК СтрокиСРаспределением
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтрокиСРаспределением.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СтрокиСРаспределением.СтатьяФинансирования КАК СтатьяФинансирования,
	|	СтрокиСРаспределением.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	СтрокиСРаспределением.СтатьяРасходов КАК СтатьяРасходов,
	|	СтрокиСРаспределением.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	СтрокиСРаспределением.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	СтрокиСРаспределением.ДоляРаспределения КАК ДоляРаспределения
	|ИЗ
	|	ВТСтрокиСРаспределением КАК СтрокиСРаспределением
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.ПериодРегистрации КАК ПериодРегистрации,
	|	Начисления.Организация КАК Организация,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.Подразделение КАК Подразделение,
	|	Начисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.ВидОперации КАК ВидОперации,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.ДокументОснование КАК ДокументОснование,
	|	Начисления.Сумма КАК Сумма,
	|	НастройкиБухучетаСотрудников.СтатьяФинансирования КАК СтатьяФинансирования,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НастройкиНачислений.ЭтоРасходыФСС, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка)
	|		ИНАЧЕ НастройкиБухучетаСотрудников.СпособОтраженияЗарплатыВБухучете
	|	КОНЕЦ КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА НЕ &РаботаВБюджетномУчреждении
	|			ТОГДА ЕСТЬNULL(НастройкиНачислений.СтатьяРасчетыПоОплатеТруда, &СтатьяРасчетыПоОплатеТруда)
	|		КОГДА ЕСТЬNULL(НастройкиНачислений.ЭтоРасходыФСС, ЛОЖЬ)
	|			ТОГДА &Статья213
	|		ИНАЧЕ НастройкиБухучетаСотрудников.СтатьяРасходов
	|	КОНЕЦ КАК СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА НастройкиБухучетаСотрудников.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ОпределяетсяЕжемесячноПроцентом)
	|				ИЛИ НастройкиБухучетаСотрудников.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОблагаетсяЕНВД
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСтрокиОтражения КАК СтрокиОтражения
	|		ПО Начисления.ИдентификаторСтроки = СтрокиОтражения.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|		ПО Начисления.Начисление = НастройкиНачислений.Начисление
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаСотрудников КАК НастройкиБухучетаСотрудников
	|		ПО Начисления.ИдентификаторСтроки = НастройкиБухучетаСотрудников.ИдентификаторСтроки";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	Если РаспределениеСохраняемогоДС Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Начисления.Сотрудник КАК СотрудникДляСоединения", "Начисления.ВременныйСотрудник КАК СотрудникДляСоединения");
	КонецЕсли;
	Результат = Запрос.ВыполнитьПакет();
	КоличествоРезультатов = Результат.ВГраница();
	Распределение = Результат[КоличествоРезультатов-1].Выгрузить();
	Начисления = Результат[КоличествоРезультатов].Выгрузить();
	
	УдалитьВТ.Добавить("ВТСтрокиСРаспределением");
	УдалитьВТ.Добавить("ВТСтрокиОтражения");
	УдалитьВТ.Добавить("ВТСтрокиКОбработке");
	
	Если Распределение.Количество() = 0 Тогда
		СоздатьПустуюВТБухучетНачислений(Запрос, "ВТБухучетОсновногоЗаработка");
	Иначе
		
		ТаблицаБухучетНачислений = НоваяТаблицаБухучетНачисленияУдержанияПоСотрудникам();
		
		Распределение.Индексы.Добавить("ИдентификаторСтроки");
		
		Отбор = Новый Структура("ИдентификаторСтроки");
		Для каждого СтрокаНачисления Из Начисления Цикл
			
			ЗаполнитьЗначенияСвойств(Отбор, СтрокаНачисления);
			СтрокиОтражения = Распределение.НайтиСтроки(Отбор);
			Коэффициенты = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиОтражения,"ДоляРаспределения");
			Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СтрокаНачисления.Сумма, Коэффициенты);
			
			Если Результаты = Неопределено Тогда
				СтрокаТаблицы = ТаблицаБухучетНачислений.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаНачисления);
				СтрокаТаблицы.ПодразделениеУчетаЗатрат = СтрокаТаблицы.Подразделение;
				Продолжить;
			КонецЕсли;
			
			Индекс = 0;
			Для Каждого СтрокаОтражения Из СтрокиОтражения Цикл
				
				СтрокаТаблицы = ТаблицаБухучетНачислений.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаНачисления);
				СтрокаТаблицы.Сумма = Результаты[Индекс];
				СтрокаТаблицы.СтатьяФинансирования = СтрокаОтражения.СтатьяФинансирования;
				СтрокаТаблицы.СпособОтраженияЗарплатыВБухучете = СтрокаОтражения.СпособОтраженияЗарплатыВБухучете;
				СтрокаТаблицы.СтатьяРасходов = СтрокаОтражения.СтатьяРасходов;
				СтрокаТаблицы.ОблагаетсяЕНВД = СтрокаОтражения.ОблагаетсяЕНВД;
				СтрокаТаблицы.ПодразделениеУчетаЗатрат = ?(ЗначениеЗаполнено(СтрокаОтражения.ПодразделениеУчетаЗатрат),СтрокаОтражения.ПодразделениеУчетаЗатрат,СтрокаТаблицы.Подразделение);
				
				Индекс = Индекс + 1;
				
			КонецЦикла;
			
		КонецЦикла;	
		
		Запрос.УстановитьПараметр("ТаблицаБухучетОсновногоЗаработка", ТаблицаБухучетНачислений);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	БухучетНачисления.ПериодРегистрации КАК ПериодРегистрации,
		|	БухучетНачисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	БухучетНачисления.ПериодПринятияРасходов КАК ПериодПринятияРасходов,
		|	БухучетНачисления.Организация КАК Организация,
		|	БухучетНачисления.Сотрудник КАК Сотрудник,
		|	БухучетНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БухучетНачисления.Подразделение КАК Подразделение,
		|	БухучетНачисления.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	БухучетНачисления.Начисление КАК Начисление,
		|	БухучетНачисления.ВидОперации КАК ВидОперации,
		|	БухучетНачисления.ДатаНачала КАК ДатаНачала,
		|	БухучетНачисления.ДатаОкончания КАК ДатаОкончания,
		|	БухучетНачисления.ДокументОснование КАК ДокументОснование,
		|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачисления.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачисления.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачисления.Сумма КАК Сумма,
		|	БухучетНачисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД
		|ПОМЕСТИТЬ ВТБухучетОсновногоЗаработка
		|ИЗ
		|	&ТаблицаБухучетОсновногоЗаработка КАК БухучетНачисления";
		Запрос.Выполнить();
		// Зарегистрируем обработанные идентификаторы строк.
		ДополнитьВТСтрокиКУдалению(Запрос, "ВТБухучетОсновногоЗаработка");
		
	КонецЕсли;
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
	
КонецПроцедуры

Процедура СоздатьВТБухучетОсновногоЗаработкаРаспределениеПоПодразделению(Запрос, ИмяВТНачисленияИсходная)

	УдалитьВТ = Новый Массив;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.Подразделение КАК Подразделение,
	|	ВЫБОР
	|		КОГДА Начисления.ДатаНачала > КОНЕЦПЕРИОДА(Начисления.ПериодРегистрации, МЕСЯЦ)
	|			ТОГДА Начисления.ПериодРегистрации
	|		ИНАЧЕ Начисления.ДатаНачала
	|	КОНЕЦ КАК ДатаНачала
	|ПОМЕСТИТЬ ВТСтрокиКОбработке
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтрокиКУдалению КАК СтрокиКУдалению
	|		ПО Начисления.ИдентификаторСтроки = СтрокиКУдалению.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаСотрудников КАК НастройкиБухучетаСотрудников
	|		ПО Начисления.ИдентификаторСтроки = НастройкиБухучетаСотрудников.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|		ПО Начисления.Начисление = НастройкиНачислений.Начисление
	|ГДЕ
	|	СтрокиКУдалению.ИдентификаторСтроки ЕСТЬ NULL
	|	И НЕ НастройкиБухучетаСотрудников.ЕстьБухучетСотрудникаТерритории
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаксимальныеПериоды.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьСтатьиФинансирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|		ИНАЧЕ БухучетЗарплатыПодразделений.СтатьяФинансирования
	|	КОНЕЦ КАК СтатьяФинансирования,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НастройкиНачислений.ЭтоРасходыФСС, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка)
	|		ИНАЧЕ БухучетЗарплатыПодразделений.СпособОтраженияЗарплатыВБухучете
	|	КОНЕЦ КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА НЕ &РаботаВБюджетномУчреждении
	|			ТОГДА ЕСТЬNULL(СтатьиРасчетыПоОплатеТруда.СтатьяРасходов, НастройкиБухучетаСотрудников.СтатьяРасходов)
	|		КОГДА ЕСТЬNULL(НастройкиНачислений.ЭтоОплатаДнейУходаЗаДетьмиИнвалидами, ЛОЖЬ)
	|			ТОГДА ЕСТЬNULL(НастройкиБухучетаСотрудников.СтатьяРасходов, &Статья213)
	|		КОГДА ЕСТЬNULL(НастройкиНачислений.ЭтоРасходыФСС, ЛОЖЬ)
	|			ТОГДА &Статья213
	|		КОГДА ЕСТЬNULL(СтатьиРасчетыПоОплатеТруда.СтатьяРасходов, НастройкиБухучетаСотрудников.СтатьяРасходов) <> ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(СтатьиРасчетыПоОплатеТруда.СтатьяРасходов, НастройкиБухучетаСотрудников.СтатьяРасходов)
	|		ИНАЧЕ &Статья211
	|	КОНЕЦ КАК СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА НЕ &ЕстьЕНВД
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
	|		ИНАЧЕ БухучетЗарплатыПодразделений.ОтношениеКЕНВД
	|	КОНЕЦ КАК ОтношениеКЕНВД,
	|	БухучетЗарплатыПодразделений.ДоляРаспределения КАК ДоляРаспределения
	|ПОМЕСТИТЬ ВТСтрокиОтражения
	|ИЗ
	|	(ВЫБРАТЬ
	|		МаксимальныеПериоды.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|		МаксимальныеПериоды.Подразделение КАК Подразделение,
	|		МаксимальныеПериоды.Начисление КАК Начисление,
	|		МаксимальныеПериоды.ДатаНачала КАК ДатаНачала,
	|		МаксимальныеПериоды.Период КАК Период
	|	ИЗ
	|		(ВЫБРАТЬ
	|			СтрокиКОбработке.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|			СтрокиКОбработке.Подразделение КАК Подразделение,
	|			СтрокиКОбработке.Начисление КАК Начисление,
	|			СтрокиКОбработке.ДатаНачала КАК ДатаНачала,
	|			МАКСИМУМ(БухучетЗарплатыПодразделений.Период) КАК Период
	|		ИЗ
	|			ВТСтрокиКОбработке КАК СтрокиКОбработке
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыПодразделений КАК БухучетЗарплатыПодразделений
	|				ПО СтрокиКОбработке.Подразделение = БухучетЗарплатыПодразделений.Подразделение
	|					И СтрокиКОбработке.ДатаНачала >= БухучетЗарплатыПодразделений.Период
	|		
	|		СГРУППИРОВАТЬ ПО
	|			СтрокиКОбработке.ИдентификаторСтроки,
	|			СтрокиКОбработке.Начисление,
	|			СтрокиКОбработке.Подразделение,
	|			СтрокиКОбработке.ДатаНачала) КАК МаксимальныеПериоды
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыПодразделений КАК БухучетЗарплатыПодразделений
	|			ПО МаксимальныеПериоды.Подразделение = БухучетЗарплатыПодразделений.Подразделение
	|				И МаксимальныеПериоды.Период = БухучетЗарплатыПодразделений.Период
	|	
	|	СГРУППИРОВАТЬ ПО
	|		МаксимальныеПериоды.ИдентификаторСтроки,
	|		МаксимальныеПериоды.Подразделение,
	|		МаксимальныеПериоды.Начисление,
	|		МаксимальныеПериоды.ДатаНачала,
	|		МаксимальныеПериоды.Период
	|	
	|	ИМЕЮЩИЕ
	|		КОЛИЧЕСТВО(БухучетЗарплатыПодразделений.ИдентификаторСтроки) > 1) КАК МаксимальныеПериоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.БухучетЗарплатыПодразделений КАК БухучетЗарплатыПодразделений
	|		ПО МаксимальныеПериоды.Подразделение = БухучетЗарплатыПодразделений.Подразделение
	|			И МаксимальныеПериоды.Период = БухучетЗарплатыПодразделений.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаСотрудников КАК НастройкиБухучетаСотрудников
	|		ПО МаксимальныеПериоды.ИдентификаторСтроки = НастройкиБухучетаСотрудников.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатьиРасчетыПоОплатеТрудаПервичныхДокументов КАК СтатьиРасчетыПоОплатеТруда
	|		ПО МаксимальныеПериоды.ИдентификаторСтроки = СтатьиРасчетыПоОплатеТруда.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|		ПО МаксимальныеПериоды.Начисление = НастройкиНачислений.Начисление
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтрокиОтражения.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СтрокиОтражения.СтатьяФинансирования КАК СтатьяФинансирования,
	|	СтрокиОтражения.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	СтрокиОтражения.СтатьяРасходов КАК СтатьяРасходов,
	|	СтрокиОтражения.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	СтрокиОтражения.ДоляРаспределения КАК ДоляРаспределения
	|ИЗ
	|	ВТСтрокиОтражения КАК СтрокиОтражения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.ПериодРегистрации КАК ПериодРегистрации,
	|	Начисления.Организация КАК Организация,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.Подразделение КАК Подразделение,
	|	Начисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.ВидОперации КАК ВидОперации,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.ДокументОснование КАК ДокументОснование,
	|	Начисления.Сумма КАК Сумма
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСтрокиОтражения КАК СтрокиОтражения
	|		ПО Начисления.ИдентификаторСтроки = СтрокиОтражения.ИдентификаторСтроки";
	
	УдалитьВТ.Добавить("ВТСтрокиКОбработке");
	УдалитьВТ.Добавить("ВТСтрокиОтражения");
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	Результат = Запрос.ВыполнитьПакет();
	Распределение = Результат[2].Выгрузить();
	Начисления = Результат[3].Выгрузить();
	
	Если Распределение.Количество() = 0 Тогда
		СоздатьПустуюВТБухучетНачислений(Запрос, "ВТБухучетОсновногоЗаработкаРаспределениеПоПодразделению");
	Иначе
		
		ТаблицаБухучетНачислений = НоваяТаблицаБухучетНачисленияУдержанияПоСотрудникам();
		
		Распределение.Индексы.Добавить("ИдентификаторСтроки");
		
		Отбор = Новый Структура("ИдентификаторСтроки");
		Для каждого СтрокаНачисления Из Начисления Цикл
			
			Отбор.ИдентификаторСтроки = СтрокаНачисления.ИдентификаторСтроки;
			СтрокиОтражения = Распределение.НайтиСтроки(Отбор);
			Коэффициенты = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиОтражения,"ДоляРаспределения");
			Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СтрокаНачисления.Сумма, Коэффициенты);
			
			Индекс = 0;
			Для Каждого СтрокаОтражения Из СтрокиОтражения Цикл
				
				СтрокаТаблицы = ТаблицаБухучетНачислений.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаНачисления);
				СтрокаТаблицы.Сумма = ?(Результаты = Неопределено, 0, Результаты[Индекс]);
				СтрокаТаблицы.СтатьяФинансирования = СтрокаОтражения.СтатьяФинансирования;
				СтрокаТаблицы.СпособОтраженияЗарплатыВБухучете = СтрокаОтражения.СпособОтраженияЗарплатыВБухучете;
				СтрокаТаблицы.СтатьяРасходов = СтрокаОтражения.СтатьяРасходов;
				СтрокаТаблицы.ОтношениеКЕНВД = СтрокаОтражения.ОтношениеКЕНВД;
				
				Индекс = Индекс + 1;
				
			КонецЦикла;
			
		КонецЦикла;	
		
		Запрос.УстановитьПараметр("ТаблицаБухучетНачислений", ТаблицаБухучетНачислений);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	БухучетНачисления.ПериодРегистрации КАК ПериодРегистрации,
		|	БухучетНачисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	БухучетНачисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	БухучетНачисления.Организация КАК Организация,
		|	БухучетНачисления.Сотрудник КАК Сотрудник,
		|	БухучетНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БухучетНачисления.Подразделение КАК Подразделение,
		|	БухучетНачисления.Подразделение КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачисления.Начисление КАК Начисление,
		|	БухучетНачисления.ВидОперации КАК ВидОперации,
		|	БухучетНачисления.ДатаНачала КАК ДатаНачала,
		|	БухучетНачисления.ДатаОкончания КАК ДатаОкончания,
		|	БухучетНачисления.ДокументОснование КАК ДокументОснование,
		|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачисления.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачисления.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачисления.Сумма КАК Сумма,
		|	БухучетНачисления.ОтношениеКЕНВД КАК ОтношениеКЕНВД
		|ПОМЕСТИТЬ ВТБухучетОсновногоЗаработкаРаспределениеПоПодразделениюВременная
		|ИЗ
		|	&ТаблицаБухучетНачислений КАК БухучетНачисления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	БухучетОсновногоЗаработка.ПериодРегистрации КАК ПериодРегистрации,
		|	БухучетОсновногоЗаработка.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	БухучетОсновногоЗаработка.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	БухучетОсновногоЗаработка.Организация КАК Организация,
		|	БухучетОсновногоЗаработка.Сотрудник КАК Сотрудник,
		|	БухучетОсновногоЗаработка.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БухучетОсновногоЗаработка.Подразделение КАК Подразделение,
		|	БухучетОсновногоЗаработка.Подразделение КАК ПодразделениеУчетаЗатрат,
		|	БухучетОсновногоЗаработка.Начисление КАК Начисление,
		|	БухучетОсновногоЗаработка.ВидОперации КАК ВидОперации,
		|	БухучетОсновногоЗаработка.ДатаНачала КАК ДатаНачала,
		|	БухучетОсновногоЗаработка.ДатаОкончания КАК ДатаОкончания,
		|	БухучетОсновногоЗаработка.ДокументОснование КАК ДокументОснование,
		|	БухучетОсновногоЗаработка.Сумма КАК Сумма,
		|	БухучетОсновногоЗаработка.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетОсновногоЗаработка.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетОсновногоЗаработка.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	ВЫБОР
		|		КОГДА БухучетОсновногоЗаработка.ОтношениеКЕНВД <> ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
		|			ТОГДА ВЫБОР
		|					КОГДА БухучетОсновногоЗаработка.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД)
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ ЛОЖЬ
		|				КОНЕЦ
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ОблагаетсяЕНВД
		|ПОМЕСТИТЬ ВТБухучетОсновногоЗаработкаРаспределениеПоПодразделению
		|ИЗ
		|	ВТБухучетОсновногоЗаработкаРаспределениеПоПодразделениюВременная КАК БухучетОсновногоЗаработка";
		Запрос.Выполнить();
		// Зарегистрируем обработанные идентификаторы строк.
		ДополнитьВТСтрокиКУдалению(Запрос, "ВТБухучетОсновногоЗаработкаРаспределениеПоПодразделению");
		
		УдалитьВТ.Добавить("ВТБухучетОсновногоЗаработкаРаспределениеПоПодразделениюВременная");
		
	КонецЕсли;
	
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);

КонецПроцедуры

Процедура СоздатьВТБухучетНачисленийБезБазы(Запрос, ИмяВТНачисленияИсходная)
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	НастройкиБухучетаСотрудников.СтатьяФинансирования КАК СтатьяФинансирования,
	|	ВЫБОР
	|		КОГДА НЕ &РаботаВБюджетномУчреждении
	|			ТОГДА ЕСТЬNULL(СтатьиРасчетыПоОплатеТруда.СтатьяРасходов, ЕСТЬNULL(НастройкиНачислений.СтатьяРасчетыПоОплатеТруда, &СтатьяРасчетыПоОплатеТруда))
	|		КОГДА ЕСТЬNULL(НастройкиНачислений.ЭтоОплатаДнейУходаЗаДетьмиИнвалидами, ЛОЖЬ)
	|			ТОГДА НастройкиБухучетаСотрудников.СтатьяРасходов
	|		КОГДА ЕСТЬNULL(НастройкиНачислений.ЭтоРасходыФСС, ЛОЖЬ)
	|			ТОГДА &Статья213
	|		ИНАЧЕ НастройкиБухучетаСотрудников.СтатьяРасходов
	|	КОНЕЦ КАК СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НастройкиНачислений.ЭтоРасходыФСС, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка)
	|		ИНАЧЕ НастройкиБухучетаСотрудников.СпособОтраженияЗарплатыВБухучете
	|	КОНЕЦ КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА НЕ &ЕстьЕНВД
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД)
	|		ИНАЧЕ НастройкиБухучетаСотрудников.ОтношениеКЕНВД
	|	КОНЕЦ КАК ОтношениеКЕНВД
	|ПОМЕСТИТЬ ВТБухучетНачисленийВременная
	|ИЗ
	|	ВТНачисленияДляРаспределения КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтрокиКУдалению КАК СтрокиКУдалению
	|		ПО Начисления.ИдентификаторСтроки = СтрокиКУдалению.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиБухучетаСотрудников КАК НастройкиБухучетаСотрудников
	|		ПО Начисления.ИдентификаторСтроки = НастройкиБухучетаСотрудников.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкиНачислений КАК НастройкиНачислений
	|		ПО Начисления.Начисление = НастройкиНачислений.Начисление
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатьиРасчетыПоОплатеТрудаПервичныхДокументов КАК СтатьиРасчетыПоОплатеТруда
	|		ПО Начисления.ИдентификаторСтроки = СтатьиРасчетыПоОплатеТруда.ИдентификаторСтроки
	|ГДЕ
	|	СтрокиКУдалению.ИдентификаторСтроки ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	БухучетНачислений.ИдентификаторСтроки,
	|	БухучетНачислений.ТерриторияВыполненияРаботВОрганизации,
	|	БухучетНачислений.СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.ОтношениеКЕНВД
	|ИЗ
	|	ВТНачисленияПоДокументамИПлановыеДляБухучета КАК БухучетНачислений
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	БухучетНачислений.ИдентификаторСтроки,
	|	БухучетНачислений.ТерриторияВыполненияРаботВОрганизации,
	|	БухучетНачислений.СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.ОтношениеКЕНВД
	|ИЗ
	|	ВТНачисленияКакЗаданоВидуРасчетаДляБухучета КАК БухучетНачислений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	Начисления.ПериодРегистрации КАК ПериодРегистрации,
	|	Начисления.Организация КАК Организация,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.Подразделение КАК Подразделение,
	|	Начисления.Подразделение КАК ПодразделениеУчетаЗатрат,
	|	Начисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.ВидОперации КАК ВидОперации,
	|	Начисления.ДокументОснование КАК ДокументОснование,
	|	ВЫБОР
	|		КОГДА БухучетНачислений.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ОпределяетсяЕжемесячноПроцентом)
	|				И &РассчитыватьДолюЕНВД
	|			ТОГДА ВЫБОР
	|					КОГДА ОтношениеКЕНВДЗатратНаЗарплату.Ссылка = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД)
	|						ТОГДА ВЫРАЗИТЬ(Начисления.Сумма * &ПроцентЕНВД / 100 КАК ЧИСЛО(15, 2))
	|					ИНАЧЕ Начисления.Сумма - (ВЫРАЗИТЬ(Начисления.Сумма * &ПроцентЕНВД / 100 КАК ЧИСЛО(15, 2)))
	|				КОНЕЦ
	|		ИНАЧЕ Начисления.Сумма
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА ВЫБОР
	|				КОГДА БухучетНачислений.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ОпределяетсяЕжемесячноПроцентом)
	|					ТОГДА ВЫБОР
	|							КОГДА &РассчитыватьДолюЕНВД
	|								ТОГДА ОтношениеКЕНВДЗатратНаЗарплату.Ссылка
	|							КОГДА &ПроцентЕНВД = 0
	|								ТОГДА ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД)
	|							ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД)
	|						КОНЕЦ
	|				ИНАЧЕ БухучетНачислений.ОтношениеКЕНВД
	|			КОНЕЦ = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОблагаетсяЕНВД
	|ПОМЕСТИТЬ ВТБухучетНачисленийБезБазы
	|ИЗ
	|	ВТБухучетНачисленийВременная КАК БухучетНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияДляРаспределения КАК Начисления
	|		ПО БухучетНачислений.ИдентификаторСтроки = Начисления.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Перечисление.ОтношениеКЕНВДЗатратНаЗарплату КАК ОтношениеКЕНВДЗатратНаЗарплату
	|		ПО (ОтношениеКЕНВДЗатратНаЗарплату.Ссылка В (ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД), ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД)))
	|			И (БухучетНачислений.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ОпределяетсяЕжемесячноПроцентом))
	|			И (&РассчитыватьДолюЕНВД)";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляРаспределения", ИмяВТНачисленияИсходная);
	Запрос.Выполнить();
	// Зарегистрируем обработанные идентификаторы строк.
	ДополнитьВТСтрокиКУдалению(Запрос, "ВТБухучетНачисленийБезБазы");
	
	Запрос.Текст = 
	"УНИЧТОЖИТЬ ВТБухучетНачисленийВременная";
	Запрос.Выполнить(); 	
	
КонецПроцедуры

Процедура СоздатьВТБухучетНачисленияПоБазе(Запрос, УчитыватьСторноЗаписи, МенеджерКадровогоУчета, МенеджерДанныхУчетаВремени)
	
	УдалитьВТ = Новый Массив;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТНачисленияПоБазе.ОчередностьОтраженияВУчете КАК ОчередностьОтраженияВУчете
	|ИЗ
	|	ВТНачисленияПоБазе КАК ВТНачисленияПоБазе
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОчередностьОтраженияВУчете";
	
	РезультатОчередностьРаспределенияПоБазе = Запрос.Выполнить();
	
	Если РезультатОчередностьРаспределенияПоБазе.Пустой() Тогда
		СоздатьПустуюВТБухучетНачислений(Запрос, "ВТБухучетНачисленияПоБазе");
	Иначе
		
		РаботаВХозрасчетнойОрганизации = ПолучитьФункциональнуюОпцию("РаботаВХозрасчетнойОрганизации");
		ИспользоватьСтатьиФинансирования = Запрос.Параметры.ИспользоватьСтатьиФинансирования;
		ОтборНулевыхСтрок = Новый Структура("Сумма", 0);
		
		ПоляГруппировки = "ИдентификаторСтроки,Организация,Сотрудник,ФизическоеЛицо,Подразделение,ТерриторияВыполненияРаботВОрганизации,
		|Начисление,ВидОперации,ДатаНачала,ДатаОкончания,ДокументОснование,
		|СпособОтраженияЗарплатыВБухучете,СтатьяФинансирования,СтатьяРасходов,ОблагаетсяЕНВД,ПодразделениеУчетаЗатрат";
		ПоляСуммирования = "Сумма";
		ТаблицаБухучетНачисленияПоБазеСвод = НоваяТаблицаБухучетНачисленияУдержанияПоСотрудникам().СкопироватьКолонки(ПоляГруппировки+","+ПоляСуммирования);
		ТаблицаБухучетНачисленияПоБазеСвод.Колонки.Добавить("Сторно", Новый ОписаниеТипов("Булево"));
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	БухучетНачисленийВременная.Организация КАК Организация,
		|	БухучетНачисленийВременная.Сотрудник КАК Сотрудник,
		|	БухучетНачисленийВременная.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БухучетНачисленийВременная.Подразделение КАК Подразделение,
		|	БухучетНачисленийВременная.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачисленийВременная.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	БухучетНачисленийВременная.Начисление КАК Начисление,
		|	БухучетНачисленийВременная.ДатаНачала КАК ДатаНачала,
		|	БухучетНачисленийВременная.ДатаОкончания КАК ДатаОкончания,
		|	БухучетНачисленийВременная.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачисленийВременная.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачисленийВременная.СтатьяРасходов КАК СтатьяРасходов,
		|	СУММА(БухучетНачисленийВременная.Сумма) КАК Сумма,
		|	БухучетНачисленийВременная.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	БухучетНачисленийВременная.Сторно КАК Сторно
		|ПОМЕСТИТЬ ВТБухучетНачисленийВременная
		|ИЗ
		|	(ВЫБРАТЬ
		|		БухучетНачисления.Организация КАК Организация,
		|		БухучетНачисления.Сотрудник КАК Сотрудник,
		|		БухучетНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|		БухучетНачисления.Подразделение КАК Подразделение,
		|		БухучетНачисления.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|		БухучетНачисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|		БухучетНачисления.Начисление КАК Начисление,
		|		БухучетНачисления.ДатаНачала КАК ДатаНачала,
		|		БухучетНачисления.ДатаОкончания КАК ДатаОкончания,
		|		БухучетНачисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|		БухучетНачисления.СтатьяФинансирования КАК СтатьяФинансирования,
		|		БухучетНачисления.СтатьяРасходов КАК СтатьяРасходов,
		|		БухучетНачисления.Сумма КАК Сумма,
		|		БухучетНачисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|		ЛОЖЬ КАК Сторно
		|	ИЗ
		|		ВТБухучетНачисленийБезРаспределяемыхПоБазе КАК БухучетНачисления
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		БухучетНачисления.Организация,
		|		БухучетНачисления.Сотрудник,
		|		БухучетНачисления.ФизическоеЛицо,
		|		БухучетНачисления.Подразделение,
		|		БухучетНачисления.Подразделение,
		|		БухучетНачисления.Территория,
		|		БухучетНачисления.Начисление,
		|		БухучетНачисления.ДатаНачала,
		|		БухучетНачисления.ДатаОкончания,
		|		БухучетНачисления.СпособОтраженияЗарплатыВБухучете,
		|		БухучетНачисления.СтатьяФинансирования,
		|		БухучетНачисления.СтатьяРасходов,
		|		БухучетНачисления.Сумма,
		|		БухучетНачисления.ОблагаетсяЕНВД,
		|		ЛОЖЬ
		|	ИЗ
		|		ВТБухучетНачисленийИзФормы КАК БухучетНачисления
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		БухучетНачисленийФормы.Организация,
		|		БухучетНачисленийФормы.Сотрудник,
		|		БухучетНачисленийФормы.ФизическоеЛицо,
		|		БухучетНачисленийФормы.Подразделение,
		|		БухучетНачисленийФормы.Подразделение,
		|		БухучетНачисленийФормы.Территория,
		|		БухучетНачисленийФормы.Начисление,
		|		БухучетНачисленийФормы.ДатаНачала,
		|		БухучетНачисленийФормы.ДатаОкончания,
		|		БухучетНачисленийФормы.СпособОтраженияЗарплатыВБухучете,
		|		БухучетНачисленийФормы.СтатьяФинансирования,
		|		БухучетНачисленийФормы.СтатьяРасходов,
		|		БухучетНачисленийФормы.Сумма,
		|		БухучетНачисленийФормы.ОблагаетсяЕНВД,
		|		ИСТИНА
		|	ИЗ
		|		ВТБухучетНачисленийСторно КАК БухучетНачисленийФормы) КАК БухучетНачисленийВременная
		|
		|СГРУППИРОВАТЬ ПО
		|	БухучетНачисленийВременная.Организация,
		|	БухучетНачисленийВременная.Сотрудник,
		|	БухучетНачисленийВременная.ФизическоеЛицо,
		|	БухучетНачисленийВременная.Подразделение,
		|	БухучетНачисленийВременная.Начисление,
		|	БухучетНачисленийВременная.ДатаНачала,
		|	БухучетНачисленийВременная.ДатаОкончания,
		|	БухучетНачисленийВременная.СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачисленийВременная.СтатьяФинансирования,
		|	БухучетНачисленийВременная.СтатьяРасходов,
		|	БухучетНачисленийВременная.ОблагаетсяЕНВД,
		|	БухучетНачисленийВременная.ПодразделениеУчетаЗатрат,
		|	БухучетНачисленийВременная.ТерриторияВыполненияРаботВОрганизации,
		|	БухучетНачисленийВременная.Сторно
		|
		|ИМЕЮЩИЕ
		|	СУММА(БухучетНачисленийВременная.Сумма) <> 0";
		
		Запрос.Выполнить();
		УдалитьВТ.Добавить("ВТБухучетНачисленийВременная");
		
		ЗарплатаКадры.СоздатьПустуюВТ(Запрос.МенеджерВременныхТаблиц, "РегистрРасчета.Начисления");
		
		Если Не ИспользоватьСтатьиФинансирования Тогда
			
			ТекстЗапроса = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Начисления.Регистратор КАК Регистратор,
			|	Начисления.НомерСтроки КАК НомерСтроки,
			|	Начисления.РассчитыватьПоРазовымНачислениямДокумента КАК РассчитыватьПоРазовымНачислениямДокумента,
			|	НачисленияПоБазе.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	НачисленияПоБазе.ОчередностьОтраженияВУчете КАК ОчередностьОтраженияВУчете,
			|	НачисленияПоБазе.Сотрудник КАК Сотрудник,
			|	НачисленияПоБазе.ФизическоеЛицо КАК ФизическоеЛицо,
			|	НачисленияПоБазе.Начисление КАК ВидРасчета,
			|	НачисленияПоБазе.ДатаНачала КАК ДатаНачала,
			|	НачисленияПоБазе.ДатаОкончания КАК ДатаОкончания
			|ПОМЕСТИТЬ ВТНачисленияПоБазеИзРегистраРасчета
			|ИЗ
			|	ВТНачисленияПоБазе КАК НачисленияПоБазе
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.Начисления КАК Начисления
			|		ПО НачисленияПоБазе.ПериодРегистрации = Начисления.ПериодРегистрации
			|			И НачисленияПоБазе.Организация = Начисления.Организация
			|			И НачисленияПоБазе.Сторно = Начисления.Сторно
			|			И НачисленияПоБазе.ФиксСторно = Начисления.ФиксСторно
			|			И НачисленияПоБазе.Сотрудник = Начисления.Сотрудник
			|			И НачисленияПоБазе.Начисление = Начисления.ВидРасчета
			|			И НачисленияПоБазе.РассчитыватьПоРазовымНачислениямДокумента = Начисления.РассчитыватьПоРазовымНачислениямДокумента
			|			И (НачисленияПоБазе.Сумма <> 0)
			|			И НачисленияПоБазе.ДатаНачала = Начисления.ПериодДействияНачало
			|			И (НачисленияПоБазе.ДатаОкончания = НАЧАЛОПЕРИОДА(Начисления.ПериодДействияКонец, ДЕНЬ))
			|			И (Начисления.Регистратор <> &ИсключаемыйРегистратор)";
			
		Иначе
			
			ТекстЗапроса = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Начисления.Регистратор КАК Регистратор,
			|	Начисления.НомерСтроки КАК НомерСтроки,
			|	Начисления.РассчитыватьПоРазовымНачислениямДокумента КАК РассчитыватьПоРазовымНачислениямДокумента,
			|	НачисленияПоБазе.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	НачисленияПоБазе.ОчередностьОтраженияВУчете КАК ОчередностьОтраженияВУчете,
			|	НачисленияПоБазе.Сотрудник КАК Сотрудник,
			|	НачисленияПоБазе.ФизическоеЛицо КАК ФизическоеЛицо,
			|	НачисленияПоБазе.Начисление КАК Начисление,
			|	НачисленияПоБазе.ДатаНачала КАК ДатаНачала,
			|	НачисленияПоБазе.ДатаОкончания КАК ДатаОкончания
			|ПОМЕСТИТЬ ВТНачисленияПоБазеИзРегистраРасчета
			|ИЗ
			|	ВТНачисленияПоБазе КАК НачисленияПоБазе
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистрРасчета_Начисления КАК Начисления
			|		ПО НачисленияПоБазе.ПериодРегистрации = Начисления.ПериодРегистрации
			|			И НачисленияПоБазе.Организация = Начисления.Организация
			|			И НачисленияПоБазе.Сторно = Начисления.Сторно
			|			И НачисленияПоБазе.ФиксСторно = Начисления.ФиксСторно
			|			И НачисленияПоБазе.Сотрудник = Начисления.Сотрудник
			|			И НачисленияПоБазе.Начисление = Начисления.ВидРасчета
			|			И НачисленияПоБазе.РассчитыватьПоРазовымНачислениямДокумента = Начисления.РассчитыватьПоРазовымНачислениямДокумента
			|			И (НачисленияПоБазе.Сумма <> 0)
			|			И НачисленияПоБазе.ДатаНачала = Начисления.ПериодДействияНачало
			|			И (НачисленияПоБазе.ДатаОкончания = НАЧАЛОПЕРИОДА(Начисления.ПериодДействияКонец, ДЕНЬ))";
			
		КонецЕсли;
		
		Если Не УчитыватьСторноЗаписи Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И НачисленияПоБазе.Сторно = Начисления.Сторно", "");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И НачисленияПоБазе.ФиксСторно = Начисления.ФиксСторно", "");
		КонецЕсли;
		Запрос.Текст = ТекстЗапроса;
		// ВТНачисленияПоБазеИзРегистраРасчета
		Запрос.Выполнить();
		УдалитьВТ.Добавить("ВТНачисленияПоБазеИзРегистраРасчета");
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОтборНачислений.Регистратор,
		|	ОтборНачислений.НомерСтроки
		|ПОМЕСТИТЬ ВТОтборНачислений
		|ИЗ
		|	ВТНачисленияПоБазеИзРегистраРасчета КАК ОтборНачислений";
		Запрос.Выполнить();
		УдалитьВТ.Добавить("ВТОтборНачислений");
	
		РасчетЗарплатыРасширенный.СоздатьВТРасчетнаяБазаНачисленийПоВременнойТаблицеКаскадно(Запрос.МенеджерВременныхТаблиц, Запрос.Параметры.ИсключаемыйРегистратор, МенеджерКадровогоУчета, МенеджерДанныхУчетаВремени);
		УдалитьВТ.Добавить("ВТРасчетнаяБаза");
		
		// ВТБазовыеНачисления
		// ИдентификаторСтроки - строка таблицы, которая распределяется по базе,
		// ОчередностьОтраженияВУчете - очередность строки, распределяемой по базе,
		// РегистраторБаза и НомерСтрокиБаза - ключ, определяющий базовую строку,
		// остальные поля - свойства базового начисления из регистра расчетов.
		
		Если ЗарплатаКадры.ВыполнятьРасчетЗарплатыБезОптимизации() Тогда
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	РасчетнаяБаза.Регистратор КАК Регистратор,
			|	РасчетнаяБаза.НомерСтроки КАК НомерСтроки,
			|	РасчетнаяБаза.РегистраторРазрез КАК РегистраторРазрез,
			|	РасчетнаяБаза.ИдентификаторСтрокиРазрез КАК ИдентификаторСтрокиРазрез,
			|	РасчетнаяБаза.НомерСтрокиРазрез КАК НомерСтрокиРазрез,
			|	РасчетнаяБаза.ВидРасчетаРазрез КАК ВидРасчетаРазрез,
			|	НАЧАЛОПЕРИОДА(Начисления.ПериодДействияНачало, ДЕНЬ) КАК ДатаНачала,
			|	НАЧАЛОПЕРИОДА(Начисления.ПериодДействияКонец, ДЕНЬ) КАК ДатаОкончания,
			|	СУММА(РасчетнаяБаза.РезультатБаза) КАК РезультатБаза,
			|	ЛОЖЬ КАК БазоваяЗаписьТекущегоРегистратора,
			|	Начисления.Сторно КАК Сторно
			|ПОМЕСТИТЬ ВТРасчетнаяБазаСПериодом
			|ИЗ
			|	ВТРасчетнаяБаза КАК РасчетнаяБаза
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.Начисления КАК Начисления
			|		ПО РасчетнаяБаза.РегистраторРазрез = Начисления.Регистратор
			|			И РасчетнаяБаза.НомерСтрокиРазрез = Начисления.НомерСтроки
			|
			|СГРУППИРОВАТЬ ПО
			|	РасчетнаяБаза.Регистратор,
			|	РасчетнаяБаза.НомерСтроки,
			|	РасчетнаяБаза.РегистраторРазрез,
			|	РасчетнаяБаза.ИдентификаторСтрокиРазрез,
			|	РасчетнаяБаза.НомерСтрокиРазрез,
			|	РасчетнаяБаза.ВидРасчетаРазрез,
			|	Начисления.Сторно,
			|	НАЧАЛОПЕРИОДА(Начисления.ПериодДействияНачало, ДЕНЬ),
			|	НАЧАЛОПЕРИОДА(Начисления.ПериодДействияКонец, ДЕНЬ)
			|
			|ИМЕЮЩИЕ
			|	СУММА(РасчетнаяБаза.РезультатБаза) <> 0
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	РасчетнаяБаза.Регистратор,
			|	РасчетнаяБаза.НомерСтроки,
			|	РасчетнаяБаза.РегистраторРазрез,
			|	РасчетнаяБаза.ИдентификаторСтрокиРазрез,
			|	РасчетнаяБаза.НомерСтрокиРазрез,
			|	РасчетнаяБаза.ВидРасчетаРазрез,
			|	НАЧАЛОПЕРИОДА(Начисления.ПериодДействияНачало, ДЕНЬ),
			|	НАЧАЛОПЕРИОДА(Начисления.ПериодДействияКонец, ДЕНЬ),
			|	СУММА(РасчетнаяБаза.РезультатБаза),
			|	ИСТИНА,
			|	Начисления.Сторно
			|ИЗ
			|	ВТРасчетнаяБаза КАК РасчетнаяБаза
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистрРасчета_Начисления КАК Начисления
			|		ПО РасчетнаяБаза.РегистраторРазрез = Начисления.Регистратор
			|			И РасчетнаяБаза.НомерСтрокиРазрез = Начисления.НомерСтроки
			|
			|СГРУППИРОВАТЬ ПО
			|	РасчетнаяБаза.Регистратор,
			|	РасчетнаяБаза.НомерСтроки,
			|	РасчетнаяБаза.РегистраторРазрез,
			|	РасчетнаяБаза.ИдентификаторСтрокиРазрез,
			|	РасчетнаяБаза.НомерСтрокиРазрез,
			|	РасчетнаяБаза.ВидРасчетаРазрез,
			|	Начисления.Сторно,
			|	НАЧАЛОПЕРИОДА(Начисления.ПериодДействияНачало, ДЕНЬ),
			|	НАЧАЛОПЕРИОДА(Начисления.ПериодДействияКонец, ДЕНЬ)
			|
			|ИМЕЮЩИЕ
			|	СУММА(РасчетнаяБаза.РезультатБаза) <> 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	НачисленияПоБазе.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	НачисленияПоБазе.ОчередностьОтраженияВУчете КАК ОчередностьОтраженияВУчете,
			|	НачисленияПоБазе.Сотрудник КАК СотрудникБаза,
			|	НачисленияПоБазе.ФизическоеЛицо КАК ФизическоеЛицоБаза,
			|	НачисленияПоБазе.РассчитыватьПоРазовымНачислениямДокумента КАК РассчитыватьПоРазовымНачислениямДокумента,
			|	База.РезультатБаза КАК РезультатБаза,
			|	База.ВидРасчетаРазрез КАК НачислениеБаза,
			|	База.ДатаНачала КАК ДатаНачалаБаза,
			|	База.ДатаОкончания КАК ДатаОкончанияБаза,
			|	База.РегистраторРазрез КАК РегистраторБаза,
			|	База.ИдентификаторСтрокиРазрез КАК ИдентификаторСтрокиРазрез,
			|	База.НомерСтрокиРазрез КАК НомерСтрокиБаза,
			|	База.БазоваяЗаписьТекущегоРегистратора КАК БазоваяЗаписьТекущегоРегистратора,
			|	База.Сторно КАК Сторно
			|ПОМЕСТИТЬ ВТБазовыеНачисления
			|ИЗ
			|	ВТНачисленияПоБазеИзРегистраРасчета КАК НачисленияПоБазе
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасчетнаяБазаСПериодом КАК База
			|		ПО НачисленияПоБазе.Регистратор = База.Регистратор
			|			И НачисленияПоБазе.НомерСтроки = База.НомерСтроки";
			
		Иначе
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	РасчетнаяБаза.Регистратор КАК Регистратор,
			|	РасчетнаяБаза.НомерСтроки КАК НомерСтроки,
			|	РасчетнаяБаза.РезультатБаза КАК РезультатБаза,
			|	РасчетнаяБаза.ВидРасчетаРазрез КАК ВидРасчетаРазрез,
			|	РасчетнаяБаза.ПериодДействияНачалоРазрез КАК ПериодДействияНачалоРазрез,
			|	РасчетнаяБаза.ПериодДействияКонецРазрез КАК ПериодДействияКонецРазрез,
			|	РасчетнаяБаза.РегистраторРазрез КАК РегистраторРазрез,
			|	РасчетнаяБаза.ИдентификаторСтрокиРазрез КАК ИдентификаторСтрокиРазрез,
			|	РасчетнаяБаза.НомерСтрокиРазрез КАК НомерСтрокиРазрез,
			|	Начисления.Сторно КАК Сторно
			|ПОМЕСТИТЬ ВТРасчетнаяБазаСПериодом
			|ИЗ
			|	ВТРасчетнаяБаза КАК РасчетнаяБаза
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.Начисления КАК Начисления
			|		ПО РасчетнаяБаза.РегистраторРазрез = Начисления.Регистратор
			|			И РасчетнаяБаза.НомерСтрокиРазрез = Начисления.НомерСтроки
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	РасчетнаяБаза.Регистратор,
			|	РасчетнаяБаза.НомерСтроки,
			|	РасчетнаяБаза.РезультатБаза,
			|	РасчетнаяБаза.ВидРасчетаРазрез,
			|	РасчетнаяБаза.ПериодДействияНачалоРазрез,
			|	РасчетнаяБаза.ПериодДействияКонецРазрез,
			|	РасчетнаяБаза.РегистраторРазрез,
			|	РасчетнаяБаза.ИдентификаторСтрокиРазрез,
			|	РасчетнаяБаза.НомерСтрокиРазрез,
			|	Начисления.Сторно
			|ИЗ
			|	ВТРасчетнаяБаза КАК РасчетнаяБаза
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистрРасчета_Начисления КАК Начисления
			|		ПО РасчетнаяБаза.РегистраторРазрез = Начисления.Регистратор
			|			И РасчетнаяБаза.НомерСтрокиРазрез = Начисления.НомерСтроки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	НачисленияПоБазе.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	НачисленияПоБазе.ОчередностьОтраженияВУчете КАК ОчередностьОтраженияВУчете,
			|	НачисленияПоБазе.Сотрудник КАК СотрудникБаза,
			|	НачисленияПоБазе.ФизическоеЛицо КАК ФизическоеЛицоБаза,
			|	НачисленияПоБазе.РассчитыватьПоРазовымНачислениямДокумента КАК РассчитыватьПоРазовымНачислениямДокумента,
			|	СУММА(База.РезультатБаза) КАК РезультатБаза,
			|	База.ВидРасчетаРазрез КАК НачислениеБаза,
			|	НАЧАЛОПЕРИОДА(База.ПериодДействияНачалоРазрез, ДЕНЬ) КАК ДатаНачалаБаза,
			|	НАЧАЛОПЕРИОДА(База.ПериодДействияКонецРазрез, ДЕНЬ) КАК ДатаОкончанияБаза,
			|	База.РегистраторРазрез КАК РегистраторБаза,
			|	База.ИдентификаторСтрокиРазрез КАК ИдентификаторСтрокиРазрез,
			|	База.НомерСтрокиРазрез КАК НомерСтрокиБаза,
			|	База.Сторно КАК Сторно,
			|	ВЫБОР
			|		КОГДА База.РегистраторРазрез = База.Регистратор
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК БазоваяЗаписьТекущегоРегистратора
			|ПОМЕСТИТЬ ВТБазовыеНачисления
			|ИЗ
			|	ВТНачисленияПоБазеИзРегистраРасчета КАК НачисленияПоБазе
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасчетнаяБазаСПериодом КАК База
			|		ПО НачисленияПоБазе.Регистратор = База.Регистратор
			|			И НачисленияПоБазе.НомерСтроки = База.НомерСтроки
			|
			|СГРУППИРОВАТЬ ПО
			|	НачисленияПоБазе.ИдентификаторСтроки,
			|	НачисленияПоБазе.Сотрудник,
			|	НачисленияПоБазе.ФизическоеЛицо,
			|	НачисленияПоБазе.РассчитыватьПоРазовымНачислениямДокумента,
			|	База.ВидРасчетаРазрез,
			|	НАЧАЛОПЕРИОДА(База.ПериодДействияНачалоРазрез, ДЕНЬ),
			|	НАЧАЛОПЕРИОДА(База.ПериодДействияКонецРазрез, ДЕНЬ),
			|	База.РегистраторРазрез,
			|	База.ИдентификаторСтрокиРазрез,
			|	База.НомерСтрокиРазрез,
			|	НачисленияПоБазе.ОчередностьОтраженияВУчете,
			|	База.Сторно,
			|	ВЫБОР
			|		КОГДА База.РегистраторРазрез = База.Регистратор
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ
			|
			|ИМЕЮЩИЕ
			|	СУММА(База.РезультатБаза) <> 0";
			
		КонецЕсли;
		УдалитьВТ.Добавить("ВТРасчетнаяБазаСПериодом");
		// ВТБазовыеНачисления
		Запрос.Выполнить();
		УдалитьВТ.Добавить("ВТБазовыеНачисления");
		
		Выборка = РезультатОчередностьРаспределенияПоБазе.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			УдалитьВТОперативно = Новый Массив;
			
			ОчередностьОтраженияВУчете = Выборка.ОчередностьОтраженияВУчете;
			Запрос.УстановитьПараметр("ОчередностьОтраженияВУчете", ОчередностьОтраженияВУчете);
			
			Если НЕ ИспользоватьСтатьиФинансирования Тогда
				
				Запрос.Текст =
				"ВЫБРАТЬ
				|	БазаНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
				|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
				|	БухучетНачислений.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
				|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
				|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
				|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
				|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
				|	БазаНачислений.НачислениеБаза КАК НачислениеБаза,
				|	БазаНачислений.РезультатБаза КАК РезультатБаза,
				|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
				|	БухучетНачислений.ДатаОкончания КАК ДатаОкончания,
				|	СУММА(БухучетНачислений.Сумма) КАК СуммаБУ,
				|	БазаНачислений.РегистраторБаза КАК РегистраторБаза,
				|	БазаНачислений.НомерСтрокиБаза КАК НомерСтрокиБаза
				|ПОМЕСТИТЬ ВТБазовыеНачисленияБУ
				|ИЗ
				|	ВТБазовыеНачисления КАК БазаНачислений
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучетНачисленийВременная КАК БухучетНачислений
				|		ПО БазаНачислений.СотрудникБаза = БухучетНачислений.Сотрудник
				|			И (БазаНачислений.ОчередностьОтраженияВУчете = &ОчередностьОтраженияВУчете)
				|			И БазаНачислений.ФизическоеЛицоБаза = БухучетНачислений.ФизическоеЛицо
				|			И БазаНачислений.НачислениеБаза = БухучетНачислений.Начисление
				|			И БазаНачислений.ДатаНачалаБаза = БухучетНачислений.ДатаНачала
				|			И БазаНачислений.ДатаОкончанияБаза = БухучетНачислений.ДатаОкончания
				|
				|СГРУППИРОВАТЬ ПО
				|	БазаНачислений.ИдентификаторСтроки,
				|	БухучетНачислений.ПодразделениеУчетаЗатрат,
				|	БухучетНачислений.ТерриторияВыполненияРаботВОрганизации,
				|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
				|	БухучетНачислений.СтатьяФинансирования,
				|	БухучетНачислений.СтатьяРасходов,
				|	БухучетНачислений.ОблагаетсяЕНВД,
				|	БазаНачислений.НачислениеБаза,
				|	БазаНачислений.РезультатБаза,
				|	БухучетНачислений.ДатаНачала,
				|	БухучетНачислений.ДатаОкончания,
				|	БазаНачислений.РегистраторБаза,
				|	БазаНачислений.НомерСтрокиБаза";
				
			Иначе
				
				Запрос.Текст =
				"ВЫБРАТЬ
				|	БазовыеНачисленияБУ.ИдентификаторСтроки КАК ИдентификаторСтроки,
				|	БазовыеНачисленияБУ.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
				|	БазовыеНачисленияБУ.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
				|	БазовыеНачисленияБУ.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
				|	БазовыеНачисленияБУ.СтатьяФинансирования КАК СтатьяФинансирования,
				|	БазовыеНачисленияБУ.СтатьяРасходов КАК СтатьяРасходов,
				|	БазовыеНачисленияБУ.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
				|	БазовыеНачисленияБУ.НачислениеБаза КАК НачислениеБаза,
				|	СУММА(БазовыеНачисленияБУ.РезультатБаза) КАК РезультатБаза,
				|	БазовыеНачисленияБУ.ДатаНачала КАК ДатаНачала,
				|	БазовыеНачисленияБУ.ДатаОкончания КАК ДатаОкончания,
				|	СУММА(БазовыеНачисленияБУ.СуммаБУ) КАК СуммаБУ,
				|	БазовыеНачисленияБУ.РегистраторБаза КАК РегистраторБаза,
				|	БазовыеНачисленияБУ.НомерСтрокиБаза КАК НомерСтрокиБаза
				|ПОМЕСТИТЬ ВТБазовыеНачисленияБУ
				|ИЗ
				|	(ВЫБРАТЬ
				|		БазаНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
				|		БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
				|		БухучетНачислений.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
				|		БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
				|		БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
				|		БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
				|		БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
				|		БазаНачислений.НачислениеБаза КАК НачислениеБаза,
				|		БазаНачислений.РезультатБаза КАК РезультатБаза,
				|		БухучетНачислений.ДатаНачала КАК ДатаНачала,
				|		БухучетНачислений.ДатаОкончания КАК ДатаОкончания,
				|		ВЫРАЗИТЬ(БухучетНачислений.Сумма КАК ЧИСЛО(13, 2)) КАК СуммаБУ,
				|		БазаНачислений.РегистраторБаза КАК РегистраторБаза,
				|		БазаНачислений.НомерСтрокиБаза КАК НомерСтрокиБаза
				|	ИЗ
				|		ВТБазовыеНачисления КАК БазаНачислений
				|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучетНачисленийВременная КАК БухучетНачислений
				|			ПО БазаНачислений.СотрудникБаза = БухучетНачислений.Сотрудник
				|				И (БазаНачислений.БазоваяЗаписьТекущегоРегистратора)
				|				И (БазаНачислений.ОчередностьОтраженияВУчете = &ОчередностьОтраженияВУчете)
				|				И БазаНачислений.ФизическоеЛицоБаза = БухучетНачислений.ФизическоеЛицо
				|				И БазаНачислений.НачислениеБаза = БухучетНачислений.Начисление
				|				И БазаНачислений.ДатаНачалаБаза = БухучетНачислений.ДатаНачала
				|				И БазаНачислений.ДатаОкончанияБаза = БухучетНачислений.ДатаОкончания
				|				И БазаНачислений.Сторно = БухучетНачислений.Сторно
				|	
				|	ОБЪЕДИНИТЬ ВСЕ
				|	
				|	ВЫБРАТЬ
				|		БазаНачислений.ИдентификаторСтроки,
				|		ВЫБОР
				|			КОГДА БухучетНачислений.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
				|				ТОГДА БухучетНачислений.Подразделение
				|			ИНАЧЕ БухучетНачислений.ПодразделениеУчетаЗатрат
				|		КОНЕЦ,
				|		ВЫБОР
				|			КОГДА &ИспользоватьОбособленныеТерритории
				|				ТОГДА БухучетНачислений.ТерриторияВыполненияРаботВОрганизации
				|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ТерриторииВыполненияРабот.ПустаяСсылка)
				|		КОНЕЦ,
				|		БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
				|		БухучетНачислений.СтатьяФинансирования,
				|		БухучетНачислений.СтатьяРасходов,
				|		БухучетНачислений.ОблагаетсяЕНВД,
				|		БазаНачислений.НачислениеБаза,
				|		БазаНачислений.РезультатБаза,
				|		БухучетНачислений.ДатаНачала,
				|		БухучетНачислений.ДатаОкончания,
				|		ВЫРАЗИТЬ(БухучетНачислений.Сумма КАК ЧИСЛО(13, 2)),
				|		БазаНачислений.РегистраторБаза,
				|		БазаНачислений.НомерСтрокиБаза
				|	ИЗ
				|		ВТБазовыеНачисления КАК БазаНачислений
				|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.БухучетНачисленияУдержанияПоСотрудникам КАК БухучетНачислений
				|			ПО БазаНачислений.СотрудникБаза = БухучетНачислений.Сотрудник
				|				И (НЕ БазаНачислений.БазоваяЗаписьТекущегоРегистратора)
				|				И (БазаНачислений.ОчередностьОтраженияВУчете = &ОчередностьОтраженияВУчете)
				|				И БазаНачислений.РегистраторБаза = БухучетНачислений.Регистратор
				|				И БазаНачислений.НачислениеБаза = БухучетНачислений.НачислениеУдержание
				|				И БазаНачислений.ИдентификаторСтрокиРазрез = БухучетНачислений.ИдентификаторСтроки) КАК БазовыеНачисленияБУ
				|
				|СГРУППИРОВАТЬ ПО
				|	БазовыеНачисленияБУ.ИдентификаторСтроки,
				|	БазовыеНачисленияБУ.ТерриторияВыполненияРаботВОрганизации,
				|	БазовыеНачисленияБУ.СпособОтраженияЗарплатыВБухучете,
				|	БазовыеНачисленияБУ.СтатьяФинансирования,
				|	БазовыеНачисленияБУ.СтатьяРасходов,
				|	БазовыеНачисленияБУ.ОблагаетсяЕНВД,
				|	БазовыеНачисленияБУ.НачислениеБаза,
				|	БазовыеНачисленияБУ.ДатаНачала,
				|	БазовыеНачисленияБУ.ДатаОкончания,
				|	БазовыеНачисленияБУ.ПодразделениеУчетаЗатрат,
				|	БазовыеНачисленияБУ.РегистраторБаза,
				|	БазовыеНачисленияБУ.НомерСтрокиБаза";
				
			КонецЕсли;
			// ВТБазовыеНачисленияБУ.
			Запрос.Выполнить();
			УдалитьВТОперативно.Добавить("ВТБазовыеНачисленияБУ");
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	БазовыеНачисленияБУ.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	БазовыеНачисленияБУ.РегистраторБаза КАК РегистраторБаза,
			|	БазовыеНачисленияБУ.НомерСтрокиБаза КАК НомерСтрокиБаза,
			|	СУММА(БазовыеНачисленияБУ.СуммаБУ) КАК СуммаБУ
			|ПОМЕСТИТЬ ВТБазовыеНачисленияБУСвод
			|ИЗ
			|	ВТБазовыеНачисленияБУ КАК БазовыеНачисленияБУ
			|
			|СГРУППИРОВАТЬ ПО
			|	БазовыеНачисленияБУ.ИдентификаторСтроки,
			|	БазовыеНачисленияБУ.РегистраторБаза,
			|	БазовыеНачисленияБУ.НомерСтрокиБаза
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	БазовыеНачисленияБУ.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	БазовыеНачисленияБУ.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
			|	БазовыеНачисленияБУ.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	БазовыеНачисленияБУ.СтатьяФинансирования КАК СтатьяФинансирования,
			|	БазовыеНачисленияБУ.СтатьяРасходов КАК СтатьяРасходов,
			|	БазовыеНачисленияБУ.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
			|	СУММА(ВЫБОР
			|			КОГДА ВТБазовыеНачисленияБУСвод.СуммаБУ = 0
			|				ТОГДА 0
			|			ИНАЧЕ ВЫРАЗИТЬ(БазовыеНачисленияБУ.РезультатБаза * БазовыеНачисленияБУ.СуммаБУ / ВТБазовыеНачисленияБУСвод.СуммаБУ КАК ЧИСЛО(25, 10))
			|		КОНЕЦ) КАК Сумма
			|ПОМЕСТИТЬ ВТБазовыеНачисленияБУКоэффициенты
			|ИЗ
			|	ВТБазовыеНачисленияБУ КАК БазовыеНачисленияБУ
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазовыеНачисленияБУСвод КАК ВТБазовыеНачисленияБУСвод
			|		ПО БазовыеНачисленияБУ.ИдентификаторСтроки = ВТБазовыеНачисленияБУСвод.ИдентификаторСтроки
			|			И БазовыеНачисленияБУ.РегистраторБаза = ВТБазовыеНачисленияБУСвод.РегистраторБаза
			|			И БазовыеНачисленияБУ.НомерСтрокиБаза = ВТБазовыеНачисленияБУСвод.НомерСтрокиБаза
			|
			|СГРУППИРОВАТЬ ПО
			|	БазовыеНачисленияБУ.ИдентификаторСтроки,
			|	БазовыеНачисленияБУ.СпособОтраженияЗарплатыВБухучете,
			|	БазовыеНачисленияБУ.СтатьяФинансирования,
			|	БазовыеНачисленияБУ.СтатьяРасходов,
			|	БазовыеНачисленияБУ.ОблагаетсяЕНВД,
			|	БазовыеНачисленияБУ.ПодразделениеУчетаЗатрат
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	НачисленияПоБазе.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	НачисленияПоБазе.РассчитыватьПоРазовымНачислениямДокумента КАК РассчитыватьПоРазовымНачислениямДокумента,
			|	НачисленияБУ.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
			|	НачисленияПоБазе.Сумма КАК Сумма,
			|	НачисленияБУ.СтатьяФинансирования КАК СтатьяФинансирования,
			|	НачисленияБУ.СтатьяРасходов КАК СтатьяРасходов,
			|	НачисленияБУ.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	НачисленияБУ.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
			|	НачисленияБУ.Сумма КАК СуммаБУ
			|ПОМЕСТИТЬ ВТНачисленияПоБазеБУКоэффициенты
			|ИЗ
			|	ВТНачисленияПоБазе КАК НачисленияПоБазе
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБазовыеНачисленияБУКоэффициенты КАК НачисленияБУ
			|		ПО НачисленияПоБазе.ИдентификаторСтроки = НачисленияБУ.ИдентификаторСтроки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	НачисленияПоБазе.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	СУММА(НачисленияПоБазе.СуммаБУ) КАК СуммаБУ
			|ПОМЕСТИТЬ ВТНачисленияПоБазеБУКоэффициентыСвод
			|ИЗ
			|	ВТНачисленияПоБазеБУКоэффициенты КАК НачисленияПоБазе
			|
			|СГРУППИРОВАТЬ ПО
			|	НачисленияПоБазе.ИдентификаторСтроки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	Начисления.РассчитыватьПоРазовымНачислениямДокумента КАК РассчитыватьПоРазовымНачислениямДокумента,
			|	Начисления.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
			|	Начисления.СтатьяФинансирования КАК СтатьяФинансирования,
			|	Начисления.СтатьяРасходов КАК СтатьяРасходов,
			|	Начисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	Начисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
			|	ВЫБОР
			|		КОГДА НачисленияСвод.СуммаБУ = 0
			|			ТОГДА 0
			|		ИНАЧЕ ВЫРАЗИТЬ(Начисления.Сумма * Начисления.СуммаБУ / НачисленияСвод.СуммаБУ КАК ЧИСЛО(25, 10))
			|	КОНЕЦ КАК База
			|ИЗ
			|	ВТНачисленияПоБазеБУКоэффициенты КАК Начисления
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияПоБазеБУКоэффициентыСвод КАК НачисленияСвод
			|		ПО Начисления.ИдентификаторСтроки = НачисленияСвод.ИдентификаторСтроки
			|ГДЕ
			|	ВЫБОР
			|			КОГДА НачисленияСвод.СуммаБУ = 0
			|				ТОГДА 0
			|			ИНАЧЕ ВЫРАЗИТЬ(Начисления.Сумма * Начисления.СуммаБУ / НачисленияСвод.СуммаБУ КАК ЧИСЛО(25, 10))
			|		КОНЕЦ <> 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	НачисленияПоБазе.ПериодРегистрации КАК ПериодРегистрации,
			|	НачисленияПоБазе.Организация КАК Организация,
			|	НачисленияПоБазе.Сотрудник КАК Сотрудник,
			|	НачисленияПоБазе.ФизическоеЛицо КАК ФизическоеЛицо,
			|	НачисленияПоБазе.Подразделение КАК Подразделение,
			|	НачисленияПоБазе.Начисление КАК Начисление,
			|	НачисленияПоБазе.ВидОперации КАК ВидОперации,
			|	НачисленияПоБазе.ДатаНачала КАК ДатаНачала,
			|	НачисленияПоБазе.ДатаОкончания КАК ДатаОкончания,
			|	НачисленияПоБазе.Сумма КАК Сумма,
			|	НачисленияПоБазе.ДокументОснование КАК ДокументОснование,
			|	НачисленияПоБазе.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	НачисленияПоБазе.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
			|	НачисленияПоБазе.СтатьяФинансирования КАК СтатьяФинансирования,
			|	НачисленияПоБазе.СтатьяРасходов КАК СтатьяРасходов,
			|	НачисленияПоБазе.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	НачисленияПоБазе.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
			|	НачисленияПоБазе.РассчитыватьПоРазовымНачислениямДокумента КАК РассчитыватьПоРазовымНачислениямДокумента,
			|	НачисленияПоБазе.Сторно КАК Сторно
			|ИЗ
			|	ВТНачисленияПоБазе КАК НачисленияПоБазе
			|ГДЕ
			|	НачисленияПоБазе.ОчередностьОтраженияВУчете = &ОчередностьОтраженияВУчете";
			
			Пакет = Запрос.ВыполнитьПакет();
			
			УдалитьВТОперативно.Добавить("ВТБазовыеНачисленияБУСвод");
			УдалитьВТОперативно.Добавить("ВТБазовыеНачисленияБУКоэффициенты");
			УдалитьВТОперативно.Добавить("ВТНачисленияПоБазеБУКоэффициенты");
			УдалитьВТОперативно.Добавить("ВТНачисленияПоБазеБУКоэффициентыСвод");
			
			КоличествоРезультатов = Пакет.ВГраница();
			
			ТаблицаКоэффициентов = Пакет[КоличествоРезультатов-1].Выгрузить();
			ТаблицаКоэффициентов.Индексы.Добавить("ИдентификаторСтроки");
			Отбор = Новый Структура("ИдентификаторСтроки");
			
			НачисленияПоБазе = Пакет[КоличествоРезультатов].Выгрузить();
			
			ТаблицаБухучетНачисленияПоБазеОчередности = ТаблицаБухучетНачисленияПоБазеСвод.СкопироватьКолонки();
			
			Для каждого СтрокаНачисления Из НачисленияПоБазе Цикл
				
				ТаблицаБухучетНачисленияПоБазе = ТаблицаБухучетНачисленияПоБазеСвод.СкопироватьКолонки();
				
				ЗаполнитьЗначенияСвойств(Отбор, СтрокаНачисления);
				СтрокиОтражения = ТаблицаКоэффициентов.НайтиСтроки(Отбор);
				Если СтрокаНачисления.РассчитыватьПоРазовымНачислениямДокумента Тогда
					НовыеСтрокиОтражения = Новый Массив;
					Для каждого СтрокаОтражения Из СтрокиОтражения Цикл
						Если СтрокаОтражения.РассчитыватьПоРазовымНачислениямДокумента Тогда
							НовыеСтрокиОтражения.Добавить(СтрокаОтражения);
						КонецЕсли;
					КонецЦикла;
					СтрокиОтражения = НовыеСтрокиОтражения;
				КонецЕсли;
				Коэффициенты = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиОтражения,"База");
				Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СтрокаНачисления.Сумма, Коэффициенты);
				
				Если Результаты = Неопределено Тогда
					
					СтрокаТаблицы = ТаблицаБухучетНачисленияПоБазе.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаНачисления);
					
				Иначе
					
					Индекс = 0;
					Для Каждого СтрокаОтражения Из СтрокиОтражения Цикл
						
						СтрокаТаблицы = ТаблицаБухучетНачисленияПоБазе.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаНачисления);
						СтрокаТаблицы.Сумма = Результаты[Индекс];
						СтрокаТаблицы.СтатьяФинансирования = СтрокаОтражения.СтатьяФинансирования;
						СтрокаТаблицы.СпособОтраженияЗарплатыВБухучете = СтрокаОтражения.СпособОтраженияЗарплатыВБухучете;
						Если Не РаботаВХозрасчетнойОрганизации Тогда
							СтрокаТаблицы.СтатьяРасходов = СтрокаОтражения.СтатьяРасходов;
						КонецЕсли;
						СтрокаТаблицы.ОблагаетсяЕНВД = СтрокаОтражения.ОблагаетсяЕНВД;
						СтрокаТаблицы.ПодразделениеУчетаЗатрат = СтрокаОтражения.ПодразделениеУчетаЗатрат;
						
						Индекс = Индекс + 1;
						
					КонецЦикла;
					
					ТаблицаБухучетНачисленияПоБазе.Свернуть(ПоляГруппировки, ПоляСуммирования);
					Если ТаблицаБухучетНачисленияПоБазе.Количество() > 1 Тогда
						СтрокиКУдалению = ТаблицаБухучетНачисленияПоБазе.НайтиСтроки(ОтборНулевыхСтрок);
						Для каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
							ТаблицаБухучетНачисленияПоБазе.Удалить(УдаляемаяСтрока);
						КонецЦикла;	
					КонецЕсли;
					
				КонецЕсли;
				
				ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаБухучетНачисленияПоБазе, ТаблицаБухучетНачисленияПоБазеОчередности);
				
			КонецЦикла;
			
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаБухучетНачисленияПоБазеОчередности, ТаблицаБухучетНачисленияПоБазеСвод);
			
			Запрос.УстановитьПараметр("ТаблицаБухучетНачисления", ТаблицаБухучетНачисленияПоБазеОчередности);
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	БухучетНачисления.Организация КАК Организация,
			|	БухучетНачисления.Сотрудник КАК Сотрудник,
			|	БухучетНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
			|	БухучетНачисления.Подразделение КАК Подразделение,
			|	БухучетНачисления.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
			|	БухучетНачисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
			|	БухучетНачисления.Начисление КАК Начисление,
			|	БухучетНачисления.ДатаНачала КАК ДатаНачала,
			|	БухучетНачисления.ДатаОкончания КАК ДатаОкончания,
			|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	БухучетНачисления.СтатьяФинансирования КАК СтатьяФинансирования,
			|	БухучетНачисления.СтатьяРасходов КАК СтатьяРасходов,
			|	БухучетНачисления.Сумма КАК Сумма,
			|	БухучетНачисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
			|	БухучетНачисления.Сторно КАК Сторно
			|ПОМЕСТИТЬ ВТБухучетНачисленийВременнаяНовая
			|ИЗ
			|	ВТБухучетНачисленийВременная КАК БухучетНачисления
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТБухучетНачисленийВременная
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	БухучетНачисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	БухучетНачисления.Организация КАК Организация,
			|	БухучетНачисления.Сотрудник КАК Сотрудник,
			|	БухучетНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
			|	БухучетНачисления.Подразделение КАК Подразделение,
			|	БухучетНачисления.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
			|	БухучетНачисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
			|	БухучетНачисления.Начисление КАК Начисление,
			|	БухучетНачисления.ВидОперации КАК ВидОперации,
			|	БухучетНачисления.ДатаНачала КАК ДатаНачала,
			|	БухучетНачисления.ДатаОкончания КАК ДатаОкончания,
			|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	БухучетНачисления.СтатьяФинансирования КАК СтатьяФинансирования,
			|	БухучетНачисления.СтатьяРасходов КАК СтатьяРасходов,
			|	БухучетНачисления.Сумма КАК Сумма,
			|	БухучетНачисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
			|	БухучетНачисления.Сторно КАК Сторно
			|ПОМЕСТИТЬ ВТБухучетНачисленияПоБазеПоОчередности
			|ИЗ
			|	&ТаблицаБухучетНачисления КАК БухучетНачисления
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	БухучетНачисленийВременнаяНовая.Организация КАК Организация,
			|	БухучетНачисленийВременнаяНовая.Сотрудник КАК Сотрудник,
			|	БухучетНачисленийВременнаяНовая.ФизическоеЛицо КАК ФизическоеЛицо,
			|	БухучетНачисленийВременнаяНовая.Подразделение КАК Подразделение,
			|	БухучетНачисленийВременнаяНовая.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
			|	БухучетНачисленийВременнаяНовая.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
			|	БухучетНачисленийВременнаяНовая.Начисление КАК Начисление,
			|	БухучетНачисленийВременнаяНовая.ДатаНачала КАК ДатаНачала,
			|	БухучетНачисленийВременнаяНовая.ДатаОкончания КАК ДатаОкончания,
			|	БухучетНачисленийВременнаяНовая.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	БухучетНачисленийВременнаяНовая.СтатьяФинансирования КАК СтатьяФинансирования,
			|	БухучетНачисленийВременнаяНовая.СтатьяРасходов КАК СтатьяРасходов,
			|	БухучетНачисленийВременнаяНовая.Сумма КАК Сумма,
			|	БухучетНачисленийВременнаяНовая.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
			|	БухучетНачисленийВременнаяНовая.Сторно КАК Сторно
			|ПОМЕСТИТЬ ВТБухучетНачисленийВременная
			|ИЗ
			|	ВТБухучетНачисленийВременнаяНовая КАК БухучетНачисленийВременнаяНовая
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	БухучетНачисленияПоБазеПоОчередности.Организация,
			|	БухучетНачисленияПоБазеПоОчередности.Сотрудник,
			|	БухучетНачисленияПоБазеПоОчередности.ФизическоеЛицо,
			|	БухучетНачисленияПоБазеПоОчередности.Подразделение,
			|	БухучетНачисленияПоБазеПоОчередности.ПодразделениеУчетаЗатрат,
			|	БухучетНачисленияПоБазеПоОчередности.ТерриторияВыполненияРаботВОрганизации,
			|	БухучетНачисленияПоБазеПоОчередности.Начисление,
			|	БухучетНачисленияПоБазеПоОчередности.ДатаНачала,
			|	БухучетНачисленияПоБазеПоОчередности.ДатаОкончания,
			|	БухучетНачисленияПоБазеПоОчередности.СпособОтраженияЗарплатыВБухучете,
			|	БухучетНачисленияПоБазеПоОчередности.СтатьяФинансирования,
			|	БухучетНачисленияПоБазеПоОчередности.СтатьяРасходов,
			|	БухучетНачисленияПоБазеПоОчередности.Сумма,
			|	БухучетНачисленияПоБазеПоОчередности.ОблагаетсяЕНВД,
			|	БухучетНачисленияПоБазеПоОчередности.Сторно
			|ИЗ
			|	ВТБухучетНачисленияПоБазеПоОчередности КАК БухучетНачисленияПоБазеПоОчередности";
			
			Запрос.Выполнить();
			
			УдалитьВТОперативно.Добавить("ВТБазовыеНачисленияБУСвод");
			УдалитьВТОперативно.Добавить("ВТБазовыеНачисленияБУКоэффициенты");
			УдалитьВТОперативно.Добавить("ВТНачисленияПоБазеБУКоэффициенты");
			УдалитьВТОперативно.Добавить("ВТНачисленияПоБазеБУКоэффициентыСвод");
			УдалитьВТОперативно.Добавить("ВТБухучетНачисленияПоБазеПоОчередности");
			УдалитьВТОперативно.Добавить("ВТБухучетНачисленийВременнаяНовая");
			
			ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТОперативно);
			
		КонецЦикла;
		
		ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
		
		Запрос.УстановитьПараметр("ТаблицаБухучетНачисления", ТаблицаБухучетНачисленияПоБазеСвод);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	БухучетНачисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	&ПериодРегистрации КАК ПериодРегистрации,
		|	БухучетНачисления.Организация КАК Организация,
		|	БухучетНачисления.Сотрудник КАК Сотрудник,
		|	БухучетНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БухучетНачисления.Подразделение КАК Подразделение,
		|	БухучетНачисления.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	БухучетНачисления.Начисление КАК Начисление,
		|	БухучетНачисления.ВидОперации КАК ВидОперации,
		|	БухучетНачисления.ДатаНачала КАК ДатаНачала,
		|	БухучетНачисления.ДатаОкончания КАК ДатаОкончания,
		|	БухучетНачисления.ДокументОснование КАК ДокументОснование,
		|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачисления.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачисления.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачисления.Сумма КАК Сумма,
		|	БухучетНачисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД
		|ПОМЕСТИТЬ ВТБухучетНачисленияПоБазе
		|ИЗ
		|	&ТаблицаБухучетНачисления КАК БухучетНачисления";
		Запрос.Выполнить();
		// Зарегистрируем обработанные идентификаторы строк.
		ДополнитьВТСтрокиКУдалению(Запрос, "ВТБухучетНачисленияПоБазе");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьВТБухучетНачисленийПоДоговорам(ИсходныеДанные, ИмяВТБухучетНачислений)
	
	МенеджерВременныхТаблиц 		   = ИсходныеДанные.МенеджерВременныхТаблиц;
	ИмяВТНачисленияИсходная 		   = ИсходныеДанные.ИмяВТНачисления;
	ПлательщикЕНВД                     = ИсходныеДанные.ПлательщикЕНВД;
	
	РаботаВБюджетномУчреждении  	   = ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении");
	ИспользоватьСтатьиФинансирования   = ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата");
	ИспользоватьОбособленныеТерритории = ПолучитьФункциональнуюОпцию("ИспользоватьОбособленныеТерритории", Новый Структура("Организация", ИсходныеДанные.Организация));
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ЕстьЕНВД", ПлательщикЕНВД);
	Запрос.УстановитьПараметр("ИспользоватьСтатьиФинансирования", ИспользоватьСтатьиФинансирования);
	Запрос.УстановитьПараметр("РаботаВБюджетномУчреждении", РаботаВБюджетномУчреждении);
	Запрос.УстановитьПараметр("ИспользоватьОбособленныеТерритории", ИспользоватьОбособленныеТерритории);
	
	УдалитьВТ = Новый Массив;
	
	// Получим сведения о настройке бухучета для договорников.
	СоздатьВТСведенияОБухучетеЗарплатыСотрудников(МенеджерВременныхТаблиц, ИмяВТНачисленияИсходная, "Сотрудник,ПериодРегистрации");
	УдалитьВТ.Добавить("ВТСведенияОБухучетеЗарплатыСотрудников");
	
	// ВТНачислениеВидНачисленияОплатыТрудаДляНУ.
	// Таблица соответствия начисления и вида начисления для НУ.
	Если Не ЗарплатаКадры.ВТСуществует(МенеджерВременныхТаблиц, "ВТНачислениеВидНачисленияОплатыТрудаДляНУ") Тогда
		ОтражениеЗарплатыВБухучете.СоздатьВТНачислениеВидНачисленияОплатыТрудаДляНУ(МенеджерВременныхТаблиц);
		УдалитьВТ.Добавить("ВТНачислениеВидНачисленияОплатыТрудаДляНУ");	
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СведенияОБухучетеЗарплатыСотрудников.СтатьяФинансирования КАК СтатьяФинансирования,
	|	СведенияОБухучетеЗарплатыСотрудников.СтатьяРасходов КАК СтатьяРасходов,
	|	СведенияОБухучетеЗарплатыСотрудников.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА &ЕстьЕНВД
	|			ТОГДА ВЫБОР
	|					КОГДА СведенияОБухучетеЗарплатыСотрудников.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ОпределяетсяЕжемесячноПроцентом)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД)
	|					ИНАЧЕ СведенияОБухучетеЗарплатыСотрудников.ОтношениеКЕНВД
	|				КОНЕЦ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД)
	|	КОНЕЦ КАК ОтношениеКЕНВД,
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Начисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации
	|ПОМЕСТИТЬ ВТБухучетСотрудниковПоДоговорам
	|ИЗ
	|	ВТНачисленияПоДоговорамГПХ КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСведенияОБухучетеЗарплатыСотрудников КАК СведенияОБухучетеЗарплатыСотрудников
	|		ПО Начисления.Сотрудник = СведенияОБухучетеЗарплатыСотрудников.Сотрудник
	|			И Начисления.Подразделение = СведенияОБухучетеЗарплатыСотрудников.Подразделение
	|			И (НЕ &ИспользоватьОбособленныеТерритории
	|				ИЛИ Начисления.ТерриторияВыполненияРаботВОрганизации = СведенияОБухучетеЗарплатыСотрудников.ТерриторияВыполненияРаботВОрганизации)
	|			И Начисления.ПериодРегистрации = СведенияОБухучетеЗарплатыСотрудников.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ЕСТЬNULL(НачисленияПоДоговорам.СтатьяФинансирования, ЕСТЬNULL(УсловияДоговораГПХ.СтатьяФинансирования, УсловияДоговораОпеки.СтатьяФинансирования)) КАК СтатьяФинансирования,
	|	ЕСТЬNULL(НачисленияПоДоговорам.СтатьяРасходов, ЕСТЬNULL(УсловияДоговораГПХ.СтатьяРасходов, УсловияДоговораОпеки.СтатьяРасходов)) КАК СтатьяРасходов,
	|	ЕСТЬNULL(НачисленияПоДоговорам.СпособОтраженияЗарплатыВБухучете, ЕСТЬNULL(УсловияДоговораГПХ.СпособОтраженияЗарплатыВБухучете, УсловияДоговораОпеки.СпособОтраженияЗарплатыВБухучете)) КАК СпособОтраженияЗарплатыВБухучете,
	|	ЕСТЬNULL(НачисленияПоДоговорам.ОтношениеКЕНВД, ЕСТЬNULL(УсловияДоговораГПХ.ОтношениеКЕНВД, ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД))) КАК ОтношениеКЕНВД,
	|	ЕСТЬNULL(НачисленияПоДоговорам.Сумма, ЕСТЬNULL(УсловияДоговораГПХ.Сумма, Начисления.Сумма)) КАК Сумма,
	|	ЕСТЬNULL(НачисленияПоДоговорам.СуммаЕНВД, ЕСТЬNULL(УсловияДоговораГПХ.СуммаЕНВД, 0)) КАК СуммаЕНВД
	|ПОМЕСТИТЬ ВТБухучетДокументов
	|ИЗ
	|	ВТНачисленияПоДоговорамГПХ КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияПоДоговорам КАК НачисленияПоДоговорам
	|		ПО Начисления.ДокументОснование = НачисленияПоДоговорам.ДоговорАкт
	|			И Начисления.ПериодРегистрации = НачисленияПоДоговорам.МесяцНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УсловияДоговораГПХ КАК УсловияДоговораГПХ
	|		ПО Начисления.ДокументОснование = УсловияДоговораГПХ.Договор
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УсловияДоговораОпеки КАК УсловияДоговораОпеки
	|		ПО Начисления.ДокументОснование = УсловияДоговораОпеки.Договор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	БухучетСотрудников.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	ВЫБОР
	|		КОГДА БухучетДокументов.СпособОтраженияЗарплатыВБухучете = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухучете.ПустаяСсылка)
	|				ИЛИ БухучетДокументов.СпособОтраженияЗарплатыВБухучете ЕСТЬ NULL
	|			ТОГДА БухучетСотрудников.СпособОтраженияЗарплатыВБухучете
	|		ИНАЧЕ БухучетДокументов.СпособОтраженияЗарплатыВБухучете
	|	КОНЕЦ КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА БухучетДокументов.СтатьяФинансирования = ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|				ИЛИ БухучетДокументов.СтатьяФинансирования ЕСТЬ NULL
	|			ТОГДА БухучетСотрудников.СтатьяФинансирования
	|		ИНАЧЕ БухучетДокументов.СтатьяФинансирования
	|	КОНЕЦ КАК СтатьяФинансирования,
	|	ВЫБОР
	|		КОГДА БухучетДокументов.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|				ИЛИ БухучетДокументов.СтатьяРасходов ЕСТЬ NULL
	|			ТОГДА БухучетСотрудников.СтатьяРасходов
	|		ИНАЧЕ БухучетДокументов.СтатьяРасходов
	|	КОНЕЦ КАК СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА &ЕстьЕНВД
	|			ТОГДА ВЫБОР
	|					КОГДА БухучетДокументов.ОтношениеКЕНВД ЕСТЬ NULL
	|							ИЛИ БухучетДокументов.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ПустаяСсылка)
	|						ТОГДА БухучетСотрудников.ОтношениеКЕНВД
	|					ИНАЧЕ БухучетДокументов.ОтношениеКЕНВД
	|				КОНЕЦ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД)
	|	КОНЕЦ КАК ОтношениеКЕНВД,
	|	Начисления.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(БухучетДокументов.Сумма, 0) = 0
	|			ТОГДА 0
	|		КОГДА Начисления.Сумма = БухучетДокументов.Сумма
	|			ТОГДА БухучетДокументов.СуммаЕНВД
	|		ИНАЧЕ ВЫРАЗИТЬ(БухучетДокументов.СуммаЕНВД * (ВЫРАЗИТЬ(Начисления.Сумма / БухучетДокументов.Сумма КАК ЧИСЛО(25, 10))) КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК СуммаЕНВД
	|ПОМЕСТИТЬ ВТУчетДоговоровВременная
	|ИЗ
	|	ВТНачисленияПоДоговорамГПХ КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетДокументов КАК БухучетДокументов
	|		ПО Начисления.ИдентификаторСтроки = БухучетДокументов.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетСотрудниковПоДоговорам КАК БухучетСотрудников
	|		ПО Начисления.ИдентификаторСтроки = БухучетСотрудников.ИдентификаторСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УчетДоговоров.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	УчетДоговоров.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	УчетДоговоров.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	УчетДоговоров.СтатьяФинансирования КАК СтатьяФинансирования,
	|	УчетДоговоров.СтатьяРасходов КАК СтатьяРасходов,
	|	УчетДоговоров.ОтношениеКЕНВД КАК ОтношениеКЕНВД,
	|	УчетДоговоров.Сумма КАК Сумма,
	|	УчетДоговоров.СуммаЕНВД КАК СуммаЕНВД,
	|	ВЫБОР
	|		КОГДА УчетДоговоров.Сумма = УчетДоговоров.СуммаЕНВД
	|				ИЛИ УчетДоговоров.СуммаЕНВД = 0
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК РассчитыватьДолюЕНВД,
	|	ВЫБОР
	|		КОГДА УчетДоговоров.Сумма = 0
	|			ТОГДА 0
	|		ИНАЧЕ УчетДоговоров.СуммаЕНВД / УчетДоговоров.Сумма
	|	КОНЕЦ КАК ДоляЕНВД
	|ПОМЕСТИТЬ ВТУчетДоговоров
	|ИЗ
	|	ВТУчетДоговоровВременная КАК УчетДоговоров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	УчетДоговоров.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	Начисления.ПериодРегистрации КАК ПериодРегистрации,
	|	Начисления.ПериодРегистрации КАК ПериодПринятияРасходов,
	|	Начисления.Организация КАК Организация,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Начисления.Подразделение КАК Подразделение,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.ДокументОснование КАК ДокументОснование,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьСтатьиФинансирования
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка)
	|		ИНАЧЕ УчетДоговоров.СтатьяФинансирования
	|	КОНЕЦ КАК СтатьяФинансирования,
	|	ВЫБОР
	|		КОГДА &ИспользоватьСтатьиФинансирования
	|			ТОГДА УчетДоговоров.СтатьяРасходов
	|		ИНАЧЕ &СтатьяРасходов
	|	КОНЕЦ КАК СтатьяРасходов,
	|	УчетДоговоров.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	ВЫБОР
	|		КОГДА ВЫБОР
	|				КОГДА УчетДоговоров.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ОпределяетсяЕжемесячноПроцентом)
	|					ТОГДА ВЫБОР
	|							КОГДА УчетДоговоров.РассчитыватьДолюЕНВД
	|								ТОГДА ОтношениеКЕНВДЗатратНаЗарплату.Ссылка
	|							КОГДА УчетДоговоров.ДоляЕНВД = 0
	|								ТОГДА ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД)
	|							ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД)
	|						КОНЕЦ
	|				ИНАЧЕ УчетДоговоров.ОтношениеКЕНВД
	|			КОНЕЦ = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОблагаетсяЕНВД,
	|	ВЫБОР
	|		КОГДА УчетДоговоров.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ОпределяетсяЕжемесячноПроцентом)
	|				И УчетДоговоров.РассчитыватьДолюЕНВД
	|			ТОГДА ВЫБОР
	|					КОГДА ОтношениеКЕНВДЗатратНаЗарплату.Ссылка = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД)
	|						ТОГДА УчетДоговоров.СуммаЕНВД
	|					ИНАЧЕ УчетДоговоров.Сумма - УчетДоговоров.СуммаЕНВД
	|				КОНЕЦ
	|		ИНАЧЕ УчетДоговоров.Сумма
	|	КОНЕЦ КАК Сумма,
	|	НачислениеВидНачисленияОплатыТрудаДляНУ.ВидНачисленияОплатыТрудаДляНУ КАК ВидНачисленияОплатыТрудаДляНУ,
	|	Начисления.ВидОперации КАК ВидОперации
	|ПОМЕСТИТЬ ВТБухучетНачисленийПоДоговорам
	|ИЗ
	|	ВТНачисленияПоДоговорамГПХ КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУчетДоговоров КАК УчетДоговоров
	|		ПО Начисления.ИдентификаторСтроки = УчетДоговоров.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Перечисление.ОтношениеКЕНВДЗатратНаЗарплату КАК ОтношениеКЕНВДЗатратНаЗарплату
	|		ПО (ОтношениеКЕНВДЗатратНаЗарплату.Ссылка В (ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД), ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД)))
	|			И (УчетДоговоров.ОтношениеКЕНВД = ЗНАЧЕНИЕ(Перечисление.ОтношениеКЕНВДЗатратНаЗарплату.ОпределяетсяЕжемесячноПроцентом))
	|			И (УчетДоговоров.РассчитыватьДолюЕНВД)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачислениеВидНачисленияОплатыТрудаДляНУ КАК НачислениеВидНачисленияОплатыТрудаДляНУ
	|		ПО Начисления.Начисление = НачислениеВидНачисленияОплатыТрудаДляНУ.Начисление";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияПоДоговорамГПХ", ИмяВТНачисленияИсходная);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТБухучетНачисленийПоДоговорам", ИмяВТБухучетНачислений);
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный")
		И (МенеджерВременныхТаблиц.Таблицы[ИмяВТНачисленияИсходная].Колонки.Найти("СтатьяРасходов") <> Неопределено) Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СтатьяРасходов", "Начисления.СтатьяРасходов");
		
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СтатьяРасходов", "УчетДоговоров.СтатьяРасходов");
	КонецЕсли;
	
	Запрос.Выполнить();
	
	УдалитьВТ.Добавить("ВТСведенияОБухучетеЗарплатыСотрудников");
	УдалитьВТ.Добавить("ВТБухучетСотрудниковПоДоговорам");
	УдалитьВТ.Добавить("ВТУчетДоговоров");
	УдалитьВТ.Добавить("ВТУчетДоговоровВременная");
	УдалитьВТ.Добавить("ВТБухучетДокументов");
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);

КонецПроцедуры

#КонецОбласти

#Область ОбслуживаниеОценочныхОбязательств

Функция НачисленияИВзносыДляРасчетаОценочныхОбязательствОтпусков(Организация, ПериодРегистрации, Сотрудники)
		
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Если ЗарплатаКадры.ИспользоватьСтатьиФинансированияЗарплата() Тогда
		СведенияОБухучетеНачисленийИВзносовПоСтатьямФинансирования(Организация, ПериодРегистрации, МенеджерВременныхТаблиц, Сотрудники);
	Иначе
		ТаблицаНачислений = ОтражениеЗарплатыВУчете.ДанныеДляОтражениеВУчетеНачислений(Организация, ПериодРегистрации, Сотрудники);
		ТаблицаВзносов 	  = ОтражениеЗарплатыВУчете.ДанныеДляОтражениеВУчетеВзносов(Организация, ПериодРегистрации, Сотрудники);
		ОтражениеЗарплатыВБухучете.СведенияОБухучетеНачисленийИВзносов(Организация, ПериодРегистрации, МенеджерВременныхТаблиц, ТаблицаНачислений, ТаблицаВзносов);
	КонецЕсли;
	
	Возврат ОтражениеЗарплатыВБухучете.ТаблицаНачисленийИВзносовДляБухучета(МенеджерВременныхТаблиц);
	
КонецФункции

// Заполняет параметры для расчета оценочных обязательств по отпускам.
//
// Параметры:
// 	Организация - СправочникСсылка.Организации.
// 	ПериодРегистрации - Дата.
// 	ПараметрыДляРасчета - Структура - Параметры, которые будут заполняться.
//                        См. ОтражениеЗарплатыВБухучете.ОписаниеПараметровДляРасчетаОценочныхОбязательствОтпусков.
// 	Сотрудники - Массив, Неопределенно.
//
Процедура ЗаполнитьПараметрыДляРасчетаОценочныхОбязательствОтпусков(Организация, ПериодРегистрации, ПараметрыДляРасчета, Сотрудники) Экспорт

	ПодработкаСотрудник = Новый Соответствие;
	СотрудникиДляНачисленийИВзносов = Сотрудники;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПодработки") И Сотрудники <> Неопределено Тогда
		
		// Для получения начислений и взносов дополним сотрудников подработками.
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	Сотрудники.ГоловнойСотрудник КАК ГоловнойСотрудник
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.ГоловнойСотрудник В(&Сотрудники)
		|	И Сотрудники.Ссылка <> Сотрудники.ГоловнойСотрудник";
		РезультатЗапроса = Запрос.Выполнить();
		
		Если Не РезультатЗапроса.Пустой() Тогда
			
			СотрудникиДляНачисленийИВзносов = ОбщегоНазначения.СкопироватьРекурсивно(Сотрудники);
			
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				ПодработкаСотрудник.Вставить(Выборка.Сотрудник, Выборка.ГоловнойСотрудник);
				СотрудникиДляНачисленийИВзносов.Добавить(Выборка.Сотрудник);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	НачисленнаяЗарплатаИВзносы = НачисленияИВзносыДляРасчетаОценочныхОбязательствОтпусков(Организация, ПериодРегистрации, СотрудникиДляНачисленийИВзносов);
	ФондОплатыТрудаИСтраховыеВзносы = ОтражениеЗарплатыВБухучете.ФондОплатыТрудаИСтраховыеВзносыДляРасчетаОценочныхОбязательствОтпусков(Организация, ПериодРегистрации, НачисленнаяЗарплатаИВзносы);
	
	Отбор = Новый Структура("РасчетПоСохраняемомуЗаработку", Ложь);
	
	ФОТПоСреднему = ФондОплатыТрудаИСтраховыеВзносы.Скопировать(Отбор);
	УдалитьСтрокиИсключаемыеИзРасчетаСреднегоЗаработка(ФОТПоСреднему, Организация, ПериодРегистрации);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ФОТПоСреднему, ПараметрыДляРасчета.ФондОплатыТрудаИСтраховыеВзносы);
	
	Отбор.РасчетПоСохраняемомуЗаработку = Истина;
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ФондОплатыТрудаИСтраховыеВзносы.Скопировать(Отбор), ПараметрыДляРасчета.ФондОплатыТрудаИСтраховыеВзносы);
	
	ОстаткиОтпусковДляРасчетаОценочныхОбязательств = ОтражениеЗарплатыВБухучете.ОстаткиОтпусковДляРасчетаОценочныхОбязательств(Организация, ПериодРегистрации, Сотрудники, "Подразделение,ТерриторияВыполненияРаботВОрганизации");
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ОстаткиОтпусковДляРасчетаОценочныхОбязательств, ПараметрыДляРасчета.ОстаткиОтпусков);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда 
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОрганизационнаяСтруктура");
		ПараметрыДляРасчета.ОстаткиОтпусков.ЗаполнитьЗначения(КонецМесяца(ПериодРегистрации),"ДатаНачала");
		ИменаТаблиц = "ФондОплатыТрудаИСтраховыеВзносы,ОстаткиОтпусков";
		Модуль.ДополнитьДанныеДокументаМестомВСтруктуреПредприятия(ПараметрыДляРасчета, ИменаТаблиц);
	КонецЕсли;
	
	Для каждого ЭлементСтруктуры Из ПараметрыДляРасчета Цикл
		Если ЭлементСтруктуры.Значение.Колонки.Найти("ПодразделениеУчетаЗатрат") <> Неопределено Тогда
			ЭлементСтруктуры.Значение.Колонки.Удалить("Подразделение");
			ЭлементСтруктуры.Значение.Колонки.ПодразделениеУчетаЗатрат.Имя = "Подразделение";
		КонецЕсли;
	КонецЦикла;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПодработки") И Сотрудники = Неопределено Тогда
		
		СотрудникиНачисленийИВзносов = ОбщегоНазначения.ВыгрузитьКолонку(ПараметрыДляРасчета.ФондОплатыТрудаИСтраховыеВзносы, "Сотрудник", Истина);
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Сотрудники", СотрудникиНачисленийИВзносов);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	Сотрудники.ГоловнойСотрудник КАК ГоловнойСотрудник
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Ссылка В(&Сотрудники)
		|	И Сотрудники.Ссылка <> Сотрудники.ГоловнойСотрудник";
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ПодработкаСотрудник.Вставить(Выборка.Сотрудник, Выборка.ГоловнойСотрудник);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ПодработкаСотрудник.Количество() > 0 Тогда
		Для каждого СтрокаТЗ Из ПараметрыДляРасчета.ФондОплатыТрудаИСтраховыеВзносы Цикл
			ГоловнойСотрудник = ПодработкаСотрудник[СтрокаТЗ.Сотрудник];
			Если ГоловнойСотрудник <> Неопределено Тогда
				СтрокаТЗ.Сотрудник = ГоловнойСотрудник;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Не выполняем свертку таблицы ОстаткиОтпусков, т.к. в ней могут быть сотрудники с нулевыми остатками.
	ВременныеПараметры = Новый Структура;
	ВременныеПараметры.Вставить("ФондОплатыТрудаИСтраховыеВзносы", ПараметрыДляРасчета.ФондОплатыТрудаИСтраховыеВзносы);
	ОтражениеЗарплатыВБухучете.СвернутьДанныеДляОтраженияЗарплатыВБухучете(ВременныеПараметры, "ДатаНачала,Начисление");
	
	ПараметрыДляРасчета.ФондОплатыТрудаИСтраховыеВзносы = ВременныеПараметры.ФондОплатыТрудаИСтраховыеВзносы;
	ПараметрыДляРасчета.ОстаткиОтпусков.Колонки.Удалить("ДатаНачала");

КонецПроцедуры

Процедура СоздатьВТНачисленияБазаОтпуска(МенеджерВременныхТаблиц)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НастройкиНачислений.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА НастройкиНачислений.Ссылка.СтратегияОтраженияВСреднемЗаработке = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПоБазовымРасчетам
	|ПОМЕСТИТЬ ВТНачисленияБазаОтпуска
	|ИЗ
	|	ПланВидовРасчета.Начисления.СреднийЗаработокОбщий КАК НастройкиНачислений
	|ГДЕ
	|	НастройкиНачислений.Значение <> ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.НеВключать)";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура УдалитьСтрокиИсключаемыеИзРасчетаСреднегоЗаработка(ФондОплатыТрудаИСтраховыеВзносы, Организация, ПериодРегистрации)
	
	Если ФондОплатыТрудаИСтраховыеВзносы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Сотрудники = ОбщегоНазначения.ВыгрузитьКолонку(ФондОплатыТрудаИСтраховыеВзносы, "Сотрудник", Истина);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.Регистратор КАК Регистратор,
	|	Начисления.ПериодДействия КАК ПериодДействия,
	|	Начисления.НачислениеУдержание КАК Начисление,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.Подразделение КАК Подразделение,
	|	СУММА(Начисления.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВТНачисления
	|ИЗ
	|	РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК Начисления
	|ГДЕ
	|	Начисления.Организация = &Организация
	|	И Начисления.ГруппаНачисленияУдержанияВыплаты В (ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Начислено), ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Льготы))
	|	И Начисления.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Начисления.Сотрудник В(&Сотрудники)
	|
	|СГРУППИРОВАТЬ ПО
	|	Начисления.НачислениеУдержание,
	|	Начисления.ПериодДействия,
	|	Начисления.Регистратор,
	|	Начисления.ДатаНачала,
	|	Начисления.ДатаОкончания,
	|	Начисления.Сотрудник,
	|	Начисления.Подразделение";
	Запрос.Выполнить();
	
	// Обработка начислений, исключаемых из среднего заработка в периоде командировок.
	// Если есть исключаемые командировки, в этой переменной будет таблица значений с полями:
	// Регистратор,Сотрудник,Подразделение,Начисление,ДатаНачала,Сумма,РезультатБаза. 
	НачисленияИсключаемыеВПериодКомандировок = Неопределено;
	ОбработатьСтрокиНачисленийИсключаемыеВПериодКомандировок(Запрос.МенеджерВременныхТаблиц, ФондОплатыТрудаИСтраховыеВзносы, ПериодРегистрации, НачисленияИсключаемыеВПериодКомандировок);
	
	// Исключение из ФОТ РК и СН, приходящихся на не включаемые в расчет среднего суммы.
	ОбработатьСтрокиНачисленийИсключаемыеИзРаспределяемыхПоБазеСреднего(Запрос.МенеджерВременныхТаблиц, ФондОплатыТрудаИСтраховыеВзносы, ПериодРегистрации, НачисленияИсключаемыеВПериодКомандировок);
	
КонецПроцедуры

Процедура ОбработатьСтрокиНачисленийИсключаемыеВПериодКомандировок(МенеджерВременныхТаблиц, ФондОплатыТрудаИСтраховыеВзносы, ПериодРегистрации, НачисленияИсключаемыеВПериодКомандировок)

	ИсключатьНачисленияВПериодКомандировок = Ложь;
	УчетСреднегоЗаработка.СоздатьВТДляИсключенияКомандировок(МенеджерВременныхТаблиц, ИсключатьНачисленияВПериодКомандировок);
	
	УдалитьВТ = Новый массив;
	
	Если ИсключатьНачисленияВПериодКомандировок Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Начисления.Регистратор КАК Регистратор
		|ИЗ
		|	ВТНачисления КАК Начисления";
		РегистраторыДляОтбора = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
		
		Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	&ПериодРегистрации КАК ПериодРегистрации,
		|	ПериодыКомандировок.Регистратор КАК Регистратор,
		|	ПериодыКомандировок.НомерСтроки КАК НомерСтроки,
		|	ПериодыКомандировок.Сотрудник КАК Сотрудник,
		|	ПериодыКомандировок.ВидРасчета КАК ВидРасчета,
		|	ПериодыКомандировок.Начало КАК БазовыйПериодНачало,
		|	ПериодыКомандировок.Окончание КАК БазовыйПериодКонец
		|ПОМЕСТИТЬ ВТОсновныеЗаписи
		|ИЗ
		|	ВТПериодыКомандировок КАК ПериодыКомандировок";
		Запрос.Выполнить();
		УдалитьВТ.Добавить("ВТОсновныеЗаписи");
		
		// Готовим запрос к расчетной базе начислений.
		ИменаИзмерений = РасчетЗарплатыРасширенный.ИменаИзмеренийРасчетнойБазыНачислений();
		ИменаИзмерений.Сотрудник = "Сотрудник";
		
		ОтборБазовыхЗаписей = Новый Массив;
		ОтборБазовыхЗаписей.Добавить(РасчетЗарплатыРасширенный.ЭлементОтбораБазовыхЗаписей("Регистратор", РегистраторыДляОтбора));
		
		РасчетЗарплатыРасширенный.СоздатьВТРасчетнаяБаза(МенеджерВременныхТаблиц, ИменаИзмерений, "ВТИсключаемыеВПериодКомандировки", ОтборБазовыхЗаписей);
		УдалитьВТ.Добавить("ВТРасчетнаяБаза");
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасчетнаяБаза.РегистраторРазрез КАК Регистратор,
		|	РасчетнаяБаза.ВидРасчетаРазрез КАК ВидРасчета,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	НАЧАЛОПЕРИОДА(Начисления.ПериодДействияНачало, ДЕНЬ) КАК ДатаНачала,
		|	НАЧАЛОПЕРИОДА(Начисления.ПериодДействияКонец, ДЕНЬ) КАК ДатаОкончания,
		|	СУММА(РасчетнаяБаза.РезультатБаза) КАК РезультатБаза
		|ПОМЕСТИТЬ ВТРасчетнаяБазаСПериодом
		|ИЗ
		|	ВТРасчетнаяБаза КАК РасчетнаяБаза
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.Начисления КАК Начисления
		|		ПО РасчетнаяБаза.РегистраторРазрез = Начисления.Регистратор
		|			И РасчетнаяБаза.НомерСтрокиРазрез = Начисления.НомерСтроки
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетнаяБаза.РегистраторРазрез,
		|	РасчетнаяБаза.ВидРасчетаРазрез,
		|	НАЧАЛОПЕРИОДА(Начисления.ПериодДействияНачало, ДЕНЬ),
		|	НАЧАЛОПЕРИОДА(Начисления.ПериодДействияКонец, ДЕНЬ),
		|	Начисления.Сотрудник
		|
		|ИМЕЮЩИЕ
		|	СУММА(РасчетнаяБаза.РезультатБаза) <> 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.Регистратор КАК Регистратор,
		|	Начисления.Подразделение КАК Подразделение,
		|	Начисления.Начисление КАК Начисление,
		|	Начисления.ДатаНачала КАК ДатаНачала,
		|	Начисления.ДатаОкончания КАК ДатаОкончания,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	СУММА(Начисления.Сумма) КАК Сумма,
		|	СУММА(РасчетнаяБазаСПериодом.РезультатБаза) КАК РезультатБаза
		|ИЗ
		|	ВТНачисления КАК Начисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасчетнаяБазаСПериодом КАК РасчетнаяБазаСПериодом
		|		ПО Начисления.Регистратор = РасчетнаяБазаСПериодом.Регистратор
		|			И Начисления.Начисление = РасчетнаяБазаСПериодом.ВидРасчета
		|			И Начисления.ДатаНачала = РасчетнаяБазаСПериодом.ДатаНачала
		|			И Начисления.ДатаОкончания = РасчетнаяБазаСПериодом.ДатаОкончания
		|			И Начисления.Сотрудник = РасчетнаяБазаСПериодом.Сотрудник
		|
		|СГРУППИРОВАТЬ ПО
		|	Начисления.Регистратор,
		|	Начисления.ДатаНачала,
		|	Начисления.Начисление,
		|	Начисления.ДатаОкончания,
		|	Начисления.Сотрудник,
		|	Начисления.Подразделение";
		
		НачисленияИсключаемыеВПериодКомандировок = Запрос.Выполнить().Выгрузить();
		УдалитьВТ.Добавить("ВТРасчетнаяБазаСПериодом");
		
		КолонкиОтбора = "Сотрудник,Начисление,ДатаНачала,Подразделение";
		ФондОплатыТрудаИСтраховыеВзносы.Индексы.Добавить(КолонкиОтбора);
		Отбор = Новый Структура(КолонкиОтбора);
		
		УдаляемыеСтроки = Новый Соответствие;
		
		Для каждого СтрокаТЗ Из НачисленияИсключаемыеВПериодКомандировок Цикл
			
			Если (СтрокаТЗ.Сумма>0 И СтрокаТЗ.РезультатБаза<0) Или (СтрокаТЗ.Сумма<0 И СтрокаТЗ.РезультатБаза>0) Тогда
				// Суммы разного знака, такие строки не обрабатываем.
				Продолжить;
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(Отбор, СтрокаТЗ);
			СтрокиФОТ = ФондОплатыТрудаИСтраховыеВзносы.НайтиСтроки(Отбор);
			
			Если СтрокаТЗ.Сумма > 0 И СтрокаТЗ.Сумма <= СтрокаТЗ.РезультатБаза
					ИЛИ СтрокаТЗ.Сумма < 0 И СтрокаТЗ.Сумма >= СтрокаТЗ.РезультатБаза Тогда
					
				// Сумма меньше или равна базе, полностью исключаем строку.
					
				Для каждого СтрокаФОТ Из СтрокиФОТ Цикл
					УдаляемыеСтроки.Вставить(СтрокаФОТ);
				КонецЦикла;
				
				Продолжить;
				
			КонецЕсли;
			
			ДоляБазы = (СтрокаТЗ.Сумма - СтрокаТЗ.РезультатБаза)/СтрокаТЗ.Сумма;
			
			ПривестиСуммыФОТКДолеБазы(СтрокиФОТ, ДоляБазы);
			
		КонецЦикла;
		
		ФондОплатыТрудаИСтраховыеВзносы.Индексы.Очистить();
		
		Для каждого ЭлементКоллекции Из УдаляемыеСтроки Цикл
			ФондОплатыТрудаИСтраховыеВзносы.Удалить(ЭлементКоллекции.Ключ);
		КонецЦикла;
		
		ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, УдалитьВТ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьСтрокиНачисленийИсключаемыеИзРаспределяемыхПоБазеСреднего(МенеджерВременныхТаблиц, ФондОплатыТрудаИСтраховыеВзносы, ПериодРегистрации, НачисленияИсключаемыеВПериодКомандировок)
	
	СоздатьВТНачисленияБазаОтпуска(МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	// Проверка наличия начислений, которые входят в средний заработок по базовым расчетам.
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.Начисление КАК Начисление,
	|	НачисленияБазовыеВидыРасчета.ВидРасчета КАК БазовыйВидРасчета
	|ПОМЕСТИТЬ ВТРаспределяемыеПоБазе
	|ИЗ
	|	ВТНачисления КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисленияБазаОтпуска КАК НачисленияБазаОтпуска
	|		ПО Начисления.Начисление = НачисленияБазаОтпуска.Ссылка
	|			И (НачисленияБазаОтпуска.ПоБазовымРасчетам)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.БазовыеВидыРасчета КАК НачисленияБазовыеВидыРасчета
	|		ПО Начисления.Начисление = НачисленияБазовыеВидыРасчета.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.Сотрудник КАК Сотрудник
	|ИЗ
	|	ВТНачисления КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРаспределяемыеПоБазе КАК РаспределяемыеПоБазе
	|		ПО Начисления.Начисление = РаспределяемыеПоБазе.Начисление
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РаспределяемыеПоБазе.Начисление КАК Начисление
	|ИЗ
	|	ВТРаспределяемыеПоБазе КАК РаспределяемыеПоБазе
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК НастройкиНачислений
	|		ПО РаспределяемыеПоБазе.Начисление = НастройкиНачислений.Ссылка
	|			И (НастройкиНачислений.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам))";
	
	Результат = Запрос.ВыполнитьПакет();
	
	// Сотрудники, имеющие начисления, отражаемые в учете среднего по базе.
	СотрудникиПоБазе = Результат[1].Выгрузить().ВыгрузитьКолонку("Сотрудник");
	
	Если СотрудникиПоБазе.Количество() = 0 Тогда
		// Нет начислений, распределяемых по базе.
		Возврат;
	КонецЕсли;
	
	// Проверка наличия начислений, которые входят в состав РК и СН и не входят в расчет среднего.
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РаспределяемыеПоБазе.БазовыйВидРасчета КАК БазовыйВидРасчета
	|ПОМЕСТИТЬ ВТБазовыеВидыРасчета
	|ИЗ
	|	ВТРаспределяемыеПоБазе КАК РаспределяемыеПоБазе
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияБазаОтпуска КАК НачисленияБазаОтпуска
	|		ПО РаспределяемыеПоБазе.БазовыйВидРасчета = НачисленияБазаОтпуска.Ссылка
	|ГДЕ
	|	НачисленияБазаОтпуска.ПоБазовымРасчетам ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Поле1
	|ИЗ
	|	ВТБазовыеВидыРасчета КАК БазовыеВидыРасчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисления КАК Начисления
	|		ПО БазовыеВидыРасчета.БазовыйВидРасчета = Начисления.Начисление";
	НетБазовыхНачислений = Запрос.Выполнить().Пустой();
	
	// Проверка наличия в составе базовых начислений РК и СН исключенных в период командировки начислений.
	// Определим переменную как пустую коллекцию.
	НачисленияИсключаемые = Новый Массив;
	Если НачисленияИсключаемыеВПериодКомандировок <> Неопределено 
		И НачисленияИсключаемыеВПериодКомандировок.Количество() > 0 Тогда
		
		НачисленияИсключаемые = НачисленияИсключаемыеВПериодКомандировок.СкопироватьКолонки();
		НачисленияИсключаемыеВПериодКомандировок.Индексы.Добавить("Сотрудник");
		
		Отбор = Новый Структура("Сотрудник");
		Для каждого Сотрудник Из СотрудникиПоБазе Цикл
			Отбор.Сотрудник = Сотрудник;
			НайденныеСтроки = НачисленияИсключаемыеВПериодКомандировок.НайтиСтроки(Отбор);
			Для каждого СтрокаТЗ Из НайденныеСтроки Цикл
				ЗаполнитьЗначенияСвойств(НачисленияИсключаемые.Добавить(), СтрокаТЗ);
			КонецЦикла;
		КонецЦикла;
		
	КонецЕсли;
	
	Если НетБазовыхНачислений И НачисленияИсключаемые.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// ВТНачисленияПоБазе - отобранные из таблицы ВТНачисления
	// входящие в средний по базовым начислениям.
	Запрос.УстановитьПараметр("Сотрудники", СотрудникиПоБазе);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.Регистратор КАК Регистратор,
	|	Начисления.ПериодДействия КАК ПериодДействия,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.Подразделение КАК Подразделение,
	|	Начисления.Сумма КАК Сумма
	|ПОМЕСТИТЬ ВТНачисленияПоБазе
	|ИЗ
	|	ВТНачисления КАК Начисления
	|ГДЕ
	|	Начисления.Сотрудник В(&Сотрудники)
	|	И Начисления.Начисление В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				РаспределяемыеПоБазе.Начисление
	|			ИЗ
	|				ВТРаспределяемыеПоБазе КАК РаспределяемыеПоБазе)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.Регистратор КАК Регистратор,
	|	Начисления.НомерСтроки КАК НомерСтроки,
	|	НачисленияПоБазе.Сотрудник КАК Сотрудник,
	|	НачисленияПоБазе.Подразделение КАК Подразделение,
	|	НачисленияПоБазе.Начисление КАК Начисление,
	|	НачисленияПоБазе.ДатаНачала КАК ДатаНачала,
	|	НачисленияПоБазе.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТНачисленияПоБазеИзРегистраРасчета
	|ИЗ
	|	ВТНачисленияПоБазе КАК НачисленияПоБазе
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.Начисления КАК Начисления
	|		ПО НачисленияПоБазе.Регистратор = Начисления.Регистратор
	|			И НачисленияПоБазе.Сотрудник = Начисления.Сотрудник
	|			И НачисленияПоБазе.Начисление = Начисления.ВидРасчета
	|			И (НачисленияПоБазе.Сумма <> 0)
	|			И НачисленияПоБазе.ДатаНачала = Начисления.ПериодДействияНачало
	|			И (НачисленияПоБазе.ДатаОкончания = НАЧАЛОПЕРИОДА(Начисления.ПериодДействияКонец, ДЕНЬ))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ПериодРегистрации КАК ПериодРегистрации,
	|	Начисления.Регистратор КАК Регистратор,
	|	Начисления.НомерСтроки КАК НомерСтроки,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.Начисление КАК ВидРасчета,
	|	Начисления.ДатаНачала КАК БазовыйПериодНачало,
	|	Начисления.ДатаОкончания КАК БазовыйПериодКонец
	|ПОМЕСТИТЬ ВТОтборНачислений
	|ИЗ
	|	ВТНачисленияПоБазеИзРегистраРасчета КАК Начисления";
	
	Запрос.Выполнить();
	
	// Получение базы.
	РасчетЗарплатыРасширенный.СоздатьВТРасчетнаяБазаНачисленийПоВременнойТаблицеКаскадно(МенеджерВременныхТаблиц);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасчетнаяБаза.Регистратор КАК Регистратор,
	|	РасчетнаяБаза.НомерСтроки КАК НомерСтроки,
	|	РасчетнаяБаза.РегистраторРазрез КАК РегистраторРазрез,
	|	РасчетнаяБаза.ВидРасчетаРазрез КАК ВидРасчетаРазрез,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	НАЧАЛОПЕРИОДА(Начисления.ПериодДействияНачало, ДЕНЬ) КАК ДатаНачала,
	|	НАЧАЛОПЕРИОДА(Начисления.ПериодДействияКонец, ДЕНЬ) КАК ДатаОкончания,
	|	СУММА(РасчетнаяБаза.РезультатБаза) КАК РезультатБаза
	|ПОМЕСТИТЬ ВТРасчетнаяБазаСПериодом
	|ИЗ
	|	ВТРасчетнаяБаза КАК РасчетнаяБаза
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.Начисления КАК Начисления
	|		ПО РасчетнаяБаза.РегистраторРазрез = Начисления.Регистратор
	|			И РасчетнаяБаза.НомерСтрокиРазрез = Начисления.НомерСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(Начисления.ПериодДействияНачало, ДЕНЬ),
	|	РасчетнаяБаза.Регистратор,
	|	НАЧАЛОПЕРИОДА(Начисления.ПериодДействияКонец, ДЕНЬ),
	|	Начисления.Сотрудник,
	|	РасчетнаяБаза.НомерСтроки,
	|	РасчетнаяБаза.РегистраторРазрез,
	|	РасчетнаяБаза.ВидРасчетаРазрез
	|
	|ИМЕЮЩИЕ
	|	СУММА(РасчетнаяБаза.РезультатБаза) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.Регистратор КАК Регистратор,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.Подразделение КАК Подразделение,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	СУММА(РасчетнаяБазаСПериодом.РезультатБаза) КАК РезультатБаза,
	|	РасчетнаяБазаСПериодом.РегистраторРазрез КАК РегистраторРазрез,
	|	РасчетнаяБазаСПериодом.ВидРасчетаРазрез КАК ВидРасчетаРазрез,
	|	РасчетнаяБазаСПериодом.ДатаНачала КАК ДатаНачалаБаза,
	|	РасчетнаяБазаСПериодом.ДатаОкончания КАК ДатаОкончанияБаза,
	|	ВЫБОР
	|		КОГДА НачисленияБазаОтпуска.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ВходитВБазуОтпуска
	|ПОМЕСТИТЬ ВТНачисленияПоБазаИБазовые
	|ИЗ
	|	ВТНачисленияПоБазеИзРегистраРасчета КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасчетнаяБазаСПериодом КАК РасчетнаяБазаСПериодом
	|		ПО Начисления.Регистратор = РасчетнаяБазаСПериодом.Регистратор
	|			И Начисления.НомерСтроки = РасчетнаяБазаСПериодом.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияБазаОтпуска КАК НачисленияБазаОтпуска
	|		ПО (РасчетнаяБазаСПериодом.ВидРасчетаРазрез = НачисленияБазаОтпуска.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетнаяБазаСПериодом.РегистраторРазрез,
	|	Начисления.Сотрудник,
	|	Начисления.Регистратор,
	|	ВЫБОР
	|		КОГДА НачисленияБазаОтпуска.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	РасчетнаяБазаСПериодом.ВидРасчетаРазрез,
	|	РасчетнаяБазаСПериодом.ДатаНачала,
	|	РасчетнаяБазаСПериодом.ДатаОкончания,
	|	Начисления.ДатаОкончания,
	|	Начисления.Подразделение,
	|	Начисления.ДатаНачала,
	|	Начисления.Начисление
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.Регистратор КАК Регистратор,
	|	Начисления.Сотрудник КАК Сотрудник,
	|	Начисления.Подразделение КАК Подразделение,
	|	Начисления.Начисление КАК Начисление,
	|	Начисления.ДатаНачала КАК ДатаНачала,
	|	Начисления.ДатаОкончания КАК ДатаОкончания,
	|	ВЫБОР
	|		КОГДА НастройкиНачислений.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК БухучетПоБазовымРасчетам,
	|	СУММА(РасчетнаяБазаСПериодом.РезультатБаза) КАК РезультатБаза
	|ИЗ
	|	ВТНачисленияПоБазеИзРегистраРасчета КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасчетнаяБазаСПериодом КАК РасчетнаяБазаСПериодом
	|		ПО Начисления.Регистратор = РасчетнаяБазаСПериодом.Регистратор
	|			И Начисления.НомерСтроки = РасчетнаяБазаСПериодом.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК НастройкиНачислений
	|		ПО Начисления.Начисление = НастройкиНачислений.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Начисления.ДатаОкончания,
	|	Начисления.ДатаНачала,
	|	Начисления.Сотрудник,
	|	Начисления.Регистратор,
	|	Начисления.Начисление,
	|	Начисления.Подразделение,
	|	ВЫБОР
	|		КОГДА НастройкиНачислений.СтратегияОтраженияВУчете = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|
	|ИМЕЮЩИЕ
	|	СУММА(РасчетнаяБазаСПериодом.РезультатБаза) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БазовыеНачисления.Регистратор КАК Регистратор,
	|	БазовыеНачисления.Сотрудник КАК Сотрудник,
	|	БазовыеНачисления.Подразделение КАК Подразделение,
	|	БазовыеНачисления.Начисление КАК Начисление,
	|	БазовыеНачисления.ДатаНачала КАК ДатаНачала,
	|	БазовыеНачисления.ДатаОкончания КАК ДатаОкончания,
	|	БазовыеНачисления.РезультатБаза КАК РезультатБаза,
	|	БазовыеНачисления.РегистраторРазрез КАК РегистраторРазрез,
	|	БазовыеНачисления.ВидРасчетаРазрез КАК ВидРасчетаРазрез,
	|	БазовыеНачисления.ДатаНачалаБаза КАК ДатаНачалаБаза,
	|	БазовыеНачисления.ДатаОкончанияБаза КАК ДатаОкончанияБаза,
	|	БазовыеНачисления.ВходитВБазуОтпуска КАК ВходитВБазуОтпуска
	|ИЗ
	|	ВТНачисленияПоБазаИБазовые КАК БазовыеНачисления";
	
	Результат = Запрос.ВыполнитьПакет();
	КоличествоРезультатов = Результат.ВГраница();
	
	// Начисления распределяемые по базе с колонкой РезультатБаза
	НачисленияПоБазе = Результат[КоличествоРезультатов-1].Выгрузить();
	БазовыеНачисления = Результат[КоличествоРезультатов].Выгрузить();
	
	КолонкиОтбора = "Регистратор,Сотрудник,Начисление,ДатаНачала,ДатаОкончания,Подразделение";
	БазовыеНачисления.Индексы.Добавить(КолонкиОтбора);
	ОтборБазы = Новый Структура(КолонкиОтбора);
	
	КолонкиОтбораЧастичноИсключаемых = "Регистратор,Сотрудник,Начисление,ДатаНачала,ДатаОкончания";
	ОтборЧастичноИсключаемых = Новый Структура(КолонкиОтбораЧастичноИсключаемых);
	
	ОбрабатыватьИсключаемыеИзКомандировок = (НачисленияИсключаемые.Количество()>0);
	Если ОбрабатыватьИсключаемыеИзКомандировок Тогда
		НачисленияИсключаемые.Индексы.Добавить(КолонкиОтбораЧастичноИсключаемых);
	КонецЕсли;
	
	КолонкиОтбораФОТ = "Сотрудник,Начисление,ДатаНачала,Подразделение";
	ФондОплатыТрудаИСтраховыеВзносы.Индексы.Добавить(КолонкиОтбораФОТ);
	ОтборФОТ = Новый Структура(КолонкиОтбораФОТ);
	УдаляемыеСтроки = Новый Соответствие;
	
	Для каждого СтрокаНачисления Из НачисленияПоБазе Цикл
		
		РезультатБаза = СтрокаНачисления.РезультатБаза;
		
		СтрокиИсключаемые = Новый Массив;
		СтрокиЧастичноИсключаемые = Новый Массив;
		
		ЗаполнитьЗначенияСвойств(ОтборФОТ, СтрокаНачисления);
		ЗаполнитьЗначенияСвойств(ОтборБазы, СтрокаНачисления);
		БазовыеСтроки = БазовыеНачисления.НайтиСтроки(ОтборБазы);
		
		Для каждого БазоваяСтрока Из БазовыеСтроки Цикл
			
			Если Не БазоваяСтрока.ВходитВБазуОтпуска Тогда
				СтрокиИсключаемые.Добавить(БазоваяСтрока);
			ИначеЕсли ОбрабатыватьИсключаемыеИзКомандировок Тогда
				
				ОтборЧастичноИсключаемых.Регистратор 	= БазоваяСтрока.РегистраторРазрез;
				ОтборЧастичноИсключаемых.Сотрудник 		= БазоваяСтрока.Сотрудник;
				ОтборЧастичноИсключаемых.Начисление 	= БазоваяСтрока.ВидРасчетаРазрез;
				ОтборЧастичноИсключаемых.ДатаНачала 	= БазоваяСтрока.ДатаНачалаБаза;
				ОтборЧастичноИсключаемых.ДатаОкончания 	= БазоваяСтрока.ДатаОкончанияБаза;
				
				НайденныеСтроки = НачисленияИсключаемые.НайтиСтроки(ОтборЧастичноИсключаемых);
				Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
					СтрокиЧастичноИсключаемые.Добавить(НайденнаяСтрока);
				КонецЦикла;
				
			КонецЕсли;
		
		КонецЦикла;
		
		СуммаИсключаемаяИзБазы = 0;
		Для каждого СтрокаИсключаемая Из СтрокиИсключаемые Цикл
			СуммаИсключаемаяИзБазы = СуммаИсключаемаяИзБазы + СтрокаИсключаемая.РезультатБаза;
		КонецЦикла;
		
		Для каждого СтрокаИсключаемая Из СтрокиЧастичноИсключаемые Цикл
			СуммаИсключаемаяИзБазы = СуммаИсключаемаяИзБазы + СтрокаИсключаемая.РезультатБаза;
		КонецЦикла;
		
		// Доля расчетной базы, включаемая в расчет среднего.
		ДоляБазы = ?(РезультатБаза=0,0,(РезультатБаза - СуммаИсключаемаяИзБазы)/РезультатБаза);
		
		Если ДоляБазы = 1 Тогда
			Продолжить;
		ИначеЕсли ДоляБазы = 0 Тогда
			
			СтрокиФОТ = ФондОплатыТрудаИСтраховыеВзносы.НайтиСтроки(ОтборФОТ);
			Для каждого СтрокаФОТ Из СтрокиФОТ Цикл
				УдаляемыеСтроки.Вставить(СтрокаФОТ);
			КонецЦикла;
			
			Продолжить;
			
		КонецЕсли;
		
		// Обработка строк у которых ДоляБазы больше 0 и меньше 1.
		
		СтрокиФОТ = ФондОплатыТрудаИСтраховыеВзносы.НайтиСтроки(ОтборФОТ);
		
		ПривестиСуммыФОТКДолеБазы(СтрокиФОТ, ДоляБазы);
	
	КонецЦикла;
	
	ФондОплатыТрудаИСтраховыеВзносы.Индексы.Очистить();
	
	Для каждого ЭлементКоллекции Из УдаляемыеСтроки Цикл
		ФондОплатыТрудаИСтраховыеВзносы.Удалить(ЭлементКоллекции.Ключ);
	КонецЦикла;

КонецПроцедуры

Процедура ПривестиСуммыФОТКДолеБазы(СтрокиФОТ, ДоляБазы)

	ФондОплатыТруда 	= 0;
	СтраховыеВзносы 	= 0;
	ФССНесчастныеСлучаи = 0;
	
	СтрокаДляПогрешностиФОТ 	= Неопределено;
	СтрокаДляПогрешностиВзносы 	= Неопределено;
	СтрокаДляПогрешностиФСС 	= Неопределено;
	МаксимальноеЗначениеФОТ		= 0;
	МаксимальноеЗначениеВзносы 	= 0;
	МаксимальноеЗначениеФСС 	= 0;
	
	Для каждого СтрокаФОТ Из СтрокиФОТ Цикл
		
		ФондОплатыТруда 	= ФондОплатыТруда + СтрокаФОТ.ФондОплатыТруда;
		СтраховыеВзносы 	= СтраховыеВзносы + СтрокаФОТ.СтраховыеВзносы;
		ФССНесчастныеСлучаи = ФССНесчастныеСлучаи + СтрокаФОТ.ФССНесчастныеСлучаи;
		
		АбсолютноеЗначение = ?(СтрокаФОТ.ФондОплатыТруда > 0, СтрокаФОТ.ФондОплатыТруда, -СтрокаФОТ.ФондОплатыТруда);
		Если МаксимальноеЗначениеФОТ < АбсолютноеЗначение Тогда
			МаксимальноеЗначениеФОТ = АбсолютноеЗначение;
			СтрокаДляПогрешностиФОТ = СтрокаФОТ;
		КонецЕсли;
		
		АбсолютноеЗначение = ?(СтрокаФОТ.СтраховыеВзносы > 0, СтрокаФОТ.СтраховыеВзносы, -СтрокаФОТ.СтраховыеВзносы);
		Если МаксимальноеЗначениеВзносы < АбсолютноеЗначение Тогда
			МаксимальноеЗначениеВзносы = АбсолютноеЗначение;
			СтрокаДляПогрешностиВзносы = СтрокаФОТ;
		КонецЕсли;
		
		АбсолютноеЗначение = ?(СтрокаФОТ.ФССНесчастныеСлучаи > 0, СтрокаФОТ.ФССНесчастныеСлучаи, -СтрокаФОТ.ФССНесчастныеСлучаи);
		Если МаксимальноеЗначениеФСС < АбсолютноеЗначение Тогда
			МаксимальноеЗначениеФСС = АбсолютноеЗначение;
			СтрокаДляПогрешностиФСС = СтрокаФОТ;
		КонецЕсли;
		
	КонецЦикла;
	
	ФондОплатыТруда 	= ОКР(ФондОплатыТруда * ДоляБазы, 2);
	СтраховыеВзносы 	= ОКР(СтраховыеВзносы * ДоляБазы, 2);
	ФССНесчастныеСлучаи = ОКР(ФССНесчастныеСлучаи * ДоляБазы, 2);
	
	НовыйФондОплатыТруда 	= 0;
	НовыйСтраховыеВзносы 	= 0;
	НовыйФССНесчастныеСлучаи = 0;
	
	Для каждого СтрокаФОТ Из СтрокиФОТ Цикл
		
		СтрокаФОТ.ФондОплатыТруда = ОКР(СтрокаФОТ.ФондОплатыТруда * ДоляБазы, 2);
		НовыйФондОплатыТруда = НовыйФондОплатыТруда + СтрокаФОТ.ФондОплатыТруда;
		
		СтрокаФОТ.СтраховыеВзносы = ОКР(СтрокаФОТ.СтраховыеВзносы * ДоляБазы, 2);
		НовыйСтраховыеВзносы = НовыйСтраховыеВзносы + СтрокаФОТ.СтраховыеВзносы;
		
		СтрокаФОТ.ФССНесчастныеСлучаи = ОКР(СтрокаФОТ.ФССНесчастныеСлучаи * ДоляБазы, 2);
		НовыйФССНесчастныеСлучаи = НовыйФССНесчастныеСлучаи + СтрокаФОТ.ФССНесчастныеСлучаи;
		
	КонецЦикла;
	
	Если НовыйФондОплатыТруда <> ФондОплатыТруда Тогда
		СтрокаДляПогрешностиФОТ.ФондОплатыТруда = СтрокаДляПогрешностиФОТ.ФондОплатыТруда + ФондОплатыТруда - НовыйФондОплатыТруда;
	КонецЕсли;
	
	Если НовыйСтраховыеВзносы <> СтраховыеВзносы Тогда
		СтрокаДляПогрешностиВзносы.СтраховыеВзносы = СтрокаДляПогрешностиФОТ.СтраховыеВзносы + СтраховыеВзносы - НовыйСтраховыеВзносы;
	КонецЕсли;
	
	Если НовыйФССНесчастныеСлучаи <> ФССНесчастныеСлучаи Тогда
		СтрокаДляПогрешностиФСС.ФССНесчастныеСлучаи = СтрокаДляПогрешностиФОТ.ФССНесчастныеСлучаи + ФССНесчастныеСлучаи - НовыйФССНесчастныеСлучаи;
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьПараметрыПолученияФОТОрганизацийДляОценочныхОбязательствОтпусков(Организации, Период, ТаблицаПараметров) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	СоздатьВТНачисленияБазаОтпуска(МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НачисленияБазаОтпуска.Ссылка КАК Ссылка
	|ИЗ
	|	ВТНачисленияБазаОтпуска КАК НачисленияБазаОтпуска";
	БазаОтпускаПоСреднемуЗаработку = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	УчитыватьОсобенностиПредприятия = Ложь;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		МодульГосударственнаяСлужба = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");
		УчитыватьОсобенностиПредприятия = МодульГосударственнаяСлужба.УчитыватьОсобенностиПредприятияПриРасчетеОценочныхОбязательствОтпусков();
	КонецЕсли;
	
	Если Не УчитыватьОсобенностиПредприятия Тогда
		
		НоваяСтрока = ТаблицаПараметров.Добавить();
		НоваяСтрока.Сотрудники = Неопределено;
		НоваяСтрока.БазовыеНачисления = БазаОтпускаПоСреднемуЗаработку;
		
	Иначе
		
		// Получим всех сотрудников, по которым собирается ФОТ текущего месяца.
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Организации", Организации);
		Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Период));
		Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Период));
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НачисленияПоСотрудникам.Сотрудник КАК Сотрудник
		|ИЗ
		|	РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК НачисленияПоСотрудникам
		|ГДЕ
		|	НачисленияПоСотрудникам.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И НачисленияПоСотрудникам.Организация В(&Организации)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СтраховыеВзносыПоФизическимЛицам.Сотрудник
		|ИЗ
		|	РегистрНакопления.СтраховыеВзносыПоФизическимЛицам КАК СтраховыеВзносыПоФизическимЛицам
		|ГДЕ
		|	СтраховыеВзносыПоФизическимЛицам.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СтраховыеВзносыПоФизическимЛицам.Организация В(&Организации)";
		
		УстановитьПривилегированныйРежим(Истина);
		СотрудникиДляРасчета = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Сотрудник");
		УстановитьПривилегированныйРежим(Ложь);
		
		// Получим таблицу, в которой указаны способы расчета ФОТ для каждого сотрудника.
		УстановитьПривилегированныйРежим(Истина);
		СотрудникиПоСпособамРасчета = СотрудникиПоСпособамРасчетаОбязательствОтпусков(СотрудникиДляРасчета, Период);
		УстановитьПривилегированныйРежим(Истина);
		
		СотрудникиПоСреднемуЗаработку  = СотрудникиПоСпособамРасчета.Скопировать(Новый Структура("РасчетПоСреднемуЗаработку", Истина)).ВыгрузитьКолонку("Сотрудник");
		Если СотрудникиПоСреднемуЗаработку.Количество() > 0 Тогда
			НоваяСтрока = ТаблицаПараметров.Добавить();
			НоваяСтрока.Сотрудники = СотрудникиПоСреднемуЗаработку;
			НоваяСтрока.БазовыеНачисления = БазаОтпускаПоСреднемуЗаработку;
		КонецЕсли;
		
		СотрудникиПоСохраняемомуЗаработку = СотрудникиПоСпособамРасчета.Скопировать(Новый Структура("РасчетПоСохраняемомуЗаработку", Истина)).ВыгрузитьКолонку("Сотрудник");
		Если СотрудникиПоСохраняемомуЗаработку.Количество() > 0 Тогда
			
			НоваяСтрока = ТаблицаПараметров.Добавить();
			НоваяСтрока.Сотрудники = СотрудникиПоСохраняемомуЗаработку;
			
			БазовыеНачисления = Новый Массив;
			Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба.РасчетДенежногоСодержания") Тогда
				МодульРасчетДенежногоСодержания = ОбщегоНазначения.ОбщийМодуль("РасчетДенежногоСодержания");
				БазовыеНачисления = МодульРасчетДенежногоСодержания.БазаОтпускаПоСохраняемомуЗаработкуДляОценочныхОбязательствОтпусков();
			КонецЕсли;
			НоваяСтрока.БазовыеНачисления = БазовыеНачисления;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыПолученияФОТСотрудниковДляОценочныхОбязательствОтпусков(СотрудникиДляРасчета, Период, ПараметрыПолученияФОТ) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	СоздатьВТНачисленияБазаОтпуска(МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НачисленияБазаОтпуска.Ссылка КАК Ссылка
	|ИЗ
	|	ВТНачисленияБазаОтпуска КАК НачисленияБазаОтпуска";
	БазаОтпускаПоСреднемуЗаработку = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	ПараметрыПолученияФОТ.БазаОтпускаПоСреднемуЗаработку = ОбщегоНазначенияБЗККлиентСервер.МассивВСоответствие(БазаОтпускаПоСреднемуЗаработку);
	
	УчитыватьОсобенностиПредприятия = Ложь;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		МодульГосударственнаяСлужба = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");
		УчитыватьОсобенностиПредприятия = МодульГосударственнаяСлужба.УчитыватьОсобенностиПредприятияПриРасчетеОценочныхОбязательствОтпусков();
	КонецЕсли;
	
	Если Не УчитыватьОсобенностиПредприятия Тогда
		
		ПараметрыПолученияФОТ.Сотрудники = ОбщегоНазначенияБЗККлиентСервер.МассивВСоответствие(СотрудникиДляРасчета);
		
	Иначе
		
		// Получим таблицу, в которой указаны способы расчета ФОТ для каждого сотрудника.
		СотрудникиПоСпособамРасчета = СотрудникиПоСпособамРасчетаОбязательствОтпусков(СотрудникиДляРасчета, Период);
		
		СотрудникиПоСреднемуЗаработку  = СотрудникиПоСпособамРасчета.Скопировать(Новый Структура("РасчетПоСреднемуЗаработку", Истина)).ВыгрузитьКолонку("Сотрудник");
		Если СотрудникиПоСреднемуЗаработку.Количество() > 0 Тогда
			ПараметрыПолученияФОТ.Сотрудники = ОбщегоНазначенияБЗККлиентСервер.МассивВСоответствие(СотрудникиПоСреднемуЗаработку);
		КонецЕсли;
		
		СотрудникиПоСохраняемомуЗаработку = СотрудникиПоСпособамРасчета.Скопировать(Новый Структура("РасчетПоСохраняемомуЗаработку", Истина)).ВыгрузитьКолонку("Сотрудник");
		Если СотрудникиПоСохраняемомуЗаработку.Количество() > 0 Тогда
			
			БазовыеНачисления = Новый Соответствие;
			Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба.РасчетДенежногоСодержания") Тогда
				МодульРасчетДенежногоСодержания = ОбщегоНазначения.ОбщийМодуль("РасчетДенежногоСодержания");
				БазовыеНачисления = МодульРасчетДенежногоСодержания.БазаОтпускаПоСохраняемомуЗаработкуДляОценочныхОбязательствОтпусков();
				БазовыеНачисления = ОбщегоНазначенияБЗККлиентСервер.МассивВСоответствие(БазовыеНачисления);
			КонецЕсли;
			ПараметрыПолученияФОТ.БазаОтпускаПоСохраняемомуЗаработку = БазовыеНачисления;
			
			Для каждого Сотрудник Из СотрудникиПоСохраняемомуЗаработку Цикл
				ПараметрыПолученияФОТ.Сотрудники.Вставить(Сотрудник, Ложь);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСведенияОбОтпускахСотрудниковДляОценочныхОбязательств(СведенияОбОтпусках, СотрудникиДляОбработки, Организация, Период) Экспорт
	
	УчитыватьОсобенностиПредприятия = Ложь;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		МодульГосударственнаяСлужба = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");
		УчитыватьОсобенностиПредприятия = МодульГосударственнаяСлужба.УчитыватьОсобенностиПредприятияПриРасчетеОценочныхОбязательствОтпусков();
	КонецЕсли;
	
	СотрудникиДляРасчета = СотрудникиДляОбработки;
	
	Если УчитыватьОсобенностиПредприятия Тогда
		ТаблицаСотрудников = СотрудникиПоСпособамРасчетаОбязательствОтпусков(СотрудникиДляОбработки, Период);
		СотрудникиДляРасчета = ТаблицаСотрудников.ВыгрузитьКолонку("Сотрудник");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сотрудники", СотрудникиДляРасчета);
	Запрос.УстановитьПараметр("ПериодНачисления", НачалоМесяца(Период));
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(Период));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФактическиеОтпуска.Сотрудник КАК Сотрудник,
	|	СУММА(ФактическиеОтпуска.Количество) КАК ОтпускАвансом
	|ИЗ
	|	РегистрНакопления.ФактическиеОтпуска КАК ФактическиеОтпуска
	|ГДЕ
	|	ФактическиеОтпуска.Сотрудник В(&Сотрудники)
	|	И НАЧАЛОПЕРИОДА(ФактическиеОтпуска.ПериодНачисления, МЕСЯЦ) = &ПериодНачисления
	|	И ФактическиеОтпуска.Период > &КонецМесяца
	|	И НЕ ФактическиеОтпуска.ВидЕжегодногоОтпуска.ОтпускБезОплаты
	|
	|СГРУППИРОВАТЬ ПО
	|	ФактическиеОтпуска.Сотрудник
	|
	|ИМЕЮЩИЕ
	|	СУММА(ФактическиеОтпуска.Количество) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыОтпусков.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ВидыОтпусков КАК ВидыОтпусков
	|ГДЕ
	|	ВидыОтпусков.ОтпускЯвляетсяЕжегодным
	|	И НЕ ВидыОтпусков.ОтпускБезОплаты";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ОтпускаАвансом = Новый Соответствие;
	Выборка = РезультатЗапроса[0].Выбрать();
	Пока Выборка.Следующий() Цикл
		ОтпускаАвансом.Вставить(Выборка.Сотрудник, Выборка.ОтпускАвансом);
	КонецЦикла;
	
	ВидыОтпусков = РезультатЗапроса[1].Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	ОстаткиОтпусковСотрудников = ОстаткиОтпусков.ОстаткиОтпусковСотрудниковНаДату(СотрудникиДляРасчета, КонецМесяца(Период), ВидыОтпусков);
	ОстаткиОтпусковСотрудников.Индексы.Добавить("Сотрудник");
	Отбор = Новый Структура("Сотрудник");
	
	Если УчитыватьОсобенностиПредприятия Тогда
		СотрудникиПоСреднемуЗаработку = ТаблицаСотрудников.Скопировать(Новый Структура("РасчетПоСреднемуЗаработку", Истина)).ВыгрузитьКолонку("Сотрудник");
	Иначе
		СотрудникиПоСреднемуЗаработку = СотрудникиДляРасчета;
	КонецЕсли;
	СпособыРасчетаОтпусковСотрудников = ОстаткиОтпусков.СпособыРасчетаОтпусковСотрудников(СотрудникиПоСреднемуЗаработку, КонецМесяца(Период));
	
	// Дата на которую рассчитывается средний и/или сохраняемый заработок,
	// первое число месяца, следующего за месяцем указанном в параметре Период.
	ДатаСобытия = НачалоМесяца(ДобавитьМесяц(Период, 1));
	
	Для каждого Сотрудник Из СотрудникиПоСреднемуЗаработку Цикл
		
		Отбор.Сотрудник = Сотрудник;
		
		НоваяСтрока = СведенияОбОтпусках.Добавить();
		НоваяСтрока.Сотрудник 		= Сотрудник;
		НоваяСтрока.ОтпускАвансом 	= ОтпускаАвансом[Сотрудник];
		
		ДополнительныеПараметры = УчетСреднегоЗаработкаКлиентСервер.ДополнительныеПараметрыРасчетаСреднегоЗаработка();
		ДополнительныеПараметры.СпособРасчетаОтпуска = СпособыРасчетаОтпусковСотрудников[Сотрудник];
		НоваяСтрока.СреднийЗаработок = УчетСреднегоЗаработка.СреднийЗаработок(Сотрудник, ДатаСобытия, ДополнительныеПараметры);
		
		НайденныеСтроки = ОстаткиОтпусковСотрудников.НайтиСтроки(Отбор);
		ОстатокОтпусков = 0;
		Если НайденныеСтроки.Количество() > 0 Тогда
			ОстатокОтпусков = НайденныеСтроки[0].КоличествоДней;
		КонецЕсли;
		НоваяСтрока.ОстатокОтпусков = ОстатокОтпусков;
		
	КонецЦикла;
	
	Если УчитыватьОсобенностиПредприятия Тогда
		
		СохраняемыйЗаработок = Новый Соответствие;
		СотрудникиПоСохраняемомуЗаработку = ТаблицаСотрудников.Скопировать(Новый Структура("РасчетПоСохраняемомуЗаработку", Истина)).ВыгрузитьКолонку("Сотрудник");
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба.РасчетДенежногоСодержания") Тогда
			МодульРасчетДенежногоСодержания = ОбщегоНазначения.ОбщийМодуль("РасчетДенежногоСодержания");
			СохраняемыйЗаработок = МодульРасчетДенежногоСодержания.СохраняемыйЗаработокСотрудниковДляОбязательствПоОтпускам(СотрудникиПоСохраняемомуЗаработку, Организация, Период);
		КонецЕсли;
		
		Для каждого Сотрудник Из СотрудникиПоСохраняемомуЗаработку Цикл
			
			Отбор.Сотрудник = Сотрудник;
			
			НоваяСтрока = СведенияОбОтпусках.Добавить();
			НоваяСтрока.Сотрудник 			= Сотрудник;
			НоваяСтрока.ОтпускАвансом 		= ОтпускаАвансом[Сотрудник];
			НоваяСтрока.СреднийЗаработок 	= СохраняемыйЗаработок[Сотрудник];
			НоваяСтрока.РасчетПоСохраняемомуЗаработку = Истина;
			
			НайденныеСтроки = ОстаткиОтпусковСотрудников.НайтиСтроки(Отбор);
			ОстатокОтпусков = 0;
			Если НайденныеСтроки.Количество() > 0 Тогда
				ОстатокОтпусков = НайденныеСтроки[0].КоличествоДней;
			КонецЕсли;
			НоваяСтрока.ОстатокОтпусков = ОстатокОтпусков;
			
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

Функция СотрудникиПоСпособамРасчетаОбязательствОтпусков(СотрудникиДляРасчета, Период)
	
	ТаблицаСотрудников = Новый ТаблицаЗначений;
	ТаблицаСотрудников.Колонки.Добавить("Сотрудник");
	ТаблицаСотрудников.Колонки.Добавить("РасчетПоСреднемуЗаработку", 	 Новый ОписаниеТипов("Булево"));
	ТаблицаСотрудников.Колонки.Добавить("РасчетПоСохраняемомуЗаработку", Новый ОписаниеТипов("Булево"));
	
	ЗапрашиваемыеДанные = "ВидДоговора,ВидДолжностиГосударственнойСлужбы,ЯвляетсяСудьей,ЯвляетсяВоеннослужащим";
	ДатаСобытия = КонецМесяца(Период);
	КадровыеДанныеСотрудников = КадровыйУчет.КадровыеДанныеСотрудников(Истина, СотрудникиДляРасчета, ЗапрашиваемыеДанные, ДатаСобытия);
	
	СтрокиДляДополнительнойОбработки = Новый Массив;
	Для каждого СтрокаТЗ Из КадровыеДанныеСотрудников Цикл
		
		Если СтрокаТЗ.ВидДоговора = Перечисления.ВидыДоговоровССотрудниками.ТрудовойДоговор Тогда
			НоваяСтрока = ТаблицаСотрудников.Добавить();
			НоваяСтрока.Сотрудник = СтрокаТЗ.Сотрудник;
			НоваяСтрока.РасчетПоСреднемуЗаработку = Истина;
		ИначеЕсли ЗначениеЗаполнено(СтрокаТЗ.ВидДоговора) Тогда
			СтрокиДляДополнительнойОбработки.Добавить(СтрокаТЗ);
		КонецЕсли;
		
	КонецЦикла;
	
	Если СтрокиДляДополнительнойОбработки.Количество() > 0 И ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		МодульГосударственнаяСлужба = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");
		МодульГосударственнаяСлужба.ДополнитьТаблицуСотрудниковПоСпособамРасчетаОбязательствОтпусков(ТаблицаСотрудников, СтрокиДляДополнительнойОбработки);
	КонецЕсли; 
	
	Возврат ТаблицаСотрудников;

КонецФункции 

#КонецОбласти

#Область ОписанияТаблицСтруктурЗначенийПоУмолчанию

Функция НоваяТаблицаБухучетНачисленияУдержанияПоСотрудникам() Экспорт
	
	Таблица = УчетНачисленнойЗарплатыРасширенный.НоваяТаблицаНачисленияУдержанияПоСотрудникам();
	Таблица.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(7, 0)));
	Таблица.Колонки.Добавить("СтатьяФинансирования", Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	Таблица.Колонки.Добавить("СтатьяРасходов", Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	Таблица.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете"));
	Таблица.Колонки.Добавить("ОблагаетсяЕНВД", Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("ВидОперации", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыОперацийПоЗарплате"));
	Таблица.Колонки.Добавить("ВидНачисленияОплатыТрудаДляНУ", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыНачисленийОплатыТрудаДляНУ"));
	Таблица.Колонки.Добавить("ПериодРегистрации", Новый ОписаниеТипов("Дата"));
	Таблица.Колонки.Добавить("ПериодПринятияРасходов", Новый ОписаниеТипов("Дата"));  
	Таблица.Колонки.Добавить("ОтношениеКЕНВД", Новый ОписаниеТипов("ПеречислениеСсылка.ОтношениеКЕНВДЗатратНаЗарплату"));
	Таблица.Колонки.Добавить("ПодразделениеУчетаЗатрат", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	
	Таблица.Колонки.НачислениеУдержание.Имя = "Начисление"; 

	Возврат Таблица;
	
КонецФункции  

Функция НоваяТаблицаБухучетНачисленияУдержанияПоКонтрагентамАкционерам() Экспорт
	
	МассивТиповНачислениеУдержание = Метаданные.РегистрыНакопления.БухучетНачисленияУдержанияПоКонтрагентамАкционерам.Измерения.НачислениеУдержание.Тип.Типы();
	МассивТиповДокументОснование = Метаданные.РегистрыНакопления.БухучетНачисленияУдержанияПоКонтрагентамАкционерам.Реквизиты.ДокументОснование.Тип.Типы();
	
	Таблица = Новый ТаблицаЗначений;

	Таблица.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	Таблица.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	Таблица.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	Таблица.Колонки.Добавить("Сотрудник",  Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	Таблица.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Таблица.Колонки.Добавить("Начисление", Новый ОписаниеТипов(МассивТиповНачислениеУдержание));
	Таблица.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	Таблица.Колонки.Добавить("ГруппаНачисленияУдержанияВыплаты", Новый ОписаниеТипов("ПеречислениеСсылка.ГруппыНачисленияУдержанияВыплаты"));
	Таблица.Колонки.Добавить("ДокументОснование", Новый ОписаниеТипов(МассивТиповДокументОснование));
	Таблица.Колонки.Добавить("ТерриторияВыполненияРаботВОрганизации", Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ТерриторияВыполненияРаботВОрганизации.Тип));
	Таблица.Колонки.Добавить("ДатаНачала", Новый ОписаниеТипов("Дата"));
	Таблица.Колонки.Добавить("ДатаОкончания", Новый ОписаниеТипов("Дата"));
	Таблица.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(7, 0)));
	Таблица.Колонки.Добавить("СтатьяФинансирования", Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	Таблица.Колонки.Добавить("СтатьяРасходов", Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	Таблица.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете"));
	Таблица.Колонки.Добавить("ОблагаетсяЕНВД", Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("ВидОперации", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыОперацийПоЗарплате"));

	Возврат Таблица;
	
КонецФункции

Функция НоваяТаблицаБухучетЗарплатыПервичныхДокументов() Экспорт
	
	МассивТиповНачисленияУдержания = Новый Массив;
	МассивТиповНачисленияУдержания.Добавить(Тип("ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний"));
	МассивТиповНачисленияУдержания.Добавить(Тип("ПланВидовРасчетаСсылка.Начисления"));
	МассивТиповНачисленияУдержания.Добавить(Тип("ПланВидовРасчетаСсылка.Удержания"));
	МассивТиповНачисленияУдержания.Добавить(Тип("СправочникСсылка.ВидыВыплатБывшимСотрудникам"));
	МассивТиповНачисленияУдержания.Добавить(Тип("СправочникСсылка.ВидыПрочихДоходовФизическихЛиц"));
	
	МассивТиповДокументОснование = Метаданные.РегистрыСведений.БухучетЗарплатыПервичныхДокументов.Измерения.ДокументОснование.Тип.Типы();
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("ДокументОснование",  Новый ОписаниеТипов(МассивТиповДокументОснование));
	Таблица.Колонки.Добавить("НачислениеУдержание",  Новый ОписаниеТипов(МассивТиповНачисленияУдержания));  
	Таблица.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете",  Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухучете"));
	Таблица.Колонки.Добавить("ОтношениеКЕНВД",  Новый ОписаниеТипов("ПеречислениеСсылка.ОтношениеКЕНВДЗатратНаЗарплату"));
	Таблица.Колонки.Добавить("СтатьяФинансирования",  Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	Таблица.Колонки.Добавить("СтатьяРасходов",  Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	
	Возврат Таблица;
КонецФункции

Функция ОписаниеИсходныхДанныхДляОтраженияНачисленийВБухучете() Экспорт

	Параметры = Новый Структура(
	"Организация,
	|МесяцНачисления,
	|МенеджерВременныхТаблиц,
	|МенеджерКадровогоУчета,
	|ИмяВТБухучетНачислений,
	|ИмяВТНачисления,
	|СтрокиБухучетСторноНачислений,
	|БухучетПервичногоДокумента,
	|ИсключаемыйРегистратор,
	|ПроцентЕНВД,
	|ПлательщикЕНВД,
	|СтрокиКоэффициентыСреднегоЗаработка,
	|СтрокиКоэффициентыСохраняемогоДС,
	|КоэффициентыСреднегоЗаработкаДокумента,
	|КоэффициентыСреднегоЗаработкаФССДокумента,
	|КоэффициентыРаспределенияДенежногоСодержания,
	|ВидыОперацийРасходыПоСтрахованию,
	|ВидыОперацийРасходыФСС,
	|УчитыватьСторноЗаписи,
	|МенеджерДанныхУчетаВремени,
	|РаспределениеСохраняемогоДС,
	|СоответствиеСотрудников,
	|ДатаНачалаДляБухучета,
	|МенеджерРасчетаЗарплаты,
	|РегистраторТекущегоРасчета,
	|ИсточникДанныхОДатахНачислений");
	
	Параметры.СтрокиБухучетСторноНачислений = Новый Соответствие;
	Параметры.БухучетПервичногоДокумента 	= НоваяТаблицаБухучетЗарплатыПервичныхДокументов();
	Параметры.СтрокиКоэффициентыСреднегоЗаработка 		= Новый Соответствие;
	Параметры.КоэффициентыСреднегоЗаработкаДокумента 	= Новый Соответствие;
	Параметры.КоэффициентыСреднегоЗаработкаФССДокумента = НоваяТаблицаКоэффициентыРаспределенияСреднегоЗаработка();
	Параметры.ИмяВТНачисления 				= "ВТНачисленияДляРаспределения";
	Параметры.УчитыватьСторноЗаписи 		= Ложь;
	Параметры.РаспределениеСохраняемогоДС 	= Ложь;
	Параметры.СоответствиеСотрудников 	  	= Новый Соответствие;
	Параметры.ДатаНачалаДляБухучета 		= Дата(0001,1,1);
	
	// Объект в котором есть метод ДатыНачисленийОплачивающихВидыВремени,
	Параметры.ИсточникДанныхОДатахНачислений = Неопределено;
	
	// Виды операций, которые обрабатываются при получении бухучета как расходы по страхованию.
	ВидыОперацийРасходыПоСтрахованию = Новый Массив;
	ВидыОперацийРасходыПоСтрахованию.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюБюджет);
	ВидыОперацийРасходыПоСтрахованию.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФСС);
	ВидыОперацийРасходыПоСтрахованию.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФССНС);
	ВидыОперацийРасходыПоСтрахованию.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПособиеНаПогребение);
	ВидыОперацийРасходыПоСтрахованию.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюРаботодатель);
	
	Параметры.ВидыОперацийРасходыПоСтрахованию = ВидыОперацийРасходыПоСтрахованию;
	
	// Виды операций, которые обрабатываются при получении бухучета как расходы ФСС.
	ВидыОперацийРасходыФСС = Новый Массив;
	ВидыОперацийРасходыФСС.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФСС); 
	ВидыОперацийРасходыФСС.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФССНС);
	ВидыОперацийРасходыФСС.Добавить(Перечисления.ВидыОперацийПоЗарплате.ПособиеНаПогребение);
	
	Параметры.ВидыОперацийРасходыФСС = ВидыОперацийРасходыФСС;
	
	Возврат Параметры;	

КонецФункции 

Функция ОписаниеИсходныхДанныхДляОтраженияУдержанийВБухучете() Экспорт

	Параметры = Новый Структура(
	"Организация,
	|МесяцНачисления,
	|МенеджерВременныхТаблиц,
	|ИмяВТБухучетНачислений,
	|ОкончательныйРасчет,
	|ИсключаемыйРегистратор,
	|ВидыНачисленийДополненияРасчетнойБазы,
	|ТаблицаУдержаний,
	|ТаблицаЗаймов,
	|РезультатРасчетаНДФЛ,
	|НДФЛКЗачету,
	|НДФЛЗачтено,
	|РегистраторыУдержанийОбновленияБухучета,
	|ОснованияУчтенныеПриРасчетеНДФЛ,
	|ЗасчитыватьДанныеАвансовНДФЛ");
	
	Возврат Параметры;	

КонецФункции

// Функция определяет состав видов особых начислений, 
//	включаемых в состав расчетной базы.
//
Функция ВидыНачисленийДополненияРасчетнойБазыУдержаний() Экспорт
	
	ВидыНачислений = Новый Массив;
	ВидыНачислений.Добавить(Перечисления.ВидыОсобыхНачисленийИУдержаний.ДоговорАвторскогоЗаказа);
	ВидыНачислений.Добавить(Перечисления.ВидыОсобыхНачисленийИУдержаний.ДоговорРаботыУслуги);
	ВидыНачислений.Добавить(Перечисления.ВидыОсобыхНачисленийИУдержаний.КомпенсацияЗаЗадержкуЗарплаты);
	ВидыНачислений.Добавить(Перечисления.ВидыОсобыхНачисленийИУдержаний.СтоимостьПодарковПризов);
	
	Возврат ВидыНачислений;
	
КонецФункции

Функция НоваяТаблицаКоэффициентыРаспределенияСреднегоЗаработка() Экспорт

	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("СтатьяФинансирования", 			 Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	Таблица.Колонки.Добавить("СтатьяРасходов", 					 Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	Таблица.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете"));
	Таблица.Колонки.Добавить("ОблагаетсяЕНВД", 					 Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("Коэффициент", 		 			 Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	
	Возврат Таблица;

КонецФункции

Функция НоваяТаблицаРаспределениеРезультатовНачислений() Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	
	Таблица.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(7, 0)));
	Таблица.Колонки.Добавить("ПодразделениеУчетаЗатрат", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Таблица.Колонки.Добавить("Территория", Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ТерриторияВыполненияРаботВОрганизации.Тип));
	Таблица.Колонки.Добавить("СтатьяФинансирования", Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	Таблица.Колонки.Добавить("СтатьяРасходов", Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	Таблица.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете"));
	Таблица.Колонки.Добавить("ОблагаетсяЕНВД", Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("Результат", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	Таблица.Колонки.Добавить("КодСтатьиФинансирования", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(3)));
	
	Возврат Таблица;
	
КонецФункции

Функция НоваяТаблицаБухучетНачислений() Экспорт

	ОписаниеТипаДата = Новый ОписаниеТипов("Дата",Новый КвалификаторыДаты(ЧастиДаты.Дата));
	
	МассивТиповНачисления = Новый Массив;
	МассивТиповНачисления.Добавить(Тип("ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний"));
	МассивТиповНачисления.Добавить(Тип("ПланВидовРасчетаСсылка.Начисления"));
	МассивТиповНачисления.Добавить(Тип("СправочникСсылка.ВидыВыплатБывшимСотрудникам"));
	МассивТиповНачисления.Добавить(Тип("СправочникСсылка.ВидыПрочихДоходовФизическихЛиц"));
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("ИдентификаторСтроки", 				Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(7,0)));
	Таблица.Колонки.Добавить("Организация",  						Новый ОписаниеТипов("СправочникСсылка.Организации"));
	Таблица.Колонки.Добавить("ФизическоеЛицо", 						Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	Таблица.Колонки.Добавить("Сотрудник",  							Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	Таблица.Колонки.Добавить("Подразделение", 						Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Таблица.Колонки.Добавить("ПодразделениеУчетаЗатрат", 			Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Таблица.Колонки.Добавить("Начисление", 							Новый ОписаниеТипов(МассивТиповНачисления));
	Таблица.Колонки.Добавить("ДатаНачала", 							ОписаниеТипаДата);
	Таблица.Колонки.Добавить("ДатаОкончания", 						ОписаниеТипаДата);
	Таблица.Колонки.Добавить("ПериодДействия", 						ОписаниеТипаДата);
	Таблица.Колонки.Добавить("Территория",							Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ТерриторияВыполненияРаботВОрганизации.Тип));
	Таблица.Колонки.Добавить("МестоПолученияДохода", 				Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ТерриторияВыполненияРаботВОрганизации.Тип));
	Таблица.Колонки.Добавить("СтатьяФинансирования",				Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	Таблица.Колонки.Добавить("СтатьяРасходов",  					Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	Таблица.Колонки.Добавить("ОблагаетсяЕНВД",						Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", 	Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухучете"));
	
	Таблица.Колонки.Добавить("Сумма", 		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	Таблица.Колонки.Добавить("Результат",	Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	
	Возврат Таблица;

КонецФункции

Функция ПараметрыДляЗаполненияТаблицДокумента() Экспорт

	Параметры = Новый Структура("
	|Организация,
	|ПериодРегистрации,
	|ДокументСсылка,
	|СтатьиФинансирования,
	|Подразделение");
	
	Возврат Параметры;

КонецФункции

Функция НоваяТаблицаКоэффициентыРаспределенияСохраняемогоДС() Экспорт

	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Сотрудник", 						 Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	Таблица.Колонки.Добавить("НазначениеРасчета", 				 Новый ОписаниеТипов("ПеречислениеСсылка.НазначениеРасчетаСохраняемогоДенежногоСодержания"));
	Таблица.Колонки.Добавить("ПодразделениеУчетаЗатрат", 		 Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Таблица.Колонки.Добавить("СтатьяФинансирования", 			 Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	Таблица.Колонки.Добавить("СтатьяРасходов", 					 Новый ОписаниеТипов("СправочникСсылка.СтатьиРасходовЗарплата"));
	Таблица.Колонки.Добавить("СпособОтраженияЗарплатыВБухучете", Новый ОписаниеТипов("СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете"));
	Таблица.Колонки.Добавить("ОблагаетсяЕНВД", 					 Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("Коэффициент", 		 			 Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	
	Возврат Таблица;

КонецФункции

Функция НоваяТаблицаНачисленияДляБухучета() Экспорт

	ОписаниеТипаДата = Новый ОписаниеТипов("Дата",Новый КвалификаторыДаты(ЧастиДаты.Дата));
	МассивТиповНачислениеУдержание = Метаданные.РегистрыНакопления.НачисленияУдержанияПоСотрудникам.Измерения.НачислениеУдержание.Тип.Типы();
	МассивТиповДокументОснование   = Метаданные.РегистрыНакопления.НачисленияУдержанияПоСотрудникам.Реквизиты.ДокументОснование.Тип.Типы();
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(7,0)));
	Таблица.Колонки.Добавить("ИдентификаторСтрокиНачисления", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(7,0)));
	Таблица.Колонки.Добавить("Организация", 		Новый ОписаниеТипов("СправочникСсылка.Организации"));
	Таблица.Колонки.Добавить("ФизическоеЛицо", 		Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	Таблица.Колонки.Добавить("Сотрудник",  			Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	Таблица.Колонки.Добавить("Подразделение",		Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	Таблица.Колонки.Добавить("Начисление", 			Новый ОписаниеТипов(МассивТиповНачислениеУдержание));
	Таблица.Колонки.Добавить("Результат",			Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	Таблица.Колонки.Добавить("РанееНачислено",		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	Таблица.Колонки.Добавить("ПериодРегистрации",	ОписаниеТипаДата);
	Таблица.Колонки.Добавить("ПериодДействия", 		ОписаниеТипаДата);
	Таблица.Колонки.Добавить("ДатаНачала", 			ОписаниеТипаДата);
	Таблица.Колонки.Добавить("ДатаОкончания", 		ОписаниеТипаДата);
	Таблица.Колонки.Добавить("ДокументОснование", 	Новый ОписаниеТипов(МассивТиповДокументОснование));
	Таблица.Колонки.Добавить("Территория",			Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ТерриторияВыполненияРаботВОрганизации.Тип));
	Таблица.Колонки.Добавить("МестоПолученияДохода",Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ТерриторияВыполненияРаботВОрганизации.Тип));
	Таблица.Колонки.Добавить("Сторно", 				Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("ФиксСторно", 			Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("КорректировкаРанееВыполненногоНачисления", 			Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("ВидОперации", 		Новый ОписаниеТипов("ПеречислениеСсылка.ВидыОперацийПоЗарплате"));
	Таблица.Колонки.Добавить("РассчитыватьПоРазовымНачислениямДокумента", Новый ОписаниеТипов("Булево"));
		
	Возврат Таблица;

КонецФункции

// Возвращает ссылку на элемент справочника СтатьиРасходовЗарплата
//	соответствующий способу расчетов РасчетыСКонтрагентами.
Функция СтатьяРасчетыСКонтрагентами() Экспорт 
	
	Если ПолучитьФункциональнуюОпцию("РаботаВХозрасчетнойОрганизации") Тогда
		ОписаниеСтатейРасходов = ЗарплатаКадры.СтатьиРасходовПоСпособамРасчетовСФизическимиЛицами();
		РасчетыСКонтрагентами = ОписаниеСтатейРасходов[Перечисления.СпособыРасчетовСФизическимиЛицами.РасчетыСКонтрагентами];
	Иначе
		РасчетыСКонтрагентами = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	КонецЕсли;
	
	Возврат РасчетыСКонтрагентами;
	
КонецФункции

// Возвращает ссылку на элемент справочника СтатьиРасходовЗарплата
//	соответствующий способу расчетов ПрочиеРасчетыСПерсоналом.
Функция СтатьяПрочиеРасчетыСПерсоналом() Экспорт 
	
	Если ПолучитьФункциональнуюОпцию("РаботаВХозрасчетнойОрганизации") Тогда
		ОписаниеСтатейРасходов = ЗарплатаКадры.СтатьиРасходовПоСпособамРасчетовСФизическимиЛицами();
		ПрочиеРасчетыСПерсоналом = ОписаниеСтатейРасходов[Перечисления.СпособыРасчетовСФизическимиЛицами.ПрочиеРасчетыСПерсоналом];
	Иначе
		ПрочиеРасчетыСПерсоналом = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	КонецЕсли;
	
	Возврат ПрочиеРасчетыСПерсоналом;
	
КонецФункции

#КонецОбласти

#Область ДанныеДляОтраженияЗарплатыВБухучете

Функция ДанныеДляОтраженияЗарплатыВБухучете(Организация, ПериодРегистрации) Экспорт

	ДанныеДляОтражения = ОтражениеЗарплатыВБухучете.НоваяСтруктураДанныеДляОтраженияЗарплатыВБухучете();
	
	Если ЗарплатаКадры.ИспользоватьСтатьиФинансированияЗарплата() Тогда
		
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		СведенияОБухучетеНачисленийИВзносовПоСтатьямФинансирования(Организация, ПериодРегистрации, МенеджерВременныхТаблиц);
		
		ДанныеДляОтражения.НачисленнаяЗарплатаИВзносы 	= ОтражениеЗарплатыВБухучете.ТаблицаНачисленийИВзносовДляБухучета(МенеджерВременныхТаблиц);
		ДанныеДляОтражения.НачисленныйНДФЛ 				= ДанныеДляОтраженияВБухучетеНДФЛПоСтатьямФинансирования(Организация, ПериодРегистрации);
		ДанныеДляОтражения.УдержаннаяЗарплата 			= ДанныеДляОтраженияВБухучетеУдержанийПоСтатьямФинансирования(Организация, ПериодРегистрации);
		
	Иначе
		
		ТаблицаНачислений = ОтражениеЗарплатыВУчете.ДанныеДляОтражениеВУчетеНачислений(Организация, ПериодРегистрации);
		ТаблицаВзносов 	  = ОтражениеЗарплатыВУчете.ДанныеДляОтражениеВУчетеВзносов(Организация, ПериодРегистрации);
		
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		ОтражениеЗарплатыВБухучете.СведенияОБухучетеНачисленийИВзносов(Организация, ПериодРегистрации, МенеджерВременныхТаблиц, ТаблицаНачислений, ТаблицаВзносов);
		ДанныеДляОтражения.НачисленнаяЗарплатаИВзносы = ОтражениеЗарплатыВБухучете.ТаблицаНачисленийИВзносовДляБухучета(МенеджерВременныхТаблиц);
		
		ТаблицаНДФЛ = ОтражениеЗарплатыВУчете.ДанныеДляОтражениеВУчетеНДФЛ(Организация, ПериодРегистрации);
		ДанныеДляОтражения.НачисленныйНДФЛ = ОтражениеЗарплатыВБухучете.ТаблицаНДФЛДляБухучета(ТаблицаНДФЛ);
		
		ТаблицаУдержаний = ОтражениеЗарплатыВУчете.ДанныеДляОтражениеВУчетеУдержаний(Организация, ПериодРегистрации);
		ДанныеДляОтражения.УдержаннаяЗарплата = ТаблицаУдержанийДляБухучета(ТаблицаУдержаний, ДанныеДляОтражения.НачисленнаяЗарплатаИВзносы);
		
	КонецЕсли;
	
	Возврат ДанныеДляОтражения;

КонецФункции

// Функция возвращает таблицу значений с данными бухучета удержаний.
//		Поля таблицы
//				ФизическоеЛицо - СправочникСсылка.ФизическиеЛица
//				Подразделение  - СправочникСсылка.ПодразделенияОрганизаций
//				ВидОперации    - ПеречислениеСсылка.ВидыОперацийПоЗарплате
//				СтатьяФинансирования - СправочникСсылка.СтатьиФинансированияЗарплата
//				СтатьяРасходов       - СправочникСсылка.СтатьиРасходовЗарплата
//				Контрагент           - СправочникСсылка.Контрагенты, контрагент, в пользу которого произведено удержание.
//				Сумма - Число 15.2.
//
// Параметры:
//	Организация - СправочникСсылка.Организации
//	ПериодРегистрации - Дата
//
Функция ДанныеДляОтраженияВБухучетеУдержанийПоСтатьямФинансирования(Организация, ПериодРегистрации)

	Запрос = Новый Запрос;
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	ВидыОсобыхНачисленийИУдержанийНДФЛ = ОтражениеЗарплатыВУчете.ВидыОсобыхНачисленийИУдержанийНДФЛ();
	Запрос.УстановитьПараметр("ВидыОсобыхНачисленийИУдержанийНДФЛ", ВидыОсобыхНачисленийИУдержанийНДФЛ);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	БухучетУдержаний.ФизическоеЛицо КАК ФизическоеЛицо,
	|	БухучетУдержаний.Сотрудник КАК Сотрудник,
	|	БухучетУдержаний.ВидОперации КАК ВидОперации,
	|	БухучетУдержаний.Подразделение КАК Подразделение,
	|	БухучетУдержаний.ДатаНачала КАК ДатаНачала,
	|	БухучетУдержаний.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	БухучетУдержаний.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетУдержаний.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетУдержаний.Контрагент КАК Контрагент,
	|	СУММА(БухучетУдержаний.Сумма) КАК Сумма,
	|	БухучетУдержаний.ЯвляетсяОснованиемОформленияКассовогоЧека КАК ЯвляетсяОснованиемОформленияКассовогоЧека,
	|	БухучетУдержаний.ОписаниеУдержанияДляЧека КАК ОписаниеУдержанияДляЧека
	|ИЗ
	|	(ВЫБРАТЬ
	|		БухучетУдержаний.ФизическоеЛицо КАК ФизическоеЛицо,
	|		БухучетУдержаний.Сотрудник КАК Сотрудник,
	|		БухучетУдержаний.ВидОперации КАК ВидОперации,
	|		БухучетУдержаний.Подразделение КАК Подразделение,
	|		БухучетУдержаний.ДатаНачала КАК ДатаНачала,
	|		ВЫБОР
	|			КОГДА БухучетУдержаний.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|				ТОГДА БухучетУдержаний.Подразделение
	|			ИНАЧЕ БухучетУдержаний.ПодразделениеУчетаЗатрат
	|		КОНЕЦ КАК ПодразделениеУчетаЗатрат,
	|		БухучетУдержаний.СтатьяФинансирования КАК СтатьяФинансирования,
	|		БухучетУдержаний.СтатьяРасходов КАК СтатьяРасходов,
	|		БухучетУдержаний.Контрагент КАК Контрагент,
	|		БухучетУдержаний.Сумма КАК Сумма,
	|		ЕСТЬNULL(Удержания.ЯвляетсяОснованиемОформленияКассовогоЧека, ЛОЖЬ) КАК ЯвляетсяОснованиемОформленияКассовогоЧека,
	|		ВЫБОР
	|			КОГДА Удержания.ЯвляетсяОснованиемОформленияКассовогоЧека = ИСТИНА
	|				ТОГДА Удержания.Наименование
	|			ИНАЧЕ """"
	|		КОНЕЦ КАК ОписаниеУдержанияДляЧека
	|	ИЗ
	|		РегистрНакопления.БухучетНачисленияУдержанияПоСотрудникам КАК БухучетУдержаний
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Удержания КАК Удержания
	|			ПО БухучетУдержаний.НачислениеУдержание = Удержания.Ссылка
	|	ГДЕ
	|		НАЧАЛОПЕРИОДА(БухучетУдержаний.Период, МЕСЯЦ) = &ПериодРегистрации
	|		И БухучетУдержаний.Организация = &Организация
	|		И БухучетУдержаний.Сумма <> 0
	|		И (БухучетУдержаний.ГруппаНачисленияУдержанияВыплаты = ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Удержано)
	|					И НЕ БухучетУдержаний.НачислениеУдержание В (&ВидыОсобыхНачисленийИУдержанийНДФЛ)
	|				ИЛИ БухучетУдержаний.НачислениеУдержание = ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.НачисленоПроцентовПоЗайму))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		БухучетУдержаний.ФизическоеЛицо,
	|		БухучетУдержаний.Сотрудник,
	|		БухучетУдержаний.ВидОперации,
	|		БухучетУдержаний.Подразделение,
	|		&ПериодРегистрации,
	|		БухучетУдержаний.Подразделение,
	|		БухучетУдержаний.СтатьяФинансирования,
	|		БухучетУдержаний.СтатьяРасходов,
	|		БухучетУдержаний.Контрагент,
	|		БухучетУдержаний.Сумма,
	|		ЕСТЬNULL(Удержания.ЯвляетсяОснованиемОформленияКассовогоЧека, ЛОЖЬ),
	|		ВЫБОР
	|			КОГДА Удержания.ЯвляетсяОснованиемОформленияКассовогоЧека = ИСТИНА
	|				ТОГДА Удержания.Наименование
	|			ИНАЧЕ """"
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.БухучетНачисленияУдержанияПоКонтрагентамАкционерам КАК БухучетУдержаний
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Удержания КАК Удержания
	|			ПО БухучетУдержаний.НачислениеУдержание = Удержания.Ссылка
	|	ГДЕ
	|		НАЧАЛОПЕРИОДА(БухучетУдержаний.Период, МЕСЯЦ) = &ПериодРегистрации
	|		И БухучетУдержаний.Организация = &Организация
	|		И БухучетУдержаний.Сумма <> 0
	|		И (БухучетУдержаний.ГруппаНачисленияУдержанияВыплаты = ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Удержано)
	|					И НЕ БухучетУдержаний.НачислениеУдержание В (&ВидыОсобыхНачисленийИУдержанийНДФЛ)
	|				ИЛИ БухучетУдержаний.НачислениеУдержание = ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.НачисленоПроцентовПоЗайму))) КАК БухучетУдержаний
	|
	|СГРУППИРОВАТЬ ПО
	|	БухучетУдержаний.Подразделение,
	|	БухучетУдержаний.ВидОперации,
	|	БухучетУдержаний.ПодразделениеУчетаЗатрат,
	|	БухучетУдержаний.СтатьяРасходов,
	|	БухучетУдержаний.Контрагент,
	|	БухучетУдержаний.ЯвляетсяОснованиемОформленияКассовогоЧека,
	|	БухучетУдержаний.СтатьяФинансирования,
	|	БухучетУдержаний.ОписаниеУдержанияДляЧека,
	|	БухучетУдержаний.ДатаНачала,
	|	БухучетУдержаний.Сотрудник,
	|	БухучетУдержаний.ФизическоеЛицо
	|
	|ИМЕЮЩИЕ
	|	СУММА(БухучетУдержаний.Сумма) <> 0";
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

// Функция возвращает таблицу значений с данными бухучета НДФЛ.
//		Поля таблицы
//				ФизическоеЛицо - СправочникСсылка.ФизическиеЛица
//				ВидОперации    - ПеречислениеСсылка.ВидыОперацийПоЗарплате
//				СтатьяФинансирования     - СправочникСсылка.СтатьиФинансированияЗарплата
//				СтатьяРасходов     - СправочникСсылка.СтатьиРасходовЗарплата
//				КодПоОКАТО     - Строка, 11
//				КодПоОКТМО     - Строка, 11
//				КПП   - Строка, 9
//				Сумма - Число 15.2.
//
// Параметры:
//	Организация - СправочникСсылка.Организации
//	ПериодРегистрации - Дата
//
Функция ДанныеДляОтраженияВБухучетеНДФЛПоСтатьямФинансирования(Организация, ПериодРегистрации)
	
	Запрос = Новый Запрос;
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("Организация", Организация);
	ВидыОсобыхНачисленийИУдержанийНДФЛ = ОтражениеЗарплатыВУчете.ВидыОсобыхНачисленийИУдержанийНДФЛ(Истина);
	Запрос.УстановитьПараметр("ВидыОсобыхНачисленийИУдержанийНДФЛ", ВидыОсобыхНачисленийИУдержанийНДФЛ);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	БухучетНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	БухучетНДФЛ.Сотрудник КАК Сотрудник,
	|	БухучетНДФЛ.ВидОперации КАК ВидОперации,
	|	БухучетНДФЛ.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНДФЛ.СтатьяРасходов КАК СтатьяРасходов,
	|	СУММА(БухучетНДФЛ.Сумма) КАК Сумма,
	|	БухучетНДФЛ.ТерриторияВыполненияРаботВОрганизации КАК Подразделение,
	|	БухучетНДФЛ.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ ВТТаблицаНДФЛ
	|ИЗ
	|	РегистрНакопления.БухучетНачисленияУдержанияПоСотрудникам КАК БухучетНДФЛ
	|ГДЕ
	|	БухучетНДФЛ.Организация = &Организация
	|	И НАЧАЛОПЕРИОДА(БухучетНДФЛ.Период, МЕСЯЦ) = &ПериодРегистрации
	|	И БухучетНДФЛ.НачислениеУдержание В(&ВидыОсобыхНачисленийИУдержанийНДФЛ)
	|
	|СГРУППИРОВАТЬ ПО
	|	БухучетНДФЛ.ФизическоеЛицо,
	|	БухучетНДФЛ.СтатьяРасходов,
	|	БухучетНДФЛ.СтатьяФинансирования,
	|	БухучетНДФЛ.ВидОперации,
	|	БухучетНДФЛ.Сотрудник,
	|	БухучетНДФЛ.ТерриторияВыполненияРаботВОрганизации,
	|	БухучетНДФЛ.РегистрацияВНалоговомОргане
	|
	|ИМЕЮЩИЕ
	|	СУММА(БухучетНДФЛ.Сумма) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	БухучетНДФЛ.ФизическоеЛицо,
	|	БухучетНДФЛ.Сотрудник,
	|	БухучетНДФЛ.ВидОперации,
	|	БухучетНДФЛ.СтатьяФинансирования,
	|	БухучетНДФЛ.СтатьяРасходов,
	|	СУММА(БухучетНДФЛ.Сумма),
	|	БухучетНДФЛ.ТерриторияВыполненияРаботВОрганизации,
	|	БухучетНДФЛ.РегистрацияВНалоговомОргане
	|ИЗ
	|	РегистрНакопления.БухучетНачисленияУдержанияПоКонтрагентамАкционерам КАК БухучетНДФЛ
	|ГДЕ
	|	БухучетНДФЛ.Организация = &Организация
	|	И НАЧАЛОПЕРИОДА(БухучетНДФЛ.Период, МЕСЯЦ) = &ПериодРегистрации
	|	И БухучетНДФЛ.НачислениеУдержание В(&ВидыОсобыхНачисленийИУдержанийНДФЛ)
	|
	|СГРУППИРОВАТЬ ПО
	|	БухучетНДФЛ.ФизическоеЛицо,
	|	БухучетНДФЛ.СтатьяРасходов,
	|	БухучетНДФЛ.СтатьяФинансирования,
	|	БухучетНДФЛ.ВидОперации,
	|	БухучетНДФЛ.Сотрудник,
	|	БухучетНДФЛ.ТерриторияВыполненияРаботВОрганизации,
	|	БухучетНДФЛ.РегистрацияВНалоговомОргане
	|
	|ИМЕЮЩИЕ
	|	СУММА(БухучетНДФЛ.Сумма) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ТаблицаНДФЛ.Сотрудник КАК Сотрудник,
	|	ТаблицаНДФЛ.ВидОперации КАК ВидОперации,
	|	ТаблицаНДФЛ.СтатьяФинансирования КАК СтатьяФинансирования,
	|	ТаблицаНДФЛ.СтатьяРасходов КАК СтатьяРасходов,
	|	СУММА(ТаблицаНДФЛ.Сумма) КАК Сумма,
	|	ЕСТЬNULL(РегистрацииВНалоговомОргане.КодПоОКАТО, """") КАК КодПоОКАТО,
	|	ЕСТЬNULL(РегистрацииВНалоговомОргане.КодПоОКТМО, """") КАК КодПоОКТМО,
	|	ЕСТЬNULL(РегистрацииВНалоговомОргане.КПП, """") КАК КПП,
	|	ЕСТЬNULL(РегистрацииВНалоговомОргане.Код, """") КАК КодНалоговогоОргана
	|ИЗ
	|	ВТТаблицаНДФЛ КАК ТаблицаНДФЛ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|		ПО ТаблицаНДФЛ.РегистрацияВНалоговомОргане = РегистрацииВНалоговомОргане.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаНДФЛ.ФизическоеЛицо,
	|	ТаблицаНДФЛ.Сотрудник,
	|	ТаблицаНДФЛ.ВидОперации,
	|	ТаблицаНДФЛ.СтатьяФинансирования,
	|	ТаблицаНДФЛ.СтатьяРасходов,
	|	ЕСТЬNULL(РегистрацииВНалоговомОргане.КодПоОКАТО, """"),
	|	ЕСТЬNULL(РегистрацииВНалоговомОргане.КодПоОКТМО, """"),
	|	ЕСТЬNULL(РегистрацииВНалоговомОргане.КПП, """"),
	|	ЕСТЬNULL(РегистрацииВНалоговомОргане.Код, """")";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ДанныеДляЗаполненияТаблицДокумента(ПараметрыДляЗаполнения) Экспорт
	
	Организация 	 	= ПараметрыДляЗаполнения.Организация;
	ПериодРегистрации 	= ПараметрыДляЗаполнения.ПериодРегистрации;
	ДокументСсылка 		= ПараметрыДляЗаполнения.ДокументСсылка;
	
	РезультатЗаполнения = ДанныеДляОтраженияЗарплатыВБухучете(Организация, ПериодРегистрации);
	РезультатЗаполнения.Вставить("ВыплатаОтпусковЗаСчетРезерва", ОтражениеЗарплатыВБухучете.НоваяТаблицаНачисленныеОтпуска());
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОценочныеОбязательстваЗарплатаКадры") Тогда
		МодульРезервОтпусков = ОбщегоНазначения.ОбщийМодуль("РезервОтпусков");
		НастройкиРезервовОтпусков = МодульРезервОтпусков.НастройкиРезервовОтпусков(Организация, ПериодРегистрации);
		Если НастройкиРезервовОтпусков.ФормироватьРезервОтпусковБУ Тогда
			ПараметрыДляСписанияРасходов = МодульРезервОтпусков.ПараметрыДляСписанияРасходовПоОплатеОтпуска();
			ПараметрыДляСписанияРасходов.Организация 						= Организация;
			ПараметрыДляСписанияРасходов.ПериодРегистрации 					= ПериодРегистрации;
			ПараметрыДляСписанияРасходов.НачисленнаяЗарплатаИВзносы 		= РезультатЗаполнения.НачисленнаяЗарплатаИВзносы;
			ПараметрыДляСписанияРасходов.НачисленныеОтпуска 			 	= РезультатЗаполнения.ВыплатаОтпусковЗаСчетРезерва;
			ПараметрыДляСписанияРасходов.УчитыватьОперацииТекущегоМесяца 	= Ложь;
			МодульРезервОтпусков.СписатьРасходыПоОтпускамЗаСчетОценочныхОбязательств(ПараметрыДляСписанияРасходов);
		КонецЕсли;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда 
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОрганизационнаяСтруктура");
		Модуль.ДополнитьДанныеДокументаМестомВСтруктуреПредприятия(РезультатЗаполнения, "НачисленнаяЗарплатаИВзносы,УдержаннаяЗарплата");
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.КонфигурацииЗарплатаКадрыРасширенная.ОтражениеРасчетовЗарплатыВБухучете") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОтражениеРасчетовЗарплатыВБухучете");
		Модуль.ОтобратьРезультатыЗаполненияТаблицДокумента(РезультатЗаполнения, ПараметрыДляЗаполнения);
	КонецЕсли;
	
	// ERP Удалено ОтражениеЗарплатыВБухучете.ПривестиРезультатыЗаполненияКСтруктуреТаблицДокумента()
	ОтражениеЗарплатыВФинансовомУчетеУП.ДополнитьТаблицуНачислений(РезультатЗаполнения.НачисленнаяЗарплатаИВзносы);
	
	КолонкиУдаления = "Начисление,ДатаНачала,ПодразделениеУчетаЗатрат,СтатьяФинансирования";
	ОтражениеЗарплатыВБухучете.СвернутьДанныеДляОтраженияЗарплатыВБухучете(РезультатЗаполнения, КолонкиУдаления);
	ОтражениеЗарплатыВБухучете.УпорядочитьДанныеДляОтраженияЗарплатыВБухучете(РезультатЗаполнения);
	
	Возврат РезультатЗаполнения;

КонецФункции

Функция ТаблицаУдержанийДляБухучета(ТаблицаУдержаний, ТаблицаНачислений)
	
	НоваяТаблицаУдержаний = ОтражениеЗарплатыВБухучете.НоваяТаблицаБухучетУдержаннаяЗарплата();
	НастройкиУдержаний = ОписаниеУдержанийДляОформленияКассовогоЧека();
	
	Начисления = ТаблицаНачислений.Скопировать(,"Сотрудник,Подразделение,ПодразделениеУчетаЗатрат,Сумма");
	Начисления.Свернуть("Сотрудник,Подразделение,ПодразделениеУчетаЗатрат","Сумма");
	
	БазаНачислений = Начисления.СкопироватьКолонки();
	Для каждого СтрокаТЗ Из Начисления Цикл
		Если СтрокаТЗ.Подразделение <> СтрокаТЗ.ПодразделениеУчетаЗатрат
					И ЗначениеЗаполнено(СтрокаТЗ.ПодразделениеУчетаЗатрат) Тогда
			ЗаполнитьЗначенияСвойств(БазаНачислений.Добавить(), СтрокаТЗ);
		КонецЕсли;
	КонецЦикла;
	БазаНачислений.Индексы.Добавить("Сотрудник,Подразделение");
	ОтборСтрок = Новый Структура("Сотрудник,Подразделение");
	
	Для каждого СтрокаТЗ Из ТаблицаУдержаний Цикл
	
		ЗаполнитьЗначенияСвойств(ОтборСтрок, СтрокаТЗ);
		СтрокиБазы = БазаНачислений.НайтиСтроки(ОтборСтрок);
		
		ЯвляетсяОснованиемОформленияКассовогоЧека = Ложь;
		ОписаниеУдержанияДляЧека = "";
		Если НастройкиУдержаний[СтрокаТЗ.Удержание] <> Неопределено Тогда
			ЯвляетсяОснованиемОформленияКассовогоЧека = Истина;
			ОписаниеУдержанияДляЧека = НастройкиУдержаний[СтрокаТЗ.Удержание];
		КонецЕсли;
		
		Если СтрокиБазы.Количество() = 0 Тогда
			
			НоваяСтрока = НоваяТаблицаУдержаний.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
			НоваяСтрока.ПодразделениеУчетаЗатрат = НоваяСтрока.Подразделение;
			НоваяСтрока.ЯвляетсяОснованиемОформленияКассовогоЧека = ЯвляетсяОснованиемОформленияКассовогоЧека;
			НоваяСтрока.ОписаниеУдержанияДляЧека = ОписаниеУдержанияДляЧека;
			
		Иначе
			
			Коэффициенты = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиБазы,"Сумма");
			Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СтрокаТЗ.Сумма, Коэффициенты);
			
			Если Результаты = Неопределено Тогда
				
				НоваяСтрока = НоваяТаблицаУдержаний.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
				НоваяСтрока.ПодразделениеУчетаЗатрат = НоваяСтрока.Подразделение;
				НоваяСтрока.ЯвляетсяОснованиемОформленияКассовогоЧека = ЯвляетсяОснованиемОформленияКассовогоЧека;
				НоваяСтрока.ОписаниеУдержанияДляЧека = ОписаниеУдержанияДляЧека;
				
			Иначе
				
				Индекс = 0;
				Для Каждого СтрокаБазы Из СтрокиБазы Цикл
					
					НоваяСтрока = НоваяТаблицаУдержаний.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
					НоваяСтрока.Сумма = Результаты[Индекс];
					НоваяСтрока.ПодразделениеУчетаЗатрат = СтрокаБазы.ПодразделениеУчетаЗатрат;
					НоваяСтрока.ЯвляетсяОснованиемОформленияКассовогоЧека = ЯвляетсяОснованиемОформленияКассовогоЧека;
					НоваяСтрока.ОписаниеУдержанияДляЧека = ОписаниеУдержанияДляЧека;
					
					Индекс = Индекс + 1;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
	
	КонецЦикла;
	
	ОтражениеЗарплатыВУчете.СвернутьТаблицу(НоваяТаблицаУдержаний);
	
	Возврат НоваяТаблицаУдержаний;
	
КонецФункции 

Функция ОписаниеУдержанийДляОформленияКассовогоЧека()

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИспользоватьОснованияОформленияКассовогоЧека", ПолучитьФункциональнуюОпцию("ИспользоватьУдержанияЯвляющиесяОснованиемОформленияКассовогоЧека"));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Удержания.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА &ИспользоватьОснованияОформленияКассовогоЧека
	|			ТОГДА Удержания.Наименование
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ОписаниеУдержанияДляЧека
	|ИЗ
	|	ПланВидовРасчета.Удержания КАК Удержания
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ИспользоватьОснованияОформленияКассовогоЧека
	|				ТОГДА Удержания.ЯвляетсяОснованиемОформленияКассовогоЧека
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Настройки = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		Настройки.Вставить(Выборка.Ссылка, Выборка.ОписаниеУдержанияДляЧека);
	КонецЦикла;
	
	Возврат Настройки;

КонецФункции


#КонецОбласти

#Область ОбслуживаниеРаспределенияРезультатов

Процедура СоздатьВТБухучетНачисленийСторно(МенеджерВременныхТаблиц, ИмяВТНачисления, СтрокиБухучетСторноНачислений)

	ИспользоватьСтатьиФинансирования = ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата");
	РаботаВБюджетномУчреждении 		 = ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если ИспользоватьСтатьиФинансирования И СтрокиБухучетСторноНачислений.Количество() > 0 Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Начисления.ПериодРегистрации,
		|	Начисления.Организация,
		|	Начисления.Сотрудник,
		|	Начисления.ФизическоеЛицо,
		|	Начисления.Подразделение,
		|	Начисления.Начисление,
		|	Начисления.ДатаНачала,
		|	Начисления.ДатаОкончания,
		|	Начисления.Сумма КАК Результат,
		|	Начисления.ДокументОснование,
		|	Начисления.ИдентификаторСтроки,
		|	Начисления.ТерриторияВыполненияРаботВОрганизации,
		|	Начисления.Сторно,
		|	Начисления.ФиксСторно
		|ИЗ
		|	ВТНачисленияДляОтраженияВБухучете КАК Начисления
		|ГДЕ
		|	Начисления.Сторно
		|	И НЕ Начисления.ФиксСторно
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.ПериодРегистрации,
		|	Начисления.Организация,
		|	Начисления.Сотрудник,
		|	Начисления.ФизическоеЛицо,
		|	Начисления.Подразделение,
		|	Начисления.Начисление,
		|	Начисления.ДатаНачала,
		|	Начисления.ДатаОкончания,
		|	Начисления.Сумма КАК Результат,
		|	Начисления.ДокументОснование,
		|	Начисления.ИдентификаторСтроки,
		|	Начисления.ТерриторияВыполненияРаботВОрганизации,
		|	Начисления.Сторно,
		|	Начисления.ФиксСторно
		|ИЗ
		|	ВТНачисленияДляОтраженияВБухучете КАК Начисления
		|ГДЕ
		|	Начисления.ФиксСторно";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляОтраженияВБухучете", ИмяВТНачисления);
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		ТаблицаНачислений = РезультатЗапроса[0].Выгрузить();
		БухучетНачисленийСторно = БухучетНачисленийСторноИзИсходнойТаблицы(ТаблицаНачислений, СтрокиБухучетСторноНачислений, ИспользоватьСтатьиФинансирования, РаботаВБюджетномУчреждении, Ложь);
		
		ТаблицаНачислений = РезультатЗапроса[1].Выгрузить();
		БухучетНачисленийФиксСторно = БухучетНачисленийСторноИзИсходнойТаблицы(ТаблицаНачислений, СтрокиБухучетСторноНачислений, ИспользоватьСтатьиФинансирования, РаботаВБюджетномУчреждении, Истина);
		
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(БухучетНачисленийФиксСторно, БухучетНачисленийСторно);
		
		Запрос.УстановитьПараметр("БухучетНачисленийСторно", БухучетНачисленийСторно);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	БухучетНачисленийСторно.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	БухучетНачисленийСторно.Организация КАК Организация,
		|	БухучетНачисленийСторно.Сотрудник КАК Сотрудник,
		|	БухучетНачисленийСторно.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БухучетНачисленийСторно.Подразделение КАК Подразделение,
		|	БухучетНачисленийСторно.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачисленийСторно.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачисленийСторно.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачисленийСторно.Результат КАК Сумма,
		|	БухучетНачисленийСторно.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	БухучетНачисленийСторно.ДатаНачала КАК ДатаНачала,
		|	БухучетНачисленийСторно.ДатаОкончания КАК ДатаОкончания,
		|	БухучетНачисленийСторно.Начисление КАК Начисление,
		|	БухучетНачисленийСторно.Территория КАК Территория
		|ПОМЕСТИТЬ ВТБухучетНачисленийСторноВременная
		|ИЗ
		|	&БухучетНачисленийСторно КАК БухучетНачисленийСторно
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	БухучетНачисленийСторно.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	БухучетНачисленийСторно.Организация КАК Организация,
		|	БухучетНачисленийСторно.Сотрудник КАК Сотрудник,
		|	БухучетНачисленийСторно.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БухучетНачисленийСторно.Подразделение КАК Подразделение,
		|	БухучетНачисленийСторно.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачисленийСторно.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачисленийСторно.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачисленийСторно.Сумма КАК Сумма,
		|	БухучетНачисленийСторно.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	БухучетНачисленийСторно.ДатаНачала КАК ДатаНачала,
		|	БухучетНачисленийСторно.ДатаОкончания КАК ДатаОкончания,
		|	БухучетНачисленийСторно.Начисление КАК Начисление,
		|	ВЫБОР
		|		КОГДА БухучетНачисленийСторно.Территория = НЕОПРЕДЕЛЕНО
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.ТерриторииВыполненияРабот.ПустаяСсылка)
		|		ИНАЧЕ БухучетНачисленийСторно.Территория
		|	КОНЕЦ КАК Территория
		|ПОМЕСТИТЬ ВТБухучетНачисленийСторно
		|ИЗ
		|	ВТБухучетНачисленийСторноВременная КАК БухучетНачисленийСторно
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТБухучетНачисленийСторноВременная";
		Запрос.Выполнить();
		
	Иначе
		
		СоздатьПустуюВТБухучетНачислений(Запрос, "ВТБухучетНачисленийСторно");
		
	КонецЕсли;

КонецПроцедуры

Функция БухучетНачисленийСторноИзИсходнойТаблицы(ТаблицаНачислений, СтрокиНачисленийСторно, ИспользоватьСтатьиФинансирования, РаботаВБюджетномУчреждении, ФиксСторно)

	ТаблицаРаспределение = НоваяТаблицаБухучетНачислений();
	
	Если ТаблицаНачислений.Количество()=0 Тогда
		Возврат ТаблицаРаспределение;
	КонецЕсли;
	
	// Параметры для проверки результатов распределения.
	ПараметрыДляПроверки = ПараметрыДляПроверкиРезультатовРаспределенияНачислений();
	ПараметрыДляПроверки.ПроверяемыеПоля.СтатьяФинансирования = ИспользоватьСтатьиФинансирования;
	ПараметрыДляПроверки.ПроверяемыеПоля.СтатьяРасходов       = РаботаВБюджетномУчреждении;
	
	Для каждого СтрокаТЗ Из ТаблицаНачислений Цикл
		
		РезультатыРаспределения = СтрокиНачисленийСторно[СтрокаТЗ.ИдентификаторСтроки];
		Если РезультатыРаспределения = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		// отберем строки распределения по территории 
		КоллекцияРаспределения = НоваяТаблицаРаспределениеРезультатовНачислений();
		Для каждого СтрокаРаспределения Из РезультатыРаспределения Цикл
			Если (СтрокаРаспределения.Территория = СтрокаТЗ.ТерриторияВыполненияРаботВОрганизации)
				Или (НЕ ЗначениеЗаполнено(СтрокаРаспределения.Территория) И Не ЗначениеЗаполнено(СтрокаТЗ.ТерриторияВыполненияРаботВОрганизации)) Тогда
				НоваяСтрока = КоллекцияРаспределения.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРаспределения);
				НоваяСтрока.ИдентификаторСтроки = СтрокаТЗ.ИдентификаторСтроки;
			КонецЕсли;
		КонецЦикла;
		
		Если КоллекцияРаспределения.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ФиксСторно Тогда
			ЗарплатаКадры.РазнестиСуммуПоБазе(СтрокаТЗ.Результат, КоллекцияРаспределения, "Результат");
		КонецЕсли;
		
		// Выполним проверку результата распределения
		ВидОперации = ПараметрыДляПроверки.НачислениеУдержаниеВидОперации[СтрокаТЗ.Начисление];
		ПараметрыДляПроверки.ПроверяемыеПоля.СпособОтраженияЗарплатыВБухучете = ПараметрыДляПроверки.ВидыОперацийПособия.Найти(ВидОперации) = Неопределено;
		ЕстьОшибкиРаспределения = РезультатРаспределенияСодержитОшибки(СтрокаТЗ.Результат, КоллекцияРаспределения, ПараметрыДляПроверки.ПроверяемыеПоля);
		
		Если ЕстьОшибкиРаспределения Тогда
			
			Если ФиксСторно Тогда
				
				// попробуем привести результат распределения к сумме строки
				ЗарплатаКадры.РазнестиСуммуПоБазе(СтрокаТЗ.Результат, КоллекцияРаспределения, "Результат");
				Если РезультатРаспределенияСодержитОшибки(СтрокаТЗ.Результат, КоллекцияРаспределения, ПараметрыДляПроверки.ПроверяемыеПоля) Тогда
					Продолжить;	
				КонецЕсли;
				
			Иначе
				Продолжить;	
			КонецЕсли;
			
		КонецЕсли;
		
		Для каждого ЭлементКоллекции Из КоллекцияРаспределения Цикл
			
			НоваяСтрока = ТаблицаРаспределение.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементКоллекции);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТаблицаРаспределение;
	
КонецФункции

Процедура ЗаполнитьКодСтатьиФинансирования(Таблица, Коды)

	Для каждого СтрокаТЗ Из Таблица Цикл
		СтрокаТЗ.КодСтатьиФинансирования = Коды[СтрокаТЗ.СтатьяФинансирования];
	КонецЦикла;	

КонецПроцедуры

Процедура СвернутьТаблицуКоэффициентовРаспределенияСреднегоЗаработка(ТаблицаКоэффициентов) Экспорт

	ТаблицаКоэффициентов.Свернуть("СтатьяФинансирования,СтатьяРасходов,СпособОтраженияЗарплатыВБухучете,ОблагаетсяЕНВД", "Коэффициент");

КонецПроцедуры

Функция ПараметрыПолученияБухучетаНачисленийПоСтатьямФинансирования()

	ПараметрыПолученияДанных = Новый Структура("
	|Организация,
	|ПериодРегистрации,
	|МенеджерВременныхТаблиц,
	|ФизическиеЛица,
	|ИсключаемыеРегистраторы,
	|ИсключатьМежрасчетныеВыплаты,
	|ТолькоДоходыСотрудников");
	
	ПараметрыПолученияДанных.ИсключатьМежрасчетныеВыплаты 	= Ложь;
	ПараметрыПолученияДанных.ТолькоДоходыСотрудников 		= Ложь;
	
	Возврат ПараметрыПолученияДанных;

КонецФункции

Процедура УпорядочитьТаблицуРаспределениеУдержаний(ТаблицаУдержаний)
	
	Если ТаблицаУдержаний.Количество() > 0 Тогда
		
		КолонкиСортировки = 
		"ИдентификаторСтроки,
		|ВидУдержания,
		|Сотрудник,
		|Подразделение,
		|СтатьяФинансирования,
		|СтатьяРасходов";
		
		ТаблицаУдержаний.Сортировать(КолонкиСортировки, Новый СравнениеЗначений);
		
	КонецЕсли;
	
КонецПроцедуры 

Процедура СоздатьВТБухучетНачисленийРанееНачислено(ТаблицаРанееНачислено, ИсходныеДанные)
	
	// Соответствие идентификаторов таблицы Начислений и Бухучета.
	СоответствиеИдентификаторов = Новый Соответствие;
	Для каждого СтрокаТЗ Из ТаблицаРанееНачислено Цикл
		СоответствиеИдентификаторов.Вставить(СтрокаТЗ.ИдентификаторСтрокиНачисления, СтрокаТЗ.ИдентификаторСтроки);
	КонецЦикла;
	
	КоэффициентыРанееНачисленныхСумм = ИсходныеДанные.МенеджерРасчетаЗарплаты.КоэффициентыРанееНачисленныхСумм();
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(КоэффициентыРанееНачисленныхСумм, "ИдентификаторСтроки");
	ОтборКоэффициентов = Новый Структура("ИдентификаторСтроки");
		
	ФильтрБухучета = Новый ТаблицаЗначений;
	ФильтрБухучета.Колонки.Добавить("Регистратор", РегистрыРасчета.Начисления.ОписаниеТиповПоляРегистратор());
	ФильтрБухучета.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Число"));
	ФильтрБухучета.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	// для корректировки перерасчетов в разовых начислениях
	ФильтрБухучета.Колонки.Добавить("ЗаписьТекущегоРегистратора", Новый ОписаниеТипов("Булево"));
	
	Для Каждого СтрокаНачисления Из ТаблицаРанееНачислено Цикл
		
		ОтборКоэффициентов.ИдентификаторСтроки = СтрокаНачисления.ИдентификаторСтрокиНачисления;
		СтрокиКоэффициентов = КоэффициентыРанееНачисленныхСумм.НайтиСтроки(ОтборКоэффициентов);
		
		Для Каждого СтрокаКоэффициентов Из СтрокиКоэффициентов Цикл
			Если СтрокаКоэффициентов.Регистратор <> ИсходныеДанные.РегистраторТекущегоРасчета Тогда
				СтрокаФильтра = ФильтрБухучета.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаФильтра, СтрокаКоэффициентов);
				// ИдентификаторСтрокиБазы - идентификатор строки ранее начисленного.
				СтрокаФильтра.ИдентификаторСтроки = СтрокаКоэффициентов.ИдентификаторСтрокиБазы;
				СтрокаФильтра.Подразделение = СтрокаНачисления.Подразделение;
				СтрокаФильтра.ЗаписьТекущегоРегистратора = Не ЗначениеЗаполнено(СтрокаКоэффициентов.Регистратор);
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	ФильтрБухучета.Свернуть("Регистратор, ИдентификаторСтроки, Подразделение, ЗаписьТекущегоРегистратора");
	
	ИдентификаторыСтрок = ТаблицаРанееНачислено.ВыгрузитьКолонку("ИдентификаторСтроки");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ИсходныеДанные.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ФильтрБухУчета", ФильтрБухУчета); 
	Запрос.УстановитьПараметр("ИдентификаторыСтрок", ИдентификаторыСтрок);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФильтрБухУчета.Регистратор КАК Регистратор,
	|	ФильтрБухУчета.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ФильтрБухУчета.Подразделение КАК Подразделение,
	|	ФильтрБухУчета.ЗаписьТекущегоРегистратора КАК ЗаписьТекущегоРегистратора
	|ПОМЕСТИТЬ ВТФильтрБухУчета
	|ИЗ
	|	&ФильтрБухУчета КАК ФильтрБухУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФильтрБухУчета.Регистратор КАК Регистратор,
	|	ФильтрБухУчета.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА БухучетНачислений.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|			ТОГДА ФильтрБухУчета.Подразделение
	|		ИНАЧЕ БухучетНачислений.ПодразделениеУчетаЗатрат
	|	КОНЕЦ КАК ПодразделениеУчетаЗатрат,
	|	БухучетНачислений.Территория КАК Территория,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	БухучетНачислений.Результат КАК Результат
	|ИЗ
	|	ВТФильтрБухУчета КАК ФильтрБухУчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеРезультатовНачислений КАК БухучетНачислений
	|		ПО ФильтрБухУчета.Регистратор = БухучетНачислений.Регистратор
	|			И ФильтрБухУчета.ИдентификаторСтроки = БухучетНачислений.ИдентификаторСтроки
	|			И (НЕ ФильтрБухУчета.ЗаписьТекущегоРегистратора)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ФильтрБухУчета.Регистратор,
	|	ФильтрБухУчета.ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА БухучетНачислений.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|			ТОГДА ФильтрБухУчета.Подразделение
	|		ИНАЧЕ БухучетНачислений.ПодразделениеУчетаЗатрат
	|	КОНЕЦ,
	|	БухучетНачислений.ТерриторияВыполненияРаботВОрганизации,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов,
	|	БухучетНачислений.ОблагаетсяЕНВД,
	|	БухучетНачислений.Сумма
	|ИЗ
	|	ВТФильтрБухУчета КАК ФильтрБухУчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТБухучетНачисленийВыходнаяТаблица КАК БухучетНачислений
	|		ПО ФильтрБухУчета.ИдентификаторСтроки = БухучетНачислений.ИдентификаторСтроки
	|			И (ФильтрБухУчета.ЗаписьТекущегоРегистратора)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	БухучетНачислений.Сотрудник КАК Сотрудник,
	|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
	|	БухучетНачислений.Подразделение КАК Подразделение,
	|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачислений.Сумма КАК Результат,
	|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
	|	БухучетНачислений.Начисление КАК Начисление,
	|	БухучетНачислений.ТерриторияВыполненияРаботВОрганизации КАК Территория
	|ИЗ
	|	ВТБухучетНачисленийВыходнаяТаблица КАК БухучетНачислений
	|ГДЕ
	|	БухучетНачислений.ИдентификаторСтроки В(&ИдентификаторыСтрок)";
	
	Результат = Запрос.ВыполнитьПакет();
	КоличествоРезультатов = Результат.ВГраница();
	
	БухучетТекущегоРасчета = Результат[КоличествоРезультатов].Выгрузить();
	БухучетТекущегоРасчета.Индексы.Добавить("ИдентификаторСтроки");
	ОтборТекущегоРасчета = Новый Структура("ИдентификаторСтроки");
	
	БухучетБазовыхНачислений = Результат[КоличествоРезультатов-1].Выгрузить();
	БухучетБазовыхНачислений.Индексы.Добавить("Регистратор, ИдентификаторСтроки");
	ОтборРаспределения = Новый Структура("Регистратор, ИдентификаторСтроки");
	
	БухучетНачислений = БухучетТекущегоРасчета.СкопироватьКолонки();
	
	ИспользоватьОбособленныеТерритории = ПолучитьФункциональнуюОпцию("ИспользоватьОбособленныеТерритории", Новый Структура("Организация", ИсходныеДанные.Организация));
	
	Для Каждого СтрокаНачисления Из ТаблицаРанееНачислено Цикл
		
		ИзмеренияРаспределения = Новый Массив;
		Коэффициенты = Новый Массив;
		
		ОтборКоэффициентов.ИдентификаторСтроки = СтрокаНачисления.ИдентификаторСтрокиНачисления;
		СтрокиКоэффициентов = КоэффициентыРанееНачисленныхСумм.НайтиСтроки(ОтборКоэффициентов);
		
		Для Каждого СтрокаКоэффициентов Из СтрокиКоэффициентов Цикл
			
			Если СтрокаКоэффициентов.Регистратор = ИсходныеДанные.РегистраторТекущегоРасчета Тогда
				ОтборТекущегоРасчета.ИдентификаторСтроки = СоответствиеИдентификаторов[СтрокаКоэффициентов.ИдентификаторСтрокиБазы];
				НайденныеСтроки = БухучетТекущегоРасчета.НайтиСтроки(ОтборТекущегоРасчета);
			Иначе	
				ЗаполнитьЗначенияСвойств(ОтборРаспределения, СтрокаКоэффициентов);
				ОтборРаспределения.ИдентификаторСтроки = СтрокаКоэффициентов.ИдентификаторСтрокиБазы;
				НайденныеСтроки = БухучетБазовыхНачислений.НайтиСтроки(ОтборРаспределения);
			КонецЕсли;
			
			СтрокиРаспределения = Новый Массив;
			Если ИспользоватьОбособленныеТерритории Тогда
				// Отберем строки с учетом Территории.
				Для каждого СтрокаРаспределения Из НайденныеСтроки Цикл
					Если СтрокаНачисления.Территория = СтрокаРаспределения.Территория
						Или Не ЗначениеЗаполнено(СтрокаНачисления.Территория) И Не ЗначениеЗаполнено(СтрокаРаспределения.Территория) Тогда
						СтрокиРаспределения.Добавить(СтрокаРаспределения);
					КонецЕсли;
				КонецЦикла;
				
				Если СтрокиРаспределения.Количество() = 0 Тогда
					// Не удалось подобрать прошлое распределение с учетом территории,
					// распределим по всей базе.
					СтрокиРаспределения = НайденныеСтроки;
				КонецЕсли;
				
			Иначе
				СтрокиРаспределения = НайденныеСтроки;
			КонецЕсли;
			
			Для Каждого СтрокаРаспределения Из СтрокиРаспределения Цикл
				Если СтрокаРаспределения.Результат <> 0 Тогда
					ИзмеренияРаспределения.Добавить(СтрокаРаспределения);
					Коэффициенты.Добавить(СтрокаРаспределения.Результат * СтрокаКоэффициентов.Коэффициент);
				КонецЕсли;	
			КонецЦикла;
			
		КонецЦикла;
		
		// Если базы распределения не нашлось, то распределим сумму РанееНачислено по текущему распределению этого начисления
		Если ИзмеренияРаспределения.Количество() = 0 Тогда
			ОтборТекущегоРасчета.ИдентификаторСтроки = СтрокаНачисления.ИдентификаторСтроки;	
			СтрокиРаспределения = БухучетТекущегоРасчета.НайтиСтроки(ОтборТекущегоРасчета);
			Для Каждого СтрокаРаспределения Из СтрокиРаспределения Цикл
				ИзмеренияРаспределения.Добавить(СтрокаРаспределения);
				Коэффициенты.Добавить(СтрокаРаспределения.Результат);
			КонецЦикла;	
		КонецЕсли;	
		
		Распределение = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СтрокаНачисления.РанееНачислено, Коэффициенты); 
		Если Распределение <> Неопределено Тогда
			Для Индекс = 0 По Распределение.Количество() - 1 Цикл
				СтрокаБухучет = БухучетНачислений.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаБухучет, СтрокаНачисления);
				ЗаполнитьЗначенияСвойств(СтрокаБухучет, ИзмеренияРаспределения[Индекс]);
				СтрокаБухучет.ИдентификаторСтроки = СтрокаНачисления.ИдентификаторСтроки;
				СтрокаБухучет.Результат = - Распределение[Индекс]
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("БухучетНачисленийРанееНачислено", БухучетНачислений);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	БухучетНачислений.Сотрудник КАК Сотрудник,
	|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
	|	БухучетНачислений.Подразделение КАК Подразделение,
	|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачислений.Результат КАК Сумма,
	|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
	|	БухучетНачислений.Начисление КАК Начисление,
	|	БухучетНачислений.Территория КАК Территория
	|ПОМЕСТИТЬ ВТБухучетНачисленийРанееНачислено
	|ИЗ
	|	&БухучетНачисленийРанееНачислено КАК БухучетНачислений";
	Запрос.Выполнить();
	
	УдалитьВТ = Новый Массив;
	УдалитьВТ.Добавить("ВТФильтрБухУчета");
	ЗарплатаКадры.УничтожитьВТ(ИсходныеДанные.МенеджерВременныхТаблиц, УдалитьВТ);
	
КонецПроцедуры

Процедура СоздатьВТБухучетКорректировкаРанееВыполненногоНачисления(ИсходныеДанные)
	
	УдалитьВТ = Новый Массив;
	БухучетНачислений = НоваяТаблицаБухучетНачислений();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ИсходныеДанные.МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НачисленияДляРаспределения.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	НачисленияДляРаспределения.ИдентификаторСтрокиНачисления КАК ИдентификаторСтрокиНачисления,
	|	НачисленияДляРаспределения.Сотрудник КАК Сотрудник,
	|	НачисленияДляРаспределения.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НачисленияДляРаспределения.Подразделение КАК Подразделение,
	|	НачисленияДляРаспределения.Начисление КАК Начисление,
	|	НачисленияДляРаспределения.ДатаНачала КАК ДатаНачала,
	|	НачисленияДляРаспределения.ТерриторияВыполненияРаботВОрганизации КАК Территория,
	|	НачисленияДляРаспределения.Сумма КАК Сумма
	|ИЗ
	|	ВТНачисленияДляОтраженияВБухучете КАК НачисленияДляРаспределения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБухучетНачисленийСторно КАК БухучетНачисленийСторно
	|		ПО НачисленияДляРаспределения.ИдентификаторСтроки = БухучетНачисленийСторно.ИдентификаторСтроки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК Начисления
	|		ПО НачисленияДляРаспределения.Начисление = Начисления.Ссылка
	|			И (Начисления.СтратегияОтраженияВУчете <> ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам))
	|			И (НачисленияДляРаспределения.КорректировкаРанееВыполненногоНачисления)
	|ГДЕ
	|	БухучетНачисленийСторно.ИдентификаторСтроки ЕСТЬ NULL";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНачисленияДляОтраженияВБухучете", ИсходныеДанные.ИмяВТНачисления);
	ТаблицаРанееНачислено = Запрос.Выполнить().Выгрузить();
	
	Если ТаблицаРанееНачислено.Количество() > 0 Тогда
		
		// ИдентификаторСтроки соответствует полю ИдентификаторСтрокиНачисления
		КоэффициентыРанееНачисленныхСумм = ИсходныеДанные.МенеджерРасчетаЗарплаты.КоэффициентыРанееНачисленныхСумм();
		ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(КоэффициентыРанееНачисленныхСумм, "ИдентификаторСтроки");
		ОтборКоэффициентов = Новый Структура("ИдентификаторСтроки");
		
		ФильтрБухучета = Новый ТаблицаЗначений;
		ФильтрБухучета.Колонки.Добавить("Регистратор", РегистрыРасчета.Начисления.ОписаниеТиповПоляРегистратор());
		ФильтрБухучета.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Число"));
		ФильтрБухучета.Колонки.Добавить("ИдентификаторСтрокиБухучета", Новый ОписаниеТипов("Число"));
		
		Для Каждого СтрокаНачисления Из ТаблицаРанееНачислено Цикл
			
			ОтборКоэффициентов.ИдентификаторСтроки = СтрокаНачисления.ИдентификаторСтрокиНачисления;
			СтрокиКоэффициентов = КоэффициентыРанееНачисленныхСумм.НайтиСтроки(ОтборКоэффициентов);
			
			Для Каждого СтрокаКоэффициентов Из СтрокиКоэффициентов Цикл
				Если СтрокаКоэффициентов.Регистратор <> ИсходныеДанные.РегистраторТекущегоРасчета Тогда
					СтрокаФильтра = ФильтрБухучета.Добавить();
					СтрокаФильтра.Регистратор = СтрокаКоэффициентов.Регистратор;
					СтрокаФильтра.ИдентификаторСтроки = СтрокаКоэффициентов.ИдентификаторСтрокиБазы;
					СтрокаФильтра.ИдентификаторСтрокиБухучета = СтрокаНачисления.ИдентификаторСтроки;
				КонецЕсли;
			КонецЦикла;
			
		КонецЦикла;
		
		ФильтрБухучета.Свернуть("Регистратор, ИдентификаторСтроки, ИдентификаторСтрокиБухучета");
		
		Регистраторы = ОбщегоНазначения.ВыгрузитьКолонку(ФильтрБухучета, "Регистратор", Истина);
		Запрос.УстановитьПараметр("Регистраторы", Регистраторы);
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	БухучетНачисления.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.БухучетНачисленияУдержанияПоСотрудникам КАК БухучетНачисления
		|ГДЕ
		|	БухучетНачисления.Регистратор В(&Регистраторы)
		|	И БухучетНачисления.ДанныеМежрасчетногоПериода";
		ИсключаемыеРегистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
		
		УдаляемыеСтроки = Новый Массив;
		Отбор = Новый Структура("Регистратор");
		Для каждого Регистратор Из ИсключаемыеРегистраторы Цикл
			Отбор.Регистратор = Регистратор;
			НайденныеСтроки = ФильтрБухучета.НайтиСтроки(Отбор);
			Для каждого СтрокаТЗ Из НайденныеСтроки Цикл
				УдаляемыеСтроки.Добавить(СтрокаТЗ);
			КонецЦикла;
		КонецЦикла;
		
		Для каждого СтрокаТЗ Из УдаляемыеСтроки Цикл
			ФильтрБухучета.Удалить(СтрокаТЗ);
		КонецЦикла;
		
		Если ФильтрБухучета.Количество() > 0 Тогда
			
			ИспользоватьОбособленныеТерритории = ПолучитьФункциональнуюОпцию("ИспользоватьОбособленныеТерритории", Новый Структура("Организация", ИсходныеДанные.Организация));
			
			Запрос.УстановитьПараметр("ФильтрБухУчета", ФильтрБухУчета);
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ФильтрБухУчета.Регистратор КАК Регистратор,
			|	ФильтрБухУчета.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	ФильтрБухУчета.ИдентификаторСтрокиБухучета КАК ИдентификаторСтрокиБухучета
			|ПОМЕСТИТЬ ВТФильтрБухУчета
			|ИЗ
			|	&ФильтрБухУчета КАК ФильтрБухУчета
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ФильтрБухУчета.ИдентификаторСтрокиБухучета КАК ИдентификаторСтрокиБухучета,
			|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
			|	БухучетНачислений.Территория КАК Территория,
			|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
			|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
			|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
			|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
			|	БухучетНачислений.Результат КАК Результат
			|ИЗ
			|	ВТФильтрБухУчета КАК ФильтрБухУчета
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеРезультатовНачислений КАК БухучетНачислений
			|		ПО ФильтрБухУчета.Регистратор = БухучетНачислений.Регистратор
			|			И ФильтрБухУчета.ИдентификаторСтроки = БухучетНачислений.ИдентификаторСтроки";
			
			БухучетРанееНачислено = Запрос.Выполнить().Выгрузить();
			УдалитьВТ.Добавить("ВТФильтрБухУчета");
			
			БухучетРанееНачислено.Индексы.Добавить("ИдентификаторСтрокиБухучета");
			ОтборРанееНачислено = Новый Структура("ИдентификаторСтрокиБухучета");
			
			Для каждого СтрокаНачисления Из ТаблицаРанееНачислено Цикл
			
				ОтборРанееНачислено.ИдентификаторСтрокиБухучета = СтрокаНачисления.ИдентификаторСтроки;
				НайденныеСтроки = БухучетРанееНачислено.НайтиСтроки(ОтборРанееНачислено);
				
				СтрокиРаспределения = Новый Массив;
				Если ИспользоватьОбособленныеТерритории Тогда
					// Отберем строки с учетом Территории.
					Для каждого СтрокаРаспределения Из НайденныеСтроки Цикл
						Если СтрокаНачисления.Территория = СтрокаРаспределения.Территория
							Или Не ЗначениеЗаполнено(СтрокаНачисления.Территория) И Не ЗначениеЗаполнено(СтрокаРаспределения.Территория) Тогда
							СтрокиРаспределения.Добавить(СтрокаРаспределения);
						КонецЕсли;
					КонецЦикла;
					
					Если СтрокиРаспределения.Количество() = 0 Тогда
						// Не удалось подобрать прошлое распределение с учетом территории,
						// распределим по всей базе.
						СтрокиРаспределения = НайденныеСтроки;
					КонецЕсли;
					
				Иначе
					СтрокиРаспределения = НайденныеСтроки;
				КонецЕсли;
				
				Коэффициенты = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиРаспределения,"Результат");
				Результаты = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(СтрокаНачисления.Сумма, Коэффициенты);
				
				Если Результаты <> Неопределено Тогда
					
					Индекс = 0;
					Для Каждого СтрокаРаспределения Из СтрокиРаспределения Цикл
						
						СтрокаТаблицы = БухучетНачислений.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаНачисления);
						ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаРаспределения);
						СтрокаТаблицы.Территория = СтрокаНачисления.Территория;
						СтрокаТаблицы.Сумма = Результаты[Индекс];
						
						Индекс = Индекс + 1;
						
					КонецЦикла;
					
				КонецЕсли;
			
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если БухучетНачислений.Количество() = 0 Тогда
		СоздатьПустуюВТБухучетНачислений(Запрос, "ВТБухучетКорректировкаРанееВыполненногоНачисления");
	Иначе
		
		Запрос.УстановитьПараметр("БухучетНачислений", БухучетНачислений);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	БухучетНачислений.Сотрудник КАК Сотрудник,
		|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БухучетНачислений.Подразделение КАК Подразделение,
		|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачислений.Сумма КАК Сумма,
		|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
		|	БухучетНачислений.Начисление КАК Начисление,
		|	БухучетНачислений.Территория КАК Территория
		|ПОМЕСТИТЬ ВТБухучетКорректировкаРанееВыполненногоНачисления
		|ИЗ
		|	&БухучетНачислений КАК БухучетНачислений";
		Запрос.Выполнить();
		
	КонецЕсли;
	
	Если УдалитьВТ.Количество() > 0 Тогда
		ЗарплатаКадры.УничтожитьВТ(ИсходныеДанные.МенеджерВременныхТаблиц, УдалитьВТ);
	КонецЕсли;
	
КонецПроцедуры

// Распределяет страховые взносы по статьям финансирования для формирования движений регистра
// СтраховыеВзносыПоФизическимЛицам.
// Параметры:
//	Движения - тип Структура или КоллекцияДвижений документа.
//	Организация - тип СправочникСсылка.Организации.
//	ПериодРегистрации - тип Дата.
//	Ссылка - ссылка на документ, движения которого переданы в параметре «Движения».
//	МенеджерВременныхТаблиц - менеджер временных таблиц, содержит временную таблицу ВТФизическиеЛица и ВТРасширенныеСведенияОВзносах.
//
Процедура СоздатьВТСтраховыеВзносыПоИсточникамФинансирования(Движения, Организация, ПериодРегистрации, Ссылка, МенеджерВременныхТаблиц)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	// Прочитаем уже зарегистрированные данные о бухучете начислений.
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФизическиеЛица.ФизическоеЛицо
	|ИЗ
	|	ВТФизическиеЛица КАК ФизическиеЛица";
	МассивФизическихЛиц = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ФизическоеЛицо");
	// Получаем уже зарегистрированные сведения о бухучете начислений всех сотрудников по физическим лицам из массива МассивФизическихЛиц,
	// без учета регистратора, указанного в параметре Ссылка.
	ПараметрыПолученияДанных = ПараметрыПолученияБухучетаНачисленийПоСтатьямФинансирования();
	ПараметрыПолученияДанных.Организация 					= Организация;
	ПараметрыПолученияДанных.ПериодРегистрации 				= ПериодРегистрации;
	ПараметрыПолученияДанных.МенеджерВременныхТаблиц 		= МенеджерВременныхТаблиц;
	ПараметрыПолученияДанных.ФизическиеЛица 				= МассивФизическихЛиц;
	ПараметрыПолученияДанных.ИсключаемыеРегистраторы 		= Ссылка;
	СоздатьВТБухучетНачисленийПоСтатьямФинансирования(ПараметрыПолученияДанных);
	
	Запрос.УстановитьПараметр("Регистратор" , Ссылка);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	
	БухучетНачисленияУдержанияПоСотрудникам = Неопределено;
	БухучетНачисленияУдержанияПоКонтрагентамАкционерам = Неопределено;  
	
	Если ТипЗнч(Движения) = Тип("Структура") Тогда
		Движения.Свойство("БухучетНачисленияУдержанияПоСотрудникам", БухучетНачисленияУдержанияПоСотрудникам);
		Движения.Свойство("БухучетНачисленияУдержанияПоКонтрагентамАкционерам", БухучетНачисленияУдержанияПоКонтрагентамАкционерам);
	Иначе
		БухучетНачисленияУдержанияПоСотрудникам = Движения.Найти("БухучетНачисленияУдержанияПоСотрудникам");
		БухучетНачисленияУдержанияПоКонтрагентамАкционерам = Движения.Найти("БухучетНачисленияУдержанияПоКонтрагентамАкционерам");
	КонецЕсли;
	
	Если БухучетНачисленияУдержанияПоСотрудникам = Неопределено Тогда
		БухучетНачисления = НоваяТаблицаБухучетНачисленияУдержанияПоСотрудникам();
	Иначе
		БухучетНачисления = БухучетНачисленияУдержанияПоСотрудникам.Выгрузить();
		БухучетНачисления.Колонки.НачислениеУдержание.Имя = "Начисление";
	КонецЕсли;
	
	Если БухучетНачисленияУдержанияПоКонтрагентамАкционерам = Неопределено Тогда
		БухучетНачисленияКА = НоваяТаблицаБухучетНачисленияУдержанияПоКонтрагентамАкционерам();
	Иначе
		БухучетНачисленияКА = БухучетНачисленияУдержанияПоКонтрагентамАкционерам.Выгрузить();
		БухучетНачисленияКА.Колонки.НачислениеУдержание.Имя = "Начисление";
	КонецЕсли;
	
	Запрос.УстановитьПараметр("БухучетНачисления", БухучетНачисления);
	Запрос.УстановитьПараметр("БухучетНачисленияКА", БухучетНачисленияКА);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БухучетНачисления.Организация КАК Организация,
	|	БухучетНачисления.Сотрудник КАК Сотрудник,
	|	БухучетНачисления.Подразделение КАК Подразделение,
	|	БухучетНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	БухучетНачисления.Начисление КАК Начисление,
	|	БухучетНачисления.ВидОперации КАК ВидОперации,
	|	БухучетНачисления.ДатаНачала КАК ДатаНачала,
	|	БухучетНачисления.ДатаОкончания КАК ДатаОкончания,
	|	БухучетНачисления.ДокументОснование КАК ДокументОснование,
	|	БухучетНачисления.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачисления.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачисления.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачисления.Сумма КАК Сумма,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыНачисленийОплатыТрудаДляНУ.ПустаяСсылка) КАК ВидНачисленияОплатыТрудаДляНУ,
	|	БухучетНачисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	БухучетНачисления.ГруппаНачисленияУдержанияВыплаты КАК ГруппаНачисленияУдержанияВыплаты
	|ПОМЕСТИТЬ ВТБухучетНачисленияУдержанияПоСотрудникам
	|ИЗ
	|	&БухучетНачисления КАК БухучетНачисления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БухучетНачисления.Организация КАК Организация,
	|	БухучетНачисления.Сотрудник КАК Сотрудник,
	|	БухучетНачисления.Подразделение КАК Подразделение,
	|	БухучетНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	БухучетНачисления.Начисление КАК Начисление,
	|	БухучетНачисления.ВидОперации КАК ВидОперации,
	|	БухучетНачисления.Период КАК ДатаНачала,
	|	БухучетНачисления.Период КАК ДатаОкончания,
	|	БухучетНачисления.ДокументОснование КАК ДокументОснование,
	|	БухучетНачисления.Подразделение КАК ПодразделениеУчетаЗатрат,
	|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачисления.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачисления.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачисления.Сумма КАК Сумма,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыНачисленийОплатыТрудаДляНУ.ПустаяСсылка) КАК ВидНачисленияОплатыТрудаДляНУ,
	|	БухучетНачисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	БухучетНачисления.ГруппаНачисленияУдержанияВыплаты КАК ГруппаНачисленияУдержанияВыплаты
	|ПОМЕСТИТЬ ВТБухучетНачисленияУдержанияПоКонтрагентамАкционерам
	|ИЗ
	|	&БухучетНачисленияКА КАК БухучетНачисления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БухучетНачисления.Организация КАК Организация,
	|	БухучетНачисления.Сотрудник КАК Сотрудник,
	|	БухучетНачисления.Подразделение КАК Подразделение,
	|	БухучетНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	БухучетНачисления.НачислениеУдержание КАК Начисление,
	|	БухучетНачисления.ВидОперации КАК ВидОперации,
	|	БухучетНачисления.ДатаНачала КАК ДатаНачала,
	|	БухучетНачисления.ДатаОкончания КАК ДатаОкончания,
	|	БухучетНачисления.ДокументОснование КАК ДокументОснование,
	|	БухучетНачисления.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
	|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачисления.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачисления.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачисления.Сумма КАК Сумма,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыНачисленийОплатыТрудаДляНУ.ПустаяСсылка) КАК ВидНачисленияОплатыТрудаДляНУ,
	|	БухучетНачисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД
	|ПОМЕСТИТЬ ВТБухучетНачисленийВременная
	|ИЗ
	|	ВТБухучетНачисленийПоСтатьямФинансирования КАК БухучетНачисления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	БухучетНачисления.Организация,
	|	БухучетНачисления.Сотрудник,
	|	БухучетНачисления.Подразделение,
	|	БухучетНачисления.ФизическоеЛицо,
	|	БухучетНачисления.Начисление,
	|	БухучетНачисления.ВидОперации,
	|	БухучетНачисления.ДатаНачала,
	|	БухучетНачисления.ДатаОкончания,
	|	БухучетНачисления.ДокументОснование,
	|	БухучетНачисления.ПодразделениеУчетаЗатрат,
	|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачисления.СтатьяФинансирования,
	|	БухучетНачисления.СтатьяРасходов,
	|	БухучетНачисления.Сумма,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыНачисленийОплатыТрудаДляНУ.ПустаяСсылка),
	|	БухучетНачисления.ОблагаетсяЕНВД
	|ИЗ
	|	ВТБухучетНачисленияУдержанияПоСотрудникам КАК БухучетНачисления
	|ГДЕ
	|	БухучетНачисления.Организация <> НЕОПРЕДЕЛЕНО
	|	И БухучетНачисления.ГруппаНачисленияУдержанияВыплаты = ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Начислено)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	БухучетНачисления.Организация,
	|	БухучетНачисления.Сотрудник,
	|	БухучетНачисления.Подразделение,
	|	БухучетНачисления.ФизическоеЛицо,
	|	БухучетНачисления.Начисление,
	|	БухучетНачисления.ВидОперации,
	|	БухучетНачисления.ДатаНачала,
	|	БухучетНачисления.ДатаОкончания,
	|	БухучетНачисления.ДокументОснование,
	|	БухучетНачисления.ПодразделениеУчетаЗатрат,
	|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачисления.СтатьяФинансирования,
	|	БухучетНачисления.СтатьяРасходов,
	|	БухучетНачисления.Сумма,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыНачисленийОплатыТрудаДляНУ.ПустаяСсылка),
	|	БухучетНачисления.ОблагаетсяЕНВД
	|ИЗ
	|	ВТБухучетНачисленияУдержанияПоСотрудникам КАК БухучетНачисления
	|ГДЕ
	|	БухучетНачисления.Организация <> НЕОПРЕДЕЛЕНО
	|	И БухучетНачисления.ГруппаНачисленияУдержанияВыплаты = ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Справочно)
	|	И БухучетНачисления.Начисление ССЫЛКА ПланВидовРасчета.Начисления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	БухучетНачисления.Организация,
	|	БухучетНачисления.Сотрудник,
	|	БухучетНачисления.Подразделение,
	|	БухучетНачисления.ФизическоеЛицо,
	|	БухучетНачисления.Начисление,
	|	БухучетНачисления.ВидОперации,
	|	БухучетНачисления.ДатаНачала,
	|	БухучетНачисления.ДатаОкончания,
	|	БухучетНачисления.ДокументОснование,
	|	БухучетНачисления.ПодразделениеУчетаЗатрат,
	|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачисления.СтатьяФинансирования,
	|	БухучетНачисления.СтатьяРасходов,
	|	БухучетНачисления.Сумма,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыНачисленийОплатыТрудаДляНУ.ПустаяСсылка),
	|	БухучетНачисления.ОблагаетсяЕНВД
	|ИЗ
	|	ВТБухучетНачисленияУдержанияПоСотрудникам КАК БухучетНачисления
	|ГДЕ
	|	БухучетНачисления.Организация <> НЕОПРЕДЕЛЕНО
	|	И БухучетНачисления.ГруппаНачисленияУдержанияВыплаты = ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Справочно)
	|	И БухучетНачисления.Начисление = ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.СтоимостьПодарковПризов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	БухучетНачисления.Организация,
	|	БухучетНачисления.Сотрудник,
	|	БухучетНачисления.Подразделение,
	|	БухучетНачисления.ФизическоеЛицо,
	|	БухучетНачисления.Начисление,
	|	БухучетНачисления.ВидОперации,
	|	БухучетНачисления.ДатаНачала,
	|	БухучетНачисления.ДатаОкончания,
	|	БухучетНачисления.ДокументОснование,
	|	БухучетНачисления.ПодразделениеУчетаЗатрат,
	|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачисления.СтатьяФинансирования,
	|	БухучетНачисления.СтатьяРасходов,
	|	БухучетНачисления.Сумма,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыНачисленийОплатыТрудаДляНУ.ПустаяСсылка),
	|	БухучетНачисления.ОблагаетсяЕНВД
	|ИЗ
	|	ВТБухучетНачисленияУдержанияПоКонтрагентамАкционерам КАК БухучетНачисления
	|ГДЕ
	|	БухучетНачисления.Организация <> НЕОПРЕДЕЛЕНО
	|	И БухучетНачисления.ГруппаНачисленияУдержанияВыплаты = ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Начислено)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТБухучетНачисленияУдержанияПоСотрудникам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТБухучетНачисленияУдержанияПоКонтрагентамАкционерам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	БухучетНачислений.Организация КАК Организация
	|ИЗ
	|	ВТБухучетНачисленийВременная КАК БухучетНачислений";
	
	Если Запрос.Выполнить().Пустой() Тогда
		
		Запрос.Текст = 
		"УНИЧТОЖИТЬ ВТБухучетНачисленийВременная
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 0
		|	0 КАК ИдентификаторСтроки,
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Сотрудник,
		|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ФизическоеЛицо,
		|	ЗНАЧЕНИЕ(ПланВидовРасчета.Начисления.ПустаяСсылка) КАК Начисление,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ПустаяСсылка) КАК ВидОперации,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаНачала,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаОкончания,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ПериодПринятияРасходов,
		|	НЕОПРЕДЕЛЕНО КАК ДокументОснование,
		|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеУчетаЗатрат,
		|	ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка) КАК СпособОтраженияЗарплатыВБухучете,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
		|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
		|	0 КАК Сумма,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыНачисленийОплатыТрудаДляНУ.ПустаяСсылка) КАК ВидНачисленияОплатыТрудаДляНУ,
		|	ЛОЖЬ КАК ОблагаетсяЕНВД
		|ПОМЕСТИТЬ ВТБухучетНачислений";
		Запрос.Выполнить();
		
	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	АВТОНОМЕРЗАПИСИ() КАК ИдентификаторСтроки,
		|	БухучетНачислений.Организация КАК Организация,
		|	БухучетНачислений.Сотрудник КАК Сотрудник,
		|	БухучетНачислений.Подразделение КАК Подразделение,
		|	БухучетНачислений.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БухучетНачислений.Начисление КАК Начисление,
		|	БухучетНачислений.ВидОперации КАК ВидОперации,
		|	БухучетНачислений.ДатаНачала КАК ДатаНачала,
		|	БухучетНачислений.ДатаОкончания КАК ДатаОкончания,
		|	БухучетНачислений.ДокументОснование КАК ДокументОснование,
		|	БухучетНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачислений.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачислений.Сумма КАК Сумма,
		|	БухучетНачислений.ВидНачисленияОплатыТрудаДляНУ КАК ВидНачисленияОплатыТрудаДляНУ,
		|	БухучетНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	ВЫБОР
		|		КОГДА БухучетНачислений.ДатаНачала = ДАТАВРЕМЯ(1, 1, 1)
		|				ИЛИ БухучетНачислений.ДатаНачала < &ПериодРегистрации
		|			ТОГДА &ПериодРегистрации
		|		ИНАЧЕ НАЧАЛОПЕРИОДА(БухучетНачислений.ДатаНачала, МЕСЯЦ)
		|	КОНЕЦ КАК ПериодПринятияРасходов
		|ПОМЕСТИТЬ ВТБухучетНачислений
		|ИЗ
		|	ВТБухучетНачисленийВременная КАК БухучетНачислений
		|ГДЕ
		|	БухучетНачислений.Сумма <> 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТБухучетНачисленийВременная";
		Запрос.Выполнить();
		
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	&Организация КАК Организация, 
	|	*
	|ИЗ
	|	ВТРасширенныеСведенияОВзносах КАК СведенияОВзносах"
	 + Символы.ПС + "ГДЕ" + Символы.ПС + СтрЗаменить(УчетСтраховыхВзносов.ОтражаемыеВУчетеВзносы(Истина, "СведенияОВзносах"), ",", " <> 0 ИЛИ ") + " <> 0";
	
	ТаблицаВзносов = ОтражениеЗарплатыВУчете.ДанныеДляОтражениеВУчетеВзносовПоТаблице(Запрос.Выполнить().Выгрузить());
	
	ОтражениеЗарплатыВБухучете.СоздатьВТБухучетСтраховыхВзносов(Организация, ПериодРегистрации, ТаблицаВзносов, МенеджерВременныхТаблиц);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	*
	|ПОМЕСТИТЬ ВТСтраховыеВзносыПоИсточникамФинансирования
	|ИЗ
	|	ВТБухучетСтраховыхВзносов КАК БухучетСтраховыхВзносов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТБухучетСтраховыхВзносов";
	Запрос.Выполнить();
		
КонецПроцедуры

#КонецОбласти

#Область ПроверкаРаспределенияПоИсточникамФинансирования

Функция ПроверитьРезультатыРаспределенияНачисленийУдержанийВСтрокеОбъекта(ПроверяемаяСтрока, СтрокиРаспределенияРезультатовРасчета, ИмяТаблицы, НачислениеУдержаниеВидОперации, РаботаВБюджетномУчреждении, ВидНачисленияВШапке = Неопределено)
	
	РезультатПроверки = СтруктураРезультатаПроверки();
	
	ИмяТаблицыРаспределениеРезультатовРасчета = ИмяТаблицыДляРаспределенияРезультата(ИмяТаблицы);
	ЭтоТаблицаНачислений = ИмяТаблицыРаспределениеРезультатовРасчета = "РаспределениеРезультатовНачислений";
	
	Если Не ЗначениеЗаполнено(ВидНачисленияВШапке) И ЭтоТаблицаНачислений И ИмяТаблицы <> "НачисленияПоДоговорам" Тогда
		ВидРасчета = ПроверяемаяСтрока.Начисление;
	ИначеЕсли ЗначениеЗаполнено(ВидНачисленияВШапке) Тогда
		ВидРасчета = ВидНачисленияВШапке;
	Иначе
		ВидРасчета = Неопределено;
	КонецЕсли;
	
	ВидыОперацийПособия = Новый Массив;
	ВидыОперацийПособия.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФСС);
	ВидыОперацийПособия.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФССНС);
	
	ПроверяемыеПоля = Новый Структура;
	ПроверяемыеПоля.Вставить("СтатьяФинансирования", Истина);
	ПроверяемыеПоля.Вставить("СтатьяРасходов", РаботаВБюджетномУчреждении);
	
	ЭтоТаблицаНДФЛ						= ИмяТаблицы = "НДФЛ";
	ЭтоТаблицаСуммыВозврата				= ИмяТаблицы = "СуммыВозврата";
	ЭтоТаблицаКорректировкиВыплаты		= ИмяТаблицы = "КорректировкиВыплаты";
	ЭтоТаблицаЗаймов					= ИмяТаблицы = "ПогашениеЗаймов";
	ЭтоТаблицаПрочихДоходов				= ИмяТаблицы = "НачисленияУдержанияВзносы";
	УточнятьПроверкуСпособаОтражения	= ЭтоТаблицаНачислений И ИмяТаблицы <> "НачисленияПоДоговорам" И ВидРасчета <> Неопределено;
	
	Если ЭтоТаблицаНачислений Тогда
		ПроверяемыеПоля.Вставить("СпособОтраженияЗарплатыВБухучете", Истина);
		ПроверяемыеПоля.Вставить("Сотрудник", Ложь);
	ИначеЕсли ЭтоТаблицаПрочихДоходов Тогда
		ПроверяемыеПоля.Вставить("СпособОтраженияЗарплатыВБухучете", Истина);
	Иначе
		// Для удержаний не проверяем способ отражения.
		ПроверяемыеПоля.Вставить("СпособОтраженияЗарплатыВБухучете", Ложь);
		ПроверяемыеПоля.Вставить("Сотрудник", Истина);
	КонецЕсли;
	
	Для Каждого ПроверяемоеПоле Из ПроверяемыеПоля Цикл
		ИмяПоля = ПроверяемоеПоле.Ключ;
		Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокиРаспределенияРезультатовРасчета, ИмяПоля) Тогда
			ПроверяемыеПоля[ИмяПоля] = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Если УточнятьПроверкуСпособаОтражения Тогда
		ПроверяемыеПоля.СпособОтраженияЗарплатыВБухучете = ВидыОперацийПособия.Найти(НачислениеУдержаниеВидОперации[ВидРасчета]) = Неопределено;
	КонецЕсли;
	
	Если ЭтоТаблицаНДФЛ Тогда
		Результат  = ПроверяемаяСтрока.Налог - ПроверяемаяСтрока.ЗачтеноАвансовыхПлатежей + ПроверяемаяСтрока.НалогСПревышения - ПроверяемаяСтрока.ЗачтеноАвансовыхПлатежейСПревышения;
	ИначеЕсли ЭтоТаблицаСуммыВозврата Тогда
		Результат  = ПроверяемаяСтрока.Налог + ПроверяемаяСтрока.НалогСПревышения;
	ИначеЕсли ЭтоТаблицаКорректировкиВыплаты Тогда
		Результат  = ПроверяемаяСтрока.КорректировкаВыплаты;
	ИначеЕсли ЭтоТаблицаЗаймов Тогда
		Результат  = ПроверяемаяСтрока.НачисленоПроцентов + ПроверяемаяСтрока.ПогашениеПроцентов + ПроверяемаяСтрока.ПогашениеЗайма + ПроверяемаяСтрока.МатериальнаяВыгода + ПроверяемаяСтрока.НалогНаМатериальнуюВыгоду;
	ИначеЕсли ЭтоТаблицаПрочихДоходов Тогда
		Результат  = ПроверяемаяСтрока.Начислено;
	Иначе
		Результат  = ПроверяемаяСтрока.Результат;
	КонецЕсли;
	
	РезультатРаспределения = 0;
	ЕстьОшибкиЗаполнения = Ложь;
	Если СтрокиРаспределенияРезультатовРасчета <> Неопределено
		И СтрокиРаспределенияРезультатовРасчета.Количество() > 0 Тогда
		
		Для каждого СтрокаРаспределения Из СтрокиРаспределенияРезультатовРасчета Цикл
			РезультатРаспределения = РезультатРаспределения + СтрокаРаспределения.Результат;
			Для Каждого КлючИЗначение Из ПроверяемыеПоля Цикл
				Если КлючИЗначение.Значение И Не ЗначениеЗаполнено(СтрокаРаспределения[КлючИЗначение.Ключ]) Тогда
					ЕстьОшибкиЗаполнения = Истина;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
	КонецЕсли;
	
	Если Результат = 0 И РезультатРаспределения = 0 Тогда
		ЕстьОшибкиРаспределения = Ложь;
		ЕстьОшибкиЗаполнения = Ложь;
	Иначе
		ЕстьОшибкиРаспределения = Результат <> РезультатРаспределения;
	КонецЕсли;
	
	РезультатПроверки.ЕстьОшибкиРаспределения = ЕстьОшибкиРаспределения;
	РезультатПроверки.ЕстьОшибкиЗаполнения    = ЕстьОшибкиЗаполнения;
	РезультатПроверки.СуммаКРаспределению     = Результат;
	РезультатПроверки.РезультатРаспределения  = РезультатРаспределения;
	
	Возврат РезультатПроверки;
	
КонецФункции

Функция ИмяТаблицыДляРаспределенияРезультата(ИмяТаблицы)
	
	ИмяТаблицыДляРаспределения = "";
	
	Если ИмяТаблицы = "Начисления"
		Или ИмяТаблицы = "НачисленияПерерасчет"
		Или ИмяТаблицы = "НачисленияПоДоговорам"
		Или ИмяТаблицы = "Пособия"
		Или ИмяТаблицы = "ПособияПерерасчет"
		Или ИмяТаблицы = "ДоначисленияИПерерасчеты"
		Или ИмяТаблицы = "ОплатаТруда"
		Или ИмяТаблицы = "Сторнировано"
		Или ИмяТаблицы = "НачисленияУдержанияВзносы" Тогда
		
		ИмяТаблицыДляРаспределения = "РаспределениеРезультатовНачислений";
		
	ИначеЕсли ИмяТаблицы = "НДФЛ"
		Или ИмяТаблицы = "СуммыВозврата"
		Или ИмяТаблицы = "Удержания"
		Или ИмяТаблицы = "ПогашениеЗаймов"
		Или ИмяТаблицы = "КорректировкиВыплаты" Тогда
		
		ИмяТаблицыДляРаспределения = "РаспределениеРезультатовУдержаний";
		
	КонецЕсли;
	
	Если ПустаяСтрока(ИмяТаблицыДляРаспределения) Тогда
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЛьготыСотрудников") Тогда
			МодульЛьготыСотрудниковКлиентСервер = ОбщегоНазначения.ОбщийМодуль("ЛьготыСотрудниковКлиентСервер");
			ИмяТаблицыДляРаспределения = МодульЛьготыСотрудниковКлиентСервер.ИмяТаблицыДляРаспределенияРезультата(ИмяТаблицы);
		КонецЕсли;
	
	КонецЕсли;
	
	Возврат ИмяТаблицыДляРаспределения;
	
КонецФункции

Функция СтруктураРезультатаПроверки()
	
	РезультатПроверки = Новый Структура;
	РезультатПроверки.Вставить("ЕстьОшибкиРаспределения", Истина);
	РезультатПроверки.Вставить("ЕстьОшибкиЗаполнения", Истина);
	РезультатПроверки.Вставить("СуммаКРаспределению", 0);
	РезультатПроверки.Вставить("РезультатРаспределения", Неопределено);
	
	Возврат РезультатПроверки;
	
КонецФункции

Функция ИмяРеквизитаИдентификаторСтрокиПоИмениТаблицы(ИмяТаблицы)
	
	ИмяРеквизитаИдентификаторСтроки = "";
	
	Если ИмяТаблицы = "НДФЛ" Или ИмяТаблицы = "СуммыВозврата" Тогда
		ИмяРеквизитаИдентификаторСтроки = "ИдентификаторСтрокиНДФЛ";
	ИначеЕсли ИмяТаблицы = "ПогашениеЗаймов" Или ИмяТаблицы = "КорректировкиВыплаты" Тогда
		ИмяРеквизитаИдентификаторСтроки = "ИдентификаторСтроки";
	ИначеЕсли ИмяТаблицы = "НачисленияПоДоговорам" Или ИмяТаблицы = "НачисленияУдержанияВзносы" Тогда
		ИмяРеквизитаИдентификаторСтроки = "ИдентификаторСтроки";
	Иначе
		ИмяРеквизитаИдентификаторСтроки = "ИдентификаторСтрокиВидаРасчета";
	КонецЕсли;
	
	Возврат ИмяРеквизитаИдентификаторСтроки;
	
КонецФункции

#КонецОбласти

#Область РедактированиеПроцентаЕНВДВФормахДокументов

Процедура ОбъектПриЧтенииНаСервереПроцентЕНВД(Форма, Организация, МесяцНачисления, ДобавлятьЭлементыФормы = Истина, ДобавлятьРеквизитыФормы = Истина, ОтложенноеИзменение = Ложь) Экспорт

	Если (НЕ ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная")) 
		ИЛИ ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ОтражениеЗарплатыВБухучете.ОбъектПриЧтенииНаСервереПроцентЕНВД(Форма, Организация, МесяцНачисления, ДобавлятьЭлементыФормы, ДобавлятьРеквизитыФормы);
	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры

Процедура ОбъектПриСозданииНаСервереПроцентЕНВД(Форма, Организация, МесяцНачисления) Экспорт
	
	Если (НЕ ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная")) 
		ИЛИ ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Возврат;
	КонецЕсли;
	ОтражениеЗарплатыВБухучете.ОбъектПриСозданииНаСервереПроцентЕНВД(Форма, Организация, МесяцНачисления);
	
КонецПроцедуры

Процедура ОбработатьИзменениеОрганизацииПроцентЕНВД(Форма, Организация, МесяцНачисления) Экспорт
	
	Если (НЕ ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная")) 
		ИЛИ ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Возврат;
	КонецЕсли;
	ОтражениеЗарплатыВБухучете.ОбработатьИзменениеОрганизацииПроцентЕНВД(Форма, Организация, МесяцНачисления)
	
КонецПроцедуры

Процедура ОбработатьИзменениеМесяцНачисленияПроцентЕНВД(Форма, Организация, МесяцНачисления) Экспорт
	
	Если (НЕ ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная")) 
		ИЛИ ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Возврат;
	КонецЕсли;
	ОтражениеЗарплатыВБухучете.ОбработатьИзменениеМесяцНачисленияПроцентЕНВД(Форма, Организация, МесяцНачисления)
	
КонецПроцедуры

Процедура ЗарегистрироватьПроцентЕНВДПослеРедактированияВФорме(Организация, Месяц, ПроцентЕНВД, ПроцентЕНВДСтрока) Экспорт

	Если (НЕ ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная")) 
		ИЛИ ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Возврат;
	КонецЕсли;
	ОтражениеЗарплатыВБухучете.ЗарегистрироватьПроцентЕНВДПослеРедактированияВФорме(Организация, Месяц, ПроцентЕНВД, ПроцентЕНВДСтрока)
	
КонецПроцедуры

#КонецОбласти

#Область ПредставлениеРезультатовРаспределения

Процедура ОбъектПриЧтенииНаСервереПредставлениеРаспределения(Форма, ОписаниеДокумента) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	
	РаботаВБюджетномУчреждении = ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении");
	НачислениеУдержаниеВидОперации = ОтражениеЗарплатыВУчете.НачислениеУдержаниеВидОперации();
	
	НачислениеИзШапкиДокумента = Неопределено;
	Если ОписаниеДокумента.ВидНачисленияВШапке И ЗначениеЗаполнено(ОписаниеДокумента.ВидНачисленияИмя) Тогда
		НачислениеИзШапкиДокумента = Объект[ОписаниеДокумента.ИменаПолейНачисления];
	КонецЕсли;
	
	Если ОписаниеДокумента.НачисленияИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.НачисленияИмя], ОписаниеДокумента, ОписаниеДокумента.НачисленияИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	Если ОписаниеДокумента.НачисленияПерерасчетИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.НачисленияПерерасчетИмя], ОписаниеДокумента, ОписаниеДокумента.НачисленияПерерасчетИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	Если ОписаниеДокумента.ПособияИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.ПособияИмя], ОписаниеДокумента, ОписаниеДокумента.ПособияИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	Если ОписаниеДокумента.НачисленияПоДоговорамИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.НачисленияПоДоговорамИмя], ОписаниеДокумента, ОписаниеДокумента.НачисленияПоДоговорамИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	Если ОписаниеДокумента.СторноИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.СторноИмя], ОписаниеДокумента, ОписаниеДокумента.СторноИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	Если ОписаниеДокумента.ЛьготыИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.ЛьготыИмя], ОписаниеДокумента, ОписаниеДокумента.ЛьготыИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	Если ОписаниеДокумента.ЛьготыПерерасчетИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.ЛьготыПерерасчетИмя], ОписаниеДокумента, ОписаниеДокумента.ЛьготыПерерасчетИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	Если ОписаниеДокумента.УдержанияИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.УдержанияИмя], ОписаниеДокумента, ОписаниеДокумента.УдержанияИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	Если ОписаниеДокумента.УдержанияПерерасчетИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.УдержанияПерерасчетИмя], ОписаниеДокумента, ОписаниеДокумента.УдержанияПерерасчетИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	Если ОписаниеДокумента.НДФЛИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.НДФЛИмя], ОписаниеДокумента, ОписаниеДокумента.НДФЛИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	Если ОписаниеДокумента.КорректировкиВыплатыИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.КорректировкиВыплатыИмя], ОписаниеДокумента, ОписаниеДокумента.КорректировкиВыплатыИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	Если ОписаниеДокумента.ПогашениеЗаймовИмя <> Неопределено Тогда
		ЗаполнитьПредставлениеРаспределения(Объект[ОписаниеДокумента.ПогашениеЗаймовИмя], ОписаниеДокумента, ОписаниеДокумента.ПогашениеЗаймовИмя, НачислениеИзШапкиДокумента, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПредставлениеРаспределения(ДанныеЗаполнения, ОписаниеДокумента, ИмяТаблицы, ВидРасчетаИзШапки, РаботаВБюджетномУчреждении, НачислениеУдержаниеВидОперации) 
	
	Если ДанныеЗаполнения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Для Каждого СтрокаТаблицы Из ДанныеЗаполнения Цикл
			
		РезультатПроверкиСтроки = ПроверитьРезультатыРаспределенияНачисленийУдержанийВСтроке(
			СтрокаТаблицы, ИмяТаблицы, ОписаниеДокумента, ВидРасчетаИзШапки, НачислениеУдержаниеВидОперации, РаботаВБюджетномУчреждении);
			
		РаспределениеСодержитОшибки = РезультатПроверкиСтроки.ЕстьОшибкиРаспределения Или РезультатПроверкиСтроки.ЕстьОшибкиЗаполнения;
		
		ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.ЗаполнитьПредставлениеРаспределенияВСтроке(
			СтрокаТаблицы, РаспределениеСодержитОшибки, ИмяТаблицы, РаботаВБюджетномУчреждении);			
			
	КонецЦикла	

КонецПроцедуры

Функция ПроверитьРезультатыРаспределенияНачисленийУдержанийВСтроке(ПроверяемаяСтрока, ИмяТаблицы, ОписаниеДокумента, ВидРасчетаИзШапки, НачислениеУдержаниеВидОперации, РаботаВБюджетномУчреждении, РезультатыРаспределения = Неопределено)
	
	ОписаниеТаблицыФормы = Неопределено;
	Если Не ОписаниеДокумента.ОписанияТаблицДляРаспределенияРезультата.Свойство(ИмяТаблицы, ОписаниеТаблицыФормы) Тогда
		Возврат СтруктураРезультатаПроверки();
	КонецЕсли;
	
	Если РезультатыРаспределения = Неопределено Тогда 
		РезультатыРаспределения = ПроверяемаяСтрока.РезультатРаспределения;
	КонецЕсли;
	
	Возврат ПроверитьРезультатыРаспределенияНачисленийУдержанийВСтрокеОбъекта(ПроверяемаяСтрока, РезультатыРаспределения, ИмяТаблицы,
		НачислениеУдержаниеВидОперации,РаботаВБюджетномУчреждении, ВидРасчетаИзШапки);
	
КонецФункции

Процедура ДополнитьТаблицуРаспределенияКодомСтатьиФинансирования(ТаблицаРаспределения) Экспорт
	
	КодыСтатей = КодыСтатейФинансирования();
	
	Если ТаблицаРаспределения.Колонки.Найти("КодСтатьиФинансирования") = Неопределено Тогда
		ТаблицаРаспределения.Колонки.Добавить("КодСтатьиФинансирования", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(3)));
	КонецЕсли;
	Для каждого СтрокаТЗ Из ТаблицаРаспределения Цикл
		СтрокаТЗ.КодСтатьиФинансирования = КодыСтатей[СтрокаТЗ.СтатьяФинансирования]
	КонецЦикла;
	
КонецПроцедуры

Функция КодыСтатейФинансирования() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтатьиФинансированияЗарплата.Код КАК КодСтатьиФинансирования,
	|	СтатьиФинансированияЗарплата.Ссылка
	|ИЗ
	|	Справочник.СтатьиФинансированияЗарплата КАК СтатьиФинансированияЗарплата";
	Выборка = Запрос.Выполнить().Выбрать();
	
	КодыСтатей = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		КодыСтатей.Вставить(Выборка.Ссылка, Выборка.КодСтатьиФинансирования);
	КонецЦикла;
	КодыСтатей.Вставить(Справочники.СтатьиФинансированияЗарплата.ПустаяСсылка(), "");
	
	Возврат КодыСтатей;

КонецФункции 

// Формирует представление результата распределения строки начисления
// Параметры
//	СуммаОсновнойСтроки - число, результат расчета строки начисления 
//	ВидРасчета - вид расчета из строки начисления
//	СтрокиРаспределения - коллекция строк с результатами распределения строки начисления, содержит поле КодСтатьиФинансирования
//	ПараметрыДляПроверки - структура, см ПараметрыДляПроверкиРезультатовРаспределенияНачислений.
//
// Возвращаемое значение - строка, для помещения в реквизит КомандаРедактированияРаспределения.
//
Функция ПредставлениеРезультатаРаспределенияСтрокиНачисления(СуммаОсновнойСтроки, ВидРасчета, СтрокиРаспределения, ПараметрыДляПроверки) Экспорт
	
	ВидОперации = ПараметрыДляПроверки.НачислениеУдержаниеВидОперации[ВидРасчета];
	ПараметрыДляПроверки.ПроверяемыеПоля.СпособОтраженияЗарплатыВБухучете = ПараметрыДляПроверки.ВидыОперацийПособия.Найти(ВидОперации) = Неопределено;
	ЕстьОшибкиРаспределения = РезультатРаспределенияСодержитОшибки(СуммаОсновнойСтроки, СтрокиРаспределения, ПараметрыДляПроверки.ПроверяемыеПоля);
	
	Возврат ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.ПредставлениеРаспределения(СтрокиРаспределения, ЕстьОшибкиРаспределения, ПараметрыДляПроверки.ПроверяемыеПоля.СтатьяРасходов)

КонецФункции

Функция ПредставлениеРезультатаРаспределенияСтрокиУдержания(СуммаОсновнойСтроки, СтрокиРаспределения, ПроверяемыеПоля) Экспорт
	
	ЕстьОшибкиРаспределения = РезультатРаспределенияСодержитОшибки(СуммаОсновнойСтроки, СтрокиРаспределения, ПроверяемыеПоля);
	Возврат ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.ПредставлениеРаспределения(СтрокиРаспределения, ЕстьОшибкиРаспределения, ПроверяемыеПоля.СтатьяРасходов)

КонецФункции

Функция ПредставлениеРезультатаРаспределенияСтрокиПогашениеЗаймов(СуммаОсновнойСтроки, СтрокиРаспределения, ПроверяемыеПоля) Экспорт
	
	ЕстьОшибкиРаспределения = РезультатРаспределенияСодержитОшибки(СуммаОсновнойСтроки, СтрокиРаспределения, ПроверяемыеПоля);
	
	РаспределениеВсейСуммыСтроки = Новый Массив;
	
	ИменаКолонок = "СтатьяФинансирования,СтатьяРасходов,КодСтатьиФинансирования,Результат,ВидУдержания,Сотрудник,Подразделение";
	Для каждого СтрокаРаспределения Из СтрокиРаспределения Цикл
		
		ОписаниеСтроки = Новый Структура(ИменаКолонок);
		ЗаполнитьЗначенияСвойств(ОписаниеСтроки, СтрокаРаспределения);
		
		СтрокаНеНайдена = Истина;
		
		Для Каждого СтрокаРаспределенияВсейСуммыСтроки Из РаспределениеВсейСуммыСтроки Цикл
			Если СтрокаРаспределенияВсейСуммыСтроки.СтатьяФинансирования = ОписаниеСтроки.СтатьяФинансирования
				И СтрокаРаспределенияВсейСуммыСтроки.КодСтатьиФинансирования = ОписаниеСтроки.КодСтатьиФинансирования
				И СтрокаРаспределенияВсейСуммыСтроки.СтатьяРасходов = ОписаниеСтроки.СтатьяРасходов
				И СтрокаРаспределенияВсейСуммыСтроки.Сотрудник = ОписаниеСтроки.Сотрудник
				И СтрокаРаспределенияВсейСуммыСтроки.Подразделение = ОписаниеСтроки.Подразделение Тогда
				СтрокаРаспределенияВсейСуммыСтроки.Результат = СтрокаРаспределенияВсейСуммыСтроки.Результат + ОписаниеСтроки.Результат;
				СтрокаНеНайдена = Ложь;
			КонецЕсли;				
		КонецЦикла;
		
		Если СтрокаНеНайдена Тогда
			РаспределениеВсейСуммыСтроки.Добавить(ОписаниеСтроки);
		КонецЕсли;			
		
	КонецЦикла;
	
	Возврат ОтражениеЗарплатыВБухучетеКлиентСерверРасширенный.ПредставлениеРаспределения(РаспределениеВсейСуммыСтроки, ЕстьОшибкиРаспределения, ПроверяемыеПоля.СтатьяРасходов)

КонецФункции

// ПроверяемыеПоля см ПараметрыДляПроверкиРезультатовРаспределенияНачислений.ПроверяемыеПоля.
Функция РезультатРаспределенияСодержитОшибки(СуммаОсновнойСтроки, СтрокиРаспределения, ПроверяемыеПоля)
	
	РезультатРаспределения = 0;
	Для каждого СтрокаРаспределения Из СтрокиРаспределения Цикл
		РезультатРаспределения = РезультатРаспределения + СтрокаРаспределения.Результат;
		Для Каждого КлючИЗначение Из ПроверяемыеПоля Цикл
			Если КлючИЗначение.Значение И Не ЗначениеЗаполнено(СтрокаРаспределения[КлючИЗначение.Ключ]) Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат СуммаОсновнойСтроки <> РезультатРаспределения;

КонецФункции 

Функция ПараметрыДляПроверкиРезультатовРаспределенияНачислений() Экспорт

	ВидыОперацийПособия = Новый Массив;
	ВидыОперацийПособия.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФСС);
	ВидыОперацийПособия.Добавить(Перечисления.ВидыОперацийПоЗарплате.РасходыПоСтрахованиюФССНС);
	
	ПроверяемыеПоля = Новый Структура;
	ПроверяемыеПоля.Вставить("СтатьяФинансирования", ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата"));
	ПроверяемыеПоля.Вставить("СтатьяРасходов", ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении"));
	ПроверяемыеПоля.Вставить("СпособОтраженияЗарплатыВБухучете", Ложь);
	
	НачислениеУдержаниеВидОперации = ОтражениеЗарплатыВУчете.НачислениеУдержаниеВидОперации();
	
	Параметры = Новый Структура;
	Параметры.Вставить("ВидыОперацийПособия", ВидыОперацийПособия);
	Параметры.Вставить("НачислениеУдержаниеВидОперации", НачислениеУдержаниеВидОперации);
	Параметры.Вставить("ПроверяемыеПоля", ПроверяемыеПоля);
	
	Возврат Параметры;

КонецФункции

Функция ПараметрыДляПроверкиРезультатовРаспределенияУдержаний() Экспорт

	Параметры = Новый Структура;
	Параметры.Вставить("СтатьяФинансирования", ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата"));
	Параметры.Вставить("СтатьяРасходов", ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении"));
	Параметры.Вставить("Сотрудник", Истина);
	
	Возврат Параметры;

КонецФункции

#КонецОбласти

#Область ЗначенияСтатейРасходовПоУмолчанию

// Возвращает ссылку на элемент справочника СтатьиРасходовЗарплата с кодом 290.
Функция СтатьяРасходов290()
	
	Статья290 = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	Если ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Статья290 = Справочники.СтатьиРасходовЗарплата.НайтиПоКоду("290");
	КонецЕсли;

	Возврат Статья290;
	
КонецФункции

// Возвращает ссылку на элемент справочника СтатьиРасходовЗарплата с кодом 296.
Функция СтатьяРасходов296()
	
	Статья296 = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	Если ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Статья296 = Справочники.СтатьиРасходовЗарплата.НайтиПоКоду("296");
	КонецЕсли;

	Возврат Статья296;
	
КонецФункции

// Возвращает ссылку на элемент справочника СтатьиРасходовЗарплата с кодом 265.
Функция СтатьяРасходов265()
	
	Статья265 = Справочники.СтатьиРасходовЗарплата.ПустаяСсылка();
	Если ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Статья265 = Справочники.СтатьиРасходовЗарплата.НайтиПоКоду("265");
	КонецЕсли;

	Возврат Статья265;
	
КонецФункции

// Возвращает дату вступления в силу приказа Минфина РФ от 29 сентября 2020 г. №222н.
//
// Параметры:
//  нет
//
// Возвращаемое значение:
//   дата
//
Функция ДатаПриказа222н()

	Возврат '20210101';

КонецФункции

#КонецОбласти

#Область Прочие

// Устанавливает подсказку ввода для элемента СтатьяРасходов.
// Параметры:
// 		Форма - форма объекта
// 		ПутьРеквизита - путь к реквизиту типа ПланыВидовРасчетаСсылка.Начисления.
//
Процедура УстановитьПодсказкуВводаСтатьиРасходовПоНачислению(Форма, ПутьРеквизита) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Возврат;
	КонецЕсли;
	
	ПодсказкаВвода = "211";
	
	ВидРасчета = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, ПутьРеквизита);
	Если ЗначениеЗаполнено(ВидРасчета) Тогда
		СтатьяРасходов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидРасчета, "СтатьяРасходов");
		Если ЗначениеЗаполнено(СтатьяРасходов) Тогда
			ПодсказкаВвода = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтатьяРасходов, "Код");
		КонецЕсли;
	КонецЕсли;	
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы, "СтатьяРасходов", "ПодсказкаВвода", ПодсказкаВвода);

КонецПроцедуры

// Устанавливает подсказку ввода для элемента СтатьяРасходов.
// Параметры:
// 		Форма - форма объекта
// 		ВидВыплаты - типа СправочникСсылка.ВидыВыплатБывшимСотрудникам
//		ПериодРегистрации - месяц регистрации выплаты.
//
Процедура УстановитьПодсказкуВводаСтатьиРасходовВыплатыБывшимСотрудникам(Форма, ВидВыплаты, ПериодРегистрации) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		Возврат;
	КонецЕсли;
	
	ПодсказкаВвода = "211";
	
	Если ЗначениеЗаполнено(ВидВыплаты) Тогда
		
		Если ВидВыплаты = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыВыплатБывшимСотрудникам.СохраняемыйЗаработокНаВремяТрудоустройства")
			Или ВидВыплаты = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыВыплатБывшимСотрудникам.СохраняемоеДенежноеСодержаниеНаПериодТрудоустройства") Тогда
			
			СтатьяРасходов = СтатьяРасходовДляСохраняемогоЗаработкаНаПериодТрудоустройства(ПериодРегистрации);
			Если ЗначениеЗаполнено(СтатьяРасходов) Тогда
				ПодсказкаВвода = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтатьяРасходов, "Код");
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;	
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы, "СтатьяРасходов", "ПодсказкаВвода", ПодсказкаВвода);

КонецПроцедуры

Процедура УстановитьСписокВыбораОтношениеКЕНВД(ЭлементыФормы, ИмяЭлемента) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		
		ЭлементФормы = ЭлементыФормы.Найти(ИмяЭлемента);
		Если ЭлементФормы <> Неопределено Тогда
			ЭлементФормы.РежимВыбораИзСписка = Истина;
			ЭлементФормы.СписокВыбора.Добавить(Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.ЕНВД);
			ЭлементФормы.СписокВыбора.Добавить(Перечисления.ОтношениеКЕНВДЗатратНаЗарплату.НеЕНВД);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Процедура УточнитьСоставПроверяемыхРеквизитовБухучетПлановыхУдержаний(ДокументОбъект, ПроверяемыеРеквизиты) Экспорт

	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата")
		Или Не ДокументОбъект.БухучетЗаданВДокументе
		Или ДокументОбъект.Действие = Перечисления.ДействияСУдержаниями.Прекратить
		Или Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОбъект.Удержание, "ДоступнаСтратегияОтраженияКакЗаданоВидуРасчета") Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СтатьяФинансирования");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СтатьяРасходов");
	ИначеЕсли НЕ ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "СтатьяРасходов");
	КонецЕсли;

КонецПроцедуры

Функция МетаданныеРегистровПодсистемы()
	
	Регистры = Новый Массив;
	Регистры.Добавить(Метаданные.РегистрыНакопления.БухучетНачисленияУдержанияПоКонтрагентамАкционерам);
	Регистры.Добавить(Метаданные.РегистрыНакопления.БухучетНачисленияУдержанияПоСотрудникам);
	Регистры.Добавить(Метаданные.РегистрыНакопления.БухучетНачисленияУдержанияПоСотрудникамАвансом);
	
	Возврат Регистры;
КонецФункции

Процедура СоздатьВТБухучетНачисленийПоСтатьямФинансирования(ПараметрыПолученияДанных)

	РаботаВБюджетномУчреждении = ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении");
	
	Организация 					= ПараметрыПолученияДанных.Организация;
	ПериодРегистрации 				= ПараметрыПолученияДанных.ПериодРегистрации;
	МенеджерВременныхТаблиц 		= ПараметрыПолученияДанных.МенеджерВременныхТаблиц;
	ФизическиеЛица 					= ПараметрыПолученияДанных.ФизическиеЛица;
	ИсключаемыеРегистраторы 		= ПараметрыПолученияДанных.ИсключаемыеРегистраторы;
	ИсключатьМежрасчетныеВыплаты 	= ПараметрыПолученияДанных.ИсключатьМежрасчетныеВыплаты;
	ТолькоДоходыСотрудников 		= ПараметрыПолученияДанных.ТолькоДоходыСотрудников;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("ИсключаемыеРегистраторы", ИсключаемыеРегистраторы);
	Запрос.УстановитьПараметр("МассивФизическихЛиц", ФизическиеЛица);
	Запрос.УстановитьПараметр("РаботаВБюджетномУчреждении", РаботаВБюджетномУчреждении);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	БухучетНачисления.Организация КАК Организация,
	|	БухучетНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	БухучетНачисления.Сотрудник КАК Сотрудник,
	|	БухучетНачисления.Подразделение КАК Подразделение,
	|	БухучетНачисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
	|	БухучетНачисления.МестоПолученияДохода КАК МестоПолученияДохода,
	|	БухучетНачисления.НачислениеУдержание КАК НачислениеУдержание,
	|	БухучетНачисления.ВидОперации КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА БухучетНачисления.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|			ТОГДА БухучетНачисления.Подразделение
	|		ИНАЧЕ БухучетНачисления.ПодразделениеУчетаЗатрат
	|	КОНЕЦ КАК ПодразделениеУчетаЗатрат,
	|	БухучетНачисления.СтатьяФинансирования КАК СтатьяФинансирования,
	|	БухучетНачисления.СтатьяРасходов КАК СтатьяРасходов,
	|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	БухучетНачисления.ДатаНачала КАК ДатаНачала,
	|	БухучетНачисления.ДатаОкончания КАК ДатаОкончания,
	|	БухучетНачисления.Сумма КАК Сумма,
	|	БухучетНачисления.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ ВТБухучетНачисленийПоСтатьямФинансирования
	|ИЗ
	|	РегистрНакопления.БухучетНачисленияУдержанияПоСотрудникам КАК БухучетНачисления
	|ГДЕ
	|	БухучетНачисления.Организация = &Организация
	|	И НАЧАЛОПЕРИОДА(БухучетНачисления.Период, МЕСЯЦ) = &ПериодРегистрации
	|	И БухучетНачисления.ГруппаНачисленияУдержанияВыплаты = ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Начислено)
	|	И &УсловиеПоРегистраторам
	|	И &УсловиеПоФизическимЛицам
	|	И &УсловиеИсключатьМежрасчетныеВыплаты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	БухучетНачисления.Организация,
	|	БухучетНачисления.ФизическоеЛицо,
	|	БухучетНачисления.Сотрудник,
	|	БухучетНачисления.Подразделение,
	|	БухучетНачисления.ТерриторияВыполненияРаботВОрганизации,
	|	БухучетНачисления.МестоПолученияДохода,
	|	БухучетНачисления.НачислениеУдержание,
	|	БухучетНачисления.ВидОперации,
	|	ВЫБОР
	|		КОГДА БухучетНачисления.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|			ТОГДА БухучетНачисления.Подразделение
	|		ИНАЧЕ БухучетНачисления.ПодразделениеУчетаЗатрат
	|	КОНЕЦ,
	|	БухучетНачисления.СтатьяФинансирования,
	|	БухучетНачисления.СтатьяРасходов,
	|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачисления.ОблагаетсяЕНВД,
	|	БухучетНачисления.ДатаНачала,
	|	БухучетНачисления.ДатаОкончания,
	|	БухучетНачисления.Сумма,
	|	БухучетНачисления.ДокументОснование
	|ИЗ
	|	РегистрНакопления.БухучетНачисленияУдержанияПоСотрудникам КАК БухучетНачисления
	|ГДЕ
	|	БухучетНачисления.Организация = &Организация
	|	И НАЧАЛОПЕРИОДА(БухучетНачисления.Период, МЕСЯЦ) = &ПериодРегистрации
	|	И БухучетНачисления.ГруппаНачисленияУдержанияВыплаты = ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Справочно)
	|	И &УсловиеПоРегистраторам
	|	И БухучетНачисления.НачислениеУдержание ССЫЛКА ПланВидовРасчета.Начисления
	|	И &УсловиеПоФизическимЛицам
	|	И &УсловиеИсключатьМежрасчетныеВыплаты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	БухучетНачисления.Организация,
	|	БухучетНачисления.ФизическоеЛицо,
	|	БухучетНачисления.Сотрудник,
	|	БухучетНачисления.Подразделение,
	|	БухучетНачисления.ТерриторияВыполненияРаботВОрганизации,
	|	БухучетНачисления.МестоПолученияДохода,
	|	БухучетНачисления.НачислениеУдержание,
	|	БухучетНачисления.ВидОперации,
	|	ВЫБОР
	|		КОГДА БухучетНачисления.ПодразделениеУчетаЗатрат = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|			ТОГДА БухучетНачисления.Подразделение
	|		ИНАЧЕ БухучетНачисления.ПодразделениеУчетаЗатрат
	|	КОНЕЦ,
	|	БухучетНачисления.СтатьяФинансирования,
	|	ВЫБОР
	|		КОГДА НЕ &РаботаВБюджетномУчреждении
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|		ИНАЧЕ БухучетНачисления.СтатьяРасходов
	|	КОНЕЦ,
	|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете,
	|	БухучетНачисления.ОблагаетсяЕНВД,
	|	БухучетНачисления.ДатаНачала,
	|	БухучетНачисления.ДатаОкончания,
	|	БухучетНачисления.Сумма,
	|	БухучетНачисления.ДокументОснование
	|ИЗ
	|	РегистрНакопления.БухучетНачисленияУдержанияПоСотрудникам КАК БухучетНачисления
	|ГДЕ
	|	БухучетНачисления.Организация = &Организация
	|	И НАЧАЛОПЕРИОДА(БухучетНачисления.Период, МЕСЯЦ) = &ПериодРегистрации
	|	И БухучетНачисления.ГруппаНачисленияУдержанияВыплаты = ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Справочно)
	|	И &УсловиеПоРегистраторам
	|	И БухучетНачисления.НачислениеУдержание = ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.СтоимостьПодарковПризов)
	|	И &УсловиеПоФизическимЛицам
	|	И &УсловиеИсключатьМежрасчетныеВыплаты";
	
	Если Не ТолькоДоходыСотрудников Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|";
		
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	БухучетНачисления.Организация КАК Организация,
		|	БухучетНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|	БухучетНачисления.Сотрудник КАК Сотрудник,
		|	БухучетНачисления.Подразделение КАК Подразделение,
		|	БухучетНачисления.ТерриторияВыполненияРаботВОрганизации КАК ТерриторияВыполненияРаботВОрганизации,
		|	БухучетНачисления.МестоПолученияДохода КАК МестоПолученияДохода,
		|	БухучетНачисления.НачислениеУдержание КАК НачислениеУдержание,
		|	БухучетНачисления.ВидОперации КАК ВидОперации,
		|	БухучетНачисления.Подразделение КАК ПодразделениеУчетаЗатрат,
		|	БухучетНачисления.СтатьяФинансирования КАК СтатьяФинансирования,
		|	БухучетНачисления.СтатьяРасходов КАК СтатьяРасходов,
		|	БухучетНачисления.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
		|	БухучетНачисления.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
		|	БухучетНачисления.Период КАК ДатаНачала,
		|	БухучетНачисления.Период КАК ДатаОкончания,
		|	БухучетНачисления.Сумма КАК Сумма,
		|	БухучетНачисления.ДокументОснование КАК ДокументОснование
		|ИЗ
		|	РегистрНакопления.БухучетНачисленияУдержанияПоКонтрагентамАкционерам КАК БухучетНачисления
		|ГДЕ
		|	БухучетНачисления.Организация = &Организация
		|	И НАЧАЛОПЕРИОДА(БухучетНачисления.Период, МЕСЯЦ) = &ПериодРегистрации
		|	И БухучетНачисления.ГруппаНачисленияУдержанияВыплаты = ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Начислено)
		|	И &УсловиеПоРегистраторам
		|	И &УсловиеПоФизическимЛицам";
		
	КонецЕсли;
	
	Если ИсключаемыеРегистраторы = Неопределено Тогда
		УсловиеПоРегистраторам = "";
	ИначеЕсли ТипЗнч(ИсключаемыеРегистраторы) = Тип("Массив") Тогда
		УсловиеПоРегистраторам = "И НЕ БухучетНачисления.Регистратор В (&ИсключаемыеРегистраторы)";
	Иначе
		УсловиеПоРегистраторам = "И БухучетНачисления.Регистратор <> &ИсключаемыеРегистраторы";
	КонецЕсли;
	
	УсловиеИсключатьМежрасчетныеВыплаты = "ИСТИНА";
	Если ИсключатьМежрасчетныеВыплаты Тогда
		ЕдиновременныеПособия = ОтражениеЗарплатыВУчетеРасширенный.ВидыОсобыхНачисленийЕдиновременныеПособия();
		Запрос.УстановитьПараметр("ЕдиновременныеПособия", ЕдиновременныеПособия);
		УсловиеИсключатьМежрасчетныеВыплаты = "НЕ БухучетНачисления.ДанныеМежрасчетногоПериода И НЕ БухучетНачисления.НачислениеУдержание В (&ЕдиновременныеПособия)";
	КонецЕсли;

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоФизическимЛицам", ?(ФизическиеЛица = Неопределено,"","И БухучетНачисления.ФизическоеЛицо В(&МассивФизическихЛиц)"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеИсключатьМежрасчетныеВыплаты", УсловиеИсключатьМежрасчетныеВыплаты);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловиеПоРегистраторам", УсловиеПоРегистраторам);
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.Выполнить();

КонецПроцедуры

// Возвращает признак применения статей утвержденных
// Приказом Минфина России от 29.11.2017 N 209н (ред. от 30.11.2018) на дату, переданную в параметре.
//
// Параметры:
//  Период - дата
//
// Возвращаемое значение:
//   Булево
//
Функция СтатьиРасходовПоПриказу209нПрименяются(Период)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.УчетБюджетныхУчреждений") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("УчетБюджетныхУчреждений");
		ДатаПрименения = Модуль.ДатаНачалаПримененияПриказа209н();
		Если Не ЗначениеЗаполнено(ДатаПрименения) Тогда
			Возврат Ложь;
		Иначе
			Возврат (НачалоМесяца(Период) >= ДатаПрименения);
		КонецЕсли;
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

Процедура ЗаполнитьРегистрациюВНалоговомОрганеВКоллекцииСтрок(Организация, Период, КоллекцияНачисленныйНДФЛ) Экспорт
	
	ГоловнаяОрганизация = ЗарплатаКадры.ГоловнаяОрганизация(Организация);
	ЮридическоеФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ГоловнаяОрганизация,"ЮридическоеФизическоеЛицо");
	ОрганизацияЮрлицо = ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", ГоловнаяОрганизация);
	
	Если ТипЗнч(КоллекцияНачисленныйНДФЛ) = Тип("ДанныеФормыКоллекция") Тогда
		Запрос.УстановитьПараметр("НачисленныйНДФЛ", КоллекцияНачисленныйНДФЛ.Выгрузить());
	Иначе
		// передана таблица значений
		
		Если КоллекцияНачисленныйНДФЛ.Колонки.Найти("НомерСтроки") = Неопределено Тогда
			КоллекцияНачисленныйНДФЛ.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 0)));
			НомерСтроки = 1;
			Для каждого СтрокаТЗ Из КоллекцияНачисленныйНДФЛ Цикл
				СтрокаТЗ.НомерСтроки = НомерСтроки;
				НомерСтроки = НомерСтроки +1;
			КонецЦикла;
		КонецЕсли;
		
		Если КоллекцияНачисленныйНДФЛ.Колонки.Найти("РегистрацияВНалоговомОргане") = Неопределено Тогда
			КоллекцияНачисленныйНДФЛ.Колонки.Добавить("РегистрацияВНалоговомОргане", Новый ОписаниеТипов("СправочникСсылка.РегистрацииВНалоговомОргане"));
		КонецЕсли;
		
		Запрос.УстановитьПараметр("НачисленныйНДФЛ", КоллекцияНачисленныйНДФЛ);
		
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РегистрацииВНалоговомОргане.Ссылка КАК РегистрацияВНалоговомОргане,
	|	РегистрацииВНалоговомОргане.КодПоОКАТО КАК КодПоОКАТО,
	|	РегистрацииВНалоговомОргане.КодПоОКТМО КАК КодПоОКТМО,
	|	РегистрацииВНалоговомОргане.КПП КАК КПП,
	|	РегистрацииВНалоговомОргане.Код КАК КодНалоговогоОргана
	|ПОМЕСТИТЬ ВТРегистрацииВНалоговомОргане
	|ИЗ
	|	Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|ГДЕ
	|	РегистрацииВНалоговомОргане.Владелец = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленныйНДФЛ.НомерСтроки КАК НомерСтроки,
	|	НачисленныйНДФЛ.КодПоОКАТО КАК КодПоОКАТО,
	|	НачисленныйНДФЛ.КодПоОКТМО КАК КодПоОКТМО,
	|	НачисленныйНДФЛ.КПП КАК КПП,
	|	НачисленныйНДФЛ.КодНалоговогоОргана КАК КодНалоговогоОргана
	|ПОМЕСТИТЬ ВТНачисленныйНДФЛ
	|ИЗ
	|	&НачисленныйНДФЛ КАК НачисленныйНДФЛ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленныйНДФЛ.НомерСтроки КАК НомерСтроки,
	|	РегистрацииВНалоговомОргане.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ИЗ
	|	ВТНачисленныйНДФЛ КАК НачисленныйНДФЛ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|		ПО НачисленныйНДФЛ.КодНалоговогоОргана = РегистрацииВНалоговомОргане.КодНалоговогоОргана
	|			И НачисленныйНДФЛ.КПП = РегистрацииВНалоговомОргане.КПП
	|			И НачисленныйНДФЛ.КодПоОКТМО = РегистрацииВНалоговомОргане.КодПоОКТМО
	|ГДЕ
	|	НЕ РегистрацииВНалоговомОргане.КПП ЕСТЬ NULL";
	
	Если НЕ ОрганизацияЮрлицо Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И НачисленныйНДФЛ.КПП = РегистрацииВНалоговомОргане.КПП", "");
	КонецЕсли;
	Если Период < УчетНДФЛ.ДатаПереходаНаКодыОКТМО() Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "НачисленныйНДФЛ.КодПоОКТМО = РегистрацииВНалоговомОргане.КодПоОКТМО", "НачисленныйНДФЛ.КодПоОКАТО = РегистрацииВНалоговомОргане.КодПоОКАТО");	
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		КоллекцияНачисленныйНДФЛ[Выборка.НомерСтроки - 1].РегистрацияВНалоговомОргане = Выборка.РегистрацияВНалоговомОргане;
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьИспользованиеЕНВД()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НастройкиСистемыНалогообложения.ПрименяетсяЕНВД КАК ПлательщикЕНВД
	|ИЗ
	|	РегистрСведений.НастройкиСистемыНалогообложения КАК НастройкиСистемыНалогообложения
	|ГДЕ
	|	НастройкиСистемыНалогообложения.ПрименяетсяЕНВД";
	
	Результат = Запрос.Выполнить();
	
	Константы.ИспользуетсяЕНВД.Установить(Не Результат.Пустой());
	Константы.ИспользуетсяЕНВДВБюджетномУчреждении.Установить(Не Результат.Пустой() И Константы.РаботаВБюджетномУчреждении.Получить());

КонецПроцедуры

Функция ИспользуетсяЕНВД() Экспорт

	Возврат ПолучитьФункциональнуюОпцию("ИспользуетсяЕНВД");
	
КонецФункции

Процедура СоздатьВТИсключаемыеСтроки(МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БухучетНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТИсключаемыеСтроки
	|ИЗ
	|	ВТБухучетНачислений КАК БухучетНачислений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПризПодарок КАК ПризПодарок
	|		ПО БухучетНачислений.ДокументОснование = ПризПодарок.Ссылка
	|			И (НЕ ПризПодарок.ПредусмотреноКолдоговором)
	|ГДЕ
	|	БухучетНачислений.Начисление = ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.СтоимостьПодарковПризов)";
	Запрос.Выполнить()

КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// Добавляет в список Обработчики процедуры-обработчики обновления,
// необходимые данной подсистеме.
//
// Параметры:
//   Обработчики - ТаблицаЗначений - см. описание функции НоваяТаблицаОбработчиковОбновления
//                                   общего модуля ОбновлениеИнформационнойБазы.
// 
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.7.3";
	Обработчик.Процедура = "ОтражениеЗарплатыВБухучетеРасширенный.ОбновитьДокументыРаспределениеОсновногоЗаработка";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();   
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("59648975-c23c-4326-9d8a-2bd3659d1ecc");
	Обработчик.Комментарий = НСтр("ru = 'Обновление документов ""Распределение основного заработка.""';
									|en = 'Update ""Basic earnings allocation"" documents.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.14.79";
	Обработчик.Процедура = "ОтражениеЗарплатыВБухучетеРасширенный.ОбновитьСтратегиюБухучетаСдельногоЗаработка";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();   
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("94c0b423-469c-4a0d-a815-10110e07fa87");
	Обработчик.Комментарий = НСтр("ru = 'Обновление стратегии отражения в учете начислений по сдельному заработку.';
									|en = 'Updating the strategy for recording piecework accruals in the accounting.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.14.128";
	Обработчик.Процедура = "РегистрыНакопления.БухучетНачисленияУдержанияПоКонтрагентамАкционерам.ОчиститьВидыДоходовИсполнительногоПроизводства";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("a4cce4bb-ab14-42f1-bc99-72494c78ea61"); 
	Обработчик.Комментарий = НСтр("ru = 'Очистка видов дохода исполнительного производства старых движений бухучета начислений и удержаний по контрагентам, акционерам.';
									|en = 'Clearing of enforcement proceedings income kinds of the old records in counterparties accrual and deduction accounting.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.14.128";
	Обработчик.Процедура = "РегистрыНакопления.БухучетНачисленияУдержанияПоСотрудникам.ОчиститьВидыДоходовИсполнительногоПроизводства";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("f5e15567-6901-4105-a6ef-9dade1de3a88"); 
	Обработчик.Комментарий = НСтр("ru = 'Очистка видов дохода исполнительного производства старых движений бухучета начислений и удержаний по сотрудникам.';
									|en = 'Clearing of enforcement proceedings income kinds of the old records in employee accrual and deduction accounting.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.14.128";
	Обработчик.Процедура = "РегистрыНакопления.БухучетНачисленияУдержанияПоСотрудникамАвансом.ОчиститьВидыДоходовИсполнительногоПроизводства";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("eb959c60-0c81-4172-b176-555702f73ae1"); 
	Обработчик.Комментарий = НСтр("ru = 'Очистка видов дохода исполнительного производства старых движений бухучета начислений и удержаний по сотрудникам авансом.';
									|en = 'Clearing of enforcement proceedings income kinds of the old records in employee accrual and deduction accounting as advance.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.17.54";
	Обработчик.Процедура = "ОтражениеЗарплатыВБухучетеРасширенный.ЗаполнитьСтатьюРасходовОплатаДнейУходаЗаДетьмиИнвалидами";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("a4cffb10-5f10-11eb-810b-4cedfb95099a"); 
	Обработчик.Комментарий = НСтр("ru = 'Заполнение статьи расходов начисления Оплата дней ухода за детьми-инвалидами.';
									|en = 'Populating the accrual expense item Payment for disabled child care days.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.18.302";
	Обработчик.Процедура = "ОтражениеЗарплатыВБухучетеРасширенный.СтатьиФинансированияЗаполнитьИспользованиеВБазеСреднегоЗаработка";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("4c6a2a4e-6e8b-4f80-9791-856f4505b809"); 
	Обработчик.Комментарий = НСтр("ru = 'Обновление свойств статей финансирования.';
									|en = 'Update financing item properties.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.18.305";
	Обработчик.Процедура = "ОтражениеЗарплатыВБухучетеРасширенный.ОбновитьБухучетСотрудниковПоСписочнымКадровымДокументам";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("8894235f-c495-4aa9-aa10-22f38f2f8911"); 
	Обработчик.Комментарий = НСтр("ru = 'Обновление бухучета сотрудников.';
									|en = 'Update employee accounting.'");
	
КонецПроцедуры

Процедура ОбновитьДокументыРаспределениеОсновногоЗаработка(ПараметрыОбновления = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументыКОбработке.Ссылка КАК Ссылка,
	|	ДокументыКОбработке.Проведен КАК Проведен,
	|	ДокументыКОбработке.УдалитьФизическоеЛицо КАК ФизическоеЛицо,
	|	ДокументыКОбработке.Дата КАК Дата
	|ИЗ
	|	Документ.РаспределениеОсновногоЗаработка КАК ДокументыКОбработке
	|ГДЕ
	|	ДокументыКОбработке.УдалитьСотрудник <> ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументыКОбработке.ПериодРегистрации УБЫВ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "Документ.РаспределениеОсновногоЗаработка", "Ссылка", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		НоваяСтрока = ДокументОбъект.Сотрудники.Добавить();
		НоваяСтрока.Сотрудник = ДокументОбъект.УдалитьСотрудник;
		НоваяСтрока.ИдентификаторСтрокиСотрудника = 1;
		
		НоваяСтрока = ДокументОбъект.ФизическиеЛица.Добавить();
		НоваяСтрока.ФизическоеЛицо = Выборка.ФизическоеЛицо;
				
		Для каждого СтрокаТЧ Из ДокументОбъект.РаспределениеЗаработка Цикл
			СтрокаТЧ.ИдентификаторСтрокиСотрудника = 1;
		КонецЦикла;
		
		ДокументОбъект.УдалитьСотрудник = Справочники.Сотрудники.ПустаяСсылка();
		ДокументОбъект.УдалитьФизическоеЛицо = Справочники.ФизическиеЛица.ПустаяСсылка();
		
		ФизическиеЛица = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Выборка.ФизическоеЛицо);
		ДокументОбъект.КраткийСоставДокумента = ЗарплатаКадры.КраткийСоставСотрудниковПоСпискуФизическихЛиц(ФизическиеЛица, Выборка.Дата);
		
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);
		
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьСтатьюРасходовВБухучетНачисленияУдержанияПоСотрудникам(ПараметрыОбновления = Неопределено) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ГруппыОпераций = Новый Массив();
	ГруппыОпераций.Добавить(Перечисления.ГруппыНачисленияУдержанияВыплаты.Начислено);
	ГруппыОпераций.Добавить(Перечисления.ГруппыНачисленияУдержанияВыплаты.Удержано);
	ГруппыОпераций.Добавить(Перечисления.ГруппыНачисленияУдержанияВыплаты.Льготы);
	
	СтатьиРасходовПоСпособамРасчетов 	= ЗарплатаКадры.СтатьиРасходовПоСпособамРасчетовСФизическимиЛицами();
	СтатьяРасчетыПоОплатеТруда 			= СтатьиРасходовПоСпособамРасчетов[Перечисления.СпособыРасчетовСФизическимиЛицами.ОплатаТруда];
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГруппыОпераций", ГруппыОпераций);
	Запрос.УстановитьПараметр("СтатьяРасчетыПоОплатеТруда", СтатьяРасчетыПоОплатеТруда);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НачисленияУдержанияПоСотрудникам.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	РегистрНакопления.БухучетНачисленияУдержанияПоСотрудникам КАК НачисленияУдержанияПоСотрудникам
	|ГДЕ
	|	НачисленияУдержанияПоСотрудникам.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|	И НачисленияУдержанияПоСотрудникам.ГруппаНачисленияУдержанияВыплаты В(&ГруппыОпераций)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленияУдержанияПоСотрудникам.Регистратор КАК Регистратор,
	|	НачисленияУдержанияПоСотрудникам.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА НачисленияУдержанияПоСотрудникам.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|				И НачисленияУдержанияПоСотрудникам.ГруппаНачисленияУдержанияВыплаты В (&ГруппыОпераций)
	|			ТОГДА &СтатьяРасчетыПоОплатеТруда
	|		ИНАЧЕ НачисленияУдержанияПоСотрудникам.СтатьяРасходов
	|	КОНЕЦ КАК СтатьяРасходов,
	|	НачисленияУдержанияПоСотрудникам.*
	|ИЗ
	|	РегистрНакопления.БухучетНачисленияУдержанияПоСотрудникам КАК НачисленияУдержанияПоСотрудникам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистраторы КАК Регистраторы
	|		ПО НачисленияУдержанияПоСотрудникам.Регистратор = Регистраторы.Регистратор
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	НомерСтроки";
	
	РезультатЗапроса = Запрос.Выполнить();
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
		
		Регистратор = Выборка.Регистратор;
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрНакопления.БухучетНачисленияУдержанияПоСотрудникам.НаборЗаписей", "Регистратор", Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей = РегистрыНакопления.БухучетНачисленияУдержанияПоСотрудникам.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		
		Пока Выборка.СледующийПоЗначениюПоля("НомерСтроки") Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		КонецЦикла;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьСтатьюРасходовВСтраховыеВзносыПоФизическимЛицам(ПараметрыОбновления = Неопределено) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	СтатьиРасходовПоСпособамРасчетов 	= ЗарплатаКадры.СтатьиРасходовПоСпособамРасчетовСФизическимиЛицами();
	СтатьяРасчетыПоОплатеТруда 			= СтатьиРасходовПоСпособамРасчетов[Перечисления.СпособыРасчетовСФизическимиЛицами.ОплатаТруда];
	СтатьяРасчетыСКонтрагентами			= СтатьиРасходовПоСпособамРасчетов[Перечисления.СпособыРасчетовСФизическимиЛицами.РасчетыСКонтрагентами];
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СтатьяРасчетыПоОплатеТруда", СтатьяРасчетыПоОплатеТруда);
	Запрос.УстановитьПараметр("СтатьяРасчетыСКонтрагентами", СтатьяРасчетыСКонтрагентами);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтраховыеВзносыПоФизическимЛицам.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	РегистрНакопления.СтраховыеВзносыПоФизическимЛицам КАК СтраховыеВзносыПоФизическимЛицам
	|ГДЕ
	|	СтраховыеВзносыПоФизическимЛицам.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтраховыеВзносыПоФизическимЛицам.Регистратор КАК Регистратор,
	|	СтраховыеВзносыПоФизическимЛицам.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА СтраховыеВзносыПоФизическимЛицам.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА СтраховыеВзносыПоФизическимЛицам.Начисление ССЫЛКА Справочник.ВидыВыплатБывшимСотрудникам
	|						ТОГДА &СтатьяРасчетыСКонтрагентами
	|					КОГДА СтраховыеВзносыПоФизическимЛицам.Начисление ССЫЛКА Справочник.ВидыПрочихДоходовФизическихЛиц
	|						ТОГДА &СтатьяРасчетыСКонтрагентами
	|					ИНАЧЕ &СтатьяРасчетыПоОплатеТруда
	|				КОНЕЦ
	|		ИНАЧЕ СтраховыеВзносыПоФизическимЛицам.СтатьяРасходов
	|	КОНЕЦ КАК СтатьяРасходов,
	|	СтраховыеВзносыПоФизическимЛицам.*
	|ИЗ
	|	РегистрНакопления.СтраховыеВзносыПоФизическимЛицам КАК СтраховыеВзносыПоФизическимЛицам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистраторы КАК Регистраторы
	|		ПО СтраховыеВзносыПоФизическимЛицам.Регистратор = Регистраторы.Регистратор
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	НомерСтроки";
	
	РезультатЗапроса = Запрос.Выполнить();
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
		
		Регистратор = Выборка.Регистратор;
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрНакопления.СтраховыеВзносыПоФизическимЛицам.НаборЗаписей", "Регистратор", Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей = РегистрыНакопления.СтраховыеВзносыПоФизическимЛицам.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		
		Пока Выборка.СледующийПоЗначениюПоля("НомерСтроки") Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		КонецЦикла;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьСтатьюРасходовВБухучетНачисленияУдержанияПоКонтрагентамАкционерам(ПараметрыОбновления = Неопределено) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	СтатьиРасходовПоСпособамРасчетов 	= ЗарплатаКадры.СтатьиРасходовПоСпособамРасчетовСФизическимиЛицами();
	СтатьяРасчетыПоОплатеТруда 			= СтатьиРасходовПоСпособамРасчетов[Перечисления.СпособыРасчетовСФизическимиЛицами.ОплатаТруда];
	СтатьяРасчетыСКонтрагентами			= СтатьиРасходовПоСпособамРасчетов[Перечисления.СпособыРасчетовСФизическимиЛицами.РасчетыСКонтрагентами];
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СтатьяРасчетыПоОплатеТруда", СтатьяРасчетыПоОплатеТруда);
	Запрос.УстановитьПараметр("СтатьяРасчетыСКонтрагентами", СтатьяРасчетыСКонтрагентами);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НачисленияУдержания.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	РегистрНакопления.БухучетНачисленияУдержанияПоКонтрагентамАкционерам КАК НачисленияУдержания
	|ГДЕ
	|	НачисленияУдержания.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленияУдержания.Регистратор КАК Регистратор,
	|	НачисленияУдержания.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА НачисленияУдержания.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)
	|			ТОГДА ВЫБОР
	|					КОГДА НачисленияУдержания.НачислениеУдержание В (ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.ДивидендыСотрудников), ЗНАЧЕНИЕ(Перечисление.ВидыОсобыхНачисленийИУдержаний.НФДЛДивидендыСотрудникам))
	|						ТОГДА &СтатьяРасчетыПоОплатеТруда
	|					ИНАЧЕ &СтатьяРасчетыСКонтрагентами
	|				КОНЕЦ
	|		ИНАЧЕ НачисленияУдержания.СтатьяРасходов
	|	КОНЕЦ КАК СтатьяРасходов,
	|	НачисленияУдержания.*
	|ИЗ
	|	РегистрНакопления.БухучетНачисленияУдержанияПоКонтрагентамАкционерам КАК НачисленияУдержания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистраторы КАК Регистраторы
	|		ПО НачисленияУдержания.Регистратор = Регистраторы.Регистратор
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	НомерСтроки";
	
	РезультатЗапроса = Запрос.Выполнить();
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
		
		Регистратор = Выборка.Регистратор;
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрНакопления.БухучетНачисленияУдержанияПоКонтрагентамАкционерам.НаборЗаписей", "Регистратор", Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей = РегистрыНакопления.БухучетНачисленияУдержанияПоКонтрагентамАкционерам.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		
		Пока Выборка.СледующийПоЗначениюПоля("НомерСтроки") Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		КонецЦикла;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;

КонецПроцедуры

// Удаление роли из пользовательских профилей.
//
Процедура УдалитьРольОтражениеЗарплатыВБухгалтерскомУчете() Экспорт
	
	ИмяРоли = "? ОтражениеЗарплатыВБухгалтерскомУчете";
	МассивРолей = Новый Массив;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.БухучетХозрасчетныхОрганизаций") Тогда
		МассивРолей.Добавить("ДобавлениеИзменениеОтраженияЗарплатыВБухучетеХозрасчетныхОрганизаций");
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.УчетБюджетныхУчреждений") Тогда
		МассивРолей.Добавить("ДобавлениеИзменениеОтраженияЗарплатыВБухучетеБюджетныхУчреждений");
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОценочныеОбязательстваЗарплатаКадры") Тогда
		МассивРолей.Добавить("ДобавлениеИзменениеОценочныхОбязательствЗарплатаКадры");
		МассивРолей.Добавить("ЧтениеНастроекОценочныхОбязательствЗарплатаКадры");
	КонецЕсли;
	
	СоответствиеРолей = Новый Соответствие;
	СоответствиеРолей.Вставить(ИмяРоли, МассивРолей);
	УправлениеДоступом.ЗаменитьРолиВПрофилях(СоответствиеРолей);

КонецПроцедуры

Процедура ОбновитьСтратегиюБухучетаСдельногоЗаработка(ПараметрыОбновления = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.Ссылка КАК Ссылка
	|ИЗ
	|	ПланВидовРасчета.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.СдельнаяОплатаТруда)
	|	И Начисления.СтратегияОтраженияВУчете <> ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоСдельномуЗаработку)";
	
	РезультатЗапроса = Запрос.Выполнить();
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;

	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "ПланВидовРасчета.Начисления", "Ссылка", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		НачислениеОбъект = Выборка.Ссылка.ПолучитьОбъект();
		НачислениеОбъект.СтратегияОтраженияВУчете = Перечисления.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоСдельномуЗаработку;
		
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(НачислениеОбъект);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСтатьюРасходовОплатаДнейУходаЗаДетьмиИнвалидами(ПараметрыОбновления = Неопределено) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Начисления.Ссылка КАК Ссылка
	|ИЗ
	|	ПланВидовРасчета.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.КатегорияНачисленияИлиНеоплаченногоВремени В (ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаДнейУходаЗаДетьмиИнвалидами), ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ДенежноеСодержаниеОплатаДнейУходаЗаДетьмиИнвалидами))
	|	И Начисления.СтатьяРасходов = ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка)";
	РезультатЗапроса = Запрос.Выполнить();
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Статья266 = СтатьяРасходов266();
	Если Не ЗначениеЗаполнено(Статья266) Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "ПланВидовРасчета.Начисления", "Ссылка", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		НачислениеОбъект = Выборка.Ссылка.ПолучитьОбъект();
		НачислениеОбъект.СтатьяРасходов = Статья266;
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(НачислениеОбъект);
		
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;

КонецПроцедуры

Процедура СтатьиФинансированияЗаполнитьИспользованиеВБазеСреднегоЗаработка(ПараметрыОбновления = Неопределено) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатьиФинансирования.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СтатьиФинансированияЗарплата КАК СтатьиФинансирования
	|ГДЕ
	|	СтатьиФинансирования.ИспользованиеВБазеСреднегоЗаработка = ЗНАЧЕНИЕ(Перечисление.ИспользованиеСтатьиФинансированияВБазеСреднегоЗаработка.ПустаяСсылка)";
	РезультатЗапроса = Запрос.Выполнить();
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "Справочник.СтатьиФинансированияЗарплата", "Ссылка", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		СтатьяОбъект = Выборка.Ссылка.ПолучитьОбъект();
		СтатьяОбъект.ИспользованиеВБазеСреднегоЗаработка = Перечисления.ИспользованиеСтатьиФинансированияВБазеСреднегоЗаработка.Используется;
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СтатьяОбъект);
		
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;

КонецПроцедуры

Процедура ОбновитьБухучетСотрудниковПоСписочнымКадровымДокументам(ПараметрыОбновления = Неопределено) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ШаблонТекстаЗапроса = 
	"ВЫБРАТЬ
	|	БухучетЗарплатыСотрудников.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ ВТДокументыКОбработке
	|ИЗ
	|	РегистрСведений.БухучетЗарплатыСотрудников КАК БухучетЗарплатыСотрудников
	|ГДЕ
	|	&УсловиеДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.Сотрудник КАК Сотрудник,
	|	ДокументыКОбработке.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	#ИмяТаблицы КАК Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыКОбработке КАК ДокументыКОбработке
	|		ПО Таблица.Ссылка = ДокументыКОбработке.ДокументОснование
	|			И (&УсловиеНеРегистрироватьБухучет)";
	
	ТекстЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "&УсловиеДокументСсылка", "БухучетЗарплатыСотрудников.ДокументОснование ССЫЛКА Документ.КадровыйПереводСписком");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяТаблицы", "Документ.КадровыйПереводСписком.Сотрудники");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеНеРегистрироватьБухучет", "НЕ Таблица.ИзменитьПодразделениеИДолжность");
	
	Запрос.Текст = ТекстЗапроса;
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	
	ПараметрыБухучета = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба.КадровыйУчетВоеннослужащих") Тогда 
		Модуль = ОбщегоНазначения.ОбщийМодуль("КадровыйУчетВоеннослужащих");
		ПараметрыБухучета = Модуль.КадровыйПереводСпискомПараметрыОбновленияБухучетаСотрудников();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыБухучета) Тогда
		
		УсловиеДокументСсылка = СтрШаблон("%1 %2", "БухучетЗарплатыСотрудников.ДокументОснование ССЫЛКА", ПараметрыБухучета.ИмяДокументСсылка);
		УсловиеНеРегистрироватьБухучет = СтрШаблон("%1 %2", "НЕ", ПараметрыБухучета.ИмяПоляРегистрироватьБухучет);
		
		ТекстЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "&УсловиеДокументСсылка", УсловиеДокументСсылка);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяТаблицы", ПараметрыБухучета.ИмяТаблицы);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеНеРегистрироватьБухучет", УсловиеНеРегистрироватьБухучет);
		
		Запрос.Текст = ТекстЗапроса;
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Запрос.Выполнить().Выгрузить(), ТаблицаДанных);
		
	КонецЕсли;
	
	Если ТаблицаДанных.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаТЗ Из ТаблицаДанных Цикл
	
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрСведений.БухучетЗарплатыСотрудников", "ДокументОснование", СтрокаТЗ.ДокументОснование) Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.БухучетЗарплатыСотрудников.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ДокументОснование.Установить(СтрокаТЗ.ДокументОснование);
		НаборЗаписей.Отбор.Сотрудник.Установить(СтрокаТЗ.Сотрудник);
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецОбласти


