
#Область СлужебныйПрограммныйИнтерфейс

Функция ВсеПроверки() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыКонтроляДокументов.Ссылка КАК ВидКонтроля
	|ИЗ
	|	ПланВидовХарактеристик.ВидыКонтроляДокументов КАК ВидыКонтроляДокументов
	|ГДЕ
	|	ВидыКонтроляДокументов.РежимКонтроля = ЗНАЧЕНИЕ(Перечисление.РежимыКонтроляДокументов.Блокировать)
	|	ИЛИ ВидыКонтроляДокументов.РежимКонтроля = ЗНАЧЕНИЕ(Перечисление.РежимыКонтроляДокументов.Информировать)
	|	ИЛИ ВидыКонтроляДокументов.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыКонтроляДокументов.КонтрольБюджетныхЛимитов)
	|	ИЛИ ВидыКонтроляДокументов.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыКонтроляДокументов.КонтрольБюджетныхРезервов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВидыКонтроляДокументов.Код";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	ПВХ = ПланыВидовХарактеристик.ВидыКонтроляДокументов;
	ОбщиеМодулиКонтроля = Новый Соответствие;
	ОбщиеМодулиКонтроля.Вставить(ПВХ.КонтрольБюджетныхЛимитов, КонтрольУХБюджетныеЛимиты);
	ОбщиеМодулиКонтроля.Вставить(ПВХ.КонтрольБюджетныхРезервов, КонтрольУХБюджетныеРезервы);
	ОбщиеМодулиКонтроля.Вставить(ПВХ.КонтрольОбщейЗадолженностиКонтрагента, КонтрольУХВзаиморасчетыПоКонтрагенту);
	ОбщиеМодулиКонтроля.Вставить(ПВХ.КонтрольМаксимальнойСуммыЗадолженностиПоДоговору, КонтрольУХВзаиморасчетыПоДоговору);
	ОбщиеМодулиКонтроля.Вставить(ПВХ.КонтрольРасходовПоДоговору, КонтрольУХРасходыПоДоговору);
	
	КонтрольУХВстраивание.ВсеПроверки(ОбщиеМодулиКонтроля);
	
	Результат = Новый Массив;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбщийМодуль = ОбщиеМодулиКонтроля[Выборка.ВидКонтроля];
		Если ОбщийМодуль <> неопределено Тогда
			Результат.Добавить(ОбщийМодуль);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция РазмещениеИсточниковКонтроля() Экспорт
	
	Результат = Новый Соответствие;
	
	КонтрольУХВстраивание.РазмещениеИсточниковКонтроля(Результат);
	
	Возврат Результат;
	
КонецФункции

// Возвращает массив зарегистрированных проверок для типа документа
//
// Параметры:
//  ИмяДокументаСсылка  - Строка - Имя из метаданных документа, для которого требуется получить список проверок
//
// Возвращаемое значение:
//   Массив   - массив структур с типом ПроверкаДокумента
//
Функция ЗарегистрированныеПроверки(ИмяДокумента = неопределено) Экспорт

	Результат = Новый Массив;
	Для Каждого Проверка Из КонтрольУХПовтИсп.ВсеПроверки() Цикл
		Если Проверка.ТребуетсяДляДокумента(ИмяДокумента) Тогда
			Результат.Добавить(Проверка.Создать());
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
	
КонецФункции // ЗарегистрированныеПроверки()

// Возвращает ОбъектПроверка по ПВХ.ВидыКонтроляДокументов
Функция ПроверкаПоВидуКонтроля(ВидКонтроля) Экспорт
	
	Для Каждого Проверка Из КонтрольУХПовтИсп.ВсеПроверки() Цикл
		ОбъектПроверка = Проверка.Создать();
		
		Если ОбъектПроверка.ВидКонтроля = ВидКонтроля Тогда
			Возврат ОбъектПроверка;
		КонецЕсли;
		
		ОбъектПроверка = неопределено;
		
	КонецЦикла;
	
	Возврат неопределено;
	
КонецФункции

#КонецОбласти

