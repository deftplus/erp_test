
#Область ПрограммныйИнтерфейс

// Зарегистрировать исходящее сообщение при записи объекта.
// 
// Параметры:
//  Источник - Объект - источник регистрации события
//  Отказ - Булево - флаг отказа
//
Процедура ЗарегистрироватьИсходящееСообщениеПриЗаписиОбъекта(Источник, Отказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	// Регистрация событий возможна только при включении использования подсистемы.
	Если Не СП_СхемыДанных.ПодсистемаВключена() Тогда
		Возврат;
	КонецЕсли;
	
	// Загружаемые данные повторно не регистрируем.
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если Источник.ДополнительныеСвойства.Свойство("СП_ЭтоЗагрузкаДанных") Тогда
		Возврат;
	КонецЕсли;
	
	// Не регистрируем служебные справочники.
	ТипИсточника = ТипЗнч(Источник);
	Если ТипИсточника = Тип("СправочникОбъект.СП_ОбъектыОбмена") Тогда
		Возврат;
	КонецЕсли;
	
	СП_РаботаССообщениями.ЗарегистрироватьИсходящееСообщение(Источник.Ссылка);
	
КонецПроцедуры

// Зарегистрировать исходящее сообщение при записи регистра.
// 
// Параметры:
//  Источник - НаборЗаписей - источник регистрации события
//  Отказ - Булево - флаг отказа
//  Замещение - Булево - флаг замещения записей
//
Процедура ЗарегистрироватьИсходящееСообщениеПриЗаписиРегистра(Источник, Отказ, Замещение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	// Регистрация событий возможна только при включении использования подсистемы.
	Если Не СП_СхемыДанных.ПодсистемаВключена() Тогда
		Возврат;
	КонецЕсли;
	
	// Загружаемые данные повторно не регистрируем.
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если Источник.ДополнительныеСвойства.Свойство("СП_ЭтоЗагрузкаДанных") Тогда
		Возврат;
	КонецЕсли;
	
	СП_РаботаССообщениями.ЗарегистрироватьИсходящееСообщение(Источник);
	
КонецПроцедуры

#КонецОбласти
