
#Область ПрограммныйИнтерфейс

// Создает платежные документы, либо формирует данные заполнения для создания одного платежного документа.
//
// Параметры:
//    СтрокиГрафика - Массив - Ключи записей графика платежей
//    ТипДокумента - Строка - Тип создаваемых документов. Если не задан, будет определен автоматически.
//
// Возвращаемое значение:
//    Структура:
//        ОткрыватьФормуПомощника - Булево - Признак необходимости открытия формы помощника создания документов
//        ДокументКСозданию - Структура - Данные заполнения единственного документа
//        ДлительнаяОперация - Структура - Длительная операция создания нескольких документов
//        АдресСтрокГрафика - Строка - Адрес временного хранилища, в котором содержатся оплачиваемые строки графика.
//
Функция ОплатитьСтрокиГрафика(СтрокиГрафика, ТипДокумента) Экспорт
	
	//++ Локализация
	//++ НЕ УТ
	Если ПолучитьФункциональнуюОпцию("ПоддержкаПлатежейВСоответствииС275ФЗ") Тогда
		Если ТребуетсяКонтрольГОЗ(СтрокиГрафика) Тогда
			Результат = Новый Структура;
			Результат.Вставить("ОткрыватьФормуПомощника", Истина);
			Результат.Вставить("ОткрыватьФормуПомощникаНемедленно", Истина);
			Результат.Вставить("ТребуетсяКонтрольГОЗ", Истина);
			Результат.Вставить("АдресСтрокГрафика", ПоместитьВоВременноеХранилище(СтрокиГрафика));
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	//-- НЕ УТ
	//-- Локализация
	Возврат Неопределено;
	
КонецФункции

// Формирует варианты наименования юридического лица в соответствии с его организационно-правовой формой.
//
// Параметры:
//    Наименование - Строка - Наименование юр. лица.
//
// Возвращаемое значение:
//    Структура - Наименование, РабочееНаименование, ПолноеНаименование.
//
Функция НаименованиеОрганизации(Знач Наименование) Экспорт
	
	Наименование = СокрЛП(Наименование);
	
	СтруктураНаименования = Новый Структура("Наименование, СокращенноеНаименование, ПолноеНаименование",
		Наименование, Наименование, Наименование);
	
	//++ Локализация
	Если УбратьИзНаименованияОрганизационнуюФорму(Наименование, НСтр("ru = 'ООО';
																	|en = 'LLC'")) Тогда
		ОрганизационноПравоваяФорма				= НСтр("ru = 'Общество с ограниченной ответственностью';
															|en = 'Limited liability company'");
		ОрганизационноПравоваяФормаСокращенно	= НСтр("ru = 'ООО';
														|en = 'LLC'");
	ИначеЕсли УбратьИзНаименованияОрганизационнуюФорму(Наименование, НСтр("ru = 'ОАО';
																			|en = 'JSC'")) Тогда
		ОрганизационноПравоваяФорма				= НСтр("ru = 'Открытое акционерное общество';
															|en = 'Open Joint-Stock Company'");
		ОрганизационноПравоваяФормаСокращенно	= НСтр("ru = 'ОАО';
														|en = 'JSC'");
	ИначеЕсли УбратьИзНаименованияОрганизационнуюФорму(Наименование, НСтр("ru = 'ЗАО';
																			|en = 'CJSC'")) Тогда
		ОрганизационноПравоваяФорма				= НСтр("ru = 'Закрытое акционерное общество';
															|en = 'Closed Joint-Stock Company'");
		ОрганизационноПравоваяФормаСокращенно	= НСтр("ru = 'ЗАО';
														|en = 'CJSC'");
	ИначеЕсли УбратьИзНаименованияОрганизационнуюФорму(Наименование, НСтр("ru = 'ИП';
																			|en = 'IE'")) Тогда
		ОрганизационноПравоваяФорма				= НСтр("ru = 'Индивидуальный предприниматель';
															|en = 'Individual entrepreneur'");
		ОрганизационноПравоваяФормаСокращенно	= НСтр("ru = 'ИП';
														|en = 'IE'");
	ИначеЕсли УбратьИзНаименованияОрганизационнуюФорму(Наименование, НСтр("ru = 'Общество с ограниченной ответственностью';
																			|en = 'Limited liability company'")) Тогда
		ОрганизационноПравоваяФорма				= НСтр("ru = 'Общество с ограниченной ответственностью';
															|en = 'Limited liability company'");
		ОрганизационноПравоваяФормаСокращенно	= НСтр("ru = 'ООО';
														|en = 'LLC'");
	ИначеЕсли УбратьИзНаименованияОрганизационнуюФорму(Наименование, НСтр("ru = 'Открытое акционерное общество';
																			|en = 'Open Joint-Stock Company'")) Тогда
		ОрганизационноПравоваяФорма				= НСтр("ru = 'Открытое акционерное общество';
															|en = 'Open Joint-Stock Company'");
		ОрганизационноПравоваяФормаСокращенно	= НСтр("ru = 'ОАО';
														|en = 'JSC'");
	ИначеЕсли УбратьИзНаименованияОрганизационнуюФорму(Наименование, НСтр("ru = 'Закрытое акционерное общество';
																			|en = 'Closed Joint-Stock Company'")) Тогда
		ОрганизационноПравоваяФорма				= НСтр("ru = 'Закрытое акционерное общество';
															|en = 'Closed Joint-Stock Company'");
		ОрганизационноПравоваяФормаСокращенно	= НСтр("ru = 'ЗАО';
														|en = 'CJSC'");
	ИначеЕсли УбратьИзНаименованияОрганизационнуюФорму(Наименование, НСтр("ru = 'Индивидуальный предприниматель';
																			|en = 'Individual entrepreneur'")) Тогда
		ОрганизационноПравоваяФорма				= НСтр("ru = 'Индивидуальный предприниматель';
															|en = 'Individual entrepreneur'");
		ОрганизационноПравоваяФормаСокращенно	= НСтр("ru = 'ИП';
														|en = 'IE'");
	Иначе
		ОрганизационноПравоваяФорма				= "";
		ОрганизационноПравоваяФормаСокращенно	= "";
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ОрганизационноПравоваяФорма) Тогда
		КоличествоКавычек	= СтрЧислоВхождений(Наименование, """");
		Если КоличествоКавычек > 1 Тогда
			// Наименование организации внутри внешних кавычек
			ПозицияПервойКавычки = СтрНайти(Наименование, """");
			ПозицияПоследнейКавычки = 0;
			
			ВремНаименование = Наименование;
			
			ПозицияКавычки = ПозицияПервойКавычки;
			Пока ПозицияКавычки > 0 Цикл
				ПозицияПоследнейКавычки = ПозицияПоследнейКавычки + ПозицияКавычки;
				ВремНаименование = Сред(ВремНаименование, ПозицияКавычки + 1);
				ПозицияКавычки = СтрНайти(ВремНаименование, """");
			КонецЦикла;
			
			Наименование = Сред(Наименование, ПозицияПервойКавычки + 1, ПозицияПоследнейКавычки - ПозицияПервойКавычки - 1);
			Если НЕ КоличествоКавычек%2 = 0 Тогда
				Наименование = Наименование + """";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ОрганизационноПравоваяФорма) Тогда
		ПолноеНаименование = Наименование;
	Иначе
		ПолноеНаименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 ""%2""",
			ОрганизационноПравоваяФорма, Наименование);
	КонецЕсли;
	
	СокращенноеНаименование = Наименование + ?(ПустаяСтрока(ОрганизационноПравоваяФормаСокращенно), "", " ")
		+ ОрганизационноПравоваяФормаСокращенно;
	
	СтруктураНаименования = Новый Структура("Наименование, СокращенноеНаименование, ПолноеНаименование",
		Наименование, СокращенноеНаименование, ПолноеНаименование);
	//-- Локализация
	
	Возврат СтруктураНаименования;
	
КонецФункции

// Функция получает валюту, соответствующую номеру банковского счета.
// 6-8 разряды номера банковского счета соответствуют коду валюты.
// Для рублей используется код 810.
//
// Параметры:
//	НомерСчета - Число - Номер банковского счета.
//
// Возвращаемое значение:
//	СправочникСсылка.Валюты - Валюта банковского счета.
//
Функция ПолучитьВалютуПоНомеруСчета(НомерСчета) Экспорт
	
	Валюта = Справочники.Валюты.ПустаяСсылка();
	
	//++ Локализация
	УстановитьПривилегированныйРежим(Истина);
	
	КодВалюты = Сред(НомерСчета, 6, 3);
	Если КодВалюты = "810" Тогда
		КодВалюты = "643";
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Валюты.Ссылка КАК Валюта
	|ИЗ
	|	Справочник.Валюты КАК Валюты
	|ГДЕ
	|	Валюты.Код = &КодВалюты
	|");
	Запрос.УстановитьПараметр("КодВалюты", КодВалюты);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Валюта = Выборка.Валюта;
	КонецЕсли;
	//-- Локализация
	
	Возврат Валюта;
	
КонецФункции

//++ Локализация

#Область ИнтерфейсныеЭлементы

// Функция формирует массив хозяйственных операций, для которых доступно перечисление в бюджет.
//
// Возвращаемое значение:
//	Массив - массив хозяйственных операций.
//
Функция МассивОперацийПеречисленияВБюджет() Экспорт
	
	МассивОпераций = Новый Массив;
	МассивОпераций.Добавить(Перечисления.ХозяйственныеОперации.ОплатаПоставщику);
	МассивОпераций.Добавить(Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту);
	МассивОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПеречислениеТаможне);
	МассивОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет);
	МассивОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств);
	МассивОпераций.Добавить(Перечисления.ХозяйственныеОперации.ОплатаПоКредитам);
	//++ НЕ УТ
	МассивОпераций.Добавить(Перечисления.ХозяйственныеОперации.ОплатаАрендодателю);
	//-- НЕ УТ
	
	Возврат МассивОпераций;
	
КонецФункции

// Процедура устанавливает видимость выбора операции оплаты таможенного платежа в форме.
//
// Параметры:
//	Поле - ПолеФормы - Поле формы для выбора хозяйственной операции.
//
Процедура УстановитьВидимостьОперацииПеречислениеТаможне(Поле) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьИмпортныеЗакупки") Тогда
		ЭлементСписка = Поле.СписокВыбора.НайтиПоЗначению(Перечисления.ХозяйственныеОперации.ПеречислениеТаможне);
		Если ЭлементСписка <> Неопределено Тогда
			Поле.СписокВыбора.Удалить(ЭлементСписка);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура устанавливает видимость выбора операций выдачи и погашения займов сотрудникам в форме.
//
// Параметры:
//	Поле - ПолеФормы - Поле формы для выбора хозяйственной операции.
//
Процедура УстановитьВидимостьОперацийЗаймамСотрудникам(Поле) Экспорт
	
	ОперацииПоЗаймамДоступны = Не ПолучитьФункциональнуюОпцию("УправлениеТорговлей");
	
	//++ НЕ УТ
	ОперацииПоЗаймамДоступны = ПолучитьФункциональнуюОпцию("ИспользоватьЗаймыСотрудникам")
		И ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплаты");
	//-- НЕ УТ
	
	Если Не ОперацииПоЗаймамДоступны Тогда
		ЭлементСписка = Поле.СписокВыбора.НайтиПоЗначению(Перечисления.ХозяйственныеОперации.ВыдачаЗаймаСотруднику);
		Если ЭлементСписка <> Неопределено Тогда
			Поле.СписокВыбора.Удалить(ЭлементСписка);
		КонецЕсли;
		ЭлементСписка = Поле.СписокВыбора.НайтиПоЗначению(Перечисления.ХозяйственныеОперации.ПогашениеЗаймаСотрудником);
		Если ЭлементСписка <> Неопределено Тогда
			Поле.СписокВыбора.Удалить(ЭлементСписка);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Подмена префикса организации префиксом кассовой книги
//
// Параметры:
//    Источник - ДокументОбъект - ПКО, РКО, Кассовая книга
//    СтандартнаяОбработка - Булево - флаг стандартной обработки подписки
//    Префикс - Строка - префикс объекта, который нужно изменить.
//
Процедура УстановитьПрефиксКассовойКнигиНомеруДокументаПриУстановкеНовогоНомера(Источник, СтандартнаяОбработка, Префикс) Экспорт
	
	РеквизитыПрефиксации = "";
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.ЛистКассовойКниги")
		Или ТипЗнч(Источник) = Тип("ДокументОбъект.ИнвентаризацияНаличныхДенежныхСредств") Тогда
		РеквизитыПрефиксации = ПолучитьПрефиксациюКассыКассовойКниги(Источник.КассоваяКнига);
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ПриходныйКассовыйОрдер")
		Или ТипЗнч(Источник) = Тип("ДокументОбъект.РасходныйКассовыйОрдер") Тогда
		РеквизитыПрефиксации = ПолучитьПрефиксациюКассыКассовойКниги(Источник.Касса);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыПрефиксации)
		И ЗначениеЗаполнено(РеквизитыПрефиксации.ИспользоватьПрефикс)
		И РеквизитыПрефиксации.ИспользоватьПрефикс Тогда
		
		ПрефиксКассовойКниги = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(РеквизитыПрефиксации.Префикс, 2, "0", "Слева");
		Префикс = ПрефиксКассовойКниги + Прав(Префикс, СтрДлина(Префикс) - 2);
	КонецЕсли;
	
КонецПроцедуры

// Сброс номера документа при необходимости его изменения
//
// Параметры:
//    Источник - ДокументОбъект - ПКО, РКО, Кассовая книга
//    Отказ - Булево - флаг отказа
//    РежимЗаписи - РежимЗаписиДокумента - Режим записи
//    РежимПроведения - РежимПроведенияДокумента - Режим проведения.
// 
Процедура ПроверитьНомерДокументаПоДатеОрганизацииКассовойКнигеПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	ИначеЕсли Источник.ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	ПрефиксацияОбъектовСобытия.ПроверитьНомерДокументаПоДатеИОрганизации(Источник, Отказ, РежимЗаписи, РежимПроведения);
	
	РеквизитыПрефиксацииДоИзменения = "";
	РеквизитыПрефиксацииПослеИзменения = "";
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.ЛистКассовойКниги")
		Или ТипЗнч(Источник) = Тип("ДокументОбъект.ИнвентаризацияНаличныхДенежныхСредств") Тогда
		
		КассоваяКнигаСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка, "КассоваяКнига");
		РеквизитыПрефиксацииДоИзменения = ПолучитьПрефиксациюКассыКассовойКниги(КассоваяКнигаСсылка);
		
		РеквизитыПрефиксацииПослеИзменения = ПолучитьПрефиксациюКассыКассовойКниги(Источник.КассоваяКнига);
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ПриходныйКассовыйОрдер")
		Или ТипЗнч(Источник) = Тип("ДокументОбъект.РасходныйКассовыйОрдер") Тогда
		
		КассаСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка, "Касса");
		РеквизитыПрефиксацииДоИзменения = ПолучитьПрефиксациюКассыКассовойКниги(КассаСсылка);
		
		РеквизитыПрефиксацииПослеИзменения = ПолучитьПрефиксациюКассыКассовойКниги(Источник.Касса);
	КонецЕсли;
	
	ПрефиксДоИзменения = ?(
		ЗначениеЗаполнено(РеквизитыПрефиксацииДоИзменения)
		И ЗначениеЗаполнено(РеквизитыПрефиксацииДоИзменения.ИспользоватьПрефикс)
		И РеквизитыПрефиксацииДоИзменения.ИспользоватьПрефикс,
		РеквизитыПрефиксацииДоИзменения.Префикс,
		"");
		
	ПрефиксПослеИзменения = ?(
		ЗначениеЗаполнено(РеквизитыПрефиксацииПослеИзменения)
		И ЗначениеЗаполнено(РеквизитыПрефиксацииПослеИзменения.ИспользоватьПрефикс)
		И РеквизитыПрефиксацииПослеИзменения.ИспользоватьПрефикс,
		РеквизитыПрефиксацииПослеИзменения.Префикс,
		"");
	
	Если ПрефиксДоИзменения <> ПрефиксПослеИзменения Тогда
		Источник.Номер = "";
	КонецЕсли;
	
КонецПроцедуры

//++ НЕ УТ

// Устанавливает параметры выбора договора с заказчиком в рамках кооперации ГОЗ
//
// Параметры:
//    Элемент - ПолеФормы - Поле ввода договора.
//    ДополнительныеПараметры - Структура - Набор дополнительных параметров
//
Процедура УстановитьПараметрыВыбораДоговораСЗаказчиком(Элемент, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыВыбораДоговора = Новый Массив;
	
	ПараметрыВыбораДоговора.Добавить(Новый ПараметрВыбора("Отбор.ПлатежиПо275ФЗ", Истина));
	ПараметрыВыбораДоговора.Добавить(Новый ПараметрВыбора("Отбор.ДоговорСУчастникомГОЗ", Истина));
	ПараметрыВыбораДоговора.Добавить(Новый ПараметрВыбора("Отбор.ПометкаУдаления", Ложь));
	ПараметрыВыбораДоговора.Добавить(Новый ПараметрВыбора("Отбор.ПодходитДляПодбораВДоговорЗаказчика", Истина));
	
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
		Для Каждого ДополнительныйПараметр Из ДополнительныеПараметры Цикл 
			ПараметрыВыбораДоговора.Добавить(Новый ПараметрВыбора("Отбор." + ДополнительныйПараметр.Ключ, ДополнительныйПараметр.Значение));
		КонецЦикла;
	КонецЕсли;
	
	Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораДоговора);
	
КонецПроцедуры
//-- НЕ УТ

#КонецОбласти

#Область ЗаполнениеДокументов

//++ НЕ УТ

// Процедура заполняет таблицу на основании ведомостей различных типов оплаты в соответствии с отборами.
//
// Параметры:
//	ТаблицаВедомостей - ТаблицаЗначений, ТабличнаяЧасть - Заполняемая таблица
//	СтруктураПараметров - Структура - Структура отборов
//	ТаблицаРаботников - ТаблицаЗначений, ТабличнаяЧасть - Таблица, в которую помещаются работники первой ведомости.
//
Процедура ЗаполнитьПоВедомостямКОплате(ТаблицаВедомостей, СтруктураПараметров, ТаблицаРаботников = Неопределено) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ИнтеграцияБЗК.ДанныеОбОплатеВедомостей(МенеджерВременныхТаблиц, СтруктураПараметров);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТВедомости.Ведомость КАК Ведомость,
	|	МАКСИМУМ(ВТВедомости.Сумма) КАК Сумма,
	|	МАКСИМУМ(ВТВедомости.СуммаПоДокументу) КАК СуммаПоДокументу
	|ИЗ
	|	ДанныеВедомостейНаОплату КАК ВТВедомости
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТВедомости.Ведомость";
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаВедомостей.Добавить();
		НоваяСтрока.Ведомость = Выборка.Ведомость;
		НоваяСтрока.СтатьяДвиженияДенежныхСредств =
			Справочники.СтатьиДвиженияДенежныхСредств.СтатьяДвиженияДенежныхСредствПоХозяйственнойОперации(СтруктураПараметров.ХозяйственнаяОперация);
		
		Если СтруктураПараметров.Свойство("ЗаполнятьСуммуПоДокументу") Тогда
			НоваяСтрока.Сумма = Выборка.СуммаПоДокументу;
		Иначе
			НоваяСтрока.Сумма = Выборка.Сумма;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТаблицаВедомостей.Количество() > 0 И ТаблицаРаботников <> Неопределено Тогда
		СтруктураПараметров.Вставить(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТаблицаВедомостей[0].Ведомость));
		СуммыКОплате = ДанныеРаботниковПоВедомостям(СтруктураПараметров);
		
		Если ТипЗнч(СуммыКОплате) = Тип("ТаблицаЗначений") Тогда
			ТаблицаРаботников.Загрузить(СуммыКОплате);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполняет таблицу на основании ведомостей на основании заявок на расходование денежных средств.
//
// Параметры:
//	ТаблицаВедомостей - ТаблицаЗначений, ТабличнаяЧасть - Заполняемая таблица
//	СтруктураПараметров - Структура - Структура отборов.
//
Процедура ЗаполнитьПоВедомостямИЗаявкамКОплате(ТаблицаВедомостей, СтруктураПараметров) Экспорт
	
	Запрос = Новый Запрос;
	
	Если СтруктураПараметров.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыПоЗарплатномуПроекту Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаявкаВедомости.Ведомость,
		|	ЗаявкаВедомости.СтатьяДвиженияДенежныхСредств,
		|	ЗаявкаВедомости.Ссылка КАК ЗаявкаНаРасходованиеДенежныхСредств,
		|	ЗаявкаВедомости.Сумма
		|ИЗ
		|	Документ.ЗаявкаНаРасходованиеДенежныхСредств.РасшифровкаПлатежа КАК ЗаявкаВедомости
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК СписаниеВедомости
		|		ПО (ЗаявкаВедомости.Ведомость = СписаниеВедомости.Ведомость
		|		И СписаниеВедомости.Ссылка <> &Списание
		|		И СписаниеВедомости.Ссылка.Проведен)
		|ГДЕ
		|	ЗаявкаВедомости.Ссылка В(&Заявка)
		|	И ЗаявкаВедомости.Ссылка.ХозяйственнаяОперация = &ХозяйственнаяОперация
		|	И СписаниеВедомости.Ведомость ЕСТЬ NULL ";
		
		Запрос.УстановитьПараметр("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВыплатаЗарплаты);
		Запрос.УстановитьПараметр("Заявка", СтруктураПараметров.Заявка);
		Запрос.УстановитьПараметр("Списание", СтруктураПараметров.Списание);
		
		
	ИначеЕсли СтруктураПараметров.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыНаЛицевыеСчета Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиКВыплате.ЗаявкаНаРасходованиеДенежныхСредств КАК Ведомость,
		|	РасшифровкаПлатежаСписаниеБДС.ЗаявкаНаРасходованиеДенежныхСредств КАК ЗаявкаНаРасходованиеДенежныхСредств,
		|	СУММА(ОстаткиКВыплате.СуммаОстаток) КАК Сумма
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваКВыплате.Остатки(, ЗаявкаНаРасходованиеДенежныхСредств ССЫЛКА Документ.ВедомостьНаВыплатуЗарплатыПеречислением) КАК ОстаткиКВыплате
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВедомостьНаВыплатуЗарплатыПеречислением.Зарплата КАК Ведомости
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОплатаВедомостейНаВыплатуЗарплаты КАК Оплата
		|			ПО Ведомости.Сотрудник.ФизическоеЛицо = Оплата.ФизическоеЛицо
		|				И Ведомости.Ссылка = Оплата.Ведомость
		|		ПО ОстаткиКВыплате.ЗаявкаНаРасходованиеДенежныхСредств = Ведомости.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК РасшифровкаПлатежаСписаниеБДС
		|		ПО ОстаткиКВыплате.ЗаявкаНаРасходованиеДенежныхСредств = РасшифровкаПлатежаСписаниеБДС.Ведомость
		|ГДЕ
		|	Оплата.Ведомость ЕСТЬ NULL
		|	И Ведомости.БанковскийСчет = &ЛицевойСчет
		|	И Ведомости.Ссылка.Организация = &Организация
		|
		|СГРУППИРОВАТЬ ПО
		|	РасшифровкаПлатежаСписаниеБДС.ЗаявкаНаРасходованиеДенежныхСредств,
		|	ОстаткиКВыплате.ЗаявкаНаРасходованиеДенежныхСредств
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДенежныеСредстваКВыплате.ЗаявкаНаРасходованиеДенежныхСредств,
		|	РасшифровкаПлатежаСписаниеБДС.ЗаявкаНаРасходованиеДенежныхСредств,
		|	СУММА(-ДенежныеСредстваКВыплате.Сумма)
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваКВыплате КАК ДенежныеСредстваКВыплате
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВедомостьНаВыплатуЗарплатыПеречислением.Зарплата КАК Ведомости
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБезналичныхДенежныхСредств
		|			ПО Ведомости.Ссылка.Организация = СписаниеБезналичныхДенежныхСредств.Организация
		|				И Ведомости.БанковскийСчет = СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента
		|		ПО ДенежныеСредстваКВыплате.ЗаявкаНаРасходованиеДенежныхСредств = Ведомости.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК РасшифровкаПлатежаСписаниеБДС
		|		ПО ДенежныеСредстваКВыплате.ЗаявкаНаРасходованиеДенежныхСредств = РасшифровкаПлатежаСписаниеБДС.Ведомость
		|ГДЕ
		|	ДенежныеСредстваКВыплате.Регистратор = &Списание
		|	И СписаниеБезналичныхДенежныхСредств.Ссылка = &Списание
		|
		|СГРУППИРОВАТЬ ПО
		|	РасшифровкаПлатежаСписаниеБДС.ЗаявкаНаРасходованиеДенежныхСредств,
		|	ДенежныеСредстваКВыплате.ЗаявкаНаРасходованиеДенежныхСредств";
		
		Запрос.УстановитьПараметр("Организация", СтруктураПараметров.Организация);
		Запрос.УстановитьПараметр("ЛицевойСчет", СтруктураПараметров.ЛицевойСчет);
		Запрос.УстановитьПараметр("Списание", СтруктураПараметров.Списание);
		
		
	ИначеЕсли СтруктураПараметров.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыЧерезКассу
		Или СтруктураПараметров.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыРаздатчиком Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа.Ведомость
		|ПОМЕСТИТЬ ВТ_Ведомости
		|ИЗ
		|	Документ.ЗаявкаНаРасходованиеДенежныхСредств.РасшифровкаПлатежа КАК ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа
		|ГДЕ
		|	ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа.Ссылка В(&Заявка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаявкаВедомости.Ведомость,
		|	ЗаявкаВедомости.СтатьяДвиженияДенежныхСредств,
		|	ЗаявкаВедомости.Ссылка КАК ЗаявкаНаРасходованиеДенежныхСредств,
		|	-КВыплатеОстатки.СуммаОстаток КАК Сумма
		|ИЗ
		|	Документ.ЗаявкаНаРасходованиеДенежныхСредств.РасшифровкаПлатежа КАК ЗаявкаВедомости
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ДенежныеСредстваКВыплате.Остатки(
		|				,
		|				ЗаявкаНаРасходованиеДенежныхСредств В
		|					(ВЫБРАТЬ
		|						ВТ_Ведомости.Ведомость КАК Ведомость
		|					ИЗ
		|						ВТ_Ведомости КАК ВТ_Ведомости)) КАК КВыплатеОстатки
		|		ПО ЗаявкаВедомости.Ведомость = КВыплатеОстатки.ЗаявкаНаРасходованиеДенежныхСредств
		|ГДЕ
		|	ЗаявкаВедомости.Ссылка В(&Заявка)";
		
		Запрос.УстановитьПараметр("Заявка", СтруктураПараметров.Заявка);
		
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ТаблицаВедомостей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;
	
КонецПроцедуры

// Возвращает таблицу значений с данными о работниках и сумме выплаты по ведомости.
// Параметры:
//	СтруктураПараметров - Структура - Структура отборов;
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - для передачи таблицы в другой запрос.
//
// Возвращаемое значение:
//	ТаблицаЗначений - данные о работниках и суммах их начисления по ведомостям.
//
Функция ДанныеРаботниковПоВедомостям(СтруктураПараметров, МенеджерВременныхТаблиц = Неопределено) Экспорт
	
	Если МенеджерВременныхТаблиц = Неопределено Тогда
		
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		ИнтеграцияБЗК.ДанныеОбОплатеВедомостей(МенеджерВременныхТаблиц, СтруктураПараметров);
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеВедомостейНаОплату.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДанныеВедомостейНаОплату.Ведомость КАК Ведомость,
	|	СУММА(ДанныеВедомостейНаОплату.Сумма) КАК Сумма
	|ИЗ
	|	ДанныеВедомостейНаОплату КАК ДанныеВедомостейНаОплату
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеВедомостейНаОплату.Ведомость,
	|	ДанныеВедомостейНаОплату.ФизическоеЛицо
	|";
	
	ТаблицаРаботников = Запрос.Выполнить().Выгрузить();
	
	Если СтруктураПараметров.Свойство("Работник") Тогда
		
		Если ТаблицаРаботников.Количество() > 0 Тогда
			Возврат ТаблицаРаботников[0].Сумма;
		Иначе
			Возврат 0;
		КонецЕсли;
		
	Иначе
		
		Возврат ТаблицаРаботников;
		
	КонецЕсли;
	
КонецФункции

// Процедура формирует данные для заполнения и расшифровку платежа платежного документа по договору займа сотруднику.
//
// Параметры:
//	Договор - ДокументСсылка.ДоговорЗаймаСотруднику - Договор займа, по которому формируется платежный документ.
//	ДанныеЗаполнения - Структура - Структура основания для заполнения документа.
//	РасшифровкаПлатежа - ТаблицаЗначений, ТабличнаяЧасть - Расшифровка платежа платежного документа.
//	ЭтоПоступление - Булево - Признак, определяющий направление движение денежных средств.
//	ТипДокумента - Строка - Тип формируемого документа. Применяется для поиска образца при заполнении расшифровки платежа.
//
Процедура ЗаполнитьПоДоговоруЗаймаСотруднику(Знач Договор, ДанныеЗаполнения, РасшифровкаПлатежа,
		ЭтоПоступление, ТипДокумента = "ПриходныйКассовыйОрдер") Экспорт
	
	ДанныеЗаполнения = ДанныеЗаполненияДокументаДДСПоДоговоруЗаймаСотруднику(Договор, ЭтоПоступление, ТипДокумента);
	РасшифровкаПлатежа.Загрузить(ДанныеЗаполнения.РасшифровкаПлатежа);
	ДанныеЗаполнения.Вставить("СуммаДокумента", РасшифровкаПлатежа.Итог("Сумма"));
	
	Если РасшифровкаПлатежа.Количество() = 0 Тогда
		НоваяСтрока = РасшифровкаПлатежа.Добавить();
		НоваяСтрока.ДоговорЗаймаСотруднику = Договор;
	КонецЕсли;
	
КонецПроцедуры

// Функция возвращает данные расшифровки платежа платежного документа по договору займа сотруднику.
//
// Параметры:
//	ДанныеЗаполнения - Структура - Структура основания для заполнения документа.
//	ЭтоПоступление - Булево - Признак, определяющий направление движение денежных средств.
//	ДокументИсключение - ДокументСсылка.РасходныйКассовыйОрдер,
//                       ДокументСсылка.СписаниеБезналичныхДенежныхСредств,
//                       ДокументСсылка.ПриходныйКассовыйОрдер,
//                       ДокументСсылка.ПоступлениеБезналичныхДенежныхСредств - Документ, движения которого следует исключить.
//	ТипДокумента - Строка - Тип формируемого документа. Применяется для поиска образца при заполнении расшифровки платежа.
//
// Возвращаемое значение:
//	ТаблицаЗначений - Данные заполнения расшифровки платежа.
Функция ДанныеРасшифровкиПлатежаДокументаДДСПоДоговоруЗаймаСотруднику(ДанныеЗаполнения, ЭтоПоступление = Ложь, 
		ДокументИсключение = Неопределено, ТипДокумента = "ПриходныйКассовыйОрдер") Экспорт
	
	ДатаАктуальности = ?(ДанныеЗаполнения.Свойство("Дата"), ДанныеЗаполнения.Дата, КонецДня(ТекущаяДатаСеанса()));
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ДанныеЗаполнения.Свойство("ДоговорЗаймаСотруднику") Тогда
		ДействующиеДоговора = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
			ДанныеЗаполнения.ДоговорЗаймаСотруднику);
	Иначе
		ДействующиеДоговора = ЗаймыСотрудникам.ДействующиеДоговорыЗаймаПоФизическомуЛицу(
			ДанныеЗаполнения.Организация,
			ДанныеЗаполнения.ПодотчетноеЛицо,
			ДатаАктуальности,
			НЕ ЭтоПоступление);
	КонецЕсли;
	
	РасшифровкаПлатежа = Новый ТаблицаЗначений;
	РасшифровкаПлатежа.Колонки.Добавить("ЗаявкаНаРасходованиеДенежныхСредств", Новый ОписаниеТипов("ДокументСсылка.ЗаявкаНаРасходованиеДенежныхСредств"));
	РасшифровкаПлатежа.Колонки.Добавить("ДоговорЗаймаСотруднику", Новый ОписаниеТипов("ДокументСсылка.ДоговорЗаймаСотруднику"));
	РасшифровкаПлатежа.Колонки.Добавить("ТипСуммыКредитаДепозита", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыСуммГрафикаКредитовИДепозитов"));
	РасшифровкаПлатежа.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
	РасшифровкаПлатежа.Колонки.Добавить("ВалютаВзаиморасчетов", Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	РасшифровкаПлатежа.Колонки.Добавить("СтатьяДвиженияДенежныхСредств", Новый ОписаниеТипов("СправочникСсылка.СтатьиДвиженияДенежныхСредств"));
	
	Если ДействующиеДоговора = Неопределено Тогда
		Возврат РасшифровкаПлатежа;
	КонецЕсли;
	
	Если ЭтоПоступление Тогда
		СтатьиДДС = СтатьиДДСЗаймовСотрудникуПоУмолчанию(ДатаАктуальности, ДокументИсключение, ТипДокумента);
		Для Каждого Договор Из ДействующиеДоговора Цикл
			
			Задолженность = ЗаймыСотрудникам.ОстатокЗадолженности(Договор, ДатаАктуальности, ДокументИсключение);
			
			Если Задолженность.СуммаЗайма > 0 Тогда
				НоваяСтрока = РасшифровкаПлатежа.Добавить();
				НоваяСтрока.ДоговорЗаймаСотруднику = Договор;
				НоваяСтрока.ТипСуммыКредитаДепозита = Перечисления.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг;
				НоваяСтрока.Сумма = Задолженность.СуммаЗайма;
				НоваяСтрока.ВалютаВзаиморасчетов = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(ДанныеЗаполнения.Организация);
				НоваяСтрока.СтатьяДвиженияДенежныхСредств = СтатьиДДС.СтатьяДвиженияОсновногоДолга;
			КонецЕсли;
			
			Если Задолженность.Проценты > 0 Тогда
				НоваяСтрока = РасшифровкаПлатежа.Добавить();
				НоваяСтрока.ДоговорЗаймаСотруднику = Договор;
				НоваяСтрока.ТипСуммыКредитаДепозита = Перечисления.ТипыСуммГрафикаКредитовИДепозитов.Проценты;
				НоваяСтрока.Сумма = Задолженность.Проценты;
				НоваяСтрока.ВалютаВзаиморасчетов = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(ДанныеЗаполнения.Организация);
				НоваяСтрока.СтатьяДвиженияДенежныхСредств = СтатьиДДС.СтатьяДвиженияПроцентов;
			КонецЕсли;
			
		КонецЦикла;
	Иначе
		
		ЗаявкиНаРасходованиеДС = Новый Соответствие;
		
		Если ДанныеЗаполнения.Свойство("ОплатаПоЗаявкам") И ДанныеЗаполнения.ОплатаПоЗаявкам Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ДенежныеСредстваКВыплате.ЗаявкаНаРасходованиеДенежныхСредств,
			|	ДенежныеСредстваКВыплате.ДоговорЗаймаСотруднику
			|ИЗ
			|	РегистрНакопления.ДенежныеСредстваКВыплате КАК ДенежныеСредстваКВыплате
			|ГДЕ
			|	ДенежныеСредстваКВыплате.ДоговорЗаймаСотруднику В (&СписокДоговоров)";
			
			Запрос.УстановитьПараметр("СписокДоговоров", ДействующиеДоговора);
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Пока Выборка.Следующий() Цикл
				ЗаявкиНаРасходованиеДС.Вставить(Выборка.ДоговорЗаймаСотруднику, Выборка.ЗаявкаНаРасходованиеДенежныхСредств);
			КонецЦикла;
			
		КонецЕсли;
		
		Для Каждого Договор Из ДействующиеДоговора Цикл
			
			Остаток = ЗаймыСотрудникам.ОстатокНевыданныхСумм(Договор, ДатаАктуальности, ДокументИсключение);
			
			Если Остаток > 0 Тогда
				НоваяСтрока = РасшифровкаПлатежа.Добавить();
				НоваяСтрока.ЗаявкаНаРасходованиеДенежныхСредств = ЗаявкиНаРасходованиеДС[Договор];
				НоваяСтрока.ДоговорЗаймаСотруднику = Договор;
				НоваяСтрока.Сумма = Остаток;
				НоваяСтрока.ВалютаВзаиморасчетов = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(ДанныеЗаполнения.Организация);
				НоваяСтрока.СтатьяДвиженияДенежныхСредств = ЗначениеНастроекПовтИсп.ПолучитьСтатьюДвиженияДенежныхСредств(Перечисления.ХозяйственныеОперации.ВыдачаЗаймаСотруднику);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат РасшифровкаПлатежа;
	
КонецФункции

// Функция возвращает структуру статей ДДС для заполнения платежного документа. Поиск статьи ДДС 
// выполняется по хозяйственной операции и типу документа.
//
// Параметры:
//    ДатаАктуальности - Дата - Дата, на которую требуется получить статьи ДДС.
//    ДокументИсключение - ДокументСсылка.РасходныйКассовыйОрдер,
//                       ДокументСсылка.СписаниеБезналичныхДенежныхСредств,
//                       ДокументСсылка.ПриходныйКассовыйОрдер,
//                       ДокументСсылка.ПоступлениеБезналичныхДенежныхСредств - Документ, движения которого следует исключить.
//    ТипДокумента - Строка - Тип формируемого документа. Применяется для поиска образца при заполнении расшифровки платежа.
//
// Возвращаемое значение:
//    Структура - Структура статей ДДС.
Функция СтатьиДДСЗаймовСотрудникуПоУмолчанию(ДатаАктуальности, ДокументИсключение = Неопределено, ТипДокумента = "ПриходныйКассовыйОрдер") Экспорт
	
	СтатьяДвиженияОсновногоДолга = Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка();
	СтатьяДвиженияПроцентов = Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументИсключение);
	Запрос.УстановитьПараметр("ДатаАктуальности", ДатаАктуальности);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Расшифровка.СтатьяДвиженияДенежныхСредств КАК СтатьяДвижения,
	|	ЕСТЬNULL(Документ.Дата, ДАТАВРЕМЯ(1,1,1)) КАК Дата
	|ИЗ
	|	Документ.%ТипДокумента%.РасшифровкаПлатежа КАК Расшифровка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.%ТипДокумента% КАК Документ
	|		ПО Расшифровка.Ссылка = Документ.Ссылка
	|ГДЕ
	|	Документ.Ссылка <> &Ссылка
	|	И Документ.Проведен
	|	И Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПогашениеЗаймаСотрудником)
	|	И Документ.Дата <= &ДатаАктуальности
	|	И Расшифровка.СтатьяДвиженияДенежныхСредств <> ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
	|	И Расшифровка.ТипСуммыКредитаДепозита = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ТипДокумента%", ТипДокумента);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СтатьяДвиженияОсновногоДолга = Выборка.СтатьяДвижения;
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Расшифровка.СтатьяДвиженияДенежныхСредств КАК СтатьяДвижения,
	|	ЕСТЬNULL(Документ.Дата, ДАТАВРЕМЯ(1,1,1)) КАК Дата
	|ИЗ
	|	Документ.%ТипДокумента%.РасшифровкаПлатежа КАК Расшифровка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.%ТипДокумента% КАК Документ
	|		ПО Расшифровка.Ссылка = Документ.Ссылка
	|ГДЕ
	|	Документ.Ссылка <> &Ссылка
	|	И Документ.Проведен
	|	И Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПогашениеЗаймаСотрудником)
	|	И Документ.Дата <= &ДатаАктуальности
	|	И Расшифровка.СтатьяДвиженияДенежныхСредств <> ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
	|	И Расшифровка.ТипСуммыКредитаДепозита = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ТипДокумента%", ТипДокумента);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СтатьяДвиженияПроцентов = Выборка.СтатьяДвижения;
	КонецЕсли;
	
	Возврат Новый Структура("СтатьяДвиженияОсновногоДолга, СтатьяДвиженияПроцентов", СтатьяДвиженияОсновногоДолга, СтатьяДвиженияПроцентов);
	
КонецФункции

//-- НЕ УТ

// Процедура заполняет документ на основании уведомления о зачислении валюты
//
// Параметры:
//	ДокументОснование - ДокументСсылка.УведомлениеОЗачисленииВалюты - Документ - основание
//	ДокументОбъект - ДокументОбъект - Текущий документ
//	ДанныеЗаполнения - Структура - Данные заполнения.
//
Процедура ЗаполнитьДокументПоУведомлениюОЗачисленииВалюты(Знач ДокументОснование, ДокументОбъект, ДанныеЗаполнения) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПлатежныхДокументов.РаспоряжениеОбОбязательнойПродаже) КАК ТипПлатежногоДокумента,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.БанковскийСчет.Подразделение КАК Подразделение,
	|	ДанныеДокумента.Валюта КАК Валюта,
	|	ДанныеДокумента.Ссылка КАК УведомлениеОЗачисленииВалюты,
	|	ДанныеДокумента.Ссылка КАК ДокументОснование,
	|
	|	ДанныеДокумента.БанковскийСчет КАК БанковскийСчет,
	|	ДанныеДокумента.БанковскийСчет.ОсновнойБанковскийСчет КАК БанковскийСчетПолучатель,
	|
	|	ДенежныеСредства.СуммаОстаток КАК СуммаДокумента
	|ИЗ
	|	Документ.УведомлениеОЗачисленииВалюты КАК ДанныеДокумента
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ДенежныеСредстваКВыплате.Остатки(,
	|			ЗаявкаНаРасходованиеДенежныхСредств = &Ссылка
	|		) КАК ДенежныеСредства
	|	ПО
	|		ИСТИНА
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не требуется вводить ""Списание безналичных ДС"" на основании документа %1';
				|en = 'It is not required to enter Non-cash payment based on the %1 document'"),
			ДокументОснование);
		ВызватьИсключение Текст;
	Иначе
		ДанныеЗаполнения = Новый Структура;
		Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
			ДанныеЗаполнения.Вставить(Колонка.Имя);
		КонецЦикла;
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(ДанныеЗаполнения, Выборка);
	КонецЕсли;
	
КонецПроцедуры

//++ НЕ УТ

// Помещает во временное хранилище таблицу подтверждающих документов
//
// Параметры:
//    ТипПлатежаФЗ275 - СправочникСсылка.ТипыПлатежейФЗ275 - Тип платежа ФЗ275
//    ИдентификаторВладельца - УникальныйИдентификатор - Идентификатор формы для помещения результата
//    Договор - СправочникСсылка.ДоговорыКонтрагентов, СправочникСсылка.ДоговорыМеждуОрганизациями, СправочникСсылка.ДоговорыКредитовИДепозитов - Договор по госконтракту
// 
// Возвращаемое значение:
//    Строка -Адрес во временном хранилище, по которому содержатся ссылки на подтверждающие документы 
//
Функция ПодтверждающиеДокументыФЗ275(ТипПлатежаФЗ275, ИдентификаторВладельца, Договор = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеСправочника.ВидДокумента КАК ВидДокумента,
	|	ДанныеСправочника.Файл         КАК Файл,
	|	ДанныеСправочника.Номер        КАК Номер,
	|	ДанныеСправочника.Дата         КАК Дата,
	|	ДанныеСправочника.Сумма        КАК Сумма
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов.ПодтверждающиеДокументы КАК ДанныеСправочника
	|ГДЕ
	|	ДанныеСправочника.Ссылка = &Договор
	|	И &УказанДоговор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеСправочника.ВидДокумента КАК ВидДокумента,
	|	ДанныеСправочника.Файл         КАК Файл,
	|	ДанныеСправочника.Номер        КАК Номер,
	|	ДанныеСправочника.Дата         КАК Дата,
	|	ДанныеСправочника.Сумма        КАК Сумма
	|ИЗ
	|	Справочник.ДоговорыМеждуОрганизациями.ПодтверждающиеДокументы КАК ДанныеСправочника
	|ГДЕ
	|	ДанныеСправочника.Ссылка = &Договор
	|	И &УказанДоговор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеСправочника.ВидДокумента КАК ВидДокумента,
	|	ДанныеСправочника.Файл         КАК Файл,
	|	ДанныеСправочника.Номер        КАК Номер,
	|	ДанныеСправочника.Дата         КАК Дата,
	|	ДанныеСправочника.Сумма        КАК Сумма
	|ИЗ
	|	Справочник.ДоговорыКредитовИДепозитов.ПодтверждающиеДокументы КАК ДанныеСправочника
	|ГДЕ
	|	ДанныеСправочника.Ссылка = &Договор
	|	И &УказанДоговор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеСправочника.ВидДокумента КАК ВидДокумента,
	|	НЕОПРЕДЕЛЕНО                   КАК Файл,
	|	НЕОПРЕДЕЛЕНО                   КАК Номер,
	|	НЕОПРЕДЕЛЕНО                   КАК Дата,
	|	НЕОПРЕДЕЛЕНО                   КАК Сумма
	|ИЗ
	|	Справочник.ТипыПлатежейФЗ275.ПодтверждающиеДокументы КАК ДанныеСправочника
	|ГДЕ
	|	ДанныеСправочника.Ссылка = &ТипПлатежаФЗ275
	|	И НЕ &УказанДоговор
	|";
	
	Запрос.УстановитьПараметр("ТипПлатежаФЗ275", ТипПлатежаФЗ275);
	Запрос.УстановитьПараметр("Договор", Договор);
	Запрос.УстановитьПараметр("УказанДоговор", ЗначениеЗаполнено(Договор));
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат ПоместитьВоВременноеХранилище(Запрос.Выполнить().Выгрузить(), ИдентификаторВладельца);;
	
КонецФункции
//-- НЕ УТ

// Очищает реквизиты платежей в бюджет после проверки даты применения Приказа 107н 
//
// Параметры:
//	Объект - ДокументСсылка - Документ оплаты.
//	ПредыдущийПериод - Дата - исходный период (до изменения)
//	НовыйПериод - Дата - новый период.
//
Процедура ПриИзмененииПравилПлатежейВБюджет(Объект, ПредыдущийПериод, НовыйПериод) Экспорт
	
	ДатаНачалаПримененияПриказа107н = Константы.ДатаНачалаПримененияПриказа107н.Получить();
	
	ДействовалиНовыеПравила = (ПредыдущийПериод >= ДатаНачалаПримененияПриказа107н);
	ДействуютНовыеПравила   = (НовыйПериод >= ДатаНачалаПримененияПриказа107н);
	
	Если ДействовалиНовыеПравила <> ДействуютНовыеПравила Тогда
		Объект.КодОКАТО = "";
		Объект.ПоказательОснования = "";
		Объект.ПоказательПериода = "";
		Объект.ПоказательНомера = "";
		Объект.ПоказательДаты = "";
		Объект.ПоказательТипа = "";
	КонецЕсли;
	
КонецПроцедуры

// Выбирает перечень КПП подразделений организации
//
// Параметры:
//    Организация - СправочникСсылка.Организации - Организация, для которой определяет перечень КПП.
//
// Возвращаемое значение:
//    Массив из Структура - содержит:
//     * КПП - Строка - КПП филиала
//     * Подразделение - СправочникСсылка.СтруктураПредприятия - Филиал, имеющий регистрацию в налоговом органе
//     * РегистрацияСсылка - СправочникСсылка.РегистрацииВНалоговомОргане - Регистрация филиала
//
Функция СписокКППОрганизации(Организация) Экспорт
	
	СписокВыбораКПП = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ЕСТЬNULL(Регистрации.РегистрацияВНалоговомОргане.КПП, """")  КАК КПП,
	|	ЕСТЬNULL(Регистрации.Подразделение.Наименование, """")       КАК Подразделение,
	|	Регистрации.РегистрацияВНалоговомОргане                      КАК РегистрацияСсылка
	|ИЗ
	|	РегистрСведений.РегистрацииВНалоговомОргане КАК Регистрации
	|ГДЕ
	|	Регистрации.Организация = &Организация
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ЕСТЬNULL(Организации.РегистрацияВНалоговомОргане.КПП, ""0""),
	|	Организации.Наименование,
	|	Организации.РегистрацияВНалоговомОргане
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ГоловнаяОрганизация = &Организация
	|	И Организации.ОбособленноеПодразделение
	|	И НЕ Организации.ПометкаУдаления
	|	И Организации.РегистрацияВНалоговомОргане <> ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ЕСТЬNULL(Регистрации.РегистрацияВНалоговомОргане.КПП, """")  КАК КПП,
	|	ЕСТЬNULL(Регистрации.Подразделение.Наименование, """")       КАК Подразделение,
	|	Регистрации.РегистрацияВНалоговомОргане                      КАК РегистрацияСсылка
	|ИЗ
	|	РегистрСведений.РегистрацииВНалоговомОргане КАК Регистрации
	|ГДЕ
	|	Регистрации.Организация.ГоловнаяОрганизация = &Организация
	|	И Регистрации.Организация <> &Организация
	|
	|;
	|//////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Организации.КПП КАК КПП
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка = &Организация
	|";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Выборка = Результат[1].Выбрать();
	Если Выборка.Следующий() Тогда
		КПП = Новый Структура("КПП, Подразделение, РегистрацияСсылка");
		ЗаполнитьЗначенияСвойств(КПП, Выборка);
		СписокВыбораКПП.Добавить(КПП);
	КонецЕсли;
	
	Выборка = Результат[0].Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.КПП) Тогда
			КПП = Новый Структура("КПП, Подразделение, РегистрацияСсылка");
			ЗаполнитьЗначенияСвойств(КПП, Выборка);
			СписокВыбораКПП.Добавить(КПП);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СписокВыбораКПП;
	
КонецФункции

#КонецОбласти

#Область ОбменСБанками

// Загружает электронную выписку банка
// 
// Параметры:
//     ЭлектроннаяВыпискаБанка - Ссылка - Электронная выписка банка
//
Процедура РазобратьВыпискуБанка(ЭлектроннаяВыпискаБанка) Экспорт
	
	Перем ДеревоВыписки;
	// ЭлектронноеВзаимодействие.ОбменСБанками
	ОбменСБанками.ПолучитьДанныеВыпискиБанкаДеревоЗначений(ЭлектроннаяВыпискаБанка, ДеревоВыписки);
	// Конец ЭлектронноеВзаимодействие.ОбменСБанками
	
	КлиентБанк = Обработки.КлиентБанк.Создать();
	КлиентБанк.ЗаполнитьТаблицуСчетовЗагрузки();
	
	КлиентБанк.СоздаватьКонтрагентов = Истина;
	КлиентБанк.ПроводитьДокументы = Истина;
	
	СтрокаСчета = Неопределено;
	Если ДеревоВыписки.Строки.Количество() Тогда
		Выписки = ДеревоВыписки.Строки[0];
		
		Если Выписки.Строки.Количество() Тогда
			Выписка = Выписки.Строки[0];
			
			СтрокаНомерСчета = Выписка.Строки.Найти("Выписки.НомерСтроки.НомерСчета", "ПолныйПуть");
			Если СтрокаНомерСчета <> Неопределено Тогда
				
				СчетаВыписки = КлиентБанк.БанковскиеСчета.НайтиСтроки(Новый Структура("НомерСчета", СтрокаНомерСчета.Значение));
				Если СчетаВыписки.Количество() Тогда
					СтрокаСчета = СчетаВыписки[0];
					СтрокаСчета.АдресХранилищаФайла = ПоместитьВоВременноеХранилище(ДеревоВыписки, Новый УникальныйИдентификатор);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если СтрокаСчета <> Неопределено Тогда
		КлиентБанк.ЗагрузитьПлатежиПоСчету(СтрокаСчета, Новый УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

//++ НЕ УТ
#Область ДенежныеДокументы

// Процедура заполняет таблицу остатками денежных документов
//
// Параметры:
//	Отбор - Структура - Параметры отбора остатков ДД
//		* Организация		- СправочникСсылка.Организации 				- Организация, в которой хранятся ДД
//		* Подразделение		- СправочникСсылка.СтруктураПредприятия 	- Подразделение, в котором хранятся ДД
//		* МОЛ 				- СправочникСсылка.ФизическиеЛица 			- МОЛ, у которого хранятся ДД
//		* Ссылка			- ДокументСсылка.ВыбытиеДенежныхДокументов 	- Документ, движения которого должны быть исключены из расчета остатков
//		* ДенежныеДокументы	- Массив 									- отбор по денежным документам
//	Таблица - ТаблицаЗначений - таблица, в которую будет помещен результат получения остатков ДД.
//
Процедура ЗаполнитьПоОстаткамДД(Отбор, Таблица) Экспорт
	
	ПараметрыОтбора = Новый Структура("ДенежныеДокументы, Организация, Подразделение, МОЛ, Ссылка, Валюта");
	ЗаполнитьЗначенияСвойств(ПараметрыОтбора, Отбор);
	
	МассивДД = ?(ПараметрыОтбора.ДенежныеДокументы = Неопределено, Новый Массив, ПараметрыОтбора.ДенежныеДокументы);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ОтборПоДД",			МассивДД.Количество() > 0);
	Запрос.УстановитьПараметр("ДенежныеДокументы",	МассивДД);
	Запрос.УстановитьПараметр("Организация",		ПараметрыОтбора.Организация);
	Запрос.УстановитьПараметр("Подразделение",		ПараметрыОтбора.Подразделение);
	Запрос.УстановитьПараметр("МОЛОтправитель",		ПараметрыОтбора.МОЛ);
	Запрос.УстановитьПараметр("Ссылка",				ПараметрыОтбора.Ссылка);
	Запрос.УстановитьПараметр("ОтборПоВалюте",		ПараметрыОтбора.Валюта <> Неопределено);
	Запрос.УстановитьПараметр("Валюта",				ПараметрыОтбора.Валюта);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Остатки.ДенежныйДокумент	КАК ДенежныйДокумент,
	|	СУММА(Остатки.Количество)	КАК Количество,
	|	СУММА(Остатки.Сумма)		КАК Сумма
	|ПОМЕСТИТЬ ВтОстатки
	|ИЗ
	|	(ВЫБРАТЬ
	|		Остатки.ДенежныйДокумент	КАК ДенежныйДокумент,
	|		Остатки.КоличествоОстаток	КАК Количество,
	|		Остатки.СуммаОстаток		КАК Сумма
	|	ИЗ
	|		РегистрНакопления.ДенежныеДокументы.Остатки(
	|				,
	|				МОЛ = &МОЛОтправитель
	|					И Организация = &Организация
	|					И Подразделение = &Подразделение
	|					И (НЕ &ОтборПоВалюте ИЛИ ДенежныйДокумент.Валюта = &Валюта)
	|					И (НЕ &ОтборПоДД ИЛИ ДенежныйДокумент В (&ДенежныеДокументы))) КАК Остатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Движения.ДенежныйДокумент КАК ДенежныйДокумент,
	|		ВЫБОР КОГДА Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|			Движения.Количество
	|		ИНАЧЕ
	|			-Движения.Количество
	|		КОНЕЦ КАК Количество,
	|		ВЫБОР КОГДА Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|			Движения.Сумма
	|		ИНАЧЕ
	|			-Движения.Сумма
	|		КОНЕЦ КАК Сумма
	|	ИЗ
	|		РегистрНакопления.ДенежныеДокументы КАК Движения
	|	ГДЕ
	|		Движения.Регистратор = &Ссылка
	|		И Движения.МОЛ = &МОЛОтправитель
	|		И Движения.Организация = &Организация
	|		И Движения.Подразделение = &Подразделение
	|		И (НЕ &ОтборПоВалюте ИЛИ Движения.ДенежныйДокумент.Валюта = &Валюта)
	|		И (НЕ &ОтборПоДД ИЛИ Движения.ДенежныйДокумент В (&ДенежныеДокументы))) КАК Остатки
	|	
	|СГРУППИРОВАТЬ ПО
	|	ДенежныйДокумент
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	ДенежныйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Остатки.ДенежныйДокумент	КАК ДенежныйДокумент,
	|	Остатки.Количество			КАК Количество,
	|	Остатки.Сумма				КАК Сумма,
	|	Остатки.Сумма				КАК СуммаВозврата,
	|	СправочникДД.Цена			КАК Цена,
	|	СправочникДД.Цена			КАК ЦенаВозврата,
	|	СправочникДД.Валюта			КАК Валюта,
	|	СправочникДД.Наименование	КАК НаименованиеДенежногоДокумента,
	|	СправочникДД.Родитель		КАК ГруппаДокумента
	|ИЗ
	|	ВтОстатки КАК Остатки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДенежныеДокументы КАК СправочникДД
	|		ПО Остатки.ДенежныйДокумент = СправочникДД.Ссылка
	|";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Таблица.Добавить(), Выборка);
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти
//-- НЕ УТ

#Область Бюджет

// Проверяет корректность заполнения налоговых реквизитов
//
// Параметры:
//	Объект - ДокументСсылка - Документ оплаты.
//	Отказ - Булево - Признак отказа от продолжения работы
//	НепроверяемыеРеквизиты - Массив - Непроверяемые реквизиты документа
//	ФлагОбменСБанками - Булево - используется обмен с банком
//	ОшибкиЗаполнения - Строка - Накопленное описание ошибок.
//
Процедура ПроверитьЗаполнениеНалоговыхРеквизитов(Объект, Отказ, НепроверяемыеРеквизиты, ФлагОбменСБанками = Ложь, ОшибкиЗаполнения = Неопределено) Экспорт
	
	ХозяйственнаяОперация = Объект.ХозяйственнаяОперация;
	ПеречислениеВБюджет = Объект.ПеречислениеВБюджет;
	ВидПеречисленияВБюджет = Объект.ВидПеречисленияВБюджет;
	СтатусСоставителя = Объект.СтатусСоставителя;
	Дата = Объект.Дата;
	
	ДатаНачалаПримененияПриказа126н = Константы.ДатаНачалаПримененияПриказа126н.Получить();
	
	ПараметрыПроверкиЗаполнения = Новый Структура("ПрименениеПриказа107н, ПрименениеПриказа126н",
		Дата >= Константы.ДатаНачалаПримененияПриказа107н.Получить() Или Не ЗначениеЗаполнено(Дата),
		ДатаНачалаПримененияПриказа126н <> '00010101' И Дата >= ДатаНачалаПримененияПриказа126н Или Не ЗначениеЗаполнено(Дата));
	
	МассивОпераций = МассивОперацийПеречисленияВБюджет();
	
	Если ПеречислениеВБюджет И МассивОпераций.Найти(ХозяйственнаяОперация) <> Неопределено Тогда
	
		// Проверки, общие для всех видов перечислений в бюджет
		Если ПлатежиВБюджетКлиентСервер.СтатусыПлательщика(
			ПараметрыПроверкиЗаполнения.ПрименениеПриказа107н, Дата).НайтиПоЗначению(СтатусСоставителя) = Неопределено Тогда
			
			ТекстОшибки = НСтр("ru = 'Неверное значение поля ""Статус составителя""';
								|en = 'Incorrect value of the ""Author status"" field '");
			Если ФлагОбменСБанками Тогда
				ДобавитьОшибкуЗаполнения(ОшибкиЗаполнения, ТекстОшибки);
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "НадписьРеквизитыПлатежаВБюджет",, Отказ);
			КонецЕсли;
		КонецЕсли;
		
		Если ПараметрыПроверкиЗаполнения.ПрименениеПриказа126н Тогда
			НепроверяемыеРеквизиты.Добавить("ПоказательТипа");
		КонецЕсли;
		
		Если ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.НалоговыйПлатеж Тогда
			ПроверитьЗаполнениеРеквизитовНалоговыхПлатежей(Объект, ПараметрыПроверкиЗаполнения, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения);
		ИначеЕсли ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.ТаможенныйПлатеж Тогда
			ПроверитьЗаполнениеРеквизитовТаможенныхПлатежей(Объект, ПараметрыПроверкиЗаполнения, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения);
		ИначеЕсли ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.ИнойПлатеж Тогда
			ПроверитьЗаполнениеРеквизитовИныхПлатежейВБюджет(Объект, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Проверяет корректность заполнения реквизитов платежа в бюджет согласно 148н
//
// Параметры:
//	Объект - ДокументОбъект - Документ оплаты.
//	Отказ - Булево - Признак отказа от продолжения работы.
//
Процедура ПроверитьИННиКППНаСоответствие148н(Объект, Отказ) Экспорт
	
	Если Объект.Дата < ПлатежиВБюджетКлиентСервер.НачалоДействияУказания3844У() Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыПлательщика = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Организация, "ИНН, КПП");
	Если ТипЗнч(Объект) = Тип("ДокументОбъект.СписаниеБезналичныхДенежныхСредств")
		И ЗначениеЗаполнено(Объект.РегистрацияВНалоговомОргане) Тогда
		РеквизитыПлательщика.КПП = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.РегистрацияВНалоговомОргане, "КПП");
	КонецЕсли;
	
	РеквизитыПолучателя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.БанковскийСчетКонтрагента, "Владелец.ИНН, Владелец.КПП");
	
	Если Лев(РеквизитыПлательщика.ИНН, 2) = "00" Тогда
		ТекстОшибки = НСтр("ru = 'Первые две цифры ИНН плательщика не могут быть ""00""';
							|en = 'The first two digits of the payer TIN cannot be ""00"" '");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Объект,,, Отказ);
	КонецЕсли;
	
	Если Лев(РеквизитыПлательщика.КПП, 2) = "00" Тогда
		ТекстОшибки = НСтр("ru = 'Первые две цифры КПП плательщика не могут быть ""00""';
							|en = 'First two digits of a payer CRTR cannot be ""00""'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Объект,,, Отказ);
	КонецЕсли;
	
	Если Лев(РеквизитыПолучателя.ВладелецИНН, 2) = "00" Тогда
		ТекстОшибки = НСтр("ru = 'Первые две цифры ИНН получателя не могут быть ""00""';
							|en = 'The first two digits of the recipient TIN cannot be ""00""'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Объект, "Контрагент",, Отказ);
	КонецЕсли;
	
	Если Лев(РеквизитыПолучателя.ВладелецКПП, 2) = "00" Тогда
		ТекстОшибки = НСтр("ru = 'Первые две цифры КПП получателя не могут быть ""00""';
							|en = 'First two digits of a recipient CRTR cannot be ""00""'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Объект, "Контрагент",, Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

//++ НЕ УТ
#Область ГОЗ

// Формирование текста назначения платежа для операций с использованием отдельных счетов
// Назначение платежа состоит из 4 частей:
//		1. Аванс / Оплата (необязательный) - Контроль необходимости указания подтверждающих исполнение документов
//		2. Предмет платежа - Контроль платежа по параметрам контракта (перечень товаров и услуг по тарифным ценам)
//		3. Блок автоматической обработки (необязательный) - Контроль состава приложенных обосновывающих документов
//		4. НДС (необязательный) - Указание в сумме платежа ставки и суммы платежа.
//
Функция НазначениеПлатежаФЗ275(Объект, Действие = "") Экспорт
	
	НазначениеПлатежа = "";
	НазначениеПлатежаМассивСтрок = Новый Массив;
	
	Если Объект.Свойство("ТипПлатежаФЗ275") Тогда
		ТипПлатежа = Объект.ТипПлатежаФЗ275;
	Иначе
		ТипПлатежа = Неопределено;
	КонецЕсли;
	
	ТипыПлатежей = Справочники.ТипыПлатежейФЗ275;
	
	ТипыПлатежейАвансаОплаты = Новый Массив;
	ТипыПлатежейАвансаОплаты.Добавить(2);
	ТипыПлатежейАвансаОплаты.Добавить(3);
	ТипыПлатежейАвансаОплаты.Добавить(9);
	ТипыПлатежейАвансаОплаты.Добавить(99);
	
	ТипыПлатежейНДС = Новый Массив;
	ТипыПлатежейНДС.Добавить(2);
	ТипыПлатежейНДС.Добавить(3);
	ТипыПлатежейНДС.Добавить(99);
	
	Если ЗначениеЗаполнено(ТипПлатежа) Тогда
		РеквизитыТипаПлатежа = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТипПлатежа, "Код, НомерПунктаЗакона");
	КонецЕсли;
	
	РеквизитыЭтапа = Новый Структура("
		|ПредметОплаты, 
		|ПунктКонтрактаПредмета, 
		|ПунктКонтрактаОплаты, 
		|ВариантОплаты"); 
		
	РеквизитыЭтапа.Вставить("ПредметОплаты",			Объект.ПредметОплаты);
	РеквизитыЭтапа.Вставить("ПунктКонтрактаПредмета",	Объект.ПунктКонтрактаПредмета);
	РеквизитыЭтапа.Вставить("ПунктКонтрактаОплаты",		Объект.ПунктКонтрактаОплаты);
	РеквизитыЭтапа.Вставить("ВариантОплаты",			Объект.ВариантОплаты);
	
	#Область АвансОплата
	Если ТипыПлатежейАвансаОплаты.Найти(РеквизитыТипаПлатежа.Код) <> Неопределено Тогда
		Если РеквизитыЭтапа.ВариантОплаты = Перечисления.ВариантыОплатыПоставщику.АвансДоПодтверждения
			Или РеквизитыЭтапа.ВариантОплаты = Перечисления.ВариантыОплатыПоставщику.ПредоплатаДоПоступления Тогда
			НазначениеПлатежаМассивСтрок.Добавить(НСтр("ru = 'Авансовый платеж';
														|en = 'Advance payment'"));
		Иначе
			НазначениеПлатежаМассивСтрок.Добавить(НСтр("ru = 'Оплата';
														|en = 'Payment'"));
		КонецЕсли;
		
		НазначениеПлатежаМассивСтрок.Добавить("(" + РеквизитыЭтапа.ПунктКонтрактаОплаты + ")");
		НазначениеПлатежаМассивСтрок.Добавить(НСтр("ru = 'за';
													|en = 'for'"));
	КонецЕсли;
	#КонецОбласти 
	
	#Область ПредметПлатежа
	Если ТипПлатежа = ТипыПлатежей.УплатаНалоговИСборовТаможенныхПлатежейСтраховыхВзносов Тогда // Тип платежа 1
		КБК = СокрЛП(Объект.КодБК);
		
		ПредметПлатежаНалог = НазначениеПлатежаДляУплатыНалогов(
			КБК,
			ДенежныеСредстваПовтИспРФ.ВидНалоговогоОбязательстваПоКБК(КБК),
			Объект.Организация,
			Объект.Дата,
			ПлатежиВБюджетКлиентСервер.ПредставлениеНалоговогоПериода(Объект.ПоказательПериода));
		
		НазначениеПлатежаМассивСтрок.Добавить(ПредметПлатежаНалог);
		НазначениеПлатежаМассивСтрок.Добавить(НСтр("ru = 'ИГК';
													|en = 'SCI'"));
		НазначениеПлатежаМассивСтрок.Добавить("<ИГК>");
	ИначеЕсли ТипПлатежа = ТипыПлатежей.ОплатаТоваровРаботУслугПоРегулируемымЦенам // Тип платежа 2
		Или ТипПлатежа = ТипыПлатежей.ОплатаИныхРасходовВПределахУстановленнойСуммыВМесяц // Тип платежа 3
		Или ТипПлатежа = ТипыПлатежей.РасчетыСИностраннымиИсполнителями // Тип платежа 9
		Или ТипПлатежа = ТипыПлатежей.СписаниеНаОтдельныйСчет Тогда // Тип платежа 99
		
		НазначениеПлатежаМассивСтрок.Добавить(РеквизитыЭтапа.ПредметОплаты); // Предмет оплаты
		НазначениеПлатежаМассивСтрок.Добавить("(" + РеквизитыЭтапа.ПунктКонтрактаПредмета + ")");
	ИначеЕсли ТипПлатежа = ТипыПлатежей.ОплатаТруда Тогда // Тип платежа 5
		НазначениеПлатежаМассивСтрок.Добавить(НСтр("ru = 'Заработная плата';
													|en = 'Payroll'"));
		НазначениеПлатежаМассивСтрок.Добавить(НСтр("ru = 'за';
													|en = 'for'"));
	ИначеЕсли ТипПлатежа = ТипыПлатежей.СписаниеСвязанноеСРазмещениемНаДепозитеУполномоченногоБанка Тогда // Тип платежа 7
		НазначениеПлатежаМассивСтрок.Добавить(НСтр("ru = 'Перечисление денежных средств на депозит';
													|en = 'Transfer cash to deposit'"));
	ИначеЕсли ТипПлатежа = ТипыПлатежей.СписаниеСуммКредитовПеречисленныхУполномоченнымБанком Тогда // Тип платежа 8
		НазначениеПлатежаМассивСтрок.Добавить(НСтр("ru = 'Возврат суммы основного долга по кредиту';
													|en = 'Repayment of principal debt amount on credit'"));
	ИначеЕсли ТипПлатежа = ТипыПлатежей.СписаниеНаВозмещениеЗатратЗаСчетСобственныхСредств Тогда // Тип платежа 10
		НазначениеПлатежаМассивСтрок.Добавить(НСтр("ru = 'Возмещение расходов на опережающее приобретение материалов';
													|en = 'Expense compensation for advance purchase of materials'"));
	ИначеЕсли ТипПлатежа = ТипыПлатежей.ПеречислениеПрибылиГоловномуИсполнителю Тогда // Тип платежа 11
		НазначениеПлатежаМассивСтрок.Добавить(НСтр("ru = 'Перечисление прибыли по выполненным контрактным обязательствам';
													|en = 'Transfer profit from performed contract obligations'"));
	КонецЕсли;
	#КонецОбласти
	
	#Область БлокАвтоматическойОбработки
	МассивПодтверждающихДокументов = Новый Массив;
	Если ТипЗнч(Объект.ПодтверждающиеДокументы) = Тип("ТаблицаЗначений") Тогда
		МассивСсылокВидов = Объект.ПодтверждающиеДокументы.ВыгрузитьКолонку("ВидДокумента");
	Иначе
		МассивСсылокВидов = Объект.ПодтверждающиеДокументы.Выгрузить(,"ВидДокумента").ВыгрузитьКолонку("ВидДокумента");
	КонецЕсли;
	СоответствиеРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивСсылокВидов, "КраткоеНаименование, Наименование");
	
	Для Каждого СтрокаПодтверждающихДокументов Из Объект.ПодтверждающиеДокументы Цикл
		ПредставлениеВидаДокумента = ПредставлениеВидаДокумента(СтрокаПодтверждающихДокументов.ВидДокумента, СоответствиеРеквизитов);
		
		Представление = ПредставлениеПодтверждающегоДокумента(ПредставлениеВидаДокумента, СтрокаПодтверждающихДокументов.Номер, СтрокаПодтверждающихДокументов.Дата);
		
		МассивПодтверждающихДокументов.Добавить(Представление);
	КонецЦикла;
	Если МассивПодтверждающихДокументов.Количество() Тогда
		ПредставлениеВсехДокументов = СтрСоединить(МассивПодтверждающихДокументов, "; ");
		
		НазначениеПлатежаМассивСтрок.Добавить(НСтр("ru = '275-ФЗ';
													|en = '275 FL'"));
		НазначениеПлатежаМассивСтрок.Добавить("{" + РеквизитыТипаПлатежа.Код);
		НазначениеПлатежаМассивСтрок.Добавить(ПредставлениеВсехДокументов + "}");
	КонецЕсли;
	#КонецОбласти
	
	#Область НДС
	Если ТипыПлатежейНДС.Найти(РеквизитыТипаПлатежа.Код) <> Неопределено Тогда
		Если ТипЗнч(Объект) <> Тип("Структура") Тогда
			ТекстСуммаНДС = ДенежныеСредстваСервер.ТекстСуммаНДСПлатежа(Объект.Организация, Объект.Валюта, Объект.РасшифровкаПлатежа, Истина);
			НазначениеПлатежаМассивСтрок.Добавить(ТекстСуммаНДС);
		КонецЕсли;
	КонецЕсли;
	#КонецОбласти
	
	Возврат СтрСоединить(НазначениеПлатежаМассивСтрок, " ");
	
КонецФункции

// Процедура очищает неиспользуемые реквизиты оплаты за счет средств ГОЗ
//
// Параметры:
//    ДокументОбъект - ДокументОбъект - Текущий документ.
//
Процедура ОчиститьНеиспользуемыеРеквизитыОплатыЗаСчетСредствГОЗ(ДокументОбъект) Экспорт
	
	Если ДокументОбъект.ДоговорыСЗаказчиками.Количество() > 0 Тогда
		ДокументОбъект.ДоговорыСЗаказчиками.Очистить();
	КонецЕсли;
	Если ДокументОбъект.ПодтверждающиеДокументы.Количество() > 0 Тогда
		ДокументОбъект.ПодтверждающиеДокументы.Очистить();
	КонецЕсли;
	
	МассивРеквизитов = Новый Массив;
	МассивРеквизитов.Добавить("ДоговорСУчастникомГОЗ");
	МассивРеквизитов.Добавить("ТипПлатежаФЗ275");
	МассивРеквизитов.Добавить("ПредметОплаты");
	
	ДенежныеСредстваСервер.ОчиститьНеиспользуемыеРеквизиты(ДокументОбъект, МассивРеквизитов, Новый Массив);
	
КонецПроцедуры

#Область КонтрольПлатежейНаСоответствие275ФЗ

// Проверяет переданный документ на соответствие требованиям федеральных законов 275-ФЗ и 3729-У.
//
// Параметры:
//   Ссылка - ДокументСсылка.ЗаявкаНаРасходованиеДенежныхСредств, ДокументСсылка.СписаниеБезналичныхДенежныхСредств -
//            проверяемый документ
//   ДанныеДокументовДляКонтроля - Массив - массив результатов запросов для контроля документов за исключением временных
//                                          таблиц и удалений временных таблиц.
//
// Возвращаемое значение:
//   РезультатыПроверок - Структура - результаты проверок
//       ВсеКонтролиПройдены - Булево
//       ОписанияРезультатов - ТаблицаЗначений - результаты проверки каждого правила
//           ИмяОбработчикаКонтроля - Строка - имя процедуры, проверяющей условия одного пункта контроля
//           КонтрольПройден - Булево - результат проверки одного пункта контроля
//           Описание - Строка - развернутое описания причин прохождения или непрохождения одного пункта контроля.
//
Функция КонтрольДокумента(Ссылка, ДанныеДокументовДляКонтроля = Неопределено) Экспорт
	РезультатыПроверок = ИнициализацияСвойствРезультатовПроверок();
	ИндексыРезультатовЗапросов = ИнициализацияИндексовРезультатовЗапросов();
	
	// Контроль раздела I "В соответствии с 275-ФЗ от 29.12.2012г."
	РезультатКонтроля_Раздел1_275_83_1_1(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, ДанныеДокументовДляКонтроля);
	РезультатКонтроля_Раздел1_275_83_1_2_б(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, ДанныеДокументовДляКонтроля);
	РезультатКонтроля_Раздел1_275_83_1_2_в(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, ДанныеДокументовДляКонтроля);
	РезультатКонтроля_Раздел1_275_83_1_2_г(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, ДанныеДокументовДляКонтроля);
	РезультатКонтроля_Раздел1_275_83_1_2_е(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, ДанныеДокументовДляКонтроля);
	РезультатКонтроля_Раздел1_275_83_1_2_з(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, ДанныеДокументовДляКонтроля);
	
	РезультатКонтроля_Раздел1_275_84_2(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, ДанныеДокументовДляКонтроля);
	РезультатКонтроля_Раздел1_275_84_10(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, ДанныеДокументовДляКонтроля);
	
	РезультатКонтроля_Раздел1_275_85_1_1(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, ДанныеДокументовДляКонтроля);
	РезультатКонтроля_Раздел1_275_85_2_1(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, ДанныеДокументовДляКонтроля);
	РезультатКонтроля_Раздел1_275_85_2_2(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, ДанныеДокументовДляКонтроля);
	
	// Контроль раздела II "Указание Банка России 3729-У от 15.07.2015г."
	РезультатКонтроля_Раздел2_3729У_2_1(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, ДанныеДокументовДляКонтроля);
	РезультатКонтроля_Раздел2_3729У_2_2(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, ДанныеДокументовДляКонтроля);
	РезультатКонтроля_Раздел2_3729У_2_3(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, ДанныеДокументовДляКонтроля);
	
	Отбор = Новый Структура("КонтрольПройден", Ложь);
	НайденныеСтроки = РезультатыПроверок.ОписанияРезультатов.НайтиСтроки(Отбор);
	РезультатыПроверок.Вставить("ВсеКонтролиПройдены", НайденныеСтроки.Количество() = 0);
	
	Возврат РезультатыПроверок;
КонецФункции

// Формирует таблицу данных для пакетной проверки документов
//
// Параметры:
//   МассивСсылок - ДокументСсылка.ЗаявкаНаРасходованиеДенежныхСредств,
//                  ДокументСсылка.СписаниеБезналичныхДенежныхСредств - проверяемые документы.
//
// Возвращаемое значение:
//   ДанныеДокументовДляКонтроля - Массив - массив результатов запросов для контроля документов за исключением временных
//                                          таблиц и удалений временных таблиц.
//
Функция ДанныеДокументовДляКонтроля(МассивСсылок) Экспорт
	ОписаниеТекстаЗапроса = ОписаниеТекстаЗапроса();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ОписаниеТекстаЗапроса.Текст;
	
	Запрос.УстановитьПараметр("ПроверяемыеДокументы", МассивСсылок);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	ДанныеДокументовДляКонтроля = Запрос.ВыполнитьПакет();
	
	// Удалим результаты запросов временных таблиц и уничтожения временных таблиц
	Для Номер = 1 По ОписаниеТекстаЗапроса.КоличествоВременныхТаблиц Цикл 
		ДанныеДокументовДляКонтроля.Удалить(0);
	КонецЦикла;
	
	Возврат ДанныеДокументовДляКонтроля;
КонецФункции

// По имени поля возвращает шаблон замены для запросах, где используется контроль отдельного банковского счета.
//
// Параметры:
//   ИсточникБанковскогоСчета - Строка - название поля банковского счета в запросе.
//
// Возвращаемое значение:
//   Строка - Текст запроса
//
Функция ТекстЗапросаПроверкиОтдельногоСчета(ИсточникБанковскогоСчета) Экспорт
	ТекстЗапроса = "// Проверка состояния отдельного счета ГОЗ (автоматически сформированный текст)";
	ТекстЗапроса = ТекстЗапроса + Символы.ПС + Символы.Таб + Символы.Таб
		+ "ВЫБОР КОГДА (" + ИсточникБанковскогоСчета + ".ОтдельныйСчетГОЗ";
	ТекстЗапроса = ТекстЗапроса + Символы.ПС + Символы.Таб + Символы.Таб
		+ "И НЕ " + ИсточникБанковскогоСчета + ".ГосударственныйКонтракт.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияГосударственныхКонтрактов.Завершен)" + ")";
	ТекстЗапроса = ТекстЗапроса + Символы.ПС + Символы.Таб + Символы.Таб
		+ "ТОГДА ИСТИНА ИНАЧЕ ЛОЖЬ КОНЕЦ";
		
	Возврат ТекстЗапроса;
КонецФункции

// Проверяет корректность заполнения подтверждающих документов
//
// Параметры:
//	Объект - СправочникСсылка.ДоговорыКонтрагентов, ДокументСсылка.ЗаявкаНаРасходованиеДенежныхСредств - проверяемый объект
//	Отказ - Булево - Признак отказа от продолжения работы
//	ПроверяемыеРеквизиты - Массив - Проверяемые реквизиты документа
//	Ошибки - Строка - Накопленное описание ошибок.
//
Процедура ОбработкаПроверкиЗаполненияПодтверждающиеДокументы(Объект, Отказ, ПроверяемыеРеквизиты, Ошибки) Экспорт
	
	Для Каждого ПодтверждающийДокумент Из Объект.ПодтверждающиеДокументы Цикл 
		ПодтверждаетИсполнениеКонтракта = 
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПодтверждающийДокумент.ВидДокумента, "ПодтверждаетИсполнениеКонтракта");
		
		Если ТипЗнч(Объект) = Тип("ДокументОбъект.ЗаявкаНаРасходованиеДенежныхСредств")
			И ПодтверждаетИсполнениеКонтракта Тогда
			Если Не ЗначениеЗаполнено(ПодтверждающийДокумент.Номер) Тогда
				СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не заполнена колонка ""Номер"" в строке %1 списка ""Подтверждающие документы""';
						|en = 'The ""Number"" column is not populated in line %1 of the ""Justification documents"" list'"),
					ПодтверждающийДокумент.НомерСтроки);
				
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки,
				"Объект.ПодтверждающиеДокументы[%1].Номер", СообщениеОбОшибке, Неопределено, ПодтверждающийДокумент.НомерСтроки-1);
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ПодтверждающийДокумент.Дата) Тогда
				СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не заполнена колонка ""Дата"" в строке %1 списка ""Подтверждающие документы""';
						|en = 'The ""Date"" column is not populated in line %1 of the ""Justification documents"" list'"),
					ПодтверждающийДокумент.НомерСтроки);
				
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки,
				"Объект.ПодтверждающиеДокументы[%1].Дата", СообщениеОбОшибке, Неопределено, ПодтверждающийДокумент.НомерСтроки-1);
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ПодтверждающийДокумент.Сумма) Тогда
				СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не заполнена колонка ""Сумма"" в строке %1 списка ""Подтверждающие документы""';
						|en = 'The ""Amount"" column is not populated in line %1 of the ""Justification documents"" list'"),
					ПодтверждающийДокумент.НомерСтроки);
				
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки,
				"Объект.ПодтверждающиеДокументы[%1].Сумма", СообщениеОбОшибке, Неопределено, ПодтверждающийДокумент.НомерСтроки-1);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Проверяет корректность заполнения подтверждающих документов
//
// Параметры:
//    Объект - СправочникСсылка.ДоговорыКонтрагентов, ДокументСсылка.ЗаявкаНаРасходованиеДенежныхСредств - проверяемый объект
//    Отказ - Булево - Признак отказа от продолжения работы.
//
Процедура ПроверитьЗаполнениеРеквизитовПлатежаГОЗ(Объект, Отказ) Экспорт
	
	ЭтоДоговорСКонтрагентом = ТипЗнч(Объект.Ссылка) = Тип("СправочникСсылка.ДоговорыКонтрагентов");
	ЭтоДоговорМеждуОрганизациями = ТипЗнч(Объект.Ссылка) = Тип("СправочникСсылка.ДоговорыМеждуОрганизациями");
	
	БанковскийСчетКонтролировать = 
		Не (ТипЗнч(Объект.Ссылка) = Тип("СправочникСсылка.ДоговорыКредитовИДепозитов")
			Или ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ЗаявкаНаРасходованиеДенежныхСредств")
			И (Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаПоКредитам
				Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеНаДепозиты));
	
	Если БанковскийСчетКонтролировать И ЗначениеЗаполнено(Объект.БанковскийСчет) Тогда
		ОтдельныйСчетГОЗ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.БанковскийСчет, "ОтдельныйСчетГОЗ");
		Если Не ОтдельныйСчетГОЗ Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Должен быть выбран отдельный банковский счет ГОЗ';
					|en = 'Separate SDO bank account must be selected'"),
				Объект,
				"БанковскийСчет",
				,
				Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Объект.Ссылка) = Тип("СправочникСсылка.ДоговорыКонтрагентов")
		Или ТипЗнч(Объект.Ссылка) = Тип("СправочникСсылка.ДоговорыКредитовИДепозитов") Тогда
		
		БанковскийСчетПолучателя = Объект.БанковскийСчетКонтрагента;
		БанковскийСчетПолучателяИмя = "БанковскийСчетКонтрагента";
		
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("СправочникСсылка.ДоговорыМеждуОрганизациями") Тогда
		
		БанковскийСчетПолучателя = Объект.БанковскийСчетПолучателя;
		БанковскийСчетПолучателяИмя = "БанковскийСчетПолучателя";
		
	КонецЕсли;
	
	Если Объект.ДоговорСУчастникомГОЗ И ЗначениеЗаполнено(БанковскийСчетПолучателя) Тогда
		ОтдельныйСчетГОЗ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БанковскийСчетПолучателя, "ОтдельныйСчетГОЗ");
		Если Не ОтдельныйСчетГОЗ Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Должен быть выбран отдельный банковский счет ГОЗ';
					|en = 'Separate SDO bank account must be selected'"),
				Объект,
				БанковскийСчетПолучателяИмя,
				,
				Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.ДоговорыСЗаказчиками.Количество() > 0 Тогда
		ЗначенияГосконтрактов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Объект.ДоговорыСЗаказчиками.ВыгрузитьКолонку("ДоговорСЗаказчиком"), "ГосударственныйКонтракт");
		
		Если ЗначениеЗаполнено(Объект.БанковскийСчет) Тогда
			Госконтракт = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.БанковскийСчет, "ГосударственныйКонтракт");
			Если ЗначениеЗаполнено(Госконтракт) Тогда
				Для каждого КлючИЗначение Из ЗначенияГосконтрактов Цикл
					Если КлючИЗначение.Значение <> Госконтракт Тогда
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
							НСтр("ru = 'Государственные контракты, указанные в договорах с заказчиками, не должны отличаться от госконтракта банковского счета.';
								|en = 'State contracts specified in the contract with customers should not differ from bank account state contract.'"),
							,,,
							Отказ);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если БанковскийСчетКонтролировать
		И Объект.ДоговорСУчастникомГОЗ Тогда
		
		Если
			ЭтоДоговорСКонтрагентом
			И ЗначениеЗаполнено(Объект.БанковскийСчет)
			И ЗначениеЗаполнено(Объект.БанковскийСчетКонтрагента)
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.БанковскийСчет, "ГосударственныйКонтракт")
			<> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.БанковскийСчетКонтрагента, "ГосударственныйКонтракт")
			
			Или ЭтоДоговорМеждуОрганизациями
			И ЗначениеЗаполнено(Объект.БанковскийСчет)
			И ЗначениеЗаполнено(Объект.БанковскийСчетПолучателя)
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.БанковскийСчет, "ГосударственныйКонтракт")
			<> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.БанковскийСчетПолучателя, "ГосударственныйКонтракт") Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Государственные контракты, указанные в банковских счетах, не должны отличаться.';
					|en = 'State contracts specified in the bank accounts should not differ.'"),
				,,,
				Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
//-- НЕ УТ

//-- Локализация

#КонецОбласти

//++ Локализация

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаполнитьСписокТиповДокумента(Объект, Элемент) Экспорт
	
	ТипыДокумента = Перечисления.ТипыПлатежныхДокументов;
	СписокВыбора = Элемент.СписокВыбора;
	
	СписокВыбора.Очистить();
	СписокВыбора.Добавить(ТипыДокумента.ПлатежноеПоручение);
	СписокВыбора.Добавить(ТипыДокумента.ИнкассовоеПоручение);
	СписокВыбора.Добавить(ТипыДокумента.ПлатежноеТребование);
	СписокВыбора.Добавить(ТипыДокумента.ПлатежныйОрдер);
	СписокВыбора.Добавить(ТипыДокумента.БанковскийОрдер);
	
	Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет
		И ПолучитьФункциональнуюОпцию("ИспользоватьВалютныеПлатежи") Тогда
		СписокВыбора.Добавить(ТипыДокумента.РаспоряжениеОбОбязательнойПродаже);
	КонецЕсли;
	
КонецПроцедуры

Процедура УправлениеЭлементамиФормыПриЧтенииСозданииНаСервере(Форма) Экспорт
	
	ИмяОбъекта = "";
	Если Форма.Параметры.Свойство("Ключ") Тогда
		ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(Форма.Параметры.Ключ));
		Если ОбъектМетаданных <> Неопределено Тогда
			ИмяОбъекта = ОбъектМетаданных.Имя;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИмяОбъекта) Тогда
		ВызватьИсключение НСтр("ru = 'Не удалось выполнить настройку полей формы';
								|en = 'Unable to configure form fields'");
	КонецЕсли;
	
	НастройкиПолейФормыЛокализация = ДенежныеСредстваСервер.ИнициализироватьНастройкиПолейФормы();
	
	МодульЛокализацииОбъекта = ОбщегоНазначения.ОбщийМодуль(ИмяОбъекта + "Локализация");
	МодульЛокализацииОбъекта.ЗаполнитьНастройкиПолейФормы(НастройкиПолейФормыЛокализация);
	
	НастройкиПолейФормы = Форма.РеквизитФормыВЗначение("НастройкиПолей");
	ОбщегоНазначенияУТ.ДобавитьСтрокиВТаблицу(НастройкиПолейФормы, НастройкиПолейФормыЛокализация);
	Форма.ЗначениеВРеквизитФормы(НастройкиПолейФормы, "НастройкиПолей");
	
	ЗависимостиПолейФормы = ДенежныеСредстваСервер.ЗависимостиПолейФормы(НастройкиПолейФормы);
	Форма.ЗначениеВРеквизитФормы(ЗависимостиПолейФормы, "ЗависимостиПолей");
	
КонецПроцедуры

Процедура НастройкиЭлементовБанков(Настройки) Экспорт
	
	Финансы = ФинансоваяОтчетностьСервер;
	
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("НомерСчета");
	Финансы.НовыйОтбор(Элемент.Условие, "Дополнительно.МеждународныеРеквизитыБанковскихСчетов", Ложь);
	Элемент.Свойства.Вставить("Маска", "!!!!!!!!!!!!!!!!!!!!");
	
КонецПроцедуры

Процедура ПриСозданииФормыСпискаПрисоединенныхФайлов(Форма) Экспорт
	
	Если ДокументыСПередачейФайловВБанк().Найти(ТипЗнч(Форма.ВладелецФайла)) <> Неопределено Тогда
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(Форма.Список.ТекстЗапроса);
		СхемаЗапроса.ПакетЗапросов[0].Операторы[0].ВыбираемыеПоля.Добавить("Файлы.ДляПередачиВБанк");
		Форма.Список.ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
		
		НовыйЭлемент = Форма.Элементы.Добавить("СписокДляПередачиВБанк", Тип("ПолеФормы"), Форма.Элементы.Список);
		НовыйЭлемент.ПутьКДанным = "Список.ДляПередачиВБанк";
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеФлажка;
		Форма.Элементы.Переместить(НовыйЭлемент, Форма.Элементы.Список, Форма.Элементы.СписокНомерКартинкиПодписанЗашифрован);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриСозданииФормыЭлементаПрисоединенныхФайлов(Форма) Экспорт
	
	Если ДокументыСПередачейФайловВБанк().Найти(ТипЗнч(Форма.Объект.ВладелецФайла)) <> Неопределено Тогда
		НовыйЭлемент = Форма.Элементы.Добавить("ДляПередачиВБанк", Тип("ПолеФормы"), Форма.Элементы.ГруппаОбщиеДанные);
		НовыйЭлемент.ПутьКДанным = "Объект.ДляПередачиВБанк";
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеФлажка;
		НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
		Форма.Элементы.Переместить(НовыйЭлемент, Форма.Элементы.ГруппаОбщиеДанные, Форма.Элементы.Наименование0);
	КонецЕсли;
	
КонецПроцедуры

Функция НадписьФайлыДляПередачиВБанк(Ссылка) Экспорт
	
	ТекстНадписи = НСтр("ru = 'Файлы для передачи в банк';
						|en = 'Files to transfer to the bank'");
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	КОЛИЧЕСТВО(*) КАК Количество
		|ИЗ
		|	%ИмяТаблицы%
		|ГДЕ
		|	ВладелецФайла = &Ссылка
		|	И ДляПередачиВБанк
		|	И НЕ ПометкаУдаления
		|";
		
		ИмяТаблицы = "Справочник." + Ссылка.Метаданные().Имя + "ПрисоединенныеФайлы";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИмяТаблицы%", ИмяТаблицы);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		ТекстНадписи = ТекстНадписи + " (" + Запрос.Выполнить().Выгрузить()[0].Количество + ")";
	КонецЕсли;
	
	Возврат ТекстНадписи;
	
КонецФункции

Функция РеквизитыПлательщика(Объект) Экспорт
	
	Результат = Новый Структура("ИННПлательщика, КПППлательщика, ТекстПлательщика");
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		
		СписокРеквизитов = Новый Структура;
		СписокРеквизитов.Вставить("ИННПлательщика", "ИНН");
		СписокРеквизитов.Вставить("КПППлательщика", "КПП");
		
		ТекстКорреспондента = "";
		Если ЗначениеЗаполнено(Объект.БанковскийСчет) Тогда
			ТекстКорреспондента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.БанковскийСчет, "ТекстКорреспондента");
		КонецЕсли;
		Если ЗначениеЗаполнено(ТекстКорреспондента) Тогда
			Результат.ТекстПлательщика = ТекстКорреспондента;
		Иначе
			СписокРеквизитов.Вставить("ТекстПлательщика", "НаименованиеСокращенное");
		КонецЕсли;
		
		РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Организация, СписокРеквизитов);
		ЗаполнитьЗначенияСвойств(Результат, РеквизитыОрганизации);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция НадписьРеквизитыПлательщика(ИНН, КПП, ТекстКорреспондента, ЮрФизЛицо, ХозяйственнаяОперация) Экспорт
	
	ШаблонНадписи = НСтр("ru = 'ИНН %1%2, %3';
						|en = 'TIN %1%2, %3'");
	
	СтрокаИНН = ?(ПустаяСтрока(ИНН), НСтр("ru = '<не указан>';
											|en = '<not specified>'"), СокрЛП(ИНН));
	
	ТребуетсяУказаниеКПП = (ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет);
	КППОтсутствует = (ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель);
	
	СтрокаКПП = ", " + НСтр("ru = 'КПП';
							|en = 'CRTR'");
	Если Не ЗначениеЗаполнено(КПП) Тогда
		Если ТребуетсяУказаниеКПП И КППОтсутствует Тогда
			СтрокаКПП = СтрокаКПП + " 0";
		ИначеЕсли Не ТребуетсяУказаниеКПП И КППОтсутствует Тогда
			СтрокаКПП = "";
		ИначеЕсли ТребуетсяУказаниеКПП И Не КППОтсутствует Тогда
			СтрокаКПП = СтрокаКПП + " " + НСтр("ru = '<не указан>';
												|en = '<not specified>'");
		ИначеЕсли Не ТребуетсяУказаниеКПП И Не КППОтсутствует Тогда
			СтрокаКПП = СтрокаКПП + " 0";
		КонецЕсли;
	Иначе
		СтрокаКПП = СтрокаКПП + " " + КПП;
	КонецЕсли;
	
	СтрокаТекстКорреспондента = СокрЛП(ТекстКорреспондента);
	
	Возврат СтрШаблон(ШаблонНадписи,
		СтрокаИНН,
		СтрокаКПП,
		СтрокаТекстКорреспондента);
		
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьПрефиксациюКассыКассовойКниги(Ссылка)
	
	РеквизитыПрефиксации = Новый Структура("ИспользоватьПрефикс, Префикс");
	
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.КассовыеКниги") Тогда
		РеквизитыПрефиксации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "ИспользоватьПрефикс, Префикс");
	ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.Кассы") Тогда
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "КассоваяКнига.ИспользоватьПрефикс, КассоваяКнига.Префикс");
		РеквизитыПрефиксации.Вставить("ИспользоватьПрефикс", Реквизиты.КассоваяКнигаИспользоватьПрефикс);
		РеквизитыПрефиксации.Вставить("Префикс", Реквизиты.КассоваяКнигаПрефикс);
	КонецЕсли;
	
	Возврат РеквизитыПрефиксации;
	
КонецФункции

Процедура ДобавитьОшибкуЗаполнения(ОшибкиЗаполнения, ТекстОшибки)
	
	ОшибкиЗаполнения = ОшибкиЗаполнения + "
	|" + ТекстОшибки;
	
КонецПроцедуры

//++ НЕ УТ

// Функция возвращает данные заполнения платежного документа по договору займа сотруднику.
//
// Параметры:
//	Договор - ДокументСсылка.ДоговорЗаймаСотруднику - Договор займа, по которому формируется платежный документ.
//	ЭтоПоступление - Булево - Признак, определяющий направление движение денежных средств.
//	ТипДокумента - Строка - Тип формируемого документа. Применяется для поиска образца при заполнении расшифровки платежа.
//
// Возвращаемое значение:
//	Структура - Данные заполнения платежного документа.
Функция ДанныеЗаполненияДокументаДДСПоДоговоруЗаймаСотруднику(Договор, ЭтоПоступление = Ложь, ТипДокумента = "ПриходныйКассовыйОрдер")
	
	ДанныеЗаполнения = ДанныеШапкиДокументаДДСПоДоговоруЗаймаСотруднику(Договор, ЭтоПоступление);
	РасшифровкаПлатежа = ДанныеРасшифровкиПлатежаДокументаДДСПоДоговоруЗаймаСотруднику(ДанныеЗаполнения, ЭтоПоступление,, ТипДокумента);
	ДанныеЗаполнения.Вставить("РасшифровкаПлатежа",РасшифровкаПлатежа);
	
	Возврат ДанныеЗаполнения;
	
КонецФункции

// Функция возвращает данные заполнения шапки платежного документа по договору займа сотруднику.
//
// Параметры:
//	Договор - ДокументСсылка.ДоговорЗаймаСотруднику - Договор займа, по которому формируется платежный документ.
//	ЭтоПоступление - Булево - Признак, определяющий направление движение денежных средств.
//
// Возвращаемое значение
//	Структура - Данные заполнения шапки платежного документа.
Функция ДанныеШапкиДокументаДДСПоДоговоруЗаймаСотруднику(Договор, ЭтоПоступление = Ложь)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	&Договор КАК ДоговорЗаймаСотруднику,
	|	&Договор КАК ДокументОснование,
	|	ДоговорЗаймаСотруднику.Организация КАК Организация,
	|	ДоговорЗаймаСотруднику.ФизическоеЛицо КАК ПодотчетноеЛицо,
	|	ДоговорЗаймаСотруднику.ФизическоеЛицо.Наименование КАК Выдать,
	|	ДоговорЗаймаСотруднику.ФизическоеЛицо.Наименование КАК ПринятоОт,
	|	ВЫБОР
	|		КОГДА &ЭтоПоступление
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПогашениеЗаймаСотрудником)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаЗаймаСотруднику)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	НЕ ДоговорЗаймаСотруднику.Проведен КАК ЕстьОшибкиПроведен,
	|	ДоговорЗаймаСотруднику.Организация.ВалютаРегламентированногоУчета КАК Валюта,
	|	Неопределено КАК Касса,
	|	Неопределено КАК БанковскийСчет,
	|	Неопределено КАК БанковскийСчетКонтрагента
	|ИЗ
	|	Документ.ДоговорЗаймаСотруднику КАК ДоговорЗаймаСотруднику
	|ГДЕ
	|	ДоговорЗаймаСотруднику.Ссылка = &Договор";
	
	Запрос.УстановитьПараметр("Договор",        Договор);
	Запрос.УстановитьПараметр("ЭтоПоступление", ЭтоПоступление);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Если Выборка.ЕстьОшибкиПроведен Тогда
		Текст = НСтр("ru = 'Договор займа сотруднику не проведен. Ввод на основании непроведенного документа запрещен';
					|en = 'Employee loan agreement is not posted. Input on the basis of unposted document is prohibited'");
		ВызватьИсключение Текст;
	КонецЕсли;
	
	ДанныеШапки = Новый Структура;
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		ДанныеШапки.Вставить(Колонка.Имя);
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(ДанныеШапки, Выборка);
	
	ДанныеШапки.Касса = Справочники.Кассы.ПолучитьКассуПоУмолчанию(ДанныеШапки.Организация, ДанныеШапки.Валюта);
	ДанныеШапки.БанковскийСчет = Справочники.БанковскиеСчетаОрганизаций.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(ДанныеШапки.Организация, ДанныеШапки.Валюта);
	ДанныеШапки.БанковскийСчетКонтрагента = Справочники.БанковскиеСчетаКонтрагентов.ПолучитьБанковскийСчетПоУмолчанию(
			Выборка.ПодотчетноеЛицо,
			Выборка.Валюта);
	
	Возврат ДанныеШапки;
	
КонецФункции

//-- НЕ УТ

Функция ДокументыСПередачейФайловВБанк()
	
	ТипыДокументов = Новый Массив;
	ТипыДокументов.Добавить(Тип("ДокументСсылка.СправкаОПодтверждающихДокументах"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.СписаниеБезналичныхДенежныхСредств"));
	
	Возврат ТипыДокументов;
	
КонецФункции

Функция УбратьИзНаименованияОрганизационнуюФорму(Наименование, ИмяОрганизационнойФормы)

	Результат = Ложь;
	
	Наименование = СокрЛП(Наименование);
	
	ДлинаНаименования			= СтрДлина(Наименование);
	ДлинаОрганизационнойФормы	= СтрДлина(ИмяОрганизационнойФормы);
	
	Если Лев(Наименование, ДлинаОрганизационнойФормы) = ИмяОрганизационнойФормы Тогда
		Наименование	= СокрЛ(Сред(Наименование, ДлинаОрганизационнойФормы + 1));
		Результат		= Истина;
	КонецЕсли;	
	
	Если Прав(Наименование, ДлинаОрганизационнойФормы) = ИмяОрганизационнойФормы Тогда
		Наименование	= СокрП(Лев(Наименование, ДлинаНаименования - ДлинаОрганизационнойФормы - 1));
		Результат		= Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#Область БюджетСлужебные

Процедура ПроверитьЗаполнениеРеквизитовНалоговыхПлатежей(Объект, ПараметрыПроверкиЗаполнения, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения)
	
	ВидПеречисленияВБюджет = Объект.ВидПеречисленияВБюджет;
	ПоказательОснования = Объект.ПоказательОснования;
	ПоказательПериода = Объект.ПоказательПериода;
	ПоказательТипа = Объект.ПоказательТипа;
	ПоказательНомера = Объект.ПоказательНомера;
	ПоказательДаты = Объект.ПоказательДаты;
	
	ПроверитьЗаполнениеКБК(Объект, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения);
	ПроверитьЗаполнениеОКТМО(Объект, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения);
	ПроверитьЗаполнениеУИН(Объект, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения);
	ПроверитьЗаполнениеПоказателяПериода(Объект, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения);
	
	ДействуетПриказ199н_202110 = ПлатежиВБюджетКлиентСервер.ДействуетПриказ199н_202110(Объект.Дата);
	
	Если ПлатежиВБюджетКлиентСервер.ОснованияПлатежа(
		ВидПеречисленияВБюджет, ПараметрыПроверкиЗаполнения.ПрименениеПриказа107н, Объект.Дата).НайтиПоЗначению(ПоказательОснования) = Неопределено
		И ПоказательТипа <> "0" Тогда
		ТекстОшибки = НСтр("ru = 'Неверное значение поля ""Основание""';
							|en = 'Incorrect value of the ""Basis"" field '");
		Если ФлагОбменСБанками Тогда
			ДобавитьОшибкуЗаполнения(ОшибкиЗаполнения, ТекстОшибки);
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "НадписьРеквизитыПлатежаВБюджет",, Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ПараметрыПроверкиЗаполнения.ПрименениеПриказа126н Тогда
		Если ПлатежиВБюджетКлиентСервер.ТипыПлатежа(ВидПеречисленияВБюджет, ПараметрыПроверкиЗаполнения.ПрименениеПриказа107н).НайтиПоЗначению(ПоказательТипа) = Неопределено
			И ПоказательТипа <> "0" Тогда
			ТекстОшибки = НСтр("ru = 'Неверно указано значение в поле ""Тип платежа""';
								|en = 'Incorrect value in the ""Payment type"" field.'");
			Если ФлагОбменСБанками Тогда
				ДобавитьОшибкуЗаполнения(ОшибкиЗаполнения, ТекстОшибки);
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "НадписьРеквизитыПлатежаВБюджет",, Отказ);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если СтрНайти("АП,АР", ПоказательОснования) > 0 Тогда
		Если ЗначениеЗаполнено(ПоказательПериода)
			И ПоказательПериода <> "0" Тогда
			ТекстОшибки = НСтр("ru = 'При основании платежа ""АП"" или ""АР"" следует указать ""0"" в поле ""Период""';
								|en = 'If the payment basis is ""AP"" or ""AR"", you should specify ""0"" in the ""Period"" field'");
			Если ФлагОбменСБанками Тогда
				ДобавитьОшибкуЗаполнения(ОшибкиЗаполнения, ТекстОшибки);
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "НадписьРеквизитыПлатежаВБюджет",, Отказ);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли СтрНайти("ТП,ЗД", ПоказательОснования) > 0	Тогда
		Если ЗначениеЗаполнено(ПоказательНомера) 
			И ПоказательНомера <> "0"
			И (НЕ ДействуетПриказ199н_202110
			ИЛИ ДействуетПриказ199н_202110
			И ПоказательОснования = "ТП") Тогда
			ТекстОшибки = СтрШаблон(НСтр("ru = 'При основании платежа ""%1"" необходимо указывать ""0"" в поле ""Номер""';
										|en = 'If the payment basis is %1, specify ""0"" in the ""Number"" field'"), ПоказательОснования);
			Если ФлагОбменСБанками Тогда
				ДобавитьОшибкуЗаполнения(ОшибкиЗаполнения, ТекстОшибки);
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "НадписьРеквизитыПлатежаВБюджет",, Отказ);
			КонецЕсли;
		КонецЕсли;
		
		ПоказательДатыСтрокой = ПоказательДаты;
		Если Лев(ПоказательДатыСтрокой, 1) = "0"
			И Сред(ПоказательДатыСтрокой, 2, 2) = " ." Тогда
			ПоказательДатыСтрокой = "0";
		КонецЕсли;
		
		Если ПустаяСтрока(СтрЗаменить(ПоказательДатыСтрокой, ".", "")) Тогда
			ПоказательДатыСтрокой = "";
		КонецЕсли;
		
		Если ПоказательОснования = "ЗД" Тогда
			Если ЗначениеЗаполнено(ПоказательДатыСтрокой)
				И ПоказательДатыСтрокой <> "0"
				И (НЕ ДействуетПриказ199н_202110
				ИЛИ ДействуетПриказ199н_202110
				И ПоказательНомера = "0") Тогда
				ТекстОшибки = НСтр("ru = 'При основании платежа ""ЗД"" следует указать ""0"" в поле ""Дата""';
									|en = 'If the payment basis is ""ZD"", you should specify ""0"" in the ""Date"" field '");
				Если ФлагОбменСБанками Тогда
					ДобавитьОшибкуЗаполнения(ОшибкиЗаполнения, ТекстОшибки);
				Иначе
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "НадписьРеквизитыПлатежаВБюджет",, Отказ);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ПоказательОснования = "ЗД"
			И ДействуетПриказ199н_202110 Тогда
			
			Если ПоказательНомера <> "0" Тогда
				
				Префикс = Лев(ПоказательНомера, 2);
				
				Если СтрНайти("ТР,ПР,АП,АР", Префикс) = 0 Тогда
					
					ТекстОшибки = НСтр("ru = 'Необходимо указать номер документа с префиксом (ТР, ПР, АП или АР)';
										|en = 'Specify a document number with prefix (TR, PR, AP or AR)'");
					
					Если ФлагОбменСБанками Тогда
						ДобавитьОшибкуЗаполнения(ОшибкиЗаполнения, ТекстОшибки);
					Иначе
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "НадписьРеквизитыПлатежаВБюджет",, Отказ);
					КонецЕсли;
					
				КонецЕсли;
				
				Если ПоказательДатыСтрокой = "0" Тогда
					
					ТекстОшибки = НСтр("ru = 'Необходимо указать дату документа';
										|en = 'Specify a document date'");
					
					Если ФлагОбменСБанками Тогда
						ДобавитьОшибкуЗаполнения(ОшибкиЗаполнения, ТекстОшибки);
					Иначе
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "НадписьРеквизитыПлатежаВБюджет",, Отказ);
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеРеквизитовТаможенныхПлатежей(Объект, ПараметрыПроверкиЗаполнения, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения)
	
	ВидПеречисленияВБюджет = Объект.ВидПеречисленияВБюджет;
	ПоказательОснования = Объект.ПоказательОснования;
	ПоказательПериода = Объект.ПоказательПериода;
	ПоказательТипа = Объект.ПоказательТипа;
	
	ПроверитьЗаполнениеКБК(Объект, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения);
	ПроверитьЗаполнениеОКТМО(Объект, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения);
	ПроверитьЗаполнениеУИН(Объект, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения);
	
	Если ПлатежиВБюджетКлиентСервер.ОснованияПлатежа(
		ВидПеречисленияВБюджет, ПараметрыПроверкиЗаполнения.ПрименениеПриказа107н,
		?(Объект.Дата = Дата('00010101'), ТекущаяДатаСеанса(), Объект.Дата)).НайтиПоЗначению(ПоказательОснования) = Неопределено
		И ПоказательТипа <> "0" Тогда
		ТекстОшибки = НСтр("ru = 'Неверное значение поля ""Основание""';
							|en = 'Incorrect value of the ""Basis"" field '");
		Если ФлагОбменСБанками Тогда
			ДобавитьОшибкуЗаполнения(ОшибкиЗаполнения, ТекстОшибки);
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "НадписьРеквизитыПлатежаВБюджет",, Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПоказательПериода) Тогда
		ТекстОшибки = НСтр("ru = 'Необходимо заполнить значение поля ""Код таможенного органа""';
							|en = 'Fill in the ""Customs authority code"" field'");
		Если ФлагОбменСБанками Тогда
			ДобавитьОшибкуЗаполнения(ОшибкиЗаполнения, ТекстОшибки);
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "НадписьРеквизитыПлатежаВБюджет",, Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ПараметрыПроверкиЗаполнения.ПрименениеПриказа126н Тогда
		Если ПлатежиВБюджетКлиентСервер.ТипыПлатежа(ВидПеречисленияВБюджет, ПараметрыПроверкиЗаполнения.ПрименениеПриказа107н).НайтиПоЗначению(ПоказательТипа) = Неопределено
			И ПоказательТипа <> "0" Тогда
			ТекстОшибки = НСтр("ru = 'Неверно указано значение в поле ""Тип платежа""';
								|en = 'Incorrect value in the ""Payment type"" field.'");
			Если ФлагОбменСБанками Тогда
				ДобавитьОшибкуЗаполнения(ОшибкиЗаполнения, ТекстОшибки);
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "НадписьРеквизитыПлатежаВБюджет",, Отказ);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеРеквизитовИныхПлатежейВБюджет(Объект, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения)
	
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("ПоказательПериода", "Период");
	СтруктураПолей.Вставить("ПоказательНомера", НСтр("ru = 'Номер документа';
													|en = 'Document number'"));
	СтруктураПолей.Вставить("ПоказательДаты", НСтр("ru = 'Дата документа';
													|en = 'Document date'"));
	
	ПроверитьЗаполнениеКБК(Объект, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения);
	ПроверитьЗаполнениеОКТМО(Объект, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения);
	ПроверитьЗаполнениеУИН(Объект, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения);
	
	Если Объект.СтатусСоставителя = "08" Тогда
		Для каждого ЭлементСтруктуры Из СтруктураПолей Цикл
			Если Объект[ЭлементСтруктуры.Ключ] <> "0" Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'При статусе составителя ""08"" следует указать ""0"" в поле ""%1""';
						|en = 'Specify ""0"" in field ""%1"" if the author status is ""08""'"),
					ЭлементСтруктуры.Значение);
				Если ФлагОбменСБанками Тогда
					ДобавитьОшибкуЗаполнения(ОшибкиЗаполнения, ТекстОшибки);
				Иначе
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "НадписьРеквизитыПлатежаВБюджет",, Отказ);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеПоказателяПериода(Объект, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения)
	ПоказательПериода = Объект.ПоказательПериода;
	ПоказательОснования = Объект.ПоказательОснования;
	
	ОшибкаЗаполненияПериода = Ложь;
	РасшифровкаОшибки = "";
	
	Если ПлатежиВБюджетКлиентСервер.ДействуетПриказ199н_202110(Объект.Дата) Тогда
	
		ПоказателиДляПроверкиКонкретнойДаты = "РС,ОТ,РТ,ВУ,ПБ,ИН,ЗД";
		ПоказателиДляПроверки = "ТП";
		
	Иначе
	
		ПоказателиДляПроверкиКонкретнойДаты = "ТР,РС,ОТ,РТ,ВУ,ПБ,ПР";
		ПоказателиДляПроверки = "ТП,ЗД";
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПоказательПериода) Тогда
		Если СтрНайти(ПоказателиДляПроверки, ПоказательОснования) > 0 И ПоказательПериода <> "0" Тогда
			ПериодичностьНалога = Сред((ПоказательПериода), 1, 2);
			
			НомерПериода = Сред((ПоказательПериода), 4, 2);
			НомерПериода = ?(ПустаяСтрока(НомерПериода), 0, Число(НомерПериода));
			
			ГодПериода = Сред((ПоказательПериода), 7, 4);
			ГодПериода = ?(ПустаяСтрока(ГодПериода), 0, Число(ГодПериода));
			
			Если СтрНайти("МС,КВ,ПЛ,ГД", ПериодичностьНалога) > 0 Тогда
				Если СтрДлина(ПоказательПериода) - СтрДлина(СтрЗаменить(ПоказательПериода, ".", "")) <> 2
				 ИЛИ ГодПериода < 2000 Тогда
					ОшибкаЗаполненияПериода = Истина;
				КонецЕсли;
			КонецЕсли;
			
			Если ПериодичностьНалога = "МС" Тогда
				Если НомерПериода < 1
				 ИЛИ НомерПериода > 12 Тогда
					ОшибкаЗаполненияПериода = Истина;
					РасшифровкаОшибки = Нстр("ru = 'четвертый и пятый знак периода платежа заполнены некорректно,
										|для месячных платежей необходимо заполнить порядковый номер месяца (с 1 по 12).';
										|en = 'the fourth and the fifth digit of the payment period are filled in incorrectly,
										|fill in the month sequence number (from 1 to 12) for monthly payments.'");
				КонецЕсли;
				
			ИначеЕсли ПериодичностьНалога = "КВ" Тогда
				Если НомерПериода < 1
				 ИЛИ НомерПериода > 4 Тогда
					ОшибкаЗаполненияПериода = Истина;
					РасшифровкаОшибки = Нстр("ru = 'четвертый и пятый знак периода платежа заполнены некорректно,
										|для квартальных платежей необходимо заполнить порядковый номер квартала (с 1 по 4).';
										|en = 'the fourth and the fifth digit of the payment period are filled in incorrectly,
										|fill in the quarter sequence number (from 1 to 4) for quarterly payments.'");
				КонецЕсли;
				
			ИначеЕсли ПериодичностьНалога = "ПЛ" > 0 Тогда
				Если НомерПериода < 1
				 ИЛИ НомерПериода > 2 Тогда
					ОшибкаЗаполненияПериода = Истина;
					РасшифровкаОшибки = Нстр("ru = 'четвертый и пятый знак периода платежа заполнены некорректно,
										|для полугодовых платежей необходимо заполнить порядковый номер полугодия (01 или 02).';
										|en = 'the fourth and the fifth digit of the payment period are filled in incorrectly,
										|fill in the half-year sequence number (01 or 02) for semiannual payments.'");
				КонецЕсли;
				
			ИначеЕсли ПериодичностьНалога = "ГД" Тогда
				Если НомерПериода <> 0 Тогда
					ОшибкаЗаполненияПериода = Истина;
					РасшифровкаОшибки = Нстр("ru = 'четвертый и пятый знак периода платежа заполнены некорректно
											|для годовых платежей необходимо заполнить значением 00.';
											|en = 'the fourth and the fifth digit of the payment period are filled in incorrectly,
											|fill in with value 00 for annual payments.'");
				КонецЕсли;
				
			Иначе
				ПроверитьДатуПериода(Объект, ОшибкаЗаполненияПериода);
			КонецЕсли;
			
		ИначеЕсли СтрНайти(ПоказателиДляПроверкиКонкретнойДаты, ПоказательОснования) > 0
			И ПоказательПериода <> "0" Тогда
			
			ПериодичностьНалога = Сред((ПоказательПериода), 1, 2);
			
			Если СтрНайти("МС,КВ,ПЛ,ГД", ПериодичностьНалога) > 0 Тогда
				ОшибкаЗаполненияПериода = Истина;
				РасшифровкаОшибки = Нстр("ru = 'необходимо указать конкретный период платежа.';
										|en = 'specify a particular payment period.'");
			Иначе
				ПроверитьДатуПериода(Объект, ОшибкаЗаполненияПериода);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ОшибкаЗаполненияПериода Тогда
		ТекстОшибки = НСтр("ru = 'Неверно указано значение в поле ""Период""';
							|en = 'Incorrect value in the ""Period"" field.'");
		
		Если РасшифровкаОшибки <> "" Тогда
			ТекстОшибки = ТекстОшибки + ":" + Символы.ПС + РасшифровкаОшибки;
		КонецЕсли;
		
		Если ФлагОбменСБанками Тогда
			ДобавитьОшибкуЗаполнения(ОшибкиЗаполнения, ТекстОшибки);
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "НадписьРеквизитыПлатежаВБюджет",, Отказ);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьЗаполнениеКБК(Объект, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения)
	
	КБК = Объект.КодБК;
	
	Если Объект.ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.ИнойПлатеж И СокрЛП(КБК) = "0" Тогда
		Возврат;
	КонецЕсли;
	
	ТекстОшибки = "";
	
	// Проверим валидность введенного кода
	Если СтрДлина(СокрП(КБК)) <> 20 Тогда
		
		ТекстОшибки =  НСтр("ru = 'КБК должен состоять из 20 знаков';
							|en = 'BCC must contain 20 characters'");
		
	ИначеЕсли Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(КБК) Тогда
		
		ТекстОшибки = НСтр("ru = 'КБК должен содержать только цифры';
							|en = 'BCC must contain only digits'");
		
	ИначеЕсли ПустаяСтрока(СокрЛП(СтрЗаменить(КБК, "0", ""))) Тогда
		
		ТекстОшибки = НСтр("ru = 'Все знаки КБК не могут одновременно принимать значение ""0""';
							|en = 'All BCC digits cannot be ""0"" at a time'");
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		Если ФлагОбменСБанками Тогда
			ДобавитьОшибкуЗаполнения(ОшибкиЗаполнения, ТекстОшибки);
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "НадписьРеквизитыПлатежаВБюджет",, Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеОКТМО(Объект, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения)
	
	КодОКАТО = Объект.КодОКАТО;
	
	ТекстОшибки = "";
	
	// Проверим валидность введенного кода
	Если СтрДлина(СокрЛП(КодОКАТО)) > 1 И ПустаяСтрока(СокрЛП(СтрЗаменить(КодОКАТО, "0", ""))) Тогда
		ТекстОшибки = НСтр("ru = 'Все знаки ОКТМО не могут одновременно принимать значение ""0""';
							|en = 'All RNCMT digits cannot be ""0"" at a time'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		Если ФлагОбменСБанками Тогда
			ДобавитьОшибкуЗаполнения(ОшибкиЗаполнения, ТекстОшибки);
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "НадписьРеквизитыПлатежаВБюджет",, Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеУИН(Объект, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения)
	
	УИН = Объект.ИдентификаторПлатежа;
	
	ТекстОшибки = "";
	
	// Проверим валидность введенного кода
	Если СтрДлина(СокрП(УИН)) <> 20 И СтрДлина(СокрП(УИН)) <> 25 И СтрДлина(СокрП(УИН)) <> 1 И СтрДлина(СокрП(УИН)) <> 0
		И СтрДлина(СокрЛП(УИН)) <> 4 Тогда
		
		ТекстОшибки =  НСтр("ru = 'УИН должен состоять из 20 или 25 знаков.
			|В случае отсутствия Уникального идентификатора начисления указывается значение ""0"".';
			|en = 'Payment ID must consist of 20 or 25 characters.
			|If there is no Unique ID of charges, ""0"" is specified.'");
		
	ИначеЕсли ПустаяСтрока(СокрЛП(СтрЗаменить(УИН, "0", ""))) И СтрДлина(СокрП(УИН)) <> 0 И СокрП(УИН) <> "0" Тогда
		
		ТекстОшибки = НСтр("ru = 'Все знаки УИН не могут одновременно принимать значение ""0""';
							|en = 'All payment ID digits cannot be ""0"" at a time'");
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		Если ФлагОбменСБанками Тогда
			ДобавитьОшибкуЗаполнения(ОшибкиЗаполнения, ТекстОшибки);
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				Объект,
				"ИдентификаторПлатежа",
				,
				Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьДатуПериода(Объект, ОшибкаЗаполненияПериода)
	
	ПоказательПериода = Объект.ПоказательПериода;
	
	ПозицияПериода = СтрНайти(СокрЛП(ПоказательПериода),".");
	ПозицияГода = СтрНайти(Сред(СокрЛП(ПоказательПериода), ПозицияПериода + 1), ".") + ПозицияПериода;

	ДатаПериода = Лев(СокрЛП(ПоказательПериода), ПозицияПериода - 1);
	НомерПериода = Сред(СокрЛП(ПоказательПериода), ПозицияПериода + 1, ПозицияГода - ПозицияПериода - 1);
	ГодПериода = Сред(СокрЛП(ПоказательПериода), ПозицияГода + 1);
	
	Попытка
		ДатаПоказателя = Дата(Число(ГодПериода), Число(НомерПериода), Число(ДатаПериода));
	Исключение
		ОшибкаЗаполненияПериода = Истина;
	КонецПопытки
	
КонецПроцедуры

#КонецОбласти

//++ НЕ УТ

#Область ГОЗСлужебные

#Область Общие

// По имени обработчика контроля возвращает номер раздела контролей
//
// Параметры:
//   ИмяОбработчика - Строка - Имя обработчика.
//
// Возвращаемое значение:
//   Строка - Раздел
//
Функция РазделКонтроля(ИмяОбработчика)
	РазделКонтроля = "";
	
	Для Сч = 1 По 9 Цикл 
		Если СтрНайти(ИмяОбработчика, "Раздел" + Сч + "_") > 0 Тогда
			РазделКонтроля = "Раздел" + Сч;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат РазделКонтроля;
КонецФункции

// Формирует назначение платежа для уплаты налогов
//
// Параметры:
//   КБК - Строка - КБК
//   ВидНалоговогоОбязательства - ПеречислениеСсылка.ВидыПлатежейВГосБюджет - Вид налогового обязательства
//   Организация - СправочникСсылка.Организации - Организация
//   Период - Дата - Период
//   ПредставлениеНалоговогоПериода - Строка - Налоговый период строкой.
//
// Возвращаемое значение:
//  Строка - назначение платежа
//
Функция НазначениеПлатежаДляУплатыНалогов(КБК, ВидНалоговогоОбязательства, Организация = Неопределено, Знач Период = Неопределено, ПредставлениеНалоговогоПериода = "")
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	НазначениеПлатежа = "";
	Если ЗначениеЗаполнено(КБК) Тогда
		ВидНалога         = ДенежныеСредстваПовтИспРФ.ВидНалогаПоКБК(КБК);
		НазначениеПлатежа = ДенежныеСредстваПовтИспРФ.НазначениеПлатежаПоКБК(КБК);
		ДатаАктуальности  = Дата('20150101');
	КонецЕсли;
	
	Если Не ПустаяСтрока(ПредставлениеНалоговогоПериода) И ВидНалоговогоОбязательства <> "Штраф" Тогда
		НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<Период>", ПредставлениеНалоговогоПериода);
	Иначе
		НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<Период>", "");
	КонецЕсли;
	
	// При уплате страховых взносов, как правило, требуется указывать регистрационный номер.
	КлючиРеквизитов = "РегистрационныйНомерПФР, ИПРегистрационныйНомерПФР, РегистрационныйНомерФСС, ИПРегистрационныйНомерФСС";
	РеквизитыОрганизации = ?(ЗначениеЗаполнено(Организация), ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, КлючиРеквизитов), Новый Структура(КлючиРеквизитов));
	
	НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<ПФР>",    РеквизитыОрганизации.РегистрационныйНомерПФР);
	НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<ПФР_ИП>", РеквизитыОрганизации.ИПРегистрационныйНомерПФР);
	НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<ФСС>",    РеквизитыОрганизации.РегистрационныйНомерФСС);
	НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "<ФСС_ИП>", РеквизитыОрганизации.ИПРегистрационныйНомерФСС);
	
	НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "  ", " ");
	
	Возврат НазначениеПлатежа;
	
КонецФункции

Функция ПредставлениеВидаДокумента(ВидДокумента, СоответствиеРеквизитов = Неопределено)
	Если Не ЗначениеЗаполнено(ВидДокумента) Тогда
		Возврат "";
	КонецЕсли;
	
	Если СоответствиеРеквизитов = Неопределено Тогда
		РеквизитыВидаДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидДокумента, "КраткоеНаименование, Наименование");
	Иначе
		РеквизитыВидаДокумента = СоответствиеРеквизитов[ВидДокумента];
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыВидаДокумента.КраткоеНаименование) Тогда
		ПредставлениеВидДокумента = РеквизитыВидаДокумента.КраткоеНаименование;
	Иначе
		ПредставлениеВидДокумента = РеквизитыВидаДокумента.Наименование;
	КонецЕсли;
	
	Возврат ПредставлениеВидДокумента;
КонецФункции

Функция ПредставлениеПодтверждающегоДокумента(Знач ПредставлениеВидаДокумента, Знач НомерДокумента, Знач ДатаДокумента)
	МассивСтрокПодтверждающийДокумент = Новый Массив;
	
	МассивСтрокПодтверждающийДокумент.Добавить(ПредставлениеВидаДокумента);
	МассивСтрокПодтверждающийДокумент.Добавить("№" + НомерДокумента);
	МассивСтрокПодтверждающийДокумент.Добавить(НСтр("ru = 'от';
													|en = 'dated'"));
	МассивСтрокПодтверждающийДокумент.Добавить(Формат(ДатаДокумента, "ДФ=ddMMyyyy"));
	
	Представление = СтрСоединить(МассивСтрокПодтверждающийДокумент, " ");
	
	Возврат Представление;
КонецФункции

Функция ТребуетсяКонтрольГОЗ(СтрокиГрафика)
	
	СписокДляПроверкиГОЗ = Новый Массив;
	Для каждого СтрокаГрафика Из СтрокиГрафика Цикл
		ТипОбъектаОплаты = ТипЗнч(СтрокаГрафика.ОбъектОплаты);
		Если ТипОбъектаОплаты = Тип("ДокументСсылка.ЗаявкаНаРасходованиеДенежныхСредств") Тогда
			СписокДляПроверкиГОЗ.Добавить(СтрокаГрафика.ОбъектОплаты);
		ИначеЕсли ТипОбъектаОплаты = Тип("ДокументСсылка.РаспоряжениеНаПеремещениеДенежныхСредств") Тогда
			СписокДляПроверкиГОЗ.Добавить(СтрокаГрафика.ОбъектОплаты);
		КонецЕсли;
	КонецЦикла;
	
	Если Не СписокДляПроверкиГОЗ.Количество() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СписокДляПроверкиГОЗ = ОбщегоНазначенияУТ.УдалитьПовторяющиесяЭлементыМассива(СписокДляПроверкиГОЗ);
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	1
		|ИЗ
		|	Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка В (&СписокДляПроверкиГОЗ)
		|	И ДанныеДокумента.ПлатежиПо275ФЗ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	1
		|ИЗ
		|	Документ.РаспоряжениеНаПеремещениеДенежныхСредств КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка В (&СписокДляПроверкиГОЗ)
		|	И ДанныеДокумента.ПлатежиПо275ФЗ
		|");
		
	Запрос.УстановитьПараметр("СписокДляПроверкиГОЗ", СписокДляПроверкиГОЗ);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Функция ИнициализацияСвойствРезультатовПроверок()
	СвойстваРезультатовПроверок = Новый Структура("ВсеКонтролиПройдены, ОписанияРезультатов", Истина);
	
	ОписанияОшибок = Новый ТаблицаЗначений;
	ОписанияОшибок.Колонки.Добавить("ИмяОбработчикаКонтроля");
	ОписанияОшибок.Колонки.Добавить("КонтрольПройден");
	ОписанияОшибок.Колонки.Добавить("Описание");
	ОписанияОшибок.Колонки.Добавить("Раздел");
	
	СвойстваРезультатовПроверок.Вставить("ОписанияРезультатов", ОписанияОшибок);
	
	Возврат СвойстваРезультатовПроверок;
КонецФункции

Функция ТипДокумента(Ссылка)
	Результат = Новый Структура;
	
	ТипЗначения = ТипЗнч(Ссылка);
	Контролируется = ТипЗначения = Тип("ДокументСсылка.ЗаявкаНаРасходованиеДенежныхСредств")
		ИЛИ ТипЗначения = Тип("ДокументСсылка.СписаниеБезналичныхДенежныхСредств");
	
	Результат.Вставить("ТипЗначения", ТипЗначения);
	Результат.Вставить("Контролируется", Контролируется);
	
	Возврат Результат;
КонецФункции

Функция ОписаниеТекстаЗапроса()
	ОписаниеТекстаЗапроса = Новый Структура("Текст, КоличествоВременныхТаблиц");
	
	ОбъединяемыеЗапросы = Новый Массив;
	ОбъединяемыеЗапросы.Добавить(ТекстЗапросаРезультатКонтроля_Раздел1_275_83_1_1());
	ОбъединяемыеЗапросы.Добавить(ТекстЗапросаРезультатКонтроля_Раздел1_275_83_1_2_б());
	ОбъединяемыеЗапросы.Добавить(ТекстЗапросаРезультатКонтроля_Раздел1_275_83_1_2_в());
	ОбъединяемыеЗапросы.Добавить(ТекстЗапросаРезультатКонтроля_Раздел1_275_83_1_2_г());
	ОбъединяемыеЗапросы.Добавить(ТекстЗапросаРезультатКонтроля_Раздел1_275_83_1_2_е());
	ОбъединяемыеЗапросы.Добавить(ТекстЗапросаРезультатКонтроля_Раздел1_275_83_1_2_з());
	ОбъединяемыеЗапросы.Добавить(ТекстЗапросаРезультатКонтроля_Раздел1_275_84_2());
	ОбъединяемыеЗапросы.Добавить(ТекстЗапросаРезультатКонтроля_Раздел1_275_84_10());
	ОбъединяемыеЗапросы.Добавить(ТекстЗапросаРезультатКонтроля_Раздел1_275_85_1_1());
	ОбъединяемыеЗапросы.Добавить(ТекстЗапросаРезультатКонтроля_Раздел1_275_85_2_1());
	ОбъединяемыеЗапросы.Добавить(ТекстЗапросаРезультатКонтроля_Раздел1_275_85_2_2());
	ОбъединяемыеЗапросы.Добавить(ТекстЗапросаРезультатКонтроля_Раздел2_3729У_2_1());
	ОбъединяемыеЗапросы.Добавить(ТекстЗапросаРезультатКонтроля_Раздел2_3729У_2_2());
	ОбъединяемыеЗапросы.Добавить(ТекстЗапросаРезультатКонтроля_Раздел2_3729У_2_3());
	
	СхемаЗапроса = Новый СхемаЗапроса;
	ВременныеТаблицы = Новый Массив;
	РезультирующиеВыборки = Новый Массив;
	УничтожениеОбщихВременныхТаблиц = Новый Массив;
	
	ШаблонПрефиксаТаблиц = "ЗапросКонтроля%1_";
	ИндексОбъединяемогоЗапроса = 0;
	Для Каждого ОбъединяемыйЗапрос Из ОбъединяемыеЗапросы Цикл 
		ПрефиксТаблиц = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПрефиксаТаблиц, ИндексОбъединяемогоЗапроса);
		СхемаЗапроса.УстановитьТекстЗапроса(ОбъединяемыйЗапрос);
		
		ИменаВременныхТаблиц = Новый Соответствие;
		
		Для Каждого ПодзапросОбъединяемогоЗапроса Из СхемаЗапроса.ПакетЗапросов Цикл 
			Если Не ПустаяСтрока(ПодзапросОбъединяемогоЗапроса.ТаблицаДляПомещения) Тогда // Временная таблица подзапроса
				УточнитьИменаВременныхТаблицВЗапросе(ПодзапросОбъединяемогоЗапроса, ИменаВременныхТаблиц, ПрефиксТаблиц);
				
				ИменаВременныхТаблиц.Вставить(ПодзапросОбъединяемогоЗапроса.ТаблицаДляПомещения, ПрефиксТаблиц + ПодзапросОбъединяемогоЗапроса.ТаблицаДляПомещения);
				ПодзапросОбъединяемогоЗапроса.ТаблицаДляПомещения = ИменаВременныхТаблиц[ПодзапросОбъединяемогоЗапроса.ТаблицаДляПомещения];
				ВременныеТаблицы.Добавить(ПодзапросОбъединяемогоЗапроса.ПолучитьТекстЗапроса());
			Иначе // Результирующая таблица подзапроса
				УточнитьИменаВременныхТаблицВЗапросе(ПодзапросОбъединяемогоЗапроса, ИменаВременныхТаблиц, ПрефиксТаблиц);
				
				РезультирующиеВыборки.Добавить(ПодзапросОбъединяемогоЗапроса.ПолучитьТекстЗапроса());
			КонецЕсли;
		КонецЦикла;
		
		ИндексОбъединяемогоЗапроса = ИндексОбъединяемогоЗапроса + 1; 
	КонецЦикла;
	
	РазделительЗапросов = "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////" + Символы.ПС;
	
	ТекстЗапросаВременныеТаблицы = СтрСоединить(ВременныеТаблицы, РазделительЗапросов);
	ТекстЗапросаРезультирующиеВыборки = СтрСоединить(РезультирующиеВыборки, РазделительЗапросов);
	
	ОписаниеТекстаЗапроса.Вставить("Текст", ТекстЗапросаВременныеТаблицы + РазделительЗапросов + ТекстЗапросаРезультирующиеВыборки);
	ОписаниеТекстаЗапроса.Вставить("КоличествоВременныхТаблиц", ВременныеТаблицы.Количество());
	
	Возврат ОписаниеТекстаЗапроса;
КонецФункции

Процедура УточнитьИменаВременныхТаблицВЗапросе(ЗапросСхемыЗапроса, ИменаВременныхТаблиц, ПрефиксТаблиц)
	ЗапросУточняемыйПоИменамВременныхТаблиц = ЗапросСхемыЗапроса.ПолучитьТекстЗапроса();
	Для Каждого ИмяВременнойТаблицы Из ИменаВременныхТаблиц Цикл 
		ЗапросУточняемыйПоИменамВременныхТаблиц = СтрЗаменить(ЗапросУточняемыйПоИменамВременныхТаблиц, ИмяВременнойТаблицы.Ключ, ИмяВременнойТаблицы.Значение); 
		ЗапросУточняемыйПоИменамВременныхТаблиц = СтрЗаменить(ЗапросУточняемыйПоИменамВременныхТаблиц, ПрефиксТаблиц + ИмяВременнойТаблицы.Значение, ИмяВременнойТаблицы.Значение); 
	КонецЦикла;
	ЗапросСхемыЗапроса.УстановитьТекстЗапроса(ЗапросУточняемыйПоИменамВременныхТаблиц);
КонецПроцедуры

Функция ИнициализацияИндексовРезультатовЗапросов()
	ИндексыРезультатовЗапроса = Новый Соответствие;
	
	// Контроль раздела I "В соответствии с 275-ФЗ от 29.12.2012г."
	ИндексыРезультатовЗапроса.Вставить("РезультатКонтроля_Раздел1_275_83_1_1", 0);
	ИндексыРезультатовЗапроса.Вставить("РезультатКонтроля_Раздел1_275_83_1_2_б", 1);
	ИндексыРезультатовЗапроса.Вставить("РезультатКонтроля_Раздел1_275_83_1_2_в", 2);
	ИндексыРезультатовЗапроса.Вставить("РезультатКонтроля_Раздел1_275_83_1_2_г", 3);
	ИндексыРезультатовЗапроса.Вставить("РезультатКонтроля_Раздел1_275_83_1_2_е", 4);
	ИндексыРезультатовЗапроса.Вставить("РезультатКонтроля_Раздел1_275_83_1_2_з", 5);
	
	ИндексыРезультатовЗапроса.Вставить("РезультатКонтроля_Раздел1_275_84_2", 6);
	ИндексыРезультатовЗапроса.Вставить("РезультатКонтроля_Раздел1_275_84_10", 7);
	
	ИндексыРезультатовЗапроса.Вставить("РезультатКонтроля_Раздел1_275_85_1_1", 8);
	ИндексыРезультатовЗапроса.Вставить("РезультатКонтроля_Раздел1_275_85_2_1", 9);
	ИндексыРезультатовЗапроса.Вставить("РезультатКонтроля_Раздел1_275_85_2_2", 10);
	//
	// Контроль раздела II "Указание Банка России 3729-У от 15.07.2015г."
	ИндексыРезультатовЗапроса.Вставить("РезультатКонтроля_Раздел2_3729У_2_1", 11);
	ИндексыРезультатовЗапроса.Вставить("РезультатКонтроля_Раздел2_3729У_2_2", 12);
	ИндексыРезультатовЗапроса.Вставить("РезультатКонтроля_Раздел2_3729У_2_3", 13);
	
	Возврат ИндексыРезультатовЗапроса;
КонецФункции

Функция ИндексРезультатаЗапроса(ИндексыРезультатовЗапросов, ИмяОбработчикаКонтроля)
	Возврат ИндексыРезультатовЗапросов[ИмяОбработчикаКонтроля];
КонецФункции

Процедура ДобавитьОписаниеРезультатаКонтроля(Знач ИмяОбработчикаКонтроля, Знач КонтрольПройден, Знач ОписаниеМассивСтрок, Знач ОписанияРезультатов)
	НовоеОписание = ОписанияРезультатов.Добавить();
	НовоеОписание.ИмяОбработчикаКонтроля = ИмяОбработчикаКонтроля;
	НовоеОписание.КонтрольПройден = КонтрольПройден;
	НовоеОписание.Описание = СтрСоединить(ОписаниеМассивСтрок, Символы.ПС + Символы.ПС);
	НовоеОписание.Раздел = РазделКонтроля(ИмяОбработчикаКонтроля);
КонецПроцедуры

#КонецОбласти

#Область КонтрольРаздела1
// Контроль раздела I "В соответствии с 275-ФЗ"

#Область Пункт83

Процедура РезультатКонтроля_Раздел1_275_83_1_1(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, Знач ДанныеДокументовДляКонтроля)
	ТипДокумента = ТипДокумента(Ссылка);
	Если НЕ ТипДокумента.Контролируется Тогда
		Возврат;
	КонецЕсли;
	
	ОписанияРезультатов = РезультатыПроверок.ОписанияРезультатов;
	ИмяОбработчикаКонтроля = "РезультатКонтроля_Раздел1_275_83_1_1";
	КонтрольПройден = Истина;
	
	ОписаниеМассивСтрок = Новый Массив;
	Описание = "";
	
	Если Ссылка.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ОплатаПоставщику
		И Ссылка.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет
		И ДенежныеСредстваПовтИспРФ.ПрочаяОперацияСписанияДСПо275ФЗ(Ссылка.ХозяйственнаяОперация)
		И Ссылка.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ОплатаПоКредитам
		И Ссылка.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПеречислениеНаДепозиты Тогда
		
		Описание = НСтр("ru = 'Контроль не требуется для операции ""%1"".';
						|en = 'Control is not required for transaction ""%1"".'");
		Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, Ссылка.ХозяйственнаяОперация);
		ОписаниеМассивСтрок.Добавить(Описание);
	Иначе
		Если ДанныеДокументовДляКонтроля = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = ТекстЗапросаРезультатКонтроля_Раздел1_275_83_1_1();
			ПроверяемыеДокументы = Новый Массив;
			ПроверяемыеДокументы.Добавить(Ссылка);
			Запрос.УстановитьПараметр("ПроверяемыеДокументы", ПроверяемыеДокументы);
			
			ДанныеДокументовДляКонтроляТаблица = Запрос.Выполнить().Выгрузить();
		Иначе
			ДанныеДокументовДляКонтроляТаблица = ДанныеДокументовДляКонтроля[ИндексРезультатаЗапроса(ИндексыРезультатовЗапросов, ИмяОбработчикаКонтроля)].Выгрузить();
		КонецЕсли;
		
		Отбор = Новый Структура("Ссылка", Ссылка);
		НайденныеСтроки = ДанныеДокументовДляКонтроляТаблица.НайтиСтроки(Отбор);
		
		Если НайденныеСтроки.Количество() >= 1 Тогда
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				Если НайденнаяСтрока.ТребуетсяПроверка И НайденнаяСтрока.УИП <> НайденнаяСтрока.ИГК Тогда
					КонтрольПройден = Ложь;
					
					Описание = НСтр("ru = 'В поле УИП (%1) не указан ИГК отдельного счета (%2).';
									|en = 'Separate account SCI (%2) is not specified in UIP field (%1).'");
					Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, 
						?(ЗначениеЗаполнено(НайденнаяСтрока.УИП), НайденнаяСтрока.УИП, "-"), 
						?(ЗначениеЗаполнено(НайденнаяСтрока.ИГК), НайденнаяСтрока.ИГК, "-"),);
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
			КонецЦикла;
			
			Если КонтрольПройден Тогда
				Описание = НСтр("ru = 'Контроль пройден.';
								|en = 'Controlled.'");
				ОписаниеМассивСтрок.Добавить(Описание);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ДобавитьОписаниеРезультатаКонтроля(ИмяОбработчикаКонтроля, КонтрольПройден, ОписаниеМассивСтрок, ОписанияРезультатов);
КонецПроцедуры

Процедура РезультатКонтроля_Раздел1_275_83_1_2_б(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, Знач ДанныеДокументовДляКонтроля)
	ТипДокумента = ТипДокумента(Ссылка);
	Если НЕ ТипДокумента.Контролируется Тогда
		Возврат;
	КонецЕсли;
	
	ОписанияРезультатов = РезультатыПроверок.ОписанияРезультатов;
	ИмяОбработчикаКонтроля = "РезультатКонтроля_Раздел1_275_83_1_2_б";
	КонтрольПройден = Истина;
	
	ОписаниеМассивСтрок = Новый Массив;
	Описание = "";
	
	Если Ссылка.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ОплатаПоставщику Тогда
		Описание = НСтр("ru = 'Контроль не требуется для операции ""%1"".';
						|en = 'Control is not required for transaction ""%1"".'");
		Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, Ссылка.ХозяйственнаяОперация);
		ОписаниеМассивСтрок.Добавить(Описание);
	ИначеЕсли Ссылка.ТипПлатежаФЗ275 <> Справочники.ТипыПлатежейФЗ275.ОплатаТоваровРаботУслугПоРегулируемымЦенам Тогда
		Описание = НСтр("ru = 'Контроль не требуется для типа платежа ""%1"".';
						|en = 'Control is not required for payment type ""%1"".'");
		Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, Ссылка.ХозяйственнаяОперация);
		ОписаниеМассивСтрок.Добавить(Описание);
	Иначе
		Если ДанныеДокументовДляКонтроля = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.Текст =  ТекстЗапросаРезультатКонтроля_Раздел1_275_83_1_2_б();
			ПроверяемыеДокументы = Новый Массив;
			ПроверяемыеДокументы.Добавить(Ссылка);
			Запрос.УстановитьПараметр("ПроверяемыеДокументы", ПроверяемыеДокументы);
			
			ДанныеДокументовДляКонтроляТаблица = Запрос.Выполнить().Выгрузить();
		Иначе
			ДанныеДокументовДляКонтроляТаблица = ДанныеДокументовДляКонтроля[ИндексРезультатаЗапроса(ИндексыРезультатовЗапросов, ИмяОбработчикаКонтроля)].Выгрузить();
		КонецЕсли;
		
		Отбор = Новый Структура("Ссылка", Ссылка);
		НайденныеСтроки = ДанныеДокументовДляКонтроляТаблица.НайтиСтроки(Отбор);
		
		Если НайденныеСтроки.Количество() >= 1 Тогда
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
				Если СтрЧислоВхождений(НайденнаяСтрока.НазначениеПлатежа, НайденнаяСтрока.ПредметОплаты) = 0 Тогда
					КонтрольПройден = Ложь;
					
					Описание = НСтр("ru = 'Товар, работа или услуга ""%1"", по которому применяются тарифы, не указан в назначении платежа.';
									|en = 'Goods, work or service ""%1"" by which the tariffs are applied is not specified in the purpose of payment.'");
					Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, НайденнаяСтрока.ПредметОплаты);
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
			КонецЦикла;
			
			Если КонтрольПройден Тогда
				Описание = НСтр("ru = 'Контроль пройден.';
								|en = 'Controlled.'");
				ОписаниеМассивСтрок.Добавить(Описание);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ДобавитьОписаниеРезультатаКонтроля(ИмяОбработчикаКонтроля, КонтрольПройден, ОписаниеМассивСтрок, ОписанияРезультатов);
КонецПроцедуры

Процедура РезультатКонтроля_Раздел1_275_83_1_2_в(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, Знач ДанныеДокументовДляКонтроля)
	ТипДокумента = ТипДокумента(Ссылка);
	Если НЕ ТипДокумента.Контролируется Тогда
		Возврат;
	КонецЕсли;
	
	ОписанияРезультатов = РезультатыПроверок.ОписанияРезультатов;
	ИмяОбработчикаКонтроля = "РезультатКонтроля_Раздел1_275_83_1_2_в";
	КонтрольПройден = Истина;
	
	ОписаниеМассивСтрок = Новый Массив;
	Описание = "";
	
	Если Ссылка.ТипПлатежаФЗ275 <> Справочники.ТипыПлатежейФЗ275.ПеречисленияПрибылиИсполнителю Тогда
		Описание = НСтр("ru = 'Контроль не требуется для типа платежа 275-ФЗ ""%1"".';
						|en = 'Control is not required for payment type 275-FZ ""%1"".'");
		Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, Ссылка.ТипПлатежаФЗ275);
		ОписаниеМассивСтрок.Добавить(Описание);
	Иначе
		Если ДанныеДокументовДляКонтроля = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.Текст =  ТекстЗапросаРезультатКонтроля_Раздел1_275_83_1_2_в();
			ПроверяемыеДокументы = Новый Массив;
			ПроверяемыеДокументы.Добавить(Ссылка);
			Запрос.УстановитьПараметр("ПроверяемыеДокументы", ПроверяемыеДокументы);
			Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
			
			ДанныеДокументовДляКонтроляТаблица = Запрос.Выполнить().Выгрузить();
		Иначе
			ДанныеДокументовДляКонтроляТаблица = ДанныеДокументовДляКонтроля[ИндексРезультатаЗапроса(ИндексыРезультатовЗапросов, ИмяОбработчикаКонтроля)].Выгрузить();
		КонецЕсли;
		
		Отбор = Новый Структура("Ссылка", Ссылка);
		НайденныеСтроки = ДанныеДокументовДляКонтроляТаблица.НайтиСтроки(Отбор);
		
		Если НайденныеСтроки.Количество() >= 1 Тогда
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
				// Контроль состояния
				Если НайденнаяСтрока.Состояние <> Перечисления.СостоянияГосударственныхКонтрактов.Завершен Тогда
					КонтрольПройден = Ложь;
					
					Описание = НСтр("ru = 'Контракт ""%1"" не завершен и имеет статус ""%2"".';
									|en = 'Contract ""%1"" is not completed and has status ""%2"".'"); 
					Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, 
						НайденнаяСтрока.КонтрактСЗаказчиком, НайденнаяСтрока.Состояние);
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
				
				// Контроль указания контракта
				Если Не НайденнаяСтрока.КонтрактУказан Тогда
					КонтрольПройден = Ложь;
					
					Описание = НСтр("ru = 'Не указан подтверждающий документ вида ""Контракт"".';
									|en = 'Justification document of the ""Contract"" kind is not specified.'"); 
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
				
				// Контроль указания документов, подтверждающих исполнение контракта
				Если Не НайденнаяСтрока.ДокументИсполненияУказан Тогда
					КонтрольПройден = Ложь;
					
					Описание = НСтр("ru = 'Не указан ни один документ, подтверждающий исполнение контракта.';
									|en = 'No document is specified that confirms contract execution.'");
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
				
				
				// Контроль накопленных по контракту сумм перечисления прибыли
				Если НайденнаяСтрока.СуммаДокумента + НайденнаяСтрока.СуммаФакт > НайденнаяСтрока.СуммаПлан Тогда
					КонтрольПройден = Ложь;
					
					ЧастиОписания = Новый Массив;
					Если НайденнаяСтрока.ЭтоСписание Тогда
						ЧастиОписания.Добавить(НСтр("ru = 'Сумма платежа';
													|en = 'Payment amount'")); 
					ИначеЕсли НайденнаяСтрока.ЭтоЗаявка Тогда
						ЧастиОписания.Добавить(НСтр("ru = 'Сумма заявки';
													|en = 'Request amount'")); 
					КонецЕсли;
					
					ЧастиОписания.Добавить(НСтр("ru = '%1 руб. с учетом ранее созданных распоряжений на сумму %2 руб. превышает размер прибыли %3 руб., указанный в контрактах.';
												|en = '%1 rub. considering previously created references to the amount of %2 rub. exceeds a profit of %3 rub. specified in the contracts.'"));
					
					Описание = СтрСоединить(ЧастиОписания, " ");
					
					Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, 
						Формат(НайденнаяСтрока.СуммаДокумента, "ЧДЦ=2; ЧН="), 
						Формат(НайденнаяСтрока.СуммаФакт, "ЧДЦ=2; ЧН="), 
						Формат(НайденнаяСтрока.СуммаПлан, "ЧДЦ=2; ЧН="));
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
			КонецЦикла;
			
			Если КонтрольПройден Тогда
				Описание = НСтр("ru = 'Контроль пройден.';
								|en = 'Controlled.'");
				ОписаниеМассивСтрок.Добавить(Описание);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ДобавитьОписаниеРезультатаКонтроля(ИмяОбработчикаКонтроля, КонтрольПройден, ОписаниеМассивСтрок, ОписанияРезультатов);
КонецПроцедуры

Процедура РезультатКонтроля_Раздел1_275_83_1_2_г(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, Знач ДанныеДокументовДляКонтроля)
	ТипДокумента = ТипДокумента(Ссылка);
	Если НЕ ТипДокумента.Контролируется Тогда
		Возврат;
	КонецЕсли;
	
	ОписанияРезультатов = РезультатыПроверок.ОписанияРезультатов;
	ИмяОбработчикаКонтроля = "РезультатКонтроля_Раздел1_275_83_1_2_г";
	КонтрольПройден = Истина;
	
	ОписаниеМассивСтрок = Новый Массив;
	Описание = "";
	
	Если Ссылка.ТипПлатежаФЗ275 <> Справочники.ТипыПлатежейФЗ275.ПеречислениеПрибылиГоловномуИсполнителю Тогда
		Описание = НСтр("ru = 'Контроль не требуется для типа платежа 275-ФЗ ""%1"".';
						|en = 'Control is not required for payment type 275-FZ ""%1"".'");
		Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, Ссылка.ТипПлатежаФЗ275);
		ОписаниеМассивСтрок.Добавить(Описание);
	Иначе
		Если ДанныеДокументовДляКонтроля = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.Текст =  ТекстЗапросаРезультатКонтроля_Раздел1_275_83_1_2_г();
			ПроверяемыеДокументы = Новый Массив;
			ПроверяемыеДокументы.Добавить(Ссылка);
			Запрос.УстановитьПараметр("ПроверяемыеДокументы", ПроверяемыеДокументы);
			Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
			
			ДанныеДокументовДляКонтроляТаблица = Запрос.Выполнить().Выгрузить();
		Иначе
			ДанныеДокументовДляКонтроляТаблица = ДанныеДокументовДляКонтроля[ИндексРезультатаЗапроса(ИндексыРезультатовЗапросов, ИмяОбработчикаКонтроля)].Выгрузить();
		КонецЕсли;
		
		Отбор = Новый Структура("Ссылка", Ссылка);
		НайденныеСтроки = ДанныеДокументовДляКонтроляТаблица.НайтиСтроки(Отбор);
		
		Если НайденныеСтроки.Количество() >= 1 Тогда
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
				// Контроль состояния
				Если НайденнаяСтрока.Статус = Перечисления.СтатусыДоговоровКонтрагентов.Закрыт Тогда
					КонтрольПройден = Ложь;
					
					Описание = НСтр("ru = 'Договор ""%1"" закрыт.';
									|en = 'The ""%1"" contract is closed.'"); 
					Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, 
						НайденнаяСтрока.КонтрактСЗаказчиком, 
						НайденнаяСтрока.Состояние);
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
				
				// Контроль головного исполнителя
				Если Не НайденнаяСтрока.ОрганизацияЭтоГоловнойИсполнитель Тогда
					КонтрольПройден = Ложь;
					
					Описание = НСтр("ru = 'Организация ""%1"" не является головным исполнителем.';
									|en = 'The ""%1"" company is not a main assignee.'"); 
					Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, 
						НайденнаяСтрока.Организация);
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
				
				// Контроль указания контракта
				Если Не НайденнаяСтрока.КонтрактУказан Тогда
					КонтрольПройден = Ложь;
					
					Описание = НСтр("ru = 'Не указан подтверждающий документ вида ""Контракт"".';
									|en = 'Justification document of the ""Contract"" kind is not specified.'"); 
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
				
				// Контроль указания документов, подтверждающих исполнение контракта
				Если Не НайденнаяСтрока.ДокументИсполненияУказан Тогда
					КонтрольПройден = Ложь;
					
					Описание = НСтр("ru = 'Не указан ни один документ, подтверждающий исполнение контракта.';
									|en = 'No document is specified that confirms contract execution.'");
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
				
				
				// Контроль накопленных по контракту сумм перечисления прибыли
				Если НайденнаяСтрока.СуммаДокумента + НайденнаяСтрока.СуммаФакт > НайденнаяСтрока.СуммаПлан Тогда
					КонтрольПройден = Ложь;
					
					ЧастиОписания = Новый Массив;
					Если НайденнаяСтрока.ЭтоСписание Тогда
						ЧастиОписания.Добавить(НСтр("ru = 'Сумма платежа';
													|en = 'Payment amount'")); 
					ИначеЕсли НайденнаяСтрока.ЭтоЗаявка Тогда
						ЧастиОписания.Добавить(НСтр("ru = 'Сумма заявки';
													|en = 'Request amount'")); 
					КонецЕсли;
					
					ЧастиОписания.Добавить(НСтр("ru = '%1 руб. с учетом ранее созданных распоряжений на сумму %2 руб. превышает размер прибыли %3 руб., указанный в контрактах.';
												|en = '%1 rub. considering previously created references to the amount of %2 rub. exceeds a profit of %3 rub. specified in the contracts.'"));
					
					Описание = СтрСоединить(ЧастиОписания, " ");
					
					Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, 
						Формат(НайденнаяСтрока.СуммаДокумента, "ЧДЦ=2; ЧН="), 
						Формат(НайденнаяСтрока.СуммаФакт, "ЧДЦ=2; ЧН="), 
						Формат(НайденнаяСтрока.СуммаПлан, "ЧДЦ=2; ЧН="));
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
			КонецЦикла;
			
			Если КонтрольПройден Тогда
				Описание = НСтр("ru = 'Контроль пройден.';
								|en = 'Controlled.'");
				ОписаниеМассивСтрок.Добавить(Описание);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ДобавитьОписаниеРезультатаКонтроля(ИмяОбработчикаКонтроля, КонтрольПройден, ОписаниеМассивСтрок, ОписанияРезультатов);
КонецПроцедуры

Процедура РезультатКонтроля_Раздел1_275_83_1_2_е(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, Знач ДанныеДокументовДляКонтроля)
	ТипДокумента = ТипДокумента(Ссылка);
	Если НЕ ТипДокумента.Контролируется Тогда
		Возврат;
	КонецЕсли;
	
	ОписанияРезультатов = РезультатыПроверок.ОписанияРезультатов;
	ИмяОбработчикаКонтроля = "РезультатКонтроля_Раздел1_275_83_1_2_е";
	КонтрольПройден = Истина;
	
	ОписаниеМассивСтрок = Новый Массив;
	Описание = "";
	
	Если Ссылка.ТипПлатежаФЗ275 <> Справочники.ТипыПлатежейФЗ275.СписаниеНаВозмещениеЗатратЗаСчетСобственныхСредств Тогда
		Описание = НСтр("ru = 'Контроль не требуется для типа платежа 275-ФЗ ""%1"".';
						|en = 'Control is not required for payment type 275-FZ ""%1"".'");
		Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, Ссылка.ТипПлатежаФЗ275);
		ОписаниеМассивСтрок.Добавить(Описание);
	Иначе
		Если ДанныеДокументовДляКонтроля = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.Текст =  ТекстЗапросаРезультатКонтроля_Раздел1_275_83_1_2_е();
			ПроверяемыеДокументы = Новый Массив;
			ПроверяемыеДокументы.Добавить(Ссылка);
			Запрос.УстановитьПараметр("ПроверяемыеДокументы", ПроверяемыеДокументы);
			Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
			
			ДанныеДокументовДляКонтроляТаблица = Запрос.Выполнить().Выгрузить();
		Иначе
			ДанныеДокументовДляКонтроляТаблица = ДанныеДокументовДляКонтроля[ИндексРезультатаЗапроса(ИндексыРезультатовЗапросов, ИмяОбработчикаКонтроля)].Выгрузить();
		КонецЕсли;
		
		Отбор = Новый Структура("Ссылка", Ссылка);
		НайденныеСтроки = ДанныеДокументовДляКонтроляТаблица.НайтиСтроки(Отбор);
		
		Если НайденныеСтроки.Количество() >= 1 Тогда
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
				// Контроль состояния
				Если НайденнаяСтрока.Статус = Перечисления.СтатусыДоговоровКонтрагентов.Закрыт Тогда
					КонтрольПройден = Ложь;
					
					Описание = НСтр("ru = 'Договор ""%1"" закрыт.';
									|en = 'The ""%1"" contract is closed.'"); 
					Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, 
						НайденнаяСтрока.КонтрактСЗаказчиком, 
						НайденнаяСтрока.Состояние);
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
				
				// Контроль указания контракта
				Если Не НайденнаяСтрока.КонтрактУказан Тогда
					КонтрольПройден = Ложь;
					
					Описание = НСтр("ru = 'Не указан подтверждающий документ вида ""Контракт"".';
									|en = 'Justification document of the ""Contract"" kind is not specified.'"); 
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
				
				// Контроль указания документов, подтверждающих исполнение контракта
				Если Не НайденнаяСтрока.ДокументИсполненияУказан Тогда
					КонтрольПройден = Ложь;
					
					Описание = НСтр("ru = 'Не указан ни один документ, подтверждающий исполнение контракта.';
									|en = 'No document is specified that confirms contract execution.'");
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
				
				
				// Контроль накопленных по контракту сумм возмещения
				Если НайденнаяСтрока.СуммаДокумента + НайденнаяСтрока.СуммаФакт > НайденнаяСтрока.СуммаПлан Тогда
					КонтрольПройден = Ложь;
					
					ЧастиОписания = Новый Массив;
					Если НайденнаяСтрока.ЭтоСписание Тогда
						ЧастиОписания.Добавить(НСтр("ru = 'Сумма платежа';
													|en = 'Payment amount'")); 
					ИначеЕсли НайденнаяСтрока.ЭтоЗаявка Тогда
						ЧастиОписания.Добавить(НСтр("ru = 'Сумма заявки';
													|en = 'Request amount'")); 
					КонецЕсли;
					
					ЧастиОписания.Добавить(НСтр("ru = '%1 руб. с учетом ранее созданных распоряжений на сумму %2 руб. превышает размер возмещения %3 руб., указанный в контрактах.';
												|en = '%1 rub. considering previously created references to the amount of %2 rub. exceeds a compensation amount of %3 rub. specified in the contracts.'"));
					
					Описание = СтрСоединить(ЧастиОписания, " ");
					
					Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, 
						Формат(НайденнаяСтрока.СуммаДокумента, "ЧДЦ=2; ЧН="), 
						Формат(НайденнаяСтрока.СуммаФакт, "ЧДЦ=2; ЧН="), 
						Формат(НайденнаяСтрока.СуммаПлан, "ЧДЦ=2; ЧН="));
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
			КонецЦикла;
			
			Если КонтрольПройден Тогда
				Описание = НСтр("ru = 'Контроль пройден.';
								|en = 'Controlled.'");
				ОписаниеМассивСтрок.Добавить(Описание);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ДобавитьОписаниеРезультатаКонтроля(ИмяОбработчикаКонтроля, КонтрольПройден, ОписаниеМассивСтрок, ОписанияРезультатов);
КонецПроцедуры

Процедура РезультатКонтроля_Раздел1_275_83_1_2_з(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, Знач ДанныеДокументовДляКонтроля)
	ТипДокумента = ТипДокумента(Ссылка);
	Если НЕ ТипДокумента.Контролируется Тогда
		Возврат;
	КонецЕсли;
	
	ОписанияРезультатов = РезультатыПроверок.ОписанияРезультатов;
	ИмяОбработчикаКонтроля = "РезультатКонтроля_Раздел1_275_83_1_2_з";
	КонтрольПройден = Истина;
	
	ОписаниеМассивСтрок = Новый Массив;
	Описание = "";
	
	Если Ссылка.ТипПлатежаФЗ275 <> Справочники.ТипыПлатежейФЗ275.ОплатаИныхРасходовВПределахУстановленнойСуммыВМесяц Тогда
		Описание = НСтр("ru = 'Контроль не требуется для типа платежа 275-ФЗ ""%1"".';
						|en = 'Control is not required for payment type 275-FZ ""%1"".'");
		Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, Ссылка.ТипПлатежаФЗ275);
		ОписаниеМассивСтрок.Добавить(Описание);
	Иначе
		Если ДанныеДокументовДляКонтроля = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.Текст =  ТекстЗапросаРезультатКонтроля_Раздел1_275_83_1_2_з();
			ПроверяемыеДокументы = Новый Массив;
			ПроверяемыеДокументы.Добавить(Ссылка);
			Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
			Запрос.УстановитьПараметр("ПроверяемыеДокументы", ПроверяемыеДокументы);
			
			ДанныеДокументовДляКонтроляТаблица = Запрос.Выполнить().Выгрузить();
		Иначе
			ДанныеДокументовДляКонтроляТаблица = ДанныеДокументовДляКонтроля[ИндексРезультатаЗапроса(ИндексыРезультатовЗапросов, ИмяОбработчикаКонтроля)].Выгрузить();
		КонецЕсли;
		
		Отбор = Новый Структура("Ссылка", Ссылка);
		НайденныеСтроки = ДанныеДокументовДляКонтроляТаблица.НайтиСтроки(Отбор);
		
		Если НайденныеСтроки.Количество() >= 1 Тогда
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
				// Контроль лимита 3 млн. руб.
				Если НайденнаяСтрока.СуммаДокумента + НайденнаяСтрока.СуммаРасходовВТекущемМесяце > 3000000 Тогда
					КонтрольПройден = Ложь;
					
					ЧастиОписания = Новый Массив;
					Если НайденнаяСтрока.ЭтоСписание Тогда
						ЧастиОписания.Добавить(НСтр("ru = 'Сумма платежа';
													|en = 'Payment amount'")); 
					ИначеЕсли НайденнаяСтрока.ЭтоЗаявка Тогда
						ЧастиОписания.Добавить(НСтр("ru = 'Сумма заявки';
													|en = 'Request amount'")); 
					КонецЕсли;
					
					ЧастиОписания.Добавить(НСтр("ru = '%1 руб. с учетом ранее созданных распоряжений на сумму %2 руб. превышает лимит 3 млн.руб. в месяц.';
												|en = '%1 rub. considering previously created references to the amount of %2 rub. exceeds the limit of 3 million rub. per month.'"));
					
					Описание = СтрСоединить(ЧастиОписания, " ");
					
					Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, 
						Формат(НайденнаяСтрока.СуммаДокумента, "ЧДЦ=2; ЧН="), 
						Формат(НайденнаяСтрока.СуммаРасходовВТекущемМесяце, "ЧДЦ=2; ЧН="));
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
				
				// Контроль, что счет-получатель не принадлежит организации
				Если НайденнаяСтрока.ЕстьБанковскиеСчетаПолучателиПринадлежащиеОрганизации Тогда
					Описание = НСтр("ru = 'Счет-получатель %1 принадлежит организации.';
									|en = 'Beneficiary account %1 belongs to the company.'");
					Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, НайденнаяСтрока.БанковскийСчет);
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
			КонецЦикла;
			
			Если КонтрольПройден Тогда
				Описание = НСтр("ru = 'Контроль пройден.';
								|en = 'Controlled.'");
				ОписаниеМассивСтрок.Добавить(Описание);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ДобавитьОписаниеРезультатаКонтроля(ИмяОбработчикаКонтроля, КонтрольПройден, ОписаниеМассивСтрок, ОписанияРезультатов);
КонецПроцедуры

Функция ТекстЗапросаРезультатКонтроля_Раздел1_275_83_1_1()
	ТекстЗапроса = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	РасшифровкаПлатежа.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА РасшифровкаПлатежа.ОбъектРасчетов.Объект ССЫЛКА Справочник.ДоговорыКонтрагентов
	|			ТОГДА ВЫРАЗИТЬ(РасшифровкаПлатежа.ОбъектРасчетов.Объект КАК Справочник.ДоговорыКонтрагентов).ГосударственныйКонтракт
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГосударственныеКонтракты.ПустаяСсылка)
	|	КОНЕЦ КАК ГосударственныйКонтракт,
	|	ВЫБОР
	|		КОГДА РасшифровкаПлатежа.ОбъектРасчетов.Объект ССЫЛКА Справочник.ДоговорыКонтрагентов
	|			ТОГДА ВЫРАЗИТЬ(РасшифровкаПлатежа.ОбъектРасчетов.Объект КАК Справочник.ДоговорыКонтрагентов).ДоговорСУчастникомГОЗ
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТребуетсяПроверка
	|ПОМЕСТИТЬ РасшифровкаЗаявок
	|ИЗ
	|	Документ.ЗаявкаНаРасходованиеДенежныхСредств.РасшифровкаПлатежа КАК РасшифровкаПлатежа
	|ГДЕ
	|	РасшифровкаПлатежа.Ссылка В(&ПроверяемыеДокументы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	РасшифровкаПлатежа.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА РасшифровкаПлатежа.ОбъектРасчетов.Объект ССЫЛКА Справочник.ДоговорыКонтрагентов
	|			ТОГДА ВЫРАЗИТЬ(РасшифровкаПлатежа.ОбъектРасчетов.Объект КАК Справочник.ДоговорыКонтрагентов).ГосударственныйКонтракт
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГосударственныеКонтракты.ПустаяСсылка)
	|	КОНЕЦ КАК ГосударственныйКонтракт,
	|	ВЫБОР
	|		КОГДА РасшифровкаПлатежа.ОбъектРасчетов.Объект ССЫЛКА Справочник.ДоговорыКонтрагентов
	|			ТОГДА ВЫРАЗИТЬ(РасшифровкаПлатежа.ОбъектРасчетов.Объект КАК Справочник.ДоговорыКонтрагентов).ПлатежиПо275ФЗ
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТребуетсяПроверка
	|ПОМЕСТИТЬ РасшифровкаСписаний
	|ИЗ
	|	Документ.СписаниеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК РасшифровкаПлатежа
	|ГДЕ
	|	РасшифровкаПлатежа.Ссылка В(&ПроверяемыеДокументы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроверяемыеДокументы.Ссылка КАК Ссылка,
	|	ПроверяемыеДокументы.УИП,
	|	ПроверяемыеДокументы.ИГК,
	|	ПроверяемыеДокументы.ТребуетсяПроверка
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка КАК Ссылка,
	|		ЗаявкаНаРасходованиеДенежныхСредств.ИдентификаторПлатежа КАК УИП,
	|		ЕСТЬNULL(Расшифровка.ГосударственныйКонтракт.Код, """") КАК ИГК,
	|		ЕСТЬNULL(Расшифровка.ТребуетсяПроверка, ЛОЖЬ) КАК ТребуетсяПроверка
	|	ИЗ
	|		Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ЗаявкаНаРасходованиеДенежныхСредств
	|			ЛЕВОЕ СОЕДИНЕНИЕ РасшифровкаЗаявок КАК Расшифровка
	|			ПО ЗаявкаНаРасходованиеДенежныхСредств.Ссылка = Расшифровка.Ссылка
	|	ГДЕ
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка В(&ПроверяемыеДокументы)
	|		И ЗаявкаНаРасходованиеДенежныхСредств.ПлатежиПо275ФЗ
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписаниеСРасчетногоСчета.Ссылка,
	|		СписаниеСРасчетногоСчета.ИдентификаторПлатежа,
	|		ЕСТЬNULL(Расшифровка.ГосударственныйКонтракт.Код, """"),
	|		ЕСТЬNULL(Расшифровка.ТребуетсяПроверка, ЛОЖЬ)
	|	ИЗ
	|		Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеСРасчетногоСчета
	|			ЛЕВОЕ СОЕДИНЕНИЕ РасшифровкаСписаний КАК Расшифровка
	|			ПО СписаниеСРасчетногоСчета.Ссылка = Расшифровка.Ссылка
	|	ГДЕ
	|		СписаниеСРасчетногоСчета.Ссылка В(&ПроверяемыеДокументы)
	|		И СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств.ПлатежиПо275ФЗ) КАК ПроверяемыеДокументы
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверяемыеДокументы.Ссылка,
	|	ПроверяемыеДокументы.УИП,
	|	ПроверяемыеДокументы.ИГК,
	|	ПроверяемыеДокументы.ТребуетсяПроверка";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаРезультатКонтроля_Раздел1_275_83_1_2_б()
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ПроверяемыеДокументы.Ссылка КАК Ссылка,
	|	ПроверяемыеДокументы.НазначениеПлатежа КАК НазначениеПлатежа,
	|	ПроверяемыеДокументы.ПредметОплаты КАК ПредметОплаты
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка КАК Ссылка,
	|		ЗаявкаНаРасходованиеДенежныхСредств.НазначениеПлатежа КАК НазначениеПлатежа,
	|		ВЫРАЗИТЬ(ЗаявкаНаРасходованиеДенежныхСредств.ПредметОплаты КАК СТРОКА(210)) КАК ПредметОплаты
	|	ИЗ
	|		Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ЗаявкаНаРасходованиеДенежныхСредств
	|	ГДЕ
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка В(&ПроверяемыеДокументы)
	|		И ЗаявкаНаРасходованиеДенежныхСредств.ПлатежиПо275ФЗ
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписаниеСРасчетногоСчета.Ссылка,
	|		СписаниеСРасчетногоСчета.НазначениеПлатежа,
	|		ВЫРАЗИТЬ(СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств.ПредметОплаты КАК СТРОКА(210))
	|	ИЗ
	|		Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеСРасчетногоСчета
	|	ГДЕ
	|		СписаниеСРасчетногоСчета.Ссылка В(&ПроверяемыеДокументы)
	|		И СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств.ПлатежиПо275ФЗ
	|) КАК ПроверяемыеДокументы
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверяемыеДокументы.Ссылка,
	|	ПроверяемыеДокументы.НазначениеПлатежа,
	|	ПроверяемыеДокументы.ПредметОплаты";
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаРезультатКонтроля_Раздел1_275_83_1_2_в()
	ТекстЗапроса = "ВЫБРАТЬ
	|	ТипыПлатежейФЗ275.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ПроверяемыеТипыПлатежей
	|ИЗ
	|	Справочник.ТипыПлатежейФЗ275 КАК ТипыПлатежейФЗ275
	|ГДЕ
	|	ТипыПлатежейФЗ275.Ссылка В (ЗНАЧЕНИЕ(Справочник.ТипыПлатежейФЗ275.ПеречисленияПрибылиИсполнителю))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыДокументов.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ПроверяемыеВидыДокументов
	|ИЗ
	|	Справочник.ВидыПодтверждающихДокументов КАК ВидыДокументов
	|ГДЕ
	|	ВидыДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыПодтверждающихДокументов.Контракт)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВидыДокументов.Ссылка
	|ИЗ
	|	Справочник.ВидыПодтверждающихДокументов КАК ВидыДокументов
	|ГДЕ
	|	ВидыДокументов.ПодтверждаетИсполнениеКонтракта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроверяемыеДокументыГруппировка.Ссылка КАК Ссылка,
	|	ПроверяемыеДокументыГруппировка.Заявка КАК Заявка
	|ПОМЕСТИТЬ ПроверяемыеДокументы
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка КАК Ссылка,
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка КАК Заявка
	|	ИЗ
	|		Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ЗаявкаНаРасходованиеДенежныхСредств
	|	ГДЕ
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка В(&ПроверяемыеДокументы)
	|		И ЗаявкаНаРасходованиеДенежныхСредств.ПлатежиПо275ФЗ
	|		И ЗаявкаНаРасходованиеДенежныхСредств.ТипПлатежаФЗ275 В
	|				(ВЫБРАТЬ
	|					ПроверяемыеТипыПлатежей.Ссылка
	|				ИЗ
	|					ПроверяемыеТипыПлатежей КАК ПроверяемыеТипыПлатежей)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписаниеСРасчетногоСчета.Ссылка,
	|		СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств
	|	ИЗ
	|		Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеСРасчетногоСчета
	|	ГДЕ
	|		СписаниеСРасчетногоСчета.Ссылка В(&ПроверяемыеДокументы)
	|		И СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств.ПлатежиПо275ФЗ
	|		И СписаниеСРасчетногоСчета.ТипПлатежаФЗ275 В
	|				(ВЫБРАТЬ
	|					ПроверяемыеТипыПлатежей.Ссылка
	|				ИЗ
	|					ПроверяемыеТипыПлатежей КАК ПроверяемыеТипыПлатежей)) КАК ПроверяемыеДокументыГруппировка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверяемыеДокументыГруппировка.Ссылка,
	|	ПроверяемыеДокументыГруппировка.Заявка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	РасшифровкаПлатежа.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА РасшифровкаПлатежа.ОбъектРасчетов.Объект ССЫЛКА Справочник.ДоговорыКонтрагентов
	|			ТОГДА ВЫРАЗИТЬ(РасшифровкаПлатежа.ОбъектРасчетов.Объект КАК Справочник.ДоговорыКонтрагентов)
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Договор
	|ПОМЕСТИТЬ ДоговорыСписаний
	|ИЗ
	|	Документ.СписаниеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК РасшифровкаПлатежа
	|ГДЕ
	|	РасшифровкаПлатежа.Ссылка В
	|			(ВЫБРАТЬ
	|				ПроверяемыеДокументы.Ссылка
	|			ИЗ
	|				ПроверяемыеДокументы КАК ПроверяемыеДокументы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроверяемыеДокументы.Ссылка КАК Ссылка,
	|	ПроверяемыеДокументы.Ссылка ССЫЛКА Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ЭтоЗаявка,
	|	ПроверяемыеДокументы.Ссылка ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств  КАК ЭтоСписание,
	|	ПроверяемыеДокументы.Заявка КАК Заявка,
	|	ЕСТЬNULL(ДоговорыСписаний.Договор, ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)) КАК КонтрактСЗаказчиком,
	|	ЕСТЬNULL(ДоговорыСписаний.Договор.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.ПустаяСсылка)) КАК Состояние,
	|	ПроверяемыеДокументы.Ссылка.СуммаДокумента КАК СуммаДокумента,
	|	СУММА(ЕСТЬNULL(ДоговорыСписаний.Договор.СуммаПрибыли, 0)) КАК СуммаПлан,
	|	СУММА(ЕСТЬNULL(ДвиженияДенежныеСредстваКонтрагентОбороты.СуммаОплатыОборот, 0)) КАК СуммаФакт,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыПодтверждающихДокументов.Контракт)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КонтрактУказан,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыПодтверждающихДокументов.Контракт)
	|					И ЗаявкаПодтверждающиеДокументы.Номер <> """"
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КонтрактНомерЗаполнен,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыПодтверждающихДокументов.Контракт)
	|					И ЗаявкаПодтверждающиеДокументы.Дата <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КонтрактДатаЗаполнена,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыПодтверждающихДокументов.Контракт)
	|					И ЗаявкаПодтверждающиеДокументы.Сумма <> 0
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КонтрактСуммаЗаполнена,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыПодтверждающихДокументов.Контракт)
	|					И ЗаявкаПодтверждающиеДокументы.Файл <> Неопределено
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КонтрактФайлЗаполнен,
	|	МАКСИМУМ(ЗаявкаПодтверждающиеДокументы.ВидДокумента.ПодтверждаетИсполнениеКонтракта) КАК ДокументИсполненияУказан,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента.ПодтверждаетИсполнениеКонтракта
	|					И ЗаявкаПодтверждающиеДокументы.Номер <> """"
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ДокументИсполненияНомерЗаполнен,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента.ПодтверждаетИсполнениеКонтракта
	|					И ЗаявкаПодтверждающиеДокументы.Дата <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ДокументИсполненияДатаЗаполнена,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента.ПодтверждаетИсполнениеКонтракта
	|					И ЗаявкаПодтверждающиеДокументы.Сумма <> 0
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ДокументИсполненияСуммаЗаполнена,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента.ПодтверждаетИсполнениеКонтракта
	|					И ЗаявкаПодтверждающиеДокументы.Файл < Неопределено
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ДокументИсполненияФайлЗаполнен
	|ИЗ
	|	ПроверяемыеДокументы КАК ПроверяемыеДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРасходованиеДенежныхСредств.ПодтверждающиеДокументы КАК ЗаявкаПодтверждающиеДокументы
	|		ПО ПроверяемыеДокументы.Заявка = ЗаявкаПодтверждающиеДокументы.Ссылка
	|			И (ЗаявкаПодтверждающиеДокументы.ВидДокумента В
	|				(ВЫБРАТЬ
	|					ПроверяемыеВидыДокументов.Ссылка
	|				ИЗ
	|					ПроверяемыеВидыДокументов КАК ПроверяемыеВидыДокументов))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыЗаявок
	|		ПО ПроверяемыеДокументы.Заявка.БанковскийСчет.ГосударственныйКонтракт = ДоговорыЗаявок.ГосударственныйКонтракт
	|			И ДоговорыЗаявок.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СПокупателем)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДоговорыСписаний КАК ДоговорыСписаний
	|		ПО (ПроверяемыеДокументы.Ссылка ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств)
	|			И ПроверяемыеДокументы.Ссылка = ДоговорыСписаний.Ссылка
	|			И (ДоговорыСписаний.Договор = ДоговорыЗаявок.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДвиженияДенежныеСредстваКонтрагент.Обороты(
	|				,
	|				&ТекущаяДата,
	|				Регистратор,
	|				ТипПлатежаФЗ275 В
	|					(ВЫБРАТЬ
	|						ПроверяемыеТипыПлатежей.Ссылка
	|					ИЗ
	|						ПроверяемыеТипыПлатежей КАК ПроверяемыеТипыПлатежей)) КАК ДвиженияДенежныеСредстваКонтрагентОбороты
	|		ПО (ДоговорыСписаний.Ссылка = ДвиженияДенежныеСредстваКонтрагентОбороты.Договор)
	|			И (ДвиженияДенежныеСредстваКонтрагентОбороты.Регистратор <> ПроверяемыеДокументы.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверяемыеДокументы.Ссылка,
	|	ПроверяемыеДокументы.Ссылка.СуммаДокумента,
	|	ДоговорыСписаний.Договор,
	|	ПроверяемыеДокументы.Заявка,
	|	ЕСТЬNULL(ДоговорыСписаний.Договор.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.ПустаяСсылка))";
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаРезультатКонтроля_Раздел1_275_83_1_2_г()
	ТекстЗапроса = "ВЫБРАТЬ
	|	ТипыПлатежейФЗ275.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ПроверяемыеТипыПлатежей
	|ИЗ
	|	Справочник.ТипыПлатежейФЗ275 КАК ТипыПлатежейФЗ275
	|ГДЕ
	|	ТипыПлатежейФЗ275.Ссылка В (ЗНАЧЕНИЕ(Справочник.ТипыПлатежейФЗ275.ПеречислениеПрибылиГоловномуИсполнителю))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыДокументов.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ПроверяемыеВидыДокументов
	|ИЗ
	|	Справочник.ВидыПодтверждающихДокументов КАК ВидыДокументов
	|ГДЕ
	|	ВидыДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыПодтверждающихДокументов.Контракт)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВидыДокументов.Ссылка
	|ИЗ
	|	Справочник.ВидыПодтверждающихДокументов КАК ВидыДокументов
	|ГДЕ
	|	ВидыДокументов.ПодтверждаетИсполнениеКонтракта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроверяемыеДокументыГруппировка.Ссылка КАК Ссылка,
	|	ПроверяемыеДокументыГруппировка.Заявка КАК Заявка
	|ПОМЕСТИТЬ ПроверяемыеДокументы
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка КАК Ссылка,
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка КАК Заявка
	|	ИЗ
	|		Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ЗаявкаНаРасходованиеДенежныхСредств
	|	ГДЕ
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка В(&ПроверяемыеДокументы)
	|		И ЗаявкаНаРасходованиеДенежныхСредств.ПлатежиПо275ФЗ
	|		И ЗаявкаНаРасходованиеДенежныхСредств.ТипПлатежаФЗ275 В
	|				(ВЫБРАТЬ
	|					ПроверяемыеТипыПлатежей.Ссылка
	|				ИЗ
	|					ПроверяемыеТипыПлатежей КАК ПроверяемыеТипыПлатежей)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписаниеСРасчетногоСчета.Ссылка,
	|		СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств
	|	ИЗ
	|		Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеСРасчетногоСчета
	|	ГДЕ
	|		СписаниеСРасчетногоСчета.Ссылка В(&ПроверяемыеДокументы)
	|		И СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств.ПлатежиПо275ФЗ
	|		И СписаниеСРасчетногоСчета.ТипПлатежаФЗ275 В
	|				(ВЫБРАТЬ
	|					ПроверяемыеТипыПлатежей.Ссылка
	|				ИЗ
	|					ПроверяемыеТипыПлатежей КАК ПроверяемыеТипыПлатежей)) КАК ПроверяемыеДокументыГруппировка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверяемыеДокументыГруппировка.Ссылка,
	|	ПроверяемыеДокументыГруппировка.Заявка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	РасшифровкаПлатежа.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА РасшифровкаПлатежа.ОбъектРасчетов.Объект ССЫЛКА Справочник.ДоговорыКонтрагентов
	|			ТОГДА ВЫРАЗИТЬ(РасшифровкаПлатежа.ОбъектРасчетов.Объект КАК Справочник.ДоговорыКонтрагентов)
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Договор
	|ПОМЕСТИТЬ ДоговорыСписаний
	|ИЗ
	|	Документ.СписаниеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК РасшифровкаПлатежа
	|ГДЕ
	|	РасшифровкаПлатежа.Ссылка В
	|			(ВЫБРАТЬ
	|				ПроверяемыеДокументы.Ссылка
	|			ИЗ
	|				ПроверяемыеДокументы КАК ПроверяемыеДокументы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроверяемыеДокументы.Ссылка КАК Ссылка,
	|	ПроверяемыеДокументы.Ссылка ССЫЛКА Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ЭтоЗаявка,
	|	ПроверяемыеДокументы.Ссылка ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств  КАК ЭтоСписание,
	|	ПроверяемыеДокументы.Заявка КАК Заявка,
	|	ВЫБОР
	|		КОГДА НЕ ДоговорыСписаний.Договор ЕСТЬ NULL 
	|				И ПроверяемыеДокументы.Ссылка ССЫЛКА Документ.ЗаявкаНаРасходованиеДенежныхСредств
	|			ТОГДА ДоговорыСписаний.Договор.Организация = ВЫРАЗИТЬ(ПроверяемыеДокументы.Ссылка КАК Документ.ЗаявкаНаРасходованиеДенежныхСредств).Организация
	|		КОГДА НЕ ДоговорыСписаний.Договор ЕСТЬ NULL 
	|				И ПроверяемыеДокументы.Ссылка ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств
	|			ТОГДА ДоговорыСписаний.Договор.Организация = ВЫРАЗИТЬ(ПроверяемыеДокументы.Ссылка КАК Документ.СписаниеБезналичныхДенежныхСредств).Организация
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрганизацияЭтоГоловнойИсполнитель,
	|	ПроверяемыеДокументы.Ссылка.Организация КАК Организация,
	|	ЕСТЬNULL(ДоговорыСписаний.Договор.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.ПустаяСсылка)) КАК Статус,
	|	ПроверяемыеДокументы.Ссылка.СуммаДокумента КАК СуммаДокумента,
	|	СУММА(ЕСТЬNULL(ДоговорыСписаний.Договор.СуммаПрибыли, 0)) КАК СуммаПлан,
	|	СУММА(ЕСТЬNULL(ДвиженияДенежныеСредстваКонтрагентОбороты.СуммаОплатыОборот, 0)) КАК СуммаФакт,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыПодтверждающихДокументов.Контракт)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КонтрактУказан,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыПодтверждающихДокументов.Контракт)
	|					И ЗаявкаПодтверждающиеДокументы.Номер <> """"
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КонтрактНомерЗаполнен,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыПодтверждающихДокументов.Контракт)
	|					И ЗаявкаПодтверждающиеДокументы.Дата <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КонтрактДатаЗаполнена,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыПодтверждающихДокументов.Контракт)
	|					И ЗаявкаПодтверждающиеДокументы.Сумма <> 0
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КонтрактСуммаЗаполнена,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыПодтверждающихДокументов.Контракт)
	|					И ЗаявкаПодтверждающиеДокументы.Файл <> Неопределено
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КонтрактФайлЗаполнен,
	|	МАКСИМУМ(ЗаявкаПодтверждающиеДокументы.ВидДокумента.ПодтверждаетИсполнениеКонтракта) КАК ДокументИсполненияУказан,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента.ПодтверждаетИсполнениеКонтракта
	|					И ЗаявкаПодтверждающиеДокументы.Номер <> """"
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ДокументИсполненияНомерЗаполнен,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента.ПодтверждаетИсполнениеКонтракта
	|					И ЗаявкаПодтверждающиеДокументы.Дата <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ДокументИсполненияДатаЗаполнена,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента.ПодтверждаетИсполнениеКонтракта
	|					И ЗаявкаПодтверждающиеДокументы.Сумма <> 0
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ДокументИсполненияСуммаЗаполнена,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента.ПодтверждаетИсполнениеКонтракта
	|					И ЗаявкаПодтверждающиеДокументы.Файл <> Неопределено
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ДокументИсполненияФайлЗаполнен
	|ИЗ
	|	ПроверяемыеДокументы КАК ПроверяемыеДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРасходованиеДенежныхСредств.ПодтверждающиеДокументы КАК ЗаявкаПодтверждающиеДокументы
	|		ПО ПроверяемыеДокументы.Заявка = ЗаявкаПодтверждающиеДокументы.Ссылка
	|			И (ЗаявкаПодтверждающиеДокументы.ВидДокумента В
	|				(ВЫБРАТЬ
	|					ПроверяемыеВидыДокументов.Ссылка
	|				ИЗ
	|					ПроверяемыеВидыДокументов КАК ПроверяемыеВидыДокументов))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыЗаявок
	|		ПО ПроверяемыеДокументы.Заявка.БанковскийСчет.ГосударственныйКонтракт = ДоговорыЗаявок.ГосударственныйКонтракт
	|			И ДоговорыЗаявок.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СПокупателем)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДоговорыСписаний КАК ДоговорыСписаний
	|		ПО (ПроверяемыеДокументы.Ссылка ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств)
	|			И ПроверяемыеДокументы.Ссылка = ДоговорыСписаний.Ссылка
	|			И (ДоговорыСписаний.Договор = ДоговорыЗаявок.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДвиженияДенежныеСредстваКонтрагент.Обороты(
	|				,
	|				&ТекущаяДата,
	|				Регистратор,
	|				ТипПлатежаФЗ275 В
	|					(ВЫБРАТЬ
	|						ПроверяемыеТипыПлатежей.Ссылка
	|					ИЗ
	|						ПроверяемыеТипыПлатежей КАК ПроверяемыеТипыПлатежей)) КАК ДвиженияДенежныеСредстваКонтрагентОбороты
	|		ПО (ДоговорыСписаний.Ссылка = ДвиженияДенежныеСредстваКонтрагентОбороты.Договор)
	|			И (ДвиженияДенежныеСредстваКонтрагентОбороты.Регистратор <> ПроверяемыеДокументы.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверяемыеДокументы.Ссылка,
	|	ПроверяемыеДокументы.Ссылка.СуммаДокумента,
	|	ВЫБОР
	|		КОГДА НЕ ДоговорыСписаний.Договор ЕСТЬ NULL 
	|				И ПроверяемыеДокументы.Ссылка ССЫЛКА Документ.ЗаявкаНаРасходованиеДенежныхСредств
	|			ТОГДА ДоговорыСписаний.Договор.Организация = ВЫРАЗИТЬ(ПроверяемыеДокументы.Ссылка КАК Документ.ЗаявкаНаРасходованиеДенежныхСредств).Организация
	|		КОГДА НЕ ДоговорыСписаний.Договор ЕСТЬ NULL 
	|				И ПроверяемыеДокументы.Ссылка ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств
	|			ТОГДА ДоговорыСписаний.Договор.Организация = ВЫРАЗИТЬ(ПроверяемыеДокументы.Ссылка КАК Документ.СписаниеБезналичныхДенежныхСредств).Организация
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ПроверяемыеДокументы.Заявка,
	|	ЕСТЬNULL(ДоговорыСписаний.Договор.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.ПустаяСсылка))";
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаРезультатКонтроля_Раздел1_275_83_1_2_е()
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	ТипыПлатежейФЗ275.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ПроверяемыеТипыПлатежей
	|ИЗ
	|	Справочник.ТипыПлатежейФЗ275 КАК ТипыПлатежейФЗ275
	|ГДЕ
	|	ТипыПлатежейФЗ275.Ссылка В (ЗНАЧЕНИЕ(Справочник.ТипыПлатежейФЗ275.СписаниеНаВозмещениеЗатратЗаСчетСобственныхСредств))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыДокументов.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ПроверяемыеВидыДокументов
	|ИЗ
	|	Справочник.ВидыПодтверждающихДокументов КАК ВидыДокументов
	|ГДЕ
	|	ВидыДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыПодтверждающихДокументов.Контракт)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВидыДокументов.Ссылка
	|ИЗ
	|	Справочник.ВидыПодтверждающихДокументов КАК ВидыДокументов
	|ГДЕ
	|	ВидыДокументов.ПодтверждаетИсполнениеКонтракта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроверяемыеДокументыГруппировка.Ссылка КАК Ссылка,
	|	ПроверяемыеДокументыГруппировка.Заявка КАК Заявка
	|ПОМЕСТИТЬ ПроверяемыеДокументы
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка КАК Ссылка,
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка КАК Заявка
	|	ИЗ
	|		Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ЗаявкаНаРасходованиеДенежныхСредств
	|	ГДЕ
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка В(&ПроверяемыеДокументы)
	|		И ЗаявкаНаРасходованиеДенежныхСредств.ПлатежиПо275ФЗ
	|		И ЗаявкаНаРасходованиеДенежныхСредств.ТипПлатежаФЗ275 В
	|				(ВЫБРАТЬ
	|					ПроверяемыеТипыПлатежей.Ссылка
	|				ИЗ
	|					ПроверяемыеТипыПлатежей КАК ПроверяемыеТипыПлатежей)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписаниеСРасчетногоСчета.Ссылка,
	|		СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств
	|	ИЗ
	|		Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеСРасчетногоСчета
	|	ГДЕ
	|		СписаниеСРасчетногоСчета.Ссылка В(&ПроверяемыеДокументы)
	|		И СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств.ПлатежиПо275ФЗ
	|		И СписаниеСРасчетногоСчета.ТипПлатежаФЗ275 В
	|				(ВЫБРАТЬ
	|					ПроверяемыеТипыПлатежей.Ссылка
	|				ИЗ
	|					ПроверяемыеТипыПлатежей КАК ПроверяемыеТипыПлатежей)) КАК ПроверяемыеДокументыГруппировка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверяемыеДокументыГруппировка.Ссылка,
	|	ПроверяемыеДокументыГруппировка.Заявка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоговорыСписаний.Ссылка,
	|	ДоговорыСписаний.Договор,
	|	0 КАК СуммаКВозмещению
	|ПОМЕСТИТЬ ДоговорыСписаний
	|ИЗ
	|	(ВЫБРАТЬ ПЕРВЫЕ 1
	|		РасшифровкаПлатежа.Ссылка КАК Ссылка,
	|		ВЫБОР
	|			КОГДА РасшифровкаПлатежа.ОбъектРасчетов.Объект ССЫЛКА Справочник.ДоговорыКонтрагентов
	|				ТОГДА ВЫРАЗИТЬ(РасшифровкаПлатежа.ОбъектРасчетов.Объект КАК Справочник.ДоговорыКонтрагентов)
	|			ИНАЧЕ """"
	|		КОНЕЦ КАК Договор
	|	ИЗ
	|		Документ.СписаниеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК РасшифровкаПлатежа
	|	ГДЕ
	|		РасшифровкаПлатежа.Ссылка В
	|				(ВЫБРАТЬ
	|					ПроверяемыеДокументы.Ссылка
	|				ИЗ
	|					ПроверяемыеДокументы КАК ПроверяемыеДокументы)) КАК ДоговорыСписаний
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроверяемыеДокументы.Ссылка КАК Ссылка,
	|	ПроверяемыеДокументы.Ссылка ССЫЛКА Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ЭтоЗаявка,
	|	ПроверяемыеДокументы.Ссылка ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств  КАК ЭтоСписание,
	|	ПроверяемыеДокументы.Заявка КАК Заявка,
	|	ЕСТЬNULL(ДоговорыСписаний.Договор, ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)) КАК КонтрактСЗаказчиком,
	|	ЕСТЬNULL(ДоговорыСписаний.Договор.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.ПустаяСсылка)) КАК Статус,
	|	ПроверяемыеДокументы.Ссылка.СуммаДокумента КАК СуммаДокумента,
	|	СУММА(ЕСТЬNULL(ДоговорыСписаний.СуммаКВозмещению, 0)) КАК СуммаПлан,
	|	СУММА(ЕСТЬNULL(ДвиженияДенежныеСредстваКонтрагентОбороты.СуммаОплатыОборот, 0)) КАК СуммаФакт,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыПодтверждающихДокументов.Контракт)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КонтрактУказан,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыПодтверждающихДокументов.Контракт)
	|					И ЗаявкаПодтверждающиеДокументы.Номер <> """"
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КонтрактНомерЗаполнен,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыПодтверждающихДокументов.Контракт)
	|					И ЗаявкаПодтверждающиеДокументы.Дата <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КонтрактДатаЗаполнена,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыПодтверждающихДокументов.Контракт)
	|					И ЗаявкаПодтверждающиеДокументы.Сумма <> 0
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КонтрактСуммаЗаполнена,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыПодтверждающихДокументов.Контракт)
	|					И ЗаявкаПодтверждающиеДокументы.Файл <> Неопределено
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КонтрактФайлЗаполнен,
	|	МАКСИМУМ(ЗаявкаПодтверждающиеДокументы.ВидДокумента.ПодтверждаетИсполнениеКонтракта) КАК ДокументИсполненияУказан,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента.ПодтверждаетИсполнениеКонтракта
	|					И ЗаявкаПодтверждающиеДокументы.Номер <> """"
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ДокументИсполненияНомерЗаполнен,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента.ПодтверждаетИсполнениеКонтракта
	|					И ЗаявкаПодтверждающиеДокументы.Дата <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ДокументИсполненияДатаЗаполнена,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента.ПодтверждаетИсполнениеКонтракта
	|					И ЗаявкаПодтверждающиеДокументы.Сумма <> 0
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ДокументИсполненияСуммаЗаполнена,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента.ПодтверждаетИсполнениеКонтракта
	|					И ЗаявкаПодтверждающиеДокументы.Файл <> Неопределено
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ДокументИсполненияФайлЗаполнен
	|ИЗ
	|	ПроверяемыеДокументы КАК ПроверяемыеДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРасходованиеДенежныхСредств.ПодтверждающиеДокументы КАК ЗаявкаПодтверждающиеДокументы
	|		ПО ПроверяемыеДокументы.Заявка = ЗаявкаПодтверждающиеДокументы.Ссылка
	|			И (ЗаявкаПодтверждающиеДокументы.ВидДокумента В
	|				(ВЫБРАТЬ
	|					ПроверяемыеВидыДокументов.Ссылка
	|				ИЗ
	|					ПроверяемыеВидыДокументов КАК ПроверяемыеВидыДокументов))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыЗаявок
	|		ПО ПроверяемыеДокументы.Заявка.БанковскийСчет.ГосударственныйКонтракт = ДоговорыЗаявок.ГосударственныйКонтракт
	|			И ДоговорыЗаявок.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СПокупателем)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДоговорыСписаний КАК ДоговорыСписаний
	|		ПО (ПроверяемыеДокументы.Ссылка ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств)
	|			И ПроверяемыеДокументы.Ссылка = ДоговорыСписаний.Ссылка
	|			И (ДоговорыСписаний.Договор = ДоговорыЗаявок.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДвиженияДенежныеСредстваКонтрагент.Обороты(
	|				,
	|				&ТекущаяДата,
	|				Регистратор,
	|				ТипПлатежаФЗ275 В
	|					(ВЫБРАТЬ
	|						ПроверяемыеТипыПлатежей.Ссылка
	|					ИЗ
	|						ПроверяемыеТипыПлатежей КАК ПроверяемыеТипыПлатежей)) КАК ДвиженияДенежныеСредстваКонтрагентОбороты
	|		ПО (ДоговорыСписаний.Ссылка = ДвиженияДенежныеСредстваКонтрагентОбороты.Договор)
	|			И (ДвиженияДенежныеСредстваКонтрагентОбороты.Регистратор <> ПроверяемыеДокументы.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверяемыеДокументы.Ссылка,
	|	ПроверяемыеДокументы.Ссылка.СуммаДокумента,
	|	ДоговорыСписаний.Договор,
	|	ПроверяемыеДокументы.Заявка,
	|	ЕСТЬNULL(ДоговорыСписаний.Договор.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.ПустаяСсылка))";
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаРезультатКонтроля_Раздел1_275_83_1_2_з()
	ТекстЗапроса = "ВЫБРАТЬ
	|	ТипыПлатежейФЗ275.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ПроверяемыеТипыПлатежей
	|ИЗ
	|	Справочник.ТипыПлатежейФЗ275 КАК ТипыПлатежейФЗ275
	|ГДЕ
	|	ТипыПлатежейФЗ275.Ссылка В (ЗНАЧЕНИЕ(Справочник.ТипыПлатежейФЗ275.ОплатаИныхРасходовВПределахУстановленнойСуммыВМесяц))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроверяемыеДокументыГруппировка.Ссылка КАК Ссылка,
	|	ПроверяемыеДокументыГруппировка.Заявка КАК Заявка,
	|	ПроверяемыеДокументыГруппировка.БанковскийСчет КАК БанковскийСчет,
	|	ПроверяемыеДокументыГруппировка.ЕстьБанковскиеСчетаПолучателиПринадлежащиеОрганизации КАК ЕстьБанковскиеСчетаПолучателиПринадлежащиеОрганизации
	|ПОМЕСТИТЬ ПроверяемыеДокументы
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка КАК Ссылка,
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка КАК Заявка,
	|		ЗаявкаНаРасходованиеДенежныхСредств.БанковскийСчетКасса КАК БанковскийСчет,
	|		ЛОЖЬ КАК ЕстьБанковскиеСчетаПолучателиПринадлежащиеОрганизации
	|	ИЗ
	|		Документ.ЗаявкаНаРасходованиеДенежныхСредств.РаспределениеПоСчетам КАК ЗаявкаНаРасходованиеДенежныхСредств
	|	ГДЕ
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка В(&ПроверяемыеДокументы)
	|		И ЗаявкаНаРасходованиеДенежныхСредств.Ссылка.ПлатежиПо275ФЗ
	|		И ЗаявкаНаРасходованиеДенежныхСредств.Ссылка.ТипПлатежаФЗ275 В
	|				(ВЫБРАТЬ
	|					ПроверяемыеТипыПлатежей.Ссылка
	|				ИЗ
	|					ПроверяемыеТипыПлатежей КАК ПроверяемыеТипыПлатежей)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписаниеСРасчетногоСчета.Ссылка,
	|		СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств,
	|		СписаниеСРасчетногоСчета.БанковскийСчет,
	|		СписаниеСРасчетногоСчета.БанковскийСчетПолучатель <> ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаОрганизаций.ПустаяСсылка)
	|	ИЗ
	|		Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеСРасчетногоСчета
	|	ГДЕ
	|		СписаниеСРасчетногоСчета.Ссылка В(&ПроверяемыеДокументы)
	|		И СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств.ПлатежиПо275ФЗ
	|		И СписаниеСРасчетногоСчета.ТипПлатежаФЗ275 В
	|				(ВЫБРАТЬ
	|					ПроверяемыеТипыПлатежей.Ссылка
	|				ИЗ
	|					ПроверяемыеТипыПлатежей КАК ПроверяемыеТипыПлатежей)) КАК ПроверяемыеДокументыГруппировка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверяемыеДокументыГруппировка.Ссылка,
	|	ПроверяемыеДокументыГруппировка.Заявка,
	|	ПроверяемыеДокументыГруппировка.БанковскийСчет,
	|	ПроверяемыеДокументыГруппировка.ЕстьБанковскиеСчетаПолучателиПринадлежащиеОрганизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиХозяйственныхОпераций.ХозяйственнаяОперация КАК Ссылка
	|ПОМЕСТИТЬ ХозяйственныеОперацииРасходов
	|ИЗ
	|	Справочник.НастройкиХозяйственныхОпераций КАК НастройкиХозяйственныхОпераций
	|ГДЕ
	|	НастройкиХозяйственныхОпераций.Расход = ЗНАЧЕНИЕ(Перечисление.ТипыДанныхУчета.ДенежныеСредства)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДвиженияДенежныеСредстваДоходыРасходыОбороты.ДенежныеСредства КАК БанковскийСчет,
	|	СУММА(ДвиженияДенежныеСредстваДоходыРасходыОбороты.СуммаРеглОборот) КАК СуммаОплат
	|ПОМЕСТИТЬ ОплатыПоБанковскимСчетам
	|ИЗ
	|	РегистрНакопления.ДвиженияДенежныеСредстваДоходыРасходы.Обороты(
	|			НАЧАЛОПЕРИОДА(&ТекущаяДата, МЕСЯЦ),
	|			КОНЕЦПЕРИОДА(&ТекущаяДата, МЕСЯЦ),
	|			Авто,
	|			ТипДенежныхСредств = ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.Безналичные)
	|				И ТипПлатежаФЗ275 В
	|					(ВЫБРАТЬ
	|						ПроверяемыеТипыПлатежей.Ссылка
	|					ИЗ
	|						ПроверяемыеТипыПлатежей)
	|				И ДенежныеСредства В
	|					(ВЫБРАТЬ
	|						ПроверяемыеДокументы.БанковскийСчет
	|					ИЗ
	|						ПроверяемыеДокументы)
	|				И ХозяйственнаяОперация В
	|					(ВЫБРАТЬ
	|						ХозяйственныеОперацииРасходов.Ссылка
	|					ИЗ
	|						ХозяйственныеОперацииРасходов)) КАК ДвиженияДенежныеСредстваДоходыРасходыОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияДенежныеСредстваДоходыРасходыОбороты.ДенежныеСредства
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДвиженияДенежныеСредстваКонтрагентОбороты.ДенежныеСредства,
	|	ДвиженияДенежныеСредстваКонтрагентОбороты.СуммаОплатыРеглОборот
	|ИЗ
	|	РегистрНакопления.ДвиженияДенежныеСредстваКонтрагент.Обороты(
	|			НАЧАЛОПЕРИОДА(&ТекущаяДата, МЕСЯЦ),
	|			КОНЕЦПЕРИОДА(&ТекущаяДата, МЕСЯЦ),
	|			Авто,
	|			ТипДенежныхСредств = ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.Безналичные)
	|				И ТипПлатежаФЗ275 В
	|					(ВЫБРАТЬ
	|						ПроверяемыеТипыПлатежей.Ссылка
	|					ИЗ
	|						ПроверяемыеТипыПлатежей)
	|				И ДенежныеСредства В
	|					(ВЫБРАТЬ
	|						ПроверяемыеДокументы.БанковскийСчет
	|					ИЗ
	|						ПроверяемыеДокументы)
	|				И ХозяйственнаяОперация В
	|					(ВЫБРАТЬ
	|						ХозяйственныеОперацииРасходов.Ссылка
	|					ИЗ
	|						ХозяйственныеОперацииРасходов)) КАК ДвиженияДенежныеСредстваКонтрагентОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДвиженияДенежныхСредствОбороты.ДенежныеСредства,
	|	ДвиженияДенежныхСредствОбороты.СуммаРеглОборот
	|ИЗ
	|	РегистрНакопления.ДвиженияДенежныхСредств.Обороты(
	|			НАЧАЛОПЕРИОДА(&ТекущаяДата, МЕСЯЦ),
	|			КОНЕЦПЕРИОДА(&ТекущаяДата, МЕСЯЦ),
	|			Авто,
	|			ТипДенежныхСредств = ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.Безналичные)
	|				И ТипПлатежаФЗ275 В
	|					(ВЫБРАТЬ
	|						ПроверяемыеТипыПлатежей.Ссылка
	|					ИЗ
	|						ПроверяемыеТипыПлатежей)
	|				И ДенежныеСредства В
	|					(ВЫБРАТЬ
	|						ПроверяемыеДокументы.БанковскийСчет
	|					ИЗ
	|						ПроверяемыеДокументы)
	|				И ХозяйственнаяОперация В
	|					(ВЫБРАТЬ
	|						ХозяйственныеОперацииРасходов.Ссылка
	|					ИЗ
	|						ХозяйственныеОперацииРасходов)) КАК ДвиженияДенежныхСредствОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроверяемыеДокументы.Ссылка КАК Ссылка,
	|	ПроверяемыеДокументы.Ссылка ССЫЛКА Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ЭтоЗаявка,
	|	ПроверяемыеДокументы.Ссылка ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств  КАК ЭтоСписание,
	|	ПроверяемыеДокументы.Заявка КАК Заявка,
	|	ПроверяемыеДокументы.БанковскийСчет КАК БанковскийСчет,
	|	ПроверяемыеДокументы.ЕстьБанковскиеСчетаПолучателиПринадлежащиеОрганизации КАК ЕстьБанковскиеСчетаПолучателиПринадлежащиеОрганизации,
	|	ПроверяемыеДокументы.Ссылка.СуммаДокумента КАК СуммаДокумента,
	|	ЕСТЬNULL(СУММА(ОплатыПоБанковскимСчетам.СуммаОплат), 0) КАК СуммаРасходовВТекущемМесяце
	|ИЗ
	|	ПроверяемыеДокументы КАК ПроверяемыеДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОплатыПоБанковскимСчетам КАК ОплатыПоБанковскимСчетам
	|		ПО ПроверяемыеДокументы.БанковскийСчет = ОплатыПоБанковскимСчетам.БанковскийСчет
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверяемыеДокументы.Ссылка,
	|	ПроверяемыеДокументы.Заявка,
	|	ПроверяемыеДокументы.БанковскийСчет,
	|	ПроверяемыеДокументы.ЕстьБанковскиеСчетаПолучателиПринадлежащиеОрганизации,
	|	ПроверяемыеДокументы.Ссылка.СуммаДокумента";
	Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти

#Область Пункт84

Процедура РезультатКонтроля_Раздел1_275_84_2(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, Знач ДанныеДокументовДляКонтроля)
	ТипДокумента = ТипДокумента(Ссылка);
	Если НЕ ТипДокумента.Контролируется Тогда
		Возврат;
	КонецЕсли;
	
	ОписанияРезультатов = РезультатыПроверок.ОписанияРезультатов;
	ИмяОбработчикаКонтроля = "РезультатКонтроля_Раздел1_275_84_2";
	КонтрольПройден = Истина;
	
	ОписаниеМассивСтрок = Новый Массив;
	Описание = "";
	
	Если Ссылка.ТипПлатежаФЗ275 <> Справочники.ТипыПлатежейФЗ275.СписаниеСуммКредитовПеречисленныхУполномоченнымБанком Тогда
		Описание = НСтр("ru = 'Контроль не требуется для типа платежа 275-ФЗ ""%1"".';
						|en = 'Control is not required for payment type 275-FZ ""%1"".'");
		Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, Ссылка.ТипПлатежаФЗ275);
		ОписаниеМассивСтрок.Добавить(Описание);
	Иначе
		Если ДанныеДокументовДляКонтроля = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.Текст =  ТекстЗапросаРезультатКонтроля_Раздел1_275_84_2();
			ПроверяемыеДокументы = Новый Массив;
			ПроверяемыеДокументы.Добавить(Ссылка);
			Запрос.УстановитьПараметр("ПроверяемыеДокументы", ПроверяемыеДокументы);
			
			ДанныеДокументовДляКонтроляТаблица = Запрос.Выполнить().Выгрузить();
		Иначе
			ДанныеДокументовДляКонтроляТаблица = ДанныеДокументовДляКонтроля[ИндексРезультатаЗапроса(ИндексыРезультатовЗапросов, ИмяОбработчикаКонтроля)].Выгрузить();
		КонецЕсли;
		
		Отбор = Новый Структура("Ссылка", Ссылка);
		НайденныеСтроки = ДанныеДокументовДляКонтроляТаблица.НайтиСтроки(Отбор);
		
		Если НайденныеСтроки.Количество() >= 1 Тогда
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				// Проверка не превышения суммы платежей нарастающим итогом над остатком кредита
				Если НайденнаяСтрока.СуммаДокумента > НайденнаяСтрока.ОстатокКредита Тогда
					КонтрольПройден = Ложь;
					
					ЧастиОписания = Новый Массив;
					Если НайденнаяСтрока.ЭтоСписание Тогда
						ЧастиОписания.Добавить(НСтр("ru = 'Сумма платежа';
													|en = 'Payment amount'")); 
					ИначеЕсли НайденнаяСтрока.ЭтоЗаявка Тогда
						ЧастиОписания.Добавить(НСтр("ru = 'Сумма заявки';
													|en = 'Request amount'")); 
					КонецЕсли;
					
					ЧастиОписания.Добавить(НСтр("ru = '%1 руб. превышает сумму остатка по кредиту %2 руб..';
												|en = '%1 rub. exceeds the remaining credit amount of %2 rub...'"));
					
					Описание = СтрСоединить(ЧастиОписания, " ");
					
					Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, 
						Формат(НайденнаяСтрока.СуммаДокумента, "ЧДЦ=2; ЧН="), 
						Формат(НайденнаяСтрока.ОстатокКредита, "ЧДЦ=2; ЧН="));
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
				
				// Проверка банковских реквизитов счета-отправителя и счета-получателя
				Если НайденнаяСтрока.БанкОтправитель <> НайденнаяСтрока.БанкПолучатель Тогда
					КонтрольПройден = Ложь;
					
					Описание = НСтр("ru = 'Банковский счет %1 должен быть открыт в банке %2 счета-получателя %3.';
									|en = 'Bank account %1 should be opened in bank %2 of beneficiary''s account %3.'"); 
					Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, 
						НайденнаяСтрока.БанковскийСчетОтправитель, 
						НайденнаяСтрока.БанкПолучатель, 
						НайденнаяСтрока.БанковскийСчетПолучатель);
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
			КонецЦикла;
			
			Если КонтрольПройден Тогда
				Описание = НСтр("ru = 'Контроль пройден.';
								|en = 'Controlled.'");
				ОписаниеМассивСтрок.Добавить(Описание);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ДобавитьОписаниеРезультатаКонтроля(ИмяОбработчикаКонтроля, КонтрольПройден, ОписаниеМассивСтрок, ОписанияРезультатов);
КонецПроцедуры

Процедура РезультатКонтроля_Раздел1_275_84_10(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, Знач ДанныеДокументовДляКонтроля)
	ТипДокумента = ТипДокумента(Ссылка);
	Если НЕ ТипДокумента.Контролируется Тогда
		Возврат;
	КонецЕсли;
	
	ОписанияРезультатов = РезультатыПроверок.ОписанияРезультатов;
	ИмяОбработчикаКонтроля = "РезультатКонтроля_Раздел1_275_84_10";
	КонтрольПройден = Истина;
	
	ОписаниеМассивСтрок = Новый Массив;
	Описание = "";
	
	Если Ссылка.ТипПлатежаФЗ275 <> Справочники.ТипыПлатежейФЗ275.СписаниеСвязанноеСРазмещениемНаДепозитеУполномоченногоБанка Тогда
		Описание = НСтр("ru = 'Контроль не требуется для типа платежа 275-ФЗ ""%1"".';
						|en = 'Control is not required for payment type 275-FZ ""%1"".'");
		Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, Ссылка.ТипПлатежаФЗ275);
		ОписаниеМассивСтрок.Добавить(Описание);
	Иначе
		Если ДанныеДокументовДляКонтроля = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.Текст =  ТекстЗапросаРезультатКонтроля_Раздел1_275_84_10();
			ПроверяемыеДокументы = Новый Массив;
			ПроверяемыеДокументы.Добавить(Ссылка);
			Запрос.УстановитьПараметр("ПроверяемыеДокументы", ПроверяемыеДокументы);
			
			ДанныеДокументовДляКонтроляТаблица = Запрос.Выполнить().Выгрузить();
		Иначе
			ДанныеДокументовДляКонтроляТаблица = ДанныеДокументовДляКонтроля[ИндексРезультатаЗапроса(ИндексыРезультатовЗапросов, ИмяОбработчикаКонтроля)].Выгрузить();
		КонецЕсли;
		
		Отбор = Новый Структура("Ссылка", Ссылка);
		НайденныеСтроки = ДанныеДокументовДляКонтроляТаблица.НайтиСтроки(Отбор);
		
		Если НайденныеСтроки.Количество() >= 1 Тогда
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
				// Проверка банковских реквизитов счета-отправителя и счета-получателя
				Если НайденнаяСтрока.БанкОтправитель <> НайденнаяСтрока.БанкПолучатель Тогда
					КонтрольПройден = Ложь;
					
					Описание = НСтр("ru = 'Банковский счет-получатель %1 должен быть открыт в уполномоченном банке %2.';
									|en = 'Beneficiary bank account %1 should be opened in authorized bank %2.'"); 
					Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, 
						НайденнаяСтрока.БанковскийСчетПолучатель, 
						НайденнаяСтрока.БанкОтправитель);
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
			КонецЦикла;
			
			Если КонтрольПройден Тогда
				Описание = НСтр("ru = 'Контроль пройден.';
								|en = 'Controlled.'");
				ОписаниеМассивСтрок.Добавить(Описание);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ДобавитьОписаниеРезультатаКонтроля(ИмяОбработчикаКонтроля, КонтрольПройден, ОписаниеМассивСтрок, ОписанияРезультатов);
КонецПроцедуры

Функция ТекстЗапросаРезультатКонтроля_Раздел1_275_84_2()
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТипыПлатежейФЗ275.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ПроверяемыеТипыПлатежей
	|ИЗ
	|	Справочник.ТипыПлатежейФЗ275 КАК ТипыПлатежейФЗ275
	|ГДЕ
	|	ТипыПлатежейФЗ275.Ссылка В (ЗНАЧЕНИЕ(Справочник.ТипыПлатежейФЗ275.СписаниеСуммКредитовПеречисленныхУполномоченнымБанком))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроверяемыеДокументыГруппировка.Ссылка КАК Ссылка,
	|	ПроверяемыеДокументыГруппировка.Заявка КАК Заявка,
	|	ПроверяемыеДокументыГруппировка.БанковскийСчетОтправитель КАК БанковскийСчетОтправитель,
	|	ПроверяемыеДокументыГруппировка.БанкОтправитель КАК БанкОтправитель,
	|	ПроверяемыеДокументыГруппировка.БанковскийСчетПолучатель КАК БанковскийСчетПолучатель,
	|	ПроверяемыеДокументыГруппировка.БанкПолучатель КАК БанкПолучатель,
	|	ПроверяемыеДокументыГруппировка.БанкОтправительЭтоБанкПолучатель КАК БанкОтправительЭтоБанкПолучатель
	|ПОМЕСТИТЬ ПроверяемыеДокументы
	|ИЗ
	|	(ВЫБРАТЬ
	|		РаспределениеПоСчетам.Ссылка КАК Ссылка,
	|		РаспределениеПоСчетам.Ссылка КАК Заявка,
	|		РаспределениеПоСчетам.БанковскийСчетКасса КАК БанковскийСчетОтправитель,
	|		ВЫРАЗИТЬ(РаспределениеПоСчетам.БанковскийСчетКасса КАК Справочник.БанковскиеСчетаОрганизаций).Банк КАК БанкОтправитель,
	|		РаспределениеПоСчетам.Ссылка.БанковскийСчетКонтрагента КАК БанковскийСчетПолучатель,
	|		РаспределениеПоСчетам.Ссылка.БанковскийСчетКонтрагента.Банк КАК БанкПолучатель,
	|		ВЫРАЗИТЬ(РаспределениеПоСчетам.БанковскийСчетКасса КАК Справочник.БанковскиеСчетаОрганизаций).Банк = РаспределениеПоСчетам.Ссылка.БанковскийСчетКонтрагента.Банк КАК БанкОтправительЭтоБанкПолучатель
	|	ИЗ
	|		Документ.ЗаявкаНаРасходованиеДенежныхСредств.РаспределениеПоСчетам КАК РаспределениеПоСчетам
	|	ГДЕ
	|		РаспределениеПоСчетам.Ссылка В(&ПроверяемыеДокументы)
	|		И РаспределениеПоСчетам.Ссылка.ПлатежиПо275ФЗ
	|		И РаспределениеПоСчетам.Ссылка.ТипПлатежаФЗ275 В
	|				(ВЫБРАТЬ
	|					ПроверяемыеТипыПлатежей.Ссылка
	|				ИЗ
	|					ПроверяемыеТипыПлатежей КАК ПроверяемыеТипыПлатежей)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписаниеСРасчетногоСчета.Ссылка,
	|		СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств,
	|		СписаниеСРасчетногоСчета.БанковскийСчет,
	|		СписаниеСРасчетногоСчета.БанковскийСчет.Банк,
	|		СписаниеСРасчетногоСчета.БанковскийСчетКонтрагента,
	|		СписаниеСРасчетногоСчета.БанковскийСчетКонтрагента.Банк,
	|		СписаниеСРасчетногоСчета.БанковскийСчет.Банк = СписаниеСРасчетногоСчета.БанковскийСчетКонтрагента.Банк
	|	ИЗ
	|		Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеСРасчетногоСчета
	|	ГДЕ
	|		СписаниеСРасчетногоСчета.Ссылка В(&ПроверяемыеДокументы)
	|		И СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств.ПлатежиПо275ФЗ
	|		И СписаниеСРасчетногоСчета.ТипПлатежаФЗ275 В
	|				(ВЫБРАТЬ
	|					ПроверяемыеТипыПлатежей.Ссылка
	|				ИЗ
	|					ПроверяемыеТипыПлатежей КАК ПроверяемыеТипыПлатежей)) КАК ПроверяемыеДокументыГруппировка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверяемыеДокументыГруппировка.Ссылка,
	|	ПроверяемыеДокументыГруппировка.Заявка,
	|	ПроверяемыеДокументыГруппировка.БанковскийСчетОтправитель,
	|	ПроверяемыеДокументыГруппировка.БанкОтправитель,
	|	ПроверяемыеДокументыГруппировка.БанковскийСчетПолучатель,
	|	ПроверяемыеДокументыГруппировка.БанкПолучатель,
	|	ПроверяемыеДокументыГруппировка.БанкОтправительЭтоБанкПолучатель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроверяемыеДокументы.Ссылка КАК Ссылка,
	|	ПроверяемыеДокументы.Ссылка ССЫЛКА Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ЭтоЗаявка,
	|	ПроверяемыеДокументы.Ссылка ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств  КАК ЭтоСписание,
	|	ПроверяемыеДокументы.Заявка КАК Заявка,
	|	ПроверяемыеДокументы.БанковскийСчетОтправитель КАК БанковскийСчетОтправитель,
	|	ПроверяемыеДокументы.БанкОтправитель КАК БанкОтправитель,
	|	ПроверяемыеДокументы.БанковскийСчетПолучатель КАК БанковскийСчетПолучатель,
	|	ПроверяемыеДокументы.БанкПолучатель КАК БанкПолучатель,
	|	ПроверяемыеДокументы.БанкОтправительЭтоБанкПолучатель КАК БанкОтправительЭтоБанкПолучатель,
	|	ПроверяемыеДокументы.Ссылка.СуммаДокумента КАК СуммаДокумента,
	|	ЕСТЬNULL(СУММА(ПоступленияКредитныхСредств.СуммаОплатыОборот), 0) КАК ОстатокКредита
	|ИЗ
	|	ПроверяемыеДокументы КАК ПроверяемыеДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДвиженияДенежныеСредстваКонтрагент.Обороты(
	|				,
	|				,
	|				,
	|				ДенежныеСредства В
	|						(ВЫБРАТЬ
	|							ПроверяемыеДокументы.БанковскийСчетОтправитель
	|						ИЗ
	|							ПроверяемыеДокументы КАК ПроверяемыеДокументы)
	|					И ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам)) КАК ПоступленияКредитныхСредств
	|		ПО ПроверяемыеДокументы.БанковскийСчетОтправитель = ПоступленияКредитныхСредств.ДенежныеСредства
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверяемыеДокументы.Ссылка,
	|	ПроверяемыеДокументы.Заявка,
	|	ПроверяемыеДокументы.Ссылка.СуммаДокумента,
	|	ПроверяемыеДокументы.БанковскийСчетОтправитель,
	|	ПроверяемыеДокументы.БанкОтправитель,
	|	ПроверяемыеДокументы.БанковскийСчетПолучатель,
	|	ПроверяемыеДокументы.БанкПолучатель,
	|	ПроверяемыеДокументы.БанкОтправительЭтоБанкПолучатель";
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаРезультатКонтроля_Раздел1_275_84_10()
	ТекстЗапроса = "ВЫБРАТЬ
	|	ТипыПлатежейФЗ275.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ПроверяемыеТипыПлатежей
	|ИЗ
	|	Справочник.ТипыПлатежейФЗ275 КАК ТипыПлатежейФЗ275
	|ГДЕ
	|	ТипыПлатежейФЗ275.Ссылка В (ЗНАЧЕНИЕ(Справочник.ТипыПлатежейФЗ275.СписаниеСвязанноеСРазмещениемНаДепозитеУполномоченногоБанка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроверяемыеДокументыГруппировка.Ссылка КАК Ссылка,
	|	ПроверяемыеДокументыГруппировка.Заявка КАК Заявка,
	|	ПроверяемыеДокументыГруппировка.БанковскийСчетОтправитель КАК БанковскийСчетОтправитель,
	|	ПроверяемыеДокументыГруппировка.БанкОтправитель КАК БанкОтправитель,
	|	ПроверяемыеДокументыГруппировка.БанковскийСчетПолучатель КАК БанковскийСчетПолучатель,
	|	ПроверяемыеДокументыГруппировка.БанкПолучатель КАК БанкПолучатель,
	|	ПроверяемыеДокументыГруппировка.БанкОтправительЭтоБанкПолучатель КАК БанкОтправительЭтоБанкПолучатель
	|ИЗ
	|	(ВЫБРАТЬ
	|		РаспределениеПоСчетам.Ссылка КАК Ссылка,
	|		РаспределениеПоСчетам.Ссылка КАК Заявка,
	|		РаспределениеПоСчетам.БанковскийСчетКасса КАК БанковскийСчетОтправитель,
	|		ВЫРАЗИТЬ(РаспределениеПоСчетам.БанковскийСчетКасса КАК Справочник.БанковскиеСчетаОрганизаций).Банк КАК БанкОтправитель,
	|		РаспределениеПоСчетам.Ссылка.БанковскийСчетКонтрагента КАК БанковскийСчетПолучатель,
	|		РаспределениеПоСчетам.Ссылка.БанковскийСчетКонтрагента.Банк КАК БанкПолучатель,
	|		ВЫРАЗИТЬ(РаспределениеПоСчетам.БанковскийСчетКасса КАК Справочник.БанковскиеСчетаОрганизаций).Банк = РаспределениеПоСчетам.Ссылка.БанковскийСчетКонтрагента.Банк КАК БанкОтправительЭтоБанкПолучатель
	|	ИЗ
	|		Документ.ЗаявкаНаРасходованиеДенежныхСредств.РаспределениеПоСчетам КАК РаспределениеПоСчетам
	|	ГДЕ
	|		РаспределениеПоСчетам.Ссылка В(&ПроверяемыеДокументы)
	|		И РаспределениеПоСчетам.Ссылка.ПлатежиПо275ФЗ
	|		И РаспределениеПоСчетам.Ссылка.ТипПлатежаФЗ275 В
	|				(ВЫБРАТЬ
	|					ПроверяемыеТипыПлатежей.Ссылка
	|				ИЗ
	|					ПроверяемыеТипыПлатежей КАК ПроверяемыеТипыПлатежей)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписаниеСРасчетногоСчета.Ссылка,
	|		СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств,
	|		СписаниеСРасчетногоСчета.БанковскийСчет,
	|		СписаниеСРасчетногоСчета.БанковскийСчет.Банк,
	|		СписаниеСРасчетногоСчета.БанковскийСчетКонтрагента,
	|		СписаниеСРасчетногоСчета.БанковскийСчетКонтрагента.Банк,
	|		СписаниеСРасчетногоСчета.БанковскийСчет.Банк = СписаниеСРасчетногоСчета.БанковскийСчетКонтрагента.Банк
	|	ИЗ
	|		Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеСРасчетногоСчета
	|	ГДЕ
	|		СписаниеСРасчетногоСчета.Ссылка В(&ПроверяемыеДокументы)
	|		И СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств.ПлатежиПо275ФЗ
	|		И СписаниеСРасчетногоСчета.ТипПлатежаФЗ275 В
	|				(ВЫБРАТЬ
	|					ПроверяемыеТипыПлатежей.Ссылка
	|				ИЗ
	|					ПроверяемыеТипыПлатежей КАК ПроверяемыеТипыПлатежей)) КАК ПроверяемыеДокументыГруппировка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверяемыеДокументыГруппировка.Ссылка,
	|	ПроверяемыеДокументыГруппировка.Заявка,
	|	ПроверяемыеДокументыГруппировка.БанковскийСчетОтправитель,
	|	ПроверяемыеДокументыГруппировка.БанкОтправитель,
	|	ПроверяемыеДокументыГруппировка.БанковскийСчетПолучатель,
	|	ПроверяемыеДокументыГруппировка.БанкПолучатель,
	|	ПроверяемыеДокументыГруппировка.БанкОтправительЭтоБанкПолучатель";
	Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти

#Область Пункт85

Процедура РезультатКонтроля_Раздел1_275_85_1_1(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, Знач ДанныеДокументовДляКонтроля)
	ТипДокумента = ТипДокумента(Ссылка);
	Если НЕ ТипДокумента.Контролируется Тогда
		Возврат;
	КонецЕсли;
	
	ОписанияРезультатов = РезультатыПроверок.ОписанияРезультатов;
	ИмяОбработчикаКонтроля = "РезультатКонтроля_Раздел1_275_85_1_1";
	КонтрольПройден = Истина;
	
	ОписаниеМассивСтрок = Новый Массив;
	Описание = "";
	
	Если Ссылка.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ОплатаПоставщику
		И Ссылка.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту 
		И Ссылка.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет
		И ДенежныеСредстваПовтИспРФ.ПрочаяОперацияСписанияДСПо275ФЗ(Ссылка.ХозяйственнаяОперация)
		И Ссылка.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ОплатаПоКредитам
		И Ссылка.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПеречислениеНаДепозиты Тогда
		Описание = НСтр("ru = 'Контроль не требуется для операции ""%1"".';
						|en = 'Control is not required for transaction ""%1"".'");
		Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, Ссылка.ХозяйственнаяОперация);
		ОписаниеМассивСтрок.Добавить(Описание);
	ИначеЕсли Ссылка.ТипПлатежаФЗ275 <> Справочники.ТипыПлатежейФЗ275.СписаниеНаОтдельныйСчет Тогда
		Описание = НСтр("ru = 'Контроль не требуется для типа платежа ""%1"".';
						|en = 'Control is not required for payment type ""%1"".'");
		Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, Ссылка.ТипПлатежаФЗ275);
		ОписаниеМассивСтрок.Добавить(Описание);
	Иначе
		Если ДанныеДокументовДляКонтроля = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.Текст =  ТекстЗапросаРезультатКонтроля_Раздел1_275_85_1_1();
			ПроверяемыеДокументы = Новый Массив;
			ПроверяемыеДокументы.Добавить(Ссылка);
			Запрос.УстановитьПараметр("ПроверяемыеДокументы", ПроверяемыеДокументы);
			
			ДанныеДокументовДляКонтроляТаблица = Запрос.Выполнить().Выгрузить();
		Иначе
			ДанныеДокументовДляКонтроляТаблица = ДанныеДокументовДляКонтроля[ИндексРезультатаЗапроса(ИндексыРезультатовЗапросов, ИмяОбработчикаКонтроля)].Выгрузить();
		КонецЕсли;
		
		Отбор = Новый Структура("Ссылка", Ссылка);
		НайденныеСтроки = ДанныеДокументовДляКонтроляТаблица.НайтиСтроки(Отбор);
		
		Если НайденныеСтроки.Количество() >= 1 Тогда
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
				// Контроль совпадения УИП и ИГК
				Если НайденнаяСтрока.ИГКСчетаОтправителя <> НайденнаяСтрока.УИП Тогда
					КонтрольПройден = Ложь;
					
					Описание = НСтр("ru = 'ИГК %1 банковского счета-отправителя ""%2"" не совпадает с УИП %3.';
									|en = 'SCI %1 of payer bank account ""%2"" does not match UIP %3.'"); 
					Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, 
						НайденнаяСтрока.ИГКСчетаОтправителя, 
						НайденнаяСтрока.БанковскийСчетОтправитель, 
						НайденнаяСтрока.УИП);
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
				
				// Контроль совпадения ИГК отдельных счетов
				Если НайденнаяСтрока.ИГКСчетаОтправителя <> НайденнаяСтрока.ИГКСчетаПолучателя Тогда
					КонтрольПройден = Ложь;
					
					Описание = НСтр("ru = 'ИГК %1 банковского счета-отправителя ""%2"" не совпадает с ИГК %3 банковского счета-получателя ""%4"".';
									|en = 'SCI %1 of payer bank account ""%2"" does not match SCI %3 of beneficiary bank account ""%4"".'"); 
					Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, 
						НайденнаяСтрока.ИГКСчетаОтправителя, 
						НайденнаяСтрока.БанковскийСчетОтправитель, 
						НайденнаяСтрока.ИГКСчетаПолучателя, 
						НайденнаяСтрока.БанковскийСчетПолучатель);
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
			КонецЦикла;
			
			Если КонтрольПройден Тогда
				Описание = НСтр("ru = 'Контроль пройден.';
								|en = 'Controlled.'");
				ОписаниеМассивСтрок.Добавить(Описание);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ДобавитьОписаниеРезультатаКонтроля(ИмяОбработчикаКонтроля, КонтрольПройден, ОписаниеМассивСтрок, ОписанияРезультатов);
КонецПроцедуры

Процедура РезультатКонтроля_Раздел1_275_85_2_1(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, Знач ДанныеДокументовДляКонтроля)
	ТипДокумента = ТипДокумента(Ссылка);
	Если НЕ ТипДокумента.Контролируется Тогда
		Возврат;
	КонецЕсли;
	
	ОписанияРезультатов = РезультатыПроверок.ОписанияРезультатов;
	ИмяОбработчикаКонтроля = "РезультатКонтроля_Раздел1_275_85_2_1";
	КонтрольПройден = Истина;
	
	ОписаниеМассивСтрок = Новый Массив;
	Описание = "";
	
	Если Ссылка.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ОплатаПоставщику
		И Ссылка.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту 
		И Ссылка.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет Тогда
		Описание = НСтр("ru = 'Контроль не требуется для операции ""%1"".';
						|en = 'Control is not required for transaction ""%1"".'");
		Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, Ссылка.ХозяйственнаяОперация);
		ОписаниеМассивСтрок.Добавить(Описание);
	Иначе
		Если ДанныеДокументовДляКонтроля = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.Текст =  ТекстЗапросаРезультатКонтроля_Раздел1_275_85_2_1();
			ПроверяемыеДокументы = Новый Массив;
			ПроверяемыеДокументы.Добавить(Ссылка);
			Запрос.УстановитьПараметр("ПроверяемыеДокументы", ПроверяемыеДокументы);
			
			ДанныеДокументовДляКонтроляТаблица = Запрос.Выполнить().Выгрузить();
		Иначе
			ДанныеДокументовДляКонтроляТаблица = ДанныеДокументовДляКонтроля[ИндексРезультатаЗапроса(ИндексыРезультатовЗапросов, ИмяОбработчикаКонтроля)].Выгрузить();
		КонецЕсли;
		
		Отбор = Новый Структура("Ссылка", Ссылка);
		НайденныеСтроки = ДанныеДокументовДляКонтроляТаблица.НайтиСтроки(Отбор);
		
		Если НайденныеСтроки.Количество() >= 1 Тогда
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
				// Контроль указания контракта
				Если Не НайденнаяСтрока.КонтрактУказан Тогда
					КонтрольПройден = Ложь;
					
					Описание = НСтр("ru = 'Не указан подтверждающий документ вида ""Контракт"".';
									|en = 'Justification document of the ""Contract"" kind is not specified.'"); 
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
			КонецЦикла;
			
			Если КонтрольПройден Тогда
				Описание = НСтр("ru = 'Контроль пройден.';
								|en = 'Controlled.'");
				ОписаниеМассивСтрок.Добавить(Описание);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ДобавитьОписаниеРезультатаКонтроля(ИмяОбработчикаКонтроля, КонтрольПройден, ОписаниеМассивСтрок, ОписанияРезультатов);
КонецПроцедуры

Процедура РезультатКонтроля_Раздел1_275_85_2_2(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, Знач ДанныеДокументовДляКонтроля)
	ТипДокумента = ТипДокумента(Ссылка);
	Если НЕ ТипДокумента.Контролируется Тогда
		Возврат;
	КонецЕсли;
	
	ОписанияРезультатов = РезультатыПроверок.ОписанияРезультатов;
	ИмяОбработчикаКонтроля = "РезультатКонтроля_Раздел1_275_85_2_2";
	КонтрольПройден = Истина;
	
	ОписаниеМассивСтрок = Новый Массив;
	Описание = "";
	
	Если Ссылка.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ОплатаПоставщику
		И Ссылка.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту 
		И Ссылка.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет Тогда
		Описание = НСтр("ru = 'Контроль не требуется для операции ""%1"".';
						|en = 'Control is not required for transaction ""%1"".'");
		Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, Ссылка.ХозяйственнаяОперация);
		ОписаниеМассивСтрок.Добавить(Описание);
	Иначе
		Если ДанныеДокументовДляКонтроля = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.Текст =  ТекстЗапросаРезультатКонтроля_Раздел1_275_85_2_2();
			ПроверяемыеДокументы = Новый Массив;
			ПроверяемыеДокументы.Добавить(Ссылка);
			Запрос.УстановитьПараметр("ПроверяемыеДокументы", ПроверяемыеДокументы);
			
			ДанныеДокументовДляКонтроляТаблица = Запрос.Выполнить().Выгрузить();
		Иначе
			ДанныеДокументовДляКонтроляТаблица = ДанныеДокументовДляКонтроля[ИндексРезультатаЗапроса(ИндексыРезультатовЗапросов, ИмяОбработчикаКонтроля)].Выгрузить();
		КонецЕсли;
		
		Отбор = Новый Структура("Ссылка", Ссылка);
		НайденныеСтроки = ДанныеДокументовДляКонтроляТаблица.НайтиСтроки(Отбор);
		
		Если НайденныеСтроки.Количество() >= 1 Тогда
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
				// Контроль указания документов, подтверждающих исполнение контракта
				Если Не НайденнаяСтрока.ДокументИсполненияУказан Тогда
					КонтрольПройден = Ложь;
					
					Описание = НСтр("ru = 'Не указан ни один документ, подтверждающий исполнение контракта.';
									|en = 'No document is specified that confirms contract execution.'");
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
			КонецЦикла;
			
			Если КонтрольПройден Тогда
				Описание = НСтр("ru = 'Контроль пройден.';
								|en = 'Controlled.'");
				ОписаниеМассивСтрок.Добавить(Описание);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ДобавитьОписаниеРезультатаКонтроля(ИмяОбработчикаКонтроля, КонтрольПройден, ОписаниеМассивСтрок, ОписанияРезультатов);
КонецПроцедуры

Функция ТекстЗапросаРезультатКонтроля_Раздел1_275_85_1_1()
	ТекстЗапроса = "ВЫБРАТЬ
	|	ПроверяемыеДокументыГруппировка.Ссылка КАК Ссылка,
	|	ПроверяемыеДокументыГруппировка.Заявка КАК Заявка,
	|	ПроверяемыеДокументыГруппировка.УИП КАК УИП,
	|	ПроверяемыеДокументыГруппировка.БанковскийСчетОтправитель КАК БанковскийСчетОтправитель,
	|	ПроверяемыеДокументыГруппировка.ЭтоОСБанковскийСчетОтправитель КАК ЭтоОСБанковскийСчетОтправитель,
	|	ПроверяемыеДокументыГруппировка.ИГКСчетаОтправителя КАК ИГКСчетаОтправителя,
	|	ПроверяемыеДокументыГруппировка.БанковскийСчетПолучатель КАК БанковскийСчетПолучатель,
	|	ПроверяемыеДокументыГруппировка.ЭтоОСБанковскийСчетПолучатель КАК ЭтоОСБанковскийСчетПолучатель,
	|	ПроверяемыеДокументыГруппировка.ИГКСчетаПолучателя КАК ИГКСчетаПолучателя,
	|	МАКСИМУМ(ПроверяемыеДокументыГруппировка.ЭтоОСБанковскийСчетОтправитель) = МАКСИМУМ(ПроверяемыеДокументыГруппировка.ЭтоОСБанковскийСчетПолучатель) КАК ВыбраныОтдельныеСчета,
	|	МАКСИМУМ(ПроверяемыеДокументыГруппировка.ИГКСчетаОтправителя) = МАКСИМУМ(ПроверяемыеДокументыГруппировка.ИГКСчетаПолучателя) КАК ИГКВыбранныхОтдельныхСчетовСовпадает
	|ИЗ
	|	(ВЫБРАТЬ
	|		РаспределениеПоСчетам.Ссылка КАК Ссылка,
	|		РаспределениеПоСчетам.Ссылка КАК Заявка,
	|		РаспределениеПоСчетам.Ссылка.ИдентификаторПлатежа КАК УИП,
	|		РаспределениеПоСчетам.БанковскийСчетКасса КАК БанковскийСчетОтправитель,
	|		РаспределениеПоСчетам.БанковскийСчетКасса.ОтдельныйСчетГОЗ КАК ЭтоОСБанковскийСчетОтправитель,
	|		РаспределениеПоСчетам.БанковскийСчетКасса.ГосударственныйКонтракт.Код КАК ИГКСчетаОтправителя,
	|		ВЫБОР
	|			КОГДА РаспределениеПоСчетам.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет)
	|				ТОГДА РаспределениеПоСчетам.Ссылка.БанковскийСчетПолучатель
	|			ИНАЧЕ РаспределениеПоСчетам.Ссылка.БанковскийСчетКонтрагента
	|		КОНЕЦ КАК БанковскийСчетПолучатель,
	|		ВЫБОР
	|			КОГДА РаспределениеПоСчетам.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет)
	|				ТОГДА РаспределениеПоСчетам.Ссылка.БанковскийСчетПолучатель.ОтдельныйСчетГОЗ
	|			ИНАЧЕ РаспределениеПоСчетам.Ссылка.БанковскийСчетКонтрагента.ОтдельныйСчетГОЗ
	|		КОНЕЦ КАК ЭтоОСБанковскийСчетПолучатель,
	|		ВЫБОР
	|			КОГДА РаспределениеПоСчетам.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет)
	|				ТОГДА РаспределениеПоСчетам.Ссылка.БанковскийСчетПолучатель.ГосударственныйКонтракт.Код
	|			ИНАЧЕ РаспределениеПоСчетам.Ссылка.БанковскийСчетКонтрагента.ГосударственныйКонтракт.Код
	|		КОНЕЦ КАК ИГКСчетаПолучателя
	|	ИЗ
	|		Документ.ЗаявкаНаРасходованиеДенежныхСредств.РаспределениеПоСчетам КАК РаспределениеПоСчетам
	|	ГДЕ
	|		РаспределениеПоСчетам.Ссылка В (&ПроверяемыеДокументы)
	|		И РаспределениеПоСчетам.Ссылка.ПлатежиПо275ФЗ
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписаниеСРасчетногоСчета.Ссылка,
	|		СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств,
	|		СписаниеСРасчетногоСчета.ИдентификаторПлатежа,
	|		СписаниеСРасчетногоСчета.БанковскийСчет,
	|		СписаниеСРасчетногоСчета.БанковскийСчет.ОтдельныйСчетГОЗ,
	|		СписаниеСРасчетногоСчета.БанковскийСчет.ГосударственныйКонтракт.Код,
	|		ВЫБОР
	|			КОГДА СписаниеСРасчетногоСчета.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет)
	|				ТОГДА СписаниеСРасчетногоСчета.БанковскийСчетПолучатель
	|			ИНАЧЕ СписаниеСРасчетногоСчета.БанковскийСчетКонтрагента
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА СписаниеСРасчетногоСчета.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет)
	|				ТОГДА СписаниеСРасчетногоСчета.БанковскийСчетПолучатель.ОтдельныйСчетГОЗ
	|			ИНАЧЕ СписаниеСРасчетногоСчета.БанковскийСчетКонтрагента.ОтдельныйСчетГОЗ
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА СписаниеСРасчетногоСчета.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет)
	|				ТОГДА СписаниеСРасчетногоСчета.БанковскийСчетПолучатель.ГосударственныйКонтракт.Код
	|			ИНАЧЕ СписаниеСРасчетногоСчета.БанковскийСчетКонтрагента.ГосударственныйКонтракт.Код
	|		КОНЕЦ
	|	ИЗ
	|		Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеСРасчетногоСчета
	|	ГДЕ
	|		СписаниеСРасчетногоСчета.Ссылка В (&ПроверяемыеДокументы)
	|		И СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств.ПлатежиПо275ФЗ
	|) КАК ПроверяемыеДокументыГруппировка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверяемыеДокументыГруппировка.Ссылка,
	|	ПроверяемыеДокументыГруппировка.Заявка,
	|	ПроверяемыеДокументыГруппировка.УИП,
	|	ПроверяемыеДокументыГруппировка.БанковскийСчетОтправитель,
	|	ПроверяемыеДокументыГруппировка.ЭтоОСБанковскийСчетОтправитель,
	|	ПроверяемыеДокументыГруппировка.ИГКСчетаОтправителя,
	|	ПроверяемыеДокументыГруппировка.БанковскийСчетПолучатель,
	|	ПроверяемыеДокументыГруппировка.ЭтоОСБанковскийСчетПолучатель,
	|	ПроверяемыеДокументыГруппировка.ИГКСчетаПолучателя";
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаРезультатКонтроля_Раздел1_275_85_2_1()
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ВидыДокументов.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ПроверяемыеВидыДокументов
	|ИЗ
	|	Справочник.ВидыПодтверждающихДокументов КАК ВидыДокументов
	|ГДЕ
	|	ВидыДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыПодтверждающихДокументов.Контракт)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроверяемыеДокументы.Ссылка КАК Ссылка,
	|	ПроверяемыеДокументы.Заявка КАК Заявка,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыПодтверждающихДокументов.Контракт)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КонтрактУказан,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыПодтверждающихДокументов.Контракт)
	|					И ЗаявкаПодтверждающиеДокументы.Номер <> """"
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КонтрактНомерЗаполнен,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыПодтверждающихДокументов.Контракт)
	|					И ЗаявкаПодтверждающиеДокументы.Дата <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КонтрактДатаЗаполнена,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыПодтверждающихДокументов.Контракт)
	|					И ЗаявкаПодтверждающиеДокументы.Сумма <> 0
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КонтрактСуммаЗаполнена,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыПодтверждающихДокументов.Контракт)
	|					И ЗаявкаПодтверждающиеДокументы.Файл <> Неопределено
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КонтрактФайлЗаполнен
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПроверяемыеДокументыГруппировка.Ссылка КАК Ссылка,
	|		ПроверяемыеДокументыГруппировка.Заявка КАК Заявка
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ЗаявкаНаРасходованиеДенежныхСредств.Ссылка КАК Ссылка,
	|			ЗаявкаНаРасходованиеДенежныхСредств.Ссылка КАК Заявка
	|		ИЗ
	|			Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ЗаявкаНаРасходованиеДенежныхСредств
	|		ГДЕ
	|			ЗаявкаНаРасходованиеДенежныхСредств.Ссылка В (&ПроверяемыеДокументы)
	|			И ЗаявкаНаРасходованиеДенежныхСредств.ПлатежиПо275ФЗ
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			СписаниеСРасчетногоСчета.Ссылка,
	|			СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств
	|		ИЗ
	|			Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеСРасчетногоСчета
	|		ГДЕ
	|			СписаниеСРасчетногоСчета.Ссылка В (&ПроверяемыеДокументы)
	|			И СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств.ПлатежиПо275ФЗ
	|) КАК ПроверяемыеДокументыГруппировка
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПроверяемыеДокументыГруппировка.Ссылка,
	|		ПроверяемыеДокументыГруппировка.Заявка) КАК ПроверяемыеДокументы
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРасходованиеДенежныхСредств.ПодтверждающиеДокументы КАК ЗаявкаПодтверждающиеДокументы
	|			ПО ПроверяемыеДокументы.Заявка = ЗаявкаПодтверждающиеДокументы.Ссылка
	|				И (ЗаявкаПодтверждающиеДокументы.ВидДокумента В
	|					(ВЫБРАТЬ
	|						ПроверяемыеВидыДокументов.Ссылка
	|					ИЗ
	|						ПроверяемыеВидыДокументов КАК ПроверяемыеВидыДокументов))
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверяемыеДокументы.Ссылка,
	|	ПроверяемыеДокументы.Заявка";
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаРезультатКонтроля_Раздел1_275_85_2_2()
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ВидыДокументов.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ПроверяемыеВидыДокументов
	|ИЗ
	|	Справочник.ВидыПодтверждающихДокументов КАК ВидыДокументов
	|ГДЕ
	|	ВидыДокументов.ПодтверждаетИсполнениеКонтракта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроверяемыеДокументы.Ссылка КАК Ссылка,
	|	ПроверяемыеДокументы.Заявка КАК Заявка,
	|	МАКСИМУМ(ЕстьNull(ЗаявкаПодтверждающиеДокументы.ВидДокумента.ПодтверждаетИсполнениеКонтракта, ЛОЖЬ)) КАК ДокументИсполненияУказан,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента.ПодтверждаетИсполнениеКонтракта
	|					И ЗаявкаПодтверждающиеДокументы.Номер <> """"
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ДокументИсполненияНомерЗаполнен,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента.ПодтверждаетИсполнениеКонтракта
	|					И ЗаявкаПодтверждающиеДокументы.Дата <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ДокументИсполненияДатаЗаполнена,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента.ПодтверждаетИсполнениеКонтракта
	|					И ЗаявкаПодтверждающиеДокументы.Сумма <> 0
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ДокументИсполненияСуммаЗаполнена,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаявкаПодтверждающиеДокументы.ВидДокумента.ПодтверждаетИсполнениеКонтракта
	|					И ЗаявкаПодтверждающиеДокументы.Файл <> Неопределено
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ДокументИсполненияФайлЗаполнен
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПроверяемыеДокументыГруппировка.Ссылка КАК Ссылка,
	|		ПроверяемыеДокументыГруппировка.Заявка КАК Заявка
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ЗаявкаНаРасходованиеДенежныхСредств.Ссылка КАК Ссылка,
	|			ЗаявкаНаРасходованиеДенежныхСредств.Ссылка КАК Заявка
	|		ИЗ
	|			Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ЗаявкаНаРасходованиеДенежныхСредств
	|		ГДЕ
	|			ЗаявкаНаРасходованиеДенежныхСредств.Ссылка В (&ПроверяемыеДокументы)
	|			И ЗаявкаНаРасходованиеДенежныхСредств.ПлатежиПо275ФЗ
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			СписаниеСРасчетногоСчета.Ссылка,
	|			СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств
	|		ИЗ
	|			Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеСРасчетногоСчета
	|		ГДЕ
	|			СписаниеСРасчетногоСчета.Ссылка В (&ПроверяемыеДокументы)
	|			И СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств.ПлатежиПо275ФЗ
	|) КАК ПроверяемыеДокументыГруппировка
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПроверяемыеДокументыГруппировка.Ссылка,
	|		ПроверяемыеДокументыГруппировка.Заявка) КАК ПроверяемыеДокументы
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРасходованиеДенежныхСредств.ПодтверждающиеДокументы КАК ЗаявкаПодтверждающиеДокументы
	|			ПО ПроверяемыеДокументы.Заявка = ЗаявкаПодтверждающиеДокументы.Ссылка
	|				И (ЗаявкаПодтверждающиеДокументы.ВидДокумента В
	|					(ВЫБРАТЬ
	|						ПроверяемыеВидыДокументов.Ссылка
	|					ИЗ
	|						ПроверяемыеВидыДокументов КАК ПроверяемыеВидыДокументов))
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверяемыеДокументы.Ссылка,
	|	ПроверяемыеДокументы.Заявка";
	Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти

#КонецОбласти

#Область КонтрольРаздела2
// Контроль раздела II "Дополнительные критерии контроля"

Процедура РезультатКонтроля_Раздел2_3729У_2_1(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, Знач ДанныеДокументовДляКонтроля)
	ТипДокумента = ТипДокумента(Ссылка);
	Если НЕ ТипДокумента.Контролируется Тогда
		Возврат;
	КонецЕсли;
	
	ОписанияРезультатов = РезультатыПроверок.ОписанияРезультатов;
	ИмяОбработчикаКонтроля = "РезультатКонтроля_Раздел2_3729У_2_1";
	КонтрольПройден = Истина;
	
	ОписаниеМассивСтрок = Новый Массив;
	Описание = "";
	
	Если Ссылка.ТипПлатежаФЗ275 <> Справочники.ТипыПлатежейФЗ275.УплатаНалоговИСборовТаможенныхПлатежейСтраховыхВзносов Тогда
		Описание = НСтр("ru = 'Контроль не требуется для типа платежа 275-ФЗ ""%1"".';
						|en = 'Control is not required for payment type 275-FZ ""%1"".'");
		Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, Ссылка.ТипПлатежаФЗ275);
		ОписаниеМассивСтрок.Добавить(Описание);
	Иначе
		Если ДанныеДокументовДляКонтроля = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.Текст =  ТекстЗапросаРезультатКонтроля_Раздел2_3729У_2_1();
			ПроверяемыеДокументы = Новый Массив;
			ПроверяемыеДокументы.Добавить(Ссылка);
			Запрос.УстановитьПараметр("ПроверяемыеДокументы", ПроверяемыеДокументы);
			Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
			
			ДанныеДокументовДляКонтроляТаблица = Запрос.Выполнить().Выгрузить();
		Иначе
			ДанныеДокументовДляКонтроляТаблица = ДанныеДокументовДляКонтроля[ИндексРезультатаЗапроса(ИндексыРезультатовЗапросов, ИмяОбработчикаКонтроля)].Выгрузить();
		КонецЕсли;
		
		Отбор = Новый Структура("Ссылка", Ссылка);
		НайденныеСтроки = ДанныеДокументовДляКонтроляТаблица.НайтиСтроки(Отбор);
		
		Если НайденныеСтроки.Количество() >= 1 Тогда
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
				Если НайденнаяСтрока.СуммаДокумента + НайденнаяСтрока.СуммаФакт > 0.5 * НайденнаяСтрока.СуммаКонтракта Тогда
					КонтрольПройден = Ложь;
					
					ЧастиОписания = Новый Массив;
					Если НайденнаяСтрока.ЭтоСписание Тогда
						ЧастиОписания.Добавить(НСтр("ru = 'Сумма платежа';
													|en = 'Payment amount'")); 
					ИначеЕсли НайденнаяСтрока.ЭтоЗаявка Тогда
						ЧастиОписания.Добавить(НСтр("ru = 'Сумма заявки';
													|en = 'Request amount'")); 
					КонецЕсли;
					
					ЧастиОписания.Добавить(НСтр("ru = '%1 руб. с учетом ранее созданных распоряжений на сумму %2 руб. превышают 50% от цены контракта %3 руб..';
												|en = '%1 rub. considering previously created references to the amount of %2 rub. exceed 50% of the contract price of %3 rub.'"));
					
					Описание = СтрСоединить(ЧастиОписания, " ");
					
					Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, 
						Формат(НайденнаяСтрока.СуммаДокумента, "ЧДЦ=2; ЧН="), 
						Формат(НайденнаяСтрока.СуммаФакт, "ЧДЦ=2; ЧН="), 
						Формат(НайденнаяСтрока.СуммаКонтракта, "ЧДЦ=2; ЧН="));
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
			КонецЦикла;
			
			Если КонтрольПройден Тогда
				Описание = НСтр("ru = 'Контроль пройден.';
								|en = 'Controlled.'");
				ОписаниеМассивСтрок.Добавить(Описание);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ДобавитьОписаниеРезультатаКонтроля(ИмяОбработчикаКонтроля, КонтрольПройден, ОписаниеМассивСтрок, ОписанияРезультатов);
КонецПроцедуры

Процедура РезультатКонтроля_Раздел2_3729У_2_2(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, Знач ДанныеДокументовДляКонтроля)
	ТипДокумента = ТипДокумента(Ссылка);
	Если НЕ ТипДокумента.Контролируется Тогда
		Возврат;
	КонецЕсли;
	
	ОписанияРезультатов = РезультатыПроверок.ОписанияРезультатов;
	ИмяОбработчикаКонтроля = "РезультатКонтроля_Раздел2_3729У_2_2";
	КонтрольПройден = Истина;
	
	ОписаниеМассивСтрок = Новый Массив;
	Описание = "";
	
	Если Ссылка.ТипПлатежаФЗ275 <> Справочники.ТипыПлатежейФЗ275.ПеречислениеПрибылиГоловномуИсполнителю
		И Ссылка.ТипПлатежаФЗ275 <> Справочники.ТипыПлатежейФЗ275.ПеречисленияПрибылиИсполнителю Тогда
		Описание = НСтр("ru = 'Контроль не требуется для типа платежа 275-ФЗ ""%1"".';
						|en = 'Control is not required for payment type 275-FZ ""%1"".'");
		Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, Ссылка.ТипПлатежаФЗ275);
		ОписаниеМассивСтрок.Добавить(Описание);
	Иначе
		Если ДанныеДокументовДляКонтроля = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.Текст =  ТекстЗапросаРезультатКонтроля_Раздел2_3729У_2_2();
			ПроверяемыеДокументы = Новый Массив;
			ПроверяемыеДокументы.Добавить(Ссылка);
			Запрос.УстановитьПараметр("ПроверяемыеДокументы", ПроверяемыеДокументы);
			Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
			
			ДанныеДокументовДляКонтроляТаблица = Запрос.Выполнить().Выгрузить();
		Иначе
			ДанныеДокументовДляКонтроляТаблица = ДанныеДокументовДляКонтроля[ИндексРезультатаЗапроса(ИндексыРезультатовЗапросов, ИмяОбработчикаКонтроля)].Выгрузить();
		КонецЕсли;
		
		Отбор = Новый Структура("Ссылка", Ссылка);
		НайденныеСтроки = ДанныеДокументовДляКонтроляТаблица.НайтиСтроки(Отбор);
		
		Если НайденныеСтроки.Количество() >= 1 Тогда
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
				Если НайденнаяСтрока.СуммаДокумента + НайденнаяСтрока.СуммаФакт > 0.2 * НайденнаяСтрока.СуммаКонтракта Тогда
					КонтрольПройден = Ложь;
					
					ЧастиОписания = Новый Массив;
					Если НайденнаяСтрока.ЭтоСписание Тогда
						ЧастиОписания.Добавить(НСтр("ru = 'Сумма платежа';
													|en = 'Payment amount'")); 
					ИначеЕсли НайденнаяСтрока.ЭтоЗаявка Тогда
						ЧастиОписания.Добавить(НСтр("ru = 'Сумма заявки';
													|en = 'Request amount'")); 
					КонецЕсли;
					
					ЧастиОписания.Добавить(НСтр("ru = '%1 руб. с учетом ранее созданных распоряжений на сумму %2 руб. превышают 50% от цены контракта %3 руб..';
												|en = '%1 rub. considering previously created references to the amount of %2 rub. exceed 50% of the contract price of %3 rub.'"));
					
					Описание = СтрСоединить(ЧастиОписания, " ");
					
					Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, 
						Формат(НайденнаяСтрока.СуммаДокумента, "ЧДЦ=2; ЧН="), 
						Формат(НайденнаяСтрока.СуммаФакт, "ЧДЦ=2; ЧН="), 
						Формат(НайденнаяСтрока.СуммаКонтракта, "ЧДЦ=2; ЧН="));
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
			КонецЦикла;
			
			Если КонтрольПройден Тогда
				Описание = НСтр("ru = 'Контроль пройден.';
								|en = 'Controlled.'");
				ОписаниеМассивСтрок.Добавить(Описание);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ДобавитьОписаниеРезультатаКонтроля(ИмяОбработчикаКонтроля, КонтрольПройден, ОписаниеМассивСтрок, ОписанияРезультатов);
КонецПроцедуры

Процедура РезультатКонтроля_Раздел2_3729У_2_3(РезультатыПроверок, ИндексыРезультатовЗапросов, Ссылка, Знач ДанныеДокументовДляКонтроля)
	ТипДокумента = ТипДокумента(Ссылка);
	Если НЕ ТипДокумента.Контролируется Тогда
		Возврат;
	КонецЕсли;
	
	ОписанияРезультатов = РезультатыПроверок.ОписанияРезультатов;
	ИмяОбработчикаКонтроля = "РезультатКонтроля_Раздел2_3729У_2_3";
	КонтрольПройден = Истина;
	
	ОписаниеМассивСтрок = Новый Массив;
	Описание = "";
	
	Если Ссылка.ТипПлатежаФЗ275 = Справочники.ТипыПлатежейФЗ275.УплатаНалоговИСборовТаможенныхПлатежейСтраховыхВзносов Тогда
		Описание = НСтр("ru = 'Контроль не требуется для типа платежа 275-ФЗ ""%1"".';
						|en = 'Control is not required for payment type 275-FZ ""%1"".'");
		Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, Ссылка.ТипПлатежаФЗ275);
		ОписаниеМассивСтрок.Добавить(Описание);
	Иначе
		Если ДанныеДокументовДляКонтроля = Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.Текст =  ТекстЗапросаРезультатКонтроля_Раздел2_3729У_2_3();
			ПроверяемыеДокументы = Новый Массив;
			ПроверяемыеДокументы.Добавить(Ссылка);
			Запрос.УстановитьПараметр("ПроверяемыеДокументы", ПроверяемыеДокументы);
			
			ДанныеДокументовДляКонтроляТаблица = Запрос.Выполнить().Выгрузить();
		Иначе
			ДанныеДокументовДляКонтроляТаблица = ДанныеДокументовДляКонтроля[ИндексРезультатаЗапроса(ИндексыРезультатовЗапросов, ИмяОбработчикаКонтроля)].Выгрузить();
		КонецЕсли;
		
		Отбор = Новый Структура("Ссылка", Ссылка);
		НайденныеСтроки = ДанныеДокументовДляКонтроляТаблица.НайтиСтроки(Отбор);
		
		Если НайденныеСтроки.Количество() >= 1 Тогда
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
				Если Макс(НайденнаяСтрока.СуммаЗаявок, НайденнаяСтрока.СуммаОплат) > НайденнаяСтрока.ПодтверждающийДокументСумма Тогда
					КонтрольПройден = Ложь;
					
					ЧастиПредставления = Новый Массив;
					ЧастиПредставления.Добавить(НайденнаяСтрока.ПодтверждающийДокументВид);
					Если ЗначениеЗаполнено(НайденнаяСтрока.ПодтверждающийДокументНомер) Тогда
						ЧастиПредставления.Добавить(НСтр("ru = '№';
														|en = 'No.'"));
						ЧастиПредставления.Добавить(НайденнаяСтрока.ПодтверждающийДокументНомер);
					КонецЕсли;
					Если ЗначениеЗаполнено(НайденнаяСтрока.ПодтверждающийДокументДата) Тогда
						ЧастиПредставления.Добавить(НСтр("ru = 'от';
														|en = 'dated'") );
						ЧастиПредставления.Добавить(Формат(НайденнаяСтрока.ПодтверждающийДокументДата, "ДЛФ=D"));
					КонецЕсли;
					ПредставлениеДокумента = СтрСоединить(ЧастиПредставления, Символы.НПП);
					
					ЧастиОписания = Новый Массив;
					Если НайденнаяСтрока.ЭтоСписание Тогда
						ЧастиОписания.Добавить(НСтр("ru = 'Сумма платежа';
													|en = 'Payment amount'")); 
					ИначеЕсли НайденнаяСтрока.ЭтоЗаявка Тогда
						ЧастиОписания.Добавить(НСтр("ru = 'Сумма заявки';
													|en = 'Request amount'")); 
					КонецЕсли;
					
					ЧастиОписания.Добавить(НСтр("ru = '%1 руб. с учетом ранее созданных распоряжений на сумму %2 руб. и оплат на сумму %3 руб. превышают сумму %4 руб. подтверждающего документа ""%5"".';
												|en = '%1 rub. considering previously created references to the amount of %2 rub. and payments to the amount of %3 rub. exceed the amount of %4 rub. of the ""%5"" justification document.'"));
					
					Описание = СтрСоединить(ЧастиОписания, " ");
						
					Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Описание, 
						Формат(НайденнаяСтрока.СуммаДокумента, "ЧДЦ=2; ЧН="), 
						Формат(НайденнаяСтрока.СуммаЗаявок, "ЧДЦ=2; ЧН="), 
						Формат(НайденнаяСтрока.СуммаОплат, "ЧДЦ=2; ЧН="), 
						Формат(НайденнаяСтрока.ПодтверждающийДокументСумма, "ЧДЦ=2; ЧН="), 
						ПредставлениеДокумента);
					ОписаниеМассивСтрок.Добавить(Описание);
				КонецЕсли;
			КонецЦикла;
			
			Если КонтрольПройден Тогда
				Описание = НСтр("ru = 'Контроль пройден.';
								|en = 'Controlled.'");
				ОписаниеМассивСтрок.Добавить(Описание);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ДобавитьОписаниеРезультатаКонтроля(ИмяОбработчикаКонтроля, КонтрольПройден, ОписаниеМассивСтрок, ОписанияРезультатов);
КонецПроцедуры

Функция ТекстЗапросаРезультатКонтроля_Раздел2_3729У_2_1()
	ТекстЗапроса =  "ВЫБРАТЬ
	|	ПроверяемыеЗаявкиГруппировка.Ссылка КАК Ссылка,
	|	ПроверяемыеЗаявкиГруппировка.Заявка КАК Заявка
	|ПОМЕСТИТЬ ПроверяемыеДокументы
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка КАК Ссылка,
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка КАК Заявка
	|	ИЗ
	|		Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ЗаявкаНаРасходованиеДенежныхСредств
	|	ГДЕ
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка В(&ПроверяемыеДокументы)
	|		И ЗаявкаНаРасходованиеДенежныхСредств.ПлатежиПо275ФЗ
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписаниеСРасчетногоСчета.Ссылка,
	|		СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств
	|	ИЗ
	|		Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеСРасчетногоСчета
	|	ГДЕ
	|		СписаниеСРасчетногоСчета.Ссылка В(&ПроверяемыеДокументы)
	|		И СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств.ПлатежиПо275ФЗ
	|) КАК ПроверяемыеЗаявкиГруппировка
	|ГДЕ
	|	ПроверяемыеЗаявкиГруппировка.Заявка.ТипПлатежаФЗ275 = ЗНАЧЕНИЕ(Справочник.ТипыПлатежейФЗ275.УплатаНалоговИСборовТаможенныхПлатежейСтраховыхВзносов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверяемыеЗаявкиГруппировка.Ссылка,
	|	ПроверяемыеЗаявкиГруппировка.Заявка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	РасшифровкаПлатежа.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА РасшифровкаПлатежа.ОбъектРасчетов.Объект ССЫЛКА Справочник.ДоговорыКонтрагентов
	|			ТОГДА ВЫРАЗИТЬ(РасшифровкаПлатежа.ОбъектРасчетов.Объект КАК Справочник.ДоговорыКонтрагентов)
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Договор
	|ПОМЕСТИТЬ ДоговорыСписаний
	|ИЗ
	|	Документ.СписаниеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК РасшифровкаПлатежа
	|ГДЕ
	|	РасшифровкаПлатежа.Ссылка В
	|			(ВЫБРАТЬ
	|				ПроверяемыеДокументы.Ссылка
	|			ИЗ
	|				ПроверяемыеДокументы КАК ПроверяемыеДокументы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПроверяемыеДокументы.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(ДоговорыВЗаявке.Ссылка, ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)) КАК КонтрактСЗаказчиком,
	|	ЕСТЬNULL(ДоговорыВЗаявке.Сумма, 0) КАК СуммаКонтракта
	|ПОМЕСТИТЬ КонтрактыСЗаказчиками
	|ИЗ
	|	ПроверяемыеДокументы КАК ПроверяемыеДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыВЗаявке
	|		ПО ПроверяемыеДокументы.Заявка.БанковскийСчет.ГосударственныйКонтракт = ДоговорыВЗаявке.ГосударственныйКонтракт
	|			И ДоговорыВЗаявке.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СПокупателем)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДвиженияДенежныеСредстваКонтрагентОбороты.Договор КАК Договор,
	|	СУММА(ДвиженияДенежныеСредстваКонтрагентОбороты.СуммаОплатыРеглОборот) КАК СуммаОплат
	|ПОМЕСТИТЬ ОплатыПоДоговорам
	|ИЗ
	|	РегистрНакопления.ДвиженияДенежныеСредстваКонтрагент.Обороты(
	|			НАЧАЛОПЕРИОДА(&ТекущаяДата, МЕСЯЦ),
	|			КОНЕЦПЕРИОДА(&ТекущаяДата, МЕСЯЦ),
	|			Авто,
	|			ТипДенежныхСредств = ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.Безналичные)
	|				И ТипПлатежаФЗ275 = ЗНАЧЕНИЕ(Справочник.ТипыПлатежейФЗ275.УплатаНалоговИСборовТаможенныхПлатежейСтраховыхВзносов)
	|				И Договор В
	|					(ВЫБРАТЬ
	|						КонтрактыСЗаказчиками.КонтрактСЗаказчиком
	|					ИЗ
	|						КонтрактыСЗаказчиками)) КАК ДвиженияДенежныеСредстваКонтрагентОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияДенежныеСредстваКонтрагентОбороты.Договор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроверяемыеДокументы.Ссылка КАК Ссылка,
	|	ПроверяемыеДокументы.Ссылка ССЫЛКА Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ЭтоЗаявка,
	|	ПроверяемыеДокументы.Ссылка ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств  КАК ЭтоСписание,
	|	ПроверяемыеДокументы.Заявка КАК Заявка,
	|	ПроверяемыеДокументы.Ссылка.СуммаДокумента КАК СуммаДокумента,
	|	СУММА(КонтрактыСЗаказчиками.СуммаКонтракта) КАК СуммаКонтракта,
	|	СУММА(ЕСТЬNULL(ОплатыПоДоговорам.СуммаОплат, 0)) КАК СуммаФакт
	|ИЗ
	|	ПроверяемыеДокументы КАК ПроверяемыеДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтрактыСЗаказчиками КАК КонтрактыСЗаказчиками
	|		ПО ПроверяемыеДокументы.Ссылка = КонтрактыСЗаказчиками.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОплатыПоДоговорам КАК ОплатыПоДоговорам
	|		ПО (КонтрактыСЗаказчиками.КонтрактСЗаказчиком = ОплатыПоДоговорам.Договор)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверяемыеДокументы.Ссылка,
	|	ПроверяемыеДокументы.Заявка,
	|	ПроверяемыеДокументы.Ссылка.СуммаДокумента";
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаРезультатКонтроля_Раздел2_3729У_2_2()
	ТекстЗапроса =  "ВЫБРАТЬ
	|	ПроверяемыеЗаявкиГруппировка.Ссылка КАК Ссылка,
	|	ПроверяемыеЗаявкиГруппировка.Заявка КАК Заявка
	|ПОМЕСТИТЬ ПроверяемыеДокументы
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка КАК Ссылка,
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка КАК Заявка
	|	ИЗ
	|		Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ЗаявкаНаРасходованиеДенежныхСредств
	|	ГДЕ
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка В(&ПроверяемыеДокументы)
	|		И ЗаявкаНаРасходованиеДенежныхСредств.ПлатежиПо275ФЗ
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписаниеСРасчетногоСчета.Ссылка,
	|		СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств
	|	ИЗ
	|		Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеСРасчетногоСчета
	|	ГДЕ
	|		СписаниеСРасчетногоСчета.Ссылка В(&ПроверяемыеДокументы)
	|		И СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств.ПлатежиПо275ФЗ
	|) КАК ПроверяемыеЗаявкиГруппировка
	|ГДЕ
	|	ПроверяемыеЗаявкиГруппировка.Заявка.ТипПлатежаФЗ275 В (ЗНАЧЕНИЕ(Справочник.ТипыПлатежейФЗ275.ПеречислениеПрибылиГоловномуИсполнителю), ЗНАЧЕНИЕ(Справочник.ТипыПлатежейФЗ275.ПеречисленияПрибылиИсполнителю))
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверяемыеЗаявкиГруппировка.Ссылка,
	|	ПроверяемыеЗаявкиГруппировка.Заявка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	РасшифровкаПлатежа.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА РасшифровкаПлатежа.ОбъектРасчетов.Объект ССЫЛКА Справочник.ДоговорыКонтрагентов
	|			ТОГДА ВЫРАЗИТЬ(РасшифровкаПлатежа.ОбъектРасчетов.Объект КАК Справочник.ДоговорыКонтрагентов)
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Договор
	|ПОМЕСТИТЬ ДоговорыСписаний
	|ИЗ
	|	Документ.СписаниеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК РасшифровкаПлатежа
	|ГДЕ
	|	РасшифровкаПлатежа.Ссылка В
	|			(ВЫБРАТЬ
	|				ПроверяемыеДокументы.Ссылка
	|			ИЗ
	|				ПроверяемыеДокументы КАК ПроверяемыеДокументы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроверяемыеДокументы.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(ДоговорыСписаний.Договор, ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)) КАК КонтрактСЗаказчиком,
	|	СУММА(ЕСТЬNULL(ДоговорыСписаний.Договор.Сумма, 0)) КАК СуммаКонтракта
	|ПОМЕСТИТЬ КонтрактыСЗаказчиками
	|ИЗ
	|	ПроверяемыеДокументы КАК ПроверяемыеДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыВЗаявке
	|		ПО ПроверяемыеДокументы.Заявка.БанковскийСчет.ГосударственныйКонтракт = ДоговорыВЗаявке.ГосударственныйКонтракт
	|			И ДоговорыВЗаявке.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СПокупателем)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыСКонтрагентами
	|		ПО (ПроверяемыеДокументы.Ссылка ССЫЛКА Документ.ЗаявкаНаРасходованиеДенежныхСредств)
	|			И (ДоговорыВЗаявке.Ссылка = ДоговорыСКонтрагентами.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДоговорыСписаний КАК ДоговорыСписаний
	|		ПО (ПроверяемыеДокументы.Ссылка ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств)
	|			И ПроверяемыеДокументы.Ссылка = ДоговорыСписаний.Ссылка
	|			И (ДоговорыСписаний.Договор = ДоговорыВЗаявке.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверяемыеДокументы.Ссылка,
	|	ЕСТЬNULL(ДоговорыСписаний.Договор, ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДвиженияДенежныеСредстваКонтрагентОбороты.Договор КАК Договор,
	|	СУММА(ДвиженияДенежныеСредстваКонтрагентОбороты.СуммаОплатыРеглОборот) КАК СуммаОплат
	|ПОМЕСТИТЬ ОплатыПоДоговорам
	|ИЗ
	|	РегистрНакопления.ДвиженияДенежныеСредстваКонтрагент.Обороты(
	|			НАЧАЛОПЕРИОДА(&ТекущаяДата, МЕСЯЦ),
	|			КОНЕЦПЕРИОДА(&ТекущаяДата, МЕСЯЦ),
	|			Авто,
	|			ТипДенежныхСредств = ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.Безналичные)
	|				И ТипПлатежаФЗ275 В (ЗНАЧЕНИЕ(Справочник.ТипыПлатежейФЗ275.ПеречислениеПрибылиГоловномуИсполнителю), ЗНАЧЕНИЕ(Справочник.ТипыПлатежейФЗ275.ПеречисленияПрибылиИсполнителю))
	|				И Договор В
	|					(ВЫБРАТЬ
	|						КонтрактыСЗаказчиками.КонтрактСЗаказчиком
	|					ИЗ
	|						КонтрактыСЗаказчиками)) КАК ДвиженияДенежныеСредстваКонтрагентОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияДенежныеСредстваКонтрагентОбороты.Договор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроверяемыеДокументы.Ссылка КАК Ссылка,
	|	ПроверяемыеДокументы.Ссылка ССЫЛКА Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ЭтоЗаявка,
	|	ПроверяемыеДокументы.Ссылка ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств  КАК ЭтоСписание,
	|	ПроверяемыеДокументы.Заявка КАК Заявка,
	|	ПроверяемыеДокументы.Ссылка.СуммаДокумента КАК СуммаДокумента,
	|	СУММА(КонтрактыСЗаказчиками.СуммаКонтракта) КАК СуммаКонтракта,
	|	СУММА(ЕСТЬNULL(ОплатыПоДоговорам.СуммаОплат, 0)) КАК СуммаФакт
	|ИЗ
	|	ПроверяемыеДокументы КАК ПроверяемыеДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонтрактыСЗаказчиками КАК КонтрактыСЗаказчиками
	|		ПО ПроверяемыеДокументы.Ссылка = КонтрактыСЗаказчиками.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОплатыПоДоговорам КАК ОплатыПоДоговорам
	|		ПО (КонтрактыСЗаказчиками.КонтрактСЗаказчиком = ОплатыПоДоговорам.Договор)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверяемыеДокументы.Ссылка,
	|	ПроверяемыеДокументы.Заявка,
	|	ПроверяемыеДокументы.Ссылка.СуммаДокумента";
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаРезультатКонтроля_Раздел2_3729У_2_3()
	ТекстЗапроса = "ВЫБРАТЬ
	|	ВидыДокументов.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ПроверяемыеВидыДокументов
	|ИЗ
	|	Справочник.ВидыПодтверждающихДокументов КАК ВидыДокументов
	|ГДЕ
	|	ВидыДокументов.ПодтверждаетИсполнениеКонтракта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроверяемыеЗаявкиГруппировка.Ссылка КАК Ссылка,
	|	ПроверяемыеЗаявкиГруппировка.Заявка КАК Заявка
	|ПОМЕСТИТЬ ПроверяемыеДокументы
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка КАК Ссылка,
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка КАК Заявка
	|	ИЗ
	|		Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ЗаявкаНаРасходованиеДенежныхСредств
	|	ГДЕ
	|		ЗаявкаНаРасходованиеДенежныхСредств.Ссылка В(&ПроверяемыеДокументы)
	|		И ЗаявкаНаРасходованиеДенежныхСредств.ПлатежиПо275ФЗ
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписаниеСРасчетногоСчета.Ссылка,
	|		СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств
	|	ИЗ
	|		Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеСРасчетногоСчета
	|	ГДЕ
	|		СписаниеСРасчетногоСчета.Ссылка В(&ПроверяемыеДокументы)
	|		И СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств.ПлатежиПо275ФЗ
	|) КАК ПроверяемыеЗаявкиГруппировка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПроверяемыеЗаявкиГруппировка.Ссылка,
	|	ПроверяемыеЗаявкиГруппировка.Заявка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроверяемыеДокументы.Ссылка КАК Ссылка,
	|	ПроверяемыеДокументы.Заявка КАК ПроверяемаяЗаявка,
	|	ЕСТЬNULL(ПроверяемыеДокументы.Ссылка.СуммаДокумента, 0) КАК СуммаДокумента,
	|	ЕСТЬNULL(ЗаявкаПодтверждающиеДокументы.ВидДокумента, Неопределено) КАК ПодтверждающийДокументВид,
	|	ЕСТЬNULL(ЗаявкаПодтверждающиеДокументы.Номер, Неопределено) КАК ПодтверждающийДокументНомер,
	|	ЕСТЬNULL(ЗаявкаПодтверждающиеДокументы.Дата, Неопределено) КАК ПодтверждающийДокументДата,
	|	ЕСТЬNULL(ЗаявкаПодтверждающиеДокументы.Сумма, 0) КАК ПодтверждающийДокументСумма,
	|	ЗаявкаНаРасходованиеДенежныхСредствПодтверждающиеДокументы.Ссылка КАК Заявка
	|ПОМЕСТИТЬ ЗаявкиИспользующиеПодтверждающиеДокументы
	|ИЗ
	|	ПроверяемыеДокументы КАК ПроверяемыеДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРасходованиеДенежныхСредств.ПодтверждающиеДокументы КАК ЗаявкаПодтверждающиеДокументы
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаРасходованиеДенежныхСредств.ПодтверждающиеДокументы КАК ЗаявкаНаРасходованиеДенежныхСредствПодтверждающиеДокументы
	|			ПО ЗаявкаПодтверждающиеДокументы.Файл = ЗаявкаНаРасходованиеДенежныхСредствПодтверждающиеДокументы.Файл
	|		ПО ПроверяемыеДокументы.Заявка = ЗаявкаПодтверждающиеДокументы.Ссылка
	|			И (ЗаявкаПодтверждающиеДокументы.ВидДокумента В
	|				(ВЫБРАТЬ
	|					ПроверяемыеВидыДокументов.Ссылка
	|				ИЗ
	|					ПроверяемыеВидыДокументов КАК ПроверяемыеВидыДокументов))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Заявка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Заявки.Заявка КАК Заявка,
	|	СУММА(ЕСТЬNULL(СписаниеСРасчетногоСчета.СуммаДокумента, 0)) КАК Сумма
	|ПОМЕСТИТЬ ОплатыПоЗаявкам
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаявкиИспользующиеПодтверждающиеДокументы.Заявка КАК Заявка
	|	ИЗ
	|		ЗаявкиИспользующиеПодтверждающиеДокументы КАК ЗаявкиИспользующиеПодтверждающиеДокументы
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ЗаявкиИспользующиеПодтверждающиеДокументы.Заявка) КАК Заявки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеСРасчетногоСчета
	|		ПО Заявки.Заявка = СписаниеСРасчетногоСчета.ЗаявкаНаРасходованиеДенежныхСредств
	|			И (СписаниеСРасчетногоСчета.Проведен)
	|			И (СписаниеСРасчетногоСчета.БанковскийСчет.ОтдельныйСчетГОЗ)
	|
	|СГРУППИРОВАТЬ ПО
	|	Заявки.Заявка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Заявка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаявкиИспользующиеПодтверждающиеДокументы.Ссылка КАК Ссылка,
	|	ЗаявкиИспользующиеПодтверждающиеДокументы.Ссылка ССЫЛКА Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ЭтоЗаявка,
	|	ЗаявкиИспользующиеПодтверждающиеДокументы.Ссылка ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств  КАК ЭтоСписание,
	|	ЗаявкиИспользующиеПодтверждающиеДокументы.СуммаДокумента КАК СуммаДокумента,
	|	СУММА(ЗаявкиИспользующиеПодтверждающиеДокументы.СуммаДокумента) КАК СуммаЗаявок,
	|	ЗаявкиИспользующиеПодтверждающиеДокументы.ПодтверждающийДокументВид КАК ПодтверждающийДокументВид,
	|	ЗаявкиИспользующиеПодтверждающиеДокументы.ПодтверждающийДокументНомер КАК ПодтверждающийДокументНомер,
	|	ЗаявкиИспользующиеПодтверждающиеДокументы.ПодтверждающийДокументДата КАК ПодтверждающийДокументДата,
	|	МАКСИМУМ(ЗаявкиИспользующиеПодтверждающиеДокументы.ПодтверждающийДокументСумма) КАК ПодтверждающийДокументСумма,
	|	СУММА(ЕстьNull(ОплатыПоЗаявкам.Сумма, 0)) КАК СуммаОплат
	|ИЗ
	|	ЗаявкиИспользующиеПодтверждающиеДокументы КАК ЗаявкиИспользующиеПодтверждающиеДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОплатыПоЗаявкам КАК ОплатыПоЗаявкам
	|		ПО ЗаявкиИспользующиеПодтверждающиеДокументы.Заявка = ОплатыПоЗаявкам.Заявка
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаявкиИспользующиеПодтверждающиеДокументы.Ссылка,
	|	ЗаявкиИспользующиеПодтверждающиеДокументы.ПодтверждающийДокументВид,
	|	ЗаявкиИспользующиеПодтверждающиеДокументы.ПодтверждающийДокументНомер,
	|	ЗаявкиИспользующиеПодтверждающиеДокументы.ПодтверждающийДокументДата,
	|	ЗаявкиИспользующиеПодтверждающиеДокументы.СуммаДокумента";
	Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти

#КонецОбласти
//-- НЕ УТ

#КонецОбласти
//-- Локализация