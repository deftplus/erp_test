
////////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ПРЕОБРАЗОВАНИЯ ТАБЛИЦЫ ЗНАЧЕНИЙ В МАССИВ СТРУКТУР И ОБРАТНО

Функция ПолучитьТаблицуИзМассиваСтруктур(МассивСтруктур) Экспорт
	
	ТЗ=Новый ТаблицаЗначений;
	
	Если МассивСтруктур.Количество()=0 Тогда
		Возврат ТЗ;
	КонецЕсли;
	
	// Определим колонки таблицы значений
	
	Для Каждого Колонка ИЗ МассивСтруктур[0] Цикл
			
		ТЗ.Колонки.Добавить(Колонка.Ключ,Колонка.Значение);
		
	КонецЦикла;
	
	// Заполним таблицу значений
	
	Для Индекс=1 ПО МассивСтруктур.Количество()-1 Цикл
		
		СтруктураСтрока=МассивСтруктур[Индекс];
		
		НоваяСтрока=ТЗ.Добавить();
		
		Для Каждого ЗначениеКолонки ИЗ СтруктураСтрока Цикл
			
			НоваяСтрока[ЗначениеКолонки.Ключ]=ЗначениеКолонки.Значение;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТЗ;
 
КонецФункции // ПолучитьТаблицуИзМассиваСтруктур()

Функция ПолучитьМассивСтруктурИЗТаблицы(ТаблицаЗначений) Экспорт
	
	МассивСтруктур=Новый Массив;
	
	// Первым элементом массива идет структура, содержащая имена и типы колонок ТЗ
	
	СтруктураТип=Новый Структура;
	Для Каждого Колонка ИЗ ТаблицаЗначений.Колонки Цикл
		СтруктураТип.Вставить(Колонка.Имя,Колонка.ТипЗначения);
	КонецЦикла;
	
	МассивСтруктур.Вставить(0,СтруктураТип);
		
	Для Каждого Строка ИЗ ТаблицаЗначений Цикл
		
		СтруктураСтрока=Новый Структура;
		
		Для Каждого Колонка ИЗ ТаблицаЗначений.Колонки Цикл
			
			СтруктураСтрока.Вставить(Колонка.Имя,Строка[Колонка.Имя]);
			
		КонецЦикла;
			
		МассивСтруктур.Добавить(СтруктураСтрока);
		
	КонецЦикла;
	
	Возврат МассивСтруктур
	
КонецФункции // МассивСтруктурИЗТаблицы()

////////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ПРЕОБРАЗОВАНИЯ ДЕРЕВА ЗНАЧЕНИЙ В МАССИВ СТРУКТУР И ОБРАТНО

Функция ПреобразоватьДеревоВМассивСтруктур(ДеревоЗначений) Экспорт
	
	МассивСтруктур=Новый Массив;
	
	// Первым элементом массива идет структура, содержащая имена и типы колонок ТЗ
	
	СтруктураТип=Новый Структура;
	Для Каждого Колонка ИЗ ДеревоЗначений.Колонки Цикл
		СтруктураТип.Вставить(Колонка.Имя,Колонка.ТипЗначения);
	КонецЦикла;
	
	МассивСтруктур.Вставить(0,СтруктураТип);
		
	Для Каждого Строка ИЗ ДеревоЗначений.Строки Цикл
		
		СтруктураСтрока=Новый Структура;
		
		Для Каждого Колонка ИЗ ДеревоЗначений.Колонки Цикл
			
			СтруктураСтрока.Вставить(Колонка.Имя,Строка[Колонка.Имя]);
			
		КонецЦикла;
		
		СтруктураСтрока.Вставить("Строки",Новый Массив);
		
		МассивСтруктур.Добавить(СтруктураСтрока);
			
		ДобавитьПодчиненныеСтрокиДерева(Строка.Строки,СтруктураСтрока.Строки,ДеревоЗначений);
		
	КонецЦикла;
	
	Возврат МассивСтруктур;
			
КонецФункции // ПреобразоватьДеревоВМассивСтруктур() 

Процедура ДобавитьПодчиненныеСтрокиДерева(КоллекцияСтрок,МассивСтрок,ДеревоЗначений)
	
	Для Каждого Строка ИЗ КоллекцияСтрок Цикл
		
		СтруктураСтрока=Новый Структура;
		
		Для Каждого Колонка ИЗ ДеревоЗначений.Колонки Цикл
			
			СтруктураСтрока.Вставить(Колонка.Имя,Строка[Колонка.Имя]);
			
		КонецЦикла;
		
		СтруктураСтрока.Вставить("Строки",Новый Массив);
		
		МассивСтрок.Добавить(СтруктураСтрока);
			
		ДобавитьПодчиненныеСтрокиДерева(Строка.Строки,СтруктураСтрока.Строки,ДеревоЗначений);
		
	КонецЦикла;	
	
КонецПроцедуры // ДобавитьПодчиненныеСтрокиДерева()

Функция ПреобразоватьМассивСтруктурВДерево(МассивСтруктур) Экспорт
	
	Дерево=Новый ДеревоЗначений;
	
	Если МассивСтруктур.Количество()=0 Тогда
		Возврат Дерево;
	КонецЕсли;
	
	// Определим колонки таблицы значений
	
	Для Каждого Колонка ИЗ МассивСтруктур[0] Цикл
		
		Если Колонка.Ключ="Строки" Тогда
			Продолжить;
		КонецЕсли;
			
		Дерево.Колонки.Добавить(Колонка.Ключ,Колонка.Значение);
		
	КонецЦикла;
	
	Для Индекс=1 ПО МассивСтруктур.Количество()-1 Цикл
		
		СтрокаКДобавлению=МассивСтруктур[Индекс];
		
		НоваяСтрока=Дерево.Строки.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаКДобавлению);
		
		Если СтрокаКДобавлению.Строки.Количество()>0 Тогда
			
			ДобавитьПодчиненныеСтроки(СтрокаКДобавлению,НоваяСтрока);
			
		КонецЕсли;
		
	КонецЦикла;	
	
	Возврат Дерево;
	
КонецФункции // ПреобразоватьМассивСтруктурВДерево() 

Процедура ДобавитьПодчиненныеСтроки(Строка,ЭлементКоллекции)
	
	Для Каждого СтрокаКДобавлению ИЗ Строка.Строки Цикл
		
		НоваяСтрока=ЭлементКоллекции.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаКДобавлению);
		
		Если СтрокаКДобавлению.Строки.Количество()>0 Тогда
			
			ДобавитьПодчиненныеСтроки(СтрокаКДобавлению,НоваяСтрока);
			
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры // ДобавитьПодчиненныеСтроки

/////////////////////////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ПРЕОБРАЗОВАНИЯ СПИСКА ТАБЛИЦ ЗНАЧЕНИЙ В СПИСОК МАССИВ СТРУКТУР И ОБРАТНО

Функция ПреобразоватьСписокТаблицДляКлиента(ИсходныйСписокЗначений) Экспорт
	
	СписокЗначенийКлиент=Новый СписокЗначений;
	
	Для Каждого Элемент ИЗ ИсходныйСписокЗначений Цикл
		
		СписокЗначенийКлиент.Добавить(ПолучитьМассивСтруктурИЗТаблицы(Элемент.Значение));
		
	КонецЦикла;
	
	Возврат СписокЗначенийКлиент;
			
КонецФункции // ПреобразоватьСписокТаблицДляКлиента()

Функция ПреобразоватьСписокМассивовДляСервера(ИсходныйСписокЗначений) Экспорт
	
	СписокЗначенийСервер=Новый СписокЗначений;
	
	Для Каждого Элемент ИЗ ИсходныйСписокЗначений Цикл
		
		СписокЗначенийСервер.Добавить(ПолучитьТаблицуИзМассиваСтруктур(Элемент.Значение));
		
	КонецЦикла;
	
	Возврат СписокЗначенийСервер;
			
КонецФункции // ПреобразоватьСписокТаблицДляКлиента()





	