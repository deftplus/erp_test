#Область ПрограммныйИнтерфейс

Функция НуженГрафикИсполнения(ВидДоговораУХ, ПорядокРасчетов, СпособФормированияПлатежей) Экспорт
	
	ТипыДоговоров = Новый Массив;
	ТипыДоговоров.Добавить(Справочники.ВидыДоговоровКонтрагентовУХ.СПокупателем);
	ТипыДоговоров.Добавить(Справочники.ВидыДоговоровКонтрагентовУХ.СДавальцем);
	ТипыДоговоров.Добавить(Справочники.ВидыДоговоровКонтрагентовУХ.СПоставщиком);
	ТипыДоговоров.Добавить(Справочники.ВидыДоговоровКонтрагентовУХ.СПереработчиком);
	ТипыДоговоров.Добавить(Справочники.ВидыДоговоровКонтрагентовУХ.Импорт);
	ТипыДоговоров.Добавить(Справочники.ВидыДоговоровКонтрагентовУХ.ВвозИзЕАЭС);
	
	ПодходящийВидДоговора = (ТипыДоговоров.Найти(ВидДоговораУХ) <> Неопределено);
	ПодходящийПорядокРасчетов = (ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамКонтрагентов);
	ПодходящийСпособФормирования = (СпособФормированияПлатежей = Перечисления.СпособыФормированияПлатежейПоДоговору.ПоГрафикуПлатежей);
	
	Возврат ПодходящийВидДоговора И ПодходящийПорядокРасчетов И ПодходящийСпособФормирования;
	
КонецФункции

Функция ПараметрыДоговораАренды(ДоговорАренды, Дата = Неопределено) Экспорт
	
	Результат = Новый Структура("СтатьяДвиженияДенежныхСредств, ВалютаВзаиморасчетов, СтавкаНДС");
	Если НЕ ЗначениеЗаполнено(ДоговорАренды) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЕСТЬNULL(УсловияДоговоровАрендыСрезПоследних.СтавкаНДС, ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)) КАК СтавкаНДС,
	|	ДоговорыАренды.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ДоговорыАренды.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов
	|ИЗ
	|	Справочник.ДоговорыАренды КАК ДоговорыАренды
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УсловияДоговоровАренды.СрезПоследних(&Дата, Договор = &Договор) КАК УсловияДоговоровАрендыСрезПоследних
	|		ПО ДоговорыАренды.Ссылка = УсловияДоговоровАрендыСрезПоследних.Договор
	|ГДЕ
	|	ДоговорыАренды.Ссылка = &Договор"
	);
	Запрос.УстановитьПараметр("Договор", ДоговорАренды);
	Запрос.УстановитьПараметр("Дата", Дата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти
