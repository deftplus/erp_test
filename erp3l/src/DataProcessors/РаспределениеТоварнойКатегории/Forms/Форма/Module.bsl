#Область ОбработкаОсновныхСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьФорму(Параметры);	
	УстановитьУсловноеОформлениеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если НЕ ЗначениеЗаполнено(ТоварнаяКатегория) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Распределение доступно только для товарной категории'"));
		Закрыть();
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаСобытийЭлементовФормы

&НаСервере
Процедура УстановитьУсловноеОформлениеФормы()
	
	НовыйПараметр = Новый ПараметрВыбора("Отбор.ТоварнаяКатегория", ТоварнаяКатегория);
	НовыйМассив = Новый Массив();
	НовыйМассив.Добавить(НовыйПараметр);
	НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);	
	Элементы.ТоварыНоменклатура.ПараметрыВыбора = НовыеПараметры;
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ВыборНоменклатуры,"ТоварнаяКатегория",ТоварнаяКатегория,ВидСравненияКомпоновкиДанных.Равно,,Истина);
	ЭтаФорма.Заголовок = "Уточнение товарной категории:"+ ТоварнаяКатегория;
	
	НоменклатураСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(
		ЭтаФорма,
		"ТоварыХарактеристика",
		"Объект.Товары.ХарактеристикиИсходныеИспользуются");
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьФорму(Параметры)
	
	Если Параметры.Свойство("ТЧЗаказыПоставщикам") Тогда
		
		ТЧЗаказыПоставщикам = ПолучитьИзВременногоХранилища(Параметры.ТЧЗаказыПоставщикам);
		ЦенаВключаетНДС = Параметры.ЦенаВключаетНДС;
		ТоварнаяКатегория = ТЧЗаказыПоставщикам[0].НоменклатураИсходная;
		Сумма = ТЧЗаказыПоставщикам[0].СуммаСНДС;
		СуммаТоварнойКатегории = ТЧЗаказыПоставщикам[0].СуммаСНДС;
		СтавкаНДСПоУмолчанию = ТЧЗаказыПоставщикам[0].СтавкаНДС;
		Элементы.ПериодыПотребности.Видимость = Ложь;
		
	ИначеЕсли Параметры.Свойство("ОперативныйПлан") Тогда
		ЦенаВключаетНДС = Истина;
		ОперативныйПлан = ПолучитьИзВременногоХранилища(Параметры.ОперативныйПлан);
		ТоварнаяКатегория = ОперативныйПлан[0].ТоварнаяКатегория;
		Сумма = ОперативныйПлан[0].Сумма;
		СуммаТоварнойКатегории = ОперативныйПлан[0].Сумма;
		Элементы.СуммаТоварнойКатегории.Видимость = Ложь;
		Элементы.ТоварыСтавкаНДС.Видимость = Ложь;
		Элементы.ЦенаВключаетНДС.Видимость = Ложь;
		Элементы.ТоварыСумма.Видимость = Ложь;
		Элементы.ТоварыЕдиницаИзмерения.Видимость = Ложь;
		Периоды = ПолучитьИзВременногоХранилища(Параметры.Периоды);
		Для Каждого Период Из Периоды Цикл
			Доб = ПериодыПотребности.Добавить();
			Доб.Период = Период.Заголовок;
			Доб.ИмяКолонки = Период.ИмяКолонки;
			Доб.Сумма = ОперативныйПлан[0]["Сумма_"+Период.ИмяКолонки];
		КонецЦикла;	
		
	КонецЕсли;

Конецпроцедуры	

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	ТекДанные = Элементы.Номенклатура.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	РасчитатьСуммыСтрокиПоЦене(ТекДанные, ЦенаВключаетНДС);
	ОбновитьСумму();
КонецПроцедуры	

&НаСервере
Процедура ОбновитьСумму()
	
	Сумма = СуммаТоварнойКатегории - Объект.Товары.Выгрузить().Итог("СуммаСНДС");
	
	Если Сумма < 0 Тогда
		Элементы.Остаток.ЦветТекста = WebЦвета.Красный;
	Иначе
		Элементы.Остаток.ЦветТекста = WebЦвета.ЗеленыйЛес;
	КонецЕсли;	

КонецПроцедуры	

&НаКлиентеНаСервереБезКонтекста
Процедура РасчитатьСуммыСтрокиПоЦене(ТекДанные, ЦенаВключаетНДС)
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ТекДанные.Номенклатура) Тогда
		ТекДанные.Сумма = 0;
		ТекДанные.СуммаНДС = 0;
		ТекДанные.СуммаСНДС = 0;
		Возврат;
	КонеЦЕсли;
	Если ЦенаВключаетНДС Тогда
		ТекДанные.СуммаСНДС = ТекДанные.Количество * ТекДанные.Цена;
	Иначе
		ТекДанные.Сумма = ТекДанные.Количество * ТекДанные.Цена;
	КонецЕсли;
	ТекДанные.СуммаНДС = РассчитатьСуммуНДС(ТекДанные, ЦенаВключаетНДС);
	Если ЦенаВключаетНДС Тогда
		ТекДанные.Сумма = ТекДанные.СуммаСНДС - ТекДанные.СуммаНДС;
	Иначе
		ТекДанные.СуммаСНДС = ТекДанные.Сумма + ТекДанные.СуммаНДС;
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РассчитатьСуммуНДС(ТекДанные, ОтСуммыСНДС)
	Если ТекДанные = Неопределено Тогда
		Возврат 0;
	КонецЕсли;
	
	Если ОтСуммыСНДС Тогда
		Сумма_ = ТекДанные.СуммаСНДС;
	Иначе
		Сумма_ = ТекДанные.Сумма;
	КонецЕсли;
	Ставка = ОбщегоНазначенияУХ.ПолучитьЗначениеРеквизита(ВстраиваниеУХВызовСервера.ПолучитьСтавкуНДС(ТекДанные.СтавкаНДС),"Ставка");
	Если Ставка = Неопределено Тогда
		Ставка = 0;
	КонецЕсли;	
	Возврат Окр(УчетНДСКлиентСервер.РассчитатьСуммуНДС(
				Сумма_,
				ОтСуммыСНДС,Ставка), 2);
КонецФункции

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекДанные = Элементы.Номенклатура.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РасчитатьСуммыСтрокиПоЦене(ТекДанные, ЦенаВключаетНДС);
	ОбновитьСумму();

КонецПроцедуры

&НаКлиенте
Процедура ТоварыСтавкаНДСПриИзменении(Элемент)
	
	ТекДанные = Элементы.Номенклатура.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РасчитатьСуммыСтрокиПоЦене(ТекДанные, ЦенаВключаетНДС);
	ОбновитьСумму();
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	ОбновитьСумму();
	ТекущиеДанные = Элементы.ПериодыПотребности.ТекущиеДанные;
	Если Элементы.ПериодыПотребности.Видимость = Истина И ТекущиеДанные <> Неопределено Тогда
		Отбор=Новый Структура();
		Отбор.Вставить("ПериодПотребности",ТекущиеДанные.ИмяКолонки);
		СуммаПоПериодуПотребности = СуммаПоПериоду(Отбор);
		ТекущиеДанные.Разнесено = СуммаПоПериодуПотребности;
		Элементы.ПериодыПотребности.Обновить();
		УстановитьСуммуПодвала();   
 	КонецЕсли;
	Если Элементы.ПериодыПотребности.Видимость = Истина Тогда
		Элементы.НоменклатураСуммаСНДС.ТекстПодвала = СуммаПоПериодуПотребности;
	Иначе 
		Элементы.НоменклатураСуммаСНДС.ТекстПодвала = СуммаПоПериоду();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СуммаПоПериоду(Отбор = Неопределено)
	Если Отбор = Неопределено Тогда
		Возврат Объект.Товары.Выгрузить().Итог("СуммаСНДС");	
	Иначе
		Возврат Объект.Товары.Выгрузить(Отбор).Итог("СуммаСНДС");
	КонецЕсли;
КонецФункции

&НаСервере
Процедура УстановитьСуммуПодвала()
	
	Элементы.ПериодыПотребностиРазнесено.ТекстПодвала = ПериодыПотребности.Итог("Разнесено");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЕдиницаИзмеренияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекДанные = ЭтаФорма.Элементы.Номенклатура.ТекущиеДанные;
	ЦентрализованныеЗакупкиКлиентУХ.ОбработчикЕдиницаИзмеренияНачалоВыбора(
		ТекДанные, ДанныеВыбора);
		
КонецПроцедуры

&НаСервере
Функция АдресХранилища()
	
	Возврат ПоместитьВоВременноеХранилище(Объект.Товары.Выгрузить(), УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Процедура Записать(Команда)
	
	Если Сумма < 0 Тогда
		ТекстСообщения = НСтр("ru = 'Записать распределение невозможно, номенклатуры распределено на сумму больше товарной категории!'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,Истина);
	Иначе
		АдресРезультата = АдресХранилища();
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("АдресРезультата", АдресРезультата);
		Оповестить("ИзмениласьТаблицаНоменклатуры", ПараметрыОповещения);
		Закрыть();
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеЕдиницыИзмеренияНаСервере()
	
	ТекСтрока = Элементы.Номенклатура.ТекущаяСтрока;
	ТекДанные = Объект.Товары.НайтиПоИдентификатору(ТекСтрока);
	
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТекДанные.Номенклатура) Тогда
		Возврат;
	КонеЦЕсли;
	
	Если ЗначениеЗаполнено(ТекДанные.ЕдиницаИзмерения) Тогда
		Коэффициент = ЦентрализованныеЗакупкиУХ.ПолучитьКоэффициентЕдиницыИзмерения(ТекДанные.Номенклатура, ТекДанные.ЕдиницаИзмерения);
		Если Коэффициент = 0 Тогда
			Коэффициент = 1;
		КонецЕсли;
	Иначе
		Коэффициент = 1;
	КонецЕсли;
	
	КоэффПересчета = ТекДанные.Коэффициент / Коэффициент;
	
	ТекДанные.Коэффициент = Коэффициент;
	ТекДанные.Количество = Окр(ТекДанные.Количество * КоэффПересчета, 2);
	ТекДанные.Цена = Окр(ТекДанные.Цена / ?(КоэффПересчета=0, 1, КоэффПересчета), 2);
	
	РасчитатьСуммыСтрокиПоЦене(ТекДанные, ЦенаВключаетНДС);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЕдиницаИзмеренияПриИзменении(Элемент)
	ТекДанные = Элементы.Номенклатура.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ОбработатьИзменениеЕдиницыИзмеренияНаСервере();
	ОбновитьСумму();	
КонецПроцедуры

&НаКлиенте
Процедура ВыборНоменклатурыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если  Элементы.ПериодыПотребности.ТекущиеДанные <> Неопределено 
		И Элементы.ПериодыПотребности.ТекущиеДанные.Сумма = 0 Тогда
		Возврат;
	КонецЕсли;	
	СтандартнаяОбработка = Ложь;
	НоваяСтрока = Объект.Товары.Добавить();
	НоваяСтрока.Номенклатура = ВыбраннаяСтрока;
	НоваяСтрока.СтавкаНДС = СтавкаНДСПоУмолчанию;
	НоваяСтрока.ХарактеристикиИсходныеИспользуются = ОпределитьХарактеристикиИспользуютсяНаСервере(ВыбраннаяСтрока);		
	НоваяСтрока.Коэффициент = 1;
	Если  Элементы.ПериодыПотребности.ТекущиеДанные <> Неопределено 
		И Элементы.ПериодыПотребности.ТекущиеДанные.Сумма > 0 Тогда
		НоваяСтрока.ПериодПотребности = Элементы.ПериодыПотребности.ТекущиеДанные.ИмяКолонки;
	КонецЕсли;	
	
КонецПроцедуры


&НаСервереБезКонтекста
Функция ОпределитьХарактеристикиИспользуютсяНаСервере(ВыбраннаяСтрока)
	Возврат
		УправлениеЗакупкамиВстраиваниеПереопределяемыйУХ.ХарактеристикиИспользуются(
			ВыбраннаяСтрока);
КонецФункции

&НаКлиенте
Процедура НоменклатураПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И Не Копирование Тогда	
		Элемент.ТекущиеДанные.СтавкаНДС = СтавкаНДСПоУмолчанию;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодыПотребностиПриАктивизацииСтроки(Элемент)
	ТекущиеДанные = Элементы.ПериодыПотребности.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Отбор=Новый Структура();
		Отбор.Вставить("ПериодПотребности",ТекущиеДанные.ИмяКолонки);
		Элементы.Номенклатура.ОтборСтрок = Новый ФиксированнаяСтруктура(Отбор);
		Элементы.НоменклатураСуммаСНДС.ТекстПодвала = СуммаПоПериоду(Отбор);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

