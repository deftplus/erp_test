
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ОбъектОбработки) Тогда
		
		Если ТипЗнч(Параметры.ОбъектОбработки)=Тип("СправочникСсылка.ДокументыБД") Тогда
			
			ОбновитьСписокДокументовБД();
			Объект.ДокументБД=Параметры.ОбъектОбработки;
			СформироватьСписокШаблоновДокумента();
			
		Иначе
			
			Объект.ШаблонКорректировки=Параметры.ОбъектОбработки;
			Элементы.ШаблонКорректировки.СписокВыбора.Добавить(Объект.ШаблонКорректировки);
			
			ОбновитьСписокДокументовБД();
			Объект.ДокументБД=Объект.ШаблонКорректировки.ДокументБД;
			
		КонецЕсли;
		
	Иначе
		
		ОбновитьСписокДокументовБД();
		
	КонецЕсли;	
			
	УстановитьВидимость();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость()
	
	Элементы.СоздаватьНовые.Доступность	=(ЗначениеЗаполнено(Объект.ШаблонКорректировки) ИЛИ ЗначениеЗаполнено(Объект.ДокументБД));
	
	Если НЕ Элементы.СоздаватьНовые.Доступность Тогда
		
		Объект.СоздаватьНовые=Ложь;
		
	КонецЕсли;
	
КонецПроцедуры // УстановитьВидимость()

&НаСервере
Процедура ОбновитьСписокДокументовБД()
	
	Элементы.ДокументБД.СписокВыбора.Очистить();
	Элементы.ДокументБД.ПодсказкаВвода="";
	
	Запрос=Новый Запрос;
	
	Если ЗначениеЗаполнено(Объект.ШаблонКорректировки) Тогда	
		
		Запрос.Текст="ВЫБРАТЬ
		|	ШаблоныТрансформационныхКорректировок.ДокументБД КАК ДокументБД,
		|	ШаблоныТрансформационныхКорректировок.ДокументБД.Наименование КАК НаименованиеДокументаБД
		|ИЗ
		|	Справочник.ШаблоныТрансформационныхКорректировок КАК ШаблоныТрансформационныхКорректировок
		|ГДЕ
		|	ШаблоныТрансформационныхКорректировок.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка",Объект.ШаблонКорректировки);
		
	Иначе
		
		Запрос.Текст="ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ШаблоныТрансформационныхКорректировок.ДокументБД КАК ДокументБД,
		|	ШаблоныТрансформационныхКорректировок.ДокументБД.Наименование КАК НаименованиеДокументаБД
		|ИЗ
		|	Справочник.ШаблоныТрансформационныхКорректировок КАК ШаблоныТрансформационныхКорректировок
		|ГДЕ
		|	НЕ ШаблоныТрансформационныхКорректировок.Отключен
		|	И НЕ ШаблоныТрансформационныхКорректировок.ПометкаУдаления
		|	И НЕ ШаблоныТрансформационныхКорректировок.ДокументБД = ЗНАЧЕНИЕ(Справочник.ДокументыБД.ПустаяСсылка)"
	
	КонецЕсли;
	
	ТабДокументы=Запрос.Выполнить().Выгрузить();
	
	Элементы.ДокументБД.СписокВыбора.ЗагрузитьЗначения(ТабДокументы.ВыгрузитьКолонку("ДокументБД"));	
	ТекстДокументы=СтрСоединить(ТабДокументы.ВыгрузитьКолонку("НаименованиеДокументаБД"),",");
	
	Если Элементы.ДокументБД.СписокВыбора.Количество()=0 Тогда
		
		Элементы.ДокументБД.ПодсказкаВвода=НСтр("ru = 'Связанные документы не указаны'");
		
	Иначе
		
		Элементы.ДокументБД.ПодсказкаВвода= ТекстДокументы;
		
	КонецЕсли;
			
КонецПроцедуры // ОбновитьСписокДокументовБД()

&НаСервере
Процедура СформироватьСписокШаблоновДокумента()
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	ШаблоныТрансформационныхКорректировок.Ссылка КАК Ссылка,
	|	ШаблоныТрансформационныхКорректировок.Наименование КАК Наименование
	|ИЗ
	|	Справочник.ШаблоныТрансформационныхКорректировок КАК ШаблоныТрансформационныхКорректировок
	|ГДЕ
	|	ШаблоныТрансформационныхКорректировок.ДокументБД = &ДокументБД
	|	И НЕ ШаблоныТрансформационныхКорректировок.Отключен
	|	И НЕ ШаблоныТрансформационныхКорректировок.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ДокументБД",Объект.ДокументБД);
	
	Элементы.ШаблонКорректировки.СписокВыбора.Очистить();
	
	ТабШаблоны=Запрос.Выполнить().Выгрузить();
	
	Элементы.ШаблонКорректировки.СписокВыбора.ЗагрузитьЗначения(ТабШаблоны.ВыгрузитьКолонку("Ссылка"));	
	ТекстШаблоны=СтрСоединить(ТабШаблоны.ВыгрузитьКолонку("Наименование"),",");
	
	Если Элементы.ШаблонКорректировки.СписокВыбора.Количество()=0 Тогда
		
		Элементы.ШаблонКорректировки.ПодсказкаВвода=НСтр("ru = 'Шаблоны операций не найдены'");
		
	Иначе
		
		Элементы.ШаблонКорректировки.ПодсказкаВвода= ТекстШаблоны;
		
	КонецЕсли;
	
	Если Элементы.ШаблонКорректировки.СписокВыбора.Количество()=1 Тогда
		
		Объект.ШаблонКорректировки=Элементы.ШаблонКорректировки.СписокВыбора[0].Значение;
		
	КонецЕсли;
	
КонецПроцедуры //  
 

&НаСервере
Процедура ВыполнитьОбработкуНаСервере()
	
	ОбработкаОбъект=РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ОбработатьКорректировки();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбработку(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.ДокументБД) Тогда
		
		ПоказатьПредупреждение(,НСтр("ru = 'Не указан вид документа для обработки.'"));
		Возврат;
		
	КонецЕсли;
	
	ВыполнитьОбработкуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ШаблонКорректировкиПриИзменении(Элемент)
	
	ПриИзмененииШаблонаКорректировки();
		
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииШаблонаКорректировки()
	
	
	
	Объект.ДокументБД=Объект.ШаблонКорректировки.ДокументБД;	
	УстановитьВидимость();
		
КонецПроцедуры // ПриИзмененииШаблонаКорректировки() 

&НаКлиенте
Процедура ДокументБДПриИзменении(Элемент)
	
	СформироватьСписокШаблоновДокумента();
	УстановитьВидимость();
	
КонецПроцедуры
