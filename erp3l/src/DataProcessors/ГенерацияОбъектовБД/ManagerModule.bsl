Функция ПолучитьПараметрыДляЗаполненияОбъектовБДПоИсходному(ИсходныйОбъектБД,Организация=Неопределено,Сценарий=Неопределено,ТипБДПриемник=Неопределено,ИмяОбъектаБД=Неопределено) Экспорт
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	НастройкиЗаполненияОбъектовБД.ПравилоЗаполнения,
	|	НастройкиЗаполненияОбъектовБД.ИспользуемаяИБ,
	|	НастройкиЗаполненияОбъектовБД.ОбъектБД,
	|	НастройкиЗаполненияОбъектовБД.ВыполнятьАвтоматически,
	|	НастройкиЗаполненияОбъектовБД.ПроводитьДокументы,
	|	НастройкиЗаполненияОбъектовБД.ОбластьГенерации,
	|	НастройкиЗаполненияОбъектовБД.ИсходныйОбъектБД.Наименование КАК НаименованиеДокументаБД
	|ИЗ
	|	РегистрСведений.НастройкиЗаполненияОбъектовБД КАК НастройкиЗаполненияОбъектовБД
	|ГДЕ
	|	НастройкиЗаполненияОбъектовБД.ИсходныйОбъектБД = &ИсходныйОбъектБД";
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		Запрос.Текст=Запрос.Текст+"	
		|	И (НастройкиЗаполненияОбъектовБД.Организация = &Организация ИЛИ НастройкиЗаполненияОбъектовБД.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))";
		Запрос.УстановитьПараметр("Организация",Организация);
					
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(Сценарий) Тогда
		
		Запрос.Текст=Запрос.Текст+"	
		|	И ( НастройкиЗаполненияОбъектовБД.Сценарий = &Сценарий ИЛИ НастройкиЗаполненияОбъектовБД.Сценарий = ЗНАЧЕНИЕ(Справочник.Сценарии.ПустаяСсылка))";
		Запрос.УстановитьПараметр("Сценарий",Сценарий);
						
	КонецЕсли;
		
	Запрос.УстановитьПараметр("ИсходныйОбъектБД",ИсходныйОбъектБД);
	
	Если ЗначениеЗаполнено(ТипБДПриемник) Тогда
		
		Запрос.Текст=Запрос.Текст+"
		|И НастройкиЗаполненияОбъектовБД.ОбъектБД.Владелец=&ТипБДПриемник";
		
		Запрос.УстановитьПараметр("ТипБДПриемник",ТипБДПриемник);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИмяОбъектаБД) Тогда
		
		Запрос.Текст=Запрос.Текст+"
		|И НастройкиЗаполненияОбъектовБД.ОбъектБД=&ОбъектБД";
		
		Запрос.УстановитьПараметр("ОбъектБД",Справочники.ДокументыБД.НайтиПоНаименованию(ИмяОбъектаБД,Истина,,ТипБДПриемник));
		
	КонецЕсли;
		
	Результат=Запрос.Выполнить().Выбрать();
	
	МассивОбъектов=Новый Массив;
	
	Пока Результат.Следующий() Цикл 
		
		МассивОбъектов.Добавить(Новый Структура("ОбъектБД,ПравилоЗаполнения,ИспользуемаяИБ,ВыполнятьАвтоматически,ПроводитьДокументы,ОбластьГенерации,НаименованиеДокументаБД",
		                        Результат.ОбъектБД,
								Результат.ПравилоЗаполнения,
								Результат.ИспользуемаяИБ,
								Результат.ВыполнятьАвтоматически,
								Результат.ПроводитьДокументы,
								Результат.ОбластьГенерации,
								Результат.НаименованиеДокументаБД));
			
	КонецЦикла;
							
	Возврат МассивОбъектов;						
		
КонецФункции // ПолучитьПараметрыДляЗаполненияОбъектовБДПоИсходному()