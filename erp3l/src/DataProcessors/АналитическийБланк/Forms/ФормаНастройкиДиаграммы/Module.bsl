
&НаКлиенте
Процедура СвязаннаяОбластьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ФормаВыбораОбластиЗавершение", ЭтаФорма);
	
	ПараметрыФормы = Новый Структура;
    ПараметрыФормы.Вставить("ТекущийМакетАдрес",ТекущийМакетАдрес);
	ПараметрыФормы.Вставить("СвязаннаяОбластьБланк",СвязаннаяОбластьБланк);
    ПараметрыФормы.Вставить("ТекущийБланк",ТекущийБланк);
	
	ПараметрыФормы.Вставить("СвязаннаяОбластьПредставление",СвязаннаяОбластьПредставление);
	
	ОткрытьФорму("Обработка.АналитическийБланк.Форма.ФормаВыбораОбластиДиаграммы",ПараметрыФормы 
	,,,,,Оповещение,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура ФормаВыбораОбластиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено  Или Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;	
	
	СвязаннаяОбластьИмя           = Результат.ИмяОбласти;
	СвязаннаяОбластьБланк         = Результат.СвязаннаяОбластьБланк;
	ТекущийМакетАдрес             = Результат.ТекущийМакетАдрес;
	
	ПолучитьПредставлениеСвязаннойОбластиПоИмени();
	
КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("СвязаннаяОбластьПредставление",СвязаннаяОбластьПредставление);
	СтруктураВозврата.Вставить("СкрытьОбластьДанныхДиаграммы",СкрытьОбластьДанныхДиаграммы);
    СтруктураВозврата.Вставить("ИмяДиаграммы",ИмяДиаграммы);
    СтруктураВозврата.Вставить("СерииВСтроках",СерииВСтроках);
    СтруктураВозврата.Вставить("СвязаннаяОбластьБланк",СвязаннаяОбластьБланк);

	Закрыть(СтруктураВозврата)
		
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийМакетАдрес 					= Параметры.ТекущийМакетАдрес;
	СвязаннаяОбластьИмя     			= Параметры.СвязаннаяОбластьИмя;
	ИмяДиаграммы            			= Параметры.ИмяДиаграммы;
	СкрытьОбластьДанныхДиаграммы 		= Параметры.СкрытьОбластьДанныхДиаграммы;
	СерииВСтроках                       = Параметры.СерииВСтроках;
	СвязаннаяОбластьБланк               = Параметры.СвязаннаяОбластьБланк;
	ТекущийБланк                        = Параметры.ТекущийБланк;
	
	ПолучитьПредставлениеСвязаннойОбластиПоИмени();

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТочкиСерииПоВыделеннойОбласти()
	
	СерииДиаграммы.Очистить();
	ТочкиДиаграммы.Очистить();
	
	//Ожидаем, что самые левые ячейки(строки) содеражт заголовки серий, верхнии (колонки) - точек.
	//Если заголовков не обнаружено, присваем технические по порядковым номерам.
	ВыделеннаяОбласть = ПолеТабличногоДокументаМакет.Область(СвязаннаяОбластьПредставление);
	
	//Смортим левую верхнюю область- если там нет значений,
	//то считаем, что первая строка и первая колонка - это область заголовков, которую не нужно включать в диаграмму.
    ПерваяЯчейка = ПолеТабличногоДокументаМакет.Область(ВыделеннаяОбласть.Верх,ВыделеннаяОбласть.Лево,ВыделеннаяОбласть.Верх,ВыделеннаяОбласть.Лево);
	Если НЕ ПерваяЯчейка.СодержитЗначение 
		И ПерваяЯчейка.Расшифровка = Неопределено Тогда
		НачальнаяСтрокаСерий = ВыделеннаяОбласть.Верх+1;
		НачальнаяСтрокаТочек = ВыделеннаяОбласть.Лево+1;
	Иначе		
		НачальнаяСтрокаСерий = ВыделеннаяОбласть.Верх;
		НачальнаяСтрокаТочек = ВыделеннаяОбласть.Лево;	
	КонецЕсли;	 
	
	Если СерииВСтроках Тогда
		ТаблицаСерий = СерииДиаграммы;
	    ТаблицаТочек = ТочкиДиаграммы;
	Иначе	
		ТаблицаСерий = ТочкиДиаграммы;
	    ТаблицаТочек = СерииДиаграммы;
	КонецЕсли;	
		
	ИндексСтроки = 1;
	Для ИндСтроки=НачальнаяСтрокаСерий По ВыделеннаяОбласть.Низ Цикл
		ОбластьЗаголовка = ПолеТабличногоДокументаМакет.Область(ИндСтроки,ВыделеннаяОбласть.Лево,ИндСтроки,ВыделеннаяОбласть.Лево);
		Если НЕ ОбластьЗаголовка.СодержитЗначение
			И ОбластьЗаголовка.Расшифровка = Неопределено
			И ЗначениеЗаполнено(ОбластьЗаголовка.Текст) Тогда
			нСерия = ТаблицаСерий.Добавить();
			нСерия.Наименование = ОбластьЗаголовка.Текст;
		Иначе	
			нСерия 				= ТаблицаСерий.Добавить();
			нСерия.Наименование = "Серия "+ИндексСтроки;
		КонецЕсли;	
		нСерия.СмещениеОбласти = ИндексСтроки;
		ИндексСтроки = ИндексСтроки+1;
	КонецЦикла;	
	
	ИндексСтроки = 1;
	Для ИндКолонки=НачальнаяСтрокаТочек По ВыделеннаяОбласть.Право Цикл
		ОбластьЗаголовка = ПолеТабличногоДокументаМакет.Область(ВыделеннаяОбласть.Верх,ИндКолонки,ВыделеннаяОбласть.Верх,ИндКолонки);
		Если НЕ ОбластьЗаголовка.СодержитЗначение
			И ОбластьЗаголовка.Расшифровка = Неопределено
			И ЗначениеЗаполнено(ОбластьЗаголовка.Текст) Тогда
			нТочка = ТаблицаТочек.Добавить();
			нТочка.Наименование = ОбластьЗаголовка.Текст;
		Иначе	
			нТочка 				= ТаблицаТочек.Добавить();
			нТочка.Наименование = Строка(ИндексСтроки);
		КонецЕсли;	
		нТочка.СмещениеОбласти = ИндексСтроки;
		ИндексСтроки = ИндексСтроки+1;
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьПредставлениеСвязаннойОбластиПоИмени()
	
	ПолеТабличногоДокументаМакет.Очистить();
	
	ПолеТабличногоДокументаМакет.Вывести(ПолучитьИзВременногоХранилища(ТекущийМакетАдрес));
	
	Если ЗначениеЗаполнено(СвязаннаяОбластьИмя) Тогда 
		
		тОбласть = ПолеТабличногоДокументаМакет.Область(СвязаннаяОбластьИмя);
		
		СвязаннаяОбластьПредставление = 
		"R"+Формат(тОбласть.Верх,"ЧГ=0")
		+"C"+Формат(тОбласть.Лево,"ЧГ=0")
		+?(тОбласть.Верх = тОбласть.Низ  И тОбласть.Лево = тОбласть.Право
		,""
		,":R"+Формат(тОбласть.Низ,"ЧГ=0")
		+"C"+Формат(тОбласть.Право,"ЧГ=0")
		);
		
		СвязаннаяОбластьПолныйПуть = Строка(СвязаннаяОбластьБланк)+"!"+СвязаннаяОбластьПредставление;
		
		ЗаполнитьТочкиСерииПоВыделеннойОбласти();	
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура Транспонировать(Команда)

	СерииВСтроках = Не СерииВСтроках;
	ПолучитьПредставлениеСвязаннойОбластиПоИмени();
	
КонецПроцедуры





