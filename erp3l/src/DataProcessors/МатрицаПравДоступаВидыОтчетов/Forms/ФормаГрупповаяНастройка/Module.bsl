
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Организация",Организация);
	Параметры.Свойство("ВидОтчета",ВидОтчета);
	Параметры.Свойство("БланкОтчета",БланкОтчета);
	Параметры.Свойство("Пользователь",Пользователь);
	
	Параметры.Свойство("ОтборВидыОтчетов",	ОтборВидыОтчетов);
	Параметры.Свойство("ОтборОрганизации",	ОтборОрганизации);
	Параметры.Свойство("ОтборПользователи",	ОтборПользователи);
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		
		Организация=ОтборОрганизации;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВидОтчета) Тогда
		
		ВидОтчета=ОтборВидыОтчетов;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Пользователь) Тогда
		
		Пользователь=ОтборПользователи;
		
	КонецЕсли;
		
	
	Параметры.Свойство("ПоГруппамВидовОтчетов",	ПоГруппамВидовОтчетов);
	Параметры.Свойство("ПоГруппамОрганизаций",	ПоГруппамОрганизаций);
	Параметры.Свойство("ПоГруппамПользователей",ПоГруппамПользователей);
	
	Параметры.Свойство("ОтображатьБланкиОтчетов",ОтображатьБланкиОтчетов);

	Если ЗначениеЗаполнено(Организация) Тогда
		
		Элементы.ТаблицаНастроекПравОрганизация.Видимость=Ложь;
		Элементы.Организация.Видимость=Истина;
		
		Если ПоГруппамОрганизаций Тогда
			
			Элементы.Организация.Заголовок=НСтр("ru = 'Группа доступа (организации)'");
			
		Иначе
			
			Элементы.Организация.Заголовок=НСтр("ru = 'Организация'");
			
		КонецЕсли;
		
	Иначе
		
		Элементы.ТаблицаНастроекПравОрганизация.Видимость=Истина;
		Элементы.Организация.Видимость=Ложь;
		
		Если ПоГруппамОрганизаций Тогда
			
			Элементы.ТаблицаНастроекПравОрганизация.Заголовок=НСтр("ru = 'Группа доступа (организации)'");
			Элементы.ТаблицаНастроекПравОрганизация.ОграничениеТипа=Новый ОписаниеТипов("СправочникСсылка.ГруппыДоступаОрганизацииВидыОтчетов");
			
			
		Иначе
			
			Элементы.ТаблицаНастроекПравОрганизация.Заголовок=НСтр("ru = 'Организация'");
			Элементы.ТаблицаНастроекПравОрганизация.ОграничениеТипа=Новый ОписаниеТипов("СправочникСсылка.Организации");
				
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Пользователь) Тогда
		
		Элементы.ТаблицаНастроекПравПользователь.Видимость=Ложь;
		Элементы.Пользователь.Видимость=Истина;
		
		Если ПоГруппамПользователей Тогда
			
			Элементы.Пользователь.Заголовок=НСтр("ru = 'Группа пользователей'");
			
		Иначе
			
			Элементы.Пользователь.Заголовок=НСтр("ru = 'Пользователь'");
			
		КонецЕсли;
		
	Иначе
		
		Элементы.ТаблицаНастроекПравПользователь.Видимость=Истина;
		Элементы.Пользователь.Видимость=Ложь;
		
		Если ПоГруппамПользователей Тогда
			
			Элементы.ТаблицаНастроекПравПользователь.Заголовок=НСтр("ru = 'Группа пользователей'");
			Элементы.ТаблицаНастроекПравПользователь.ОграничениеТипа=Новый ОписаниеТипов("СправочникСсылка.ГруппыДоступаПользователиВидыОтчетов");
			
			
		Иначе
			
			Элементы.ТаблицаНастроекПравПользователь.Заголовок=НСтр("ru = 'Пользователь'");
			Элементы.ТаблицаНастроекПравПользователь.ОграничениеТипа=Новый ОписаниеТипов("СправочникСсылка.Пользователи");
				
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВидОтчета) Тогда
		
		Элементы.ТаблицаНастроекПравВидОтчета.Видимость=Ложь;
		Элементы.ВидОтчета.Видимость=Истина;
		
		Если ПоГруппамВидовОтчетов Тогда
			
			Элементы.ВидОтчета.Заголовок=НСтр("ru = 'Группа доступа (виды отчетов)'");
			
		Иначе
			
			Элементы.ВидОтчета.Заголовок=НСтр("ru = 'Вид отчета'");
			
		КонецЕсли;
		
	Иначе
		
		Элементы.ТаблицаНастроекПравВидОтчета.Видимость=Истина;
		Элементы.ВидОтчета.Видимость=Ложь;
		
		Если ПоГруппамВидовОтчетов Тогда
			
			Элементы.ТаблицаНастроекПравВидОтчета.Заголовок=НСтр("ru = 'Группа доступа (виды отчетов)'");
			Элементы.ТаблицаНастроекПравВидОтчета.ОграничениеТипа=Новый ОписаниеТипов("СправочникСсылка.ГруппыДоступаКВидамОтчетов");	
			
		Иначе
			
			Элементы.ТаблицаНастроекПравВидОтчета.Заголовок=НСтр("ru = 'Вид отчета'");
			Элементы.ТаблицаНастроекПравВидОтчета.ОграничениеТипа=Новый ОписаниеТипов("СправочникСсылка.ВидыОтчетов");
				
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.БланкОтчета.Видимость=ЗначениеЗаполнено(БланкОтчета);
	
	Если Элементы.БланкОтчета.Видимость Тогда
		
		ЭтаФорма.Заголовок=НСтр("ru = 'Настройка прав доступа к бланку отчета'");
		
	ИначеЕсли Элементы.ВидОтчета.Видимость Тогда
		
		ЭтаФорма.Заголовок=НСтр("ru = 'Настройка прав доступа к виду отчета'");
		
	Иначе
		
		ЭтаФорма.Заголовок=НСтр("ru = 'Настройка прав доступа к видам отчетов'");
		
	КонецЕсли;
		
		
	ЗначениеВРеквизитФормы(ПолучитьИзВременногоХранилища(Параметры.АдресТекущихНастроек),"ТаблицаНастроекПрав");
		
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Оповестить("ГрупповоеИзменениеНастроекПравДоступаВО",ПолучитьАдресТаблицы());
	Закрыть();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьАдресТаблицы()
	
	Возврат ПоместитьВоВременноеХранилище(РеквизитФормыВЗначение("ТаблицаНастроекПрав"));
	
КонецФункции // ПолучитьАдресТаблицы()

&НаКлиенте
Процедура УстановитьДляВыбранных(Команда)
	
	ТекВидДоступа=ПредопределенноеЗначение("Перечисление.ВидыДоступа.ЧтениеЗапись");
	
	ОписаниеОповещения=Новый ОписаниеОповещения("ПослеВводаВидаДоступаТаблица",ЭтотОбъект);
	ПоказатьВводЗначения(ОписаниеОповещения,ТекВидДоступа,НСтр("ru = 'Выберите вид доступа'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаВидаДоступаТаблица(Значение,ДополнительныеПараметры) Экспорт
	
	Если Значение=Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Для Каждого Строка ИЗ Элементы.ТаблицаНастроекПрав.ВыделенныеСтроки Цикл
		
		СтрокаДоступа=ТаблицаНастроекПрав.НайтиПоИдентификатору(Строка);	
		СтрокаДоступа.ВидДоступа=Значение;
		
	КонецЦикла;
		
КонецПроцедуры // ПослеВводаВидаДоступа()

&НаКлиенте
Процедура ЗаполнитьПраваДоступа(Команда)
		
	ТекВидДоступа=ПредопределенноеЗначение("Перечисление.ВидыДоступа.ЧтениеЗапись");
	
	ОписаниеОповещения=Новый ОписаниеОповещения("ПослеВводаВидаДоступаЗаполнить",ЭтотОбъект);
	ПоказатьВводЗначения(ОписаниеОповещения,ТекВидДоступа,НСтр("ru = 'Выберите вид доступа'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаВидаДоступаЗаполнить(Значение,ДополнительныеПараметры) Экспорт
	
	Если Значение=Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ЗаполнитьПраваДоступаСервер(Значение);
		
КонецПроцедуры // ПослеВводаВидаДоступа()

&НаСервере
Процедура ЗаполнитьПраваДоступаСервер(Значение)
	
	ТабВидыОтчетов=?(ПоГруппамВидовОтчетов,"ГруппыДоступаКВидамОтчетов","ВидыОтчетов");
	ТабОрганизации=?(ПоГруппамОрганизаций,"ГруппыДоступаОрганизацииВидыОтчетов","Организации");
	ТабПользователи=?(ПоГруппамПользователей,"ГруппыДоступаПользователиВидыОтчетов","Пользователи");
	
	Запрос=Новый Запрос;
	ТекстОтбор="";
	
	Если ЗначениеЗаполнено(ОтборВидыОтчетов) Тогда
		
		ТекстОтбор=ТекстОтбор+" И СправочникВидыОтчетов.Ссылка=&ВидОтчета";
		Запрос.УстановитьПараметр("ВидОтчета",ОтборВидыОтчетов);
		
	ИначеЕсли ЗначениеЗаполнено(ВидОтчета) Тогда
		
		ТекстОтбор=ТекстОтбор+" И СправочникВидыОтчетов.Ссылка=&ВидОтчета";
		Запрос.УстановитьПараметр("ВидОтчета",ВидОтчета);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборОрганизации) Тогда
		
		ТекстОтбор=ТекстОтбор+" И СправочникОрганизации.Ссылка=&Организация";
		Запрос.УстановитьПараметр("Организация",ОтборОрганизации);
		
	ИначеЕсли ЗначениеЗаполнено(Организация) Тогда
		
		ТекстОтбор=ТекстОтбор+" И СправочникОрганизации.Ссылка=&Организация";
		Запрос.УстановитьПараметр("Организация",Организация);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборПользователи) Тогда
		
		ТекстОтбор=ТекстОтбор+" И СправочникПользователи.Ссылка=&Пользователь";
		Запрос.УстановитьПараметр("Пользователь",ОтборПользователи);
		
	ИначеЕсли ЗначениеЗаполнено(Пользователь) Тогда
		
		ТекстОтбор=ТекстОтбор+" И СправочникПользователи.Ссылка=&Пользователь";
		Запрос.УстановитьПараметр("Пользователь",Пользователь);
		
	КонецЕсли;	
	
	Запрос.Текст="ВЫБРАТЬ
	|	СправочникВидыОтчетов.Ссылка КАК ВидОтчета,
	|	СправочникОрганизации.Ссылка КАК Организация,
	|	СправочникПользователи.Ссылка КАК Пользователь
	|ИЗ
	|	Справочник."+ТабВидыОтчетов+" КАК СправочникВидыОтчетов,
	|	Справочник."+ТабОрганизации+" КАК СправочникОрганизации,
	|	Справочник."+ТабПользователи+" КАК СправочникПользователи
	|ГДЕ (НЕ СправочникПользователи.Наименование=""<Не указан>"")"+ТекстОтбор;
		
	ТаблицаНастроекПрав.Очистить();
	
	Результат=Запрос.Выполнить().Выбрать();
	
	Пока Результат.Следующий() Цикл
		
		НоваяСтрока=ТаблицаНастроекПрав.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Результат);
		НоваяСтрока.ВидДоступа=Значение;
		
	КонецЦикла;
		
КонецПроцедуры // ЗаполнитьПраваДоступа()
