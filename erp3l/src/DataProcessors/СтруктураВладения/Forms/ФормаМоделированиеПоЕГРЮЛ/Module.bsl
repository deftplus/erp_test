
&НаКлиенте
Процедура Подробно(Команда)
	
	ВключеноПодробно = Не Элементы.ИнвестицииПодробно.Пометка;
	
	Элементы.ИнвестицииПодробно.Пометка = ВключеноПодробно;
	
	Элементы.ИнвестицииИНН.Видимость = ВключеноПодробно;
	Элементы.ИнвестицииПрямаяДоляПоЕГРЮЛ.Видимость = ВключеноПодробно;
	Элементы.ИнвестицииТекущаяПрямаяДоля.Видимость = ВключеноПодробно;
	
	Элементы.ИнвестицииИнвестицииПоЕГРЮЛ.Видимость = ВключеноПодробно;
	Элементы.ИнвестицииТекущиеИнвестиции.Видимость = ВключеноПодробно;
		
КонецПроцедуры

&НаКлиенте
Процедура СформироватьДокументыМоделирования(Команда)	
	Закрыть(ПолучитьРезультат());	
КонецПроцедуры

&НаСервере
Функция ПолучитьРезультат()
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("ОбъектИнвестирования");
	Результат.Колонки.Добавить("ПрямаяДоля");
	Результат.Колонки.Добавить("Инвестиции");
	
	Для каждого СтрокаТаб Из Инвестиции Цикл
		
		Если Не СтрокаТаб.Использовать Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаТаб.ОбъектИнвестирования.Пустая() Тогда
			
			СтрокаТаб.ОбъектИнвестирования = Справочники.Организации.НайтиПоРеквизиту("ИНН", СтрокаТаб.ИНН);
			Если СтрокаТаб.ОбъектИнвестирования.Пустая() Тогда
				
				ДопРеквизиты = Новый Структура("ИспользоватьВРегламентированномУчете");
				
				СтрокаТаб.ОбъектИнвестирования = ИнтеграцияВИБПереопределяемыйУХ.СоздатьОрганизацию(СтрокаТаб.ИНН, Истина, ДопРеквизиты);
				Если СтрокаТаб.ОбъектИнвестирования.Пустая() Тогда
					Продолжить;				 
				КонецЕсли;		
			КонецЕсли;			
						
		КонецЕсли;		
		
		НоваяСтрока = Результат.Добавить();
		НоваяСтрока.ОбъектИнвестирования = СтрокаТаб.ОбъектИнвестирования;
		НоваяСтрока.ПрямаяДоля = СтрокаТаб.МоделируемоеИзменениеДоли;
		НоваяСтрока.Инвестиции = СтрокаТаб.МоделируемыеИнвестиции;
		
	КонецЦикла;
	
	Возврат ПоместитьВоВременноеХранилище(Результат, УникальныйИдентификатор);

КонецФункции

&НаКлиенте
Процедура ОткрытьСоздатьОрганизацию(Команда)
	
	ТекущаяСтрока = Элементы.Инвестиции.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущаяСтрока.ОбъектИнвестирования.Пустая() Тогда
		ОткрытьФорму("Справочник.Организации.ФормаОбъекта", Новый Структура("ИНН", ТекущаяСтрока.ИНН));	
	Иначе 
		ОткрытьФорму("Справочник.Организации.ФормаОбъекта", Новый Структура("Ключ", ТекущаяСтрока.ОбъектИнвестирования));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнвестицииПриАктивизацииСтроки(Элемент)
	
	ТекущаяСтрока = Элементы.Инвестиции.ТекущиеДанные;	
	Элементы.ИнвестицииОткрытьСоздатьОрганизацию.Видимость = (ТекущаяСтрока <> Неопределено);
		
	Если (ТекущаяСтрока <> Неопределено) И ТекущаяСтрока.ОбъектИнвестирования.Пустая() Тогда
		Элементы.ИнвестицииОткрытьСоздатьОрганизацию.Заголовок = НСтр("ru = 'Создать объект инвестирования'");
	Иначе 
		Элементы.ИнвестицииОткрытьСоздатьОрганизацию.Заголовок = НСтр("ru = 'Открыть объект инвестирования'");
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ДосьеОбъектаИнвестирования(Команда)
	
	ТекущаяСтрока = Элементы.Инвестиции.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ОткрытьФорму("Отчет.ДосьеКонтрагента.Форма", Новый Структура("ИНН", ТекущаяСтрока.ИНН));
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Инвестор = Параметры.Инвестор;
	
	ТабПрямыеДоли = РасчетДолейВладения.ПолучитьТаблицуПрямыеДоли(Параметры.Сценарий, ТекущаяДата(), Истина, Параметры.Инвестор);
	ТабПрямыеДоли.Индексы.Добавить("ОбъектИнвестирования");
	
	Для каждого ДанныеОИ Из Параметры.ДанныеЕГРЮЛ Цикл
		
		НоваяСтрока = Инвестиции.Добавить();
		
		НоваяСтрока.ИНН = ДанныеОИ.Ключ;
		
		НоваяСтрока.Наименование = ДанныеОИ.Значение.Наименование;
		НоваяСтрока.ПрямаяДоляПоЕГРЮЛ = ДанныеОИ.Значение.ПрямаяДоля;
		НоваяСтрока.ИнвестицииПоЕГРЮЛ = ДанныеОИ.Значение.Инвестиции;
		НоваяСтрока.Статус = ДанныеОИ.Значение.Статус;
		
		НоваяСтрока.Использовать = НоваяСтрока.ПрямаяДоляПоЕГРЮЛ > 0;
		
		НоваяСтрока.ОбъектИнвестирования = Справочники.Организации.НайтиПоРеквизиту("ИНН", ДанныеОИ.Ключ);
		Если Не НоваяСтрока.ОбъектИнвестирования.Пустая() Тогда
			
			СтрокаПрямаяДоля = ТабПрямыеДоли.Найти(НоваяСтрока.ОбъектИнвестирования, "ОбъектИнвестирования");
			Если СтрокаПрямаяДоля <> Неопределено Тогда
				НоваяСтрока.ТекущаяПрямаяДоля = СтрокаПрямаяДоля.ПрямаяДоля;
				НоваяСтрока.ТекущиеИнвестиции = СтрокаПрямаяДоля.Инвестиции;
			КонецЕсли;		
		КонецЕсли;
		
		НоваяСтрока.МоделируемоеИзменениеДоли = НоваяСтрока.ПрямаяДоляПоЕГРЮЛ - НоваяСтрока.ТекущаяПрямаяДоля;
		НоваяСтрока.МоделируемыеИнвестиции = НоваяСтрока.ИнвестицииПоЕГРЮЛ - НоваяСтрока.ТекущиеИнвестиции;
		
	КонецЦикла;
	
	Инвестиции.Сортировать("МоделируемоеИзменениеДоли Убыв, ПрямаяДоляПоЕГРЮЛ Убыв");
	
КонецПроцедуры
