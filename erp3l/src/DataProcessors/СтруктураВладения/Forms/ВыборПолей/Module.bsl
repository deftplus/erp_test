
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Перем ВыборПолейСвязи;
	
	СКД = ТиповыеОтчетыУХ.ПолучитьСхемуКомпоновкиДанныхПоОбъекту("Контекст", "ТабОрганизации", "ТабОрганизации");
	
	ТабОрганизации = СКД.НаборыДанных.ТабОрганизации;
	
	Для каждого ДоступноеПоле Из Параметры.ДоступныеПоля Цикл		
		ПолеНД = ТиповыеОтчетыУХ.ДобавитьПолеНабораДанных(ТабОрганизации, ДоступноеПоле.Поле, , , ДоступноеПоле.ТипЗначения);
		ПолеНД.Роль.Измерение = Истина;
	КонецЦикла;
	
	АдресСКД = ПоместитьВоВременноеХранилище(СКД);
	Компоновщик.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСКД));
	
	ВыборПолейСвязи = Параметры.ДоступныеПоля[0].Поле = "Инвестор";
	
	Для каждого ВыбранноеПолеИсточник Из СтрРазделить(Параметры.ВыбранныеПоля, ";") Цикл
		
		ВыбранноеПоле = Компоновщик.Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
		
		Если ВыборПолейСвязи = Истина Тогда
			ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных(СокрЛП(ВыбранноеПолеИсточник));			
		Иначе
			ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных(СтрЗаменить(СокрЛП(ВыбранноеПолеИсточник), "Инвестор", "Организация"));
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
	
	ВыбранныеПоля = ПолучитьВыбранныеПоля();
	
	Результат = "";
	Для каждого ВыбранноеПоле Из ВыбранныеПоля Цикл
		Если ВыбранноеПоле.Использование Тогда
			Результат = ?(Результат = "", Строка(ВыбранноеПоле.Поле), Результат + ";" + Строка(ВыбранноеПоле.Поле));	
		КонецЕсли;		
	КонецЦикла;
	
	ЭтаФорма.ОповеститьОВыборе(Результат);
	//Закрыть(Результат);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьВыбранныеПоля()	
	Возврат ТиповыеОтчетыУХ.ПолучитьВыбранныеПоля(Компоновщик.ПолучитьНастройки().Выбор);	
КонецФункции

