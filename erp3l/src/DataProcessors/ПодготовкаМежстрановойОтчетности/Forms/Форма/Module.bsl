
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбъектСервер = РеквизитФормыВЗначение("Объект");
	СписокОрганизаций.ТекстЗапроса = ОбъектСервер.ПолучитьТекстЗапросаМСО(Новый Структура("Организация, СтрановойОтчет, УчастиеВМеждународнойГруппеКомпаний", "Организация", "СтрановойОтчет", "УчастиеВМеждународнойГруппеКомпаний"));
	СписокОрганизаций.Параметры.УстановитьЗначениеПараметра("Период", КонецГода(Период.Дата));
	Элементы.СписокОрганизаций.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если НЕ ЗначениеЗаполнено(Период) Тогда
		Период.Дата = НачалоГода(ТекущаяДата());
		ПериодПриИзменении(Неопределено);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	Элементы.СписокОрганизаций.Обновить();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	СписокОрганизаций.Параметры.УстановитьЗначениеПараметра("Период", КонецГода(Период.Дата));
	Элементы.СписокОрганизаций.Обновить();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокОрганизаций

&НаКлиенте
Процедура СписокОрганизацийОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	Элементы.СписокОрганизаций.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура СписокОрганизацийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если ТипЗнч(ВыбраннаяСтрока) = Тип("Число") Тогда
		Если Поле.Имя = "СписокОрганизацийСтрановойОтчет" Тогда
			ПоказатьЗначение(, Элементы.СписокОрганизаций.ДанныеСтроки(ВыбраннаяСтрока).СтрановойОтчет);
		ИначеЕсли Поле.Имя = "СписокОрганизацийУчастиеВМеждународнойГруппеКомпаний" Тогда
			ПоказатьЗначение(, Элементы.СписокОрганизаций.ДанныеСтроки(ВыбраннаяСтрока).УчастиеВМеждународнойГруппеКомпаний);
		ИначеЕсли Поле.Имя = "СписокОрганизацийОрганизация" Тогда
			ПоказатьЗначение(, Элементы.СписокОрганизаций.ДанныеСтроки(ВыбраннаяСтрока).Организация);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьСтрановойОтчет(Команда)
	СоздатьРегламентированныйОтчет("СтрановойОтчет");
КонецПроцедуры

&НаКлиенте
Процедура СоздатьУведомление(Команда)
	СоздатьРегламентированныйОтчет("УчастиеВМеждународнойГруппеКомпаний");
КонецПроцедуры

&НаКлиенте
Процедура УбавитьГод(Команда)
	Период.Дата = НачалоГода(ДобавитьМесяц(НачалоГода(Период.Дата), -1));
	ПериодПриИзменении(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьГод(Команда)
	Период.Дата = НачалоГода(ДобавитьМесяц(КонецГода(Период.Дата), 1));
	ПериодПриИзменении(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ПометкаУдаления(Команда)
	Если Элементы.СписокОрганизаций.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Элементы.СписокОрганизаций.ТекущийЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Элементы.СписокОрганизаций.ТекущийЭлемент.Имя = "СписокОрганизацийСтрановойОтчет" Тогда
		Ссылка = Элементы.СписокОрганизаций.ТекущиеДанные.СтрановойОтчет;
	ИначеЕсли Элементы.СписокОрганизаций.ТекущийЭлемент.Имя = "СписокОрганизацийУчастиеВМеждународнойГруппеКомпаний" Тогда
		Ссылка = Элементы.СписокОрганизаций.ТекущиеДанные.УчастиеВМеждународнойГруппеКомпаний;
	Иначе
		Возврат;
	КонецЕсли;
	Если ЗначениеЗаполнено(Ссылка) Тогда
		ПометитьНаУдаление(Ссылка);
		ПериодПриИзменении(Неопределено);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СоздатьРегламентированныйОтчет(ВидОтчета)
	Если Элементы.СписокОрганизаций.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Организация = Элементы.СписокОрганизаций.ТекущиеДанные.Организация;
	
	РегОтчет = Элементы.СписокОрганизаций.ТекущиеДанные[ВидОтчета];
	
	Если ЗначениеЗаполнено(РегОтчет) Тогда
		ПоказатьЗначение(, РегОтчет);
		Возврат;
	КонецЕсли;
	
	ФормаОтчета = ПолучитьФормуСтрановогоОтчета(КонецГода(Период.Дата), "РегламентированныйОтчет" + ВидОтчета);
	
	Если ТипЗнч(ФормаОтчета) = Тип("Строка") Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("мДатаНачалаПериодаОтчета", НачалоГода(Период.Дата));
		ПараметрыФормы.Вставить("мДатаКонцаПериодаОтчета",  КонецГода(Период.Дата));
		ПараметрыФормы.Вставить("мПериодичность",           ПредопределенноеЗначение("Перечисление.Периодичность.Год"));
		ПараметрыФормы.Вставить("Организация",              Организация);
		ПараметрыФормы.Вставить("мВыбраннаяФорма",          ФормаОтчета);
		ПараметрыФормы.Вставить("СформироватьФормуОтчетаАвтоматически", Истина);
		ОткрытьФорму("Отчет.РегламентированныйОтчет" + ВидОтчета + ".Форма." + ФормаОтчета, ПараметрыФормы, ЭтаФорма, Истина);
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Форма отчета для выбранного периода отсутствует в программе.");
		Возврат;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьФормуСтрановогоОтчета(ДатаОкончания, ИмяОтчета)
	Форма = Новый Структура;
	
	Форма.Вставить("мТаблицаФормОтчета", РегламентированнаяОтчетностьВызовСервера.ОтчетОбъект(ИмяОтчета).ТаблицаФормОтчета());
	Форма.Вставить("мДатаКонцаПериодаОтчета", ДатаОкончания);
	
	КоличествоФорм = РегламентированнаяОтчетностьКлиентСервер.КоличествоФормСоответствующихВыбранномуПериоду(Форма);
	
	Если КоличествоФорм = 1 Тогда
		Возврат Форма.мТаблицаФормОтчета[0].ФормаОтчета;
	//ИначеЕсли КоличествоФорм > 1 Тогда
	// Новый списокЗначений
	КонецЕсли;
КонецФункции

&НаСервереБезКонтекста
Процедура ПометитьНаУдаление(Ссылка)
	Объект = Ссылка.ПолучитьОбъект();
	Объект.УстановитьПометкуУдаления(Истина);
КонецПроцедуры

#КонецОбласти

