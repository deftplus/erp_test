
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПолучитьТекстЗапросаМСО(СтруктураВыбранныхПолей, СтруктураОтбора = Неопределено) Экспорт
	Построитель = Новый ПостроительЗапроса("ВЫБРАТЬ
	                                       |	СтатусыОбъектовИнвестированияСрезПоследних.Инвестор КАК Инвестор,
	                                       |	СтатусыОбъектовИнвестированияСрезПоследних.ОбъектИнвестирования КАК ОбъектИнвестирования,
	                                       |	СтатусыОбъектовИнвестированияСрезПоследних.ОтношениеКГруппе КАК ОтношениеКГруппе,
	                                       |	СтатусыОбъектовИнвестированияСрезПоследних.Инвестор.ИностранныйНалоговыйРезидент КАК ИнвесторИностранныйНалоговыйРезидент,
	                                       |	СтатусыОбъектовИнвестированияСрезПоследних.ОбъектИнвестирования.ИностранныйНалоговыйРезидент КАК ОбъектИнвестированияИностранныйНалоговыйРезидент,
	                                       |	СтатусыОбъектовИнвестированияСрезПоследних.ЭффективнаяДоля КАК ЭффективнаяДоля
	                                       |ПОМЕСТИТЬ ИОИ
	                                       |ИЗ
	                                       |	РегистрСведений.СтатусыОбъектовИнвестирования.СрезПоследних(
	                                       |			&Период,
	                                       |			Сценарий В
	                                       |				(ВЫБРАТЬ
	                                       |					СценарийОтчетностиКИК.Значение КАК Значение
	                                       |				ИЗ
	                                       |					Константа.СценарийОтчетностиКИК КАК СценарийОтчетностиКИК)) КАК СтатусыОбъектовИнвестированияСрезПоследних
	                                       |ГДЕ
	                                       |	СтатусыОбъектовИнвестированияСрезПоследних.ОтношениеКГруппе.ВидОтношенияКГруппе = ЗНАЧЕНИЕ(Перечисление.ВидыОтношенийКГруппе.Дочернее)
	                                       |
	                                       |ИНДЕКСИРОВАТЬ ПО
	                                       |	Инвестор,
	                                       |	ОбъектИнвестирования,
	                                       |	ИнвесторИностранныйНалоговыйРезидент,
	                                       |	ОбъектИнвестированияИностранныйНалоговыйРезидент
	                                       |;
	                                       |
	                                       |////////////////////////////////////////////////////////////////////////////////
	                                       |ВЫБРАТЬ
	                                       |	ИОИ.Инвестор КАК Организация,
	                                       |	ИОИ.ИнвесторИностранныйНалоговыйРезидент КАК ИностранныйНалоговыйРезидент
	                                       |ПОМЕСТИТЬ ВсеОрганизации
	                                       |ИЗ
	                                       |	ИОИ КАК ИОИ
	                                       |
	                                       |ОБЪЕДИНИТЬ
	                                       |
	                                       |ВЫБРАТЬ
	                                       |	ИОИ.ОбъектИнвестирования,
	                                       |	ИОИ.ОбъектИнвестированияИностранныйНалоговыйРезидент
	                                       |ИЗ
	                                       |	ИОИ КАК ИОИ
	                                       |
	                                       |ИНДЕКСИРОВАТЬ ПО
	                                       |	Организация,
	                                       |	ИностранныйНалоговыйРезидент
	                                       |;
	                                       |
	                                       |////////////////////////////////////////////////////////////////////////////////
	                                       |ВЫБРАТЬ
	                                       |	ВсеОрганизации.Организация КАК Организация,
	                                       |	ИОИ.ОбъектИнвестирования КАК ОбъектИнвестирования,
	                                       |	ВсеОрганизации.ИностранныйНалоговыйРезидент КАК ИностранныйНалоговыйРезидент,
	                                       |	ИОИ.ОбъектИнвестированияИностранныйНалоговыйРезидент КАК ОбъектИнвестированияИностранныйНалоговыйРезидент
	                                       |ПОМЕСТИТЬ ПервыйУровеньПодчинения
	                                       |ИЗ
	                                       |	ВсеОрганизации КАК ВсеОрганизации
	                                       |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИОИ КАК ИОИ
	                                       |		ПО ВсеОрганизации.Организация = ИОИ.Инвестор
	                                       |
	                                       |ОБЪЕДИНИТЬ ВСЕ
	                                       |
	                                       |ВЫБРАТЬ
	                                       |	ВсеОрганизации.Организация,
	                                       |	ИОИ.Инвестор,
	                                       |	ВсеОрганизации.ИностранныйНалоговыйРезидент,
	                                       |	ИОИ.ИнвесторИностранныйНалоговыйРезидент
	                                       |ИЗ
	                                       |	ВсеОрганизации КАК ВсеОрганизации
	                                       |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИОИ КАК ИОИ
	                                       |		ПО ВсеОрганизации.Организация = ИОИ.ОбъектИнвестирования
	                                       |
	                                       |ИНДЕКСИРОВАТЬ ПО
	                                       |	Организация,
	                                       |	ОбъектИнвестирования,
	                                       |	ИностранныйНалоговыйРезидент,
	                                       |	ОбъектИнвестированияИностранныйНалоговыйРезидент
	                                       |;
	                                       |
	                                       |////////////////////////////////////////////////////////////////////////////////
	                                       |ВЫБРАТЬ РАЗЛИЧНЫЕ
	                                       |	Основная.Организация КАК Организация,
	                                       |	Соединение.ОбъектИнвестирования КАК СоставГруппы,
	                                       |	Основная.ИностранныйНалоговыйРезидент КАК ИностранныйНалоговыйРезидент,
	                                       |	Соединение.ОбъектИнвестированияИностранныйНалоговыйРезидент КАК ОбъектИнвестированияИностранныйНалоговыйРезидент
	                                       |ПОМЕСТИТЬ СписокВсехГрупп
	                                       |ИЗ
	                                       |	ПервыйУровеньПодчинения КАК Основная
	                                       |		ЛЕВОЕ СОЕДИНЕНИЕ ПервыйУровеньПодчинения КАК Соединение
	                                       |		ПО Основная.ОбъектИнвестирования = Соединение.Организация
	                                       |
	                                       |ОБЪЕДИНИТЬ
	                                       |
	                                       |ВЫБРАТЬ
	                                       |	ПервыйУровеньПодчинения.Организация,
	                                       |	ПервыйУровеньПодчинения.ОбъектИнвестирования,
	                                       |	ПервыйУровеньПодчинения.ИностранныйНалоговыйРезидент,
	                                       |	ПервыйУровеньПодчинения.ОбъектИнвестированияИностранныйНалоговыйРезидент
	                                       |ИЗ
	                                       |	ПервыйУровеньПодчинения КАК ПервыйУровеньПодчинения
	                                       |
	                                       |ИНДЕКСИРОВАТЬ ПО
	                                       |	Организация,
	                                       |	СоставГруппы,
	                                       |	ИностранныйНалоговыйРезидент,
	                                       |	ОбъектИнвестированияИностранныйНалоговыйРезидент
	                                       |;
	                                       |
	                                       |////////////////////////////////////////////////////////////////////////////////
	                                       |ВЫБРАТЬ
	                                       |	СписокВсехГрупп.Организация КАК Организация,
	                                       |	СписокВсехГрупп.СоставГруппы КАК ОбъектИнвестирования,
	                                       |	СписокВсехГрупп.ОбъектИнвестированияИностранныйНалоговыйРезидент КАК ИностранныйНалоговыйРезидент,
	                                       |	МАКСИМУМ(СтрановойОтчет.Ссылка) КАК СтрановойОтчет,
	                                       |	МАКСИМУМ(УчастиеВМеждународнойГруппеКомпаний.Ссылка) КАК УчастиеВМеждународнойГруппеКомпаний,
	                                       |	ВЫБОР
	                                       |		КОГДА СписокВсехГрупп.ОбъектИнвестированияИностранныйНалоговыйРезидент
	                                       |			ТОГДА СписокВсехГрупп.СоставГруппы.СтранаРегистрации
	                                       |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	                                       |	КОНЕЦ КАК Страна,
	                                       |	МАКСИМУМ(ИностранныйИнвестор.Инвестор) КАК ИностранныйИнвестор,
	                                       |	МАКСИМУМ(РоссийскийИнвестор.Инвестор) КАК РоссийскийИнвестор
	                                       |{ВЫБРАТЬ
	                                       |	Организация.*,
	                                       |	ОбъектИнвестирования.*,
	                                       |	СтрановойОтчет.*,
	                                       |	УчастиеВМеждународнойГруппеКомпаний.*,
	                                       |	ИностранныйНалоговыйРезидент,
	                                       |	Страна.*,
	                                       |	ИностранныйИнвестор.*,
	                                       |	РоссийскийИнвестор.*}
	                                       |ИЗ
	                                       |	СписокВсехГрупп КАК СписокВсехГрупп
	                                       |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	                                       |			СписокВсехГрупп.Организация КАК Организация,
	                                       |			МАКСИМУМ(СписокВсехГрупп.ОбъектИнвестированияИностранныйНалоговыйРезидент) КАК ВЭтойГруппеИнтостранные
	                                       |		ИЗ
	                                       |			СписокВсехГрупп КАК СписокВсехГрупп
	                                       |		
	                                       |		СГРУППИРОВАТЬ ПО
	                                       |			СписокВсехГрупп.Организация) КАК ОтборПоИностраннымУчастникам
	                                       |		ПО СписокВсехГрупп.Организация = ОтборПоИностраннымУчастникам.Организация
	                                       |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РегламентированныйОтчет КАК СтрановойОтчет
	                                       |		ПО СписокВсехГрупп.Организация = СтрановойОтчет.Организация
	                                       |			И (НЕ СтрановойОтчет.ПометкаУдаления)
	                                       |			И (НАЧАЛОПЕРИОДА(СтрановойОтчет.Дата, ГОД) = НАЧАЛОПЕРИОДА(&Период, ГОД))
	                                       |			И (СтрановойОтчет.ИсточникОтчета = ""РегламентированныйОтчетСтрановойОтчет"")
	                                       |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РегламентированныйОтчет КАК УчастиеВМеждународнойГруппеКомпаний
	                                       |		ПО СписокВсехГрупп.Организация = УчастиеВМеждународнойГруппеКомпаний.Организация
	                                       |			И (НЕ УчастиеВМеждународнойГруппеКомпаний.ПометкаУдаления)
	                                       |			И (НАЧАЛОПЕРИОДА(УчастиеВМеждународнойГруппеКомпаний.Дата, ГОД) = НАЧАЛОПЕРИОДА(&Период, ГОД))
	                                       |			И (УчастиеВМеждународнойГруппеКомпаний.ИсточникОтчета = ""РегламентированныйОтчетУчастиеВМеждународнойГруппеКомпаний"")
	                                       |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	                                       |			ИОИ.ОбъектИнвестирования КАК ОбъектИнвестирования,
	                                       |			МАКСИМУМ(ИОИ.ЭффективнаяДоля) КАК ЭффективнаяДоля
	                                       |		ИЗ
	                                       |			ИОИ КАК ИОИ
	                                       |		ГДЕ
	                                       |			ИОИ.ИнвесторИностранныйНалоговыйРезидент
	                                       |		
	                                       |		СГРУППИРОВАТЬ ПО
	                                       |			ИОИ.ОбъектИнвестирования) КАК ПоискИностранногоИнвестора
	                                       |			ЛЕВОЕ СОЕДИНЕНИЕ ИОИ КАК ИностранныйИнвестор
	                                       |			ПО ПоискИностранногоИнвестора.ЭффективнаяДоля = ИностранныйИнвестор.ЭффективнаяДоля
	                                       |				И (ИностранныйИнвестор.ИнвесторИностранныйНалоговыйРезидент)
	                                       |		ПО СписокВсехГрупп.Организация = ПоискИностранногоИнвестора.ОбъектИнвестирования
	                                       |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	                                       |			ИОИ.ОбъектИнвестирования КАК ОбъектИнвестирования,
	                                       |			МАКСИМУМ(ИОИ.ЭффективнаяДоля) КАК ЭффективнаяДоля
	                                       |		ИЗ
	                                       |			ИОИ КАК ИОИ
	                                       |		ГДЕ
	                                       |			ИОИ.ИнвесторИностранныйНалоговыйРезидент = ЛОЖЬ
	                                       |		
	                                       |		СГРУППИРОВАТЬ ПО
	                                       |			ИОИ.ОбъектИнвестирования) КАК ПоискРоссийскогоИнвестора
	                                       |			ЛЕВОЕ СОЕДИНЕНИЕ ИОИ КАК РоссийскийИнвестор
	                                       |			ПО ПоискРоссийскогоИнвестора.ЭффективнаяДоля = РоссийскийИнвестор.ЭффективнаяДоля
	                                       |				И (НЕ РоссийскийИнвестор.ИнвесторИностранныйНалоговыйРезидент)
	                                       |		ПО СписокВсехГрупп.Организация = ПоискРоссийскогоИнвестора.ОбъектИнвестирования
	                                       |ГДЕ
	                                       |	ОтборПоИностраннымУчастникам.ВЭтойГруппеИнтостранные
	                                       |	И СписокВсехГрупп.ИностранныйНалоговыйРезидент = ЛОЖЬ
	                                       |{ГДЕ
	                                       |	СписокВсехГрупп.Организация.*}
	                                       |
	                                       |СГРУППИРОВАТЬ ПО
	                                       |	СписокВсехГрупп.Организация,
	                                       |	СписокВсехГрупп.СоставГруппы,
	                                       |	СписокВсехГрупп.ОбъектИнвестированияИностранныйНалоговыйРезидент,
	                                       |	ВЫБОР
	                                       |		КОГДА СписокВсехГрупп.ОбъектИнвестированияИностранныйНалоговыйРезидент
	                                       |			ТОГДА СписокВсехГрупп.СоставГруппы.СтранаРегистрации
	                                       |		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	                                       |	КОНЕЦ");
	
	Построитель.ВыбранныеПоля.Очистить();
	Если ТипЗнч(СтруктураВыбранныхПолей) = Тип("Структура") Тогда
		Для каждого Элемент Из СтруктураВыбранныхПолей Цикл
			Построитель.ВыбранныеПоля.Добавить(Элемент.Значение, Элемент.Ключ);
		КонецЦикла;
	ИначеЕсли ТипЗнч(СтруктураВыбранныхПолей) = Тип("Строка") Тогда
		МассивПолей = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтруктураВыбранныхПолей, ",");
		Для каждого Элемент Из МассивПолей Цикл
			Построитель.ВыбранныеПоля.Добавить(СокрЛП(Элемент), СокрЛП(Элемент));
		КонецЦикла;
	КонецЕсли;
	Если ТипЗнч(СтруктураОтбора) = Тип("Структура") Тогда
		Для каждого Элемент Из СтруктураОтбора Цикл
			НовыйОтбор = Построитель.Отбор.Добавить(Элемент.Ключ, Элемент.Ключ);
			НовыйОтбор.Установить(Элемент.Значение);
		КонецЦикла;
	КонецЕсли;
	Возврат Построитель.ПолучитьЗапрос().Текст;
КонецФункции

Процедура ЗаполнитьСтрановойОтчет(ПараметрыОтчета, Контейнер) Экспорт
	
	Запрос = ПолучитьЗапросИнвестиций(ПараметрыОтчета.мДатаКонцаПериодаОтчета, ПараметрыОтчета.Организация);
	Результат = Запрос.Выполнить();
	
	ТаблицаИнвестиций = Результат.Выгрузить();
	Если ТаблицаИнвестиций.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	АдресаОрганизаций = ПолучитьТаблицуАдресовОрганизаций(ТаблицаИнвестиций, ПараметрыОтчета.Организация, ПараметрыОтчета.мДатаКонцаПериодаОтчета);
	СписокСтран = ТаблицаИнвестиций.Скопировать(, "Страна, КодАльфа2");
	СписокСтран.Свернуть("Страна, КодАльфа2");
	ПоказателиРНБВП = ПолучитьТаблицуПоказателейРНБВП(ПараметрыОтчета.мДатаКонцаПериодаОтчета);
	Контейнер.Титульный.Язык = "RU";
	СтатусУчастника = "2";
	Материнская = Неопределено;
	ЗаполнитьТитульныйЛист(ТаблицаИнвестиций[0], Контейнер, СтатусУчастника, Материнская, ПараметрыОтчета.Организация, ПараметрыОтчета.мДатаКонцаПериодаОтчета);
	
	Контейнер.Раздел1.Строки[0].ДанныеМногострочныхЧастей.П00001М1.Строки[0].Данные.П00001М100001 = "RU";
	Контейнер.Раздел1.Строки[0].Данные = СтрановойОтчетЗаполнитьСтруктуруДанныхРазделПоОрганизации("Р1", ПараметрыОтчета.Организация, "CBC70" + СтатусУчастника, АдресаОрганизаций);
	
	Контейнер.Раздел2.Строки.Очистить();
	Для каждого Стр Из СписокСтран Цикл
		
		НоваяСтрока0 = Контейнер.Раздел2.Строки.Добавить();
		СтрокаПоказателей = ПоказателиРНБВП.Найти(Стр.Страна);
		НоваяСтрока0.Данные = СтрановойОтчетПолучитьСтруктуруДанныхРаздел20(Стр.КодАльфа2, СтрокаПоказателей);
		НоваяСтрока0.ДанныеМногострочныхЧастей = Новый Структура;
		
		НоваяСтрока1 = НоваяСтрока0.Строки.Добавить();
		НоваяСтрока1.Данные = Новый Структура;
		НоваяСтрока1.ДанныеМногострочныхЧастей = Новый Структура;
		
		СписокИнвестицийПоСтране = ТаблицаИнвестиций.Скопировать(Новый Структура("Страна", Стр.Страна));
		Для каждого Стр2 Из СписокИнвестицийПоСтране Цикл
			НоваяСтрока2 = НоваяСтрока1.Строки.Добавить();
			Если Стр2.ОбъектИнвестирования = ПараметрыОтчета.Организация И СтатусУчастника = "1" ИЛИ Стр2.ОбъектИнвестирования = Материнская Тогда
				СтатусУчастника2 = "1";
			ИначеЕсли Стр2.ОбъектИнвестирования = ПараметрыОтчета.Организация Тогда
				СтатусУчастника2 = "2";
			Иначе
				СтатусУчастника2 = "3";
			КонецЕсли;
			НоваяСтрока2.Данные = СтрановойОтчетЗаполнитьСтруктуруДанныхРазделПоОрганизации("Р21", Стр2.ОбъектИнвестирования, "CBC70" + СтатусУчастника2, АдресаОрганизаций);
			НоваяСтрока2.ДанныеМногострочныхЧастей = СтрановойОтчетПолучитьДеревоВидовЭкономическойДейтельности("CBC501");
		КонецЦикла;
	КонецЦикла;
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("Контейнер", Контейнер);
	ПоместитьВоВременноеХранилище(Контейнер, ПараметрыОтчета.АдресВоВременномХранилище);

КонецПроцедуры

Процедура ЗаполнитьУчастиеВМеждународнойГруппеКомпаний(ПараметрыОтчета, Контейнер) Экспорт
	Запрос = ПолучитьЗапросИнвестиций(ПараметрыОтчета.мДатаКонцаПериодаОтчета, ПараметрыОтчета.Организация);
	Результат = Запрос.Выполнить();
	
	ТаблицаИнвестиций = Результат.Выгрузить();
	Если ТаблицаИнвестиций.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтатусУчастника = "2";
	Материнская = Неопределено;
	ЗаполнитьТитульныйЛист(ТаблицаИнвестиций[0], Контейнер, СтатусУчастника, Материнская, ПараметрыОтчета.Организация, ПараметрыОтчета.мДатаКонцаПериодаОтчета);
	Контейнер.Титульный.СтатУчМГ = СтатусУчастника;

	Контейнер.Титульный.НаимУчМГЛат = СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(Контейнер.Титульный.НаимОрг);
	Контейнер.Титульный.ПрУчМГ = "1";
	Контейнер.Титульный.ПрСтрПред = "1";
	Контейнер.Титульный.ПолнУчМГ = "1";
	Контейнер.Титульный.ДатаНач = НачалоГода(ПараметрыОтчета.мДатаНачалаПериодаОтчета);
	Контейнер.Титульный.ОтчетГод = Формат(Год(ПараметрыОтчета.мДатаКонцаПериодаОтчета), "ЧГ=");
	
	Данные1 = Новый Структура;
	Данные2 = Новый Структура;
	ИнициализироватьСтруктуры(Контейнер, Данные1, Данные2);
	
	АдресаОрганизаций = ПолучитьТаблицуАдресовОрганизаций(ТаблицаИнвестиций, ПараметрыОтчета.Организация, ПараметрыОтчета.мДатаКонцаПериодаОтчета);
	Для каждого Стр Из ТаблицаИнвестиций Цикл
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Стр.ОбъектИнвестирования, "НаименованиеПолное, НаименованиеИнострОрганизации, ИНН, КПП, ОГРН, ИностраннаяСтруктураБезОбразованияЮрЛица, ВидИностраннойСтруктуры, КодВСтранеРегистрации, КодНалогоплательщикаИностранный");
		Если Стр.Страна = Справочники.СтраныМира.Россия Тогда
			НоваяСтрока = Контейнер.Раздел2.Строки.Добавить();
			НоваяСтрока.ДанныеМногострочныхЧастей = Новый Структура;
			НовыеДанные = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(Данные2);
			НовыеДанные.ПрСтрПредР2 = "1";
			НовыеДанные.СтатУчМГР2 = ?(Стр.ОбъектИнвестирования = Материнская, "1", ?(Стр.ОбъектИнвестирования = ПараметрыОтчета.Организация, "2", "3"));
			НовыеДанные.ПрУчМГР2 = "1";
			НовыеДанные.ИННР2 = Реквизиты.ИНН;
			НовыеДанные.КППР2 = Реквизиты.КПП;
			НовыеДанные.ОГРНР2 = Реквизиты.ОГРН;
			НовыеДанные.НаимУчЛатР2 = ?(ПустаяСтрока(Реквизиты.НаименованиеИнострОрганизации), СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(Реквизиты.НаименованиеПолное), Реквизиты.НаименованиеИнострОрганизации);
			НовыеДанные.НаимУчР2 = Реквизиты.НаименованиеПолное;
			НоваяСтрока.Данные = НовыеДанные;
		ИначеЕсли Стр.ОбъектИнвестирования = Материнская Тогда //Добавляем только те иностранные компании которые являются материнскими. Статья 105.16-2.
			НоваяСтрока = Контейнер.Раздел1.Строки.Добавить();
			НоваяСтрока.ДанныеМногострочныхЧастей = Новый Структура;
			НовыеДанные = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(Данные1);
			НовыеДанные.НаимУчЛатР1 = ?(ПустаяСтрока(Реквизиты.НаименованиеИнострОрганизации), СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(Реквизиты.НаименованиеПолное), Реквизиты.НаименованиеИнострОрганизации);
			НовыеДанные.НаимУчР1 = Реквизиты.НаименованиеПолное;
			НовыеДанные.СтатУчМГР1 = ?(Стр.ОбъектИнвестирования = Материнская, "1", ?(Стр.ОбъектИнвестирования = ПараметрыОтчета.Организация, "2", "3"));
			НовыеДанные.ОргФормР1 = ?(Реквизиты.ИностраннаяСтруктураБезОбразованияЮрЛица, "2", "1");
			Если Реквизиты.ИностраннаяСтруктураБезОбразованияЮрЛица Тогда
				НовыеДанные.ОргФормСтрБЮЛР1 = РасчетыПоКорпоративнымНалогам.ПолучитьКодВидаИностраннойСтруктуры(Реквизиты.ВидИностраннойСтруктуры);
			КонецЕсли;
			НовыеДанные.РегНомР1 = Реквизиты.КодВСтранеРегистрации;
			НовыеДанные.НомНПР1 = Реквизиты.КодНалогоплательщикаИностранный;
			НовыеДанные.СтрНалРезидР1 = Стр.КодАльфа2;
			НовыеДанные.СтрРегР1 = Стр.КодАльфа2;
			НовыеДанные.СтрАдрР1 = Стр.КодАльфа2;
			СтрокаАдреса = АдресаОрганизаций.Найти(Стр.ОбъектИнвестирования, "Объект");
			Если СтрокаАдреса <> Неопределено Тогда
				Представление = СтрокаАдреса.Представление;
				НовыеДанные.АдрР1 = Представление;
				НовыеДанные.АдрЛатР1 = СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(Представление);
			КонецЕсли;
			НоваяСтрока.Данные = НовыеДанные;
		КонецЕсли;
		
	КонецЦикла;
	
	ДополнитьПустыеДеревья(Контейнер, Данные1, Данные2);
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("Контейнер", Контейнер);
	ПоместитьВоВременноеХранилище(Контейнер, ПараметрыОтчета.АдресВоВременномХранилище);
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеСтрановогоОтчета

Процедура ЗаполнитьТитульныйЛист(СтрокаТаблицыИнвестиций, Контейнер, СтатусУчастника, Материнская, Организация, ДатаКонцаПериодаОтчета)
	Если ЗначениеЗаполнено(СтрокаТаблицыИнвестиций.ИностранныйИнвестор) Тогда
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаТаблицыИнвестиций.ИностранныйИнвестор, "НаименованиеПолное, НаименованиеИнострОрганизации, КодВСтранеРегистрации, ИНН");
		Контейнер.Титульный.НаимМГ = Реквизиты.НаименованиеПолное;
		Контейнер.Титульный.НаимМГЛат = ?(ПустаяСтрока(Реквизиты.НаименованиеИнострОрганизации), СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(Реквизиты.НаименованиеПолное), Реквизиты.НаименованиеИнострОрганизации);
		Контейнер.Титульный.НомИнМатК = Реквизиты.КодВСтранеРегистрации;
		Материнская = СтрокаТаблицыИнвестиций.ИностранныйИнвестор;
	ИначеЕсли ЗначениеЗаполнено(СтрокаТаблицыИнвестиций.РоссийскийИнвестор) Тогда
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаТаблицыИнвестиций.РоссийскийИнвестор, "НаименованиеПолное, НаименованиеИнострОрганизации, КодВСтранеРегистрации, ИНН");
		Контейнер.Титульный.НаимМГ = Реквизиты.НаименованиеПолное;
		Контейнер.Титульный.НаимМГЛат = ?(ПустаяСтрока(Реквизиты.НаименованиеИнострОрганизации), СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(Реквизиты.НаименованиеПолное), Реквизиты.НаименованиеИнострОрганизации);
		Контейнер.Титульный.НомРосМатК = Реквизиты.ИНН;
		Материнская = СтрокаТаблицыИнвестиций.РоссийскийИнвестор;
	Иначе
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "НаименованиеПолное, НаименованиеИнострОрганизации, КодВСтранеРегистрации, ИНН");
		Контейнер.Титульный.НаимМГ = Реквизиты.НаименованиеПолное;
		Контейнер.Титульный.НаимМГЛат = ?(ПустаяСтрока(Реквизиты.НаименованиеИнострОрганизации), СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(Реквизиты.НаименованиеПолное), Реквизиты.НаименованиеИнострОрганизации);
		СтатусУчастника = "1";
		Материнская = Организация;
	КонецЕсли;
	
	ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(Организация, ДатаКонцаПериодаОтчета);
	Если ОтветственныеЛица.Свойство("ОтветственныйЗаНалоговыеРегистры") Тогда
		Контейнер.Титульный.ДолжнПодп = ОтветственныеЛица.ОтветственныйЗаНалоговыеРегистрыДолжностьПредставление;
		Контейнер.Титульный.ОргПодписант = ОтветственныеЛица.ОтветственныйЗаНалоговыеРегистры;
		
		ЭлПочтаПодп = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ОтветственныеЛица.ОтветственныйЗаНалоговыеРегистры, Справочники.ВидыКонтактнойИнформации.EMailФизическиеЛица);
		ТлфПодп = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ОтветственныеЛица.ОтветственныйЗаНалоговыеРегистры, Справочники.ВидыКонтактнойИнформации.ТелефонРабочийФизическиеЛица);
		
		Если Контейнер.Титульный.Свойство("КонтИнф") Тогда
			Контейнер.Титульный.КонтИнф = "" + ЭлПочтаПодп + ", " + ТлфПодп;
		КонецЕсли;
		Если Контейнер.Титульный.Свойство("ЭлПочтаПодп") Тогда
			Контейнер.Титульный.ЭлПочтаПодп = ЭлПочтаПодп;
		КонецЕсли;
		Если Контейнер.Титульный.Свойство("ТлфПодп") Тогда
			Контейнер.Титульный.ТлфПодп = ТлфПодп;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьТаблицуПоказателейРНБВП(Период)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА НЕ ДанныеРасчетаПоказателейРНБВП.Организация.ИностранныйНалоговыйРезидент
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
		|		ИНАЧЕ ДанныеРасчетаПоказателейРНБВП.Организация.СтранаРегистрации
		|	КОНЕЦ КАК Страна,
		|	СУММА(ДанныеРасчетаПоказателейРНБВП.Прибыль) КАК Прибыль,
		|	СУММА(ДанныеРасчетаПоказателейРНБВП.Активы) КАК Активы,
		|	СУММА(ДанныеРасчетаПоказателейРНБВП.НалогНаПрибыль) КАК НалогНаПрибыль,
		|	СУММА(ДанныеРасчетаПоказателейРНБВП.EBITDA) КАК EBITDA,
		|	СУММА(ДанныеРасчетаПоказателейРНБВП.РасходыПоПроцентам) КАК РасходыПоПроцентам,
		|	СУММА(ДанныеРасчетаПоказателейРНБВП.Капитал) КАК Капитал,
		|	СУММА(ДанныеРасчетаПоказателейРНБВП.ДобавочныйКапитал) КАК ДобавочныйКапитал,
		|	СУММА(ДанныеРасчетаПоказателейРНБВП.НакопленнаяПрибыль) КАК НакопленнаяПрибыль,
		|	СУММА(ДанныеРасчетаПоказателейРНБВП.Доход) КАК Доход,
		|	СУММА(ДанныеРасчетаПоказателейРНБВП.ЧисленностьРаботников) КАК ЧисленностьРаботников
		|ИЗ
		|	РегистрСведений.ДанныеРасчетаПоказателейРНБВП КАК ДанныеРасчетаПоказателейРНБВП
		|ГДЕ
		|	ДанныеРасчетаПоказателейРНБВП.Период = &Период
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА НЕ ДанныеРасчетаПоказателейРНБВП.Организация.ИностранныйНалоговыйРезидент
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
		|		ИНАЧЕ ДанныеРасчетаПоказателейРНБВП.Организация.СтранаРегистрации
		|	КОНЕЦ";
	
	Запрос.УстановитьПараметр("Период", НачалоГода(Период));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

Функция СтрановойОтчетЗаполнитьСтруктуруДанныхРазделПоОрганизации(Суффикс, Организация, СтатусУчастника, АдресаОрганизаций)
	Перем Регион, Город, НаселПункт, Район, Улица, Дом, Корпус, Квартира, АбЯщик, Индекс, КодРегиона;
	СтрокаАдреса = АдресаОрганизаций.Найти(Организация, "Объект");
	Если СтрокаАдреса <> Неопределено Тогда
		Представление = СтрокаАдреса.Представление;
		СтруктураАдреса = РаботаСАдресами.СведенияОбАдресе(СтрокаАдреса.ЗначенияПолей);
	Иначе
		Представление = "";
		СтруктураАдреса = Новый Структура;
	КонецЕсли;
	СтруктураАдреса.Свойство("Регион", Регион);
	СтруктураАдреса.Свойство("Город", Город);
	СтруктураАдреса.Свойство("НаселенныйПункт", НаселПункт);
	СтруктураАдреса.Свойство("Район", Район);
	СтруктураАдреса.Свойство("Улица", Улица);
	СтруктураАдреса.Свойство("Дом", Дом);
	СтруктураАдреса.Свойство("Корпус", Корпус);
	СтруктураАдреса.Свойство("Квартира", Квартира);
	СтруктураАдреса.Свойство("АбЯщик", АбЯщик);
	СтруктураАдреса.Свойство("Индекс", Индекс);
	СтруктураАдреса.Свойство("КодРегиона", КодРегиона);
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, Новый Структура("Наименование, КодАльфа2, КодВСтранеРегистрации, ОГРН, ИНН, КодНалогоплательщикаИностранный", "Наименование", "СтранаРегистрации.КодАльфа2", "КодВСтранеРегистрации", "ОГРН", "ИНН", "КодНалогоплательщикаИностранный"));
	Возврат СтрановойОтчетПолучитьСтруктуруДанныхРаздел(Суффикс, Реквизиты.Наименование, ?(НЕ ЗначениеЗаполнено(Реквизиты.КодАльфа2), "RU", Реквизиты.КодАльфа2), ?(ПустаяСтрока(Реквизиты.КодВСтранеРегистрации), Реквизиты.ОГРН, Реквизиты.КодВСтранеРегистрации), СтатусУчастника, Регион, Город, НаселПункт, Район, Улица, Дом, Корпус, Квартира, Представление, АбЯщик, Индекс, КодРегиона, Реквизиты.ИНН, Реквизиты.КодНалогоплательщикаИностранный);
КонецФункции

Функция СтрановойОтчетПолучитьСтруктуруДанныхРаздел(Суффикс, НаимУч, СтрРегНом, РегНом, СтатусУчастника, Регион, Город, НаселПункт, Район, Улица, Дом, Корпус, Квартира, АдрИной, АбЯщик, Индекс, КодРегиона, ИНН, ИННиН)
	Данные = Новый Структура;
	
	Данные.Вставить("СтатУчМГ" 		+ Суффикс,		СтатусУчастника);
	Данные.Вставить("ПрУчМГ" 		+ Суффикс,		"1");
	Данные.Вставить("НомКор" 		+ Суффикс,		"0");  //1
	Данные.Вставить("ТипАдрес" 		+ Суффикс,		"OECD301");
	Данные.Вставить("РегНом" 		+ Суффикс,		РегНом);
	Данные.Вставить("РегНомЛат" 	+ Суффикс,		СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(РегНом)); //2
	Данные.Вставить("ТипРегНомЛат" 	+ Суффикс,		?(СтрРегНом = "RU", "OGRN", "Registracionnjy nomer"));
	Данные.Вставить("ТипРегНом" 	+ Суффикс,		?(СтрРегНом = "RU", "ОГРН", "Регистрационный номер"));
	Данные.Вставить("РегионЛат" 	+ Суффикс,		СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(Регион));
	Данные.Вставить("Регион"		+ Суффикс,		Регион);
	Данные.Вставить("ГородЛат"		+ Суффикс,		СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(Город));
	Данные.Вставить("Город"			+ Суффикс,		Город);
	Данные.Вставить("НаселПунктЛат" + Суффикс,		СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(НаселПункт));
	Данные.Вставить("НаселПункт"	+ Суффикс,		НаселПункт);
	Данные.Вставить("РайонЛат"		+ Суффикс,		СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(Улица));
	Данные.Вставить("Район"			+ Суффикс,		Улица);
	Данные.Вставить("УлицаЛат"		+ Суффикс,		СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(Улица));
	Данные.Вставить("Улица"			+ Суффикс,		Улица);
	Данные.Вставить("Дом"			+ Суффикс,		Дом);
	Данные.Вставить("Корпус"		+ Суффикс,		Корпус);
	Данные.Вставить("Квартира"		+ Суффикс,		Квартира);
	Данные.Вставить("АдрИнойЛат"	+ Суффикс,		СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(АдрИной));
	Данные.Вставить("АдрИной"		+ Суффикс,		АдрИной);
	
	Данные.Вставить("НаимУчЛат"		+ Суффикс,		СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(НаимУч));
	
	Данные.Вставить("АбЯщик"		+ Суффикс,		АбЯщик);
	Данные.Вставить("ИндексРос"		+ Суффикс, 		Индекс);
	Данные.Вставить("КодРегиона"	+ Суффикс,		КодРегиона);
	Данные.Вставить("СтрРегНом"		+ Суффикс,		СтрРегНом);

	Если Суффикс = "Р21" Тогда
		Данные.Вставить("СтрАдрР21",				СтрРегНом);
		Данные.Вставить("НаимУчР21",		 		НаимУч);
		Данные.Вставить("НомНПР21",					ИННиН);
		Данные.Вставить("ИННЮЛР21",					ИНН);
		Данные.Вставить("СтрНомНПР21",				СтрРегНом);
		Данные.Вставить("ИндексИнР21",				"    ");
		Данные.Вставить("СтрРегИнкР21",				СтрРегНом);
		Данные.Вставить("ДопИнфУчИнЯзР21",			"");
		Данные.Вставить("ДопИнфУчР21",				"");
		Данные.Вставить("ПрНПР21",					"");
	КонецЕсли;
	
	Возврат Данные;
КонецФункции

Функция СтрановойОтчетПолучитьСтруктуруДанныхРаздел20(КодСтраныРегистрации, СтрокаПоказателей)
	Данные = Новый Структура;
	Данные.Вставить("ДопИнфЧислРабИнЯзР2", "");
	Данные.Вставить("ДопИнфЧислРабР2", "на конец отчетного периода");
	Данные.Вставить("КодВалР2", "RUB");
	Данные.Вставить("НомКорР2", 0);
	Данные.Вставить("П000020001002", ?(СтрокаПоказателей = Неопределено, 0, СтрокаПоказателей.Доход));
	Данные.Вставить("П000020001003", "");
	Данные.Вставить("П000020001004", "");
	Данные.Вставить("П000020002002", 0);
	Данные.Вставить("П000020002003", "");
	Данные.Вставить("П000020002004", "");
	Данные.Вставить("П000020003002", ?(СтрокаПоказателей = Неопределено, 0, СтрокаПоказателей.Доход));
	Данные.Вставить("П000020003003", "");
	Данные.Вставить("П000020003004", "");
	Данные.Вставить("П000020004002", ?(СтрокаПоказателей = Неопределено, 0, СтрокаПоказателей.Прибыль));
	Данные.Вставить("П000020004003", "");
	Данные.Вставить("П000020004004", "");
	Данные.Вставить("П000020005002", ?(СтрокаПоказателей = Неопределено, 0, СтрокаПоказателей.НалогНаПрибыль));
	Данные.Вставить("П000020005003", "");
	Данные.Вставить("П000020005004", "");
	Данные.Вставить("П000020006002", ?(СтрокаПоказателей = Неопределено, 0, СтрокаПоказателей.НалогНаПрибыль));
	Данные.Вставить("П000020006003", "");
	Данные.Вставить("П000020006004", "");
	Данные.Вставить("П000020007002", ?(СтрокаПоказателей = Неопределено, 0, СтрокаПоказателей.Капитал));
	Данные.Вставить("П000020007003", "");
	Данные.Вставить("П000020007004", "");
	Данные.Вставить("П000020008002", ?(СтрокаПоказателей = Неопределено, 0, СтрокаПоказателей.ДобавочныйКапитал));
	Данные.Вставить("П000020008003", "");
	Данные.Вставить("П000020008004", "");
	Данные.Вставить("П000020009002", ?(СтрокаПоказателей = Неопределено, 0, СтрокаПоказателей.НакопленнаяПрибыль));
	Данные.Вставить("П000020009003", "");
	Данные.Вставить("П000020009004", "");
	Данные.Вставить("П000020010002", ?(СтрокаПоказателей = Неопределено, 0, СтрокаПоказателей.Активы));
	Данные.Вставить("П000020010003", "");
	Данные.Вставить("П000020010004", "");
	Данные.Вставить("ПрНалРезидР2", "");
	Данные.Вставить("СтрНалРезидР2", КодСтраныРегистрации);
	Данные.Вставить("ЧислРабВсегоР2", ?(СтрокаПоказателей = Неопределено, 0, СтрокаПоказателей.ЧисленностьРаботников));
	Данные.Вставить("ЧислРабНезР2", 0);
	Данные.Вставить("ЧислРабШтатР2", ?(СтрокаПоказателей = Неопределено, 0, СтрокаПоказателей.ЧисленностьРаботников));
	Возврат Данные;
КонецФункции

Функция СтрановойОтчетПолучитьДеревоВидовЭкономическойДейтельности(МассивВидов)
	
	Если ТипЗнч(МассивВидов) = Тип("Строка") Тогда
		Итератор = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(МассивВидов, , , Истина);
	Иначе
		Итератор = МассивВидов;
	КонецЕсли;
	
	Дерево = Новый ДеревоЗначений;
	Дерево.Колонки.Добавить("Данные", Новый ОписаниеТипов("Структура"));
	Дерево.Колонки.Добавить("ДанныеМногострочныхЧастей", Новый ОписаниеТипов("Структура"));
	
	Для каждого Эл Из Итератор Цикл
		ДанныеСтруктуры = Новый Структура("П00021М100001", Эл);
		СтрокаДерева = Дерево.Строки.Добавить();
		СтрокаДерева.Данные = ДанныеСтруктуры;
	КонецЦикла;
	
	Возврат Новый Структура("П00021М1", Дерево);
	
КонецФункции

#КонецОбласти

#Область ЗаполнениеУчастиеВМеждународнойГруппеКомпаний

Процедура ИнициализироватьСтруктуры(Контейнер, Данные1, Данные2)
	Для каждого Элемент Из Контейнер.Раздел1.Строки[0].Данные Цикл
		Данные1.Вставить(Элемент.Ключ);
	КонецЦикла;
	
	Для каждого Элемент Из Контейнер.Раздел2.Строки[0].Данные Цикл
		Данные2.Вставить(Элемент.Ключ);
	КонецЦикла;
	Контейнер.Раздел1.Строки.Очистить();
	Контейнер.Раздел2.Строки.Очистить();
КонецПроцедуры

Процедура ДополнитьПустыеДеревья(Контейнер, Данные1, Данные2)
		
	Если Контейнер.Раздел1.Строки.Количество() = 0 Тогда
		НоваяСтрока = Контейнер.Раздел1.Строки.Добавить();
		НоваяСтрока.ДанныеМногострочныхЧастей = Новый Структура;
		НоваяСтрока.Данные = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(Данные1);
	КонецЕсли;
	
	Если Контейнер.Раздел2.Строки.Количество() = 0 Тогда
		НоваяСтрока = Контейнер.Раздел2.Строки.Добавить();
		НоваяСтрока.ДанныеМногострочныхЧастей = Новый Структура;
		НоваяСтрока.Данные = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(Данные2);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ПрочиеПроцедурыИФункции

Функция ПолучитьЗапросИнвестиций(Период, Организация)
	Запрос = Новый Запрос;
	ПоляЗапроса = Новый Структура;
	ПоляЗапроса.Вставить("Организация", "Организация");
	ПоляЗапроса.Вставить("РоссийскийИнвестор", "РоссийскийИнвестор");
	ПоляЗапроса.Вставить("ИностранныйИнвестор", "ИностранныйИнвестор");
	ПоляЗапроса.Вставить("ОбъектИнвестирования", "ОбъектИнвестирования");
	ПоляЗапроса.Вставить("СтранаРегистрации", "Страна");
	ПоляЗапроса.Вставить("КодСтраныРегистрации", "Страна.Код");
	ПоляЗапроса.Вставить("КодАльфа2", "Страна.КодАльфа2");
	ПоляОтбора = Новый Структура;
	ПоляОтбора.Вставить("Организация", Организация);
	Запрос.Текст = ПолучитьТекстЗапросаМСО(ПоляЗапроса, ПоляОтбора);
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Параметр1", Организация);
	Возврат Запрос;
КонецФункции

Функция ПолучитьТаблицуАдресовОрганизаций(СписокИнвестиций, Организация, Дата)
	МассивОрганизацийДляАдресов = СписокИнвестиций.ВыгрузитьКолонку("ОбъектИнвестирования");
	МассивОрганизацийДляАдресов.Добавить(Организация);
	Виды = Новый Массив;
	Виды.Добавить(ВстраиваниеУХ.ВидыКонтактнойИнформацииИО());
	Виды.Добавить(Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
	АдресаОрганизаций = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(МассивОрганизацийДляАдресов, Перечисления.ТипыКонтактнойИнформации.Адрес, Виды, Дата);
	Возврат АдресаОрганизаций;
КонецФункции

#КонецОбласти

#КонецЕсли
