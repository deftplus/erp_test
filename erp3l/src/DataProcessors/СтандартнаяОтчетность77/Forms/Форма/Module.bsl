
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Параметры.АдресХранилищаПеременныхДляРасчета) Тогда
		ВызватьИсключение НСтр("ru = 'Обработка не предназначена для непосредственного использования.'");
	КонецЕсли;
	
	ДанныеРасшифровки=ПолучитьИзВременногоХранилища(Параметры.АдресХранилищаПеременныхДляРасчета);
	РабочийОбъект=РеквизитФормыВЗначение("Объект");
	ЗаполнитьЗначенияСвойств(РабочийОбъект,ДанныеРасшифровки);
	
	ИспользуемаяИБ=РабочийОбъект.ОбъектРасчета.ИспользуемаяИБ;
	РабочийОбъект.НаименованиеФормы="";	
	
	Если НЕ ПустаяСтрока(Параметры.АдресРасшифровки) Тогда
		
		ДанныеДляВывода=ОбработкаРасшифровкиСервер(РабочийОбъект,ПолучитьИзВременногоХранилища(Параметры.АдресРасшифровки));
		
	Иначе
		
		ДанныеДляВывода=РабочийОбъект.ПолучитьТаблицуОтчета();
		
	КонецЕсли;
	
	Если ЭтаФорма.АвтоЗаголовок И (НЕ ПустаяСтрока(РабочийОбъект.НаименованиеФормы)) Тогда
		
		ЭтаФорма.АвтоЗаголовок=Ложь;
		ЭтаФорма.Заголовок=РабочийОбъект.НаименованиеФормы;
		
	КонецЕсли;
	
	ТаблицаОтчета.Вывести(ДанныеДляВывода);
	
	РабочийОбъектАдрес=ПоместитьВоВременноеХранилище(РабочийОбъект.ПодготовитьСтруктуруПеременныхДляРасчета(),ЭтаФорма.УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаОтчетаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	СтандартнаяОбработка=Ложь;
	
	Если НЕ ТипЗнч(Расшифровка)=Тип("Структура") Тогда
		
		Возврат;
		
	КонецЕсли;
			
	ОткрытьФорму("Обработка.СтандартнаяОтчетность77.Форма",Новый Структура("АдресХранилищаПеременныхДляРасчета,АдресРасшифровки",РабочийОбъектАдрес,ПоместитьВоВременноеХранилище(Расшифровка,ЭтаФорма.УникальныйИдентификатор)),,Новый УникальныйИдентификатор);
			
КонецПроцедуры

&НаСервере
Функция ОбработкаРасшифровкиСервер(ОбработкаОбъект,Расшифровка)
	
	Перем Счет;
	Перем КоррСчет;
	
	Если Расшифровка.Свойство("ТипОбъектаМетаданных") Тогда
		
		Расшифровка.Вставить("ИспользуемаяИБ",ИспользуемаяИБ);
		
		ОбработкаОтображения=Обработки.ПолучениеОтчетностиВИБ.Создать();
		
		НаименованиеФормы="";
		
		ОтображениеОбъекта=ОбработкаОтображения.ПолучитьОтображениеОтчета(ОбработкаОбъект.СтруктураЗапроса.База,Расшифровка,НаименованиеФормы);
		
		Если Не ПустаяСтрока(НаименованиеФормы) Тогда
			
			ЭтаФорма.АвтоЗаголовок=Ложь;
			ЭтаФорма.Заголовок=НаименованиеФормы;
			
		КонецЕсли;
		
		Возврат ОтображениеОбъекта;
	
	ИначеЕсли Расшифровка.Свойство("ДтКт") Тогда
		
		Если Расшифровка.ДтКт="Дт" Тогда 
			
			Расшифровка.Свойство("Счет",Счет);
			Расшифровка.Свойство("КоррСчет",КоррСчет);
			
		Иначе
			
			Расшифровка.Свойство("Счет",КоррСчет);
			Расшифровка.Свойство("КоррСчет",Счет);
			
		КонецЕсли;
		
		ОбработкаОбъект.СтруктураЗапроса.СписокСчетов.Очистить();
		ОбработкаОбъект.СтруктураЗапроса.СписокСчетов.Добавить(Счет);
		
		ОбработкаОбъект.СтруктураЗапроса.СписокКоррСчетов.Очистить();
		ОбработкаОбъект.СтруктураЗапроса.СписокКоррСчетов.Добавить(КоррСчет);
		
		ТаблицаОтчета.Очистить();
		Возврат ОбработкаОбъект.ВывестиОтчетПоПроводкам();
		
	ИначеЕсли Расшифровка.Свойство("СтрокаРегистра") Тогда
		
		СписокДопОтборов=Новый СписокЗначений;
		
		Для Каждого КлючИЗначение ИЗ Расшифровка Цикл
			
			Если КлючИЗначение.Ключ="СтрокаРегистра" ИЛИ КлючИЗначение.Ключ="Ресурс" Тогда
				Продолжить;
			КонецЕсли;
			
			ОбъектАналитики=ОбработкаОбъект.СтруктураЗапроса.База.CreateObject(КлючИЗначение.Значение.Тип+"."+КлючИЗначение.Значение.Вид);
			ОбъектАналитики.НайтиПоКоду(КлючИЗначение.Значение.Код);
			
			Если ОбъектАналитики.Выбран() Тогда
				
				СписокДопОтборов.Добавить(ОбъектАналитики.ТекущийЭлемент(), КлючИЗначение.Ключ);
					
			КонецЕсли;
			
		КонецЦикла;
		
		ТаблицаОтчета.Очистить();
		Возврат ОбработкаОбъект.ВывестиИтогиПоРегистру(ОбработкаОбъект.СтруктураИтоги,СписокДопОтборов);
		
	КонецЕсли;
	
		
КонецФункции // ОбработкаРасшифровкиСервер()


