
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СтруктураПараметровДинамическогоСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	СтруктураПараметровДинамическогоСписка.Вставить("ТекстЗапроса", ПретензионноИсковаяРабота.ТекстЗапросаПросроченнаяЗадолженность(Ложь));
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.ПросроченнаяЗадолженность, СтруктураПараметровДинамическогоСписка);
	ПросроченнаяЗадолженность.Параметры.УстановитьЗначениеПараметра("ДатаДляГенерации", КонецДня(ТекущаяДатаСеанса()));
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗакрытьПретензию(Команда)
	ОткрытьФорму("Обработка.ВзысканиеДебиторскойЗадолженности.Форма.ФормаЗакрытия", Новый Структура("Претензия", Элементы.НеЗакрытыеПретензии.ТекущиеДанные.Претензия), ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПретензию(Команда)
	ТД = Элементы.ПросроченнаяЗадолженность.ТекущиеДанные;
	Если ТД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("Организация", ТД.Организация);
	ДанныеЗаполнения.Вставить("Контрагент", ТД.Контрагент);
	ДанныеЗаполнения.Вставить("Договор", ТД.Договор);
	ДанныеЗаполнения.Вставить("ОбъектРасчетов", ТД.ОбъектРасчетов);
	ДанныеЗаполнения.Вставить("РасчетныйДокумент", ТД.РасчетныйДокумент);
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Основание", ДанныеЗаполнения);
	
	ОткрытьФорму("Документ.ИзменениеПретензии.ФормаОбъекта", ДопПараметры, ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЭтап(Команда)
	Если Элементы.НеЗакрытыеПретензии.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ОткрытьФорму("Справочник.Претензии.ФормаОбъекта", Новый Структура("Ключ", Элементы.НеЗакрытыеПретензии.ТекущиеДанные.Претензия));
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура ПросроченнаяЗадолженностьВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТД = Элементы.ПросроченнаяЗадолженность.ТекущиеДанные;
	Если ТД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	
	Если Поле = Элементы.ПросроченнаяЗадолженностьРасчетныйДокумент Тогда
		ЗначениеДляПоказа = ТД.РасчетныйДокумент;
	ИначеЕсли Поле = Элементы.ПросроченнаяЗадолженностьОбъектРасчетов Тогда
		ЗначениеДляПоказа = ТД.ОбъектРасчетов;
	ИначеЕсли Поле = Элементы.ПросроченнаяЗадолженностьДоговор Тогда
		ЗначениеДляПоказа = ТД.Договор;
	ИначеЕсли Поле = Элементы.ПросроченнаяЗадолженностьКонтрагент Тогда
		ЗначениеДляПоказа = ТД.Контрагент;
	ИначеЕсли Поле = Элементы.ПросроченнаяЗадолженностьОрганизация Тогда
		ЗначениеДляПоказа = ТД.Организация;
	Иначе
		ЗначениеДляПоказа = неопределено;
	КонецЕсли;
	
	Если ЗначениеДляПоказа <> неопределено Тогда
		ПоказатьЗначение(, ЗначениеДляПоказа);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НеЗакрытыеПретензииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Элементы.НеЗакрытыеПретензии.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Справочник.Претензии.ФормаОбъекта", Новый Структура("Ключ", Элементы.НеЗакрытыеПретензии.ТекущиеДанные.Претензия));
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	Элементы.ПросроченнаяЗадолженность.Обновить();
	Элементы.НеЗакрытыеПретензии.Обновить();
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ИзменениеПретензии" Тогда
		Элементы.ПросроченнаяЗадолженность.Обновить();
		Элементы.НеЗакрытыеПретензии.Обновить();
	КонецЕсли;	
	
КонецПроцедуры

