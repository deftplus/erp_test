
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
			
	ЗначениеВРеквизитФормы(ОбщегоНазначенияУХ.ПолучитьЗначениеПеременной("глТаблицаЯзыков"),"ДополнительныеЯзыки");	
	
	УстановитьЗаголовокДополнительныеЯзыки(ЭтотОбъект);
	
 КонецПроцедуры
 
&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Константы.ДополнительныеЯзыкиВыводаОтчета.Установить(Новый ХранилищеЗначения(ДополнительныеЯзыки.Выгрузить()));
	Константы.ИспользоватьДополнительныеЯзыкиПредставления.Установить(ДополнительныеЯзыки.Количество()>0);
	
	оПлан = Справочники.Сценарии.План.ПолучитьОбъект();
	оПлан.Записать();

	оРезерв = Справочники.Сценарии.Резерв.ПолучитьОбъект();
	оРезерв.Записать();
	
	ОбновитьПовторноИспользуемыеЗначения();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ОбновитьИнтерфейс();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДополнительныеЯзыки

&НаКлиенте
Процедура ДополнительныеЯзыкиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если ДополнительныеЯзыки.Количество() = 2 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеЯзыкиПослеУдаления(Элемент)
	
	Если ДополнительныеЯзыки.Количество() > 0 Тогда
		ДополнительныеЯзыки[0].ПорядковыйНомер = 1;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеЯзыкиПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если НоваяСтрока Тогда
		ТекущиеДанные = Элементы.ДополнительныеЯзыки.ДанныеСтроки(Элементы.ДополнительныеЯзыки.ТекущаяСтрока);
		Индекс        = ДополнительныеЯзыки.Индекс(ТекущиеДанные);
		ТекущиеДанные.ПорядковыйНомер = Индекс + 1;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеЯзыкиПриИзменении(Элемент)
	УстановитьЗаголовокДополнительныеЯзыки(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьМатрицуПолномочий(Команда)
	
	ОткрытьФорму("Обработка.НастройкаМатрицыПолномочий.Форма", Новый Структура("ВерсияОрганизационнойСтруктуры", Неопределено));

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПравилаЗаполнения(Команда)
	
	ПараметрыФормы = Новый Структура("ВерсияОрганизационнойСтруктуры", Неопределено);
	ОткрытьФорму("Обработка.НастройкаПараметровОтчетовПоУмолчанию.Форма", ПараметрыФормы);

КонецПроцедуры

&НаКлиенте
Процедура ТекущаяИнформационнаяБаза(Команда)
	
	ПараметрыФормы = Новый Структура("Ключ", ПредопределенноеЗначение("Справочник.ТипыБазДанных.ТекущаяИБ"));
	ОткрытьФорму("Справочник.ТипыБазДанных.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокДополнительныеЯзыки(Форма)
	
	МассивЯзыков = Новый Массив;
	Для Каждого ТекЯзык Из Форма.ДополнительныеЯзыки Цикл
		МассивЯзыков.Добавить(ТекЯзык.КодЯзыка);
	КонецЦикла;
    СтрокаЯзыки = СтрСоединить(МассивЯзыков, ",");
	
	ТекстЗаголовка = НСтр("ru = 'Дополнительные языки'");	
	Если Не ПустаяСтрока(СтрокаЯзыки) Тогда
		ТекстЗаголовка = ТекстЗаголовка + СтрШаблон(" (%1)", СтрокаЯзыки);
	КонецЕсли;
	УстановитьЗаголовокГруппы(Форма,"Языки", ТекстЗаголовка);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокГруппы(Форма, НазваниеГруппы, ЗаголовокТекст)
	
	Форма.Элементы[НазваниеГруппы].ЗаголовокСвернутогоОтображения = ЗаголовокТекст;
	
КонецПроцедуры

#КонецОбласти










