Процедура СформироватьОтчетПоПоказателям(ДиаграммаГанта, ДанныеОбъекта, Сценарий) Экспорт
	
	ЗапросКонстанты=Новый Запрос;
	ЗапросКонстанты.Текст="ВЫБРАТЬ
	|	ВалютаРасчетаПоказателейПроекта.Значение КАК ВалютаРасчетаПоказателейПроекта,
	|	ИтоговыйПоказательПоступленияБДДС.Значение КАК ИтоговыйПоказательПоступленияБДДС,
	|	ИтоговыйПоказательСписанияБДДС.Значение КАК ИтоговыйПоказательСписанияБДДС,
	|	ИтоговыйПоказательДоходыБДР.Значение КАК ИтоговыйПоказательДоходыБДР,
	|	ИтоговыйПоказательРасходыБДР.Значение КАК ИтоговыйПоказательРасходыБДР,
	|	ИтоговыйПоказательПоступленияРесурсы.Значение КАК ИтоговыйПоказательПоступленияРесурсы,
	|	ИтоговыйПоказательСписанияРесурсы.Значение КАК ИтоговыйПоказательСписанияРесурсы,
	|	ЕСТЬNULL(ГруппаРаскрытияДоходыБДР.ЧислоАналитик, 0) КАК ЧислоАналитикДоходыБДР,
	|	ЕСТЬNULL(ГруппаРаскрытияДоходыБДР.АналитикаЭтапПроекта, 0) КАК АналитикаЭтапПроектаДоходыБДР,
	|	ЕСТЬNULL(ГруппаРаскрытияПоступленияБДДС.ЧислоАналитик, 0) КАК ЧислоАналитикПоступленияБДДС,
	|	ЕСТЬNULL(ГруппаРаскрытияПоступленияБДДС.АналитикаЭтапПроекта, 0) КАК АналитикаЭтапПроектаПоступленияБДДС,
	|	ЕСТЬNULL(ГруппаРаскрытияПоступленияРесурсы.ЧислоАналитик, 0) КАК ЧислоАналитикПоступленияРесурсы,
	|	ЕСТЬNULL(ГруппаРаскрытияПоступленияРесурсы.АналитикаЭтапПроекта, 0) КАК АналитикаЭтапПроектаПоступленияРесурсы,
	|	ЕСТЬNULL(ГруппаРаскрытияРасходыБДР.ЧислоАналитик, 0) КАК ЧислоАналитикРасходыБДР,
	|	ЕСТЬNULL(ГруппаРаскрытияРасходыБДР.АналитикаЭтапПроекта, 0) КАК АналитикаЭтапПроектаРасходыБДР,
	|	ЕСТЬNULL(ГруппаРаскрытияСписанияБДДС.ЧислоАналитик, 0) КАК ЧислоАналитикСписанияБДДС,
	|	ЕСТЬNULL(ГруппаРаскрытияСписанияБДДС.АналитикаЭтапПроекта, 0) КАК АналитикаЭтапПроектаСписанияБДДС,
	|	ЕСТЬNULL(ГруппаРаскрытияСписанияРесурсы.ЧислоАналитик, 0) КАК ЧислоАналитикСписанияРесурсы,
	|	ЕСТЬNULL(ГруппаРаскрытияСписанияРесурсы.АналитикаЭтапПроекта, 0) КАК АналитикаЭтапПроектаСписанияРесурсы
	|ИЗ
	|	Константа.ВалютаРасчетаПоказателейПроекта КАК ВалютаРасчетаПоказателейПроекта,
	|	Константа.ИтоговыйПоказательПоступленияБДДС КАК ИтоговыйПоказательПоступленияБДДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыРаскрытия КАК ГруппаРаскрытияПоступленияБДДС
	|		ПО ИтоговыйПоказательПоступленияБДДС.Значение.ГруппаРаскрытия = ГруппаРаскрытияПоступленияБДДС.Ссылка,
	|	Константа.ИтоговыйПоказательСписанияБДДС КАК ИтоговыйПоказательСписанияБДДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыРаскрытия КАК ГруппаРаскрытияСписанияБДДС
	|		ПО ИтоговыйПоказательСписанияБДДС.Значение.ГруппаРаскрытия = ГруппаРаскрытияСписанияБДДС.Ссылка,
	|	Константа.ИтоговыйПоказательДоходыБДР КАК ИтоговыйПоказательДоходыБДР
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыРаскрытия КАК ГруппаРаскрытияДоходыБДР
	|		ПО ИтоговыйПоказательДоходыБДР.Значение.ГруппаРаскрытия = ГруппаРаскрытияДоходыБДР.Ссылка,
	|	Константа.ИтоговыйПоказательРасходыБДР КАК ИтоговыйПоказательРасходыБДР
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыРаскрытия КАК ГруппаРаскрытияРасходыБДР
	|		ПО ИтоговыйПоказательРасходыБДР.Значение.ГруппаРаскрытия = ГруппаРаскрытияРасходыБДР.Ссылка,
	|	Константа.ИтоговыйПоказательПоступленияРесурсы КАК ИтоговыйПоказательПоступленияРесурсы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыРаскрытия КАК ГруппаРаскрытияПоступленияРесурсы
	|		ПО ИтоговыйПоказательПоступленияРесурсы.Значение.ГруппаРаскрытия = ГруппаРаскрытияПоступленияРесурсы.Ссылка,
	|	Константа.ИтоговыйПоказательСписанияРесурсы КАК ИтоговыйПоказательСписанияРесурсы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыРаскрытия КАК ГруппаРаскрытияСписанияРесурсы
	|		ПО ИтоговыйПоказательСписанияРесурсы.Значение.ГруппаРаскрытия = ГруппаРаскрытияСписанияРесурсы.Ссылка";
	
	ЗначенияКонстант=ЗапросКонстанты.Выполнить().Выбрать();
	ЗначенияКонстант.Следующий();
	
	ПоказателиПроектов=Новый СписокЗначений;
	ПоказателиПроектов.Добавить(ЗначенияКонстант.ИтоговыйПоказательПоступленияБДДС);
	ПоказателиПроектов.Добавить(ЗначенияКонстант.ИтоговыйПоказательСписанияБДДС);
	ПоказателиПроектов.Добавить(ЗначенияКонстант.ИтоговыйПоказательДоходыБДР);
	ПоказателиПроектов.Добавить(ЗначенияКонстант.ИтоговыйПоказательРасходыБДР);
	ПоказателиПроектов.Добавить(ЗначенияКонстант.ИтоговыйПоказательПоступленияРесурсы);
	ПоказателиПроектов.Добавить(ЗначенияКонстант.ИтоговыйПоказательСписанияРесурсы);
	
	Запрос=Новый Запрос;
	Запрос.МенеджерВременныхТаблиц=Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ПоказателиПроектов",ПоказателиПроектов);
	Запрос.УстановитьПараметр("ВалютаУпрУчета",ЗначенияКонстант.ВалютаРасчетаПоказателейПроекта);
	Запрос.УстановитьПараметр("ПоказательБДДСПриход",ЗначенияКонстант.ИтоговыйПоказательПоступленияБДДС);
	Запрос.УстановитьПараметр("ПоказательБДДСРасход",ЗначенияКонстант.ИтоговыйПоказательСписанияБДДС);
	Запрос.УстановитьПараметр("ПоказательБДРПриход",ЗначенияКонстант.ИтоговыйПоказательДоходыБДР);
	Запрос.УстановитьПараметр("ПоказательБДРРасход",ЗначенияКонстант.ИтоговыйПоказательРасходыБДР);
	Запрос.УстановитьПараметр("ПоказательРесурсыПриход",ЗначенияКонстант.ИтоговыйПоказательПоступленияРесурсы);
	Запрос.УстановитьПараметр("ПоказательРесурсыРасход",ЗначенияКонстант.ИтоговыйПоказательСписанияРесурсы);
	
	Запрос.УстановитьПараметр("Проект",ДанныеОбъекта.Проект);
		
	Запрос.Текст=
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗначенияПоказателейОтчетовСинтетика.Версия,
	|	ЗначенияПоказателейОтчетовСинтетика.Версия.ПериодОтчета.ДатаНачала КАК ПериодОтчетаДатаНачала,
	|	ЗначенияПоказателейОтчетовСинтетика.Версия.ПериодОтчета.ДатаОкончания КАК ПериодОтчетаДатаОкончания,
	|	ЗначенияПоказателейОтчетовСинтетика.Версия.Проект КАК Проект
	|ПОМЕСТИТЬ РабочиеВерсии
	|ИЗ
	|	РегистрСведений.ЗначенияПоказателейОтчетовСинтетика КАК ЗначенияПоказателейОтчетовСинтетика
	|ГДЕ
	|	ЗначенияПоказателейОтчетовСинтетика.Версия.Валюта = &ВалютаУпрУчета
	|	И ЗначенияПоказателейОтчетовСинтетика.Версия.Проект В ИЕРАРХИИ(&Проект)
	|	И ЗначенияПоказателейОтчетовСинтетика.Версия.Сценарий = &Сценарий
	|	И ЗначенияПоказателейОтчетовСинтетика.Показатель В(&ПоказателиПроектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДвиженияПоРегистрам.Проект,
	|	ДвиженияПоРегистрам.ПриходРасход,
	|	ДвиженияПоРегистрам.ПериодОтчетаДатаНачала КАК ДатаНачала,
	|	КОНЕЦПЕРИОДА(ДвиженияПоРегистрам.ПериодОтчетаДатаОкончания, ДЕНЬ) КАК ДатаОкончания,
	|	СУММА(ДвиженияПоРегистрам.СуммаБДДС) КАК СуммаБДДС,
	|	СУММА(ДвиженияПоРегистрам.СуммаБДР) КАК СуммаБДР,
	|	СУММА(ДвиженияПоРегистрам.СуммаРесурсы) КАК СуммаРесурсы
	|ПОМЕСТИТЬ ТаблицаТранзакций
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Приход) КАК ПриходРасход,
	|		ЗначенияПоказателейОтчетовСинтетика.Значение КАК СуммаБДДС,
	|		0 КАК СуммаБДР,
	|		0 КАК СуммаРесурсы,
	|		РабочиеВерсии.ПериодОтчетаДатаНачала КАК ПериодОтчетаДатаНачала,
	|		РабочиеВерсии.ПериодОтчетаДатаОкончания КАК ПериодОтчетаДатаОкончания,
	|		РабочиеВерсии.Проект КАК Проект
	|	ИЗ
	|		РегистрСведений.ЗначенияПоказателейОтчетовСинтетика КАК ЗначенияПоказателейОтчетовСинтетика
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РабочиеВерсии КАК РабочиеВерсии
	|			ПО ЗначенияПоказателейОтчетовСинтетика.Версия = РабочиеВерсии.Версия
	|	ГДЕ
	|		ЗначенияПоказателейОтчетовСинтетика.Показатель = &ПоказательБДДСПриход";
	
	Если ЗначенияКонстант.АналитикаЭтапПроектаПоступленияБДДС>0 Тогда
		
		Запрос.Текст=Запрос.Текст+"	
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Приход),
		|		ЗначенияПоказателейОтчетов1.Значение,
		|		0,
		|		0,
		|		РабочиеВерсии.ПериодОтчетаДатаНачала,
		|		РабочиеВерсии.ПериодОтчетаДатаОкончания,
		|		ЗначенияПоказателейОтчетов1.Аналитика"+ЗначенияКонстант.АналитикаЭтапПроектаПоступленияБДДС+"
		|	ИЗ
		|		РегистрСведений.ЗначенияПоказателейОтчетов"+ЗначенияКонстант.ЧислоАналитикПоступленияБДДС+" КАК ЗначенияПоказателейОтчетов1
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РабочиеВерсии КАК РабочиеВерсии
		|			ПО ЗначенияПоказателейОтчетов1.Версия = РабочиеВерсии.Версия
		|	ГДЕ
		|		ЗначенияПоказателейОтчетов1.Показатель = &ПоказательБДДСПриход
		|		И ЗначенияПоказателейОтчетов1.Аналитика"+ЗначенияКонстант.АналитикаЭтапПроектаПоступленияБДДС+" В ИЕРАРХИИ(&Проект)";
		
	КонецЕсли;
	
	Запрос.Текст=Запрос.Текст+"		
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Расход),
	|		ЗначенияПоказателейОтчетовСинтетика.Значение * -1,
	|		0,
	|		0,
	|		РабочиеВерсии.ПериодОтчетаДатаНачала,
	|		РабочиеВерсии.ПериодОтчетаДатаОкончания,
	|		РабочиеВерсии.Проект
	|	ИЗ
	|		РегистрСведений.ЗначенияПоказателейОтчетовСинтетика КАК ЗначенияПоказателейОтчетовСинтетика
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РабочиеВерсии КАК РабочиеВерсии
	|			ПО ЗначенияПоказателейОтчетовСинтетика.Версия = РабочиеВерсии.Версия
	|	ГДЕ
	|		ЗначенияПоказателейОтчетовСинтетика.Показатель = &ПоказательБДДСРасход";
	
	Если ЗначенияКонстант.АналитикаЭтапПроектаСписанияБДДС>0 Тогда
		
		Запрос.Текст=Запрос.Текст+"
		
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Расход),
		|		ЗначенияПоказателейОтчетов1.Значение * -1,
		|		0,
		|		0,
		|		РабочиеВерсии.ПериодОтчетаДатаНачала,
		|		РабочиеВерсии.ПериодОтчетаДатаОкончания,
		|		ЗначенияПоказателейОтчетов1.Аналитика"+ЗначенияКонстант.АналитикаЭтапПроектаСписанияБДДС+"
		|	ИЗ
		|		РегистрСведений.ЗначенияПоказателейОтчетов"+ЗначенияКонстант.ЧислоАналитикСписанияБДДС+" КАК ЗначенияПоказателейОтчетов1
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РабочиеВерсии КАК РабочиеВерсии
		|			ПО ЗначенияПоказателейОтчетов1.Версия = РабочиеВерсии.Версия
		|	ГДЕ
		|		ЗначенияПоказателейОтчетов1.Показатель = &ПоказательБДДСРасход
		|		И ЗначенияПоказателейОтчетов1.Аналитика"+ЗначенияКонстант.АналитикаЭтапПроектаСписанияБДДС+" В ИЕРАРХИИ(&Проект)";
		
	КонецЕсли;
	
	Запрос.Текст=Запрос.Текст+"
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Приход),
	|		0,
	|		ЗначенияПоказателейОтчетовСинтетика.Значение,
	|		0,
	|		РабочиеВерсии.ПериодОтчетаДатаНачала,
	|		РабочиеВерсии.ПериодОтчетаДатаОкончания,
	|		РабочиеВерсии.Проект
	|	ИЗ
	|		РегистрСведений.ЗначенияПоказателейОтчетовСинтетика КАК ЗначенияПоказателейОтчетовСинтетика
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РабочиеВерсии КАК РабочиеВерсии
	|			ПО ЗначенияПоказателейОтчетовСинтетика.Версия = РабочиеВерсии.Версия
	|	ГДЕ
	|		ЗначенияПоказателейОтчетовСинтетика.Показатель = &ПоказательБДРПриход";
	
	Если ЗначенияКонстант.АналитикаЭтапПроектаДоходыБДР>0 Тогда
		
		Запрос.Текст=Запрос.Текст+"
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Приход),
		|		0,
		|		ЗначенияПоказателейОтчетов1.Значение,
		|		0,
		|		РабочиеВерсии.ПериодОтчетаДатаНачала,
		|		РабочиеВерсии.ПериодОтчетаДатаОкончания,
		|		ЗначенияПоказателейОтчетов1.Аналитика"+ЗначенияКонстант.АналитикаЭтапПроектаДоходыБДР+"
		|	ИЗ
		|		РегистрСведений.ЗначенияПоказателейОтчетов"+ЗначенияКонстант.ЧислоАналитикДоходыБДР+" КАК ЗначенияПоказателейОтчетов1
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РабочиеВерсии КАК РабочиеВерсии
		|			ПО ЗначенияПоказателейОтчетов1.Версия = РабочиеВерсии.Версия
		|	ГДЕ
		|		ЗначенияПоказателейОтчетов1.Показатель = &ПоказательБДРПриход
		|		И ЗначенияПоказателейОтчетов1.Аналитика"+ЗначенияКонстант.АналитикаЭтапПроектаДоходыБДР+" В ИЕРАРХИИ(&Проект)";
		
	КонецЕсли;
	
	Запрос.Текст=Запрос.Текст+"
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Расход),
	|		0,
	|		ЗначенияПоказателейОтчетовСинтетика.Значение * -1,
	|		0,
	|		РабочиеВерсии.ПериодОтчетаДатаНачала,
	|		РабочиеВерсии.ПериодОтчетаДатаОкончания,
	|		РабочиеВерсии.Проект
	|	ИЗ
	|		РегистрСведений.ЗначенияПоказателейОтчетовСинтетика КАК ЗначенияПоказателейОтчетовСинтетика
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РабочиеВерсии КАК РабочиеВерсии
	|			ПО ЗначенияПоказателейОтчетовСинтетика.Версия = РабочиеВерсии.Версия
	|	ГДЕ
	|		ЗначенияПоказателейОтчетовСинтетика.Показатель = &ПоказательБДРРасход";
	
	Если ЗначенияКонстант.АналитикаЭтапПроектаРасходыБДР>0 Тогда
		
		Запрос.Текст=Запрос.Текст+"
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Расход),
		|		0,
		|		ЗначенияПоказателейОтчетов1.Значение * -1,
		|		0,
		|		РабочиеВерсии.ПериодОтчетаДатаНачала,
		|		РабочиеВерсии.ПериодОтчетаДатаОкончания,
		|		ЗначенияПоказателейОтчетов1.Аналитика"+ЗначенияКонстант.АналитикаЭтапПроектаРасходыБДР+"
		|	ИЗ
		|		РегистрСведений.ЗначенияПоказателейОтчетов"+ЗначенияКонстант.ЧислоАналитикРасходыБДР+" КАК ЗначенияПоказателейОтчетов1
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РабочиеВерсии КАК РабочиеВерсии
		|			ПО ЗначенияПоказателейОтчетов1.Версия = РабочиеВерсии.Версия
		|	ГДЕ
		|		ЗначенияПоказателейОтчетов1.Показатель = &ПоказательБДРРасход
		|		И ЗначенияПоказателейОтчетов1.Аналитика"+ЗначенияКонстант.АналитикаЭтапПроектаРасходыБДР+" В ИЕРАРХИИ(&Проект)";
		
	КонецЕсли;
	
	Запрос.Текст=Запрос.Текст+"
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Приход),
	|		0,
	|		0,
	|		ЗначенияПоказателейОтчетовСинтетика.Значение,
	|		РабочиеВерсии.ПериодОтчетаДатаНачала,
	|		РабочиеВерсии.ПериодОтчетаДатаОкончания,
	|		РабочиеВерсии.Проект
	|	ИЗ
	|		РегистрСведений.ЗначенияПоказателейОтчетовСинтетика КАК ЗначенияПоказателейОтчетовСинтетика
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РабочиеВерсии КАК РабочиеВерсии
	|			ПО ЗначенияПоказателейОтчетовСинтетика.Версия = РабочиеВерсии.Версия
	|	ГДЕ
	|		ЗначенияПоказателейОтчетовСинтетика.Показатель = &ПоказательРесурсыПриход";
	
	Если ЗначенияКонстант.АналитикаЭтапПроектаПоступленияРесурсы>0 Тогда
		
		Запрос.Текст=Запрос.Текст+"
		
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Приход),
		|		0,
		|		0,
		|		ЗначенияПоказателейОтчетов1.Значение,
		|		РабочиеВерсии.ПериодОтчетаДатаНачала,
		|		РабочиеВерсии.ПериодОтчетаДатаОкончания,
		|		ЗначенияПоказателейОтчетов1.Аналитика"+ЗначенияКонстант.АналитикаЭтапПроектаПоступленияРесурсы+"
		|	ИЗ
		|		РегистрСведений.ЗначенияПоказателейОтчетов"+ЗначенияКонстант.ЧислоАналитикПоступленияРесурсы+" КАК ЗначенияПоказателейОтчетов1
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РабочиеВерсии КАК РабочиеВерсии
		|			ПО ЗначенияПоказателейОтчетов1.Версия = РабочиеВерсии.Версия
		|	ГДЕ
		|		ЗначенияПоказателейОтчетов1.Показатель = &ПоказательРесурсыПриход
		|		И ЗначенияПоказателейОтчетов1.Аналитика"+ЗначенияКонстант.АналитикаЭтапПроектаПоступленияРесурсы+" В ИЕРАРХИИ(&Проект)";
		
	КонецЕсли;
	
	Запрос.Текст=Запрос.Текст+"
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Расход),
	|		0,
	|		0,
	|		ЗначенияПоказателейОтчетовСинтетика.Значение * -1,
	|		РабочиеВерсии.ПериодОтчетаДатаНачала,
	|		РабочиеВерсии.ПериодОтчетаДатаОкончания,
	|		РабочиеВерсии.Проект
	|	ИЗ
	|		РегистрСведений.ЗначенияПоказателейОтчетовСинтетика КАК ЗначенияПоказателейОтчетовСинтетика
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РабочиеВерсии КАК РабочиеВерсии
	|			ПО ЗначенияПоказателейОтчетовСинтетика.Версия = РабочиеВерсии.Версия
	|	ГДЕ
	|		ЗначенияПоказателейОтчетовСинтетика.Показатель = &ПоказательРесурсыРасход";
	
	Если ЗначенияКонстант.АналитикаЭтапПроектаСписанияРесурсы>0 Тогда
		
		Запрос.Текст=Запрос.Текст+"
		
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Расход),
		|		0,
		|		0,
		|		ЗначенияПоказателейОтчетов1.Значение * -1,
		|		РабочиеВерсии.ПериодОтчетаДатаНачала,
		|		РабочиеВерсии.ПериодОтчетаДатаОкончания,
		|		ЗначенияПоказателейОтчетов1.Аналитика"+ЗначенияКонстант.АналитикаЭтапПроектаСписанияРесурсы+"
		|	ИЗ
		|		РегистрСведений.ЗначенияПоказателейОтчетов"+ЗначенияКонстант.ЧислоАналитикСписанияРесурсы+" КАК ЗначенияПоказателейОтчетов1
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РабочиеВерсии КАК РабочиеВерсии
		|			ПО ЗначенияПоказателейОтчетов1.Версия = РабочиеВерсии.Версия
		|	ГДЕ
		|		ЗначенияПоказателейОтчетов1.Показатель = &ПоказательРесурсыРасход
		|		И ЗначенияПоказателейОтчетов1.Аналитика"+ЗначенияКонстант.АналитикаЭтапПроектаСписанияРесурсы+" В ИЕРАРХИИ(&Проект)";
		
	КонецЕсли;
	
	Запрос.Текст=Запрос.Текст+") КАК ДвиженияПоРегистрам
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияПоРегистрам.Проект,
	|	ДвиженияПоРегистрам.ПриходРасход,
	|	ДвиженияПоРегистрам.ПериодОтчетаДатаНачала,
	|	ДвиженияПоРегистрам.ПериодОтчетаДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ТаблицаТранзакций.СуммаБДР) КАК СуммаБДР,
	|	СУММА(ТаблицаТранзакций.СуммаБДДС) КАК СуммаБДДС,
	|	СУММА(ТаблицаТранзакций.СуммаРесурсы) КАК СуммаРесурсы
	|ИЗ
	|	ТаблицаТранзакций КАК ТаблицаТранзакций
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(АтрибутыПроектов.ДатаНачала) КАК ДатаНачала,
	|	МАКСИМУМ(КОНЕЦПЕРИОДА(АтрибутыПроектов.ДатаОкончания, ДЕНЬ)) КАК ДатаОкончания
	|ИЗ
	|	РегистрСведений.АтрибутыПроектов КАК АтрибутыПроектов
	|ГДЕ
	|	НЕ(АтрибутыПроектов.ДатаНачала = &ПустаяДата
	|				ИЛИ АтрибутыПроектов.ДатаОкончания = &ПустаяДата)
	|	И АтрибутыПроектов.ДатаНачала >= &НижняяГраницаДиаграммы";
	
	Если ЗначениеЗаполнено(ДанныеОбъекта.Проект) Тогда
		
		Запрос.Текст=Запрос.Текст+"
		|И АтрибутыПроектов.Проект В Иерархии(&Проект)";
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Сценарий) Тогда
		
		Запрос.Текст=Запрос.Текст+"
		|И АтрибутыПроектов.Сценарий = &Сценарий";
		
		Запрос.УстановитьПараметр("Сценарий", Сценарий);
		
	КонецЕсли;
	
	Запрос.Текст=Запрос.Текст+"
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АтрибутыПроектов.ДатаНачала КАК ДатаНачала,
	|	КОНЕЦПЕРИОДА(АтрибутыПроектов.ДатаОкончания,ДЕНЬ) КАК ДатаОкончания,
	|	АтрибутыПроектов.Проект КАК Ссылка,
	|	АтрибутыПроектов.Проект.Ответственный КАК Ответственный,
	|	АтрибутыПроектов.Проект.Наименование КАК Наименование,
	|	АтрибутыПроектов.Проект.Родитель КАК Родитель,
	|	АтрибутыПроектов.Проект.ПроектыПредшественники.(
	|		Ссылка КАК Поле1,
	|		НомерСтроки КАК Поле2,
	|		Проект КАК Поле3
	|	),
	|	ВЫБОР
	|		КОГДА СтадииПроектов.Стадия ЕСТЬ NULL 
	|			ТОГДА """"
	|		ИНАЧЕ СтадииПроектов.Стадия.Наименование
	|	КОНЕЦ КАК Стадия
	|ИЗ
	|	РегистрСведений.АтрибутыПроектов КАК АтрибутыПроектов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтадииПроектов КАК СтадииПроектов
	|		ПО АтрибутыПроектов.Проект = СтадииПроектов.Этап
	|ГДЕ
	|	НЕ(АтрибутыПроектов.ДатаНачала = &ПустаяДата
	|				ИЛИ АтрибутыПроектов.ДатаОкончания = &ПустаяДата)";
	
	Если ЗначениеЗаполнено(ДанныеОбъекта.Проект) Тогда
		
		Запрос.Текст=Запрос.Текст+"
		|И АтрибутыПроектов.Проект В Иерархии(&Проект)";
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Сценарий) Тогда
		
		Запрос.Текст=Запрос.Текст+"
		|И АтрибутыПроектов.Сценарий = &Сценарий";
		
		Запрос.УстановитьПараметр("Сценарий", Сценарий);
		
	КонецЕсли;
	
	Запрос.Текст=Запрос.Текст+"	
	|
	|УПОРЯДОЧИТЬ ПО
	|	АтрибутыПроектов.ДатаНачала, АтрибутыПроектов.Проект ИЕРАРХИЯ";
	
	Запрос.УстановитьПараметр("ПустаяДата",'00010101');
	Запрос.УстановитьПараметр("НижняяГраницаДиаграммы",'10000101');
	
	Результат=Запрос.ВыполнитьПакет();
	
	ТаблицаДвижений		= Результат[2].Выгрузить();
	ТаблицаИнтервала	= Результат[3].Выгрузить();
	ДанныеПроектов		= Результат[4].Выгрузить();
	
	ДанныеПроектов.Колонки.Добавить("Интервал");
	
	ДиаграммаГанта.Очистить();
	
	Если ТаблицаИнтервала.Количество()=0 Тогда
		Возврат;
	ИначеЕсли ТаблицаИнтервала[0].ДатаНачала = Null ИЛИ ТаблицаИнтервала[0].ДатаОкончания = Null Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Проверьте интервалы проекта! Диаграмма не может быть построена.";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если ТаблицаИнтервала[0].ДатаНачала = Null ИЛИ ТаблицаИнтервала[0].ДатаОкончания = Null Тогда
		Возврат;
	КонецЕсли;
	
	ДиаграммаГанта.Обновление = Ложь;
	ДиаграммаГанта.АвтоОпределениеПолногоИнтервала = Ложь;

	ДиаграммаГанта.УстановитьПолныйИнтервал(НачалоМесяца(ТаблицаИнтервала[0].ДатаНачала),КонецМесяца(ТаблицаИнтервала[0].ДатаОкончания));
	
	РазмахИнтервала=(КонецМесяца(ТаблицаИнтервала[0].ДатаОкончания)-НачалоМесяца(ТаблицаИнтервала[0].ДатаНачала))/86400;
	ОпределитьШкалу(ДиаграммаГанта,РазмахИнтервала);
	
	СерияПроект	= ДиаграммаГанта.УстановитьСерию("Проект");

	Если ТаблицаДвижений.Количество()>0 Тогда
		
		Если НЕ ТаблицаДвижений[0].СуммаБДР=0 Тогда
			
			СерияБДР		= ДиаграммаГанта.УстановитьСерию("БДР");
			СерияБДР.Цвет = WebЦвета.ТемноСиний;
			
		КонецЕсли;
		
		Если НЕ ТаблицаДвижений[0].СуммаБДДС=0 Тогда
			
			СерияБДДС		= ДиаграммаГанта.УстановитьСерию("БДДС");
			СерияБДДС.Цвет 	= WebЦвета.Бирюзовый;
			
		КонецЕсли;
		
		Если НЕ ТаблицаДвижений[0].СуммаРесурсы=0 Тогда
			
			СерияРесурсы		= ДиаграммаГанта.УстановитьСерию("Ресурсы");
			СерияРесурсы.Цвет 	= WebЦвета.Оранжевый;
			
		КонецЕсли;
		
	КонецЕсли;
		
	МассивЦветов=Новый Массив;
	МассивЦветов.Добавить(WebЦвета.ДымчатоБелый);
	МассивЦветов.Добавить(WebЦвета.СеребристоСерый);
	
	НомерЦвета=0;
	
	Для Каждого СтрПроект ИЗ ДанныеПроектов Цикл
		
		Запрос.Текст="ВЫБРАТЬ
		|	ДатаНачала,
		|	ДатаОкончания,
		|	СУММА(ТаблицаТранзакций.СуммаБДДС) КАК СуммаБДДС,
		|	СУММА(ТаблицаТранзакций.СуммаБДР) КАК СуммаБДР,
		|	СУММА(ТаблицаТранзакций.СуммаРесурсы) КАК СуммаРесурсы
		|ИЗ
		|	ТаблицаТранзакций КАК ТаблицаТранзакций
		|ГДЕ ТаблицаТранзакций.Проект = &Проект
		|СГРУППИРОВАТЬ ПО ДатаНачала,ДатаОкончания
		|;
		|ВЫБРАТЬ
		|ТаблицаТранзакций.ДатаНачала,
		|ТаблицаТранзакций.ДатаОкончания,
		|ТаблицаТранзакций.СуммаБДДС,
		|ТаблицаТранзакций.СуммаБДР,
		|ТаблицаТранзакций.СуммаРесурсы
		|ИЗ
		|	ТаблицаТранзакций КАК ТаблицаТранзакций
		|ГДЕ ТаблицаТранзакций.Проект=&Проект";
			
		Запрос.УстановитьПараметр("Проект",СтрПроект.Ссылка);
		Результат=Запрос.ВыполнитьПакет();
		
		ДанныеТранзакций=Результат[0].Выгрузить();
		
		ИтогБДР=	ДанныеТранзакций.Итог("СуммаБДР");
		ИтогБДДС=	ДанныеТранзакций.Итог("СуммаБДДС");
		ИтогРесурсы	=ДанныеТранзакций.Итог("СуммаРесурсы");
		
		ТекстИтог="";
				
		Если НЕ ИтогБДР=0 Тогда
			
			ТекстИтог=ТекстИтог+", БДР = "+Формат(ИтогБДР,"ЧДЦ=2; ЧГ=3,0");
			
		КонецЕсли;
		
		Если НЕ ИтогБДДС=0 Тогда
			
			ТекстИтог=ТекстИтог+", БДДС = "+Формат(ИтогБДДС,"ЧДЦ=2; ЧГ=3,0");
			
		КонецЕсли;
		
		Если НЕ ИтогРесурсы=0 Тогда
			
			ТекстИтог=ТекстИтог+", Ресурсы = "+Формат(ИтогРесурсы,"ЧДЦ=2; ЧГ=3,0");
			
		КонецЕсли;
	
		ТочкаПроект=ДиаграммаГанта.УстановитьТочку(СтрПроект.Ссылка,СтрПроект.Родитель);
		ТочкаПроект.Текст=?(ПустаяСтрока(СтрПроект.Стадия),"", "Стадия: " + СтрПроект.Стадия + Символы.ПС) + СтрПроект.Наименование+?(ПустаяСтрока(ТекстИтог),"",Символы.ПС+Сред(ТекстИтог,3));
		ТочкаПроект.ЦветФона=МассивЦветов[НомерЦвета];
		ТочкаПроект.Расшифровка=СтрПроект.Ссылка;
		НомерЦвета=?(НомерЦвета=0,1,0);
		ТочкаПроект.Шрифт=Новый Шрифт(ТочкаПроект.Шрифт,,,Истина);

		Значение=ДиаграммаГанта.ПолучитьЗначение(ТочкаПроект,СерияПроект);
		Значение.Редактирование=Истина;
		Значение.Текст=ТочкаПроект.Текст;
		Интервал=Значение.Добавить();
		Интервал.Расшифровка= Новый Структура("Проект,ДатаНачала,ДатаОкончания",СтрПроект.Ссылка,СтрПроект.ДатаНачала,СтрПроект.ДатаОкончания);
		Интервал.Начало		= СтрПроект.ДатаНачала;
		Интервал.Конец		= СтрПроект.ДатаОкончания;
		
		СтрПроект.Интервал=Интервал;
		
		Для Каждого СтрокаТранзакций ИЗ ДанныеТранзакций Цикл
			
			Если ЗначениеЗаполнено(СтрокаТранзакций.СуммаБДР)Тогда
				
				Значение=ДиаграммаГанта.ПолучитьЗначение(ТочкаПроект,СерияБДР);
				Интервал=Значение.Добавить();
				Значение.Редактирование=Истина;
				Интервал.Расшифровка= Новый Структура("Проект,ДатаНачала,ДатаОкончания,БДР",СтрПроект.Ссылка,СтрокаТранзакций.ДатаНачала,СтрокаТранзакций.ДатаОкончания,Истина);
				Интервал.Текст		= Формат(СтрокаТранзакций.СуммаБДР, "ЧГ=3,0");
				Интервал.Начало		= СтрокаТранзакций.ДатаНачала;
				Интервал.Конец		= СтрокаТранзакций.ДатаОкончания;
							
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаТранзакций.СуммаБДДС) Тогда
				
				Значение=ДиаграммаГанта.ПолучитьЗначение(ТочкаПроект,СерияБДДС);
				Интервал=Значение.Добавить();
				Интервал.Расшифровка= Новый Структура("Проект,ДатаНачала,ДатаОкончания,БДДС",СтрПроект.Ссылка,СтрокаТранзакций.ДатаНачала,СтрокаТранзакций.ДатаОкончания,Истина);
				Значение.Редактирование=Истина;
				Интервал.Текст		= Формат(СтрокаТранзакций.СуммаБДДС, "ЧГ=3,0");
				Интервал.Начало		= СтрокаТранзакций.ДатаНачала;
				Интервал.Конец		= СтрокаТранзакций.ДатаОкончания;
								
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаТранзакций.СуммаРесурсы) Тогда
				
				Значение=ДиаграммаГанта.ПолучитьЗначение(ТочкаПроект,СерияРесурсы);
				Интервал=Значение.Добавить();
				Значение.Редактирование=Истина;
				Интервал.Расшифровка= Новый Структура("Проект,ДатаНачала,ДатаОкончания,Ресурсы",СтрПроект.Ссылка,СтрокаТранзакций.ДатаНачала,СтрокаТранзакций.ДатаОкончания,Истина);
				Интервал.Текст		= Формат(СтрокаТранзакций.СуммаРесурсы, "ЧГ=3,0");
				Интервал.Начало		= СтрокаТранзакций.ДатаНачала;
				Интервал.Конец		= СтрокаТранзакций.ДатаОкончания;
							
			КонецЕсли;
			
		КонецЦикла;
				
	КонецЦикла;
	
	// Отобразим связи между проектами
	
	Для Каждого СтрПроект ИЗ ДанныеПроектов Цикл
		
		Если СтрПроект.Ссылка.ПроектыПредшественники.Количество()=0 Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Для Каждого СтрПредшественник ИЗ СтрПроект.Ссылка.ПроектыПредшественники Цикл
			
			ДанныеПредшественника=ДанныеПроектов.Найти(СтрПредшественник.Проект,"Ссылка");
			
			Если НЕ ДанныеПредшественника=Неопределено Тогда
				
				Связь=ДанныеПредшественника.Интервал.Добавить(СтрПроект.Интервал);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ДиаграммаГанта.Обновление = Истина;
	
КонецПроцедуры // СформироватьОтчетПоПроектам()

Функция ОпределитьШкалу(ДиаграммаГанта,РазмахИнтервала)
	
	ЭлементыШкалыВремени = ДиаграммаГанта.ОбластьПостроения.ШкалаВремени.Элементы;
	
	ПервыйЭлемент = ЭлементыШкалыВремени[0];
	Для Индекс = 1 по ЭлементыШкалыВремени.Количество()-1 Цикл
		ЭлементыШкалыВремени.Удалить(ЭлементыШкалыВремени[1]);
	КонецЦикла; 
	
	Если РазмахИнтервала > 2000 Тогда
		
		ПервыйЭлемент.Единица = ТипЕдиницыШкалыВремени.Год;
		ПервыйЭлемент.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Сплошная, 1);
		ПервыйЭлемент.ФорматДня =  ФорматДняШкалыВремени.ДеньМесяца;
				
	ИначеЕсли РазмахИнтервала <= 2000 И РазмахИнтервала >750 Тогда
		
		ПервыйЭлемент.Единица = ТипЕдиницыШкалыВремени.Год;
		ПервыйЭлемент.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Сплошная, 1);
		ПервыйЭлемент.ФорматДня =  ФорматДняШкалыВремени.ДеньМесяца;
		
		Элемент = ЭлементыШкалыВремени.Добавить();
		Элемент.Единица = ТипЕдиницыШкалыВремени.Квартал;
		Элемент.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Точечная, 1);
		
	ИначеЕсли РазмахИнтервала <= 750 И РазмахИнтервала >90 Тогда
		
		ПервыйЭлемент.Единица = ТипЕдиницыШкалыВремени.Квартал;
		ПервыйЭлемент.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Сплошная, 1);
		ПервыйЭлемент.ФорматДня =  ФорматДняШкалыВремени.ДеньМесяца;
		
		Элемент = ЭлементыШкалыВремени.Добавить();
		Элемент.Единица = ТипЕдиницыШкалыВремени.Месяц;
		Элемент.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Точечная, 1);
		
	ИначеЕсли РазмахИнтервала <=90 И РазмахИнтервала >30 Тогда
		
		ПервыйЭлемент.Единица = ТипЕдиницыШкалыВремени.Месяц;
		ПервыйЭлемент.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Сплошная, 1);
		ПервыйЭлемент.ФорматДня =  ФорматДняШкалыВремени.ДеньНедели;
		
		Элемент = ЭлементыШкалыВремени.Добавить();
		Элемент.Единица = ТипЕдиницыШкалыВремени.Неделя;
		Элемент.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Точечная, 1);
	
	ИначеЕсли РазмахИнтервала <=30 Тогда
		
		ПервыйЭлемент.Единица = ТипЕдиницыШкалыВремени.Месяц;
		ПервыйЭлемент.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Сплошная, 1);
		ПервыйЭлемент.ФорматДня =  ФорматДняШкалыВремени.ДеньМесяца;
		
		Элемент = ЭлементыШкалыВремени.Добавить();
		Элемент.Единица = ТипЕдиницыШкалыВремени.День;
		Элемент.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Точечная, 1);
				
	КонецЕсли;	
		
КонецФункции // ОпределитьТипЕдиницы()

Функция ПодготовитьДанныеОбъекта()
	
	СтруктураОбъекта=Новый Структура;
	СтруктураОбъекта.Вставить("ДоговорКонтрагента",	ДоговорКонтрагента);
	СтруктураОбъекта.Вставить("Проект",				Проект);
	СтруктураОбъекта.Вставить("ДатаНач",			ДатаНач);
	СтруктураОбъекта.Вставить("ДатаКон",			ДатаКон);
	СтруктураОбъекта.Вставить("ПоОперациям",		0);
	
	Возврат СтруктураОбъекта;
	
КонецФункции 