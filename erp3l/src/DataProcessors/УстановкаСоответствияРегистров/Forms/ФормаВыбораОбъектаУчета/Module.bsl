
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	ПолучитьДеревоИсточников();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьДеревоИсточников()
	
	Если ЗначениеЗаполнено(Параметры.ИдентификаторТаблицы) Тогда
		
		ЗначениеВРеквизитФормы(ПолучитьИзВременногоХранилища(Параметры.ИдентификаторТаблицы),"ТаблицаРегистровИсточник");
		
	Иначе
		
		ДеревоОбъектовУчета=Перечисления.ОбъектыБухгалтерскогоУчета.ДеревоОбъектовУчета();
		
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		|	ИсточникиДанныхДляРасчетов.Ссылка КАК Ссылка,
		|	ИсточникиДанныхДляРасчетов.Наименование КАК Наименование,
		|	ИсточникиДанныхДляРасчетов.ОбъектУчета КАК ОбъектУчета,
		|	ИсточникиДанныхДляРасчетов.ПутьКДаннымОбъектНастройки КАК ПутьКДаннымОбъектНастройки
		|ИЗ
		|	Справочник.ИсточникиДанныхДляРасчетов КАК ИсточникиДанныхДляРасчетов
		|ГДЕ
		|	НЕ ИсточникиДанныхДляРасчетов.ОбъектУчета = НЕОПРЕДЕЛЕНО";
		
		ТабШаблоныИсточников=Запрос.Выполнить().Выгрузить();
		
		Для Каждого СтрокаГруппа ИЗ ДеревоОбъектовУчета.Строки Цикл
			
			НоваяГруппа=ТаблицаРегистровИсточник.ПолучитьЭлементы().Добавить();
			НоваяГруппа.ИсточникНаименование=СтрокаГруппа.Представление;
			НоваяГруппа.ЭтоГруппа=Истина;
			
			Для Каждого СтрокаИсточник ИЗ СтрокаГруппа.Строки Цикл
				
				ДанныеИсточника=ТабШаблоныИсточников.Найти(СтрокаИсточник.ОбъектУчета,"ОбъектУчета");
				
				Если ДанныеИсточника=Неопределено Тогда
					
					Продолжить;
					
				КонецЕсли;
				
				НовыйИсточник=НоваяГруппа.ПолучитьЭлементы().Добавить();
				НовыйИсточник.ОбъектУчетаИсточник=ДанныеИсточника.Ссылка;
				НовыйИсточник.ИсточникНаименование=ДанныеИсточника.Наименование;					
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
		
КонецПроцедуры // ПолучитьДеревоИсточников() 

&НаКлиенте
Процедура ТаблицаРегистровИсточникВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаИсточник=Элементы.ТаблицаРегистровИсточник.ТекущиеДанные;
	
	СтруктураИсточник=Новый Структура;
	СтруктураИсточник.Вставить("ОбъектУчетаИсточник",СтрокаИсточник.ОбъектУчетаИсточник);
	СтруктураИсточник.Вставить("ОбъектНастройки",СтрокаИсточник.ОбъектНастройки);
	СтруктураИсточник.Вставить("ИдентификаторИсточника",СтрокаИсточник.ПолучитьИдентификатор());
	
	Оповестить("ВыбранОбъектУчета",СтруктураИсточник);
	Закрыть();
	
КонецПроцедуры
