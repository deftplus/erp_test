///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////
#Область ОписаниеПеременных

&НаКлиенте
Перем КонтекстЭДОКлиент;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	// 0 - ожидание
	// 1 - компонента не установлена
	// 2 - сертификат не найден
	// 3 - сертификат найден
	// 4 - нет криптопровайдера
	НачальныеНастройкиФормы();
	ВариантОформления = Параметры.ВариантОформления;
	ОтпечатокСертификата = Параметры.ОтпечатокСертификата;
	УчетнаяЗапись = Параметры.УчетнаяЗапись;
	ИскатьСертификат = Ложь;
	
	Если ВариантОформления = 0 Тогда
		Если Параметры.СертификатНайден = Неопределено Тогда
			ИскатьСертификат = Истина;
		ИначеЕсли Параметры.СертификатНайден = Ложь Тогда
			ВариантОформления = 2;
		Иначе	
			ВариантОформления = 3;
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодготовитьОформлениеФормы();
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отмена(Команда)
	
	ЗакрытьФорму("Отмена");
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьОтправку(Команда)
	
	ЗакрытьФорму("Продолжить");
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ЗакрытьФорму("Обновить");
	
КонецПроцедуры

&НаКлиенте
Процедура ТребуетсяПомощьНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура();
	
	ОткрытьФорму(КонтекстЭДОКлиент.ПутьКОбъекту + ".Форма.Мастер_Помощь", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	
	Если ИскатьСертификат Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПредупредитьОПереходеПослеПоискаКриптопровайдера", 
			ЭтотОбъект);
		
		КриптографияЭДКОКлиент.ПолучитьКриптопровайдеры(ОписаниеОповещения, , Ложь);
		
	Иначе
		ПодготовитьОформлениеФормы();
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПредупредитьОПереходеПослеПоискаКриптопровайдера(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Выполнено Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПредупредитьОПереходеПослеПоискаСертификата", 
			ЭтотОбъект);
		
		КриптографияЭДКОКлиент.НайтиСертификатПоОтпечатку(ОписаниеОповещения, ОтпечатокСертификата, "MY");
	Иначе
		ТекстОшибки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "ОписаниеОшибки", "");
		Если СтрНайти(ТекстОшибки, "Не удалось создать объект для работы с криптографией. В системе не обнаружен криптопровайдер.") Тогда
			ВариантОформления = 1;
		Иначе	
			ВариантОформления = 4;
		КонецЕсли;	
		ПодготовитьОформлениеФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредупредитьОПереходеПослеПоискаСертификата(Результат, ДополнительныеПараметры) Экспорт
	
	СертификатНайден = Результат.Выполнено И Результат.СертификатНайден;
	Если НЕ Результат.Выполнено Тогда
		ВариантОформления = 1;
	ИначеЕсли НЕ Результат.СертификатНайден Тогда
		ВариантОформления = 2;
	Иначе
		ВариантОформления = 3;
	КонецЕсли;
	
	ПодготовитьОформлениеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(РезультатВыбора = "")
	
	Закрыть(РезультатВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодготовитьОформлениеФормы()
	
	КартинкаОжидане = Ложь;
	КнопкаПомощь = Ложь;
	КнопкаОтмена = Истина;
	КнопкаОбновить = Ложь;
	КнопкаПродолжить = Истина;
	
	Если ВариантОформления = 0 Тогда
		КартинкаОжидане = Истина;
		Элементы.ФормаПродолжить.Заголовок = НСтр("ru = 'Пропустить';
													|en = 'Ignore'");
		ТекстЗаголовка = НСтр("ru = 'Выполняется поиск сертификата';
								|en = 'Searching for certificate'");
		ТекстИнформации = НСтр("ru = 'Подождите, пожалуйста...';
								|en = 'Please wait...'");
		
	ИначеЕсли ВариантОформления = 1 // компонента не установлена
			ИЛИ ВариантОформления = 2 // сертификат не найден
			ИЛИ ВариантОформления = 4 // нет криптопровайдера
			Тогда 
		КнопкаПомощь = Истина;
		Элементы.ФормаПродолжить.КнопкаПоУмолчанию = Истина;
		Элементы.ФормаПродолжить.Заголовок = НСтр("ru = 'Продолжить отправку заявления';
													|en = 'Continue sending your application'");
		Элементы.ФормаОтмена.Заголовок = НСтр("ru = 'Отправить заявление позже';
												|en = 'Send application later'");
		ТекстЗаголовка = НСтр("ru = 'Перенос хранения закрытых ключей в программу';
								|en = 'Transfer private key storage to the application'");
		ТекстИнформации = НСтр("ru = 'После перехода на хранение ключа в программе могут возникнуть проблемы с расшифровкой сообщений по 1С-Отчетности, которые высланы до момента смены ключа, но еще не получены вами и не расшифрованы.';
								|en = 'Once the key is transferred to the storage, the application might contain issues with decrypting 1C Reporting messages which were sent before the key change, but were not received and decrypted yet.'")
						+ Символы.ПС + Символы.ПС
						+ НСтр("ru = 'Перед тем как продолжить отправку заявления, необходимо получить входящие сообщения <b>под пользователем 1С-Отчетности</b> на том рабочем месте, где <b>настроена работа с закрытым ключом</b>.';
								|en = 'Before continuing to send the application, you must receive incoming messages <b>under the 1C-Reporting user</b> at the computer where <b>using private key is configured</b>.'") + " "
						+ НСтр("ru = 'Для этого в соответствующем сеансе необходимо нажать кнопку ""Обновить"" в форме ""1С-Отчетность"".';
								|en = 'To do this, click ""Refresh"" in the ""1C-Reporting"" form in the corresponding session.'");

	Иначе
		КнопкаОтмена = Ложь;
		КнопкаОбновить = Истина;
		Элементы.ФормаОбновить.КнопкаПоУмолчанию = Истина;
		Элементы.ФормаОбновить.Заголовок = НСтр("ru = 'Получить и расшифровать сообщения';
												|en = 'Receive and decrypt messages'");
		Элементы.ФормаПродолжить.Заголовок = НСтр("ru = 'Продолжить отправку заявления';
													|en = 'Continue sending your application'");
		ТекстЗаголовка = НСтр("ru = 'Перенос хранения закрытых ключей в программу';
								|en = 'Transfer private key storage to the application'");
		ТекстИнформации = НСтр("ru = 'После перехода на хранение ключа в программе могут возникнуть проблемы с расшифровкой сообщений по 1С-Отчетности, которые высланы до момента смены ключа, но еще не получены вами и не расшифрованы.';
								|en = 'Once the key is transferred to the storage, the application might contain issues with decrypting 1C Reporting messages which were sent before the key change, but were not received and decrypted yet.'")
						+ Символы.ПС + Символы.ПС
						+ НСтр("ru = 'Перед тем как продолжить отправку заявления, необходимо получить входящие сообщения по 1С-Отчетности.';
								|en = 'Before proceeding with sending the application, you must receive incoming messages using 1C-Reporting.'") + " "
						+ НСтр("ru = 'Нажмите ""Получить и расшифровать сообщения"", чтобы сделать это сейчас.';
								|en = 'Click ""Receive and decrypt messages"" to do that now.'");
		
	КонецЕсли;
	
	Элементы.ФормаОбновить.Видимость = КнопкаОбновить;
	Элементы.ФормаПродолжить.Видимость = КнопкаПродолжить;
	Элементы.ФормаОтмена.Видимость = КнопкаОтмена;
	Элементы.ТребуетсяПомощь.Видимость = КнопкаПомощь;
	Элементы.ДекорацияКартинка.Видимость = КартинкаОжидане;
	Элементы.ДекорацияИнформация.Заголовок = СтроковыеФункцииКлиент.ФорматированнаяСтрока(ТекстИнформации);
	
	Заголовок = ТекстЗаголовка;
	
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьРазмерыФормы", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьРазмерыФормы()
	
	Элементы.ВариантОформления.Видимость = НЕ Элементы.ВариантОформления.Видимость;
	Элементы.ВариантОформления.Видимость = НЕ Элементы.ВариантОформления.Видимость;
	
КонецПроцедуры

&НаСервере
Процедура НачальныеНастройкиФормы()
	
	ПутьФормы = ЭтотОбъект.ИмяФормы + "/НастройкиФормы";
	ПутьОкна =  ЭтотОбъект.ИмяФормы + "/НастройкиОкна";
	
	ОбщегоНазначения.ХранилищеСистемныхНастроекУдалить(ПутьФормы, "", ИмяПользователя());
	ОбщегоНазначения.ХранилищеСистемныхНастроекУдалить(ПутьОкна, "", ИмяПользователя());

КонецПроцедуры

#КонецОбласти
