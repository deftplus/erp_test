
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПараметрыДокумента = ПолучитьПараметрыДокументаНаСервере(ПараметрКоманды);
	
	Если ЗначениеЗаполнено(ПараметрыДокумента.СохраненныйЧерновик) Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросОткрытияВерсииСоглашенияПродолжение", ЭтотОбъект, ПараметрыДокумента);
		
		ТекстВопроса = НСтр("ru = 'Обнаружен непроведенный черновик договора. Продолжить редактирование или создать новую версию на основании действующей?'");
		
		Кнопки = Новый СписокЗначений();
		Кнопки.Добавить(ПараметрыДокумента.СохраненныйЧерновик, НСтр("ru = 'Продолжить редактирование'"));
		Кнопки.Добавить(Неопределено, НСтр("ru = 'Создать новую версию'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru = 'Отмена'"));
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки);
	Иначе
		СоздатьНовуюВерсиюСоглашения(ПараметрыДокумента.ИмяФормы, ПараметрКоманды);	
	КонецЕсли;
		
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовуюВерсиюСоглашения(ИмяФормы, Договор)
	
	ПараметрыФормы = Новый Структура("Основание", Договор);
	ОткрытьФорму(ИмяФормы, ПараметрыФормы);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПараметрыДокументаНаСервере(Договор);
	
	СтруктураПараметров = НовыйСтруктураПараметровДокумента();
	СтруктураПараметров.Договор = Договор;
	
	ВидДоговораУХ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ВидДоговораУХ");
	ИмяДокумента = УправлениеДоговорамиУХКлиентСерверПовтИсп.ПолучитьИмяДокументаПоВидуДоговора(ВидДоговораУХ);
	
	СтруктураПараметров.ИмяФормы = СтрШаблон("Документ.%1.ФормаОбъекта", ИмяДокумента);
	
	СтруктураПараметров.СохраненныйЧерновик = РаботаСДоговорамиКонтрагентовУХ.СохраненныйЧерновик(Договор, ИмяДокумента);
	
	Возврат СтруктураПараметров;
	
КонецФункции

&НаСервере
Функция НовыйСтруктураПараметровДокумента()
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("ИмяФормы");
	Параметры.Вставить("СохраненныйЧерновик");
	Параметры.Вставить("Договор");
	
	Возврат Параметры;
	
КонецФункции

&НаКлиенте
Процедура ВопросОткрытияВерсииСоглашенияПродолжение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	ИначеЕсли ЗначениеЗаполнено(Результат) Тогда
		ОткрытьФорму(Параметры.ИмяФормы, Новый Структура("Ключ", Результат));
	Иначе
		СоздатьНовуюВерсиюСоглашения(Параметры.ИмяФормы, Параметры.Договор);
	КонецЕсли;
	
КонецПроцедуры