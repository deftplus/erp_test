#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для Каждого ТекОперация Из Параметры.ОперацииГрафика Цикл
		Элементы.ОперацияГрафика.СписокВыбора.Добавить(ТекОперация.Ключ);	
	КонецЦикла;
	
	Организация = Параметры.Организация;
	Контрагент = Параметры.Контрагент;
	
	// Массив описаний статей формы
	МассивОписанийСтатей = Новый Массив;
	
	АналитикиСтатейБюджетовУХ.СтатьяИАналитикиРеквизитовФормыВПолях(МассивОписанийСтатей,
		"СтатьяБюджета", "СтатьяБюджета",
		"Аналитика%1", "Аналитика%1",
		ФормыУХ.РазместитьВГруппеСтрокой(Элементы.ГруппаАналитикиСтатьи)
	);
	
	Если МассивОписанийСтатей.Количество() > 0 Тогда
		АналитикиСтатейБюджетовУХ.СоздатьСтатьиБюджетовИАналитики(ЭтотОбъект, МассивОписанийСтатей, ПараметрыПоляВвода());
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОперацияГрафикаПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ОперацияГрафика) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОперации = Параметры.ОперацииГрафика[ОперацияГрафика];
	
	Если ПараметрыОперации <> Неопределено Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыОперации);
		Если ЗначениеЗаполнено(ВидБюджета) Тогда
			ТипСтатьиБюджета = ТипСтатьиБюджета(ВидБюджета);
			Элементы.СтатьяБюджета.ОграничениеТипа = ТипСтатьиБюджета;
			Элементы.СтатьяБюджета.ТолькоПросмотр = Ложь;
			Элементы.СтатьяБюджета.ПодсказкаВвода = "";
			
			//
			Элементы.СтатьяБюджета.Видимость = ПараметрыОперации.ИспользуетсяПланирование;
			Элементы.ГруппаАналитикиСтатьи.Видимость = ПараметрыОперации.ИспользуетсяПланирование;
			Элементы.ЦФО.Видимость = ПараметрыОперации.ИспользуетсяПланирование;
			Элементы.Проект.Видимость = ПараметрыОперации.ИспользуетсяПланирование;
			
			АналитикиСтатейБюджетовУХКлиент.ПриИзмененииСтатьиБюджета(ЭтотОбъект, "СтатьяБюджета");
			
		Иначе
			СтатьяБюджета = Неопределено;
			АналитикиСтатейБюджетовУХКлиент.ПриИзмененииСтатьиБюджета(ЭтотОбъект, "СтатьяБюджета");
			Элементы.СтатьяБюджета.ТолькоПросмотр = Истина;
			Элементы.СтатьяБюджета.ПодсказкаВвода = НСтр("ru = '<не используется>'");
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СтатьяБюджета_ПриИзменении(Элемент)
	АналитикиСтатейБюджетовУХКлиент.ПриИзмененииСтатьиБюджета(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_АналитикаСтатьиБюджета_ПриИзменении(Элемент)
	АналитикиСтатейБюджетовУХКлиент.ПриИзмененииАналитикиСтатьиБюджета(ЭтотОбъект, Элемент.Имя);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		ОповеститьОВыборе(ВыбранныеПараметрыПозиции());
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПараметрыПоляВвода()
	ПараметрыПоляВвода = Новый Структура;
	ПараметрыПоляВвода.Вставить("ПоложениеЗаголовка", 		ПоложениеЗаголовкаЭлементаФормы.Лево);
	ПараметрыПоляВвода.Вставить("АвтоМаксимальнаяШирина",	Ложь);
	ПараметрыПоляВвода.Вставить("МаксимальнаяШирина",		40);
	
	Возврат ПараметрыПоляВвода;

КонецФункции

&НаСервереБезКонтекста
Функция ТипСтатьиБюджета(ВидБюджета)
	
	Возврат ВидБюджета.ТипЗначения;
	
КонецФункции

&НаКлиенте
Функция ВыбранныеПараметрыПозиции()
	
	Результат = Новый Структура(АналитикиСтатейБюджетовУХКлиентСервер.РеквизитыАналитикСтрокой());
	Результат.Вставить("ЦФО");
	Результат.Вставить("Валюта");
	Результат.Вставить("ВидБюджета");
	Результат.Вставить("Дата");
	Результат.Вставить("Проект");
	Результат.Вставить("ОперацияГрафика");
	Результат.Вставить("СтатьяБюджета");
	Результат.Вставить("Сумма");	
	Результат.Вставить("СчетОрганизации");
	Результат.Вставить("СчетКонтрагента");
	Результат.Вставить("КолонкаСекции");
	Результат.Вставить("ВидОперацииУХ");
	Результат.Вставить("НаправлениеДвижения");
	Результат.Вставить("ЭлементСтруктурыЗадолженности");
	
	ЗаполнитьЗначенияСвойств(Результат, ЭтотОбъект);
	Возврат Результат;
	
КонецФункции

#КонецОбласти


