
&НаКлиенте
Процедура Выбрать(Команда)
	
	 ВыборЭлемента();	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(МодульУправленияОповещениямиУХ.ВернутьОбъектШаблона(Параметры.ТипОбъектаОповещения,Параметры.НазначениеОповещения));
	
	ОграничениеТипаСтрока = Параметры.ОграничениеТипа;
	ОграничиватьСсылочные = Параметры.ОграничиватьСсылочные;	
	
	Если МетаданныеОбъекта = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	МакетСКД = ПолучитьСхемуСКДДляПодстановкиВШаблоне(Параметры.ТипОбъектаОповещения,Параметры.ВидОбъектаОповещения,Параметры.НазначениеОповещения, , ОграничиватьСсылочные);
	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(ПоместитьВоВременноеХранилище(МакетСКД, УникальныйИдентификатор)));

КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастроекНастройкиПорядокДоступныеПоляПорядкаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыборЭлемента();

КонецПроцедуры

&НаКлиенте
Процедура ВыборЭлемента()
	ТекСтрокаКомпоновка = Элементы.КомпоновщикНастроекНастройкиПорядокДоступныеПоляПорядка.ТекущаяСтрока;
	Если ТекСтрокаКомпоновка <> Неопределено Тогда
		СтрокаОтбора = КомпоновщикНастроек.Настройки.Порядок.ДоступныеПоляПорядка.ПолучитьОбъектПоИдентификатору(Элементы.КомпоновщикНастроекНастройкиПорядокДоступныеПоляПорядка.ТекущаяСтрока);
		
		Если ЗначениеЗаполнено(ОграничениеТипаСтрока) Тогда
			Если  СтрокаОтбора.ТипЗначения.СодержитТип(Тип(ОграничениеТипаСтрока)) Тогда
				Закрыть(Новый Структура("Поле",СтрокаОтбора.Поле))
			Иначе
				
				СтрокаШаблона = Нстр("ru = 'Поле должно иметь тип %1 !'");
				
				Если Не ПустаяСтрока(СтрокаШаблона) тогда
					ОбщегоНазначенияУХ.СообщитьОбОшибке(СтрШаблон(СтрокаШаблона, ОграничениеТипаСтрока));
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе	
			
			Закрыть(Новый Структура("Поле",СтрокаОтбора.Поле))
			
		КонецЕсли;
	Иначе
		ТекстСообщения = НСтр("ru = 'Поле не выбрано. Операция отменена.'");
		ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьСхемуСКДДляПодстановкиВШаблоне(ТипОбъектаОповещения, ВидОбъектаОповещения, НазначениеОповещения, ВидСобытияВход = Неопределено, ОграничиватьСсылочныеВход = Ложь) Экспорт
	
	МакетСкд = ПолучитьОбщийМакет("МакетНастройкиОтборов");	
	МакетСкд.НаборыДанных[0].Поля.Очистить();
	
	Если ЗначениеЗаполнено(ТипОбъектаОповещения) Тогда	
		Для Каждого СтрПоле Из ТипОбъектаОповещения.Реквизиты Цикл	
			ДобавитьОписаниеПоляСКД(СтрПоле.Имя,СтрПоле.Синоним,СтрПоле.ТипДанных,МакетСкд);
		КонецЦикла;	
		ДобавитьСтандартныеРеквизитыВСхему(ТипОбъектаОповещения, МакетСкд);
	КонецЕсли;	
	
	Возврат МакетСкд;
	
КонецФункции

Процедура ДобавитьСтандартныеРеквизитыВСхему(ТипОбъектаОповещенияВход, МакетСкд)
	Если ТипЗнч(ТипОбъектаОповещенияВход) = Тип("СправочникСсылка.СправочникиБД") Тогда
		// Поиск текущего справочника в метаданных.
		НайденныйСправочник = Неопределено;
		Для Каждого ТекСправочники Из Метаданные.Справочники Цикл
			Если ТипОбъектаОповещенияВход.Наименование = ТекСправочники.Имя Тогда
				НайденныйСправочник = ТекСправочники;
			Иначе
				// Выполняем поиск далее.
			КонецЕсли;	
		КонецЦикла;	
		// Поиск типов владельца.
		МассивТиповВладельца = Новый Массив;
		Для Каждого ТекВладельцы Из НайденныйСправочник.Владельцы Цикл
			//ИмяБазовогоТипа = ОбщегоНазначения.ИмяБазовогоТипаПоОбъектуМетаданных(НайденныйСправочник);
			Если ОбщегоНазначения.ЭтоСправочник(НайденныйСправочник) Тогда
				МассивТиповВладельца.Добавить("Справочник." + ТекВладельцы.Имя);
			ИначеЕсли ОбщегоНазначения.ЭтоПланВидовХарактеристик(НайденныйСправочник) Тогда
				МассивТиповВладельца.Добавить("ПланВидовХарактеристик." + ТекВладельцы.Имя);	
			Иначе
				// Пропускаем.
			КонецЕсли;
		КонецЦикла;	
		// Добавление поля владельца по найденным типам.
		Если МассивТиповВладельца.Количество() > 0 Тогда
			СтрокаТипВладельца = СтрСоединить(МассивТиповВладельца, ";");
			ДобавитьОписаниеПоляСКД("Владелец", "Владелец", СтрокаТипВладельца, МакетСкд);
		Иначе
			// Нет владельцев.
		КонецЕсли;
		// Добавление поля родителя.
		СправочникПодчиненЭлементам = (Строка(НайденныйСправочник.ИспользованиеПодчинения) = "Элементам");
		СправочникПодчиненГруппамИЭлементам = (Строка(НайденныйСправочник.ИспользованиеПодчинения) = "ГруппамИЭлементам");
		Если СправочникПодчиненЭлементам ИЛИ СправочникПодчиненГруппамИЭлементам Тогда
			СтрокаТипРодителя = "Справочник." + НайденныйСправочник.Имя;
			ДобавитьОписаниеПоляСКД("Родитель", "Родитель", СтрокаТипРодителя, МакетСкд);
		Иначе
			// Нет владельцев.
		КонецЕсли;
	Иначе
		// Для прочих вариантов не добавляем.
	КонецЕсли;
КонецПроцедуры		// ДобавитьСтандартныеРеквизитыВСхему()

Процедура ДобавитьОписаниеПоляСКД(ИмяПоля,СинонимПоля,ТипДанных,МакетСкд, ТипОбъектаОповещения = Неопределено)
		
	НовоеПоле = МакетСкд.НаборыДанных[0].Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));	
	НовоеПоле.Поле = ИмяПоля;
	НовоеПоле.Заголовок = СинонимПоля;
	НовоеПоле.ПутьКДанным = ИмяПоля;
	
	МассивТипов = Новый Массив;
	МассивПриведенныхТипов = Новый Массив();
	
	Если (ТипОбъектаОповещения <> Неопределено) И (СокрЛП(ИмяПоля) = "Ссылка") Тогда
		// Ссылка представлен в виде уникального идетификатора. Подменим её тип на тип элемента.
		ТипСсылки = ОбщегоНазначенияСерверУХ.ВернутьТипПоСсылкеБД(ТипОбъектаОповещения);
		МассивПриведенныхТипов.Добавить(ТипСсылки);
		НовоеПоле.ТипЗначения = Новый ОписаниеТипов(МассивПриведенныхТипов);
	Иначе	
		МассивТипов=СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ТипДанных, ";");
		Для Каждого Эл Из МассивТипов Цикл
			НазваниеТипа = СтрЗаменить(Эл,".","Ссылка.");
			Попытка
				ПриведенныйТип = Тип(НазваниеТипа);
				МассивПриведенныхТипов.Добавить(ПриведенныйТип);
			Исключение
				ТекстОшибки = НСтр("ru = 'Не удалось добавить тип %НазваниеТипа%. Возможно, требуется обновить метаданные по 
				|данным текущей информационной базы в разделе Интеграция и упрвление НСИ - 
				|Типы ИБ - Текущая информационная база - Загрузить структуру данных'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%НазваниеТипа%", НазваниеТипа);
				ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстОшибки);
			КонецПопытки;
		КонецЦикла;
	КонецЕсли;
	НовоеПоле.ТипЗначения = Новый ОписаниеТипов(МассивПриведенныхТипов);		
КонецПроцедуры


