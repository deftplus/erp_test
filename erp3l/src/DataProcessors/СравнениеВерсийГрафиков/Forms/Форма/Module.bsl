#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("РеквизитыОбъекта") Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыОбъекта = Параметры.РеквизитыОбъекта;
	ЗаполнитьЗначенияСвойств(Объект, РеквизитыОбъекта);
	
	Объект.ВерсияСоглашенияДляСравнения = Параметры.ВерсияСоглашенияДляСравнения;
	ЗаполнитьРеквизитыПоВерсииСоглашения(Объект.ВерсияСоглашенияДляСравнения);
	
	АдресГрафикаРасчетов = Параметры.АдресГрафикаРасчетов;
	Объект.ГрафикРасчетов.Загрузить(ПолучитьИзВременногоХранилища(АдресГрафикаРасчетов));
	УдалитьИзВременногоХранилища(АдресГрафикаРасчетов);	
			
	МенеджерВерсииСоглашения = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Объект.Ссылка);
	ВидДоговораУХ = РаботаСДоговорамиКонтрагентовУХ.ВидДоговораУХОбъекта(РеквизитыОбъекта);
	ЭтотОбъект.ОписаниеГрафика = МенеджерВерсииСоглашения.ОписаниеГрафика(ВидДоговораУХ);
	ЭтотОбъект.ОперацииГрафика = Новый ФиксированноеСоответствие(
		РаботаСДоговорамиКонтрагентовУХ.ОперацииГрафика(ЭтотОбъект.ОписаниеГрафика, РеквизитыОбъекта));
		
	ЭтотОбъект.ТекущаяВерсияПредставление = ?(ЗначениеЗаполнено(Объект.Ссылка), 
		Строка(Объект.Ссылка), 
		Нстр("ru = '<новый документ>'"));
	МассивТиповВерсияСоглашения = Новый Массив;
	МассивТиповВерсияСоглашения.Добавить(ТипЗнч(Объект.Ссылка));
	Элементы.ВерсияСоглашенияДляСравнения.ОграничениеТипа = Новый ОписаниеТипов(МассивТиповВерсияСоглашения);
	ЭтотОбъект.ИмяТипаВерсииСоглашения = Объект.Ссылка.Метаданные().ПолноеИмя();
	
	Элементы.ОбъектРасчетов.Видимость = РеквизитыОбъекта.Свойство("ОбъектРасчетов");
	
	ДоговорыКонтрагентовФормыУХ.СоздатьТаблицуСравненияГрафиковНаФорме(ЭтотОбъект);
	ДоговорыКонтрагентовФормыУХ.ОбновитьТаблицуСравненияГрафиковРасчетов(ЭтотОбъект, Объект.ГрафикРасчетовВерсияСоглашения);
	
	УстановитьОтборСтрокГрафика(ЭтотОбъект);
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиЭлементовШапкиФормы
&НаКлиенте
Процедура ВерсияСоглашенияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
	ПараметрыОткрытия.Вставить("Отбор", РаботаСДоговорамиКонтрагентовУХКлиентСервер.ОтборВерсийСоглашений(Объект));
	ОткрытьФорму(СтрШаблон("%1.ФормаВыбора", ЭтотОбъект.ИмяТипаВерсииСоглашения), ПараметрыОткрытия, Элемент,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ВерсияСоглашенияПриИзменении(Элемент)
	ВерсияСоглашенияПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ВерсияСоглашенияПриИзмененииНаСервере()
	ЗаполнитьРеквизитыПоВерсииСоглашения(Объект.ВерсияСоглашенияДляСравнения);
	ДоговорыКонтрагентовФормыУХ.ОбновитьТаблицуСравненияГрафиковРасчетов(ЭтотОбъект, Объект.ГрафикРасчетовВерсияСоглашения);
КонецПроцедуры

&НаКлиенте
Процедура ОтборТолькоОтклоненияПриИзменении(Элемент)
	УстановитьОтборСтрокГрафика(ЭтотОбъект);
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьРеквизитыПоВерсииСоглашения(ВерсияСоглашения)
		
	Если ЗначениеЗаполнено(ВерсияСоглашения) Тогда		
		Объект.ГрафикРасчетовВерсияСоглашения.Загрузить(
			РаботаСДоговорамиКонтрагентовУХ.ГрафикРасчетовВерсииСоглашения(ВерсияСоглашения));		
	Иначе
		Объект.ГрафикРасчетовВерсияСоглашения.Очистить();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборСтрокГрафика(Форма)
	
	ОтборСтрок = ?(Форма.ОтображатьТолькоОтклонения, 
		Новый ФиксированнаяСтруктура("ЕстьОтклонения", Истина), 
		Неопределено);
	Форма.Элементы.ГрафикРасчетов.ОтборСтрок = ОтборСтрок;
КонецПроцедуры
#КонецОбласти