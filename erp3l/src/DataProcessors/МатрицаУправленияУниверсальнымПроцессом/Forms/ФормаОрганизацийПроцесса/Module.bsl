// Управляет доступностью элементов на форме.
&НаСервере
Процедура УправлениеДоступностью()
	// Видимость элементов.
	Элементы.ДекорацияПредупреждениеЭтапы.Видимость = ОтображатьПредупреждениеЭтапы;
	Элементы.УстанавливатьСтатусОтчетов.Видимость = ОтображатьФлагУстановкиСтатусовОтчетов;
	Элементы.ЗаполнятьДокументы.Видимость = ОтображатьФлагЗаполненияОтчетов;
	// Заголовок флага УстанавливатьСтатусОтчетов.
	Если Действие = "Открыть этап" Тогда
		Элементы.УстанавливатьСтатусОтчетов.Заголовок = НСтр("ru = 'Установить отчетам статус ""Черновик""'");
	ИначеЕсли Действие = "Закрыть этап" Тогда
		Элементы.УстанавливатьСтатусОтчетов.Заголовок = НСтр("ru = 'Установить отчетам статус ""Утвержден""'");
	ИначеЕсли Действие = "Отменить обработку этапа" Тогда
		Элементы.УстанавливатьСтатусОтчетов.Заголовок = НСтр("ru = 'Установить отчетам статус ""Черновик""'");
	Иначе
		Элементы.УстанавливатьСтатусОтчетов.Заголовок = НСтр("ru = 'Установить отчетам новый статус'");
	КонецЕсли;
КонецПроцедуры		// УправлениеДоступностью()

&НаКлиенте
Процедура ВыполнитьДействие(Команда)
	
	ВыполнитьДействиеСервер();
	
	СтруктураОповещения = Новый Структура;
	СтруктураОповещения.Вставить("ВыбОрганизацииАдрес", ВыбОрганизацииАдрес);
	СтруктураОповещения.Вставить("Действие", Действие);
	СтруктураОповещения.Вставить("ОбрабатыватьДокументы", ОбрабатыватьДокументы);
	СтруктураОповещения.Вставить("ЗаполнятьДокументы", ЗаполнятьДокументы);
	СтруктураОповещения.Вставить("УстанавливатьСтатусОтчетов", УстанавливатьСтатусОтчетов);
	
	Закрыть(СтруктураОповещения);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// Считаем параметры. 
	ОтображатьПредупреждениеЭтапы = Параметры.ОтображатьПредупреждениеЭтапы;
	//ОтображатьФлагОбработкиОтчетов =  Параметры.ОтображатьФлагОбработкиОтчетов;
	ОтображатьФлагЗаполненияОтчетов =  Параметры.ОтображатьФлагЗаполненияОтчетов;
	ОтображатьФлагУстановкиСтатусовОтчетов =  Параметры.ОтображатьФлагУстановкиСтатусовОтчетов;
	// Считаем таблицу организаций.
	Если ЭтоАдресВременногоХранилища(Параметры.АдресТЗОрганизаций) Тогда
		ТЗОрганизаций = ПолучитьИзВременногоХранилища(Параметры.АдресТЗОрганизаций);
		Для Каждого Стр из ТЗОрганизаций Цикл
			НС = ТаблицаОрганизаций.Добавить();
			НС.Организация = Стр.Организация;
		КонецЦикла;
		Действие = Параметры.Действие;
		ТекстЗаголовка = НСтр("ru = '%Действие% для следующих организаций:'");
		ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%Действие%", Строка(Действие));
		Заголовок = ТекстЗаголовка;
	Иначе
		// Не заполняем таблицу.
		Действие = "";
		ТекстЗаголовка = НСтр("ru = 'Выбор организаций:'");
		ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%Действие%", Строка(Действие));
		Заголовок = ТекстЗаголовка;
	КонецЕсли;
	// Установим параметр Выбран для строк организаций.
	ВыбранныеОрганизации = Параметры.ВыбранныеОрганизации;
	Для Каждого ТекТаблицаОрганизаций Из ТаблицаОрганизаций Цикл
		ТекОрганизация = ТекТаблицаОрганизаций.Организация;
		НайденныеОрганизации = ВыбранныеОрганизации.НайтиПоЗначению(ТекОрганизация);
		ВыбраннаяОрганизацияНайдена = (НайденныеОрганизации <> Неопределено);
		ТекТаблицаОрганизаций.Выбран = ВыбраннаяОрганизацияНайдена;
	КонецЦикла;
	// По умолчанию взведём флаги обработки документов.
	ЗаполнятьДокументы = ОтображатьФлагЗаполненияОтчетов;
	УстанавливатьСтатусОтчетов = ОтображатьФлагУстановкиСтатусовОтчетов;
	// Управление элементами.
	УправлениеДоступностью();
КонецПроцедуры

&НаСервере
Процедура ВыполнитьДействиеСервер()
	
	ТаблицаВыбранныхОрганизаций.Очистить();
	Для Каждого Стр из ТаблицаОрганизаций Цикл
		Если Стр.Выбран Тогда
			 Нстр = ТаблицаВыбранныхОрганизаций.Добавить();
			 Нстр.Организация = Стр.Организация; 	
		КонецЕсли;
	КонецЦикла;
    ВыбОрганизацииАдрес = ПоместитьВоВременноеХранилище(РеквизитФормыВЗначение("ТаблицаВыбранныхОрганизаций"),Новый УникальныйИдентификатор());
	
КонецПроцедуры	

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	Для Каждого Стр Из ТаблицаОрганизаций Цикл
		  Стр.Выбран = Истина;
	КонецЦикла;
	 
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	Для Каждого Стр Из ТаблицаОрганизаций Цикл
		 Стр.Выбран = Ложь;
	КонецЦикла;

КонецПроцедуры
