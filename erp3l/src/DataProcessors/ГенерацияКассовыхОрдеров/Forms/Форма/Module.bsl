
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(Объект, Параметры);
	
	Если НЕ (Параметры.Свойство("НачалоПериода") И Параметры.Свойство("КонецПериода")) Тогда
		Объект.НачалоПериода = ОбщегоНазначения.ТекущаяДатаПользователя();
		Объект.КонецПериода  = ОбщегоНазначения.ТекущаяДатаПользователя();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		Объект.Организация = ОбщегоНазначенияУХ.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	ОбновитьТаблицуКассовыеОрдераНаСервере();
	ОбновитьИтогиВПодвале();	
	
	УправлениеФормой(ЭтотОбъект);	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КассаПриИзменении(Элемент)
	ОбновитьСписокНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ОбновитьСписокНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	ОбновитьСписокНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	ОбновитьСписокНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКассовыеОрдера

&НаКлиенте
Процедура КассовыеОрдераФормироватьПриИзменении(Элемент)
	
	ТекДанные = Элементы.КассовыеОрдера.ТекущиеДанные;
	Если ТекДанные.СоздаватьКассовыеОрдера Тогда
		Знак = 1;
	Иначе
		Знак = -1;
	КонецЕсли;
	
	КоличествоКСозданию = КоличествоКСозданию + Знак;
	СуммаПриходКСозданию = СуммаПриходКСозданию + Знак * ТекДанные.СуммаПриход;
	СуммаРасходКСозданию = СуммаРасходКСозданию + Знак * ТекДанные.СуммаРасход;
КонецПроцедуры

&НаКлиенте
Процедура КассовыеОрдераВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПоказатьЗначение(,Элемент.ТекущиеДанные.ЗаявкаНаОперацию);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура КомандаОбновитьСписокЗаявок(Команда)
	
	ОбновитьСписокНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура СоздатьКассовыеОрдера(Команда)
	СоздатьКассовыеОрдераНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	ПараметрыВыбора = Новый Структура("НачалоПериода,КонецПериода", Объект.НачалоПериода, Объект.КонецПериода);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыВыбора,,,,, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	УстановитьОтметку(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметкуСоВсех(Команда)
	УстановитьОтметку(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура СписокСозданныхОбъектов(Команда)
	
	Если Объект.СозданныеДокументы.Количество() = 1 Тогда
		ПоказатьЗначение(, Объект.СозданныеДокументы[0].КассовыйОрдер);
	Иначе
		ОткрытьФорму("Обработка.ГенерацияКассовыхОрдеров.Форма.СозданныеДокументы", Новый Структура("Объект", Объект), ЭтаФорма);	
	КонецЕсли;

	
КонецПроцедуры

#КонецОбласти

#Область ЗавершениеНемодальныхВызовов

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект.НачалоПериода = РезультатВыбора.НачалоПериода;
	Объект.КонецПериода  = РезультатВыбора.КонецПериода;
	
	ОбновитьСписокНаСервере();	
КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьТаблицуКассовыеОрдераНаСервере();
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый Массив;
	ТекстыЗапроса.Добавить(ДенежныеСредстваВстраиваниеУХ.ПолучитьТекстЗапросаТаблицаКассовыеОрдера());
	
	Запрос.УстановитьПараметр("ДополнительныйОтбор", Истина);
	Запрос.УстановитьПараметр("ДатаНачало", НачалоДня(Объект.НачалоПериода));
	Запрос.УстановитьПараметр("ДатаОкончание", КонецДня(Объект.КонецПериода));
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Касса", Объект.Касса);
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстыЗапроса.Добавить("РазмещениеЗаявок.ЗаявкаНаОперацию.Организация = &Организация");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Касса) Тогда
		ТекстыЗапроса.Добавить("РазмещениеЗаявок.БанковскийСчетКасса = &Касса");
	КонецЕсли;
	
	Разделитель = Символы.ПС + "И ";
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, Разделитель);
	Объект.ЗаявкиКИсполнению.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры

&НаСервере
Процедура УстановитьОтметку(Отметка)
	
	Для каждого СтрокаДокумента Из Объект.ЗаявкиКИсполнению Цикл
		СтрокаДокумента.СоздаватьКассовыеОрдера = Отметка;
	КонецЦикла;
	
	ОбновитьИтогиВПодвале();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИтогиВПодвале()
	
	ДокументыКСозданию		= Объект.ЗаявкиКИсполнению.Выгрузить(Новый Структура("СоздаватьКассовыеОрдера", Истина));
	КоличествоКСозданию		= ДокументыКСозданию.Количество();
	СуммаПриходКСозданию	= ДокументыКСозданию.Итог("СуммаПриход");
	СуммаРасходКСозданию	= ДокументыКСозданию.Итог("СуммаРасход");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокНаСервере()
	
	ОбновитьТаблицуКассовыеОрдераНаСервере();
	ОбновитьИтогиВПодвале();
	
КонецПроцедуры

&НаСервере
Процедура СоздатьКассовыеОрдераНаСервере()
	РезультатПроверки = ЭтаФорма.ПроверитьЗаполнение();
	Если РезультатПроверки Тогда
		ДокументыКВыгрузке = Объект.ЗаявкиКИсполнению.НайтиСтроки(Новый Структура("СоздаватьКассовыеОрдера", Истина));
		СоответствиеДокументов = ДенежныеСредстваВстраиваниеУХ.СоздатьКассовыеОрдераПоВыгрузке(ДокументыКВыгрузке);
		Для Каждого ТекСоответствиеДокументов Из СоответствиеДокументов Цикл
			НовыйСозданныйДокумент = Объект.СозданныеДокументы.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйСозданныйДокумент, ТекСоответствиеДокументов.Ключ);
			НовыйСозданныйДокумент.КассовыйОрдер = ТекСоответствиеДокументов.Значение;
		КонецЦикла;	
		ОбновитьСписокНаСервере();
		УправлениеФормой(ЭтотОбъект);
	Иначе
		// Проверка не пройдена. Создание отменено.
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	КоличествоСозданныхДокументов = Объект.СозданныеДокументы.Количество();
	Если КоличествоСозданныхДокументов > 0 Тогда
		ЗаголовокКомандыСозданныеОбъекты = СтрШаблон(НСтр("ru = 'Созданные документы (%1)'"), КоличествоСозданныхДокументов);
	Иначе
		ЗаголовокКомандыСозданныеОбъекты = НСтр("ru = 'Созданные документы'");
	КонецЕсли;
	Элементы.ФормаСписокСозданныхОбъектов.Заголовок = ЗаголовокКомандыСозданныеОбъекты;
	
	Элементы.ФормаСписокСозданныхОбъектов.Доступность = (КоличествоСозданныхДокументов > 0);
	
	
КонецПроцедуры

#КонецОбласти
