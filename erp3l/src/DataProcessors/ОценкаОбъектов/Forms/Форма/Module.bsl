

#Область ОбработкаОсновныхСобытийФормы


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьТекущегоСотрудника();
	
	Если Параметры.Свойство("ВладелецОбъектовВыбора", ВладелецОбъектовВыбора) Тогда
		// Проверяем, что объект выбора доступен для оценки пользователем
		Если НЕ ВыборОбъектовУХ.СотрудникМожетОцениватьПоВладельцу(Сотрудник, ВладелецОбъектовВыбора) Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон(Нстр("ru = 'Вам недоступна возможность оценивать для ""%1""'"), ВладелецОбъектовВыбора);
			Сообщение.Сообщить();
			
			ВладелецОбъектовВыбора = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьНастройкиПроцессаВыбора(Ложь);
	ОбработатьИзменениеОбъектаОценки(); // Скрыть значения критериев
	// Установка заголовка поля Контрагент в таблице.
	Если ТипЗнч(ВладелецОбъектовВыбора) = Тип("СправочникСсылка.РазделыИнвестиционныхПрограмм") Тогда
		Элементы.ОбъектыОценкиКонтрагент.Заголовок = НСтр("ru = 'Основной контрагент'");
	ИначеЕсли ТипЗнч(ВладелецОбъектовВыбора) = Тип("СправочникСсылка.Лоты") Тогда
		Элементы.ОбъектыОценкиКонтрагент.Заголовок = НСтр("ru = 'Поставщик'");
	Иначе
		Элементы.ОбъектыОценкиКонтрагент.Заголовок = НСтр("ru = 'Контрагент'");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимостьКомандыРасчетаЗначений();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьСостояниеВыбораАльтернатив" Тогда
		Если ТипЗнч(Параметр) <> Тип("Структура") Тогда
			Возврат;
		КонецЕсли;
		
		Если Параметр.Идентификатор = ЭтаФорма.УникальныйИдентификатор Тогда
			Возврат;
		КонеЦЕсли;
		
		Если Параметр.Владелец <> ВладелецОбъектовВыбора Тогда
			Возврат;
		КонецЕсли;
		
		ВладелецОбъектовВыбораСтарый = Неопределено;
		ВладелецПриИзмененииНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


#Область ОбработкаСобытийЭлементовФормы


&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	УстановитьВидимостьКомандыРасчетаЗначений();
	ВладелецПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ВладелецПриИзмененииНаСервере()
	ЗаполнитьНастройкиПроцессаВыбора();
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыОценкиПриАктивизацииСтроки(Элемент)
	ТекДанные = Элементы.ОбъектыОценки.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		ОбъектОценки = Неопределено;
	Иначе
		ОбъектОценки = ТекДанные.ОбъектОценки;
	КонеЦЕсли;
	
	Если ОбъектОценки <> ОбъектОценкиСтарый Тогда
		ОбработатьИзменениеОбъектаОценки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначенияКритериевПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	ТекДанные = Элементы.ЗначенияКритериев.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекСтрокаОбъекта = Элементы.ОбъектыОценки.ТекущиеДанные;
	Если ТекСтрокаОбъекта = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ТекСтрокаОбъекта.ОценкаЗавершена Тогда
		Возврат;
	КонецЕсли;
	
	ДопПараметры = Новый Структура("МаксимальноеЗначение, МинимальноеЗначение", ТекДанные.МаксимальноеЗначение, ТекДанные.МинимальноеЗначение);
	
	Если ТекДанные.ВыборИзМножества Тогда
		
		Значение_= ТекДанные.ЗначениеМножества;
		Если ЗначениеЗаполнено(Значение_) Тогда
			ДопПараметры.Вставить("ТекущаяСтрока", Значение_);
		КонецЕсли;
		ДопПараметры.Вставить("ЗначениеМножества", Значение_);
		ДопПараметры.Вставить("Отбор", Новый Структура("Владелец",ТекДанные.Критерий));
		ДопПараметры.Вставить("РежимВыбора", Истина);
		ДопПараметры.Вставить("ЗакрыватьПриВыборе", Истина);
		
		ОбработкаВводаЗначения = Новый ОписаниеОповещения("ОбработатьВыбораЗначенияКритерияИзМножества",
			ЭтаФорма, ДопПараметры);
		ОткрытьФорму("Справочник.ЗначенияПеречислимыхКритериев.ФормаВыбора", ДопПараметры, ЭтаФорма,,,,
			ОбработкаВводаЗначения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	Иначе
		
		Значение_= ТекДанные.Значение;
		
		ОбработкаВводаЗначения = Новый ОписаниеОповещения("ОбработатьВводЗначенияКритерия", ЭтаФорма, ДопПараметры);
		Если ЗначениеЗаполнено(ТекДанные.МаксимальноеЗначение) Тогда
			ЗаголовокВвода = СтрШаблон(Нстр("ru = 'Введите значение критерия от %1 до %2'"), 
				ТекДанные.МинимальноеЗначение, ТекДанные.МаксимальноеЗначение);
		Иначе	
			ЗаголовокВвода = СтрШаблон(Нстр("ru = 'Введите значение критерия от %1'"), 
				ТекДанные.МинимальноеЗначение);
		КонецЕсли;
		
		ПоказатьВводЧисла(ОбработкаВводаЗначения, Значение_, ЗаголовокВвода, 15, 3);
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыОценкиОценкаЗавершенаПриИзменении(Элемент)
	ОбработатьИзменениеПризнакаЗавершенияОценки();
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеПризнакаЗавершенияОценки()
	
	ТекДанные = Элементы.ОбъектыОценки.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеЗначения = Новый Структура;
	ОписаниеЗначения.Вставить("Владелец", ВладелецОбъектовВыбора);
	ОписаниеЗначения.Вставить("Ответственный", Сотрудник);
	ОписаниеЗначения.Вставить("ОбъектОценки", ТекДанные.ОбъектОценки);
	ОписаниеЗначения.Вставить("ЭтапОценки", ЭтапОценки);
	ОписаниеЗначения.Вставить("Завершен", ТекДанные.ОценкаЗавершена);
	
	УстановитьПризнакЗавершенияОценкиНаСервере(ОписаниеЗначения);
	
	ДопПараметры = Новый Структура("Владелец,Идентификатор", ВладелецОбъектовВыбора, ЭтаФорма.УникальныйИдентификатор);
	Оповестить("ОбновитьСостояниеВыбораАльтернатив", ДопПараметры);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокОбъектов(Команда)
	ВладелецОбъектовВыбораСтарый = Неопределено;
	ЗаполнитьНастройкиПроцессаВыбора();
КонецПроцедуры

&НаКлиенте
Процедура ВладелецОбъектовВыбораНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормыВыбора = Новый Структура("Ответственный,ПоказыватьТолькоНезавершенные,ТолькоСПравомВыбора", Сотрудник, Ложь, Ложь);
	ОткрытьФорму("ОбщаяФорма.ВыборВладельцаОбъектовВыбора", ПараметрыФормыВыбора, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ЗначенияРассчетныхКритериевПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыОценкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекДанные = Элементы.ОбъектыОценки.ТекущиеДанные;
	Если ТекДанные = Неопределено  Тогда
		Возврат;
	КонецЕсли;
	Если Поле.Имя = "ОбъектыОценкиОбъектОценки" Тогда
		Значение = ТекДанные.ОбъектОценки;
		Если ЗначениеЗаполнено(Значение) Тогда
			ПоказатьЗначение(, Значение);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьЗначенияПоказателей(Команда)
	ВыборОбъектовВызовСервераУХ.РассчитатьЗначенияКритериев(ВладелецОбъектовВыбора);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьКомандыРасчетаЗначений()
	флВидимостьРасчета = Ложь;
	Если ЗначениеЗаполнено(ВладелецОбъектовВыбора) Тогда
		ТекущаяСтрока = Элементы.ЗначенияРассчетныхКритериев.ТекущаяСтрока;
		Если ТекущаяСтрока <> Неопределено Тогда
			флВидимостьРасчета = Истина;
		КонецЕсли;
	КонецЕсли;
		
	Элементы.ФормаРассчитатьЗначенияПоказателей.Видимость = флВидимостьРасчета;
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыНаКлиенте


&НаКлиенте
Процедура ОбработатьВыбораЗначенияКритерияИзМножества(ЗначениеИзМножества, ДопПараметры) Экспорт 
	Если ЗначениеИзМножества = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекДанные = Элементы.ЗначенияКритериев.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДопПараметры.ЗначениеМножества = ЗначениеИзМножества Тогда
		Возврат;
	КонецЕсли;
	
	ДопПараметры.ЗначениеМножества = ЗначениеИзМножества;
	
	ДопПараметры.Вставить("ОбъектОценки", ОбъектОценки);
	ДопПараметры.Вставить("Критерий", ТекДанные.Критерий);
	ДопПараметры.Вставить("Вес", ТекДанные.Вес);
	ОбработатьВводЗначенияКритерияНаСервере(0, ДопПараметры);
	
	ДопПараметры = Новый Структура("Владелец,Идентификатор", ВладелецОбъектовВыбора, ЭтаФорма.УникальныйИдентификатор);
	Оповестить("ОбновитьСостояниеВыбораАльтернатив", ДопПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВводЗначенияКритерия(Значение, ДопПараметры) Экспорт 
	Если Значение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекДанные = Элементы.ЗначенияКритериев.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Значение < ДопПараметры.МинимальноеЗначение Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон(Нстр("ru = 'Введенное значение %1 меньше допустимого порога %2'"), Значение, 
			ДопПараметры.МинимальноеЗначение);
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если ДопПараметры.МаксимальноеЗначение <> 0 И Значение > ДопПараметры.МаксимальноеЗначение Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон(Нстр("ru = 'Введенное значение %1 превышает допустимый порог %2'"), Значение, 
			ДопПараметры.МаксимальноеЗначение);
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	ДопПараметры.Вставить("ОбъектОценки", ОбъектОценки);
	ДопПараметры.Вставить("Критерий", ТекДанные.Критерий);
	ДопПараметры.Вставить("Вес", ТекДанные.Вес);
	ОбработатьВводЗначенияКритерияНаСервере(Значение, ДопПараметры);
	
	ДопПараметры = Новый Структура("Владелец,Идентификатор", ВладелецОбъектовВыбора, ЭтаФорма.УникальныйИдентификатор);
	Оповестить("ОбновитьСостояниеВыбораАльтернатив", ДопПараметры);
	
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыНаСервере

&НаСервере
Процедура ЗаполнитьНастройкиПроцессаВыбора(ПроверятьСтарогоВладельца=Истина)
	Если ПроверятьСтарогоВладельца И ВладелецОбъектовВыбораСтарый = ВладелецОбъектовВыбора Тогда
		Возврат;
	КонецЕсли;
	
	флРаботаСотрудникаВыполнена = Ложь;
	
	Если ЗначениеЗаполнено(ВладелецОбъектовВыбора) Тогда
		НастройкаПроцессаВыбора = ВыборОбъектовУХ.НастройкаПроцессаВыбораВладельца(ВладелецОбъектовВыбора);
	Иначе
		НастройкаПроцессаВыбора = Справочники.НастройкиПроцессаВыбора.ПустаяСсылка();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НастройкаПроцессаВыбора) Тогда
		
		ЭтапыОценки.Загрузить(НастройкаПроцессаВыбора.ЭтапыВыбора.Выгрузить());
		ОписаниеСостояния = ВыборОбъектовУХ.ПолучитьСостояниеПроцессаСУчетомПереторжки(ВладелецОбъектовВыбора);
	
		ЭтапОценки = ОписаниеСостояния.ТекущийЭтапВыбора;
		
		флВыборЗавершен = ОписаниеСостояния.ПроцессВыбораЗавершен;
		
		флЭтоПоследующаяОценка = флВыборЗавершен И НастройкаПроцессаВыбора.ПоследующаяОценка;
		Если флЭтоПоследующаяОценка Тогда
			ЭтапОценки = Справочники.ЭтапыОценки.ПоследующаяОценка;
			флВыборЗавершен = ЛОжь;
		КонецЕсли;
		
		мКритерииЭтапа = ВыборОбъектовУХ.ПолучитьКритерииЭтапаДляФункциональногоНаправления(
			НастройкаПроцессаВыбора,
			ЭтапОценки,
			ФункциональноеНаправлениеСотрудника);
		КоличествоКритериев = мКритерииЭтапа.Количество();
		
		Элементы.ДекорацияВыборЗавершен.Видимость = флВыборЗавершен;
		
	Иначе
		ЭтапыОценки.Очистить();
		ЭтапОценки = Справочники.ЭтапыОценки.ПустаяСсылка();
		флВыборЗавершен = Истина;
		флЭтоПоследующаяОценка = Ложь;
		ОбъектыОценки.Очистить();
		РазрешитьИзменятьЭтапОценки = Ложь;
		КоличествоКритериев = 0;
		
		Элементы.ДекорацияВыборЗавершен.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ОбъектыОценкиОценкаЗавершена.Видимость = НЕ флЭтоПоследующаяОценка;
	
	ОбработатьИзменениеЭтапаНаСервере();
	
	ВладелецОбъектовВыбораСтарый = ВладелецОбъектовВыбора;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуОбъектовВыбора()
		
	Если ЗначениеЗаполнено(ВладелецОбъектовВыбора) И ЗначениеЗаполнено(Сотрудник)
		И ЗначениеЗаполнено(ЭтапОценки) Тогда

		Запрос = Новый Запрос;
		
		Если флЭтоПоследующаяОценка Тогда
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	МАКСИМУМ(ЗначенияКритериевОценки.Значение) КАК Значение,
				|	ЗначенияКритериевОценки.ЭтапОценки КАК ЭтапОценки,
				|	ЗначенияКритериевОценки.Критерий КАК Критерий
				|ПОМЕСТИТЬ МаксимальныеЗначения
				|ИЗ
				|	РегистрСведений.ЗначенияКритериевОценки КАК ЗначенияКритериевОценки
				|ГДЕ
				|	ЗначенияКритериевОценки.Владелец = &Владелец
				|	И НЕ ЗначенияКритериевОценки.НеЗаполнен
				|
				|СГРУППИРОВАТЬ ПО
				|	ЗначенияКритериевОценки.Критерий,
				|	ЗначенияКритериевОценки.ЭтапОценки
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ЗначенияКритериевОценки.ОбъектОценки КАК ОбъектОценки,
				|	ЗначенияКритериевОценки.Ответственный КАК Ответственный,
				|	СУММА(ВЫБОР
				|			КОГДА ЗначенияКритериевОценки.НеЗаполнен
				|				ТОГДА 0
				|			ИНАЧЕ ВЫБОР
				|					КОГДА НастройкиПроцессаВыбораКритерииОценки.НеИспользоватьПреобразованиеВБаллы
				|						ТОГДА ВЫБОР
				|								КОГДА ЕСТЬNULL(МаксимальныеЗначения.Значение, 0) - НастройкиПроцессаВыбораКритерииОценки.МинимальноеЗначение = 0
				|									ТОГДА ЗначенияКритериевОценки.Значение - НастройкиПроцессаВыбораКритерииОценки.МинимальноеЗначение
				|								ИНАЧЕ (ЗначенияКритериевОценки.Значение - НастройкиПроцессаВыбораКритерииОценки.МинимальноеЗначение) / (ЕСТЬNULL(МаксимальныеЗначения.Значение, 0) - НастройкиПроцессаВыбораКритерииОценки.МинимальноеЗначение)
				|							КОНЕЦ
				|					ИНАЧЕ ЗначенияКритериевОценки.БалльноеЗначение / 5
				|				КОНЕЦ * ЗначенияКритериевОценки.Вес
				|		КОНЕЦ) КАК Оценка,
				|	СУММА(ВЫБОР
				|			КОГДА ЗначенияКритериевОценки.НеЗаполнен
				|				ТОГДА 0
				|			ИНАЧЕ 1
				|		КОНЕЦ) КАК КоличествоЗаполненных,
				|	ЕСТЬNULL(ФункциональныеНаправленияФизическихЛиц.ФункциональноеНаправление, ЗНАЧЕНИЕ(Справочник.ФункциональныеНаправления.ПустаяСсылка)) КАК ФункциональноеНаправление,
				|	ЗначенияКритериевОценки.Критерий.ФункциональноеНаправление КАК ФункциональноеНаправлениеКритерия,
				|	ЗначенияКритериевОценки.ЭтапОценки КАК ЭтапОценки
				|ПОМЕСТИТЬ ЗначенияКритериев
				|ИЗ
				|	РегистрСведений.ЗначенияКритериевОценки КАК ЗначенияКритериевОценки
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НастройкиПроцессаВыбора.КритерииОценки КАК НастройкиПроцессаВыбораКритерииОценки
				|		ПО ЗначенияКритериевОценки.Критерий = НастройкиПроцессаВыбораКритерииОценки.КритерийОценки
				|			И (НастройкиПроцессаВыбораКритерииОценки.Ссылка = &НастройкаПроцессаВыбора)
				|			И (ЗначенияКритериевОценки.Владелец = &Владелец)
				|			И ЗначенияКритериевОценки.ЭтапОценки = НастройкиПроцессаВыбораКритерииОценки.ЭтапВыбора
				|			И (ЗначенияКритериевОценки.ЭтапОценки = &ЭтапОценки)
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФункциональныеНаправленияФизическихЛиц КАК ФункциональныеНаправленияФизическихЛиц
				|		ПО ЗначенияКритериевОценки.Ответственный = ФункциональныеНаправленияФизическихЛиц.ФизическоеЛицо
				|		ЛЕВОЕ СОЕДИНЕНИЕ МаксимальныеЗначения КАК МаксимальныеЗначения
				|		ПО ЗначенияКритериевОценки.ЭтапОценки = МаксимальныеЗначения.ЭтапОценки
				|			И ЗначенияКритериевОценки.Критерий = МаксимальныеЗначения.Критерий
				|ГДЕ
				|	(НастройкиПроцессаВыбораКритерииОценки.КритерийОценки.ФункциональноеНаправление = ЕСТЬNULL(ФункциональныеНаправленияФизическихЛиц.ФункциональноеНаправление, ЗНАЧЕНИЕ(Справочник.ФункциональныеНаправления.ПустаяСсылка))
				|			ИЛИ НастройкиПроцессаВыбораКритерииОценки.КритерийОценки.ФункциональноеНаправление = ЗНАЧЕНИЕ(Справочник.ФункциональныеНаправления.ПустаяСсылка))
				|
				|СГРУППИРОВАТЬ ПО
				|	ЗначенияКритериевОценки.ОбъектОценки,
				|	ЗначенияКритериевОценки.Ответственный,
				|	ЕСТЬNULL(ФункциональныеНаправленияФизическихЛиц.ФункциональноеНаправление, ЗНАЧЕНИЕ(Справочник.ФункциональныеНаправления.ПустаяСсылка)),
				|	ЗначенияКритериевОценки.Критерий.ФункциональноеНаправление,
				|	ЗначенияКритериевОценки.ЭтапОценки
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	СУММА(ЗначенияКритериев.КоличествоЗаполненных) КАК КоличествоЗаполненных,
				|	ЗначенияКритериев.ОбъектОценки КАК ОбъектОценки
				|ПОМЕСТИТЬ КоличествоКритериевСотрудника
				|ИЗ
				|	ЗначенияКритериев КАК ЗначенияКритериев
				|ГДЕ
				|	ЗначенияКритериев.Ответственный = &Ответственный
				|	И (ЗначенияКритериев.ФункциональноеНаправление = ЗначенияКритериев.ФункциональноеНаправлениеКритерия
				|			ИЛИ ЗначенияКритериев.ФункциональноеНаправлениеКритерия = ЗНАЧЕНИЕ(Справочник.ФункциональныеНаправления.ПустаяСсылка))
				|	И ЗначенияКритериев.ЭтапОценки = &ЭтапОценки
				|
				|СГРУППИРОВАТЬ ПО
				|	ЗначенияКритериев.ОбъектОценки
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ЗначенияКритериев.ОбъектОценки КАК ОбъектОценки,
				|	СУММА(ЗначенияКритериев.Оценка) КАК Оценка
				|ПОМЕСТИТЬ ИнтегральнаяОценкаОбъекта
				|ИЗ
				|	ЗначенияКритериев КАК ЗначенияКритериев
				|
				|СГРУППИРОВАТЬ ПО
				|	ЗначенияКритериев.ОбъектОценки
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|УНИЧТОЖИТЬ ЗначенияКритериев
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ОбъектыВыбора.Объект КАК ОбъектОценки,
				|	ЕСТЬNULL(ИнтегральнаяОценкаОбъекта.Оценка, 0) КАК Оценка,
				|	ЕСТЬNULL(КоличествоКритериевСотрудника.КоличествоЗаполненных, 0) КАК КоличествоЗаполненных,
				|	&КоличествоКритериев КАК КоличествоКритериев,
				|	&КоличествоКритериев - ЕСТЬNULL(КоличествоКритериевСотрудника.КоличествоЗаполненных, 0) КАК НеЗаполнено,
				|	ЕСТЬNULL(СостояниеОценкиОбъектов.Завершен, ЛОЖЬ) КАК ОценкаЗавершена,
				|	ВЫБОР
				|		КОГДА ОбъектыВыбора.Объект ССЫЛКА Документ.ПредложениеПоставщика
				|			ТОГДА ОбъектыВыбора.Объект.Контрагент
				|		КОГДА ОбъектыВыбора.Объект ССЫЛКА Справочник.Проекты
				|			ТОГДА ОбъектыВыбора.Объект.ОсновнойКонтрагент
				|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
				|	КОНЕЦ КАК Поле1
				|ИЗ
				|	РегистрСведений.ОбъектыВыбора КАК ОбъектыВыбора
				|		ЛЕВОЕ СОЕДИНЕНИЕ КоличествоКритериевСотрудника КАК КоличествоКритериевСотрудника
				|		ПО ОбъектыВыбора.Объект = КоличествоКритериевСотрудника.ОбъектОценки
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеОценкиОбъектов КАК СостояниеОценкиОбъектов
				|		ПО ОбъектыВыбора.Объект = СостояниеОценкиОбъектов.ОбъектОценки
				|			И (СостояниеОценкиОбъектов.Ответственный = &Ответственный)
				|			И (СостояниеОценкиОбъектов.ЭтапОценки = &ЭтапОценки)
				|			И (СостояниеОценкиОбъектов.Владелец = &Владелец)
				|			И ОбъектыВыбора.Владелец = СостояниеОценкиОбъектов.Владелец
				|		ЛЕВОЕ СОЕДИНЕНИЕ ИнтегральнаяОценкаОбъекта КАК ИнтегральнаяОценкаОбъекта
				|		ПО ОбъектыВыбора.Объект = ИнтегральнаяОценкаОбъекта.ОбъектОценки
				|ГДЕ
				|	ОбъектыВыбора.Владелец = &Владелец
				|	И ОбъектыВыбора.ЭтапВыбора = &ЭтапОценки";
				
		Иначе // это оценка в рамках процедуры выбора
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	МАКСИМУМ(ЗначенияКритериевОценки.Значение) КАК Значение,
				|	ЗначенияКритериевОценки.ЭтапОценки КАК ЭтапОценки,
				|	ЗначенияКритериевОценки.Критерий КАК Критерий
				|ПОМЕСТИТЬ МаксимальныеЗначения
				|ИЗ
				|	РегистрСведений.ЗначенияКритериевОценки КАК ЗначенияКритериевОценки
				|ГДЕ
				|	ЗначенияКритериевОценки.Владелец = &Владелец
				|	И НЕ ЗначенияКритериевОценки.НеЗаполнен
				|
				|СГРУППИРОВАТЬ ПО
				|	ЗначенияКритериевОценки.Критерий,
				|	ЗначенияКритериевОценки.ЭтапОценки
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ЗначенияКритериевОценки.ОбъектОценки КАК ОбъектОценки,
				|	ЗначенияКритериевОценки.Ответственный КАК Ответственный,
				|	СУММА(ВЫБОР
				|			КОГДА ЗначенияКритериевОценки.НеЗаполнен
				|				ТОГДА 0
				|			ИНАЧЕ ВЫБОР
				|					КОГДА НастройкиПроцессаВыбораКритерииОценки.НеИспользоватьПреобразованиеВБаллы
				|						ТОГДА ВЫБОР
				|								КОГДА ЕСТЬNULL(МаксимальныеЗначения.Значение, 0) - НастройкиПроцессаВыбораКритерииОценки.МинимальноеЗначение = 0
				|									ТОГДА ЗначенияКритериевОценки.Значение - НастройкиПроцессаВыбораКритерииОценки.МинимальноеЗначение
				|								ИНАЧЕ (ЗначенияКритериевОценки.Значение - НастройкиПроцессаВыбораКритерииОценки.МинимальноеЗначение) / (ЕСТЬNULL(МаксимальныеЗначения.Значение, 0) - НастройкиПроцессаВыбораКритерииОценки.МинимальноеЗначение)
				|							КОНЕЦ
				|					ИНАЧЕ ЗначенияКритериевОценки.БалльноеЗначение / 5
				|				КОНЕЦ * ЗначенияКритериевОценки.Вес
				|		КОНЕЦ) КАК Оценка,
				|	СУММА(ВЫБОР
				|			КОГДА ЗначенияКритериевОценки.НеЗаполнен
				|				ТОГДА 0
				|			ИНАЧЕ 1
				|		КОНЕЦ) КАК КоличествоЗаполненных,
				|	ЕСТЬNULL(ФункциональныеНаправленияФизическихЛиц.ФункциональноеНаправление, ЗНАЧЕНИЕ(Справочник.ФункциональныеНаправления.ПустаяСсылка)) КАК ФункциональноеНаправление,
				|	ЗначенияКритериевОценки.Критерий.ФункциональноеНаправление КАК ФункциональноеНаправлениеКритерия,
				|	ЗначенияКритериевОценки.ЭтапОценки КАК ЭтапОценки
				|ПОМЕСТИТЬ ЗначенияКритериев
				|ИЗ
				|	РегистрСведений.ЗначенияКритериевОценки КАК ЗначенияКритериевОценки
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НастройкиПроцессаВыбора.КритерииОценки КАК НастройкиПроцессаВыбораКритерииОценки
				|		ПО ЗначенияКритериевОценки.Критерий = НастройкиПроцессаВыбораКритерииОценки.КритерийОценки
				|			И (НастройкиПроцессаВыбораКритерииОценки.Ссылка = &НастройкаПроцессаВыбора)
				|			И (ЗначенияКритериевОценки.Владелец = &Владелец)
				|			И ЗначенияКритериевОценки.ЭтапОценки = НастройкиПроцессаВыбораКритерииОценки.ЭтапВыбора
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФункциональныеНаправленияФизическихЛиц КАК ФункциональныеНаправленияФизическихЛиц
				|		ПО ЗначенияКритериевОценки.Ответственный = ФункциональныеНаправленияФизическихЛиц.ФизическоеЛицо
				|		ЛЕВОЕ СОЕДИНЕНИЕ МаксимальныеЗначения КАК МаксимальныеЗначения
				|		ПО ЗначенияКритериевОценки.ЭтапОценки = МаксимальныеЗначения.ЭтапОценки
				|			И ЗначенияКритериевОценки.Критерий = МаксимальныеЗначения.Критерий
				|ГДЕ
				|	(НастройкиПроцессаВыбораКритерииОценки.КритерийОценки.ФункциональноеНаправление = ЕСТЬNULL(ФункциональныеНаправленияФизическихЛиц.ФункциональноеНаправление, ЗНАЧЕНИЕ(Справочник.ФункциональныеНаправления.ПустаяСсылка))
				|			ИЛИ НастройкиПроцессаВыбораКритерииОценки.КритерийОценки.ФункциональноеНаправление = ЗНАЧЕНИЕ(Справочник.ФункциональныеНаправления.ПустаяСсылка))
				|
				|СГРУППИРОВАТЬ ПО
				|	ЗначенияКритериевОценки.ОбъектОценки,
				|	ЗначенияКритериевОценки.Ответственный,
				|	ЕСТЬNULL(ФункциональныеНаправленияФизическихЛиц.ФункциональноеНаправление, ЗНАЧЕНИЕ(Справочник.ФункциональныеНаправления.ПустаяСсылка)),
				|	ЗначенияКритериевОценки.Критерий.ФункциональноеНаправление,
				|	ЗначенияКритериевОценки.ЭтапОценки
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	СУММА(ЗначенияКритериев.КоличествоЗаполненных) КАК КоличествоЗаполненных,
				|	ЗначенияКритериев.ОбъектОценки КАК ОбъектОценки
				|ПОМЕСТИТЬ КоличествоКритериевСотрудника
				|ИЗ
				|	ЗначенияКритериев КАК ЗначенияКритериев
				|ГДЕ
				|	ЗначенияКритериев.Ответственный = &Ответственный
				|	И (ЗначенияКритериев.ФункциональноеНаправление = ЗначенияКритериев.ФункциональноеНаправлениеКритерия
				|			ИЛИ ЗначенияКритериев.ФункциональноеНаправлениеКритерия = ЗНАЧЕНИЕ(Справочник.ФункциональныеНаправления.ПустаяСсылка))
				|	И ЗначенияКритериев.ЭтапОценки = &ЭтапОценки
				|
				|СГРУППИРОВАТЬ ПО
				|	ЗначенияКритериев.ОбъектОценки
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ЗначенияКритериев.ОбъектОценки КАК ОбъектОценки,
				|	СУММА(ЗначенияКритериев.Оценка) КАК Оценка
				|ПОМЕСТИТЬ ИнтегральнаяОценкаОбъекта
				|ИЗ
				|	ЗначенияКритериев КАК ЗначенияКритериев
				|
				|СГРУППИРОВАТЬ ПО
				|	ЗначенияКритериев.ОбъектОценки
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|УНИЧТОЖИТЬ ЗначенияКритериев
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
				|	ПереторжкаЗакупокСрезПоследних.ЗакупочнаяПроцедура КАК ЗакупочнаяПроцедура,
				|	ПереторжкаЗакупокСрезПоследних.НомерПереторжки КАК НомерПереторжки
				|ПОМЕСТИТЬ ВТ_НомерПереторжки
				|ИЗ
				|	РегистрСведений.ПереторжкаЗакупок.СрезПоследних КАК ПереторжкаЗакупокСрезПоследних
				|ГДЕ
				|	ВЫБОР
				|			КОГДА &ЗакупочнаяПроцедура = ЗНАЧЕНИЕ(Справочник.ЗакупочныеПроцедуры.ПустаяСсылка)
				|				ТОГДА ИСТИНА
				|			ИНАЧЕ ПереторжкаЗакупокСрезПоследних.ЗакупочнаяПроцедура = &ЗакупочнаяПроцедура
				|		КОНЕЦ
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ОбъектыВыбора.Объект КАК ОбъектОценки,
				|	ЕСТЬNULL(ИнтегральнаяОценкаОбъекта.Оценка, 0) КАК Оценка,
				|	ЕСТЬNULL(КоличествоКритериевСотрудника.КоличествоЗаполненных, 0) КАК КоличествоЗаполненных,
				|	&КоличествоКритериев КАК КоличествоКритериев,
				|	&КоличествоКритериев - ЕСТЬNULL(КоличествоКритериевСотрудника.КоличествоЗаполненных, 0) КАК НеЗаполнено,
				|	ЕСТЬNULL(СостояниеОценкиОбъектов.Завершен, ЛОЖЬ) КАК ОценкаЗавершена,
				|	ОбъектыВыбора.Объект.НомерПереторжки КАК ОбъектНомерПереторжки,
				|	ЕСТЬNULL(ВТ_НомерПереторжки.НомерПереторжки, 0) КАК НомерПереторжки,
				|	ВЫБОР
				|		КОГДА ОбъектыВыбора.Объект ССЫЛКА Документ.ПредложениеПоставщика
				|			ТОГДА ОбъектыВыбора.Объект.Контрагент
				|		КОГДА ОбъектыВыбора.Объект ССЫЛКА Справочник.Проекты
				|			ТОГДА ОбъектыВыбора.Объект.ОсновнойКонтрагент
				|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
				|	КОНЕЦ КАК Контрагент
				|ИЗ
				|	РегистрСведений.ОбъектыВыбора КАК ОбъектыВыбора
				|		ЛЕВОЕ СОЕДИНЕНИЕ КоличествоКритериевСотрудника КАК КоличествоКритериевСотрудника
				|		ПО ОбъектыВыбора.Объект = КоличествоКритериевСотрудника.ОбъектОценки
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеОценкиОбъектов КАК СостояниеОценкиОбъектов
				|		ПО ОбъектыВыбора.Объект = СостояниеОценкиОбъектов.ОбъектОценки
				|			И (СостояниеОценкиОбъектов.Ответственный = &Ответственный)
				|			И (СостояниеОценкиОбъектов.ЭтапОценки = &ЭтапОценки)
				|			И (СостояниеОценкиОбъектов.Владелец = &Владелец)
				|			И ОбъектыВыбора.Владелец = СостояниеОценкиОбъектов.Владелец
				|		ЛЕВОЕ СОЕДИНЕНИЕ ИнтегральнаяОценкаОбъекта КАК ИнтегральнаяОценкаОбъекта
				|		ПО ОбъектыВыбора.Объект = ИнтегральнаяОценкаОбъекта.ОбъектОценки
				|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НомерПереторжки КАК ВТ_НомерПереторжки
				|		ПО ОбъектыВыбора.Объект.Лот.Владелец = ВТ_НомерПереторжки.ЗакупочнаяПроцедура
				|ГДЕ
				|	ОбъектыВыбора.Владелец = &Владелец
				|	И ОбъектыВыбора.ЭтапВыбора = &ЭтапОценки
				|	И ВЫБОР
				|			КОГДА &ЗакупочнаяПроцедура = ЗНАЧЕНИЕ(Справочник.ЗакупочныеПроцедуры.ПустаяСсылка)
				|				ТОГДА ИСТИНА
				|			ИНАЧЕ ОбъектыВыбора.Объект.НомерПереторжки = ЕСТЬNULL(ВТ_НомерПереторжки.НомерПереторжки, 0)
				|		КОНЕЦ";
		КонецЕсли;
		
		Запрос.УстановитьПараметр("Владелец", ВладелецОбъектовВыбора);
		Запрос.УстановитьПараметр("НастройкаПроцессаВыбора", ВыборОбъектовУХ.НастройкаПроцессаВыбораВладельца(ВладелецОбъектовВыбора));
		Запрос.УстановитьПараметр("Ответственный", Сотрудник);
		Запрос.УстановитьПараметр("ЭтапОценки", ЭтапОценки);
		Запрос.УстановитьПараметр("КоличествоКритериев", КоличествоКритериев);
		Если ТипЗнч(ВладелецОбъектовВыбора) = Тип("СправочникСсылка.Лоты") Тогда
			Запрос.УстановитьПараметр("ЗакупочнаяПроцедура", ВладелецОбъектовВыбора.Владелец);
		Иначе
			Запрос.УстановитьПараметр("ЗакупочнаяПроцедура", Справочники.ЗакупочныеПроцедуры.ПустаяСсылка());
		КонецЕсли;
		
		РезультатЗапроса = Запрос.Выполнить();
		Выгрузка = РезультатЗапроса.Выгрузить();
		ОбъектыОценки.Загрузить(Выгрузка);
		
	Иначе
		ОбъектыОценки.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеОбъектаОценки()
	
	//Элементы.ЗначенияКритериев.Видимость = НЕ флВыборЗавершен;
	
	флДобавитьКритерии = ЗначениеЗаполнено(Сотрудник) и ЗначениеЗаполнено(ВладелецОбъектовВыбора)
		и ЗначениеЗаполнено(ОбъектОценки) и ЗначениеЗаполнено(ЭтапОценки) и НЕ флВыборЗавершен;
		
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(ЗначенияКритериев, "Владелец", ВладелецОбъектовВыбора, Истина);
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(ЗначенияКритериев, "Ответственный", Сотрудник, Истина);
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(ЗначенияКритериев, "ОбъектОценки", ОбъектОценки, Истина);
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(ЗначенияКритериев, "ЭтапОценки", ЭтапОценки, Истина);
	
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(ЗначенияРассчетныхКритериев, "Владелец", ВладелецОбъектовВыбора, Истина);
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(ЗначенияРассчетныхКритериев, "ОбъектОценки", ОбъектОценки, Истина);
	ОтборыСписковКлиентСерверУХ.ИзменитьЭлементОтбораСписка(ЗначенияРассчетныхКритериев, "ЭтапОценки", ЭтапОценки, Истина);
	
	Если флДобавитьКритерии Тогда
		ДобавитьЗначенияНезаполненныхКритериевНаСервере(ВладелецОбъектовВыбора, Сотрудник, ОбъектОценки, ЭтапОценки, НастройкаПроцессаВыбора, ФункциональноеНаправлениеСотрудника);
	КонецЕсли;
		
	Элементы.ЗначенияКритериев.Обновить();
	
	ОбъектОценкиСтарый = ОбъектОценки;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьЗначенияНезаполненныхКритериевНаСервере(ВладелецОбъектовВыбора, Сотрудник, ОбъектОценки, ЭтапОценки, НастройкаПроцессаВыбора, ФункциональноеНаправлениеСотрудника)
	
	Если НЕ ЗначениеЗаполнено(ВладелецОбъектовВыбора) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Нстр("ru = 'Не выбран владелец!'");
		Сообщение.Поле = "ВладелецОбъектовВыбора";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ЭтапОценки) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Нстр("ru = 'Не указан текущий этап оценки!'");
		Сообщение.Поле = "ЭтапОценки";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Сотрудник) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Нстр("ru = 'Не выбран ответственный!'");
		Сообщение.Поле = "Ответственный";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ОбъектОценки) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Нстр("ru = 'Не выбрано предложение поставщика!'");
		Сообщение.Поле = "ОбъектОценки";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиПроцессаВыбораКритерииОценки.КритерийОценки,
		|	НастройкиПроцессаВыбораКритерииОценки.Вес
		|ПОМЕСТИТЬ КритерииОценкиОбласти
		|ИЗ
		|	Справочник.НастройкиПроцессаВыбора.КритерииОценки КАК НастройкиПроцессаВыбораКритерииОценки
		|ГДЕ
		|	НастройкиПроцессаВыбораКритерииОценки.ЭтапВыбора = &ЭтапОценки
		|	И НастройкиПроцессаВыбораКритерииОценки.Ссылка = &НастройкаПроцессаВыбора
		|	И НЕ НастройкиПроцессаВыбораКритерииОценки.РассчитатьЗначение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КритерииОценкиОбласти.КритерийОценки КАК Критерий,
		|	КритерииОценкиОбласти.Вес
		|ИЗ
		|	КритерииОценкиОбласти КАК КритерииОценкиОбласти
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияКритериевОценки КАК ЗначенияКритериевОценки
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФункциональныеНаправленияФизическихЛиц КАК ФункциональныеНаправленияФизическихЛиц
		|			ПО ЗначенияКритериевОценки.Ответственный = ФункциональныеНаправленияФизическихЛиц.ФизическоеЛицо
		|		ПО (ЗначенияКритериевОценки.Критерий = КритерииОценкиОбласти.КритерийОценки)
		|			И (ЗначенияКритериевОценки.Ответственный = &Ответственный)
		|			И (ЗначенияКритериевОценки.ОбъектОценки = &ОбъектОценки)
		|			И (ЗначенияКритериевОценки.ЭтапОценки = &ЭтапОценки)
		|			И (ЗначенияКритериевОценки.Владелец = &ВладелецОбъектовВыбора)
		|ГДЕ
		|	ЗначенияКритериевОценки.Критерий ЕСТЬ NULL 
		|	И (ЕСТЬNULL(ЗначенияКритериевОценки.Критерий.ФункциональноеНаправление, ЗНАЧЕНИЕ(Справочник.ФункциональныеНаправления.ПустаяСсылка)) = ЕСТЬNULL(ФункциональныеНаправленияФизическихЛиц.ФункциональноеНаправление, ЗНАЧЕНИЕ(Справочник.ФункциональныеНаправления.ПустаяСсылка))
		|			ИЛИ ЕСТЬNULL(ЗначенияКритериевОценки.Критерий.ФункциональноеНаправление, ЗНАЧЕНИЕ(Справочник.ФункциональныеНаправления.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.ФункциональныеНаправления.ПустаяСсылка))";
	
	Запрос.УстановитьПараметр("ВладелецОбъектовВыбора", ВладелецОбъектовВыбора);
	Запрос.УстановитьПараметр("НастройкаПроцессаВыбора", НастройкаПроцессаВыбора);
	Запрос.УстановитьПараметр("Ответственный", Сотрудник);
	Запрос.УстановитьПараметр("ОбъектОценки", ОбъектОценки);
	Запрос.УстановитьПараметр("ЭтапОценки", ЭтапОценки);
	//Запрос.УстановитьПараметр("ФункциональноеНаправление", ФункциональноеНаправлениеСотрудника);
	
	Если НЕ ЗначениеЗаполнено(ФункциональноеНаправлениеСотрудника) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И НастройкиПроцессаВыбораКритерииОценки.КритерийОценки.ФункциональноеНаправление = &ФункциональноеНаправление", "");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			МенеджерЗаписи = РегистрыСведений.ЗначенияКритериевОценки.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Владелец = ВладелецОбъектовВыбора;
			МенеджерЗаписи.Ответственный = Сотрудник;
			МенеджерЗаписи.ОбъектОценки = ОбъектОценки;
			МенеджерЗаписи.ЭтапОценки = ЭтапОценки;
			МенеджерЗаписи.Критерий = ВыборкаДетальныеЗаписи.Критерий;
			МенеджерЗаписи.Значение = 0;
			МенеджерЗаписи.Вес = ВыборкаДетальныеЗаписи.Вес;
			МенеджерЗаписи.НеЗаполнен = Истина;
			МенеджерЗаписи.Записать(Ложь);
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ОценкаПеречислимогоЗначенияКритерия(Критерий)
	Если ЗначениеЗаполнено(Критерий) Тогда
		Возврат Критерий.Оценка;
	КонецЕсли;
	
	Возврат 0;
КонецФункции

&НаСервере
Процедура ОбработатьВводЗначенияКритерияНаСервере(Значение, ДопПараметры)

	ОписаниеЗначения = Новый Структура;
	
	Если ДопПараметры.Свойство("ЗначениеМножества") Тогда
		ОписаниеЗначения.Вставить("Значение", ДопПараметры.ЗначениеМножества.Оценка);
		ОписаниеЗначения.Вставить("ЗначениеМножества", ДопПараметры.ЗначениеМножества);
	Иначе
		ОписаниеЗначения.Вставить("Значение", Значение);
		ОписаниеЗначения.Вставить("ЗначениеМножества", Справочники.ЗначенияПеречислимыхКритериев.ПустаяСсылка());
	КонецЕсли;
	
	
	ОписаниеЗначения.Вставить("Владелец", ВладелецОбъектовВыбора);
	ОписаниеЗначения.Вставить("Ответственный", Сотрудник);
	ОписаниеЗначения.Вставить("ОбъектОценки", ДопПараметры.ОбъектОценки);
	ОписаниеЗначения.Вставить("ЭтапОценки", ЭтапОценки);
	ОписаниеЗначения.Вставить("Критерий", ДопПараметры.Критерий);
	ОписаниеЗначения.Вставить("Вес", ДопПараметры.Вес);
	ОписаниеЗначения.Вставить("НеЗаполнен", Ложь);
	ОписаниеЗначения.Вставить("БалльноеЗначение",
		ВыборОбъектовУХ.ПолучитьБалльноеЗначениеКритерия(ВладелецОбъектовВыбора, ДопПараметры.Критерий, ОписаниеЗначения.Значение));
		
	ЗаписатьЗначениеКритерия(ОписаниеЗначения);
	
	ЗначенияРеквизитовОбъекта = Новый Структура;
	ЗначенияРеквизитовОбъекта.Вставить("Оценка", ИнтегральнаяОценкаОбъекта(ВладелецОбъектовВыбора, ЭтапОценки, ОбъектОценки));
	ОбновитьТекущийОбъектНаСервере(ЗначенияРеквизитовОбъекта);
	

КонецПроцедуры

&НаСервере
Процедура ОбновитьТекущийОбъектНаСервере(ЗначенияРеквизитовОбъекта)
	
	ТекСтрока = Элементы.ОбъектыОценки.ТекущаяСтрока;
	СтрокаОбъекта = ОбъектыОценки.НайтиПоИдентификатору(ТекСтрока);
	ЗаполнитьЗначенияСвойств(СтрокаОбъекта, ЗначенияРеквизитовОбъекта);
	
	Элементы.ОбъектыОценки.Обновить();
	Элементы.ЗначенияКритериев.Обновить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьЗначениеКритерия(ОписаниеЗначения)
	МенеджерЗаписи = РегистрыСведений.ЗначенияКритериевОценки.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ОписаниеЗначения);
	МенеджерЗаписи.Записать();
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеЭтапаНаСервере()
	
	Если ЗначениеЗаполнено(ЭтапОценки) Тогда
		флЭтоПоследнийЭтап = (ЭтапОценки = НастройкаПроцессаВыбора.ПоследнийЭтап);
	Иначе
		флЭтоПоследнийЭтап = ЛОжь;
		флЭтоПоследующаяОценка = Ложь;
	КонецЕсли;
	
	Если флЭтоПоследующаяОценка Тогда
		Элементы.ДекорацияВыборЗавершен.Заголовок = Нстр("ru = 'Последующая оценка объектов.'");
		Элементы.ДекорацияВыборЗавершен.ЦветТекста = Новый Цвет(0, 0, 255);
		
	Иначе
		Элементы.ДекорацияВыборЗавершен.Заголовок = Нстр("ru = 'Процесс оценки и выбора по данному владельцу завершен.'");	
		Элементы.ДекорацияВыборЗавершен.ЦветТекста = Новый Цвет(255, 0, 0);
		
	КонецЕсли;
		
	ЗаполнитьТаблицуОбъектовВыбора();
	
	Если ОбъектыОценки.Количество() = 0 Тогда
		ОбъектОценки = Неопределено;
	Иначе
		
		Если НЕ ЗначениеЗаполнено(ОбъектОценки) Тогда
			ОбъектОценки = ОбъектыОценки[0].ОбъектОценки;
		КонецЕсли;
		
		// ищем объект оценки
		мСтрокиОбъекта = ОбъектыОценки.НайтиСтроки(Новый Структура("ОбъектОценки", ОбъектОценки));
		Если мСтрокиОбъекта.Количество() > 0 тогда
			Элементы.ОбъектыОценки.ТекущаяСтрока = мСтрокиОбъекта[0].ПолучитьИдентификатор();
			ОбъектОценкиСтарый = Неопределено; // обработать изменение объекта оценки
		КонецЕсли;
			
	КонецЕсли;
	
	Если ОбъектОценки <> ОбъектОценкиСтарый Тогда
		ОбработатьИзменениеОбъектаОценки();
	КонецЕсли;
	
	Если флВыборЗавершен Тогда
		
		Элементы.ОбъектыОценки.ТолькоПросмотр = Истина;
		Элементы.ЗначенияКритериев.ТолькоПросмотр = Истина;
		
	иначе
		
		Элементы.ОбъектыОценки.ТолькоПросмотр = ЛОжь;
		Элементы.ЗначенияКритериев.ТолькоПросмотр = ЛОжь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекущегоСотрудника()
	
	Сотрудник = Пользователи.ТекущийПользователь().ФизическоеЛицо;
	ФункциональноеНаправлениеСотрудника = РегистрыСведений.ФункциональныеНаправленияФизическихЛиц.ФункциональноеНаправлениеФизЛица(Сотрудник);
	РазрешитьИзменятьЭтапОценки = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Сотрудник) Тогда
		ЭтаФорма.ТолькоПросмотр = Истина;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Пользователю не назначено физическое лицо!'");
		Сообщение.Сообщить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьПризнакЗавершенияОценкиНаСервере(ОписаниеЗначения)
	Попытка
		МенеджерЗаписи = РегистрыСведений.СостояниеОценкиОбъектов.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ОписаниеЗначения);
		МенеджерЗаписи.Записать(Истина);
	Исключение
		ТекстСообщения = НСтр("ru = 'При установке признака завершения для оценки объекта ""%Объект%"" в этапе ""%Этап%"" произошли ошибки: %ОписаниеОшибки%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Объект%", Строка(ОписаниеЗначения.ОбъектОценки));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Этап%", Строка(ОписаниеЗначения.ЭтапОценки));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%", ОписаниеОшибки());
		ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
	КонецПопытки;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИнтегральнаяОценкаОбъекта(ВладелецОбъектовВыбора, ЭтапОценки, ОбъектОценки)
	Возврат ВыборОбъектовУХ.ИнтегральнаяОценкаОбъекта(ВладелецОбъектовВыбора, ОбъектОценки, ЭтапОценки, (ЭтапОценки = Справочники.ЭтапыОценки.ПоследующаяОценка));
КонецФункции


#КонецОбласти
