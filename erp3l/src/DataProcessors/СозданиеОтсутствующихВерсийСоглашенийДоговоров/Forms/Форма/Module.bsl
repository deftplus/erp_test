#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьДоговорыНаСервере();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаСервере
Процедура ЗаполнитьДоговорыНаСервере()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ИСТИНА КАК Создать,
	|	ДоговорыКонтрагентов.Ссылка КАК ДоговорКонтрагента,
	|	НЕОПРЕДЕЛЕНО КАК ВерсияСоглашения
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	НЕ ДоговорыКонтрагентов.ПометкаУдаления
	|	И ДоговорыКонтрагентов.ВерсияСоглашения В(&ПустыеВерсии)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА,
	|	ДоговорыКредитовИДепозитов.Ссылка,
	|	НЕОПРЕДЕЛЕНО
	|ИЗ
	|	Справочник.ДоговорыКредитовИДепозитов КАК ДоговорыКредитовИДепозитов
	|ГДЕ
	|	НЕ ДоговорыКредитовИДепозитов.ПометкаУдаления
	|	И ДоговорыКредитовИДепозитов.ВерсияСоглашения В(&ПустыеВерсии)"
	);
	
	ПустыеВерсии = Новый Массив;
	ПустыеВерсии.Добавить(Неопределено);
	ТипыСоглашений = Метаданные.ОпределяемыеТипы.ВерсияСоглашения.Тип.Типы();
	Для каждого Тип Из ТипыСоглашений Цикл
		ПустыеВерсии.Добавить(Новый (Тип));
	КонецЦикла;
		
	Запрос.УстановитьПараметр("ПустыеВерсии", ПустыеВерсии);
	
	Таблица = Запрос.Выполнить().Выгрузить();
	Объект.ДоговорыКонтрагентов.Загрузить(Таблица);
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДоговоры(Команда)
	ЗаполнитьДоговорыНаСервере();
КонецПроцедуры

&НаСервере
Процедура СоздатьВерсииСоглашенийНаСервере()
	
	СозданыВерсии = Ложь;
	ВыбраныДоговоры = Ложь;
	
	Для каждого Строка ИЗ Объект.ДоговорыКонтрагентов Цикл
		Если Строка.Создать 
			И НЕ ЗначениеЗаполнено(Строка.ВерсияСоглашения) Тогда
			
			ВыбраныДоговоры = Истина;
			
			НачатьТранзакцию();
			
			Попытка
				
				ДоговорОбъект = Строка.ДоговорКонтрагента.ПолучитьОбъект();
				Если НЕ ЗначениеЗаполнено(ДоговорОбъект.ВидДоговораУХ) Тогда
					ТекстОшибки = СтрШаблон(
					Нстр("ru = 'Не заполнен реквизит ""Вид договора (УХ)"" договора %1, версия соглашения не может быть создана'"),
						ДоговорОбъект);
					ПолеФормы = СтрШаблон("ДоговорыКонтрагентов[%1].ДоговорКонтрагента", 
						Формат(Объект.ДоговорыКонтрагентов.Индекс(Строка), "ЧН=0; ЧГ="));		
					ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,, ПолеФормы, "Объект");
					
					ОтменитьТранзакцию();
					Продолжить;	
				КонецЕсли;	    	                  
				
				РаботаСДоговорамиКонтрагентовУХ.СоздатьВерсиюСоглашенияПоДоговору(ДоговорОбъект);
				ДоговорОбъект.ОбменДанными.Загрузка = Истина; // чтобы повторно не создавалась версия
				ДоговорОбъект.Записать();
				
				Строка.ВерсияСоглашения = ДоговорОбъект.ВерсияСоглашения;
				Если ЗначениеЗаполнено(Строка.ВерсияСоглашения) Тогда
					Строка.Создать = Ложь;
					СозданыВерсии = Истина;
				КонецЕсли;
				
				ЗафиксироватьТранзакцию();
				
			Исключение
				ОтменитьТранзакцию();
				
				НомерСтроки = Объект.ДоговорыКонтрагентов.Индекс(Строка) + 1;
				ТекстОшибки = СтрШаблон(Нстр("ru = 'Не удалось создать версию соглашения для договора: %1, по причине: %2'"),
					Строка.ДоговораКонтрагента, ОписаниеОшибки());
				ПолеФормы = СтрШаблон("ДоговорыКонтрагентов[%1].ДоговорКонтрагента", НомерСтроки - 1, "ЧН=0; ЧГ=");		
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,, ПолеФормы, "Объект");
			КонецПопытки;
			
		КонецЕсли;
	КонецЦикла;
	
	Если СозданыВерсии Тогда
		ОбщегоНазначения.СообщитьПользователю(Нстр("ru = 'Созданы версии соглашений по договорам'"));
	ИначеЕсли НЕ ВыбраныДоговоры Тогда
		ОбщегоНазначения.СообщитьПользователю(Нстр("ru = 'Выберите договоры для создания версий соглашений'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВерсииСоглашений(Команда)
	
	Оповещение = Новый ОписаниеОповещения("СоздатьВерсииСоглашенийПродолжение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, Нстр("ru = 'Создать версии соглашений для выбранных договоров?'"), РежимДиалогаВопрос.ОКОтмена);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВерсииСоглашенийПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		 СоздатьВерсииСоглашенийНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсеФлажки(Команда)
	УстановитьФлагСоздать(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВсеФлажки(Команда)
	УстановитьФлагСоздать(Истина);
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Процедура УстановитьФлагСоздать(Флаг)	
	Для каждого Строка Из Объект.ДоговорыКонтрагентов Цикл
		Строка.Создать = Флаг;
	КонецЦикла;
КонецПроцедуры
#КонецОбласти