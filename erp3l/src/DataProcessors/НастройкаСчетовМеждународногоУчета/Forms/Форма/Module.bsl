#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ТипНастройкиОбщаяПоОбъектуУчета = Перечисления.ТипыНастроекОтраженияВМеждународномУчете.ОбщаяПоОбъектуУчета;
	ТипНастройкиИсключениеПоОбъектуУчета = Перечисления.ТипыНастроекОтраженияВМеждународномУчете.ИсключениеПоОбъектуУчета;
	
	Организация = Параметры.Организация;
	ПланСчетов = Параметры.ПланСчетов;
	НастройкаФормированияПроводок = Параметры.НастройкаФормированияПроводок;
	
	ЗаполнитьДеревоОбъектовБухгалтерскогоУчета();

	Если Не ЗначениеЗаполнено(Организация) Тогда
		Организация = Справочники.Организации.ПолучитьОрганизациюПоУмолчанию();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПланСчетов) Тогда
		ПланСчетов = Справочники.ПланыСчетовМеждународногоУчета.ПланСчетовПоУмолчанию();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НастройкаФормированияПроводок) Тогда
		НастройкаФормированияПроводок = 
			Справочники.НастройкиФормированияПроводокМеждународногоУчета.НастройкаФормированияПроводокПоУмолчанию(ПланСчетов);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		НастройкиОтраженияКорреспонденций, "ТекущийОбъектУчета", ТекущийОбъектУчета);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		НастройкиОтраженияКорреспонденций, "ПоложениеДтКт", НСтр("ru = 'Дт/Кт';
																|en = 'Dr/Cr'"));
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		НастройкиОтраженияКорреспонденций, "ПоложениеДт", НСтр("ru = 'Дт';
																|en = 'Dr'"));
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		НастройкиОтраженияКорреспонденций, "ПоложениеКт", НСтр("ru = 'Кт';
																|en = 'Cr'"));
		
	ТекущийМесяц = НачалоМесяца(ТекущаяДатаСеанса());
	
	НастройкаФормированияПроводокПриИзмененииСервер();
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		ПланСчетовМеждународный, 
		"ПланСчетов",
		ПланСчетов,
		,
		,
		Истина,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	
	РежимПросмотраНастроекОтраженияКорреспонденций = "ОбщимСписком";
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(НастройкиОтраженияКорреспонденций, "ОбщимСписком", Истина);
	Если РежимПросмотраНастроекОтраженияКорреспонденций = "ОбщимСписком" Тогда
		Элементы.ДеревоОбъектовБухгалтерскогоУчетаПоКорреспонденциям.Видимость = Ложь;
		Элементы.ВариантОтбораОбъектовУчетаКорреспонденция.Видимость = Ложь;
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(НастройкиОтраженияКорреспонденций, "ОбщимСписокм", Истина);
	ИначеЕсли РежимПросмотраНастроекОтраженияКорреспонденций = "ОбщимСписком" Тогда
		Элементы.ДеревоОбъектовБухгалтерскогоУчетаПоКорреспонденциям.Видимость = Истина;
		Элементы.ВариантОтбораОбъектовУчетаКорреспонденция.Видимость = Истина;
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(НастройкиОтраженияКорреспонденций, "ОбщимСписокм", Ложь);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ОбъектУчета) Тогда
		ТекущаяСтрокаУстановлена = Ложь;
		Для каждого ГруппаДерева Из ДеревоОбъектовБухгалтерскогоУчета.ПолучитьЭлементы() Цикл
			Для каждого СтрокаДерева Из ГруппаДерева.ПолучитьЭлементы() Цикл
				Если СтрокаДерева.ОбъектУчета = Параметры.ОбъектУчета Тогда
					Элементы.ДеревоОбъектовБухгалтерскогоУчета.ТекущаяСтрока = СтрокаДерева.ПолучитьИдентификатор();
					ТекущаяСтрокаУстановлена = Истина;
				КонецЕсли;
				Если ТекущаяСтрокаУстановлена Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если ТекущаяСтрокаУстановлена Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		ВариантОтбораОбъектовУчета = 1;
	КонецЕсли;
	Если ЗначениеЗаполнено(Параметры.ОбъектНастройки) Тогда
		ТекущийОбъектНастройкиПриОткрытии = Параметры.ОбъектНастройки;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	НастройкаФормированияПроводокПриИзмененииСервер();
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		ПланСчетовМеждународный, 
		"ПланСчетов",
		ПланСчетов,
		,
		,
		Истина,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	Если ЗначениеЗаполнено(Организация) И Настройки["Организация"] <> Неопределено Тогда
		Настройки.Удалить("Организация");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПланСчетов) И Настройки["ПланСчетов"] <> Неопределено Тогда
		Настройки.Удалить("ПланСчетов");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НастройкаФормированияПроводок) И Настройки["НастройкаФормированияПроводок"] <> Неопределено Тогда
		Настройки.Удалить("НастройкаФормированияПроводок");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Оповестить("ЗаконченаНастройкаСчетовМеждународногоУчета")
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НастройкиСчетовМеждународногоУчетаПоОбъектам" Тогда
		ЗаполнитьИнформациюПоНастройкам(Истина);
	ИначеЕсли ИмяСобытия = "Запись_НастройкиОтраженияКорреспонденцийВМеждународномУчете" Тогда
		ЗаполнитьИнформациюПоНастройкам();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ОрганизацияПриИзмененииСервер();
КонецПроцедуры

&НаКлиенте
Процедура ПланСчетовПриИзменении(Элемент)
	УстановитьТекущуюСтраницуНастроекПоОбъектамУчета();
	УстановитьТекущуюСтраницуНастроекПоКорреспонденциям();
	ПланСчетовПриИзмененииСервер();
КонецПроцедуры

&НаКлиенте
Процедура НастройкаФормированияПроводокПриИзменении(Элемент)
	УстановитьТекущуюСтраницуНастроекПоОбъектамУчета();
	УстановитьТекущуюСтраницуНастроекПоКорреспонденциям();
	НастройкаФормированияПроводокПриИзмененииСервер();
КонецПроцедуры

&НаКлиенте
Процедура СтатусНастройкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Отбор = Новый Структура;
	Отбор.Вставить("Организация", Организация);
	Отбор.Вставить("ПланСчетов",   ПланСчетов);
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "СоздатьЗаписьПланыСчетовОрганизаций" Тогда
		ОбработкаОповещения = Новый ОписаниеОповещения("ВыборПериодаЗаписиПланыСчетовОрганизаций", ЭтотОбъект);
		ПоказатьВводДаты(ОбработкаОповещения, ТекущийМесяц, Нстр("ru = 'Включить настройку с';
																|en = 'Enable setting since'"), ЧастиДаты.Дата);
		СтандартнаяОбработка = Ложь;
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьИсториюПланыСчетовОрганизаций" Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор", Отбор);
		ОткрытьФорму("РегистрСведений.ПланыСчетовМеждународногоУчетаОрганизаций.Форма.ФормаСписка", 
			ПараметрыФормы, ЭтотОбъект, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовНастроекПоОбъектамУчета

&НаКлиенте
Процедура РежимНастройкиПриИзменении(Элемент)
	НастроитьФормуПриИзмененииРежимаНастройки();
	ОчиститьОтборСпискаИсключенийПоОбъектуНастройки();
КонецПроцедуры

&НаКлиенте
Процедура ДеревоОбъектовБухгалтерскогоУчетаПриАктивизацииСтроки(Элемент)
	
	УстановитьТекущуюСтраницуНастроекПоОбъектамУчета();
	ПодключитьОбработчикОжидания("ОбработчикДеревоОбъектовБухгалтерскогоУчетаПриАктивизацииСтроки", 0.1 ,Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаОбщиеНастройкиСчетУчетаПриИзменении(Элемент)
	
	ТекущиеДанныеСтроки = Элементы.ТаблицаОбщиеНастройки.ТекущиеДанные;
	
	ПараметрыСохраненияНастройки = Новый Структура;
	ПараметрыСохраненияНастройки.Вставить("ПланСчетов", ПланСчетов);
	ПараметрыСохраненияНастройки.Вставить("НастройкаФормированияПроводок", НастройкаФормированияПроводок);
	ПараметрыСохраненияНастройки.Вставить("ТипНастройки", ТипНастройкиОбщаяПоОбъектуУчета);
	ПараметрыСохраненияНастройки.Вставить("ОбъектУчета", ТекущийОбъектУчета);
	ПараметрыСохраненияНастройки.Вставить("ОбъектНастройки", ТекущиеДанныеСтроки.ОбъектНастройки);
	ПараметрыСохраненияНастройки.Вставить("Организация", Неопределено);
	ПараметрыСохраненияНастройки.Вставить("МестоУчета", Неопределено);
	ПараметрыСохраненияНастройки.Вставить("СчетУчета", ТекущиеДанныеСтроки.СчетУчета);
	ПараметрыСохраненияНастройки.Вставить("ТребуетсяНастройка", ТекущиеДанныеСтроки.ТребуетсяНастройка);
	
	СохранитьНастройкиСервер(ПараметрыСохраненияНастройки);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаОбщиеНастройкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТаблицаОбщиеНастройки.ТекущиеДанные;
	Если Поле = Элементы.ТаблицаОбщиеНастройкиИсключения Тогда
		РежимНастройки = 1;
		НастроитьФормуПриИзмененииРежимаНастройки();
		УстановитьОтборИсключенийПоОбъектуНастройки(ТекущиеДанные.ОбъектНастройки);
		Если ТекущиеДанные.КоличествоИсключений = 0 Тогда
			Отбор = ОтборНастройкиСчетовПоОбъектуУчета(ТекущийОбъектУчета, ОтборОбъектНастройкиЗначение, ТипНастройкиИсключениеПоОбъектуУчета);
			ОткрытьФормуЗаписиИсключения(Отбор, Истина);
		КонецЕсли;
	ИначеЕсли Поле = Элементы.ТаблицаОбщиеНастройкиОбъектНастройки Тогда
		Если ЗначениеЗаполнено(ТекущиеДанные.ОбъектНастройки) Тогда
			ПоказатьЗначение(, ТекущиеДанные.ОбъектНастройки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОбъектНастройкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОчиститьОтборПоОбъектуНастройки" Тогда
		ОчиститьОтборСпискаИсключенийПоОбъектуНастройки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантОтбораОбъектовУчетаПриИзменении(Элемент)
	
	Если ВариантОтбораОбъектовУчета = 1 Тогда // Требующие настройки
		
		ТекущиеДанные = Элементы.ДеревоОбъектовБухгалтерскогоУчета.ТекущиеДанные;
		СтрокиДерева  = ДеревоОбъектовБухгалтерскогоУчета.ПолучитьЭлементы();
		
		Если ТекущиеДанные = Неопределено Или ТекущиеДанные.ТребуетсяНастройкаСчетовУчетаКоличество = 0 Тогда
			Для Каждого СтрокаДерева Из СтрокиДерева Цикл
				Если СтрокаДерева.ТребуетсяНастройкаСчетовУчетаКоличество > 0 Тогда
					Элементы.ДеревоОбъектовБухгалтерскогоУчета.ТекущаяСтрока = СтрокаДерева.ПолучитьИдентификатор();
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		ТекущиеДанные = Элементы.ТаблицаОбщиеНастройки.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Или НЕ ТекущиеДанные.ТребуетсяНастройка Тогда
			Для Каждого СтрокаТаблицы Из ТаблицаОбщиеНастройки Цикл
				Если СтрокаТаблицы.ТребуетсяНастройка Тогда
					Элементы.ТаблицаОбщиеНастройки.ТекущаяСтрока = СтрокаТаблицы.ПолучитьИдентификатор();
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокИсключенияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	
	Отказ = Истина;
	ТекущиеДанные = Элементы.СписокИсключения.ТекущиеДанные;
	
	Отбор = ОтборНастройкиСчетовПоОбъектуУчета(ТекущийОбъектУчета, ОтборОбъектНастройкиЗначение, ТипНастройкиИсключениеПоОбъектуУчета);
	Если Копирование Тогда
		Отбор.Вставить("СчетУчета");
		ЗаполнитьЗначенияСвойств(Отбор, ТекущиеДанные);
	КонецЕсли;
	ОткрытьФормуЗаписиИсключения(Отбор, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокИсключенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	Отбор = ОтборНастройкиСчетовПоОбъектуУчета(ТекущийОбъектУчета, ОтборОбъектНастройкиЗначение, ТипНастройкиИсключениеПоОбъектуУчета);
	ЗаполнитьЗначенияСвойств(Отбор, Элементы.СписокИсключения.ТекущиеДанные );
	ОткрытьФормуЗаписиИсключения(Отбор);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокИсключенияПослеУдаления(Элемент)
	ЗаполнитьИнформациюПоНастройкам(Истина);
КонецПроцедуры

&НаКлиенте
Процедура НастройкаЗаполненияСубконтоОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	Если НавигационнаяСсылка = "ОткрытьНастройкуЗаполненияСубконто" Тогда
		
		СтандартнаяОбработка = Ложь;
		ТекущаяСтрока = Элементы.ДеревоОбъектовБухгалтерскогоУчета.ТекущаяСтрока;
		Если ТекущаяСтрока = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ТекущиеДанные = ДеревоОбъектовБухгалтерскогоУчета.НайтиПоИдентификатору(ТекущаяСтрока);
		
		ПараметрыОткрытияФормы = Новый Структура();
		ПараметрыОткрытияФормы.Вставить("ПланСчетов", ПланСчетов);
		ПараметрыОткрытияФормы.Вставить("НастройкаФормированияПроводок", НастройкаФормированияПроводок);
		ПараметрыОткрытияФормы.Вставить("ОбъектУчета", ТекущиеДанные.ОбъектУчета);
		ПараметрыОткрытияФормы.Вставить("ИсточникДанных", ТекущиеДанные.ИсточникДанных);
		
		ДополнительныеПараметры = Новый Структура();
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеОкончанияНастройкиЗаполненияСубконто", ЭтотОбъект, ДополнительныеПараметры);
		
		ОткрытьФорму("Обработка.НастройкаСчетовМеждународногоУчета.Форма.ФормаЗаполненияСубконтоНаСчетах", 
			ПараметрыОткрытияФормы,
			ЭтотОбъект, 
			,,,
			ОписаниеОповещения);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовНастроекПоКорреспонденциям

&НаКлиенте
Процедура ДеревоОбъектовБухгалтерскогоУчетаПоКорреспонденциямПриАктивизацииСтроки(Элемент)
	
	УстановитьТекущуюСтраницуНастроекПоКорреспонденциям();
	ПодключитьОбработчикОжидания("ОбработчикДеревоОбъектовБухгалтерскогоУчетаПоКорреспонденциямПриАктивизацииСтроки", 0.1 ,Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура РежимПросмотраНастроекОтраженияКорреспонденцийПриИзменении(Элемент)
	
	Если РежимПросмотраНастроекОтраженияКорреспонденций = "ОбщимСписком" Тогда
		Элементы.ДеревоОбъектовБухгалтерскогоУчетаПоКорреспонденциям.Видимость = Ложь;
		Элементы.ВариантОтбораОбъектовУчетаКорреспонденция.Видимость = Ложь;
		Элементы.НастройкиОтраженияКорреспонденцийПоложение.Видимость = Ложь;
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(НастройкиОтраженияКорреспонденций, "ОбщимСписком", Истина);
	ИначеЕсли РежимПросмотраНастроекОтраженияКорреспонденций = "СОтборомПоОбъектамУчета" Тогда
		Элементы.ДеревоОбъектовБухгалтерскогоУчетаПоКорреспонденциям.Видимость = Истина;
		Элементы.ВариантОтбораОбъектовУчетаКорреспонденция.Видимость = Истина;
		Элементы.НастройкиОтраженияКорреспонденцийПоложение.Видимость = Истина;
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(НастройкиОтраженияКорреспонденций, "ОбщимСписком", Ложь);
	КонецЕсли;
	
	УстановитьТекущуюСтраницуНастроекПоКорреспонденциям();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиОтраженияКорреспонденцийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.НастройкиОтраженияКорреспонденцийПорядокПрименения Тогда
		
		СтандартнаяОбработка = Ложь;
		ДанныеСтроки = Элементы.НастройкиОтраженияКорреспонденций.ДанныеСтроки(ВыбраннаяСтрока);
		
		Отбор = Новый Структура;
		Отбор.Вставить("ПланСчетов", ПланСчетов);
		Отбор.Вставить("НастройкаФормированияПроводок", НастройкаФормированияПроводок);
		Отбор.Вставить("ОбъектУчетаДт", ДанныеСтроки.ОбъектУчетаДт);
		Отбор.Вставить("ОбъектУчетаКт", ДанныеСтроки.ОбъектУчетаКт);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор", Отбор);
		
		ОткрытьФорму("РегистрСведений.НастройкиОтраженияКорреспонденцийВМеждународномУчете.ФормаСписка", 
			ПараметрыФормы,
			ЭтотОбъект,
			,
			,
			,
			,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиОтраженияКорреспонденцийПослеУдаления(Элемент)
	ЗаполнитьИнформациюПоНастройкам();
КонецПроцедуры

&НаКлиенте
Процедура ВариантОтбораОбъектовУчетаКорреспонденцияПриИзменении(Элемент)
	
	Если ВариантОтбораОбъектовУчетаКорреспонденция = 1 Тогда // С настройками
		ТекущиеДанные = Элементы.ДеревоОбъектовБухгалтерскогоУчетаПоКорреспонденциям.ТекущиеДанные;
		СтрокиДерева  = ДеревоОбъектовБухгалтерскогоУчета.ПолучитьЭлементы();
		Если ТекущиеДанные = Неопределено Или ТекущиеДанные.НастройкиОтраженияКорреспонденцийКоличество = 0 Тогда
			Для Каждого СтрокаДерева Из СтрокиДерева Цикл
				Если СтрокаДерева.НастройкиОтраженияКорреспонденцийКоличество > 0 Тогда
					Элементы.ДеревоОбъектовБухгалтерскогоУчетаПоКорреспонденциям.ТекущаяСтрока = СтрокаДерева.ПолучитьИдентификатор();
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовПросмотраНастроекПоПлануСчетов

&НаКлиенте
Процедура ПланСчетовМеждународныйПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("ОбработчикПланСчетовМеждународныПриАктивизацииСтроки", 0.1 ,Истина);
КонецПроцедуры

&НаКлиенте
Процедура НастройкиПоСчетуВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОткрытьФормуНастройки(ВыбраннаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантОтбораСчетовУчетаПриИзменении(Элемент)
	
	Если ВариантОтбораСчетовУчета = "0" Тогда
		Элементы.ПланСчетовМеждународный.Отображение = ОтображениеТаблицы.Дерево;
	Иначе
		Элементы.ПланСчетовМеждународный.Отображение = ОтображениеТаблицы.Список;
	КонецЕсли;
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(
		ПланСчетовМеждународный, 
		"ЕстьНастройки", 
		Истина, 
		ВариантОтбораСчетовУчета = 1);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчкиКомандФормы

&НаКлиенте
Процедура НастройкаФормированияПроводок(Команда)
	Если ПроверитьЗаполнение() Тогда
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("Ключ", НастройкаФормированияПроводок);
		ОткрытьФорму("Справочник.НастройкиФормированияПроводокМеждународногоУчета.ФормаОбъекта", 
			ПараметрыФормы, ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ЗаполнитьСчетУчета(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ПланСчетов", ПланСчетов);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ВыделенныеСтроки", Элементы.ТаблицаОбщиеНастройки.ВыделенныеСтроки);
	ОписаниеОповещений = Новый ОписаниеОповещения("ПриОкончанииВыбораСчетаУчетаДляЗаполнения", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму("Обработка.НастройкаСчетовМеждународногоУчета.Форма.ФормаВыбораСчетаУчета", 
		ПараметрыФормы,
		ЭтотОбъект, , , ,
		ОписаниеОповещений, 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца );
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройку(Команда)
	
	Если Элементы.НастройкиПоСчету.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ОткрытьФормуНастройки(Элементы.НастройкиПоСчету.ТекущаяСтрока)
	
КонецПроцедуры

&НаКлиенте
Процедура АнализКорреспонденций(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КлючВарианта", "АнализКорреспонденцийШахматка");
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ПараметрОтбор = Новый Структура();
	Если ЗначениеЗаполнено(ПланСчетов) Тогда
		ПараметрОтбор.Вставить("ПланСчетов", ПланСчетов);
	КонецЕсли;
	Если ЗначениеЗаполнено(НастройкаФормированияПроводок) Тогда
		ПараметрОтбор.Вставить("НастройкаФормированияПроводок", НастройкаФормированияПроводок);
	КонецЕсли;
	ПараметрыФормы.Вставить("Отбор", ПараметрОтбор);
	
	ОткрытьФорму("Отчет.АнализКорреспонденцийОперативногоУчета.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	НастройкаФормированияПроводокПриИзмененииСервер();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаОбщиеНастройкиСчетУчета.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаОбщиеНастройки.СчетУчета");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаОбщиеНастройки.ТребуетсяНастройка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоОбъектовБухгалтерскогоУчета.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоОбъектовБухгалтерскогоУчетаЭтоГруппа.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоОбъектовБухгалтерскогоУчетаПредставление.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоОбъектовБухгалтерскогоУчетаТребуетсяНастройкаСчетовУчетаКоличество.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоОбъектовБухгалтерскогоУчета.Видимость");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоОбъектовБухгалтерскогоУчета.ТребуетсяНастройкаСчетовУчетаКоличество");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоОбъектовБухгалтерскогоУчета.ОбщиеНастройкиКоличество");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоОбъектовБухгалтерскогоУчета.НастройкиИсключенийКоличество");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость",  Ложь);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоОбъектовБухгалтерскогоУчета.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоОбъектовБухгалтерскогоУчетаЭтоГруппа.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоОбъектовБухгалтерскогоУчетаПредставление.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоОбъектовБухгалтерскогоУчетаТребуетсяНастройкаСчетовУчетаКоличество.Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
 	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоОбъектовБухгалтерскогоУчета.ТребуетсяНастройкаСчетовУчетаКоличество");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВариантОтбораОбъектовУчета");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 1; // Требующие настройки
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость",  Ложь);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаОбщиеНастройки.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаОбщиеНастройкиОбъектНастройки.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаОбщиеНастройкиИсключения.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаОбщиеНастройкиСчетУчета.Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаОбщиеНастройки.ТребуетсяНастройка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВариантОтбораОбъектовУчета");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 1; // Требующие настройки
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость",  Ложь);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокИсключенияОрганизация.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокИсключения.Организация");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Все';
																|en = 'All'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокИсключенияМестоУчета.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокИсключения.МестоУчета");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Все';
																|en = 'All'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	
	//

	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаОбщиеНастройкиОбъектНастройки.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаОбщиеНастройки.ОбъектНастройки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Неопределено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Общая';
																|en = 'General'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаОбщиеНастройкиОбъектНастройки.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаОбщиеНастройки.ОбъектНастройки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не указан(а)';
																|en = 'Not specified'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокИсключенияОбъектНастройки.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокИсключения.ОбъектНастройки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не указан(а)';
																|en = 'Not specified'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	
	//

	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиОтраженияКорреспонденцийПрименятьПоследующиеНастройки.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НастройкиОтраженияКорреспонденций.ПрименятьПоследующиеНастройки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не применять';
																|en = 'Do not apply'"));
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиОтраженияКорреспонденцийПрименятьПоследующиеНастройки.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НастройкиОтраженияКорреспонденций.ПрименятьПоследующиеНастройки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Применять';
																|en = 'Apply'"));
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоОбъектовБухгалтерскогоУчетаПоКорреспонденциям.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоОбъектовБухгалтерскогоУчетаПоКорреспонденциямЭтоГруппа.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоОбъектовБухгалтерскогоУчетаПоКорреспонденциямПредставление.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоОбъектовБухгалтерскогоУчетаПоКорреспонденциямКоличество.Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоОбъектовБухгалтерскогоУчета.НастройкиОтраженияКорреспонденцийКоличество");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВариантОтбораОбъектовУчетаКорреспонденция");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 1; // С настройками
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость",  Ложь);
	
	//
	
	#Область НастройкиПоСчету
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиПоСчетуОбъектУчета.Имя);
	
	ОтборЭлемента =  Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НастройкиПоСчету.ТипНастройки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке;
	СписокТиповНастройки = Новый СписокЗначений();
	СписокТиповНастройки.Добавить(Перечисления.ТипыНастроекОтраженияВМеждународномУчете.ОбщаяПоОбъектуУчета);
	СписокТиповНастройки.Добавить(Перечисления.ТипыНастроекОтраженияВМеждународномУчете.ИсключениеПоОбъектуУчета);
	ОтборЭлемента.ПравоеЗначение = СписокТиповНастройки;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиПоСчетуЕстьДополнительныйОтборДт.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиПоСчетуОбъектУчетаДт.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиПоСчетуЕстьДополнительныйОтборКт.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиПоСчетуОбъектУчетаКт.Имя);
	
	ОтборЭлемента =  Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НастройкиПоСчету.ТипНастройки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ТипыНастроекОтраженияВМеждународномУчете.ПоКорреспонденции;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	#КонецОбласти
	
КонецПроцедуры

#Область СерверныеОбработчикиЭлементомШапкиФормы

&НаСервере
Процедура ОрганизацияПриИзмененииСервер()
	
	ПолучитьСтатусНастройки();
	УстановитьОсновнойОтборСпискаИсключений(СписокИсключения, Организация, ПланСчетов, НастройкаФормированияПроводок);
	УстановитьОсновнойОтборНастроекОтраженияКорреспонденций(НастройкиОтраженияКорреспонденций, ПланСчетов, НастройкаФормированияПроводок);
	
КонецПроцедуры

&НаСервере
Процедура ПланСчетовПриИзмененииСервер()
	
	НастройкаФормированияПроводок = 
		Справочники.НастройкиФормированияПроводокМеждународногоУчета.НастройкаФормированияПроводокПоУмолчанию(ПланСчетов);
	НастройкаФормированияПроводокПриИзмененииСервер();
	УстановитьОсновнойОтборСпискаИсключений(СписокИсключения, Организация, ПланСчетов, НастройкаФормированияПроводок);
	УстановитьОсновнойОтборНастроекОтраженияКорреспонденций(НастройкиОтраженияКорреспонденций, ПланСчетов, НастройкаФормированияПроводок);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		ПланСчетовМеждународный, 
		"ПланСчетов",
		ПланСчетов,
		,
		,
		Истина,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		
КонецПроцедуры

&НаСервере
Процедура НастройкаФормированияПроводокПриИзмененииСервер()
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		ПланСчетовМеждународный, 
		"НастройкаФормированияПроводок",
		НастройкаФормированияПроводок);
	
	ЗаполнитьТаблицыНастройки();
	ПолучитьСтатусНастройки();
	ЗаполнитьИнформациюПоНастройкам();
	УстановитьОсновнойОтборСпискаИсключений(СписокИсключения, Организация, ПланСчетов, НастройкаФормированияПроводок);
	УстановитьОсновнойОтборНастроекОтраженияКорреспонденций(НастройкиОтраженияКорреспонденций, ПланСчетов, НастройкаФормированияПроводок);
	
	Если Элементы.ПланСчетовМеждународный.ТекущаяСтрока <> Неопределено Тогда
		ПолучитьНастройкиПоСчету(Элементы.ПланСчетовМеждународный.ТекущаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСтатусНастройки()
	
	Если Не ЗначениеЗаполнено(ПланСчетов) 
			Или Не ЗначениеЗаполнено(Организация)
			Или Не ЗначениеЗаполнено(НастройкаФормированияПроводок) Тогда
		СтатусНастройки = Неопределено;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(ПланыСчетовМеждународногоУчета.Период) КАК Период
	|ПОМЕСТИТЬ ВтПрименяетсяС
	|ИЗ
	|	РегистрСведений.ПланыСчетовМеждународногоУчетаОрганизаций КАК ПланыСчетовМеждународногоУчета
	|ГДЕ
	|	ПланыСчетовМеждународногоУчета.Организация = &Организация
	|	И ПланыСчетовМеждународногоУчета.ПланСчетов = &ПланСчетов
	|	И ПланыСчетовМеждународногоУчета.НастройкаФормированияПроводок = &НастройкаФормированияПроводок
	|;
	|
	|/////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрименяетсяС.Период КАК ПериодС,
	|	МИНИМУМ(ЕСТЬNULL(ПланыСчетовМеждународногоУчета.Период, &ПустаяДата)) КАК ПериодПо
	|ИЗ
	|	ВтПрименяетсяС КАК ПрименяетсяС
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.ПланыСчетовМеждународногоУчетаОрганизаций КАК ПланыСчетовМеждународногоУчета
	|	ПО
	|		ПланыСчетовМеждународногоУчета.Организация = &Организация
	|		И ПланыСчетовМеждународногоУчета.ПланСчетов = &ПланСчетов
	|		И ПланыСчетовМеждународногоУчета.НастройкаФормированияПроводок <> &НастройкаФормированияПроводок
	|		И ПланыСчетовМеждународногоУчета.Период >ПрименяетсяС.Период
	|ГДЕ
	|	ПрименяетсяС.Период <>&ПустаяДата
	|
	|СГРУППИРОВАТЬ ПО
	|	ПрименяетсяС.Период";
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПланСчетов", ПланСчетов);
	Запрос.УстановитьПараметр("НастройкаФормированияПроводок", НастройкаФормированияПроводок);
	Запрос.УстановитьПараметр("ПустаяДата", Дата(1,1,1));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Если ЗначениеЗаполнено(Выборка.ПериодПо) Тогда
			Представление = СтрШаблон(НСтр("ru = 'Применялась с %1 по %2';
											|en = 'Has been applied since %1 till %2'"), 
													Формат(Выборка.ПериодС, "ДЛФ=D"), Формат(Выборка.ПериодПо, "ДЛФ=D"));
			СтатусНастройки = Новый ФорматированнаяСтрока(
													Представление, , , , "ОткрытьИсториюПланыСчетовОрганизаций");
		Иначе
			Представление = СтрШаблон(НСтр("ru = 'Применяется с %1';
											|en = 'Applied since %1'"), Формат(Выборка.ПериодС, "ДЛФ=D"));
			СтатусНастройки = Новый ФорматированнаяСтрока(
													Представление, , , , "ОткрытьИсториюПланыСчетовОрганизаций");
		КонецЕсли;
		
	ИначеЕсли ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ПланыСчетовМеждународногоУчетаОрганизаций) Тогда 
		Представление = СтрШаблон(НСтр("ru = 'Включить для организации с %1';
										|en = 'Enable for company since %1'"), Формат(ТекущийМесяц, "ДЛФ=D"));
		СтатусНастройки = Новый ФорматированнаяСтрока(Представление, , ЦветаСтиля.ГиперссылкаЦвет , , "СоздатьЗаписьПланыСчетовОрганизаций");
	Иначе
		Представление = СтрШаблон(НСтр("ru = 'Не применяется для организации';
										|en = 'Not applied for the company'"));
		СтатусНастройки = Новый ФорматированнаяСтрока(Представление, , ЦветаСтиля.ЦветТекстаВнимание);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыборПериодаЗаписиПланыСчетовОрганизаций(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	СоздатьЗаписьПланыСчетовОрганизаций(Результат);
	
КонецПроцедуры

&НаСервере
Процедура СоздатьЗаписьПланыСчетовОрганизаций(Период)
	
	МенеджерЗаписи = РегистрыСведений.ПланыСчетовМеждународногоУчетаОрганизаций.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период = Период;
	МенеджерЗаписи.ПланСчетов = ПланСчетов;
	МенеджерЗаписи.Организация = Организация;
	МенеджерЗаписи.НастройкаФормированияПроводок = НастройкаФормированияПроводок;
	МенеджерЗаписи.Записать();
	ПолучитьСтатусНастройки();
	
КонецПроцедуры


#КонецОбласти

#Область НастройкиПоОбъектамУчета

&НаСервере
Процедура ЗаполнитьДеревоОбъектовБухгалтерскогоУчета()
	
	ДеревоОбъектовБухгалтерскогоУчета.ПолучитьЭлементы().Очистить();
	ОбщегоНазначения.ЗаполнитьКоллекциюЭлементовДереваДанныхФормы(
		ДеревоОбъектовБухгалтерскогоУчета.ПолучитьЭлементы(),
		Перечисления.ОбъектыБухгалтерскогоУчета.ДеревоОбъектовУчета());
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИнформациюПоНастройкам(ТолькоТекущийОбъектУчета = Ложь)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СчетаМеждународногоУчетаТребующиеНастройки.ОбъектУчета КАК ОбъектУчета,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СчетаМеждународногоУчетаТребующиеНастройки.ОбъектНастройки) КАК Количество
	|ИЗ
	|	РегистрСведений.СчетаМеждународногоУчетаТребующиеНастройки КАК СчетаМеждународногоУчетаТребующиеНастройки
	|ГДЕ
	|	СчетаМеждународногоУчетаТребующиеНастройки.ПланСчетов = &ПланСчетов
	|	И СчетаМеждународногоУчетаТребующиеНастройки.НастройкаФормированияПроводок = &НастройкаФормированияПроводок
	|	И (СчетаМеждународногоУчетаТребующиеНастройки.ОбъектУчета = &ТекущийОбъектУчета ИЛИ &ВсеОбъектыУчета) 
	|СГРУППИРОВАТЬ ПО
	|	СчетаМеждународногоУчетаТребующиеНастройки.ОбъектУчета
	|;
	|
	|///////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиСчетовМеждународногоУчетаПоОбъектам.ОбъектУчета КАК ОбъектУчета,
	|	СУММА(1) КАК Количество
	|ИЗ
	|	РегистрСведений.НастройкиСчетовМеждународногоУчетаПоОбъектам КАК НастройкиСчетовМеждународногоУчетаПоОбъектам
	|ГДЕ
	|	НастройкиСчетовМеждународногоУчетаПоОбъектам.ПланСчетов = &ПланСчетов
	|	И НастройкиСчетовМеждународногоУчетаПоОбъектам.НастройкаФормированияПроводок = &НастройкаФормированияПроводок
	|	И НастройкиСчетовМеждународногоУчетаПоОбъектам.ТипНастройки = &ТипНастройкиОбщая
	|	И (НастройкиСчетовМеждународногоУчетаПоОбъектам.ОбъектУчета = &ТекущийОбъектУчета ИЛИ &ВсеОбъектыУчета) 
	|
	|СГРУППИРОВАТЬ ПО
	|	НастройкиСчетовМеждународногоУчетаПоОбъектам.ОбъектУчета
	|;
	|
	|///////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиСчетовМеждународногоУчетаПоОбъектам.ОбъектУчета КАК ОбъектУчета,
	|	НастройкиСчетовМеждународногоУчетаПоОбъектам.ОбъектНастройки КАК ОбъектНастройки,
	|	СУММА(1) КАК Количество
	|ИЗ
	|	РегистрСведений.НастройкиСчетовМеждународногоУчетаПоОбъектам КАК НастройкиСчетовМеждународногоУчетаПоОбъектам
	|ГДЕ
	|	НастройкиСчетовМеждународногоУчетаПоОбъектам.ПланСчетов = &ПланСчетов
	|	И НастройкиСчетовМеждународногоУчетаПоОбъектам.НастройкаФормированияПроводок = &НастройкаФормированияПроводок
	|	И НастройкиСчетовМеждународногоУчетаПоОбъектам.ТипНастройки = &ТипНастройкиИсключение
	|	И (НастройкиСчетовМеждународногоУчетаПоОбъектам.ОбъектУчета = &ТекущийОбъектУчета ИЛИ &ВсеОбъектыУчета) 
	|
	|СГРУППИРОВАТЬ ПО
	|	НастройкиСчетовМеждународногоУчетаПоОбъектам.ОбъектУчета,
	|	НастройкиСчетовМеждународногоУчетаПоОбъектам.ОбъектНастройки
	|
	|ИТОГИ 
	|	СУММА(Количество)
	|ПО
	|	ОбъектУчета
	|;
	|
	|///////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.ОбъектУчета КАК ОбъектУчета,
	|	СУММА(ВложенныйЗапрос.Количество) КАК Количество
	|ИЗ
	|	(ВЫБРАТЬ
	|		НастройкиОтраженияКорреспонденцийВМеждународномУчете.ОбъектУчетаДт КАК ОбъектУчета,
	|		1 КАК Количество
	|	ИЗ
	|		РегистрСведений.НастройкиОтраженияКорреспонденцийВМеждународномУчете КАК НастройкиОтраженияКорреспонденцийВМеждународномУчете
	|	ГДЕ
	|		НастройкиОтраженияКорреспонденцийВМеждународномУчете.ПланСчетов = &ПланСчетов
	|		И НастройкиОтраженияКорреспонденцийВМеждународномУчете.НастройкаФормированияПроводок = &НастройкаФормированияПроводок
	|		И (НастройкиОтраженияКорреспонденцийВМеждународномУчете.ОбъектУчетаДт = &ТекущийОбъектУчета ИЛИ &ВсеОбъектыУчета)
	|		И НастройкиОтраженияКорреспонденцийВМеждународномУчете.ОбъектУчетаКт <> НастройкиОтраженияКорреспонденцийВМеждународномУчете.ОбъектУчетаДт
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		НастройкиОтраженияКорреспонденцийВМеждународномУчете.ОбъектУчетаКт КАК ОбъектУчета,
	|		1 КАК Количество
	|	ИЗ
	|		РегистрСведений.НастройкиОтраженияКорреспонденцийВМеждународномУчете КАК НастройкиОтраженияКорреспонденцийВМеждународномУчете
	|	ГДЕ
	|		НастройкиОтраженияКорреспонденцийВМеждународномУчете.ПланСчетов = &ПланСчетов
	|		И НастройкиОтраженияКорреспонденцийВМеждународномУчете.НастройкаФормированияПроводок = &НастройкаФормированияПроводок
	|		И (НастройкиОтраженияКорреспонденцийВМеждународномУчете.ОбъектУчетаКт = &ТекущийОбъектУчета ИЛИ &ВсеОбъектыУчета)
	|		И НастройкиОтраженияКорреспонденцийВМеждународномУчете.ОбъектУчетаДт <> НастройкиОтраженияКорреспонденцийВМеждународномУчете.ОбъектУчетаКт 
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		НастройкиОтраженияКорреспонденцийВМеждународномУчете.ОбъектУчетаДт КАК ОбъектУчета,
	|		1 КАК Количество
	|	ИЗ
	|		РегистрСведений.НастройкиОтраженияКорреспонденцийВМеждународномУчете КАК НастройкиОтраженияКорреспонденцийВМеждународномУчете
	|	ГДЕ
	|		НастройкиОтраженияКорреспонденцийВМеждународномУчете.ПланСчетов = &ПланСчетов
	|		И НастройкиОтраженияКорреспонденцийВМеждународномУчете.НастройкаФормированияПроводок = &НастройкаФормированияПроводок
	|		И (НастройкиОтраженияКорреспонденцийВМеждународномУчете.ОбъектУчетаДт = &ТекущийОбъектУчета ИЛИ &ВсеОбъектыУчета)
	|		И НастройкиОтраженияКорреспонденцийВМеждународномУчете.ОбъектУчетаДт = НастройкиОтраженияКорреспонденцийВМеждународномУчете.ОбъектУчетаКт) ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.ОбъектУчета
	|";
	Запрос.УстановитьПараметр("ПланСчетов", ПланСчетов);
	Запрос.УстановитьПараметр("НастройкаФормированияПроводок", НастройкаФормированияПроводок);
	Запрос.УстановитьПараметр("ТипНастройкиОбщая", ТипНастройкиОбщаяПоОбъектуУчета);
	Запрос.УстановитьПараметр("ТипНастройкиИсключение", ТипНастройкиИсключениеПоОбъектуУчета);
	Запрос.УстановитьПараметр("ТекущийОбъектУчета", ТекущийОбъектУчета);
	Запрос.УстановитьПараметр("ВсеОбъектыУчета", НЕ ТолькоТекущийОбъектУчета);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ОбъектыТребуетсяНастройкаСчетовУчета = Новый Соответствие();
	ВыборкаТребуетсяНастройкаСчетовУчета = РезультатЗапроса[0].Выбрать();
	Пока ВыборкаТребуетсяНастройкаСчетовУчета.Следующий() Цикл
		ОбъектыТребуетсяНастройкаСчетовУчета.Вставить(ВыборкаТребуетсяНастройкаСчетовУчета.ОбъектУчета, ВыборкаТребуетсяНастройкаСчетовУчета.Количество);
	КонецЦикла;
	
	ОбъектыЕстьОбщиеНастройки = Новый Соответствие();
	ВыборкаЕстьОбщиеНастройки = РезультатЗапроса[1].Выбрать();
	Пока ВыборкаЕстьОбщиеНастройки.Следующий() Цикл
		ОбъектыЕстьОбщиеНастройки.Вставить(
			ВыборкаЕстьОбщиеНастройки.ОбъектУчета,
			ВыборкаЕстьОбщиеНастройки.Количество);
	КонецЦикла;
	
	ВыборкаЕстьНастройкиИсключений = РезультатЗапроса[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	ОбъектыЕстьНастройкиОтраженияКорреспонденций = Новый Соответствие();
	ВыборкаЕстьНастройкиОтраженияКорреспонденций = РезультатЗапроса[3].Выбрать();
	Пока ВыборкаЕстьНастройкиОтраженияКорреспонденций.Следующий() Цикл
		ОбъектыЕстьНастройкиОтраженияКорреспонденций.Вставить(
			ВыборкаЕстьНастройкиОтраженияКорреспонденций.ОбъектУчета, 
			ВыборкаЕстьНастройкиОтраженияКорреспонденций.Количество);
	КонецЦикла;
	
	Если ТолькоТекущийОбъектУчета Тогда
		
		СтрокаДерева = ДеревоОбъектовБухгалтерскогоУчета.НайтиПоИдентификатору(Элементы.ДеревоОбъектовБухгалтерскогоУчета.ТекущаяСтрока);
		ТребуетсяНастройкаСчетовУчетаКоличество = ОбъектыТребуетсяНастройкаСчетовУчета.Получить(СтрокаДерева.ОбъектУчета) ;
		Если ТребуетсяНастройкаСчетовУчетаКоличество  <> Неопределено Тогда
			СтрокаДерева.ТребуетсяНастройкаСчетовУчетаКоличество = ТребуетсяНастройкаСчетовУчетаКоличество;
		Иначе
			СтрокаДерева.ТребуетсяНастройкаСчетовУчетаКоличество = 0;
		КонецЕсли;
		
		ОбщиеНастройкиКоличество = ОбъектыЕстьОбщиеНастройки.Получить(СтрокаДерева.ОбъектУчета);
		Если ОбщиеНастройкиКоличество <> Неопределено Тогда
			СтрокаДерева.ОбщиеНастройкиКоличество = ОбщиеНастройкиКоличество;
		Иначе
			СтрокаДерева.ОбщиеНастройкиКоличество = 0;
		КонецЕсли;
		
		ВыборкаЕстьНастройкиИсключений.Сбросить();
		Если ВыборкаЕстьНастройкиИсключений.НайтиСледующий(Новый Структура("ОбъектУчета", СтрокаДерева.ОбъектУчета)) Тогда
			СтрокаДерева.НастройкиИсключенийКоличество = ВыборкаЕстьНастройкиИсключений.Количество;
			
			ВыборкаИсключенийПоОбъектамНастройки = ВыборкаЕстьНастройкиИсключений.Выбрать();
			КоличествоИсключенияПоОбъектамНастройки = Новый Соответствие();
			Пока ВыборкаИсключенийПоОбъектамНастройки.Следующий() Цикл
				КоличествоИсключенияПоОбъектамНастройки.Вставить(
					ВыборкаИсключенийПоОбъектамНастройки.ОбъектНастройки,
					ВыборкаИсключенийПоОбъектамНастройки.Количество);
			КонецЦикла;
			
			Для каждого СтрокаТаблицыОбщихНастроек Из ТаблицаОбщиеНастройки Цикл
				КоличествоИсключений = КоличествоИсключенияПоОбъектамНастройки[СтрокаТаблицыОбщихНастроек.ОбъектНастройки];
				Если КоличествоИсключений = Неопределено Тогда
					СтрокаТаблицыОбщихНастроек.Исключения  =   НСтр("ru = 'Настроить';
																	|en = 'Set up'");
					СтрокаТаблицыОбщихНастроек.КоличествоИсключений = 0;
				Иначе
					СтрокаТаблицыОбщихНастроек.Исключения = СтрШаблон(НСтр("ru = 'Настроить (%1)';
																			|en = 'Set up (%1)'"), КоличествоИсключений);
					СтрокаТаблицыОбщихНастроек.КоличествоИсключений = КоличествоИсключений;
				КонецЕсли;
			КонецЦикла; 
			
		Иначе
			СтрокаДерева.НастройкиИсключенийКоличество = 0;
			Для каждого СтрокаТаблицыОбщихНастроек Из ТаблицаОбщиеНастройки Цикл
				СтрокаТаблицыОбщихНастроек.Исключения = НСтр("ru = 'Настроить';
															|en = 'Set up'");
				СтрокаТаблицыОбщихНастроек.КоличествоИсключений = 0;
			КонецЦикла; 
		КонецЕсли;
		
		ЭлементИсключения =  Элементы.РежимНастройки.СписокВыбора.НайтиПоЗначению(1);
		ЭлементИсключения.Представление = СтрШаблон(НСтр("ru = 'Исключения (%1)';
														|en = 'Exceptions (%1)'"), СтрокаДерева.НастройкиИсключенийКоличество);
		
		НастройкиОтраженияКорреспонденцийКоличество = ОбъектыЕстьНастройкиОтраженияКорреспонденций.Получить(СтрокаДерева.ОбъектУчета);
		Если НастройкиОтраженияКорреспонденцийКоличество <> Неопределено Тогда
			СтрокаДерева.НастройкиОтраженияКорреспонденцийКоличество = НастройкиОтраженияКорреспонденцийКоличество;
		Иначе
			СтрокаДерева.НастройкиОтраженияКорреспонденцийКоличество = 0;
		КонецЕсли;
		
		Если СтрокаДерева.ПолучитьРодителя() = Неопределено Тогда
			ОбновитьИтогиПоГруппеДерева(СтрокаДерева);
		Иначе
			ОбновитьИтогиПоГруппеДерева(СтрокаДерева.ПолучитьРодителя());
		КонецЕсли;
		
	Иначе
		Для каждого ГруппаДерева Из ДеревоОбъектовБухгалтерскогоУчета.ПолучитьЭлементы() Цикл
			
			Для каждого СтрокаДерева Из ГруппаДерева.ПолучитьЭлементы() Цикл 
				
				ТребуетсяНастройкаСчетовУчетаКоличество = ОбъектыТребуетсяНастройкаСчетовУчета.Получить(СтрокаДерева.ОбъектУчета) ;
				Если ТребуетсяНастройкаСчетовУчетаКоличество  <> Неопределено Тогда
					СтрокаДерева.ТребуетсяНастройкаСчетовУчетаКоличество = ТребуетсяНастройкаСчетовУчетаКоличество;
				Иначе
					СтрокаДерева.ТребуетсяНастройкаСчетовУчетаКоличество = 0;
				КонецЕсли;
				
				ОбщиеНастройкиКоличество = ОбъектыЕстьОбщиеНастройки.Получить(СтрокаДерева.ОбъектУчета);
				Если ОбщиеНастройкиКоличество <> Неопределено Тогда
					СтрокаДерева.ОбщиеНастройкиКоличество = ОбщиеНастройкиКоличество;
				Иначе
					СтрокаДерева.ОбщиеНастройкиКоличество = 0;
				КонецЕсли;
				
				ВыборкаЕстьНастройкиИсключений.Сбросить();
				Если ВыборкаЕстьНастройкиИсключений.НайтиСледующий(Новый Структура("ОбъектУчета", СтрокаДерева.ОбъектУчета)) Тогда
					СтрокаДерева.НастройкиИсключенийКоличество = ВыборкаЕстьНастройкиИсключений.Количество;
				Иначе
					СтрокаДерева.НастройкиИсключенийКоличество = 0;
				КонецЕсли;
				
				НастройкиОтраженияКорреспонденцийКоличество = ОбъектыЕстьНастройкиОтраженияКорреспонденций.Получить(СтрокаДерева.ОбъектУчета);
				Если НастройкиОтраженияКорреспонденцийКоличество <> Неопределено Тогда
					СтрокаДерева.НастройкиОтраженияКорреспонденцийКоличество = НастройкиОтраженияКорреспонденцийКоличество;
				Иначе
					СтрокаДерева.НастройкиОтраженияКорреспонденцийКоличество = 0;
				КонецЕсли;
		
			КонецЦикла;
			
			ОбновитьИтогиПоГруппеДерева(ГруппаДерева)
			
		КонецЦикла;
	
	КонецЕсли;
	
	СформироватьСсылкуНастройкаЗаполненияСубконто();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИтогиПоГруппеДерева(ГруппаДерева)
	
	ТребуетсяНастройкаСчетовУчетаКоличество = 0;
	НастройкиИсключенийКоличество = 0;
	ОбщиеНастройкиКоличество = 0;
	НастройкиОтраженияКорреспонденцийКоличество = 0;
	
	Для каждого СтрокаДерева Из ГруппаДерева.ПолучитьЭлементы() Цикл
		ТребуетсяНастройкаСчетовУчетаКоличество = 
			ТребуетсяНастройкаСчетовУчетаКоличество 
			+ СтрокаДерева.ТребуетсяНастройкаСчетовУчетаКоличество;
			
		ОбщиеНастройкиКоличество = 
			ОбщиеНастройкиКоличество
			+ СтрокаДерева.ОбщиеНастройкиКоличество;
			
		НастройкиИсключенийКоличество = 
			НастройкиИсключенийКоличество
			+ СтрокаДерева.НастройкиИсключенийКоличество;
			
		НастройкиОтраженияКорреспонденцийКоличество =
			НастройкиОтраженияКорреспонденцийКоличество
			+ СтрокаДерева.НастройкиОтраженияКорреспонденцийКоличество;
			
	КонецЦикла;
	
	ГруппаДерева.ТребуетсяНастройкаСчетовУчетаКоличество = ТребуетсяНастройкаСчетовУчетаКоличество;
	ГруппаДерева.ОбщиеНастройкиКоличество = ОбщиеНастройкиКоличество;
	ГруппаДерева.НастройкиИсключенийКоличество = НастройкиИсключенийКоличество;
	ГруппаДерева.НастройкиОтраженияКорреспонденцийКоличество = НастройкиОтраженияКорреспонденцийКоличество;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицыНастройки()
	
	ТаблицаОбщиеНастройки.Очистить();
	
	Если Элементы.ДеревоОбъектовБухгалтерскогоУчета.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрокаДерева = ДеревоОбъектовБухгалтерскогоУчета.НайтиПоИдентификатору(Элементы.ДеревоОбъектовБухгалтерскогоУчета.ТекущаяСтрока);

	Если ТекущаяСтрокаДерева.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапросаВтТаблицаОбъектНастройки = Новый Массив;
	ТекстыЗапросаВтТаблицаОбъектНастройки.Добавить(
		"ВЫБРАТЬ
		|	НЕОПРЕДЕЛЕНО КАК Ссылка
		|ПОМЕСТИТЬ ВтТаблицаОбъектНастройки
		|ГДЕ ЛОЖЬ");
	
	Если Не ЗначениеЗаполнено(ТекущаяСтрокаДерева.ОписаниеТиповОбъектНастройки) Тогда
		ТекстыЗапросаВтТаблицаОбъектНастройки.Добавить(
			"ВЫБРАТЬ
			|	НЕОПРЕДЕЛЕНО КАК Ссылка
			|");
	Иначе
		ТекстЗапроса = 
			"ВЫБРАТЬ 
			|	НЕОПРЕДЕЛЕНО КАК Ссылка
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ОбъектыНастройки.Ссылка КАК Ссылка
			|ИЗ
			|	&ТаблицаОбъектНастройки КАК ОбъектыНастройки
			|ГДЕ
			|	&ДополнительныйОтбор
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Ссылка
			|";
		ТипОбъектаНастройки = ТекущаяСтрокаДерева.ОписаниеТиповОбъектНастройки.Типы()[0];
		МетаданныеОбъектНастройки = Метаданные.НайтиПоТипу(ТипОбъектаНастройки);
		
		ПолноеИмяОбъектаМетаданных = МетаданныеОбъектНастройки.ПолноеИмя();
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаОбъектНастройки", ПолноеИмяОбъектаМетаданных);
		Запрос.УстановитьПараметр("ПустаяСсылка", Новый (ТипОбъектаНастройки));
		
		Если ЗначениеЗаполнено(ТекущаяСтрокаДерева.ОтборОбъектовНастройки) Тогда
			ДополнительныйОтбор = МеждународныйУчетПоДаннымОстаточныхФинансовыхРегистров.СтрокаУсловияВыборкиДанных(ТекущаяСтрокаДерева.ОтборОбъектовНастройки, Запрос.Параметры);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДополнительныйОтбор", ДополнительныйОтбор);
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДополнительныйОтбор", "ИСТИНА");
		КонецЕсли;
	
		ТекстыЗапросаВтТаблицаОбъектНастройки.Добавить(ТекстЗапроса);
	КонецЕсли;
	
	ТекстЗапросаВтТаблицаОбъектНастройки = 
		СтрСоединить(ТекстыЗапросаВтТаблицаОбъектНастройки, ОбщегоНазначенияУт.РазделительЗапросовВОбъединении());
	
	Запрос.Текст =
		ТекстЗапросаВтТаблицаОбъектНастройки
		+ ОбщегоНазначенияУт.РазделительЗапросовВПакете()
		+ "ВЫБРАТЬ
		|	ОбъектыНастройки.Ссылка КАК ОбъектНастройки,
		|	ПорядокОтраженияОбщие.СчетУчета КАК СчетУчета,
		|	КОЛИЧЕСТВО(ПорядокОтраженияИсключения.СчетУчета) КАК КоличествоИсключений
		|ПОМЕСТИТЬ ТаблицаНастройки
		|ИЗ
		|	ВтТаблицаОбъектНастройки КАК ОбъектыНастройки
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрСведений.НастройкиСчетовМеждународногоУчетаПоОбъектам КАК ПорядокОтраженияОбщие
		|	ПО
		|		ПорядокОтраженияОбщие.ПланСчетов = &ПланСчетов
		|		И ПорядокОтраженияОбщие.НастройкаФормированияПроводок = &НастройкаФормированияПроводок
		|		И ПорядокОтраженияОбщие.ТипНастройки = &ТипНастройкиОбщая
		|		И ПорядокОтраженияОбщие.ОбъектУчета = &ОбъектУчета
		|		И ОбъектыНастройки.Ссылка = ПорядокОтраженияОбщие.ОбъектНастройки
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ 
		|		РегистрСведений.НастройкиСчетовМеждународногоУчетаПоОбъектам КАК ПорядокОтраженияИсключения
		|	ПО
		|		ПорядокОтраженияИсключения.ПланСчетов = &ПланСчетов
		|		И ПорядокОтраженияИсключения.НастройкаФормированияПроводок = &НастройкаФормированияПроводок
		|		И ПорядокОтраженияИсключения.ТипНастройки = &ТипНастройкиИсключение
		|		И ПорядокОтраженияИсключения.ОбъектУчета = &ОбъектУчета
		|		И ОбъектыНастройки.Ссылка = ПорядокОтраженияИсключения.ОбъектНастройки
		|
		|СГРУППИРОВАТЬ ПО
		|	ОбъектыНастройки.Ссылка,
		|	ПорядокОтраженияОбщие.СчетУчета
		|;
		|
		|ВЫБРАТЬ
		|	ТаблицаНастройки.ОбъектНастройки КАК ОбъектНастройки,
		|	ТаблицаНастройки.СчетУчета КАК СчетУчета,
		|	ТаблицаНастройки.КоличествоИсключений КАК КоличествоИсключений,
		|	ВЫБОР
		|		КОГДА ИСТИНА В (
		|				ВЫБРАТЬ ПЕРВЫЕ 1
		|					ИСТИНА
		|				ИЗ
		|					РегистрСведений.СчетаМеждународногоУчетаТребующиеНастройки КАК СчетаМеждународногоУчетаТребующиеНастройки
		|				ГДЕ
		|					СчетаМеждународногоУчетаТребующиеНастройки.ПланСчетов = &ПланСчетов
		|					И СчетаМеждународногоУчетаТребующиеНастройки.НастройкаФормированияПроводок = &НастройкаФормированияПроводок
		|					И СчетаМеждународногоУчетаТребующиеНастройки.ОбъектУчета = &ОбъектУчета
		|					И ТаблицаНастройки.ОбъектНастройки = СчетаМеждународногоУчетаТребующиеНастройки.ОбъектНастройки)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ТребуетсяНастройка
		|ИЗ
		|	ТаблицаНастройки КАК ТаблицаНастройки
		|";
	
	Запрос.УстановитьПараметр("ПланСчетов", ПланСчетов);
	Запрос.УстановитьПараметр("НастройкаФормированияПроводок", НастройкаФормированияПроводок);
	Запрос.УстановитьПараметр("ТипНастройкиОбщая", ТипНастройкиОбщаяПоОбъектуУчета);
	Запрос.УстановитьПараметр("ТипНастройкиИсключение", ТипНастройкиИсключениеПоОбъектуУчета);
	Запрос.УстановитьПараметр("ОбъектУчета", ТекущаяСтрокаДерева.ОбъектУчета);
	
	ТаблицаОбщиеНастройки.Загрузить(Запрос.Выполнить().Выгрузить());
	Для каждого СтрокаТаблицы Из ТаблицаОбщиеНастройки Цикл
		Если СтрокаТаблицы.КоличествоИсключений = 0 Тогда
			СтрокаТаблицы.Исключения  =   НСтр("ru = 'Настроить';
												|en = 'Set up'");
		Иначе
			СтрокаТаблицы.Исключения = СтрШаблон(НСтр("ru = 'Настроить (%1)';
														|en = 'Set up (%1)'"), СтрокаТаблицы.КоличествоИсключений);
		КонецЕсли;
	КонецЦикла;
	
	Если ТекущийОбъектНастройкиПриОткрытии <> Неопределено Тогда
		МассивСтрок = ТаблицаОбщиеНастройки.НайтиСтроки(Новый Структура("ОбъектНастройки", ТекущийОбъектНастройкиПриОткрытии));
		Если МассивСтрок.Количество() > 0 Тогда
			Элементы.ТаблицаОбщиеНастройки.ТекущаяСтрока = МассивСтрок[0].ПолучитьИдентификатор();
		КонецЕсли;
		ТекущийОбъектНастройкиПриОткрытии = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормыПриИзмененииОбъектаУчета()
	
	ТекущаяСтрокаДерева = ДеревоОбъектовБухгалтерскогоУчета.НайтиПоИдентификатору(Элементы.ДеревоОбъектовБухгалтерскогоУчета.ТекущаяСтрока);

	ЭлементИсключения =  Элементы.РежимНастройки.СписокВыбора.НайтиПоЗначению(1);
	ЭлементИсключения.Представление = СтрШаблон(НСтр("ru = 'Исключения (%1)';
													|en = 'Exceptions (%1)'"), ТекущаяСтрокаДерева.НастройкиИсключенийКоличество);
	
	Элементы.ТаблицаОбщиеНастройкиИсключения.Видимость = ЗначениеЗаполнено(ТекущаяСтрокаДерева.ОписаниеТиповОбъектНастройки);
	Элементы.СписокИсключенияОбъектНастройки.Видимость = ЗначениеЗаполнено(ТекущаяСтрокаДерева.ОписаниеТиповОбъектНастройки);
	
	Элементы.ТаблицаОбщиеНастройкиОбъектНастройки.ОграничениеТипа = ТекущаяСтрокаДерева.ОписаниеТиповОбъектНастройки;
	Элементы.СписокИсключенияОбъектНастройки.ОграничениеТипа = ТекущаяСтрокаДерева.ОписаниеТиповОбъектНастройки;

	ЗаголовокОбъектНастройки = Перечисления.ОбъектыБухгалтерскогоУчета.ЗаголовокОбъектаНастройкиПоОписаниюТипов(ТекущаяСтрокаДерева.ОписаниеТиповОбъектНастройки);
	Элементы.ТаблицаОбщиеНастройкиОбъектНастройки.Заголовок = ЗаголовокОбъектНастройки;
	Элементы.СписокИсключенияОбъектНастройки.Заголовок = ЗаголовокОбъектНастройки;
	
	Элементы.СписокИсключенияМестоУчета.Видимость = ТекущаяСтрокаДерева.ОписаниеТиповМестоУчета.Типы().Количество() > 0;
	Элементы.СписокИсключенияМестоУчета.ОграничениеТипа = ТекущаяСтрокаДерева.ОписаниеТиповМестоУчета;
	Элементы.СписокИсключенияМестоУчета.Заголовок = Перечисления.ОбъектыБухгалтерскогоУчета.ЗаголовокМестаУчета(ТекущаяСтрокаДерева.ОписаниеТиповМестоУчета);
	
	Обработки.НастройкаСчетовМеждународногоУчета.ИнициализироватьКомпоновщикНастроек(ЭтотОбъект, ТекущаяСтрокаДерева.ИсточникДанных);
		
	СформироватьСсылкуНастройкаЗаполненияСубконто();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьСсылкуНастройкаЗаполненияСубконто()
	
	ОтборНастроек = 
		Обработки.НастройкаСчетовМеждународногоУчета.ОтборНастроекЗаполненияСубконто(
			ПланСчетов, 
			НастройкаФормированияПроводок,
			ТекущийОбъектУчета);
	НастройкиЗаполненияСубконто = 
		Обработки.НастройкаСчетовМеждународногоУчета.НастройкиЗаполненияСубконто(
			ОтборНастроек,
			КомпоновщикНастроек);
	
	МассивСчетовСПроблемамиЗаполненияСубконто =  
		НастройкиЗаполненияСубконто.НайтиСтроки(Новый Структура("ЕстьПроблемыЗаполненияСубконто", Истина));
	
	НавигационнаяСсылка = "ОткрытьНастройкуЗаполненияСубконто";
	Если МассивСчетовСПроблемамиЗаполненияСубконто.Количество() > 0 Тогда
		ТекстСсылка = СтрШаблон(
			НСтр("ru = 'Настройка заполнения субконто (%1)';
				|en = 'Set up extra dimensions (%1)'"),
			МассивСчетовСПроблемамиЗаполненияСубконто.Количество());
		НастройкаЗаполненияСубконто = 
			Новый ФорматированнаяСтрока(ТекстСсылка, , ЦветаСтиля.ПросроченныеДанныеЦвет, , НавигационнаяСсылка);
	Иначе
		ТекстСсылка = НСтр("ru = 'Настройка заполнения субконто';
							|en = 'Set up extra dimensions'");
		НастройкаЗаполненияСубконто = 
			Новый ФорматированнаяСтрока(ТекстСсылка, , ЦветаСтиля.ГиперссылкаЦвет, , НавигационнаяСсылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОкончанияНастройкиЗаполненияСубконто(Результат, ДополнительныеПарметры) Экспорт
	СформироватьСсылкуНастройкаЗаполненияСубконто();
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикДеревоОбъектовБухгалтерскогоУчетаПриАктивизацииСтроки()

	ТекущиеДанные = Элементы.ДеревоОбъектовБухгалтерскогоУчета.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ОбъектУчета = ТекущийОбъектУчета Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийОбъектУчета = ТекущиеДанные.ОбъектУчета;
	УстановитьТекущуюСтраницуНастроекПоОбъектамУчета();
	ПриАктивацииСтрокиДереваОбъектовУчетаСервер();
	
КонецПроцедуры

&НаСервере
Процедура ПриАктивацииСтрокиДереваОбъектовУчетаСервер()
	
	ЗаполнитьТаблицыНастройки();
	НастроитьЭлементыФормыПриИзмененииОбъектаУчета();
	
	ОтборСписка = ОбщегоНазначенияУТКлиентСервер.ПолучитьОтборДинамическогоСписка(СписокИсключения);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ОтборСписка, "ОбъектУчета", ТекущийОбъектУчета);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиСервер(ПараметрыСохраненияНастройки)
	
	МенеджерЗаписиПорядокОтражения = РегистрыСведений.НастройкиСчетовМеждународногоУчетаПоОбъектам.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписиПорядокОтражения, ПараметрыСохраненияНастройки);
	Если МенеджерЗаписиПорядокОтражения.ТипНастройки = Перечисления.ТипыНастроекОтраженияВМеждународномУчете.ОбщаяПоОбъектуУчета Тогда
		МенеджерЗаписиПорядокОтражения.Организация = Неопределено;
		МенеджерЗаписиПорядокОтражения.МестоУчета = Неопределено;
	ИначеЕсли НЕ ЗначениеЗаполнено(МенеджерЗаписиПорядокОтражения.МестоУчета) Тогда
		МенеджерЗаписиПорядокОтражения.МестоУчета = Неопределено;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(МенеджерЗаписиПорядокОтражения.ОбъектНастройки) Тогда
		МенеджерЗаписиПорядокОтражения.ОбъектНастройки = Неопределено;
	КонецЕсли;
	Если ЗначениеЗаполнено(ПараметрыСохраненияНастройки.СчетУчета) Тогда
		МенеджерЗаписиПорядокОтражения.Записать();
	Иначе
		МенеджерЗаписиПорядокОтражения.Удалить();
	КонецЕсли;
	
	ЗаполнитьИнформациюПоНастройкам(Истина);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОсновнойОтборСпискаИсключений(СписокИсключения, Организация, ПланСчетов, НастройкаФормированияПроводок)
	
	ОтборСписка = ОбщегоНазначенияУТКлиентСервер.ПолучитьОтборДинамическогоСписка(СписокИсключения);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ОтборСписка, "ПланСчетов", ПланСчетов);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ОтборСписка, "НастройкаФормированияПроводок", НастройкаФормированияПроводок);
	
	СписокОрганизаций = Новый СписокЗначений();
	СписокОрганизаций.Добавить(ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
	Если ЗначениеЗаполнено(Организация) Тогда
		СписокОрганизаций.Добавить(Организация);
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ОтборСписка, "Организация", СписокОрганизаций, , , ЗначениеЗаполнено(Организация));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОсновнойОтборНастроекОтраженияКорреспонденций(СписокНастроекОтраженияКорреспонденций, ПланСчетов, НастройкаФормированияПроводок)
	
	ОтборСписка = ОбщегоНазначенияУТКлиентСервер.ПолучитьОтборДинамическогоСписка(СписокНастроекОтраженияКорреспонденций);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ОтборСписка, "ПланСчетов", ПланСчетов);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(ОтборСписка, "НастройкаФормированияПроводок", НастройкаФормированияПроводок);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьФормуПриИзмененииРежимаНастройки()
	Если РежимНастройки = 1 Тогда
		ИмяСтраницыСНастройками = Элементы.СтраницаИсключения.Имя;
		ИмяСтраницыКоманднойПанели = Элементы.СтраницаКоманднаяПанельИсключения.Имя;
	Иначе
		ИмяСтраницыСНастройками = Элементы.СтраницаОбщиеНастройки.Имя;
		ИмяСтраницыКоманднойПанели = Элементы.СтраницаКоманднаяПанельОбщиеНастройки.Имя;
	КонецЕсли;
	УстановитьТекущуюСтраницуНастроекПоОбъектамУчета();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборИсключенийПоОбъектуНастройки(ОбъектНастройки)
	
	ОтборОбъектНастройкиЗначение = ОбъектНастройки;
	
	ОтборСписка = ОбщегоНазначенияУТКлиентСервер.ПолучитьОтборДинамическогоСписка(СписокИсключения);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		ОтборСписка,
		"ОбъектНастройки", 
		ОтборОбъектНастройкиЗначение);
	
	Если ЗначениеЗаполнено(ОбъектНастройки) Тогда
		СтрокаОтбор = СтрШаблон("%1: %2", Элементы.СписокИсключенияОбъектНастройки.Заголовок, ОтборОбъектНастройкиЗначение);
	Иначе
		СтрокаОтбор = СтрШаблон(Нстр("ru = '%1: Не указана';
									|en = '%1: Not specified'"), Элементы.СписокИсключенияОбъектНастройки.Заголовок);
	КонецЕсли;
	
	ОтборОбъектНастройкиПредставление = СтроковыеФункцииКлиент.ФорматированнаяСтрока(
		"%1 <a href=""ОчиститьОтборПоОбъектуНастройки""><img src='ОчиститьОтбор9на9'></a>", СтрокаОтбор);
	Элементы.ОтборОбъектНастройкиПредставление.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтборСпискаИсключенийПоОбъектуНастройки()
	ОтборОбъектНастройкиЗначение = Неопределено;
	ОтборСписка = ОбщегоНазначенияУТКлиентСервер.ПолучитьОтборДинамическогоСписка(СписокИсключения);
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(ОтборСписка, "ОбъектНастройки");
	ОтборОбъектНастройкиПредставление = "";
	Элементы.ОтборОбъектНастройкиПредставление.Видимость = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуЗаписиИсключения(Отбор, НоваяЗапись = Ложь)
	
	ПараметрыФормы = Новый Структура;
	Если НоваяЗапись Тогда
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", Отбор);
	Иначе
		МассивОтбор = Новый Массив;
		МассивОтбор.Добавить(Отбор);
		КлючЗаписи = Новый("РегистрСведенийКлючЗаписи.НастройкиСчетовМеждународногоУчетаПоОбъектам", МассивОтбор);
		ПараметрыФормы.Вставить("Ключ", КлючЗаписи);
	КонецЕсли;
	
	ПараметрыФормы.Вставить("НеИзменятьТипНастройки", Истина);
	ПараметрыФормы.Вставить("НеИзменятьОбъектУчета", Истина);
	ОткрытьФорму("РегистрСведений.НастройкиСчетовМеждународногоУчетаПоОбъектам.Форма.ФормаЗаписи", 
			ПараметрыФормы, ЭтотОбъект,, , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Функция ОтборНастройкиСчетовПоОбъектуУчета(ОбъектУчета, ОбъектНастройки, ТипНастройки)

	Отбор = Новый Структура;
	Отбор.Вставить("ПланСчетов", ПланСчетов);
	Отбор.Вставить("НастройкаФормированияПроводок", НастройкаФормированияПроводок);
	Отбор.Вставить("ТипНастройки", ТипНастройки);
	Отбор.Вставить("ОбъектУчета", ОбъектУчета);
	Отбор.Вставить("ОбъектНастройки", ОбъектНастройки);
	Отбор.Вставить("Организация");
	Отбор.Вставить("МестоУчета");
	Возврат Отбор;
	
КонецФункции

&НаКлиенте
Функция ОтборНастройкиПоКорреспонденции()

	Отбор = Новый Структура;
	Отбор.Вставить("ПланСчетов", ПланСчетов);
	Отбор.Вставить("НастройкаФормированияПроводок", НастройкаФормированияПроводок);
	Отбор.Вставить("ОбъектУчетаДт");
	Отбор.Вставить("ОбъектУчетаКт");
	Отбор.Вставить("ПорядокПрименения");
	Возврат Отбор;
	
КонецФункции

&НаКлиенте
Процедура ПриОкончанииВыбораСчетаУчетаДляЗаполнения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьСчетУчетаОбщихНастроекНаСервере(Результат, ДополнительныеПараметры.ВыделенныеСтроки);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСчетУчетаОбщихНастроекНаСервере(СчетУчета, ВыделенныеСтроки)
	
	Для каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
		
		ДанныеСтроки = ТаблицаОбщиеНастройки.НайтиПоИдентификатору(ИдентификаторСтроки);
		ДанныеСтроки.СчетУчета = СчетУчета;
	
		ПараметрыСохраненияНастройки = Новый Структура;
		ПараметрыСохраненияНастройки.Вставить("ПланСчетов", ПланСчетов);
		ПараметрыСохраненияНастройки.Вставить("НастройкаФормированияПроводок", НастройкаФормированияПроводок);
		ПараметрыСохраненияНастройки.Вставить("ТипНастройки", Перечисления.ТипыНастроекОтраженияВМеждународномУчете.ОбщаяПоОбъектуУчета);
		ПараметрыСохраненияНастройки.Вставить("ОбъектУчета", ТекущийОбъектУчета);
		ПараметрыСохраненияНастройки.Вставить("ОбъектНастройки", ДанныеСтроки.ОбъектНастройки);
		ПараметрыСохраненияНастройки.Вставить("Организация", Неопределено);
		ПараметрыСохраненияНастройки.Вставить("МестоУчета", Неопределено);
		ПараметрыСохраненияНастройки.Вставить("СчетУчета", ДанныеСтроки.СчетУчета);
		ПараметрыСохраненияНастройки.Вставить("ТребуетсяНастройка", ДанныеСтроки.ТребуетсяНастройка);
		СохранитьНастройкиСервер(ПараметрыСохраненияНастройки);
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТекущуюСтраницуНастроекПоОбъектамУчета()
	
	Если Не ЗначениеЗаполнено(ПланСчетов) Или Не ЗначениеЗаполнено(НастройкаФормированияПроводок) Тогда
		ТекущаяСтраницаНастройки = Элементы.СтраницыНастройкиПоОбъектамУчетаНеВыбранаНастройка;
		ТекущаяСтраницаКоманднойПанелиНастройки = Элементы.СтраницаКоманднаяПанельПустая;
	ИначеЕсли Элементы.ДеревоОбъектовБухгалтерскогоУчета.ТекущиеДанные <> Неопределено
		И Элементы.ДеревоОбъектовБухгалтерскогоУчета.ТекущиеДанные.ЭтоГруппа Тогда
		ТекущаяСтраницаНастройки = Элементы.СтраницыНастройкиПоОбъектамУчетаВыбранаГруппа;
		ТекущаяСтраницаКоманднойПанелиНастройки = Элементы.СтраницаКоманднаяПанельПустая;
	ИначеЕсли ИмяСтраницыСНастройками <> "" Тогда
			ТекущаяСтраницаНастройки = Элементы[ИмяСтраницыСНастройками];
			ТекущаяСтраницаКоманднойПанелиНастройки = Элементы[ИмяСтраницыКоманднойПанели];
		Иначе
			ТекущаяСтраницаНастройки = Элементы.СтраницаОбщиеНастройки;
			ТекущаяСтраницаКоманднойПанелиНастройки = Элементы.СтраницаКоманднаяПанельОбщиеНастройки;
		КонецЕсли;
		
		Если Элементы.СтраницыНастройкиПоОбъектамУчета.ТекущаяСтраница <> ТекущаяСтраницаНастройки Тогда
			Элементы.СтраницыНастройкиПоОбъектамУчета.ТекущаяСтраница = ТекущаяСтраницаНастройки;
			Элементы.СтраницыКомандныеПанелиНастройки.ТекущаяСтраница = ТекущаяСтраницаКоманднойПанелиНастройки;
		КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область НастройкиПоКорреспонденциям

&НаКлиенте
Процедура УстановитьТекущуюСтраницуНастроекПоКорреспонденциям()
		
		Если Не ЗначениеЗаполнено(ПланСчетов) Или Не ЗначениеЗаполнено(НастройкаФормированияПроводок) Тогда
			ТекущаяСтраницаНастройки = Элементы.СтраницаНастройкиОтраженияКорреспонденцийНеВыбранаНастройка;
		ИначеЕсли (Элементы.ДеревоОбъектовБухгалтерскогоУчетаПоКорреспонденциям.ТекущиеДанные <> Неопределено
				И Элементы.ДеревоОбъектовБухгалтерскогоУчетаПоКорреспонденциям.ТекущиеДанные.ЭтоГруппа)
				И РежимПросмотраНастроекОтраженияКорреспонденций = "СОтборомПоОбъектамУчета" Тогда
		ТекущаяСтраницаНастройки = Элементы.СтраницаНастройкиОтраженияКорреспонденцийВыбранаГруппа;
	Иначе
		ТекущаяСтраницаНастройки = Элементы.СтраницаНастройкиОтраженияКорреспонденций;
	КонецЕсли;
	
	Если Элементы.СтраницыНастройкиОтраженияКорреспонденций.ТекущаяСтраница <> ТекущаяСтраницаНастройки Тогда
		Элементы.СтраницыНастройкиОтраженияКорреспонденций.ТекущаяСтраница = ТекущаяСтраницаНастройки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикДеревоОбъектовБухгалтерскогоУчетаПоКорреспонденциямПриАктивизацииСтроки()

	ТекущиеДанные = Элементы.ДеревоОбъектовБухгалтерскогоУчетаПоКорреспонденциям.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ОбъектУчета = ТекущийОбъектУчета Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийОбъектУчета = ТекущиеДанные.ОбъектУчета;
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(НастройкиОтраженияКорреспонденций, "ТекущийОбъектУчета", ТекущийОбъектУчета);
	УстановитьТекущуюСтраницуНастроекПоКорреспонденциям();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастройки(СтрокаНастроекПоСчету)

	ДанныеСтроки = НастройкиПоСчету.НайтиПоИдентификатору(СтрокаНастроекПоСчету);
	
	Если ДанныеСтроки.ТипНастройки = ПредопределенноеЗначение("Перечисление.ТипыНастроекОтраженияВМеждународномУчете.ПоКорреспонденции") Тогда
		
		Отбор = ОтборНастройкиПоКорреспонденции();
		ЗаполнитьЗначенияСвойств(Отбор, ДанныеСтроки);
		
		ПараметрыФормы = Новый Структура;
		МассивОтбор = Новый Массив;
		МассивОтбор.Добавить(Отбор);
		КлючЗаписи = Новый("РегистрСведенийКлючЗаписи.НастройкиОтраженияКорреспонденцийВМеждународномУчете", МассивОтбор);
		ПараметрыФормы.Вставить("Ключ", КлючЗаписи);
		ОткрытьФорму("РегистрСведений.НастройкиОтраженияКорреспонденцийВМеждународномУчете.Форма.ФормаЗаписи", 
				ПараметрыФормы, ЭтотОбъект, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	Иначе
		Отбор = ОтборНастройкиСчетовПоОбъектуУчета(ДанныеСтроки.ОбъектУчета, ДанныеСтроки.ОбъектНастройки, ДанныеСтроки.ТипНастройки);
		ЗаполнитьЗначенияСвойств(Отбор, ДанныеСтроки);
		
		ПараметрыФормы = Новый Структура;
		МассивОтбор = Новый Массив;
		МассивОтбор.Добавить(Отбор);
		КлючЗаписи = Новый("РегистрСведенийКлючЗаписи.НастройкиСчетовМеждународногоУчетаПоОбъектам", МассивОтбор);
		ПараметрыФормы.Вставить("Ключ", КлючЗаписи);
	
		ОткрытьФорму("РегистрСведений.НастройкиСчетовМеждународногоУчетаПоОбъектам.ФормаЗаписи", 
				ПараметрыФормы, ЭтотОбъект, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ПросмотрНастроекПоПлануСчетов

&НаКлиенте
Процедура ОбработчикПланСчетовМеждународныПриАктивизацииСтроки()
	
	ТекущиеДанные = Элементы.ПланСчетовМеждународный.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		НастройкиПоСчету.Очистить();
	Иначе
		ПолучитьНастройкиПоСчету(Элементы.ПланСчетовМеждународный.ТекущаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьНастройкиПоСчету(СчетУчета)
	
	НастройкиПоСчету.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Настройки.ТипНастройки КАК ТипНастройки,
	|	Настройки.ОбъектУчета КАК ОбъектУчета,
	|	&ПоложениеДтКт КАК Положение,
	|	Настройки.ОбъектНастройки  КАК ОбъектНастройки,
	|	Настройки.Организация  КАК Организация,
	|	Настройки.МестоУчета  КАК МестоУчета
	|ИЗ
	|	РегистрСведений.НастройкиСчетовМеждународногоУчетаПоОбъектам КАК Настройки
	|ГДЕ
	|	Настройки.ПланСчетов = &ПланСчетов
	|	И Настройки.НастройкаФормированияПроводок = &НастройкаФормированияПроводок
	|	И Настройки.СчетУчета = &СчетУчета
	|;
	|
	|////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНастроекОтраженияВМеждународномУчете.ПоКорреспонденции) КАК ТипНастройки,
	|	Настройки.ОбъектУчетаДт КАК ОбъектУчетаДт,
	|	Настройки.ОбъектУчетаКт КАК ОбъектУчетаКт,
	|	ВЫБОР 
	|		КОГДА Настройки.СчетУчетаДт = &СчетУчета И Настройки.СчетУчетаКт = &СчетУчета
	|			ТОГДА &ПоложениеДтКт
	|		КОГДА Настройки.СчетУчетаДт = &СчетУчета
	|			ТОГДА &ПоложениеДт
	|		КОГДА Настройки.СчетУчетаКт = &СчетУчета
	|			ТОГДА &ПоложениеКт
	|	КОНЕЦ КАК Положение,
	|	Настройки.ПорядокПрименения КАК ПорядокПрименения,
	|	Настройки.ЕстьДополнительныйОтборДт КАК ЕстьДополнительныйОтборДт,
	|	Настройки.ЕстьДополнительныйОтборКт КАК ЕстьДополнительныйОтборКт,
	|	Настройки.Описание КАК Описание
	|ИЗ
	|	РегистрСведений.НастройкиОтраженияКорреспонденцийВМеждународномУчете КАК Настройки
	|ГДЕ
	|	Настройки.ПланСчетов = &ПланСчетов
	|	И Настройки.НастройкаФормированияПроводок = &НастройкаФормированияПроводок
	|	И (Настройки.СчетУчетаДт = &СчетУчета ИЛИ Настройки.СчетУчетаКт = &СчетУчета)
	|";
	Запрос.УстановитьПараметр("СчетУчета", СчетУчета);
	Запрос.УстановитьПараметр("ПоложениеДтКт", НСтр("ru = 'Дт/Кт';
													|en = 'Dr/Cr'"));
	Запрос.УстановитьПараметр("ПоложениеДт", НСтр("ru = 'Дт';
													|en = 'Dr'"));
	Запрос.УстановитьПараметр("ПоложениеКт", НСтр("ru = 'Кт';
													|en = 'Cr'"));
	Запрос.УстановитьПараметр("ПланСчетов", ПланСчетов);
	Запрос.УстановитьПараметр("НастройкаФормированияПроводок", НастройкаФормированияПроводок);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	ВыборкаНастроекПоОбъектамУчета = РезультатыЗапроса[0].Выбрать();
	ВыборкаНастроекПоКорреспонденциям = РезультатыЗапроса[1].Выбрать();
	
	Пока ВыборкаНастроекПоКорреспонденциям.Следующий() Цикл
		НоваяСтрока = НастройкиПоСчету.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаНастроекПоКорреспонденциям);
	КонецЦикла;
	
	Пока ВыборкаНастроекПоОбъектамУчета.Следующий() Цикл
		НоваяСтрока = НастройкиПоСчету.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаНастроекПоОбъектамУчета);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
