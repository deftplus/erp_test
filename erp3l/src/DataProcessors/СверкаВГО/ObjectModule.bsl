Перем ПостроительЗапросаОрганизация Экспорт;
Перем ПостроительЗапросаПоказатели Экспорт;
Перем ТаблицаСоответствийПоказателей Экспорт;

Процедура Инициализация(ТабличноеПолеПериметров, СписокОрганизаций, АдресТаблицыСоответствияПоказаталей = Неопределено, УникальныйИдентификатор = Неопределено) Экспорт
	
	ТаблицаСверки.Очистить();
	ТаблицаСверкиОрганизаций.Очистить();
	
	///////////////////////////////////////////////////////////////////////////////////////////////////////////
	
	Запрос=Новый Запрос;
	
	Запрос.Текст="ВЫБРАТЬ
	|	СоответствиеВнутригрупповыхПоказателей.ПоказательБазис,
	|	СоответствиеВнутригрупповыхПоказателей.ПоказательСравнение,
	|	СоответствиеВнутригрупповыхПоказателей.ПоказательБазис.ГруппаРаскрытия КАК ГруппаРаскрытия,
	|	СоответствиеВнутригрупповыхПоказателей.ПоказательСравнение.ГруппаРаскрытия КАК ГруппаРаскрытияСоответствие,
	|	СоответствиеВнутригрупповыхПоказателей.ВидОтчетаБазис,
	|	СоответствиеВнутригрупповыхПоказателей.ВидОтчетаСравнение,
	|	СоответствиеВнутригрупповыхПоказателей.СоответствиеАналитик
	|ИЗ
	|	РегистрСведений.СоответствиеВнутригрупповыхПоказателей КАК СоответствиеВнутригрупповыхПоказателей";
		
	ТабРезультат=Запрос.Выполнить().Выгрузить();
	ТабРезультат.Колонки.Добавить("АналитикаВГО_Базис",ОбщегоНазначенияУХ.ПолучитьОписаниеТиповЧисла(1,0));
	ТабРезультат.Колонки.Добавить("АналитикаВГО_Сравнение",ОбщегоНазначенияУХ.ПолучитьОписаниеТиповЧисла(1,0));
	
	Для Каждого СтрСоотвествие ИЗ ТабРезультат Цикл
		
		// Определяем номер аналитики с типом "Организации" для первого и второго показателей
		
		НомерИсходной=0;
		НомерСоответствие=0;
		
		АналитикОтчетаБазис=0;
		АналитикОтчетаСравнение=0;
		
		Для Инд=1 По ПараметрыСеанса.ЧислоДопАналитик Цикл
			
			Если ЗначениеЗаполнено(СтрСоотвествие.ВидОтчетаБазис["ВидАналитики"+Инд]) Тогда
				
				АналитикОтчетаБазис=АналитикОтчетаБазис+1;
				Продолжить;
				
			ИначеЕсли ЗначениеЗаполнено(СтрСоотвествие.ГруппаРаскрытия["ВидАналитики"+Инд]) И СтрСоотвествие.ГруппаРаскрытия["ВидАналитики"+Инд].ТипЗначения.Типы()[0]=Тип("СправочникСсылка.Организации") Тогда
				
				НомерИсходной=Инд;
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Для Инд=1 По ПараметрыСеанса.ЧислоДопАналитик Цикл
			
			Если ЗначениеЗаполнено(СтрСоотвествие.ВидОтчетаСравнение["ВидАналитики"+Инд]) Тогда
				
				АналитикОтчетаСравнение=АналитикОтчетаСравнение+1;
				Продолжить;
				
			ИначеЕсли ЗначениеЗаполнено(СтрСоотвествие.ГруппаРаскрытияСоответствие["ВидАналитики"+Инд]) И СтрСоотвествие.ГруппаРаскрытияСоответствие["ВидАналитики"+Инд].ТипЗначения.Типы()[0]=Тип("СправочникСсылка.Организации") Тогда
				
				НомерСоответствие=Инд;
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
		СтрСоотвествие.АналитикаВГО_Базис=НомерИсходной;
		СтрСоотвествие.АналитикаВГО_Сравнение=НомерСоответствие;
		
		МаксНомерАналитик=Макс(НомерИсходной,СтрСоотвествие.ГруппаРаскрытия.ЧислоАналитик);
		
		Запрос.Текст="ВЫБРАТЬ
		|	СравнениеГруппировка.ОрганизацияБазис,
		|	ЭкземплярыОтчетов.ЭкземплярОтчета КАК ЭкземплярОтчетаБазис,
		|	СравнениеГруппировка.ОрганизацияСравнение,
		|	ЭкземплярыСравнение.ЭкземплярОтчета КАК ЭкземплярОтчетаСравнение,
		|	СравнениеГруппировка.ЗначениеБазис,
		|	СравнениеГруппировка.ЗначениеСравнение
		|ИЗ
		|	(ВЫБРАТЬ
		|		СравнениеПоказателей.ОрганизацияБазис КАК ОрганизацияБазис,
		|		СравнениеПоказателей.ОрганизацияСравнение КАК ОрганизацияСравнение,
		|		СУММА(СравнениеПоказателей.ЗначениеБазис) КАК ЗначениеБазис,
		|		СУММА(СравнениеПоказателей.ЗначениеСравнение) КАК ЗначениеСравнение
		|	ИЗ
		|		(ВЫБРАТЬ
		|			ВерсииЗначенийПоказателей.Организация КАК ОрганизацияБазис,
		|			ЗначенияПоказателейОтчетов.Аналитика"+НомерИсходной+" КАК ОрганизацияСравнение,
		|			ЗначенияПоказателейОтчетов.Значение КАК ЗначениеБазис,
		|			0 КАК ЗначениеСравнение
		|		ИЗ
		|			РегистрСведений.ЗначенияПоказателейОтчетов"+МаксНомерАналитик+" КАК ЗначенияПоказателейОтчетов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВерсииЗначенийПоказателей КАК ВерсииЗначенийПоказателей 
		|			ПО ЗначенияПоказателейОтчетов.Версия=ВерсииЗначенийПоказателей.Ссылка 
		|		ГДЕ
		|			ЗначенияПоказателейОтчетов.Показатель = &ПоказательБазис";
		
		Если ЗначениеЗаполнено(ОрганизацияБазис) Тогда
			
			Запрос.Текст=Запрос.Текст+"
			|И ВерсииЗначенийПоказателей.Организация=&ОрганизацияБазис";
			
			Запрос.УстановитьПараметр("ОрганизацияБазис",ОрганизацияБазис);
			
		Иначе
			
			Запрос.Текст=Запрос.Текст+"
			|И ВерсииЗначенийПоказателей.Организация.ТипОрганизации=&ТипОрганизацийДляСверки
			|И ВерсииЗначенийПоказателей.Организация В(&СписокОрганизаций)";
			
			Запрос.УстановитьПараметр("ТипОрганизацийДляСверки",Перечисления.ТипыОрганизационныхЕдиниц.Обычная);
			
		КонецЕсли;
		
		Запрос.Текст=Запрос.Текст+"
		|И ВерсииЗначенийПоказателей.Валюта=&Валюта";			
		
		Если ЗначениеЗаполнено(ОрганизацияСравнение) Тогда
			
			Запрос.Текст=Запрос.Текст+"
			|И ЗначенияПоказателейОтчетов.Аналитика"+НомерИсходной+"=&ОрганизацияСравнение";
			
			Запрос.УстановитьПараметр("ОрганизацияСравнение",ОрганизацияСравнение);
			
		Иначе
			
			Запрос.Текст=Запрос.Текст+"
			|И ((НЕ ЗначенияПоказателейОтчетов.Аналитика"+НомерИсходной+"=Неопределено) И ЗначенияПоказателейОтчетов.Аналитика"+НомерИсходной+".ТипОрганизации=&ТипОрганизацийДляСверки)
			|И ЗначенияПоказателейОтчетов.Аналитика"+НомерИсходной+" В(&СписокОрганизаций)";
			
			Запрос.УстановитьПараметр("ТипОрганизацийДляСверки",Перечисления.ТипыОрганизационныхЕдиниц.Обычная);
			
		КонецЕсли;
		
		МаксНомерАналитик=Макс(НомерСоответствие,СтрСоотвествие.ГруппаРаскрытияСоответствие.ЧислоАналитик);
		
		Запрос.Текст=Запрос.Текст+"
		|			И ВерсииЗначенийПоказателей.Сценарий = &Сценарий
		|			И ВерсииЗначенийПоказателей.ПериодОтчета = &ПериодОтчета
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ЗначенияПоказателейОтчетов.Аналитика"+НомерСоответствие+",
		|			ВерсииЗначенийПоказателей.Организация,
		|			0,
		|			ЗначенияПоказателейОтчетов.Значение
		|		ИЗ
		|			РегистрСведений.ЗначенияПоказателейОтчетов"+МаксНомерАналитик+" КАК ЗначенияПоказателейОтчетов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВерсииЗначенийПоказателей КАК ВерсииЗначенийПоказателей 
		|			ПО ЗначенияПоказателейОтчетов.Версия=ВерсииЗначенийПоказателей.Ссылка
		|		ГДЕ
		|			ЗначенияПоказателейОтчетов.Показатель = &ПоказательСравнение";
		
		Если ЗначениеЗаполнено(ОрганизацияСравнение) Тогда
			
			Запрос.Текст=Запрос.Текст+"
			|И ВерсииЗначенийПоказателей.Организация=&ОрганизацияСравнение";
			
			Запрос.УстановитьПараметр("ОрганизацияСравнение",ОрганизацияСравнение);
			
		Иначе
			
			Запрос.Текст=Запрос.Текст+"
			|И ВерсииЗначенийПоказателей.Организация.ТипОрганизации=&ТипОрганизацийДляСверки
			|И ВерсииЗначенийПоказателей.Организация В(&СписокОрганизаций)";
			
			Запрос.УстановитьПараметр("ТипОрганизацийДляСверки",Перечисления.ТипыОрганизационныхЕдиниц.Обычная);
			
		КонецЕсли;
		
		Запрос.Текст=Запрос.Текст+"
		|И ВерсииЗначенийПоказателей.Валюта=&Валюта";
		
		Если ЗначениеЗаполнено(ОрганизацияБазис) Тогда
			
			Запрос.Текст=Запрос.Текст+"
			|И ЗначенияПоказателейОтчетов.Аналитика"+НомерСоответствие+"=&ОрганизацияБазис";
			
			Запрос.УстановитьПараметр("ОрганизацияБазис",ОрганизацияБазис);
			
		Иначе
			
			Запрос.Текст=Запрос.Текст+"
			|И ((НЕ ЗначенияПоказателейОтчетов.Аналитика"+НомерСоответствие+"=Неопределено) И ЗначенияПоказателейОтчетов.Аналитика"+НомерСоответствие+".ТипОрганизации=&ТипОрганизацийДляСверки)
			|И ЗначенияПоказателейОтчетов.Аналитика"+НомерСоответствие+" В(&СписокОрганизаций)";
			
			Запрос.УстановитьПараметр("ТипОрганизацийДляСверки",Перечисления.ТипыОрганизационныхЕдиниц.Обычная);
			
		КонецЕсли;
		
		Запрос.Текст=Запрос.Текст+"
		|			И ВерсииЗначенийПоказателей.Сценарий = &Сценарий
		|			И ВерсииЗначенийПоказателей.ПериодОтчета = &ПериодОтчета) КАК СравнениеПоказателей
		|	
		|	СГРУППИРОВАТЬ ПО
		|		СравнениеПоказателей.ОрганизацияБазис,
		|		СравнениеПоказателей.ОрганизацияСравнение) КАК СравнениеГруппировка
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			Ссылка КАК ЭкземплярОтчета,
		|			Организация КАК Организация
		|		ИЗ  Документ.НастраиваемыйОтчет КАК НастраиваемыйОтчет
		|		ГДЕ
		|			НастраиваемыйОтчет.ВидОтчета = &ВидОтчетаБазис
		|			И НастраиваемыйОтчет.Сценарий = &Сценарий
		|			И НастраиваемыйОтчет.ПериодОтчета = &ПериодОтчета) КАК ЭкземплярыОтчетов
		|		ПО СравнениеГруппировка.ОрганизацияБазис = ЭкземплярыОтчетов.Организация
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			Ссылка КАК ЭкземплярОтчета,
		|			Организация КАК Организация
		|		ИЗ  Документ.НастраиваемыйОтчет КАК НастраиваемыйОтчет
		|		ГДЕ
		|			НастраиваемыйОтчет.ВидОтчета = &ВидОтчетаСравнение
		|			И НастраиваемыйОтчет.Сценарий = &Сценарий
		|			И НастраиваемыйОтчет.ПериодОтчета = &ПериодОтчета) КАК ЭкземплярыСравнение
		|		ПО СравнениеГруппировка.ОрганизацияСравнение = ЭкземплярыСравнение.Организация";
		
		Запрос.УстановитьПараметр("ПоказательБазис",СтрСоотвествие.ПоказательБазис);
		Запрос.УстановитьПараметр("ПоказательСравнение",СтрСоотвествие.ПоказательСравнение);
		
		Запрос.УстановитьПараметр("ВидОтчетаБазис",СтрСоотвествие.ВидОтчетаБазис);
		Запрос.УстановитьПараметр("ВидОтчетаСравнение",СтрСоотвествие.ВидОтчетаСравнение);
		
		Запрос.УстановитьПараметр("Сценарий",Сценарий);
		Запрос.УстановитьПараметр("ПериодОтчета",Период);
		Запрос.УстановитьПараметр("Валюта",Валюта);
		
		Запрос.УстановитьПараметр("СписокОрганизаций", СписокОрганизаций);
		
		Результат=Запрос.Выполнить().Выбрать();
		
		Пока Результат.Следующий() Цикл
			
			Поворот = ТаблицаСверки.НайтиСтроки(Новый Структура("ОрганизацияБазис, ОрганизацияСравнение", Результат.ОрганизацияСравнение, Результат.ОрганизацияБазис)).Количество() > 0;
			
			НоваяСтрока = ТаблицаСверки.Добавить();
			
			НоваяСтрока.ВидОтчетаБазис           = ?(Поворот, СтрСоотвествие.ВидОтчетаСравнение, СтрСоотвествие.ВидОтчетаБазис);
			НоваяСтрока.ВидОтчетаСверка          = ?(Поворот, СтрСоотвествие.ВидОтчетаБазис, СтрСоотвествие.ВИдОтчетаСравнение);
			НоваяСтрока.ПоказательБазис          = ?(Поворот, СтрСоотвествие.ПоказательСравнение, СтрСоотвествие.ПоказательБазис);
			НоваяСтрока.ПоказательСравнение      = ?(Поворот, СтрСоотвествие.ПоказательБазис, СтрСоотвествие.ПоказательСравнение);
			НоваяСтрока.ОрганизацияБазис         = ?(Поворот, Результат.ОрганизацияСравнение, Результат.ОрганизацияБазис);
			НоваяСтрока.ОрганизацияСравнение     = ?(Поворот, Результат.ОрганизацияБазис, Результат.ОрганизацияСравнение);
			НоваяСтрока.ЗначениеБазис            = ?(Поворот, Результат.ЗначениеСравнение, Результат.ЗначениеБазис);
			Новаястрока.ЗначениеСравнение        = ?(Поворот, Результат.ЗначениеБазис, Результат.ЗначениеСравнение);
			НоваяСтрока.РасхождениеАбсолютное    = НоваяСтрока.ЗначениеБазис - НоваяСтрока.ЗначениеСравнение;
			НоваяСтрока.ЭкземплярОтчетаСравнение = ?(Поворот, Результат.ЭкземплярОтчетаБазис, Результат.ЭкземплярОтчетаСравнение);
			НоваяСтрока.ЭкземплярОтчетаБазис     = ?(Поворот, Результат.ЭкземплярОтчетаСравнение, Результат.ЭкземплярОтчетаБазис);
			НоваяСтрока.РасхождениеАбсолютноеМодуль    = ОбщегоНазначенияУХ.ЗначениеПоМодулю(НоваяСтрока.РасхождениеАбсолютное);
					
			СтрокиОрганизаций = ТаблицаСверкиОрганизаций.НайтиСтроки(Новый Структура("ОрганизацияБазис, ОрганизацияСравнение", НоваяСтрока.ОрганизацияБазис, НоваяСтрока.ОрганизацияСравнение));
			
			Если СтрокиОрганизаций.Количество() > 0 Тогда
				СтрокаОрганизации = СтрокиОрганизаций[0];
			Иначе
				СтрокаОрганизации = ТаблицаСверкиОрганизаций.Добавить();
				СтрокаОрганизации.ОрганизацияБазис = НоваяСтрока.ОрганизацияБазис;
				СтрокаОрганизации.ОрганизацияСравнение = НоваяСтрока.ОрганизацияСравнение;
				СтрокаОрганизации.КорректныеДанные = Ложь;
				СтрокаОрганизации.Превышение       = Ложь;
				СтрокаОрганизации.НеполныеДанные   = Ложь;
			КонецЕсли;
			
			СтрокаБазис = ТабличноеПолеПериметров.Найти(НоваяСтрока.ОрганизацияБазис, "Организация");
			СтрокаСравнение = ТабличноеПолеПериметров.Найти(НоваяСтрока.ОрганизацияСравнение, "Организация");
			
			Если НоваяСтрока.ЗначениеБазис <> 0 Тогда
				НоваяСтрока.РасхождениеОтносительное = НоваяСтрока.РасхождениеАбсолютное/НоваяСтрока.ЗначениеБазис * 100;
				НоваяСтрока.РасхождениеОтносительноеМодуль = ОбщегоНазначенияУХ.ЗначениеПоМодулю(НоваяСтрока.РасхождениеОтносительное);
			КонецЕсли;
			
			Если НЕ (ЗначениеЗаполнено(НоваяСтрока.ЗначениеСравнение) И ЗначениеЗаполнено(НоваяСтрока.ЗначениеБазис)) Тогда
				НоваяСтрока.ПризнакРасхождения = 3;
				СтрокаОрганизации.НеполныеДанные = Истина;
			ИначеЕсли ЗначениеЗаполнено(ПорогСущественности) И ПорогСущественности < ОбщегоНазначенияУХ.ЗначениеПоМодулю(?(АбсолютноеПредставление, НоваяСтрока.РасхождениеАбсолютное, НоваяСтрока.РасхождениеОтносительное)) Тогда
				НоваяСтрока.ПризнакРасхождения = 2;
				СтрокаОрганизации.Превышение = Истина;
			Иначе
				НоваяСтрока.ПризнакРасхождения = 0;
				СтрокаОрганизации.КорректныеДанные = Истина;
			КонецЕсли;
			
			Если СтрокаБазис.ПризнакРасхождения < НоваяСтрока.ПризнакРасхождения Тогда
				СтрокаБазис.ПризнакРасхождения = НоваяСтрока.ПризнакРасхождения;
			КонецЕсли;
			
			Если СтрокаСравнение.ПризнакРасхождения < НоваяСтрока.ПризнакРасхождения Тогда
				СтрокаСравнение.ПризнакРасхождения = НоваяСтрока.ПризнакРасхождения;
			КонецЕсли;
			
			СтрокаОРганизации.ЗначениеБазис = СтрокаОРганизации.ЗначениеБазис + НоваяСтрока.ЗначениеБазис;
			СтрокаОРганизации.ЗначениеСравнение = СтрокаОРганизации.ЗначениеСравнение + НоваяСтрока.ЗначениеСравнение;
			СтрокаОрганизации.РасхождениеОтносительное = СтрокаОрганизации.РасхождениеОтносительное + НоваяСтрока.РасхождениеОтносительное;
			СтрокаОрганизации.РасхождениеАбсолютное = СтрокаОрганизации.РасхождениеАбсолютное + НоваяСтрока.РасхождениеАбсолютное;
			
			СтрокаОрганизации.РасхождениеАбсолютноеМодуль = ОбщегоНазначенияУХ.ЗначениеПоМодулю(СтрокаОрганизации.РасхождениеАбсолютное);
			СтрокаОрганизации.РасхождениеОтносительноеМодуль = ОбщегоНазначенияУХ.ЗначениеПоМодулю(СтрокаОрганизации.РасхождениеОтносительное);
			
			Если СтрокаОрганизации.ПризнакРасхождения < НоваяСтрока.ПризнакРасхождения Тогда
				СтрокаОрганизации.ПризнакРасхождения = Новаястрока.ПризнакРасхождения;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;		
		
	ПостроительЗапросаОрганизация = Новый ПостроительЗапроса;
	ПостроительЗапросаОрганизация.ИсточникДанных = Новый ОписаниеИсточникаДанных(ТаблицаСверкиОрганизаций);
	ПостроительЗапросаОрганизация.ЗаполнитьНастройки();
	
	ПостроительЗапросаОрганизация.Отбор.Добавить("ОрганизацияБазис");
	ПостроительЗапросаОрганизация.Отбор.Добавить("ОрганизацияСравнение");
	ПостроительЗапросаОрганизация.Отбор.Добавить("КорректныеДанные");
	ПостроительЗапросаОрганизация.Отбор.Добавить("Превышение");
	ПостроительЗапросаОрганизация.Отбор.Добавить("НЕполныеДанные");
	ПостроительЗапросаОрганизация.Отбор.Добавить("НомерСтроки");
	
	ПостроительЗапросаПоказатели = Новый ПостроительЗапроса;
	ПостроительЗапросаПоказатели.ИсточникДанных = Новый ОписаниеИсточникаДанных(ТаблицаСверки);
	ПостроительЗапросаПоказатели.ЗаполнитьНастройки();
	
	ПостроительЗапросаПоказатели.Отбор.Добавить("ОрганизацияБазис");
	ПостроительЗапросаПоказатели.Отбор.Добавить("ОрганизацияСравнение");
	ПостроительЗапросаПоказатели.Отбор.Добавить("ПризнакРасхождения");
	
	Если УникальныйИдентификатор <> Неопределено Тогда
		АдресТаблицыСоответствияПоказаталей = ПоместитьВоВременноеХранилище(ТабРезультат, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры


Процедура ПолучитьРаскрытиеПоАналитикам(ТД, ТЗ, АдресТаблицыСоответствийПоказателей = Неопределено) Экспорт
	
	Если АдресТаблицыСоответствийПоказателей <> Неопределено И НЕ ПустаяСтрока(АдресТаблицыСоответствийПоказателей) Тогда
		
		ТаблицаСоответствийПоказателей = ПолучитьИзВременногоХранилища(АдресТаблицыСоответствийПоказателей);
		
	КонецЕсли;
	
	ТЗ = Новый ТаблицаЗначений;
	
	ПОказательБазис = ТД.ПоказательБазис;
	ПоказательСравнение = ТД.ПоказательСравнение;
	
	ТекСтроки = ТаблицаСоответствийПоказателей.НайтиСтроки(НОвый Структура("ПоказательБазис, ПоказательСравнение", ПоказательБазис, ПоказательСравнение));
	
	Если ТекСтроки.Количество() = 0 Тогда
		ТекСтроки = ТаблицаСоответствийПоказателей.НайтиСтроки(НОвый Структура("ПоказательСравнение, ПоказательБазис", ПоказательБазис, ПоказательСравнение));
		Если ТекСтроки.Количество() = 0 Тогда
			Возврат;
		Иначе
			Поворот = Истина;
		КонецЕсли;
	Иначе
		Поворот = Ложь;
	КонецЕсли;
	
	ТекСтрока = ТекСтроки[0];
	ТаблицаСоответствия = ТекСтрока.СоответствиеАналитик.Получить();
	
	Если ТаблицаСоответствия = Неопределено ИЛИ ТаблицаСоответствия.Количество() = 0 Тогда
		
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = НСтр("ru = 'Не настроено соответствие дополнительных аналитик для сравниваемых внутригрупповых показателей.'");
		СообщениеПользователю.Сообщить();
		Возврат;
		
	КонецЕсли;
	
	АналитикаБазис = "";
	АналитикаСравнение = "";
	
	Первый = Истина;
	МассивАналитик = Новый Массив;
	
	Для Каждого Строка Из ТаблицаСоответствия Цикл
		Если ЗначениеЗаполнено(Строка.КодАналитикиБазис) И ЗначениеЗаполнено(Строка.КодАналитикиСравнение) Тогда
			ТЗ.Колонки.Добавить(Строка.КодАналитикиБазис, , Строка.АналитикаБазис + "/" + Строка.АналитикаСравнение);
			АналитикаБазис = АналитикаБазис + ", " + "<>." + Строка.КодАналитикиБазис;
			АналитикаСравнение = АналитикаСравнение + ", ЗначенияПоказателейОтчетов." + Строка.КодАналитикиСравнение;
			МассивАналитик.Добавить(Строка.КодАналитикиБазис);
		КонецЕсли;
	КонецЦикла;
	
	ТЗ.Колонки.ДОбавить("ЗначениеБазис", ОбщегоНазначенияУХ.ПолучитьОписаниеТиповЧисла(15, 2), НСтр("ru = 'Значение базис'"));
	ТЗ.Колонки.Добавить("ЗначениеСравнение", ОбщегоНазначенияУХ.ПолучитьОписаниеТиповЧисла(15, 2), НСтр("ru = 'Значение сравнение'"));
	ТЗ.Колонки.ДОбавить("Расхождение", , "Расхождение");
	
	Если ПустаяСтрока(АналитикаБазис) ИЛИ ПустаяСтрока(АналитикаСравнение) Тогда
		#Если Клиент Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Не настроено соответствие аналитик для сравниваемых внутригрупповых показателей.'"));
		#КонецЕсли
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст="ВЫБРАТЬ
	|	СравнениеГруппировка.ЗначениеБазис - СравнениеГруппировка.ЗначениеСравнение КАК РасхождениеАбсолютное,
	|	ВЫБОР КОГДА СравнениеГруппировка.ЗначениеБазис = 0 ТОГДА 0
	|	ИНАЧЕ (СравнениеГруппировка.ЗначениеБазис - СравнениеГруппировка.ЗначениеСравнение) * 100 / СравнениеГруппировка.ЗначениеБазис
	|	КОНЕЦ КАК РасхождениеОтносительное,
	|	СравнениеГруппировка.ЗначениеБазис,
	|	СравнениеГруппировка.ЗначениеСравнение " + СтрЗаменить(АналитикаБазис, "<>", "СравнениеГруппировка") + "
	|ИЗ
	|	(ВЫБРАТЬ
	|		СУММА(СравнениеПоказателей.ЗначениеБазис) КАК ЗначениеБазис,
	|		СУММА(СравнениеПоказателей.ЗначениеСравнение) КАК ЗначениеСравнение
	|	" + СтрЗаменить(АналитикаБазис, "<>", "СравнениеПоказателей") + "
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ЗначенияПоказателейОтчетов.ЗначениеЧисло КАК ЗначениеБазис
	|			, 0 КАК ЗначениеСравнение
	|		" + СтрЗаменить(АналитикаБазис, "<>", "ЗначенияПоказателейОтчетов") + "
	|		ИЗ
	|			РегистрСведений.ЗначенияПоказателейОтчетов КАК ЗначенияПоказателейОтчетов
	|		ГДЕ
	|			ЗначенияПоказателейОтчетов.Версия.Организация = &ОрганизацияБазис
	|			И ЗначенияПоказателейОтчетов.Аналитика" + ТекСтрока.АналитикаВГО_Базис +" = &ОрганизацияСравнение
	|			И ЗначенияПоказателейОтчетов.Показатель = &ПоказательБазис
	|			И ЗначенияПоказателейОтчетов.Версия.Валюта=&Валюта
	|			И ЗначенияПоказателейОтчетов.Версия.Сценарий = &Сценарий
	|			И ЗначенияПоказателейОтчетов.Версия.ПериодОтчета = &ПериодОтчета
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			0,
	|			ЗначенияПоказателейОтчетов.ЗначениеЧисло
	|		" + АналитикаСравнение + "
	|		ИЗ
	|			РегистрСведений.ЗначенияПоказателейОтчетов КАК ЗначенияПоказателейОтчетов
	|		ГДЕ
	|		ЗначенияПоказателейОтчетов.Версия.Организация = &ОрганизацияСравнение
	|		И ЗначенияПоказателейОтчетов.Аналитика" + ТекСтрока.АналитикаВГО_Сравнение +" =  &ОрганизацияБазис
	|		И ЗначенияПоказателейОтчетов.Показатель = &ПоказательСравнение
	|		И ЗначенияПоказателейОтчетов.Версия.Валюта=&Валюта
	|		И ЗначенияПоказателейОтчетов.Версия.Сценарий = &Сценарий
	|		И ЗначенияПоказателейОтчетов.Версия.ПериодОтчета = &ПериодОтчета) КАК СравнениеПоказателей
	|	СГРУППИРОВАТЬ ПО
	|" + Сред(СтрЗаменить(АналитикаБазис, "<>", "СравнениеПоказателей"), 2) + "
	|) КАК СравнениеГруппировка
	| ГДЕ НЕ (СравнениеГруппировка.ЗначениеБазис = 0 И СравнениеГруппировка.ЗначениеСравнение = 0)";
	
	Запрос.УстановитьПараметр("ПоказательБазис",ТекСтрока.ПоказательБазис);
	Запрос.УстановитьПараметр("ПоказательСравнение",ТекСтрока.ПоказательСравнение);
	
	Запрос.УстановитьПараметр("ВидОтчетаБазис",ТекСтрока.ВидОтчетаБазис);
	Запрос.УстановитьПараметр("ВидОтчетаСравнение",ТекСтрока.ВидОтчетаСравнение);
	Запрос.УстановитьПараметр("Сценарий",Сценарий);
	Запрос.УстановитьПараметр("ПериодОтчета",Период);
	Запрос.УстановитьПараметр("Валюта",Валюта);
	Запрос.УстановитьПараметр("ОрганизацияБазис", ?(Поворот, ТД.ОрганизацияСравнение, ТД.ОрганизацияБазис));
	Запрос.УстановитьПараметр("ОрганизацияСравнение", ?(Поворот, ТД.ОрганизацияБазис, ТД.ОрганизацияСравнение));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ТЗ.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.Расхождение = ?(АбсолютноеПредставление, Формат(Выборка.РасхождениеАбсолютное, "ЧЦ=15; ЧДЦ=2; ЧН=0"), Формат(Выборка.РасхождениеОтносительное, "ЧЦ=5; ЧДЦ=2; ЧН=0") + "%");
	КонецЦикла;
	
КонецПроцедуры
