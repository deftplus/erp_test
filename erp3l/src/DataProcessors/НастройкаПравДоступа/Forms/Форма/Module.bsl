&НаКлиенте
Перем ВыполняетсяЗакрытие;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОтборЗначение = Параметры.ПараметрНастройки;
	
	Если НЕ ЗначениеЗаполнено(ОтборЗначение) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЗначениеСсылочногоТипа(ОтборЗначение)
		И ОбщегоНазначения.ОбъектЯвляетсяГруппой(ОтборЗначение) Тогда
		
			Отказ = Истина;
			Возврат;
			
	КонецЕсли;
	
	ТипЗначенияОтбора = ТипЗнч(ОтборЗначение);
	
	Если ТипЗначенияОтбора = Тип("СправочникСсылка.Организации") ИЛИ РасширениеБизнесЛогикиУХКлиентСервер.ПроверитьТипЗначения(ОтборЗначение, "СправочникСсылка.ГруппыДоступаКОрганизациям") Тогда
				
		ОтборИмя = "Организация";
		
	ИначеЕсли ТипЗначенияОтбора = Тип("СправочникСсылка.ВидыОтчетов") ИЛИ РасширениеБизнесЛогикиУХКлиентСервер.ПроверитьТипЗначения(ОтборЗначение, "СправочникСсылка.ГруппыДоступаКВидамОтчетов") Тогда
		
		ОтборИмя = "ВидОтчета";
		
	ИначеЕсли ТипЗначенияОтбора = Тип("СправочникСсылка.Пользователи") ИЛИ ТипЗначенияОтбора = Тип("СправочникСсылка.ГруппыПользователей") Тогда
	
		ОтборИмя = "Пользователь";
		
	КонецЕсли;
	
	Элементы.ОтборЗначение.Заголовок = Метаданные.НайтиПоТипу(ТипЗначенияОтбора).ПредставлениеОбъекта;
	Элементы["НастройкиПравДоступаПользователейВидыОтчетов" + ОтборИмя].Видимость = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЗначениеОтбора", ОтборЗначение);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиПравДоступаПользователейВидыОтчетов.Организация,
	|	НастройкиПравДоступаПользователейВидыОтчетов.ВидОтчета,
	|	НастройкиПравДоступаПользователейВидыОтчетов.Пользователь,
	|	НастройкиПравДоступаПользователейВидыОтчетов.ВидДоступа
	|ИЗ
	|	РегистрСведений.НастройкиПравДоступаПользователейВидыОтчетов КАК НастройкиПравДоступаПользователейВидыОтчетов";
	
	Запрос.Текст = Запрос.Текст + "
	|ГДЕ НастройкиПравДоступаПользователейВидыОтчетов." + ОтборИмя + " = &ЗначениеОтбора";
	
	ЗначениеВРеквизитФормы(Запрос.Выполнить().Выгрузить(), "НастройкиПравДоступаПользователейВидыОтчетов");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ОтборЗначение = ПредопределенноеЗначение("Справочник.ГруппыПользователей.ВсеПользователи") Тогда
		
		Отказ = Истина;
		ПоказатьПредупреждение(, "Настройка прав доступа для предопределенной группы ""Все пользователи"" не поддерживается!");		
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием_Завершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		ВыполняетсяЗакрытие = Истина;
		ЗаписатьНаКлиенте();
		Закрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		ВыполняетсяЗакрытие = Истина;
		Закрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда	
		// Не выполняем закрытие.
	Иначе
		ТекстСообщения = НСтр("ru = 'Выбран неизвестный вариант %Результат%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Результат%", Строка(Результат));
		ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		Если НЕ ВыполняетсяЗакрытие Тогда
			Отказ = Истина;
			ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить?'");
			ОписаниеЗакрытия = Новый ОписаниеОповещения("ПередЗакрытием_Завершение", ЭтотОбъект);
			ПоказатьВопрос(ОписаниеЗакрытия, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		Иначе
			// Выполняем далее.
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиПравДоступаПользователейВидыОтчетовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И НЕ Копирование Тогда
		Элементы.НастройкиПравДоступаПользователейВидыОтчетов.ТекущиеДанные[ОтборИмя] = ОтборЗначение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	ЗаписатьНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Если ЗаписатьНаКлиенте() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ЗаписатьНаКлиенте()
	
	УспешнаяЗапись = ЗаписатьНаСервере();
	
	Если НЕ УспешнаяЗапись Тогда
		ПоказатьПредупреждение(, "Запись не удалась!" + Символы.ПС + "Подробная информация находится в журнале регистрации.");
	КонецЕсли;
	
	Возврат УспешнаяЗапись;
	
КонецФункции

&НаСервере
Функция ЗаписатьНаСервере()
	
	ПРОТОКОЛИРУЕМОЕ_СОБЫТИЕ = "Обработка.ОбработкаУстановитьПраваДоступа.Форма.Форма.Модуль";
	
	НаборЗаписей = РегистрыСведений.НастройкиПравДоступаПользователейВидыОтчетов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор[ОтборИмя].Установить(ОтборЗначение);
	НаборЗаписей.Загрузить(РеквизитФормыВЗначение("НастройкиПравДоступаПользователейВидыОтчетов"));
	
	НачатьТранзакцию();
	
	Попытка
		
		НаборЗаписей.Записать();
		
	Исключение
		ОтменитьТранзакцию();
		ПротоколируемыеСобытияУХ.ДобавитьЗаписьОшибка(ПРОТОКОЛИРУЕМОЕ_СОБЫТИЕ,,, "Системная ошибка. Подробности в полном протоколе.", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Ложь;
	КонецПопытки;
	
	ЗафиксироватьТранзакцию();
	
	Модифицированность = Ложь;
	
	Возврат Истина;
	
КонецФункции

ВыполняетсяЗакрытие = Ложь;