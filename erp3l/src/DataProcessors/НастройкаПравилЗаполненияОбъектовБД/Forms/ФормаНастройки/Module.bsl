
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбъектБД=Параметры.ОбъектБД;
	Организация=Параметры.Организация;
	
	Для Каждого СтрНастройка ИЗ Параметры.МассивНастроек Цикл
		
		НоваяСтрока=ТаблицаПравилЗаполнения.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрНастройка);
		
	КонецЦикла;
	
	ТекстЗаголовок="Документ: "+ОбъектБД;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		ТекстЗаголовок=ТекстЗаголовок+", организация: "+Организация;
		
	Иначе
		
		ТекстЗаголовок=ТекстЗаголовок+", по всем организациям";
		
	КонецЕсли;
	
	Если НЕ ОбъектБД.Владелец=Справочники.ТипыБазДанных.ТекущаяИБ Тогда
		
		ТекстЗаголовок=ТекстЗаголовок+", "+ОбъектБД.Владелец;
		
		Элементы.ТаблицаПравилНастройкиИспользуемаяИБ.Заголовок="Информационная база (приемник)";
		
	Иначе
		
		Элементы.ТаблицаПравилНастройкиИспользуемаяИБ.Заголовок="Информационная база (источник)";
			
	КонецЕсли;
	
	ТекстЗаголовок=ТекстЗаголовок+".";
	
	Элементы.ДекорацияОтбор.Заголовок=ТекстЗаголовок;
		
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПравилНастройкиПравилоЗаполненияПриИзменении(Элемент)
	
	ТаблицаПравилНастройкиПравилоЗаполненияПриИзмененииНаСервере(Элементы.ТаблицаПравилНастройки.ТекущиеДанные.ПолучитьИдентификатор());
	
КонецПроцедуры

&НаСервере
Процедура ТаблицаПравилНастройкиПравилоЗаполненияПриИзмененииНаСервере(ИдентификаторСтроки)
	
	ТекущиеДанные=ТаблицаПравилЗаполнения.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	ПравилоЗаполнения=ТекущиеДанные.ПравилоЗаполнения;
	
	Если ОбъектБД.Владелец=Справочники.ТипыБазДанных.ТекущаяИБ Тогда
		
		ТекущиеДанные.ТипБД	=ПравилоЗаполнения.ТипБД;
		
		Если ПравилоЗаполнения.ТипБД=Справочники.ТипыБазДанных.ТекущаяИБ Тогда
			
			ТекущиеДанные.ИспользуемаяИБ=Справочники.ТипыБазДанных.ТекущаяИБ;
			
		КонецЕсли;
		
	Иначе
		
		ТекущиеДанные.ТипБД	=ОбъектБД.Владелец;
		
	КонецЕсли;
	
	ТекущиеДанные.ИсходныйОбъектБД=ПравилоЗаполнения.ИсходныйОбъектБД;
		
КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
	
	СтруктураПравила=Новый Структура;
	
	Если Модифицированность Тогда
		
		МассивНастроек=Новый Массив;
		
		Для Каждого Строка ИЗ ТаблицаПравилЗаполнения Цикл
			
			СтруктураНастройки=Новый Структура;
			СтруктураНастройки.Вставить("ПравилоЗаполнения",		Строка.ПравилоЗаполнения);
			СтруктураНастройки.Вставить("ИсходныйОбъектБД",			Строка.ИсходныйОбъектБД);
			СтруктураНастройки.Вставить("ИспользуемаяИБ",			Строка.ИспользуемаяИБ);
			СтруктураНастройки.Вставить("ВыполнятьАвтоматически",	Строка.ВыполнятьАвтоматически);
			СтруктураНастройки.Вставить("ПроводитьДокументы",		Строка.ПроводитьДокументы);
			СтруктураНастройки.Вставить("ТипБД",					Строка.ТипБД);
						
			МассивНастроек.Добавить(СтруктураНастройки);
			
		КонецЦикла;
		
		СтруктураПравила.Вставить("Организация",Параметры.Организация);
		СтруктураПравила.Вставить("ОбъектБД",ОбъектБД);
		СтруктураПравила.Вставить("МассивНастроек",МассивНастроек);
		СтруктураПравила.Вставить("Модифицированность",Истина);
		
	Иначе
		
		СтруктураПравила.Вставить("Модифицированность",Ложь);
		
	КонецЕсли;
	
	Оповестить("ИзмененыПравилаЗаполненияОбъектаБД",СтруктураПравила);
	Закрыть();
			
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоОбъектуБДНаСервере()
	
	ТаблицаПравилЗаполнения.Очистить();
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПравилаЗаполненияОбъектовБД.Владелец.Владелец = ЗНАЧЕНИЕ(Справочник.ТипыБазДанных.ТекущаяИБ)
	|			ТОГДА ПравилаЗаполненияОбъектовБД.ТипБД
	|		ИНАЧЕ ПравилаЗаполненияОбъектовБД.Владелец.Владелец
	|	КОНЕЦ КАК ТипБД,
	|	ПравилаЗаполненияОбъектовБД.ИсходныйОбъектБД,
	|	ПравилаЗаполненияОбъектовБД.Ссылка КАК ПравилоЗаполнения,
	|	ВЫБОР
	|		КОГДА ПравилаЗаполненияОбъектовБД.Владелец.Владелец = ЗНАЧЕНИЕ(Справочник.ТипыБазДанных.ТекущаяИБ)
	|			ТОГДА ПравилаЗаполненияОбъектовБД.ТипБД.ВИБПоУмолчанию
	|		ИНАЧЕ ПравилаЗаполненияОбъектовБД.Владелец.Владелец.ВИБПоУмолчанию
	|	КОНЕЦ КАК ИспользуемаяИБ
	|ИЗ
	|	Справочник.ПравилаЗаполненияОбъектовБД КАК ПравилаЗаполненияОбъектовБД
	|ГДЕ
	|	ПравилаЗаполненияОбъектовБД.Владелец = &ОбъектБД
	|	И НЕ ПравилаЗаполненияОбъектовБД.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ОбъектБД",ОбъектБД);
	
	Результат=Запрос.Выполнить().Выбрать();
	
	Пока Результат.Следующий() Цикл
		
		НоваяСтрока=ТаблицаПравилЗаполнения.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Результат);
		
		НоваяСтрока.ВыполнятьАвтоматически=ЗначениеЗаполнено(Результат.ИсходныйОбъектБД) И Результат.ТипБД=Справочники.ТипыБазДанных.ТекущаяИБ;
		НоваяСтрока.ПроводитьДокументы=НоваяСтрока.ВыполнятьАвтоматически;
		
		Если Результат.ТипБД=Справочники.ТипыБазДанных.ТекущаяИБ Тогда
			
			НоваяСтрока.ИспользуемаяИБ=Справочники.ВнешниеИнформационныеБазы.ТекущаяИБ;
			
		КонецЕсли;
				
	КонецЦикла;
			
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОбъектуБД(Команда)
	
	ЗаполнитьПоОбъектуБДНаСервере();
	Модифицированность=Истина;
	
КонецПроцедуры
