
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ИдентификаторПоля"   , ИдентификаторПоля);
	Параметры.Свойство("КомпоновщикНастроек" , КомпоновщикНастроек);
	
	Если ИдентификаторПоля <> Неопределено Тогда
		
		ПользовательскоеПоле = КомпоновщикНастроек.Настройки.ПользовательскиеПоля.ПолучитьОбъектПоИдентификатору(ИдентификаторПоля);
		
		Если ПользовательскоеПоле = Неопределено Тогда
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст = "Не удалось прочитать данные по произвольному полю";
			СообщениеПользователю.Сообщить();
			Отказ = Истина;
		Иначе
			Наименование = ПользовательскоеПоле.Заголовок;
			ВыражениеДетальныхЗаписей.Удалить();
			ВыражениеДетальныхЗаписей.Добавить(ПользовательскоеПоле.ПолучитьПредставлениеВыраженияДетальныхЗаписей());
			ВыражениеИтогов = ТиповыеОтчетыУХ.ПолучитьВыражениеАгрегатаИтоговыхЗаписей(ПользовательскоеПоле);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастроекНастройкиВыборДоступныеПоляВыбораНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") Тогда
		Если ПараметрыПеретаскивания.Значение.Количество() > 0 Тогда
			ПервыйЭлемент = ПараметрыПеретаскивания.Значение[0];
			ПараметрыПеретаскивания.Значение = "[" + ПервыйЭлемент.Заголовок + "]";
		Иначе
			ПараметрыПеретаскивания.Значение = "[]";
		КонецЕсли;
	Иначе	
		ПараметрыПеретаскивания.Значение = "[" + ПараметрыПеретаскивания.Значение.Заголовок + "]";
	КонецЕсли;
	ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Копирование;
	
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастроекНастройкиВыборДоступныеПоляВыбораВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекСтрока = КомпоновщикНастроек.Настройки.Выбор.ДоступныеПоляВыбора.ПолучитьОбъектПоИдентификатору(ВыбраннаяСтрока);
	Если ТекСтрока <> Неопределено Тогда
		ВыражениеДетальныхЗаписей.Добавить("[" + ТекСтрока.Заголовок + "]");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	СозданоПользовательскоеПоле = Ложь;
	
	Если ПользовательскоеПоле = Неопределено Тогда
		ПользовательскоеПоле = КомпоновщикНастроек.Настройки.ПользовательскиеПоля.Элементы.Добавить(Тип("ПользовательскоеПолеВыражениеКомпоновкиДанных"));
	Иначе
		ПользовательскоеПоле = КомпоновщикНастроек.Настройки.ПользовательскиеПоля.ПолучитьОбъектПоИдентификатору(ИдентификаторПоля);
	КонецЕсли;
	Попытка
		ПользовательскоеПоле.УстановитьПредставлениеВыраженияДетальныхЗаписей(ВыражениеДетальныхЗаписей.ПолучитьТекст());
		ПользовательскоеПоле.УстановитьПредставлениеВыраженияИтоговыхЗаписей(ВыражениеИтогов + ВыражениеДетальныхЗаписей.ПолучитьТекст() + ")");
		ПользовательскоеПоле.Заголовок = Наименование;
	Исключение
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = "Ошибка в формуле";
		СообщениеПользователю.Поле  = "ВыражениеДетальныхЗаписей";
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецПопытки;
	
	Закрыть(КомпоновщикНастроек);
	
КонецПроцедуры
