
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Параметры.Объект.Ссылка) = Тип("ДокументСсылка.ВерсияГрафикаЦеннойБумаги") Тогда
		
		Заголовок = Параметры.ЗаголовокКолонкиГрафика;
		СтрокаГрафика = Параметры.Объект.График.НайтиПоИдентификатору(Параметры.СтрокаГрафика);
		Дата = СтрокаГрафика.Дата;
		СуммаИтого = СтрокаГрафика[Параметры.ТекущееПоле];
		
		СтрокиРасчета = Параметры.Объект.Проценты.НайтиСтроки(Новый Структура("Дата", Дата));
		
		Для Каждого ТекНайденнаяСтрока Из СтрокиРасчета Цикл
			
			НоваяСтрока = ДанныеРасчета.Добавить();
			
			НоваяСтрока.Сумма = ТекНайденнаяСтрока[Параметры.ТекущееПоле];
			НоваяСтрока.ДатаНачала = ТекНайденнаяСтрока.ДатаНачала;
			НоваяСтрока.ДатаОкончания = ТекНайденнаяСтрока.ДатаОкончания;
			НоваяСтрока.База = ТекНайденнаяСтрока.СуммаЗадолженности;
			НоваяСтрока.Ставка = ТекНайденнаяСтрока.ПроцентнаяСтавка;
			
		КонецЦикла;
		
		СуммаРасчет = ДанныеРасчета.Итог("Сумма");
		
	Иначе
		
		Дата = Параметры.ДатаРасшифровки;
		ОперацияГрафика = РаботаСДоговорамиКонтрагентовУХКлиентСервер.ОперацияГрафикаПоИмениКолонки(Параметры.ТекущееПоле, Параметры.ОписаниеГрафика);
		
		Заголовок = Параметры.ЗаголовокКолонкиГрафика + " " + Формат(Дата, "ДЛФ=D");
		ЗаполнитьСуммыИтогоКорректировка(Параметры.Объект, Дата, ОперацияГрафика);
		СтрокиРасчета = Параметры.Объект[Параметры.ИмяСекцииГрафика].НайтиСтроки(Новый Структура("Дата", Дата));
		
		Для Каждого ТекНайденнаяСтрока Из СтрокиРасчета Цикл
			СуммаСтроки = ТекНайденнаяСтрока[Параметры.ТекущееПоле + "Расчет"];
			
			Если СуммаСтроки = 0 Тогда
				// Эта строка нас не интересует.
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ДанныеРасчета.Добавить();
			НоваяСтрока.Сумма = СуммаСтроки;
			НоваяСтрока.ДатаНачала = ТекНайденнаяСтрока.ДатаНачала;
			НоваяСтрока.ДатаОкончания = ТекНайденнаяСтрока.ДатаОкончания;
			НоваяСтрока.База = ТекНайденнаяСтрока.СуммаЗадолженности;
			НоваяСтрока.Ставка = ТекНайденнаяСтрока.ПроцентнаяСтавка;
			
		КонецЦикла;
		СуммаРасчет = ДанныеРасчета.Итог("Сумма");
		
	КонецЕсли;

	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСуммыИтогоКорректировка(Объект, Дата, Операция)
	
	СтруктураПоиска = Новый Структура("ОперацияГрафика,Дата", Операция, Дата);
	СтрокиГрафика = Объект.ГрафикРасчетов.Выгрузить(СтруктураПоиска);
	
	СуммаИтого = СтрокиГрафика.Итог("Сумма");
	СуммаРучнаяКорректировка = СтрокиГрафика.Итог("СуммаКорректировка");
	
КонецПроцедуры

#КонецОбласти


