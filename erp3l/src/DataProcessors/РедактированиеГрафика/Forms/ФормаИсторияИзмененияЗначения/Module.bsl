
&НаКлиенте
Процедура ИсторияИзмененийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ПоказатьЗначение(,Элемент.ТекущиеДанные.ВерсияГрафика);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Заголовок = СтрШаблон(НСтр("ru = '%1 %2: история изменения'"),СокрЛП(Параметры.ЗаголовокКолонкиГрафика), Формат(Параметры.ДатаОперации, "ДЛФ=D"));
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "ДатаОперации, ТекущееЗначение");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВерсииРасчетов.ВерсияГрафика КАК ВерсияГрафика,
	|	ВерсииРасчетов.ОпорныйГрафик КАК Опорный,
	|	ВерсииРасчетов.Период КАК Дата,
	|	СУММА(ЕСТЬNULL(ДокументГрафик.Сумма, 0)) КАК Сумма
	|ПОМЕСТИТЬ ВТ_ИсходныеДанные
	|ИЗ
	|	РегистрСведений.ВерсииРасчетов КАК ВерсииРасчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВерсияСоглашенияКредит.ГрафикРасчетов КАК ДокументГрафик
	|		ПО ВерсииРасчетов.ВерсияГрафика = ДокументГрафик.Ссылка
	|			И (ДокументГрафик.Дата = &Дата)
	|			И (ДокументГрафик.ОперацияГрафика = &ОперацияГрафика)
	|ГДЕ
	|	ВерсииРасчетов.ПредметГрафика = &ФинансовыйИнструмент
	|
	|СГРУППИРОВАТЬ ПО
	|	ВерсииРасчетов.ВерсияГрафика,
	|	ВерсииРасчетов.ОпорныйГрафик,
	|	ВерсииРасчетов.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ИсходныеДанные.ВерсияГрафика КАК ВерсияГрафика,
	|	ВТ_ИсходныеДанные.Опорный КАК Опорный,
	|	ВТ_ИсходныеДанные.Дата КАК Дата,
	|	ВТ_ИсходныеДанные.Сумма КАК Сумма,
	|	СУММА(1) КАК Номер
	|ПОМЕСТИТЬ ВТ_ИсходныеДанныеНомер
	|ИЗ
	|	ВТ_ИсходныеДанные КАК ВТ_ИсходныеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИсходныеДанные КАК ВТ_ИсходныеДанные1
	|		ПО ВТ_ИсходныеДанные.Дата >= ВТ_ИсходныеДанные1.Дата
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ИсходныеДанные.ВерсияГрафика,
	|	ВТ_ИсходныеДанные.Опорный,
	|	ВТ_ИсходныеДанные.Дата,
	|	ВТ_ИсходныеДанные.Сумма
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ИсходныеДанныеНомер.ВерсияГрафика КАК ВерсияГрафика,
	|	ВТ_ИсходныеДанныеНомер.Опорный КАК Опорный,
	|	ВТ_ИсходныеДанныеНомер.Дата КАК Дата,
	|	ВТ_ИсходныеДанныеНомер.Сумма КАК Сумма,
	|	ВТ_ИсходныеДанныеНомер.Сумма - ЕСТЬNULL(ВТ_ИсходныеДанныеНомер1.Сумма, 0) КАК Изменение
	|ИЗ
	|	ВТ_ИсходныеДанныеНомер КАК ВТ_ИсходныеДанныеНомер
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИсходныеДанныеНомер КАК ВТ_ИсходныеДанныеНомер1
	|		ПО (ВТ_ИсходныеДанныеНомер.Номер = ВТ_ИсходныеДанныеНомер1.Номер + 1)
	|ГДЕ
	|	ВТ_ИсходныеДанныеНомер.Сумма - ЕСТЬNULL(ВТ_ИсходныеДанныеНомер1.Сумма, 0) <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ";

	Запрос.УстановитьПараметр("Дата", Параметры.ДатаОперации);
	Запрос.УстановитьПараметр("ФинансовыйИнструмент", Параметры.ФинансовыйИнструмент);
	Запрос.УстановитьПараметр("ОперацияГрафика", Параметры.ОперацияГрафика);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВерсияСоглашенияКредит", Параметры.Документ.Метаданные().Имя);
	
	ИсторияИзменений.Загрузить(Запрос.Выполнить().Выгрузить());

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	//
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, Элементы.ИсторияИзмененийИзменение.Имя);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ИсторияИзменений.Изменение", ВидСравненияКомпоновкиДанных.Больше,0,,Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.ТемноЗеленый);
	
	//
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, Элементы.ИсторияИзмененийИзменение.Имя);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ИсторияИзменений.Изменение", ВидСравненияКомпоновкиДанных.Меньше,0,,Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Киноварь);

	
КонецПроцедуры
