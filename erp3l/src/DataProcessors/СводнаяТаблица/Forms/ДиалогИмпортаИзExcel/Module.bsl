

&НаКлиенте
Процедура ФайлНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Перем АдресХранилища;
	СтандартнаяОбработка = Ложь;
	
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Заголовок = Нстр("ru = 'Выберите файл для экспорта'");
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.Фильтр = Нстр("ru = 'Таблица Excel'") + " (*.xlsx)|*.xlsx";
	
	ОповещениеВыборФайлаЗавершение = Новый ОписаниеОповещения("ВыборФайлаЗавершение", ЭтаФорма);
	ДиалогВыбораФайла.Показать(ОповещениеВыборФайлаЗавершение);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаЗавершение(ВыбранныеФайлы,ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено Тогда
		
		ПутьКФайлу = ВыбранныеФайлы[0];
		
		Файл = Новый Файл(ПутьКФайлу);
		
		ИмяФайла = Файл.Имя;
		ПолноеИмяФайла = Файл.ПолноеИмя;
		
		СуществующийФайл=ПолноеИмяФайла;
		СписокЛистов = ПолучитьСписокЛистовФайла();
		Элементы.ИмяЛиста.СписокВыбора.ЗагрузитьЗначения(СписокЛистов.ВыгрузитьЗначения());
			
	КонецЕсли;
		
КонецПроцедуры


&НаКлиенте
Процедура Сохранить(Команда)
	
	Если Не ЗначениеЗаполнено(СуществующийФайл) Тогда
		ОбщегоНазначенияУХ.СообщитьОбОшибке(Нстр("ru = 'Необходимо выбрать файл!'"));
		Возврат;
	КонецЕсли;	
	Если Не ЗначениеЗаполнено(ИмяЛиста) Тогда
		ОбщегоНазначенияУХ.СообщитьОбОшибке(Нстр("ru = 'Необходимо выбрать имя листа!'"));
		Возврат;
	КонецЕсли;	

	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ИмяКниги",СуществующийФайл);
	СтруктураПараметров.Вставить("ИмяЛиста",ИмяЛиста);
	
	Закрыть(СтруктураПараметров);
	 
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСписокЛистовФайла() Экспорт
	
	Ексел  = Неопределено;
	Если НЕ ОткрытьФайлЕксель(Ексел) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	
	#Если Клиент Тогда
		Состояние(Нстр("ru = 'Определение рабочей области ...'"));
	#КонецЕсли
	
	КоличествоЛистовДокумента = Ексел.ActiveWorkBook.Sheets.Count;
	СписокЛистов = Новый СписокЗначений;
	Для Инд = 1 По КоличествоЛистовДокумента Цикл
		ТекЛист = Ексел.Sheets.Item(Инд);
		СписокЛистов.Добавить(ТекЛист.Name);
	КонецЦикла;
	Ексел.ActiveWorkBook.Close(-1);
	Ексел  = Неопределено;
	Возврат СписокЛистов;
	
КонецФункции

&НаКлиенте
Функция ОткрытьФайлЕксель(Ексел)
	
	Попытка
		
		Excel_Настройки = Неопределено;
		Ексел = ОбщегоНазначенияMicrosoftExcelКлиентСерверУХ.Создать(Excel_Настройки);
		
		Если Ексел = Неопределено Тогда
			ВызватьИсключение Нстр("ru = 'Не удалось создать COM-объект Microsoft Excel.'");
		КонецЕсли;
		
		РабочаяКнига = Ексел.Workbooks.Open(СуществующийФайл);	
		Ексел.Visible = Ложь;
		
	Исключение
		
		#Если Клиент Тогда
			ПоказатьПредупреждение(,Нстр("ru = 'Не удается открыть файл'"));
		#Иначе
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст = СтрШаблон(Нстр("ru = 'Не удалось открыть файл XLS.
			|%1
			|Возможные причины ошибки описаны в справке к документу ""Экземпляр отчета"" в разделе ""Импорт""'"), 
			?(ТекстОшибки = Неопределено, "", ТекстОшибки));
			СообщениеПользователю.Сообщить();
		#КонецЕсли
		
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;
	 
КонецФункции

