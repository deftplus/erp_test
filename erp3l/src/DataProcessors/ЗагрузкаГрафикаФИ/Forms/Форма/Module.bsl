
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//СписокКолонок - Список значений
	//(колонки нумеруются по порядку, представление значения - представление колонки, 
	//Значение - тип значения для преобразования)
	Если Параметры.ПараметрыГрафика <> Неопределено Тогда
		АвтоЗаголовок = Ложь;
		Заголовок = НСтр("ru = 'Загрузка графика финансового инструмента: '") + Параметры.ПараметрыГрафика.ФИ;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Заголовок) Тогда
		ЭтаФорма.Заголовок = Параметры.Заголовок;
		АвтоЗаголовок = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ЗаголовокКнопкиЗаписать) Тогда
		Элементы.ЗаписатьГрафик.Заголовок = Параметры.ЗаголовокКнопкиЗаписать;
	КонецЕсли;
		
	Если ЭтоАдресВременногоХранилища(Параметры.АдресТаблицыГрафика) Тогда
		
		ТаблицаГрафика = ПолучитьИзВременногоХранилища(Параметры.АдресТаблицыГрафика);
		ЧислоКолонок = ТаблицаГрафика.Колонки.Количество();
		ЧислоСтрок = ТаблицаГрафика.Количество();
		
		НомераКолонокДат = Новый Массив;
		ТипДата = Тип("Дата");
		Для НомерКолонки = 1 По ЧислоКолонок Цикл
			
			ОписаниеКолонки = ТаблицаГрафика.Колонки.Получить(НомерКолонки-1);
			ТабличныйДокумент.Область(1, НомерКолонки).Текст = ?(ОписаниеКолонки.Заголовок = "", ОписаниеКолонки.Имя, ОписаниеКолонки.Заголовок);
			ТабличныйДокумент.Область(,НомерКолонки,,НомерКолонки).ШиринаКолонки = 20;
			Если ОписаниеКолонки.ТипЗначения.СодержитТип(ТипДата) Тогда
				НомераКолонокДат.Добавить(НомерКолонки);
			КонецЕсли;
			
		КонецЦикла;
		
		Для НомерСтроки = 1 По ЧислоСтрок Цикл	
			
			СтрокаГрафика = ТаблицаГрафика[НомерСтроки-1];
			Для НомерКолонки = 1 По ЧислоКолонок Цикл				
				ОбластьЗаполнения = ТабличныйДокумент.Область(НомерСтроки + 1, НомерКолонки);
				Если НомераКолонокДат.Найти(НомерКолонки) = Неопределено Тогда
					ОбластьЗаполнения.Текст = СтрокаГрафика[НомерКолонки - 1];
				Иначе
					ОбластьЗаполнения.Текст = Формат(СтрокаГрафика[НомерКолонки - 1], "ДФ=dd.MM.yyyy");
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
		ОбластьЗаголовка = ТабличныйДокумент.Область(1, 1, 1, ЧислоКолонок);
		ОбластьЗаголовка.ЦветФона = ЦветаСтиля.ЦветФонаТолькоПросмотр;
		ОбластьЗаголовка.Шрифт = ШрифтыСтиля.ШрифтВажнойНадписи;
		
		АдресТаблицыГрафика = ПоместитьВоВременноеХранилище(ТаблицаГрафика, УникальныйИдентификатор);
		
	Иначе	
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьГрафик(Команда)
	
	АдресДанныхГрафика = ПолучитьГрафикНаСервере();
	Оповестить("ЗагрузитьГрафикИзТабличногоДокумента", АдресДанныхГрафика, ЭтаФорма.ВладелецФормы);
	
	Закрыть(АдресДанныхГрафика);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьГрафикНаСервере()

	ОбработкаЗагрузки = РеквизитФормыВЗначение("Объект");
	СтарыйГрафик = ПолучитьИзВременногоХранилища(АдресТаблицыГрафика);
	ТаблицаГрафик = ОбработкаЗагрузки.ПолучитьТаблицуГрафика(ТабличныйДокумент, СтарыйГрафик);
	ДанныеГрафика = Новый Структура("График,ПараметрыГрафика", ТаблицаГрафик, Параметры.ПараметрыГрафика);
	
	Возврат ПоместитьВоВременноеХранилище(ДанныеГрафика, УникальныйИдентификатор);

КонецФункции


