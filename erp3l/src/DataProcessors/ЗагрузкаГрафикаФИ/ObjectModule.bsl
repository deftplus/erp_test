#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
Процедура СообщитьОбОшибкеПреобразования(НомерСтроки, ИмяКолонки, СтроковоеЗначение)
	
	ШаблонТекста = НСтр("ru='Строка №%1 колонка ""%2"" - ошибка преобразования значения ""%3""
						|Строка не загружена'");
	Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, НомерСтроки, ИмяКолонки, СтроковоеЗначение);
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
	
КонецПроцедуры

Функция ТекстВДату(Знач СтроковоеЗначение, НомерСтроки, ИмяКолонки, Разделитель = ".") Экспорт
	
	Цифры = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтроковоеЗначение,Разделитель);
	Попытка
		Результат = СокрЛП(?(СтрДлина(Цифры[2]) = 2, "20"+Цифры[2], Цифры[2]))
					+ СокрЛП(Формат(Цифры[1], "ЧЦ=2; ЧВН=;"))
					+ СокрЛП(Формат(Цифры[0], "ЧЦ=2; ЧВН=;"));
		Результат = Дата(Результат);
	Исключение
		СообщитьОбОшибкеПреобразования(НомерСтроки, ИмяКолонки, СтроковоеЗначение);
	КонецПопытки;
	Возврат Результат;
	
КонецФункции

Функция ТекстВЧисло(СтроковоеЗначение, НомерСтроки, ИмяКолонки) Экспорт
	
	Если ПустаяСтрока(СтроковоеЗначение) ИЛИ СтроковоеЗначение = Неопределено Тогда
		Возврат 0;
	КонецЕсли;
	
	Попытка
		Результат = Число(СтрЗаменить(СтроковоеЗначение," ",""));
	Исключение
		СообщитьОбОшибкеПреобразования(НомерСтроки, ИмяКолонки, СтроковоеЗначение);
	КонецПопытки;
	Возврат Результат;
	
КонецФункции

Функция ПолучитьТаблицуГрафика(ТабличныйДокументГрафик, СтарыйГрафик) Экспорт

	КоличествоКолонок = СтарыйГрафик.Колонки.Количество();
	ТипДата = Тип("Дата");
	ТипСтрока = Тип("Строка");
	ТипЧисло = Тип("Число");
		
	ТаблицаГрафика = СтарыйГрафик.СкопироватьКолонки();
	
	Для НомерСтроки = 2 По ТабличныйДокументГрафик.ВысотаТаблицы Цикл
		
		СтрокаГрафика = ТаблицаГрафика.Добавить();
		
		Для НомерКолонки = 1 По (КоличествоКолонок) Цикл
			
			Попытка
				ТекстЯчейки = ТабличныйДокументГрафик.Область(НомерСтроки, НомерКолонки).Текст;		
			Исключение
				СообщитьОбОшибкеПреобразования(НомерСтроки, НомерКолонки, "");
				Продолжить;
			КонецПопытки;
			
			ЭлементКолонка = ТаблицаГрафика.Колонки.Получить(НомерКолонки-1);
			Если ЭлементКолонка = Неопределено Тогда
				СообщитьОбОшибкеПреобразования(НомерСтроки, НомерКолонки, "");
				Продолжить;
			КонецЕсли;
			
			Если ЭлементКолонка.ТипЗначения.СодержитТип(ТипДата) Тогда
				СтрокаГрафика[ЭлементКолонка.Имя] = ТекстВДату(ТекстЯчейки, НомерСтроки, НомерКолонки);
			ИначеЕсли ЭлементКолонка.ТипЗначения.СодержитТип(ТипСтрока) И Не ЭлементКолонка.ТипЗначения.СодержитТип(ТипЧисло) Тогда
				СтрокаГрафика[ЭлементКолонка.Имя] = ТекстЯчейки;
			Иначе
				СтрокаГрафика[ЭлементКолонка.Имя] = ТекстВЧисло(ТекстЯчейки, НомерСтроки, НомерКолонки);
			КонецЕсли; 

		КонецЦикла;
	
	КонецЦикла;
	
	Возврат ТаблицаГрафика;
	
КонецФункции

#КонецЕсли
