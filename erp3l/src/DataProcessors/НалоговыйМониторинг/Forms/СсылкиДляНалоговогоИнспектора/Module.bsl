

&НаКлиенте
Процедура ЗапросыУточняющихДокументов(Команда)
	ОткрытьФорму("Обработка.НалоговыйМониторинг.Форма.ЗапросыУточняющихДокументов");
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьФормуНаКлиенте()
	
	Для каждого Строка Из ТаблицаКонтрольныхПроцедур Цикл
		Если ЗначениеЗаполнено(Строка.Клиент) Тогда
			РезультатЗаголовок = "";
			РезультатСтатус = 0;
			Попытка
				Выполнить(Строка.Клиент);
			Исключение
				ЗаписатьВЖурналРегистрации(Строка.Клиент);
			КонецПопытки;
			Если РезультатЗаголовок <> "" ИЛИ РезультатСтатус <> 0 Тогда
				Строка.Статус = РезультатСтатус;
				Строка.Заголовок = РезультатЗаголовок;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьВЖурналРегистрации(Код)
	ЗаписьЖурналаРегистрации("ПанельСсылокНалоговогоИнспектора", УровеньЖурналаРегистрации.Ошибка, , Код);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьФорму()
	ПараметрыБазы = НалоговыйМониторинг.ПолучитьСтруктуруНастроекПоУмолчанию(ТекущаяДата(), ТекущийПользователь);
	ТекущаяОрганизация = ПараметрыБазы.Организация;
	НачалоПериода = ПараметрыБазы.НачалоПериода;
	ОкончаниеПериода = ПараметрыБазы.ОкончаниеПериода;
	Если ЗначениеЗаполнено(ТекущаяОрганизация) И ЗначениеЗаполнено(НачалоПериода) Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.НалоговыеКонтроли;
	Иначе
		Элементы.Страницы.ТекущаяСтраница = Элементы.НеЗаполненыНастройки;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасширенияПанелиНалоговогоМониторинга.Наименование КАК Наименование,
		|	РасширенияПанелиНалоговогоМониторинга.АлгоритмОпределенияСтатуса КАК Алгоритм,
		|	РасширенияПанелиНалоговогоМониторинга.АлгоритмРасшифровки КАК Расшифровка,
		|	РасширенияПанелиНалоговогоМониторинга.АлгоритмОпределенияСтатусаНаКлиенте КАК Клиент
		|ИЗ
		|	Справочник.РасширенияПанелиНалоговогоМониторинга КАК РасширенияПанелиНалоговогоМониторинга
		|ГДЕ
		|	РасширенияПанелиНалоговогоМониторинга.ПометкаУдаления = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	РасширенияПанелиНалоговогоМониторинга.Код";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ТаблицаКонтрольныхПроцедур.Загрузить(РезультатЗапроса.Выгрузить());
	КонецЕсли;
	
	Для каждого Строка Из ТаблицаКонтрольныхПроцедур Цикл
	
		РезультатЗаголовок = "";
		РезультатСтатус = 0;
		Выполнить(Строка.Алгоритм);
		Строка.Статус = РезультатСтатус;
		Строка.Заголовок = РезультатЗаголовок;
	
	КонецЦикла;
	Если ТекущаяОрганизация.ФайлЛоготип.Пустая() Тогда
		Элементы.АдресЛоготип.Видимость = Ложь;
	Иначе
		Элементы.АдресЛоготип.Видимость = Истина;
		АдресЛоготип = РаботаСФайлами.ДанныеФайла(ТекущаяОрганизация.ФайлЛоготип, УникальныйИдентификатор).СсылкаНаДвоичныеДанныеФайла
	КонецЕсли;
КонецПроцедуры
	
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СтруктураПроизволныхДанных = Новый Структура;
	ТекущийПользователь = ПараметрыСеанса.АвторизованныйПользователь;
	ЗаполнитьФорму();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьУставныеДокументы(Команда)
	ОткрытьФорму("Обработка.НалоговыйМониторинг.Форма.ОрганизацииПрисоединенныеФайлы", Новый Структура("Организация", ТекущаяОрганизация));
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЗаполнитьФормуНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНастройкиПоУмолчанию(Команда)
	ОткрытьФорму("РегистрСведений.РеквизитыПоУмолчаниюНалоговогоМониторинга.ФормаЗаписи", 
		Новый Структура("ЗначенияЗаполнения", Новый Структура("Пользователь", ТекущийПользователь)), ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	Если ТипЗнч(НовыйОбъект) = Тип("РегистрСведенийКлючЗаписи.РеквизитыПоУмолчаниюНалоговогоМониторинга") Тогда
		ЗаполнитьФорму();
		ЗаполнитьФормуНаКлиенте();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКонтрольныхПроцедурВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ДанныеСтроки = ТаблицаКонтрольныхПроцедур.НайтиПоИдентификатору(ВыбраннаяСтрока);
	Если ДанныеСтроки <> Неопределено Тогда
		Выполнить(ДанныеСтроки.Расшифровка);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКонтроли(Команда)
	ЗаполнитьФорму();
	ЗаполнитьФормуНаКлиенте();
КонецПроцедуры
