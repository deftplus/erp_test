
// Выставляет представление элементов в узле дереве УзелВход. Параметр ГлубинаРекурсииВход
// защищает от бесконечной рекурсии.
&НаСервере
Процедура ПроставитьПредставленияЭлементов(УзелВход, ГлубинаРекурсииВход = 0)
	Если ГлубинаРекурсииВход > 9999 Тогда
		Возврат;
	КонецЕсли;
	Для Каждого ТекСтроки Из УзелВход.Строки Цикл
		ТекСтроки.ПредставлениеЭлемента = Строка(ТекСтроки.ЭлементАдресации);
		ПроставитьПредставленияЭлементов(ТекСтроки, ГлубинаРекурсииВход + 1);
	КонецЦикла;	
КонецПроцедуры		// ПроставитьПредставленияЭлементов()

// Передаёт родител формы выбранный элемент адресации.
&НаКлиенте
Процедура ПередатьВыбранныйЭлементИЗакрытьФорму()
	ТекДанные = Элементы.ДеревоИерархии.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		ВыбранныйЭлемент = ТекДанные.ЭлементАдресации;
		Закрыть(ВыбранныйЭлемент);
	Иначе
		// Строка не выбрана. Ничего не делаем.
	КонецЕсли;
КонецПроцедуры		// ВыбратьЭлементИЗакрытьФорму()

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// Считывание параметров.
	Если ЭтоАдресВременногоХранилища(Параметры.АдресДеревоИерархии) Тогда
		ДеревоИерархииЗначение = ПолучитьИзВременногоХранилища(Параметры.АдресДеревоИерархии);
		ДеревоИерархииЗначение.Колонки.Добавить("ПредставлениеЭлемента");
		ПроставитьПредставленияЭлементов(ДеревоИерархииЗначение);
		НоваяСтрока = ДеревоИерархииЗначение.Строки.Вставить(0);		// Вставка на первую позицию.
		ПредставлениеКореньДерева = РеквизитФормыВЗначение("Объект").ПредставлениеКореньДерева();
		НоваяСтрока.ЭлементАдресации = ПредставлениеКореньДерева;
		НоваяСтрока.ПредставлениеЭлемента = НСтр("ru = '<Корень Дерева>'");
		ЗначениеВРеквизитФормы(ДеревоИерархииЗначение, "ДеревоИерархии");
	Иначе
		ДеревоИерархии.ПолучитьЭлементы().Очистить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЭлемент(Команда)
	ПередатьВыбранныйЭлементИЗакрытьФорму();
КонецПроцедуры

&НаКлиенте
Процедура ДеревоИерархииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	 ПередатьВыбранныйЭлементИЗакрытьФорму();
КонецПроцедуры
