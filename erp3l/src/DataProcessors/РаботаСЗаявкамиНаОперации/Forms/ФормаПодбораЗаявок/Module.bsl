&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Учет особенностей решений
	ТекстЗапроса = Список.ТекстЗапроса;
	Если ИдентификацияПродуктаУХКлиентСервер.ЭтоЕХ() Тогда
		ТекстЗаменыДоговора = "РазмещениеЗаявок.ЗаявкаНаОперацию.Договор";
		ТекстЗаментыФормыОплатыЗаявки = "ВЫРАЗИТЬ(РазмещениеЗаявок.ЗаявкаНаОперацию КАК Документ.ЗаявкаНаРасходованиеДенежныхСредств).ФормаОплатыЗаявки";
	Иначе
		ТекстЗаменыДоговора = "РазмещениеЗаявок.ЗаявкаНаОперацию.ДоговорКонтрагента";
		ТекстЗаментыФормыОплатыЗаявки = "ВЫРАЗИТЬ(РазмещениеЗаявок.ЗаявкаНаОперацию КАК Документ.ЗаявкаНаРасходованиеДенежныхСредств).ФормаОплаты";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДоговорКонтрагента", ТекстЗаменыДоговора);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ФормаОплатыЗаявки", ТекстЗаментыФормыОплатыЗаявки);
	Список.ТекстЗапроса = ТекстЗапроса;
		
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "ВводНаОсновании, ИмяОбъектаМетаданныхСоздаваемогоДокумента");
	
	УправлениеФормойУХ.УстановитьПредставлениеОтбора(ЭтотОбъект,, Метаданные.Документы.ЗаявкаНаРасходованиеДенежныхСредств);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ВводНаОсновании Тогда
		ДополнительныеПараметры = Новый Структура("ИмяОбъектаМетаданных", ИмяОбъектаМетаданныхСоздаваемогоДокумента);
		ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ВыборПозицииФормированиеПлатежногоПоручения", ОперативноеПланированиеФормыУХКлиент, ДополнительныеПараметры);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекДанные = Элементы.Список.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВводНаОсновании Тогда
		
		Результат = Новый Структура("ВводНаОснованииПлатежнойПозиции, Основание", Истина, ТекДанные.ИдентификаторПозиции);
		Закрыть(Результат);
		
	Иначе
		
		Результат = Новый Структура("ДокументПланирования, ИдентификаторПозиции, СуммаПлатежа, ДоговорКонтрагента");
		Результат.ДокументПланирования			 = ТекДанные.ЗаявкаНаОперацию;
		Результат.СуммаПлатежа					 = ТекДанные.Сумма;
		Результат.ДоговорКонтрагента			 = ТекДанные.ДоговорКонтрагента;
		Результат.ИдентификаторПозиции			 = ТекДанные.ИдентификаторПозиции;
		
		ОповеститьОВыборе(Результат);
	КонецЕсли;
	
КонецПроцедуры