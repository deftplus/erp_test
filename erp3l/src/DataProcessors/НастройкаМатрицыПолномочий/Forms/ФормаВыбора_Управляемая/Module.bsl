
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ГрупповоеИзменение", ГрупповоеИзменение);
	Элементы.ТабличноеПолеНастроекИспользование.Видимость = ГрупповоеИзменение;
	
	НоваяСтрока = ТабличноеПолеНастроек.Добавить();
	
	НоваяСтрока.Наименование = "Исполняющий";
	Параметры.Свойство("Исполняющий", НоваяСтрока.Пользователь);
	Параметры.Свойство("Исполняющий_Организация", НоваяСтрока.Организация);
	НоваяСтрока.Использование_Организация = ТипЗнч(Параметры.Исполняющий) = Тип("СправочникСсылка.РолиКонтактныхЛиц");
	
	НоваяСтрока = ТабличноеПолеНастроек.Добавить();
	
	НоваяСтрока.Наименование = "Согласование";
	Параметры.Свойство("Согласование", НоваяСтрока.Пользователь);
	Параметры.Свойство("Согласование_Организация", НоваяСтрока.Организация);
	НоваяСтрока.Использование_Организация = ТипЗнч(Параметры.Согласование) = Тип("СправочникСсылка.РолиКонтактныхЛиц");
	
КонецПроцедуры

&НаКлиенте
Процедура ПользовательВыбор_Завершение(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	Если ВыбранноеЗначение <> Неопределено Тогда
		ДополнительныеПараметры.ТекущаяСтрока.Пользователь = ВыбранноеЗначение;
	Иначе
		// Пользователь отказался от выбора. Не изменяем значение строки.
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Пользователь_ПослеВыбораИзМеню(ВыбранныйЭлемент, Параметры) Экспорт
	Если ВыбранныйЭлемент = Неопределено Тогда
		// Пользователь отказался. Не выбираем.
	ИначеЕсли ВыбранныйЭлемент.Значение.СодержитТип(Тип("СправочникСсылка.Пользователи")) Тогда
		ТекЗначение								 = Параметры.ТекущаяСтрока.Пользователь;
		Параметры.ТекущаяСтрока.Пользователь	 = ВыбранныйЭлемент.Значение.ПривестиЗначение(ТекЗначение);
		Параметры.Элемент.ОграничениеТипа		 = ВыбранныйЭлемент.Значение;
		ОписаниеОЗакрытии = Новый ОписаниеОповещения("ПользовательВыбор_Завершение", ЭтаФорма, Параметры);
		ОткрытьФорму("Справочник.Пользователи.Форма.ФормаВыбора", , , , , , ОписаниеОЗакрытии);
	ИначеЕсли ВыбранныйЭлемент.Значение.СодержитТип(Тип("СправочникСсылка.РолиКонтактныхЛиц")) Тогда
		ТекЗначение								 = Параметры.ТекущаяСтрока.Пользователь;
		Параметры.ТекущаяСтрока.Пользователь	 = ВыбранныйЭлемент.Значение.ПривестиЗначение(ТекЗначение);
		Параметры.Элемент.ОграничениеТипа		 = ВыбранныйЭлемент.Значение;
		ОписаниеОЗакрытии = Новый ОписаниеОповещения("ПользовательВыбор_Завершение", ЭтаФорма, Параметры);
		ОткрытьФорму("Справочник.РолиКонтактныхЛиц.ФормаВыбора", , , , , , ОписаниеОЗакрытии);
	Иначе
		ТекстСообщения = НСтр("ru = 'Выбрано неизвестное значение: %Значение%. Операция отменена.'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Значение%", Строка(ВыбранныйЭлемент));
		ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстСообщения);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТабличноеПолеНастроекПользовательНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущаяСтрока = ТабличноеПолеНастроек.НайтиПоИдентификатору(Элементы.ТабличноеПолеНастроек.ТекущаяСтрока);
	
	Если ГрупповоеИзменение Тогда
		ТекущаяСтрока.Использование = Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.Пользователь) Тогда
		
		СписокЗначений = Новый СписокЗначений;
		СписокЗначений.Добавить(Новый ОписаниеТипов("СправочникСсылка.Пользователи"), "Пользователь");
		СписокЗначений.Добавить(Новый ОписаниеТипов("СправочникСсылка.РолиКонтактныхЛиц"), "Роль");
		
		Если ТекущаяСтрока.Наименование = "Согласование" Тогда	
			СписокЗначений.Добавить(Новый ОписаниеТипов("СправочникСсылка.ШаблоныУниверсальныхПроцессов"), "Маршрут согласования");
		КонецЕсли;
		
		СтандартнаяОбработка = Ложь;		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("ТекущаяСтрока", ТекущаяСтрока);
		СтруктураПараметров.Вставить("Элемент", Элемент);
		Оповещение = Новый ОписаниеОповещения("Пользователь_ПослеВыбораИзМеню", ЭтаФорма, СтруктураПараметров);
		ПоказатьВыборИзМеню(Оповещение, СписокЗначений);
		
	Иначе
		СтандартнаяОбработка = Истина;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТабличноеПолеНастроекПользовательПриИзменении(Элемент)
	
	ТекущаяСтрока = ТабличноеПолеНастроек.НайтиПоИдентификатору(Элементы.ТабличноеПолеНастроек.ТекущаяСтрока);
	ТекущаяСтрока.Использование_Организация = ТипЗнч(ТекущаяСтрока.Пользователь) = Тип("СправочникСсылка.РолиКонтактныхЛиц");
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	СтруктураОтвета = Новый Структура("ИспользоватьИсполняющий, Исполняющий, Исполняющий_Организация, ИспользоватьСогласование, Согласование, Согласование_Организация"
									  , ТабличноеПолеНастроек[0].Использование
									  , ТабличноеПолеНастроек[0].Пользователь
									  , ТабличноеПолеНастроек[0].Организация
									  , ТабличноеПолеНастроек[1].Использование
									  , ТабличноеПолеНастроек[1].Пользователь
									  , ТабличноеПолеНастроек[1].Организация);
	Закрыть(Новый ФиксированнаяСтруктура(СтруктураОтвета));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

