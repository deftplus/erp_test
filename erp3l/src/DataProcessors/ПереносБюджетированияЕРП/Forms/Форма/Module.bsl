
&НаКлиенте
Процедура ПродолжитьЦФО(Команда)
	
	Для Каждого Строка ИЗ ТаблицаПодразделенияЦФО Цикл
		
		Если Не ЗначениеЗаполнено(Строка.ЦФО) Тогда
			
			ПоказатьПредупреждение(,Нстр("ru = 'Должны быть указаны ЦФО для всех подразделений'"));
			
			Возврат;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПродолжитьЦФОСервер();
	
КонецПроцедуры

&НаСервере
Процедура ПродолжитьЦФОСервер()
	
	Для Каждого Строка ИЗ ТаблицаПодразделенияЦФО Цикл
		
		ОбъектПодразделение= Строка.Подразделение.ПолучитьОбъект();
		ОбъектПодразделение.ЦФО=Строка.ЦФО;
		
		Попытка
			
			ОбъектПодразделение.ОбменДанными.Загрузка=Истина;
			ОбъектПодразделение.Записать();
			
		Исключение
			
			ТекстОшибки=СтрШаблон(Нстр("ru = 'Не удалось заполнить реквизит ЦФО для подразделения %1: %2'"),ОбъектПодразделение,ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстОшибки,,,СтатусСообщения.Внимание);
			
			Возврат;
			
		КонецПопытки;
		
	КонецЦикла;
	
	ВыполнитьПереносДанных();
	
КонецПроцедуры // ПродолжитьЦФОСервер()

Процедура ВыполнитьПереносДанных()
	
	ОбработкаОбъект=РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ВыполнитьПереносДанных();
	
КонецПроцедуры // ВыполнитьПереносДанных() 

&НаСервере
Процедура ЗаполнитьПоОборотамБюджетовНаСервере()
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	|	ТаблицаПодразделенияЦФО.Подразделение КАК Подразделение,
	|	ТаблицаПодразделенияЦФО.ЦФО КАК ЦФО
	|ПОМЕСТИТЬ ТаблицаПодразделенияЦФО
	|ИЗ
	|	&ТаблицаПодразделенияЦФО КАК ТаблицаПодразделенияЦФО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ОборотыБюджетов.Организация) КАК Организация,
	|	ОборотыБюджетов.Подразделение КАК Подразделение
	|ПОМЕСТИТЬ ПоОборотам
	|ИЗ
	|	РегистрНакопления.ОборотыБюджетов КАК ОборотыБюджетов
	|
	|СГРУППИРОВАТЬ ПО
	|	ОборотыБюджетов.Подразделение
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ОборотыБюджетов.Организация) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ТаблицаПодразделенияЦФО.ЦФО = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ПоОборотам.Организация
	|		ИНАЧЕ ТаблицаПодразделенияЦФО.ЦФО
	|	КОНЕЦ КАК ЦФО,
	|	ТаблицаПодразделенияЦФО.Подразделение КАК Подразделение
	|ИЗ
	|	ТаблицаПодразделенияЦФО КАК ТаблицаПодразделенияЦФО
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПоОборотам КАК ПоОборотам
	|		ПО ТаблицаПодразделенияЦФО.Подразделение = ПоОборотам.Подразделение
	|
	|УПОРЯДОЧИТЬ ПО
	|	Подразделение";
	
	Запрос.УстановитьПараметр("ТаблицаПодразделенияЦФО",РеквизитФормыВЗначение("ТаблицаПодразделенияЦФО"));
	ЗначениеВРеквизитФормы(Запрос.Выполнить().Выгрузить(),"ТаблицаПодразделенияЦФО");
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыполнитьПереносДанных(Команда)
	
	ПроверитьВыполнитьПереносДанныхНаСервере();	
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьВыполнитьПереносДанныхНаСервере()
		
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОборотыБюджетов.Подразделение КАК Подразделение,
	|	ОборотыБюджетов.Подразделение.ЦФО КАК ЦФО
	|ИЗ
	|	РегистрНакопления.ОборотыБюджетов КАК ОборотыБюджетов
	|ГДЕ
	|	ОборотыБюджетов.Подразделение.ЦФО = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И НЕ ОборотыБюджетов.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ФактическиеДанныеБюджетирования.Подразделение,
	|	ФактическиеДанныеБюджетирования.Подразделение.ЦФО
	|ИЗ
	|	РегистрНакопления.ФактическиеДанныеБюджетирования КАК ФактическиеДанныеБюджетирования
	|ГДЕ
	|	ФактическиеДанныеБюджетирования.Подразделение.ЦФО = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И НЕ ФактическиеДанныеБюджетирования.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)";
	
	ЗначениеВРеквизитФормы(Запрос.Выполнить().Выгрузить(),"ТаблицаПодразделенияЦФО");
	
	Если ТаблицаПодразделенияЦФО.Количество()=0 Тогда
		
		ВыполнитьПереносДанных();
		
	Иначе
		
		Элементы.ГруппаЗаполнениеЦФО.Видимость=Истина;
		Возврат;
		
	КонецЕсли;
		
КонецПроцедуры // ПроверитьВыполнитьПереносДанныхНаСервере()

&НаСервереБезКонтекста
Процедура ПередЗакрытиемНаСервере()
	//Константы.ИспользоватьБюджетирование.Установить(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	ПередЗакрытиемНаСервере();
КонецПроцедуры
 

