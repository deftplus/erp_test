
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция ПолучитьЗначениеКэша(ЗначениеСтрокой, Кэш, отЗначения) Экспорт

	Если Не ЗначениеЗаполнено(ЗначениеСтрокой) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(отЗначения) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТипСтрокой = Кэш.ВидыСубконто.Получить(отЗначения);
	Если ТипСтрокой = Неопределено Тогда
		ТипПВХ = Тип("ПланВидовХарактеристикСсылка.ВидыСубконтоКорпоративные");
		ТекущийОТ = ?(ТипЗнч(отЗначения) = ТипПВХ, отЗначения.ТипЗначения, отЗначения);
		ТипСтрокой = Метаданные.НайтиПоТипу(ТекущийОТ.Типы().Получить(0)).ПолноеИмя();
		Кэш.ВидыСубконто.Вставить(отЗначения, ТипСтрокой);
	КонецЕсли;

	Результат = Кэш.ЗначенияСубконто.Получить(ТипСтрокой + ЗначениеСтрокой);
	Если Результат <> Неопределено Тогда
		Возврат Результат;	
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	т.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК т
	|ГДЕ
	|	т.Наименование = &Поиск");    
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "Справочник.Номенклатура", ТипСтрокой);
	Если СтрНачинаетсяС(ТипСтрокой, "ПланСчетов") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ".Наименование", ".Код");
	КонецЕсли;
	Запрос.УстановитьПараметр("Поиск", ЗначениеСтрокой);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Сообщить(СтрШаблон("Не найден по наименованию <%1> в <%2>", ЗначениеСтрокой, ТипСтрокой)); 
		Кэш.ЗначенияСубконто.Вставить(ТипСтрокой + ЗначениеСтрокой, Неопределено);
		Возврат Неопределено;
	КонецЕсли;
	
	РезультатЗначение = РезультатЗапроса.Выгрузить().Получить(0).Получить(0);
	Кэш.ЗначенияСубконто.Вставить(ТипСтрокой + ЗначениеСтрокой, РезультатЗначение);
	
	Возврат РезультатЗначение;
	
КонецФункции
	
#КонецЕсли
