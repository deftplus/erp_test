&НаСервере
Функция ПровестиДокумент(ПараметрКоманды)
	
	ДокументОбъект = ПараметрКоманды.ПолучитьОбъект();
	Попытка
		Если НЕ ДокументОбъект.ПроверитьЗаполнение() Тогда 
			Возврат Ложь;
		КонецЕсли;
		
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;

КонецФункции

&НаСервере
Функция ПолучитьПараметрыДокумента(Ссылка)
	
	МетаданныеДокумента	= Ссылка.Метаданные();
	
	ИменаРеквизитов	= "Проведен, ПометкаУдаления";
	
	Результат = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, ИменаРеквизитов);
	Если Не Результат.Проведен Тогда
		Если Метаданные.НайтиПоТипу(ТипЗнч(Ссылка)).Проведение 
			= Метаданные.СвойстваОбъектов.Проведение.Запретить Тогда
			Результат.Вставить("Проведен", Истина);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПараметрыДокумента = ПолучитьПараметрыДокумента(ПараметрКоманды);
	
	Если НЕ ПараметрыДокумента.Проведен Тогда
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Вставить(0, КодВозвратаДиалога.Да, "Провести");
		Кнопки.Вставить(1, КодВозвратаДиалога.Отмена, "Отмена");
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ПараметрыВыполненияКоманды", ПараметрыВыполненияКоманды);
		ДополнительныеПараметры.Вставить("ПараметрКоманды", ПараметрКоманды);
		
		Оповещение = Новый ОписаниеОповещения("ВопросПередПросмотромСледуетПровестиЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Перед просмотром проводок документ следует провести'"), Кнопки,, КодВозвратаДиалога.Да);
	Иначе
		
		Если ОткрытьФормуБП(ПараметрКоманды, ПараметрыВыполненияКоманды) Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура("ДокументСсылка", ПараметрКоманды);
		ОткрытьФорму("Обработка.ДвиженияДокумента.Форма", 
			ПараметрыФормы, 
			ПараметрыВыполненияКоманды.Источник, 
			ПараметрКоманды);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередПросмотромСледуетПровестиЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрКоманды = ДополнительныеПараметры.ПараметрКоманды;
	ПараметрыВыполненияКоманды = ДополнительныеПараметры.ПараметрыВыполненияКоманды;
	
	ДокументПроведен = ПровестиДокумент(ПараметрКоманды);
	Если ДокументПроведен Тогда
		ОповеститьОбИзменении(ПараметрКоманды);
		Оповестить("ВыполненаЗаписьДокумента", Новый Структура("ДокументСсылка", ПараметрКоманды));
	Иначе
		Сообщить(НСтр("ru = 'Не удалось провести документ'"), СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;

	Если ОткрытьФормуБП(ПараметрКоманды, ПараметрыВыполненияКоманды) Тогда
		Возврат;
	КонецЕсли;
		
	ПараметрыФормы = Новый Структура("ДокументСсылка", ПараметрКоманды);
	ОткрытьФорму("Обработка.ДвиженияДокумента.Форма", 
		ПараметрыФормы, 
		ПараметрыВыполненияКоманды.Источник, 
		ПараметрКоманды);

КонецПроцедуры	

&НаКлиенте
Функция ОткрытьФормуБП(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	//Если ТипЗнч(ПараметрКоманды) <> Тип("ДокументСсылка.НачисленияПоФинансовымИнструментам") Тогда
	Если Истина Тогда
		Возврат Ложь;	
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ДокументДвижений", ПараметрКоманды);
	ИмяФормы = "Обработка.КорректировкаДвижений.Форма";
	ОткрытьФорму(ИмяФормы, 
		ПараметрыФормы, 
		ПараметрыВыполненияКоманды.Источник, 
		ПараметрКоманды);
		
	Возврат Истина;

КонецФункции


