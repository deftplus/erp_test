
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Пользователи.ЭтоПолноправныйПользователь() Тогда
		СтандартнаяОбработка = Ложь;
		Отказ = Истина;
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = "Групповое изменение доступно только пользователю с полными правами";
		СообщениеПользователю.Сообщить();
	КонецЕсли;
	
	Если ТипЗнч(Параметры.МассивОтчетов) <> Тип("Массив") Тогда
		СтандартнаяОбработка = Ложь;
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьИЗакрыть(Команда)
	
	Если Объект.Заблокировать = 0 И Объект.СостояниеОтчета.Пустая() Тогда
		ОбщегоНазначенияУХ.СообщитьОбОшибке("Не установлено состояние отчета или признак блокировки.");
		Возврат;
	КонецЕсли;
		
	ИзменитьСостояниеОтчетов(Новый ФиксированныйМассив(Параметры.МассивОтчетов), Объект.СостояниеОтчета, Объект.Заблокировать);
		
	ОповеститьОбИзменении(Тип("ДокументСсылка.НастраиваемыйОтчет"));
	
	Закрыть();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИзменитьСостояниеОтчетов(Знач МассивОтчетов, Знач СостояниеОтчета, Знач Заблокировать)
	
	ДокументыНеВноситьИзменения = Новый Соответствие;
	
	Для Каждого СсылкаНаОтчет ИЗ МассивОтчетов Цикл
		
		ЗаписыватьОбъект=Ложь;
		
		ТекОбъект = СсылкаНаОтчет.ПолучитьОбъект();
		
		Если ТипЗнч(ТекОбъект) = Тип("ДокументОбъект.НастраиваемыйОтчет") Тогда
		
			ТекОбъект.ПроверкаПриЗаписи = Истина;
		
			ТекОбъект.ФормированиеДвиженийПриЗаписи=Ложь;
		
		КонецЕсли;
		
		МетаданныеОбъекта = ТекОбъект.Метаданные();
		ЕстьРеквизитНеВноситьИзменения = ДокументыНеВноситьИзменения.Получить(МетаданныеОбъекта.Имя);
		Если ЕстьРеквизитНеВноситьИзменения = Неопределено Тогда
			ЕстьРеквизитНеВноситьИзменения = ОбщегоНазначенияУХ.ЕстьРеквизитОбъектаМД("НеВноситьИзменения", МетаданныеОбъекта);
			ДокументыНеВноситьИзменения.Вставить(МетаданныеОбъекта.Имя, ЕстьРеквизитНеВноситьИзменения);
		КонецЕсли;
		
		Если ЕстьРеквизитНеВноситьИзменения Тогда
	
			Если Заблокировать=1 Тогда
				
				Если НЕ ТекОбъект.НеВноситьИзменения Тогда
					
					ТекОбъект.НеВноситьИзменения = Истина;
					ЗаписыватьОбъект=Истина;
					
				КонецЕсли;
				
			ИначеЕсли Заблокировать=2 Тогда
				
				Если ТекОбъект.НеВноситьИзменения Тогда
					
					ТекОбъект.НеВноситьИзменения = Ложь;	
					ЗаписыватьОбъект=Истина;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СостояниеОтчета) И (НЕ ТекОбъект.Состояние = СостояниеОтчета)  Тогда
			
			РасширениеПроцессыИСогласованиеУХ.ИзменитьСостояниеСогласованияОбъекта(ТекОбъект, СостояниеОтчета);
			ЗаписыватьОбъект=Истина;
			
		КонецЕсли;
		
		Если ЗаписыватьОбъект Тогда
			
			Попытка
				ТекОБъект.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				ОбщегоНазначенияУХ.СообщитьОбОшибке("Ошибка сохранения документа<" + ТекОбъект.ВидОтчета + "><" + ТекОбъект.Организация + "><" + ТекОбъект.ПериодОтчета + "><" + ТекОбъект.Сценарий + ">.");
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура СостояниеОтчетаПриИзменении(Элемент)
	
	Если Объект.СостояниеОтчета = ПредопределенноеЗначение("Перечисление.СостоянияОтчетов.Утвержден") Тогда
		Объект.Заблокировать = 1;
	КонецЕсли;
	
	ВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидимостьДоступность()
	
	Элементы.Заблокировать.Доступность = НЕ Объект.СостояниеОтчета = ПредопределенноеЗначение("Перечисление.СостоянияОтчетов.Утвержден");
	
КонецПроцедуры
