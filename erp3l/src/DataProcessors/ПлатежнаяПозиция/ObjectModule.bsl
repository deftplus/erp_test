#Область СлужебныеПроцедурыИФункции
Процедура ИнициализироватьКомпоновщикНастроек()
	
	// Инициализируем компоновщик настроек.
	СхемаКомпоновкиДанных 		= ПолучитьМакет("ОстаткиНаСчетахПереопределяемый");
	
	// Учет ареала функционирования
	ТекстЗапроса = СхемаКомпоновкиДанных.НаборыДанных.НаборДанных1.Запрос;
	
	// Разные наименования справочников БанковскиеСчета и БанковскиеСчетаОрганизаций
	ТекстДляЗамены = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК Валюта,
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	неопределено КАК БанковскийСчет
	|ПОМЕСТИТЬ ВТ_БанковскиеСчетаОрганизаций";
	
	Если ИдентификацияПродуктаУХКлиентСервер.ЭтоЕХ() Тогда
		
		ТекстЗамены = 
		"ВЫБРАТЬ
		|	БанковскиеСчета.ВалютаДенежныхСредств КАК Валюта,
		|	БанковскиеСчета.Владелец КАК Организация,
		|	БанковскиеСчета.Ссылка КАК БанковскийСчет
		|ПОМЕСТИТЬ ВТ_БанковскиеСчетаОрганизаций
		|ИЗ
		|	Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчета
		|ГДЕ
		|	НЕ БанковскиеСчета.ПометкаУдаления";
		
	Иначе
		
		ТекстЗамены = 
		"ВЫБРАТЬ
		|	БанковскиеСчета.ВалютаДенежныхСредств КАК Валюта,
		|	БанковскиеСчета.Владелец КАК Организация,
		|	БанковскиеСчета.Ссылка КАК БанковскийСчет
		|ПОМЕСТИТЬ ВТ_БанковскиеСчетаОрганизаций
		|ИЗ
		|	Справочник.БанковскиеСчета КАК БанковскиеСчета
		|ГДЕ
		|	БанковскиеСчета.Владелец ССЫЛКА Справочник.Организации
		|	И НЕ БанковскиеСчета.ПометкаУдаления";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ТекстДляЗамены, ТекстЗамены);
	
	СхемаКомпоновкиДанных.НаборыДанных.НаборДанных1.Запрос = ТекстЗапроса;
	
	//
	АдресСхемыКомпоновкиДанных 	= ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, Новый УникальныйИдентификатор);
	
	// Создадим источник.
	ИсточникДоступныхНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных);
	
	КомпоновщикНастроек.Инициализировать(ИсточникДоступныхНастроек);
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
КонецПроцедуры

#КонецОбласти

ИнициализироватьКомпоновщикНастроек();


