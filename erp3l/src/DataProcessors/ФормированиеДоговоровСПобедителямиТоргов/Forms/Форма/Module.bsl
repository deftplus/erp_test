
#Область ОбработкаОсновныхСобытийФормы


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьИмяВладельцаДоговораВЗапросеПобедителей();
	мОшибки = Неопределено;
	ЗаполнитьРеквизитыФормыИзПараметров(Параметры, мОшибки);
	Если мОшибки <> Неопределено И мОшибки.Количество() > 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(
			мОшибки, Отказ);
		Возврат;
	КонецЕсли;
	ОбновитьФлагиПротоколаВыбора();
	УстановитьОтборДокументовВыбора();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьОформлениеФормыПоПротоколу();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ЗаписанПротоколВыбораПобедителейЛота" 
			И ТипЗнч(Параметр) = Тип("Структура")
			И Параметр.ПротоколВыбораПобедителей = ПротоколВыбораПобедителей Тогда
		Элементы.ПобедившиеПредложенияПоставщиков.Обновить();
	ИначеЕсли ЦентрализованныеЗакупкиКлиентСерверУХ.ЭтоИмяСобытияСогласования(
															ИмяСобытия) Тогда
		ОбновитьФлагиПротоколаВыбора();
	КонецЕсли;
КонецПроцедуры


#КонецОбласти


#Область ОбработкаСобытийЭлементовФормы


&НаКлиенте
Процедура СоздатьДоговорПоВыбранномуПредложениюПоставщика(Команда)
	Если Модифицированность Тогда 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Протокол выбора модифицирован, "
			+ "необходимо его перепровести!'");
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	ТекДанные = Элементы.ПобедившиеПредложенияПоставщиков.ТекущиеДанные;
	Если ТекДанные <> Неопределено 
			И НЕ ЗначениеЗаполнено(ТекДанные.Договор) Тогда
		ОткрытьНовыйДоговорПоПредложениюПоставщика(
			ТекДанные.ПредложениеПоставщика);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПобедившиеПредложенияПоставщиковПриАктивизацииСтроки(Элемент)
	УстановитьДоступностьКнопкиСоздатьДоговор();
КонецПроцедуры

&НаКлиенте
Процедура ПобедившиеПредложенияПоставщиковВыбор(Элемент,
												ВыбраннаяСтрока,
												Поле,
												СтандартнаяОбработка)
	ТекДанные = Элементы.ПобедившиеПредложенияПоставщиков.ТекущиеДанные;
	СмещениеВИмениЭлемента =
		СтрДлина(Элементы.ПобедившиеПредложенияПоставщиков.Имя) + 1;
	Если ТекДанные <> Неопределено
		И (Поле.Имя = "ПобедившиеПредложенияПоставщиковЛот"
			ИЛИ Поле.Имя = "ПобедившиеПредложенияПоставщиковПоставщик"
			ИЛИ Поле.Имя = "ПобедившиеПредложенияПоставщиковПредложениеПоставщика"
			ИЛИ Поле.Имя = "ПобедившиеПредложенияПоставщиковДоговор") Тогда
		ЗначениеПоля = ТекДанные[Сред(Поле.Имя, СмещениеВИмениЭлемента)];
		Если ЗначениеЗаполнено(ЗначениеПоля) Тогда
			СтандартнаяОбработка = Ложь;
			ПоказатьЗначение(, ЗначениеПоля);
		ИначеЕсли флМожноСоздаватьДоговоры
			И МожноСоздатьДоговорДляПредложения(ТекДанные) Тогда
			СтандартнаяОбработка = Ложь;
			ОткрытьНовыйДоговорПоПредложениюПоставщика(
				ТекДанные.ПредложениеПоставщика);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыНаКлиенте

&НаКлиенте
Процедура УстановитьОформлениеФормыПоПротоколу()
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКнопкиСоздатьДоговор()
	ТекДанные = Элементы.ПобедившиеПредложенияПоставщиков.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		ДоступноСозданиеДоговора = ((флМожноСоздаватьДоговоры) И (МожноСоздатьДоговорДляПредложения(ТекДанные)));
		Элементы.СоздатьДоговорПоВыбранномуПредложениюПоставщика.Доступность = ДоступноСозданиеДоговора;
	Иначе
		Элементы.СоздатьДоговорПоВыбранномуПредложениюПоставщика.Доступность = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция МожноСоздатьДоговорДляПредложения(ДанныеСтрокиПобедителя)
	Если ДанныеСтрокиПобедителя <> Неопределено Тогда
		Возврат флМожноСоздаватьДоговоры
			//И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(
			//		ДанныеСтрокиПобедителя, "Договор") 
			И НЕ ЗначениеЗаполнено(ДанныеСтрокиПобедителя.Договор);
	КонецЕсли;
	Возврат Ложь;
КонецФункции

&НаКлиенте
Процедура ОткрытьНовыйДоговорПоПредложениюПоставщика(ПредложениеПоставщика)
	ОписаниеОповещенияОЗакрытии = 
		Новый ОписаниеОповещения("ОбработкаЗакрытияФормыСозданияДоговора", ЭтаФорма);
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ДокументОснование", ПредложениеПоставщика);
	ЗначенияЗаполнения.Вставить("ВидДоговораУХ", ПредопределенноеЗначение(
		"Справочник.ВидыДоговоровКонтрагентовУХ.СПоставщиком"));
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ОткрытьФорму(
		"Документ.ВерсияСоглашенияКоммерческийДоговор.ФормаОбъекта", 
		ПараметрыФормы, 
		ЭтаФорма,
		,
		,
		,
		ОписаниеОповещенияОЗакрытии);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗакрытияФормыСозданияДоговора(Результат, ДопПараметры) Экспорт
	Элементы.ПобедившиеПредложенияПоставщиков.Обновить();
	УстановитьДоступностьКнопкиСоздатьДоговор();
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыНаСервере


&НаСервере
Процедура ЗаполнитьРеквизитыФормыИзПараметров(Параметры, Ошибки)
	ПротоколВыбораПобедителей = Параметры.ПротоколВыбораПобедителей;
	Если НЕ ЗначениеЗаполнено(ПротоколВыбораПобедителей) Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
			Ошибки,, НСтр("ru='Не указан протокол выбора победителей'"));
		Возврат;
	КонецЕсли;
	ЗакупочнаяПроцедура = ПротоколВыбораПобедителей.ЗакупочнаяПроцедура;
	Если НЕ ЗначениеЗаполнено(ЗакупочнаяПроцедура) Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
			Ошибки,, НСтр("ru='В протоколе выбора победителей не указана закупочная процедура'"));
		Возврат;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьФлагиПротоколаВыбора()
	флМожноСоздаватьДоговоры = ПротоколВыбораПобедителей.Проведен;
	СтатусПротокола = ?(флМожноСоздаватьДоговоры, 2, 1);
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборДокументовВыбора()
	ПобедившиеПредложенияПоставщиков.Параметры.УстановитьЗначениеПараметра(
		"ПротоколВыбораПобедителей",
		ПротоколВыбораПобедителей);
КонецПроцедуры

&НаСервере
Процедура УстановитьИмяВладельцаДоговораВЗапросеПобедителей()
	ПутьКВладельцу = "ДоговорыПоЗакупочнымПроцедурамСрезПоследних.Договор."
		+ ЦентрализованныеЗакупкиУХ.ПолучитьИмяВладельцаДоговора();
	ПобедившиеПредложенияПоставщиков.ТекстЗапроса =
		СтрЗаменить(ПобедившиеПредложенияПоставщиков.ТекстЗапроса,
			"&ДоговорыПоЗакупочнымПроцедурамСрезПоследнихДоговорВладелец",
			ПутьКВладельцу);
КонецПроцедуры

#КонецОбласти
