
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("СравниватьТолькоЗначения", Объект.СравниватьТолькоЗначения);
	Параметры.Свойство("СравниватьПоШаблону", Объект.СравниватьПоШаблону);
	Параметры.Свойство("ОписаниеДокумента1", Объект.ОписаниеДокумента1);
	Параметры.Свойство("ОписаниеДокумента2", Объект.ОписаниеДокумента2);
	
	Если Параметры.Свойство("СвойстваИсключения") Тогда
		Для Каждого Свойство Из Параметры.СвойстваИсключения Цикл
			НСтрока = Объект.СвойстваИсключения.Добавить();
			НСтрока.Свойство = Свойство;
		КонецЦикла;
	КонецЕсли;
	
	Если Параметры.Свойство("СравнитьДокументы") Тогда
		
		Документ1 = Неопределено;
		Документ2 = Неопределено;
		
		Если Параметры.Свойство("Документ1", Документ1)
			И Параметры.Свойство("Документ2", Документ2) Тогда
			
			Если ТипЗнч(Документ1) = Тип("ТабличныйДокумент") 
				И ТипЗнч(Документ2) = Тип("ТабличныйДокумент") Тогда
				
				// Переданы 2 табличных документа
				Элементы.ПутьКДокументу1.Видимость = Ложь;
				Элементы.ПутьКДокументу2.Видимость = Ложь;
				ДокументыЗагружены = Истина;
				Объект.ТабличныйДокумент1 = Документ1;
				Объект.ТабличныйДокумент2 = Документ2;
				СравнитьДокументы();
				
			ИначеЕсли ТипЗнч(Документ1) = Тип("Строка") 
				И ТипЗнч(Документ2) = Тип("Строка") Тогда
				
				Если ЭтоАдресВременногоХранилища(Документ1) 
					И ЭтоАдресВременногоХранилища(Документ2) Тогда
					
					// Переданы 2 адреса на временное хранилище
					Элементы.ПутьКДокументу1.Видимость = Ложь;
					Элементы.ПутьКДокументу2.Видимость = Ложь;
					Объект.АдресДокумента1 = Документ1;
					Объект.АдресДокумента2 = Документ2;
					СравнитьДокументы();
					
				Иначе
					Объект.ПутьКДокументу1 = Документ1;
					Объект.ПутьКДокументу2 = Документ2;
				КонецЕсли;	
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПутьКДокументу1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыборФайлаНачалоВыбора("ПутьКДокументу1");
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКДокументу2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыборФайлаНачалоВыбора("ПутьКДокументу2");
	
КонецПроцедуры
	
&НаКлиенте
Процедура ВыборФайлаНачалоВыбора(ИмяРеквизита)
	
	СтандартнаяОбработка = Ложь;
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогОткрытияФайла.Фильтр = "(*.mxl;*.xlsx;*.xls;)|*.mxl;*.xlsx;*.xls;";
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ДиалогОткрытияФайла.Заголовок = НСтр("ru = 'Выберите файл для сравнения'");
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяРеквизита", ИмяРеквизита);
	
	Оповещение = Новый ОписаниеОповещения("ОбработчикВыбораФайлаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ФайловаяСистемаКлиент.ПоказатьДиалогВыбора(Оповещение, ДиалогОткрытияФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикВыбораФайлаЗавершение(МассивФайлов, ДополнительныеПараметры) Экспорт
	
	Если МассивФайлов = Неопределено 
		ИЛИ МассивФайлов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Объект[ДополнительныеПараметры.ИмяРеквизита] = МассивФайлов.Получить(0);	
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКДокументу1Открытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяРеквизита", "АдресДокумента1");
	ДополнительныеПараметры.Вставить("ПутьКФайлу", Объект.ПутьКДокументу1);
	ДополнительныеПараметры.Вставить("ДействиеПослеЧтения", "ОткрытьФайл");
	
	ПрочитатьФайл(ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКДокументу2Открытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяРеквизита", "АдресДокумента2");
	ДополнительныеПараметры.Вставить("ПутьКФайлу", Объект.ПутьКДокументу2);
	ДополнительныеПараметры.Вставить("ДействиеПослеЧтения", "ОткрытьФайл");
	
	ПрочитатьФайл(ДополнительныеПараметры); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьФайл(ДополнительныеПараметры)
	
	ПутьКФайлу = ДополнительныеПараметры.ПутьКФайлу;
	Объект[ДополнительныеПараметры.ИмяРеквизита] = "";
	
	Оповещение = Новый ОписаниеОповещения("ОбработчикОкончанияПомещения", ЭтотОбъект, ДополнительныеПараметры);
	
	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	ПараметрыЗагрузки.Интерактивно = Ложь;

	ФайловаяСистемаКлиент.ЗагрузитьФайл(Оповещение, ПараметрыЗагрузки, ПутьКФайлу); 
	
КонецПроцедуры
	
&НаКлиенте
Процедура ОбработчикОкончанияПомещения(ПомещенныйФайл, ДополнительныеПараметры) Экспорт
	
	Если ПомещенныйФайл = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Файл не был помещен в хранилище.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	Иначе
		Если ПрочитатьФайлНаСервере(ПомещенныйФайл.Хранение, ДополнительныеПараметры) Тогда
			Если ДополнительныеПараметры.Свойство("ДействиеПослеЧтения") Тогда
				Если ДополнительныеПараметры.ДействиеПослеЧтения = "ОткрытьФайл" Тогда
					ИмяРеквизита = ДополнительныеПараметры.ИмяРеквизита;
					ТабличныйДокумент = ПолучитьИзВременногоХранилища(Объект[ИмяРеквизита]);
					ТабличныйДокумент.Показать(ДополнительныеПараметры.ПутьКФайлу);
				ИначеЕсли ДополнительныеПараметры.ДействиеПослеЧтения = "ПрочитатьДокумент2" Тогда
					ПрочитатьДокумент2();
				ИначеЕсли ДополнительныеПараметры.ДействиеПослеЧтения = "СравнитьДокументы" Тогда
					СравнитьДокументы();	
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли
	
КонецПроцедуры 

&НаСервере
Функция ПрочитатьФайлНаСервере(Адрес, ДополнительныеПараметры)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;	
	МассивЧастейФайла = СтрРазделить(ДополнительныеПараметры.ПутьКФайлу,".");
	Расширение = МассивЧастейФайла.Получить(МассивЧастейФайла.ВГраница());
	
	Попытка
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(Адрес);
		Если Расширение = "xls" Или Расширение = "xlsx" Тогда		
			ИмяВременногоФайла = ПолучитьИмяВременногоФайла(Расширение);
			ДвоичныеДанные.Записать(ИмяВременногоФайла);
			ТабличныйДокумент.Прочитать(ИмяВременногоФайла);
			УдалитьФайлы(ИмяВременногоФайла);
	    Иначе			
			Поток = ДвоичныеДанные.ОткрытьПотокДляЧтения();	
			ТабличныйДокумент.Прочитать(Поток);
			Поток.Закрыть();
		КонецЕсли;
		УдалитьИзВременногоХранилища(Адрес);		
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ТекстСообщения = НСтр("ru = 'Не удалось выполнить чтение файла, по причине: %1'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат Ложь;
	КонецПопытки;
	
	Объект[ДополнительныеПараметры.ИмяРеквизита] = 
		ПоместитьВоВременноеХранилище(ТабличныйДокумент, УникальныйИдентификатор);
	
	Возврат Истина;
		
КонецФункции

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрочитатьДокумент1()
	
	Если ДокументыЗагружены Тогда
		СравнитьДокументы();
		Возврат;
	КонецЕсли;
	
	// Прочитаем первый файл
	ДополнительныеПараметры = Новый Структура;	
	ДополнительныеПараметры.Вставить("ИмяРеквизита", "АдресДокумента1");
	ДополнительныеПараметры.Вставить("ПутьКФайлу", Объект.ПутьКДокументу1);
	ДополнительныеПараметры.Вставить("ДействиеПослеЧтения", "ПрочитатьДокумент2"); 	
	ПрочитатьФайл(ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьДокумент2()
	
	// Прочитаем первый файл
	ДополнительныеПараметры = Новый Структура;	
	ДополнительныеПараметры.Вставить("ИмяРеквизита", "АдресДокумента2");
	ДополнительныеПараметры.Вставить("ПутьКФайлу", Объект.ПутьКДокументу2);
	ДополнительныеПараметры.Вставить("ДействиеПослеЧтения", "СравнитьДокументы"); 	
	ПрочитатьФайл(ДополнительныеПараметры);
	
КонецПроцедуры

&НаСервере 
Процедура СравнитьДокументы()
	
	Отказ = Ложь;
	Если Не ДокументыЗагружены Тогда
		Если ПустаяСтрока(Объект.АдресДокумента1) Тогда
			ТекстСообщения = НСтр("ru = 'Не загружен Документ №1'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "ПутьКДокументу1", "ПутьКДокументу1", Отказ);
		КонецЕсли;
		Объект.ТабличныйДокумент1 = ПолучитьИзВременногоХранилища(Объект.АдресДокумента1);
		Если ПустаяСтрока(Объект.АдресДокумента2) Тогда
			ТекстСообщения = НСтр("ru = 'Не загружен Документ №2'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "ПутьКДокументу2", "ПутьКДокументу2", Отказ);
		КонецЕсли;
		Объект.ТабличныйДокумент2 = ПолучитьИзВременногоХранилища(Объект.АдресДокумента2);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	СвойстваИсключения = Неопределено;
	Если Объект.СвойстваИсключения.Количество() > 0 Тогда
		СвойстваИсключения = Объект.СвойстваИсключения.Выгрузить().ВыгрузитьКолонку("Свойство");
	КонецЕсли;
	
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	РезультатСравнения = ТекущийОбъект.СравнитьДваТабличныхДокумента(
		Объект.ТабличныйДокумент1, Объект.ТабличныйДокумент2, 
		СвойстваИсключения, Объект.СравниватьТолькоЗначения, Объект.СравниватьПоШаблону);
	
	Если РезультатСравнения.ДокументыИдентичны Тогда
		Заголовок = НСтр("ru = 'Документы идентичны'");
	Иначе
		Заголовок = НСтр("ru = 'Документы отличаются'");
	КонецЕсли;
	
	Ошибки.Очистить();
	Для Каждого Ошибка Из РезультатСравнения.Ошибки Цикл
		Ошибки.ДобавитьСтроку(Ошибка);
	КонецЦикла;
	Результат = РезультатСравнения.ДокументРезультат;

КонецПроцедуры

#КонецОбласти