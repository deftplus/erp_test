
#Область ОбработкаОсновныхСобытийФормы


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ ПользователиСлужебныйПовтИсп.ЭтоСеансВнешнегоПользователя() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	АккредитацияПоставщиковУХ.УстановитьРеквизитыФормыСпискаДляПоставщика(ЭтаФорма);
	
	УстановитьПараметрыДинамическихСписков();
	УстановитьОформлениеФормы();
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	ЗавершитьРаботуСистемы(Ложь);
КонецПроцедуры


#КонецОбласти

#Область ОбработкаСобытийЭлементовФормы


&НаКлиенте
Процедура ОбщийСтатусАккредитацииНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Документ.АккредитацияПоставщика.ФормаСписка",, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура РегламентирующиеДокументыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ТекДанные = Элементы.РегламентирующиеДокументы.ТекущиеДанные;
	Если ТекДанные <> Неопределено И НЕ ПустаяСтрока(ТекДанные.ИДФайла) Тогда
		СвязанныеФайлыКлиентУХ.ПодготовитьИОткрытьФайлДляПросмотра(ТекДанные.ИДФайла, ЭтаФорма);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОформлениеФормы(Команда)
	УстановитьОформлениеФормы();
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыНаКлиенте


#КонецОбласти

#Область СлужебныеПроцедурыНаСервере


&НаСервере
Процедура УстановитьОформлениеФормы()
	ДанныеАккредитации = АккредитацияПоставщиковУХ.ПолучитьОписаниеСтатусаАккредитацииПоставщика(
		АнкетаПоставщика, ТекущаяДатаСеанса());
		
	ОбщийСтатусАккредитации = АккредитацияПоставщиковУХ.ПолучитьПредставлениеОбщегоСтатусаАккредитации(ДанныеАккредитации);
	
	Элементы.ГруппаПраваяКолонкаШапки.Видимость = ДанныеАккредитации.Аккредитован;
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыДинамическихСписков()
	СостояниеАккредитацииПоОрганизациям.Параметры.УстановитьЗначениеПараметра("АнкетаПоставщика", АнкетаПоставщика);
	ДокументыАккредитации.Параметры.УстановитьЗначениеПараметра("АнкетаПоставщика", АнкетаПоставщика);
КонецПроцедуры

&НаКлиенте
Процедура СостояниеАккредитацииПоОрганизациямВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ТекДанные = Элементы.СостояниеАккредитацииПоОрганизациям.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ПоказатьЗначение(, ТекДанные.Документ);
КонецПроцедуры


#КонецОбласти
