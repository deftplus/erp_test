
&НаКлиенте
Процедура Вн_ОткрытьФорму(Команда)
	
	Если Элементы.Список.ТекущаяСтрока <> Неопределено Тогда
		ТД = Элементы.Список.ДанныеСтроки(Элементы.Список.ТекущаяСтрока);
		ОткрытьФорму("Справочник.ПротоколРассылки.ФормаОбъекта", Новый Структура("Ключ, ОткрыватьСобственнуюФорму", ТД.Ссылка, Истина));
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьДанныеПоПротоколу(СсылкаНаПодписку = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПротоколРассылки.Ссылка
	|ИЗ
	|	Справочник.ПротоколРассылки КАК ПротоколРассылки";
	Если СсылкаНаПодписку <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		|ГДЕ
		|	ПротоколРассылки.Источник = &СсылкаНаПодписку";
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ТекОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ТекОбъект.Удалить();
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПротокол(Команда)
	
	Источник = Неопределено;
	
	ПолеПоиска = Новый ПолеКомпоновкиДанных("Источник");
	Для Каждого Элемент Из Список.Отбор.Элементы Цикл
		Если Элемент.ЛевоеЗначение = ПолеПоиска И Элемент.Использование = Истина Тогда
			Источник = Элемент.ПравоеЗначение;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ДопПараметры = Новый Структура("Источник", Источник);
	Оповещение = Новый ОписаниеОповещения("Подключаемый_ОчиститьПротоколЗавершение", ЭтотОбъект, ДопПараметры);
	ПоказатьВопрос(Оповещение, Нстр("ru = 'Удалить историю отправленных уведомлений?'"), РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОчиститьПротоколЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Источник = ДополнительныеПараметры.Источник;    
    
    Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
    
    Состояние(Нстр("ru = 'Удаление истории уведомлений'"));
    УдалитьДанныеПоПротоколу(Источник);
    Состояние();

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОтчет(Команда)
	
	Если Элементы.Список.ТекущаяСтрока <> Неопределено Тогда
		
		ДанныеСтроки = Элементы.Список.ДанныеСтроки(Элементы.Список.ТекущаяСтрока);
		ОткрытьФорму("Справочник.ПротоколРассылки.ФормаОбъекта", Новый Структура("Ключ, ОткрыватьСобственнуюФорму", ДанныеСтроки.Ссылка, Ложь));
		
	КонецЕсли;
	
КонецПроцедуры
