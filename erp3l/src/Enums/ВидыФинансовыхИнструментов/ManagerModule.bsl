#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Параметры.Свойство("ВидДоговора") Тогда
		
		ДанныеВыбора = ПолучитьДоступныеЗначенияПоВидуДоговора(Параметры.ВидДоговора);
		
	ИначеЕсли Параметры.Свойство("ЦеннаяБумага") Тогда
		
		ДанныеВыбора = ПолучитьВидыЦенныхБумаг();
	Иначе
		
		ДанныеВыбора = Новый СписокЗначений;
		Для Каждого ЗначениеПеречисления Из Метаданные.Перечисления.ВидыФинансовыхИнструментов.ЗначенияПеречисления Цикл
			
			Если СтрНачинаетсяС(Врег(ЗначениеПеречисления.Имя), "УДАЛИТЬ") Тогда
				Продолжить;
			КонецЕсли;
			
			ДанныеВыбора.Добавить(Перечисления.ВидыФинансовыхИнструментов[ЗначениеПеречисления.Имя]);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДоступныеЗначенияПоВидуДоговора(ВидДоговора = Неопределено)
	
	Результат = Новый СписокЗначений;
	
	ВидФИ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидДоговора, "ВидФинансовогоИнструмента");
	
	Если ЗначениеЗаполнено(ВидФИ) Тогда
		Результат.Добавить(ВидФИ);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьВидыЦенныхБумаг() Экспорт
	
	ДоступныеЗначения = Новый СписокЗначений;
	
	ДоступныеЗначения.Добавить(Перечисления.ВидыФинансовыхИнструментов.Вексель);	
	ДоступныеЗначения.Добавить(Перечисления.ВидыФинансовыхИнструментов.Облигация);	
	ДоступныеЗначения.Добавить(Перечисления.ВидыФинансовыхИнструментов.Акция);
	ДоступныеЗначения.Добавить(Перечисления.ВидыФинансовыхИнструментов.ПрочиеЦенныеБумаги);
	
	Возврат ДоступныеЗначения;
	
КонецФункции

#КонецОбласти

#КонецЕсли
