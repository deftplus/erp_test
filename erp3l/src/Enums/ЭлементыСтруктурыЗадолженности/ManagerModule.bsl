#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ДоговорКонтрагента") 
		И ТипЗнч(Параметры.ДоговорКонтрагента) = Тип("СправочникСсылка.ДоговорыАренды") тогда

		СтандартнаяОбработка = Ложь;
		
		ДанныеВыбора = Новый СписокЗначений;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ЭлементыСтруктурыЗадолженности.УслугаПоАренде) КАК Ссылка,
		|	1 КАК Порядок
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ЭлементыСтруктурыЗадолженности.ОбеспечительныйПлатеж),
		|	2
		|ИЗ
		|	Справочник.ДоговорыАренды КАК Т
		|ГДЕ
		|	Т.Ссылка = &Договор
		|	И Т.ЕстьОбеспечительныйПлатеж
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ЭлементыСтруктурыЗадолженности.ВыкупнаяСтоимость),
		|	3
		|ИЗ
		|	Справочник.ДоговорыАренды КАК Т
		|ГДЕ
		|	Т.Ссылка = &Договор
		|	И Т.ЕстьВыкупПредметовАренды
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ЭлементыСтруктурыЗадолженности.ВыкупнаяСтоимостьАванс),
		|	4
		|ИЗ
		|	Справочник.ДоговорыАренды КАК Т
		|ГДЕ
		|	Т.Ссылка = &Договор
		|	И Т.ЕстьАвансированиеВыкупнойСтоимости
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порядок";
		
		Запрос.УстановитьПараметр("Договор", Параметры.ДоговорКонтрагента);
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			ДанныеВыбора.Добавить(Выборка.Ссылка);
		КонецЦикла;
		
		Возврат;
		
	КонецЕсли;
	
	Если Параметры.Свойство("ВидФинансовогоИнструмента") 
		И Параметры.ВидФинансовогоИнструмента = Перечисления.ВидыФинансовыхИнструментов.ЛизингПолученный Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ДанныеВыбора = Новый СписокЗначений;
		ДанныеВыбора.Добавить(Перечисления.ЭлементыСтруктурыЗадолженности.УслугаПоАренде);
		ДанныеВыбора.Добавить(Перечисления.ЭлементыСтруктурыЗадолженности.ВыкупнаяСтоимость);
		ДанныеВыбора.Добавить(Перечисления.ЭлементыСтруктурыЗадолженности.ОбеспечительныйПлатеж);
		
	КонецЕсли;
		
	Если (Параметры.Свойство("ДоговорКонтрагента") 
			ИЛИ Параметры.Свойство("ОбъектРасчетов"))
		 И Параметры.Свойство("Ссылка") Тогда
		 
		Если Параметры.Свойство("ОбъектРасчетов") И ЗначениеЗаполнено(Параметры.ОбъектРасчетов) Тогда
			Договор = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ОбъектРасчетов, "Договор");
		Иначе
			Договор = Параметры.ДоговорКонтрагента;
		КонецЕсли;
		 
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = Новый СписокЗначений;
		МассивЭлементов = ЗаявкиНаОперацииВызовСервера.ЭлементыСтруктурыЗадолженностиПоДоговору(Договор, Параметры.Ссылка);
		ДанныеВыбора.ЗагрузитьЗначения(МассивЭлементов);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли

