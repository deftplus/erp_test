#Если НаСервере Тогда
	
	
Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений;
	ДанныеВыбора.ЗагрузитьЗначения(
		Перечисления.СпособыВыбораПоставщика.ДоступныеДляВыбора());
КонецПроцедуры

Функция ПолучитьАукционы() Экспорт
	мСтатусыЛотов = Новый Массив;
	мСтатусыЛотов.Добавить(Перечисления.СпособыВыбораПоставщика.Аукцион);
	мСтатусыЛотов.Добавить(Перечисления.СпособыВыбораПоставщика.Редукцион);
	Возврат мСтатусыЛотов;
КонецФункции

Функция ЭтоАукцион(СпособДляПроверки) Экспорт
	Возврат ПолучитьАукционы().Найти(СпособДляПроверки) <> Неопределено;
КонецФункции

// Возвращает строку представления элемента перечисления
// в родительном падеже (отвечает на вопрос "кого? чего?").
Функция РодительныйПадеж(Значение) Экспорт
	Если Значение = Перечисления.СпособыВыбораПоставщика.ЗакупкаУЕдинственногоПоставщика Тогда
		Возврат "Закупки у единственного поставщика";
	ИначеЕсли Значение = Перечисления.СпособыВыбораПоставщика.Конкурс Тогда
		Возврат "Конкурса";
	ИначеЕсли Значение = Перечисления.СпособыВыбораПоставщика.Аукцион Тогда
		Возврат "Аукциона";
	ИначеЕсли Значение = Перечисления.СпособыВыбораПоставщика.ЗапросКотировок Тогда
		Возврат "Запроса котировок";
	ИначеЕсли Значение = Перечисления.СпособыВыбораПоставщика.ЗапросПредложений Тогда
		Возврат "Запроса предложений";
	ИначеЕсли Значение = Перечисления.СпособыВыбораПоставщика.Редукцион Тогда
		Возврат "Редукциона";
	ИначеЕсли Значение = Перечисления.СпособыВыбораПоставщика.КонкурентныеПереговоры Тогда
		Возврат "Конкурентных переговоров";
	КонецЕсли;
	
	Возврат "";
КонецФункции

Функция ДоступныеДляВыбора() Экспорт
	мЗначений = Новый Массив;
	мЗначений.Добавить(Перечисления.СпособыВыбораПоставщика.ЗакупкаУЕдинственногоПоставщика);
	мЗначений.Добавить(Перечисления.СпособыВыбораПоставщика.Конкурс);
	мЗначений.Добавить(Перечисления.СпособыВыбораПоставщика.ЗапросКотировок);
	мЗначений.Добавить(Перечисления.СпособыВыбораПоставщика.ЗапросПредложений);
	мЗначений.Добавить(Перечисления.СпособыВыбораПоставщика.КонкурентныеПереговоры);
	мЗначений.Добавить(Перечисления.СпособыВыбораПоставщика.Аукцион);
	мЗначений.Добавить(Перечисления.СпособыВыбораПоставщика.КонкурсУМсп);
	мЗначений.Добавить(Перечисления.СпособыВыбораПоставщика.АукционУМсп);
	мЗначений.Добавить(Перечисления.СпособыВыбораПоставщика.ЗапросКотировокУМсп);
	мЗначений.Добавить(Перечисления.СпособыВыбораПоставщика.ЗапросПредложенийУМсп);
	Возврат мЗначений;
КонецФункции

// Возвращает массив способов закупки у МСП.
Функция ЗакупкаУМспДоступныеДляВыбора() Экспорт
	мЗначений = Новый Массив;
	мЗначений.Добавить(Перечисления.СпособыВыбораПоставщика.КонкурсУМсп);
	мЗначений.Добавить(Перечисления.СпособыВыбораПоставщика.АукционУМсп);
	мЗначений.Добавить(Перечисления.СпособыВыбораПоставщика.ЗапросКотировокУМсп);
	мЗначений.Добавить(Перечисления.СпособыВыбораПоставщика.ЗапросПредложенийУМсп);
	Возврат мЗначений;
КонецФункции		 // ЗакупкаУМспДоступныеДляВыбора()

// Возвращает массив возможных значений перечисления, которые доступны
// для организаций, не ведущих учёт по 223-ФЗ.
Функция НеФз223ДоступныеДляВыбора() Экспорт
	РезультатФункции = Новый Массив;
	МассивДоступных = ДоступныеДляВыбора();
	МассивМСП = ЗакупкаУМспДоступныеДляВыбора();
	Для Каждого ТекМассивДоступных Из МассивДоступных Цикл
		Если МассивМСП.Найти(ТекМассивДоступных) = Неопределено Тогда
			РезультатФункции.Добавить(ТекМассивДоступных);
		Иначе
			// Это закупка у МСП. Пропускаем.
		КонецЕсли;
	КонецЦикла;	
	Возврат РезультатФункции;
КонецФункции		 // НеФз223ДоступныеДляВыбора()

#КонецЕсли

