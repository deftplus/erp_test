
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет субконто проводки из переданных данных проводки
//
// Параметры:
//  Проводка - РегистрБухгалтерииЗаписьИмяРегистраБухгалтерии - проводка у которой требуется заполнить субконто
//  Данные   - Структура - структура содержащая данные субконто
//             ключ структуры указывает имя поля проводки - "ВидСубконтоДт1,СубконтоДт1...."
//             значение структуры соответствующее значение поля проводки
//  ВидДвижения - Строка- "Дт" или "Кт".
//
Процедура УстановитьСубконто(Проводка, Данные, ВидДвижения) Экспорт
	
	Если ВидДвижения = "Дт" Тогда
		ИмяСчет = "СчетДт";
		ИмяСубконто = "СубконтоДт";
		ИмяВидаСубконто = "ВидСубконтоДт";
	Иначе
		ИмяСчет = "СчетКт";
		ИмяСубконто = "СубконтоКт";
		ИмяВидаСубконто = "ВидСубконтоКт";
	КонецЕсли;
	
	Счет = Проводка[ИмяСчет];
	СвойстваСчета = МеждународныйУчетСерверПовтИсп.СвойстваСчета(Счет);
	Для Номер = 1 По СвойстваСчета.КоличествоСубконто Цикл
		ВидСубконто = Данные[ИмяВидаСубконто + Номер];
		Если ВидДвижения = "Дт" Тогда
			Проводка.СубконтоДт[ВидСубконто] = Данные[ИмяСубконто + Номер];
		Иначе
			Проводка.СубконтоКт[ВидСубконто] = Данные[ИмяСубконто + Номер];
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Добавляет переданную проводку в текущий набор записей
//
// Параметры:
//  Проводка - РегистрБухгалтерииЗапись.Международный, Структура - добавляемая проводка
//  ТекстСодержания - Строка - текст содержания проводки.
//
Процедура ДобавитьПроводоку(Проводка, ТекстСодержания = "") Экспорт
	
	НоваяПроводка = ЭтотОбъект.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяПроводка, Проводка);
	Если НЕ ПустаяСтрока(ТекстСодержания) Тогда
		НоваяПроводка.Содержание = ТекстСодержания;
	КонецЕсли;
	Для Каждого Субконто Из Проводка.СубконтоДт Цикл
		НоваяПроводка.СубконтоДт[Субконто.Ключ] = Субконто.Значение;
	КонецЦикла;
	Для Каждого Субконто Из Проводка.СубконтоКт Цикл
		НоваяПроводка.СубконтоКт[Субконто.Ключ] = Субконто.Значение;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПустыеСуммыПроводки = Ложь;
	Для Каждого Запись Из ЭтотОбъект Цикл
		Если Запись.Сумма = 0 И Запись.СуммаПредставления = 0 Тогда
			ПустыеСуммыПроводки = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ПустыеСуммыПроводки Тогда
		МассивНепроверяемыхРеквизитов = Новый Массив;
		МассивНепроверяемыхРеквизитов.Добавить("Сумма");
		МассивНепроверяемыхРеквизитов.Добавить("СуммаПредставления");
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Регистратор = ЭтотОбъект.Отбор.Регистратор.Значение;
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	ФормироватьКорректировочныеПроводки = ЕстьНеобходимостьФормированияКорректировочныхПроводок(Регистратор, МенеджерВременныхТаблиц);
	Текст = НСтр("ru = 'Установлена дата запрета формирования проводок по международному учету.
						|Изменение проводок документа по международному учету запрещено.';
						|en = 'Closing date of financial accounting posting generation is set.
						|It is prohibited to change financial accounting postings for the document.'");
	
	Если ТипЗнч(Регистратор) = Тип("ДокументСсылка.ОперацияМеждународный") Тогда
		ВводитьПериодПоСтрокам = ДополнительныеСвойства.Свойство("ВводитьПериодПоСтрокам");
		Если ФормироватьКорректировочныеПроводки И НЕ ВводитьПериодПоСтрокам Тогда
			Текст = Текст + Символы.ПС + Символы.ПС
				+ НСтр("ru = 'Корректировать проводки документа ""Операция (международный учет)"" в закрытом периоде по международному учету
				|допускается только с включенным флагом ""Вводить дату в строках"" на закладке ""Основное"".';
				|en = 'You can make adjustments to the ""Transaction (financial accounting)"" document entries for the closed period of international accounting
				|only if the ""Enter date in lines"" check box on the Main tab is selected.'");
			ВызватьИсключение Текст;
		КонецЕсли;
	КонецЕсли;
	
	Если ФормироватьКорректировочныеПроводки Тогда
		
		ОтменаПроведения = ДополнительныеСвойства.Свойство("СвойстваДокумента")
			И ДополнительныеСвойства.СвойстваДокумента.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения;
		
		Если Количество() = 0 И НЕ ОтменаПроведения Тогда
			Прочитать();
		КонецЕсли;
		НетАктивныхЗаписей = Количество() = 0 ИЛИ НЕ ЭтотОбъект[0].Активность;
		
		Если ОтменаПроведения ИЛИ НетАктивныхЗаписей Тогда
			ЗаписыватьПустойНабор = Ложь;
			Если НЕ ДополнительныеСвойства.Свойство("ЗаписыватьПустойНабор", ЗаписыватьПустойНабор) ИЛИ НЕ ЗаписыватьПустойНабор Тогда
				ВызватьИсключение Текст;
			КонецЕсли;
		КонецЕсли;
		СформироватьКорректировочныеПроводки(МенеджерВременныхТаблиц);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьКорректировочныеПроводки(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапросаКорректировочныхПроводок();
	
	Очистить();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяПроводка = Добавить();
		ЗаполнитьЗначенияСвойств(НоваяПроводка, Выборка);
		УстановитьСубконто(НоваяПроводка, Выборка, "Дт");
		УстановитьСубконто(НоваяПроводка, Выборка, "Кт");
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстЗапросаКорректировочныхПроводок()
	
	Возврат
	"ВЫБРАТЬ
	|	ВложеныйЗапрос.Период КАК Период,
	|	ВложеныйЗапрос.Регистратор,
	|	ВложеныйЗапрос.ПланСчетов,
	|	ВложеныйЗапрос.Организация,
	|	ВложеныйЗапрос.СчетДт,
	|	ВложеныйЗапрос.ПодразделениеДт,
	|	ВложеныйЗапрос.НаправлениеДеятельностиДт,
	|	ВложеныйЗапрос.ВидСубконтоДт1,
	|	ВложеныйЗапрос.СубконтоДт1,
	|	ВложеныйЗапрос.ВидСубконтоДт2,
	|	ВложеныйЗапрос.СубконтоДт2,
	|	ВложеныйЗапрос.ВидСубконтоДт3,
	|	ВложеныйЗапрос.СубконтоДт3,
	|	ВложеныйЗапрос.ВалютаДт,
	|	ВложеныйЗапрос.СчетКт,
	|	ВложеныйЗапрос.ПодразделениеКт,
	|	ВложеныйЗапрос.НаправлениеДеятельностиКт,
	|	ВложеныйЗапрос.ВидСубконтоКт1,
	|	ВложеныйЗапрос.СубконтоКт1,
	|	ВложеныйЗапрос.ВидСубконтоКт2,
	|	ВложеныйЗапрос.СубконтоКт2,
	|	ВложеныйЗапрос.ВидСубконтоКт3,
	|	ВложеныйЗапрос.СубконтоКт3,
	|	ВложеныйЗапрос.ВалютаКт,
	|	СУММА(ВложеныйЗапрос.ВалютнаяСуммаДт) КАК ВалютнаяСуммаДт,
	|	СУММА(ВложеныйЗапрос.ВалютнаяСуммаКт) КАК ВалютнаяСуммаКт,
	|	СУММА(ВложеныйЗапрос.КоличествоДт) КАК КоличествоДт,
	|	СУММА(ВложеныйЗапрос.КоличествоКт) КАК КоличествоКт,
	|	СУММА(ВложеныйЗапрос.Сумма) КАК Сумма,
	|	СУММА(ВложеныйЗапрос.СуммаПредставления) КАК СуммаПредставления,
	|	ВложеныйЗапрос.ШаблонПроводки КАК ШаблонПроводки,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПроводокМеждународныйУчет.Корректировка) КАК ТипПроводки
	|ПОМЕСТИТЬ КорректировочныеПроводки
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПроводкиСторно.Регистратор КАК Регистратор,
	|		ПроводкиСторно.ПериодКорректировки КАК Период,
	|		ПроводкиСторно.ПланСчетов КАК ПланСчетов,
	|		ПроводкиСторно.Организация КАК Организация,
	|		ПроводкиСторно.СчетДт КАК СчетДт,
	|		ПроводкиСторно.ПодразделениеДт КАК ПодразделениеДт,
	|		ЕСТЬNULL(ПроводкиСторно.НаправлениеДеятельностиДт, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельностиДт,
	|		ЕСТЬNULL(ПроводкиСторно.ВидСубконтоДт1, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконтоДт1,
	|		ЕСТЬNULL(ПроводкиСторно.СубконтоДт1, НЕОПРЕДЕЛЕНО) КАК СубконтоДт1,
	|		ЕСТЬNULL(ПроводкиСторно.ВидСубконтоДт2, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконтоДт2,
	|		ЕСТЬNULL(ПроводкиСторно.СубконтоДт2, НЕОПРЕДЕЛЕНО) КАК СубконтоДт2,
	|		ЕСТЬNULL(ПроводкиСторно.ВидСубконтоДт3, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконтоДт3,
	|		ЕСТЬNULL(ПроводкиСторно.СубконтоДт3, НЕОПРЕДЕЛЕНО) КАК СубконтоДт3,
	|		ЕСТЬNULL(ПроводкиСторно.ВалютаДт, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК ВалютаДт,
	|		ЕСТЬNULL(-ПроводкиСторно.ВалютнаяСуммаДт, 0) КАК ВалютнаяСуммаДт,
	|		ЕСТЬNULL(-ПроводкиСторно.КоличествоДт, 0) КАК КоличествоДт,
	|		ПроводкиСторно.СчетКт КАК СчетКт,
	|		ПроводкиСторно.ПодразделениеКт КАК ПодразделениеКт,
	|		ЕСТЬNULL(ПроводкиСторно.НаправлениеДеятельностиКт, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельностиКт,
	|		ЕСТЬNULL(ПроводкиСторно.ВидСубконтоКт1, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконтоКт1,
	|		ЕСТЬNULL(ПроводкиСторно.СубконтоКт1, НЕОПРЕДЕЛЕНО) КАК СубконтоКт1,
	|		ЕСТЬNULL(ПроводкиСторно.ВидСубконтоКт2, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконтоКт2,
	|		ЕСТЬNULL(ПроводкиСторно.СубконтоКт2, НЕОПРЕДЕЛЕНО) КАК СубконтоКт2,
	|		ЕСТЬNULL(ПроводкиСторно.ВидСубконтоКт3, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка)) КАК ВидСубконтоКт3,
	|		ЕСТЬNULL(ПроводкиСторно.СубконтоКт3, НЕОПРЕДЕЛЕНО) КАК СубконтоКт3,
	|		ЕСТЬNULL(ПроводкиСторно.ВалютаКт, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК ВалютаКт,
	|		ЕСТЬNULL(-ПроводкиСторно.ВалютнаяСуммаКт, 0) КАК ВалютнаяСуммаКт,
	|		ЕСТЬNULL(-ПроводкиСторно.КоличествоКт, 0) КАК КоличествоКт,
	|		-ПроводкиСторно.Сумма КАК Сумма,
	|		-ПроводкиСторно.СуммаПредставления КАК СуммаПредставления,
	|		ПроводкиСторно.ШаблонПроводки КАК ШаблонПроводки
	|	ИЗ
	|		ИсходныеПроводки КАК ПроводкиСторно
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НовыеПроводки.Регистратор,
	|		НовыеПроводки.ПериодКорректировки,
	|		НовыеПроводки.ПланСчетов,
	|		НовыеПроводки.Организация,
	|		НовыеПроводки.СчетДт,
	|		НовыеПроводки.ПодразделениеДт,
	|		НовыеПроводки.НаправлениеДеятельностиДт,
	|		НовыеПроводки.ВидСубконтоДт1,
	|		НовыеПроводки.СубконтоДт1,
	|		НовыеПроводки.ВидСубконтоДт2,
	|		НовыеПроводки.СубконтоДт2,
	|		НовыеПроводки.ВидСубконтоДт3,
	|		НовыеПроводки.СубконтоДт3,
	|		НовыеПроводки.ВалютаДт,
	|		НовыеПроводки.ВалютнаяСуммаДт,
	|		НовыеПроводки.КоличествоДт,
	|		НовыеПроводки.СчетКт,
	|		НовыеПроводки.ПодразделениеКт,
	|		НовыеПроводки.НаправлениеДеятельностиКт,
	|		НовыеПроводки.ВидСубконтоКт1,
	|		НовыеПроводки.СубконтоКт1,
	|		НовыеПроводки.ВидСубконтоКт2,
	|		НовыеПроводки.СубконтоКт2,
	|		НовыеПроводки.ВидСубконтоКт3,
	|		НовыеПроводки.СубконтоКт3,
	|		НовыеПроводки.ВалютаКт,
	|		НовыеПроводки.ВалютнаяСуммаКт,
	|		НовыеПроводки.КоличествоКт,
	|		НовыеПроводки.Сумма,
	|		НовыеПроводки.СуммаПредставления,
	|		НовыеПроводки.ШаблонПроводки
	|	ИЗ
	|		НовыеПроводки КАК НовыеПроводки) КАК ВложеныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложеныйЗапрос.ВидСубконтоКт2,
	|	ВложеныйЗапрос.СчетДт,
	|	ВложеныйЗапрос.ВидСубконтоДт1,
	|	ВложеныйЗапрос.СубконтоКт1,
	|	ВложеныйЗапрос.СубконтоДт1,
	|	ВложеныйЗапрос.ВидСубконтоКт1,
	|	ВложеныйЗапрос.ВидСубконтоДт2,
	|	ВложеныйЗапрос.СчетКт,
	|	ВложеныйЗапрос.ВалютаДт,
	|	ВложеныйЗапрос.ВидСубконтоДт3,
	|	ВложеныйЗапрос.ПодразделениеДт,
	|	ВложеныйЗапрос.ПодразделениеКт,
	|	ВложеныйЗапрос.НаправлениеДеятельностиДт,
	|	ВложеныйЗапрос.НаправлениеДеятельностиКт,
	|	ВложеныйЗапрос.Регистратор,
	|	ВложеныйЗапрос.ПланСчетов,
	|	ВложеныйЗапрос.Организация,
	|	ВложеныйЗапрос.СубконтоДт3,
	|	ВложеныйЗапрос.СубконтоДт2,
	|	ВложеныйЗапрос.СубконтоКт3,
	|	ВложеныйЗапрос.ВидСубконтоКт3,
	|	ВложеныйЗапрос.СубконтоКт2,
	|	ВложеныйЗапрос.ВалютаКт,
	|	ВложеныйЗапрос.ШаблонПроводки,
	|	ВложеныйЗапрос.Период
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВложеныйЗапрос.Сумма) <> 0
	|	ИЛИ СУММА(ВложеныйЗапрос.КоличествоДт) <> 0
	|	ИЛИ СУММА(ВложеныйЗапрос.КоличествоКт) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходныеПроводки.Период,
	|	ИсходныеПроводки.Регистратор,
	|	ИсходныеПроводки.ПланСчетов,
	|	ИсходныеПроводки.Организация,
	|	ИсходныеПроводки.СчетДт,
	|	ИсходныеПроводки.ПодразделениеДт,
	|	ИсходныеПроводки.НаправлениеДеятельностиДт,
	|	ИсходныеПроводки.ВидСубконтоДт1,
	|	ИсходныеПроводки.СубконтоДт1,
	|	ИсходныеПроводки.ВидСубконтоДт2,
	|	ИсходныеПроводки.СубконтоДт2,
	|	ИсходныеПроводки.ВидСубконтоДт3,
	|	ИсходныеПроводки.СубконтоДт3,
	|	ИсходныеПроводки.СчетКт,
	|	ИсходныеПроводки.ПодразделениеКт,
	|	ИсходныеПроводки.НаправлениеДеятельностиКт,
	|	ИсходныеПроводки.ВидСубконтоКт1,
	|	ИсходныеПроводки.СубконтоКт1,
	|	ИсходныеПроводки.ВидСубконтоКт2,
	|	ИсходныеПроводки.СубконтоКт2,
	|	ИсходныеПроводки.ВидСубконтоКт3,
	|	ИсходныеПроводки.СубконтоКт3,
	|	ИсходныеПроводки.ВалютаДт,
	|	ИсходныеПроводки.ВалютаКт,
	|	ИсходныеПроводки.ВалютнаяСуммаДт,
	|	ИсходныеПроводки.ВалютнаяСуммаКт,
	|	ИсходныеПроводки.КоличествоДт,
	|	ИсходныеПроводки.КоличествоКт,
	|	ИсходныеПроводки.Сумма,
	|	ИсходныеПроводки.СуммаПредставления,
	|	ИсходныеПроводки.ШаблонПроводки,
	|	ИсходныеПроводки.ТипПроводки
	|ИЗ
	|	ИсходныеПроводки КАК ИсходныеПроводки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировочныеПроводки.Период,
	|	КорректировочныеПроводки.Регистратор,
	|	КорректировочныеПроводки.ПланСчетов,
	|	КорректировочныеПроводки.Организация,
	|	КорректировочныеПроводки.СчетДт,
	|	КорректировочныеПроводки.ПодразделениеДт,
	|	КорректировочныеПроводки.НаправлениеДеятельностиДт,
	|	КорректировочныеПроводки.ВидСубконтоДт1,
	|	КорректировочныеПроводки.СубконтоДт1,
	|	КорректировочныеПроводки.ВидСубконтоДт2,
	|	КорректировочныеПроводки.СубконтоДт2,
	|	КорректировочныеПроводки.ВидСубконтоДт3,
	|	КорректировочныеПроводки.СубконтоДт3,
	|	КорректировочныеПроводки.СчетКт,
	|	КорректировочныеПроводки.ПодразделениеКт,
	|	КорректировочныеПроводки.НаправлениеДеятельностиКт,
	|	КорректировочныеПроводки.ВидСубконтоКт1,
	|	КорректировочныеПроводки.СубконтоКт1,
	|	КорректировочныеПроводки.ВидСубконтоКт2,
	|	КорректировочныеПроводки.СубконтоКт2,
	|	КорректировочныеПроводки.ВидСубконтоКт3,
	|	КорректировочныеПроводки.СубконтоКт3,
	|	КорректировочныеПроводки.ВалютаДт,
	|	КорректировочныеПроводки.ВалютаКт,
	|	КорректировочныеПроводки.ВалютнаяСуммаДт,
	|	КорректировочныеПроводки.ВалютнаяСуммаКт,
	|	КорректировочныеПроводки.КоличествоДт,
	|	КорректировочныеПроводки.КоличествоКт,
	|	КорректировочныеПроводки.Сумма,
	|	КорректировочныеПроводки.СуммаПредставления,
	|	КорректировочныеПроводки.ШаблонПроводки,
	|	КорректировочныеПроводки.ТипПроводки
	|ИЗ
	|	КорректировочныеПроводки КАК КорректировочныеПроводки";
	
КонецФункции

Функция ТекстЗапросаПроверкиДатыЗапрета()
	
	Возврат
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Организации.Ссылка КАК Организация,
	|	ВЫБОР
	|		КОГДА НЕ ДатыЗапретаПоОрганизации.ДатаЗапрета ЕСТЬ NULL 
	|			ТОГДА КОНЕЦПЕРИОДА(ДатыЗапретаПоОрганизации.ДатаЗапрета, ДЕНЬ)
	|		КОГДА НЕ ДатыЗапретаОбщая.ДатаЗапрета ЕСТЬ NULL 
	|			ТОГДА КОНЕЦПЕРИОДА(ДатыЗапретаОбщая.ДатаЗапрета, ДЕНЬ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК ДатаЗапрета
	|ПОМЕСТИТЬ ДатыЗапретаПоОрганизациям
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыЗапретаФормированияПроводокМеждународныйУчет КАК ДатыЗапретаПоОрганизации
	|		ПО Организации.Ссылка = ДатыЗапретаПоОрганизации.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыЗапретаФормированияПроводокМеждународныйУчет КАК ДатыЗапретаОбщая
	|		ПО (ДатыЗапретаОбщая.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|ГДЕ
	|	ВЫБОР
	|		КОГДА НЕ ДатыЗапретаПоОрганизации.ДатаЗапрета ЕСТЬ NULL 
	|			ТОГДА КОНЕЦПЕРИОДА(ДатыЗапретаПоОрганизации.ДатаЗапрета, ДЕНЬ)
	|		КОГДА НЕ ДатыЗапретаОбщая.ДатаЗапрета ЕСТЬ NULL 
	|			ТОГДА КОНЕЦПЕРИОДА(ДатыЗапретаОбщая.ДатаЗапрета, ДЕНЬ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ <> ДАТАВРЕМЯ(1, 1, 1)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходныеПроводки.Период,
	|	ИсходныеПроводки.Регистратор,
	|	ИсходныеПроводки.ПланСчетов,
	|	ИсходныеПроводки.Организация,
	|	ИсходныеПроводки.СчетДт,
	|	ИсходныеПроводки.ПодразделениеДт,
	|	ИсходныеПроводки.НаправлениеДеятельностиДт,
	|	ИсходныеПроводки.ВидСубконтоДт1,
	|	ИсходныеПроводки.СубконтоДт1,
	|	ИсходныеПроводки.ВидСубконтоДт2,
	|	ИсходныеПроводки.СубконтоДт2,
	|	ИсходныеПроводки.ВидСубконтоДт3,
	|	ИсходныеПроводки.СубконтоДт3,
	|	ИсходныеПроводки.СчетКт,
	|	ИсходныеПроводки.ПодразделениеКт,
	|	ИсходныеПроводки.НаправлениеДеятельностиКт,
	|	ИсходныеПроводки.ВидСубконтоКт1,
	|	ИсходныеПроводки.СубконтоКт1,
	|	ИсходныеПроводки.ВидСубконтоКт2,
	|	ИсходныеПроводки.СубконтоКт2,
	|	ИсходныеПроводки.ВидСубконтоКт3,
	|	ИсходныеПроводки.СубконтоКт3,
	|	ИсходныеПроводки.ВалютаДт,
	|	ИсходныеПроводки.ВалютаКт,
	|	ИсходныеПроводки.ВалютнаяСуммаДт,
	|	ИсходныеПроводки.ВалютнаяСуммаКт,
	|	ИсходныеПроводки.КоличествоДт,
	|	ИсходныеПроводки.КоличествоКт,
	|	ИсходныеПроводки.Сумма,
	|	ИсходныеПроводки.СуммаПредставления,
	|	ИсходныеПроводки.ШаблонПроводки,
	|	ИсходныеПроводки.ТипПроводки,
	|	ДОБАВИТЬКДАТЕ(ДатыЗапретаПоОрганизациям.ДатаЗапрета, СЕКУНДА, 1) КАК ПериодКорректировки
	|ПОМЕСТИТЬ ИсходныеПроводки
	|ИЗ
	|	РегистрБухгалтерии.Международный.ДвиженияССубконто(, , Регистратор В (&Документ), , ) КАК ИсходныеПроводки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатыЗапретаПоОрганизациям КАК ДатыЗапретаПоОрганизациям
	|		ПО ИсходныеПроводки.Организация = ДатыЗапретаПоОрганизациям.Организация
	|			И ИсходныеПроводки.Период <= ДатыЗапретаПоОрганизациям.ДатаЗапрета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НаборДвижений.Период КАК Период,
	|	НаборДвижений.Регистратор,
	|	НаборДвижений.ПланСчетов,
	|	НаборДвижений.Организация,
	|	НаборДвижений.СчетДт,
	|	НаборДвижений.ПодразделениеДт,
	|	НаборДвижений.НаправлениеДеятельностиДт,
	|	НаборДвижений.ВидСубконтоДт1 КАК ВидСубконтоДт1,
	|	НаборДвижений.СубконтоДт1,
	|	НаборДвижений.ВидСубконтоДт2 КАК ВидСубконтоДт2,
	|	НаборДвижений.СубконтоДт2,
	|	НаборДвижений.ВидСубконтоДт3 КАК ВидСубконтоДт3,
	|	НаборДвижений.СубконтоДт3,
	|	НаборДвижений.ВалютаДт,
	|	НаборДвижений.ВалютнаяСуммаДт КАК ВалютнаяСуммаДт,
	|	НаборДвижений.КоличествоДт КАК КоличествоДт,
	|	НаборДвижений.СчетКт,
	|	НаборДвижений.ПодразделениеКт,
	|	НаборДвижений.НаправлениеДеятельностиКт,
	|	НаборДвижений.ВидСубконтоКт1 КАК ВидСубконтоКт1,
	|	НаборДвижений.СубконтоКт1,
	|	НаборДвижений.ВидСубконтоКт2 КАК ВидСубконтоКт2,
	|	НаборДвижений.СубконтоКт2,
	|	НаборДвижений.ВидСубконтоКт3 КАК ВидСубконтоКт3,
	|	НаборДвижений.СубконтоКт3,
	|	НаборДвижений.ВалютаКт,
	|	НаборДвижений.ВалютнаяСуммаКт КАК ВалютнаяСуммаКт,
	|	НаборДвижений.КоличествоКт КАК КоличествоКт,
	|	НаборДвижений.Сумма КАК Сумма,
	|	НаборДвижений.СуммаПредставления КАК СуммаПредставления,
	|	НаборДвижений.ШаблонПроводки
	|ПОМЕСТИТЬ НаборДвижений
	|ИЗ
	|	&НаборДвижений КАК НаборДвижений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НаборДвижений.Период КАК Период,
	|	НаборДвижений.Регистратор,
	|	НаборДвижений.ПланСчетов,
	|	НаборДвижений.Организация,
	|	НаборДвижений.СчетДт,
	|	НаборДвижений.ПодразделениеДт,
	|	НаборДвижений.НаправлениеДеятельностиДт,
	|	НаборДвижений.ВидСубконтоДт1 КАК ВидСубконтоДт1,
	|	НаборДвижений.СубконтоДт1,
	|	НаборДвижений.ВидСубконтоДт2 КАК ВидСубконтоДт2,
	|	НаборДвижений.СубконтоДт2,
	|	НаборДвижений.ВидСубконтоДт3 КАК ВидСубконтоДт3,
	|	НаборДвижений.СубконтоДт3,
	|	НаборДвижений.ВалютаДт,
	|	НаборДвижений.ВалютнаяСуммаДт КАК ВалютнаяСуммаДт,
	|	НаборДвижений.КоличествоДт КАК КоличествоДт,
	|	НаборДвижений.СчетКт,
	|	НаборДвижений.ПодразделениеКт,
	|	НаборДвижений.НаправлениеДеятельностиКт,
	|	НаборДвижений.ВидСубконтоКт1 КАК ВидСубконтоКт1,
	|	НаборДвижений.СубконтоКт1,
	|	НаборДвижений.ВидСубконтоКт2 КАК ВидСубконтоКт2,
	|	НаборДвижений.СубконтоКт2,
	|	НаборДвижений.ВидСубконтоКт3 КАК ВидСубконтоКт3,
	|	НаборДвижений.СубконтоКт3,
	|	НаборДвижений.ВалютаКт,
	|	НаборДвижений.ВалютнаяСуммаКт КАК ВалютнаяСуммаКт,
	|	НаборДвижений.КоличествоКт КАК КоличествоКт,
	|	НаборДвижений.Сумма КАК Сумма,
	|	НаборДвижений.СуммаПредставления КАК СуммаПредставления,
	|	НаборДвижений.ШаблонПроводки,
	|	ВЫБОР
	|		КОГДА НаборДвижений.Период > ЕстьNULL(ДатыЗапретаПоОрганизациям.ДатаЗапрета, ДатаВремя(1,1,1)) 
	|			ТОГДА НаборДвижений.Период
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(ДатыЗапретаПоОрганизациям.ДатаЗапрета, СЕКУНДА, 1)
	|	КОНЕЦ КАК ПериодКорректировки
	|ПОМЕСТИТЬ НовыеПроводки
	|ИЗ
	|	НаборДвижений КАК НаборДвижений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыЗапретаПоОрганизациям КАК ДатыЗапретаПоОрганизациям
	|		ПО НаборДвижений.Организация = ДатыЗапретаПоОрганизациям.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИсходныеПроводки.Регистратор
	|ИЗ
	|	ИсходныеПроводки КАК ИсходныеПроводки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НовыеПроводки.Регистратор
	|ИЗ
	|	НовыеПроводки КАК НовыеПроводки
	|ГДЕ
	|	НовыеПроводки.Период < НовыеПроводки.ПериодКорректировки";
	
КонецФункции

Функция ЕстьНеобходимостьФормированияКорректировочныхПроводок(Регистратор, МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапросаПроверкиДатыЗапрета();
	Запрос.УстановитьПараметр("Документ", Регистратор);
	Запрос.УстановитьПараметр("НаборДвижений", МеждународныйУчетОбщегоНазначения.ВыгрузитьНаборЗаписейРегистраБухгалтерии(ЭтотОбъект));
	
	Результат = Запрос.Выполнить();
	ЕстьНеобходимость = Не Результат.Пустой();
	
	Возврат ЕстьНеобходимость;
	
КонецФункции

#КонецОбласти

#КонецЕсли