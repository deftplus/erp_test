
Функция ПроверитьВозможностьЗаписиПоДатеЗапрета()
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Организации.Ссылка КАК Организация,
	|	ВЫБОР
	|		КОГДА НЕ ДатыЗапретаПоОрганизации.ДатаЗапрета ЕСТЬ NULL
	|			ТОГДА КОНЕЦПЕРИОДА(ДатыЗапретаПоОрганизации.ДатаЗапрета, ДЕНЬ)
	|		КОГДА НЕ ДатыЗапретаОбщая.ДатаЗапрета ЕСТЬ NULL
	|			ТОГДА КОНЕЦПЕРИОДА(ДатыЗапретаОбщая.ДатаЗапрета, ДЕНЬ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК ДатаЗапрета
	|ПОМЕСТИТЬ ДатыЗапретаПоОрганизациям
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыЗапретаФормированияПроводокМеждународныйУчет КАК ДатыЗапретаПоОрганизации
	|		ПО Организации.Ссылка = ДатыЗапретаПоОрганизации.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыЗапретаФормированияПроводокМеждународныйУчет КАК ДатыЗапретаОбщая
	|		ПО (ДатыЗапретаОбщая.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|ГДЕ
	|	ВЫБОР
	|			КОГДА НЕ ДатыЗапретаПоОрганизации.ДатаЗапрета ЕСТЬ NULL
	|				ТОГДА КОНЕЦПЕРИОДА(ДатыЗапретаПоОрганизации.ДатаЗапрета, ДЕНЬ)
	|			КОГДА НЕ ДатыЗапретаОбщая.ДатаЗапрета ЕСТЬ NULL
	|				ТОГДА КОНЕЦПЕРИОДА(ДатыЗапретаОбщая.ДатаЗапрета, ДЕНЬ)
	|			ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|		КОНЕЦ <> ДАТАВРЕМЯ(1, 1, 1)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НаборДвижений.Период КАК Период,
	|	НаборДвижений.Организация КАК Организация
	|ПОМЕСТИТЬ ПериодыОрганизации
	|ИЗ
	|	&НаборДвижений КАК НаборДвижений
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПериодыОрганизации.Период КАК Период,
	|	ПериодыОрганизации.Организация КАК Организация
	|ИЗ
	|	ПериодыОрганизации КАК ПериодыОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыЗапретаПоОрганизациям КАК ДатыЗапретаПоОрганизациям
	|		ПО ПериодыОрганизации.Организация = ДатыЗапретаПоОрганизациям.Организация
	|ГДЕ
	|	ПериодыОрганизации.Период <= ЕСТЬNULL(ДатыЗапретаПоОрганизациям.ДатаЗапрета, ДАТАВРЕМЯ(1, 1, 1))";
	
	Запрос.УстановитьПараметр("НаборДвижений",ЭтотОбъект.Выгрузить(,"Период,Организация"));
	
	Результат=Запрос.Выполнить().Выбрать();
	
	Если Результат.Следующий() Тогда
		
		Возврат СтрШаблон(Нстр("ru = 'Ддя организации %1 и периода %2 установлен запрет на формирование проводок по МСФО.'"),Результат.Организация,Результат.Период)
			+Символы.ПС+СтрШаблон(Нстр("ru = 'Проведение документа %1 отменено.'"),ЭтотОбъект.Отбор.Регистратор.Значение);
			
	Иначе
		
		Возврат "";
		
	КонецЕсли;
				
КонецФункции // ПроверитьВозможностьЗаписи() 

Процедура ПередЗаписью(Отказ, РежимЗаписи)
		
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстОшибки=ПроверитьВозможностьЗаписиПоДатеЗапрета();
	
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		
		ОбщегоНазначенияУХ.СообщитьОбОшибке(ТекстОшибки,Отказ,,СтатусСообщения.Важное);
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры
