
&ИзменениеИКонтроль("ПолучитьТекстЗапросаОстаткиИОбороты")
Функция ллл_ПолучитьТекстЗапросаОстаткиИОбороты(Параметры)

	МассивПодзапросы = Новый Массив;

	МассивПодзапросы.Добавить(
	"ВЫБРАТЬ
	|	МатрицаВесовЛиквидности.Источник КАК Источник,
	|	МатрицаВесовЛиквидности.ПриходРасход КАК ПриходРасход,
	|	МатрицаВесовЛиквидности.ГоризонтПланированияЛиквидности КАК ГоризонтПланированияЛиквидности,
	|	МатрицаВесовЛиквидности.Вес КАК Вес
	|ПОМЕСТИТЬ ВТ_МатрицаВесовЛиквидности
	|ИЗ
	|	&МатрицаВесовЛиквидности КАК МатрицаВесовЛиквидности
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВерсииРасчетовСрезПоследних.ПредметГрафика КАК ПредметГрафика,
	|	ВерсииРасчетовСрезПоследних.Организация КАК Организация,
	|	ВерсииРасчетовСрезПоследних.ПредметГрафика.ДатаПервогоТранша КАК ДатаНачала,
	|	ВерсииРасчетовСрезПоследних.ПредметГрафика.ДатаПоследнегоПлатежа КАК СрокДействия,
	|	ВерсииРасчетовСрезПоследних.ПредметГрафика.СуммаТраншей КАК Стоимость,
	|	ВерсииРасчетовСрезПоследних.ПредметГрафика.БанковскийСчет КАК СчетОрганизации,
	|	ВерсииРасчетовСрезПоследних.ПредметГрафика.ВидДоговораУХ = ЗНАЧЕНИЕ(Справочник.ВидыДоговоровКонтрагентовУХ.Овердрафт) КАК ЭтоОвердрафт,
	|	ВерсииРасчетовСрезПоследних.ПредметГрафика.ВидДоговораУХ = ЗНАЧЕНИЕ(Справочник.ВидыДоговоровКонтрагентовУХ.МинимальныйНеснижаемыйОстаток) КАК ЭтоМНО
	|ПОМЕСТИТЬ ВТ_ОвердрафтыИМНО
	|ИЗ
	|	РегистрСведений.ВерсииРасчетов.СрезПоследних КАК ВерсииРасчетовСрезПоследних
	|ГДЕ
	|	ВерсииРасчетовСрезПоследних.ПредметГрафика.ВидДоговораУХ В (ЗНАЧЕНИЕ(Справочник.ВидыДоговоровКонтрагентовУХ.Овердрафт), ЗНАЧЕНИЕ(Справочник.ВидыДоговоровКонтрагентовУХ.МинимальныйНеснижаемыйОстаток))
	|	И ВерсииРасчетовСрезПоследних.ПредметГрафика ССЫЛКА Справочник.ДоговорыКредитовИДепозитов
	|	И ВерсииРасчетовСрезПоследних.ПредметГрафика.БанковскийСчет <> &ПустойБанковскийСчет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВходныеДанныеНесгруппированные.Организация КАК Организация,
	|	ВходныеДанныеНесгруппированные.ПулЛиквидности КАК ПулЛиквидности,
	|	ВходныеДанныеНесгруппированные.Валюта КАК Валюта,
	|	ВходныеДанныеНесгруппированные.БанковскийСчет КАК БанковскийСчет,
	|	ВходныеДанныеНесгруппированные.Банк КАК Банк,
	|	ВходныеДанныеНесгруппированные.ДатаПредставление КАК ДатаПредставление,
	|	СУММА(ВходныеДанныеНесгруппированные.Поступление) КАК Поступление,
	|	СУММА(ВходныеДанныеНесгруппированные.Списание) КАК Списание,
	|	СУММА(ВходныеДанныеНесгруппированные.ЕстьПросрочки) КАК ЕстьПросрочки,
	|	СУММА(ВходныеДанныеНесгруппированные.СуммаБезОвердрафта) КАК СуммаБезОвердрафта,
	|	СУММА(ВходныеДанныеНесгруппированные.СуммаСМно) КАК СуммаСМно
	|ПОМЕСТИТЬ ВТ_ВходныеДанные
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПредставлениеПлатежногоКалендаря.Организация КАК Организация,
	|		ПредставлениеПлатежногоКалендаря.ПулЛиквидности КАК ПулЛиквидности,
	|		ПредставлениеПлатежногоКалендаря.Валюта КАК Валюта,
	|		ПредставлениеПлатежногоКалендаря.БанковскийСчет КАК БанковскийСчет,
	|		ПредставлениеПлатежногоКалендаря.Банк КАК Банк,
	|		ВЫБОР
	|			КОГДА ПредставлениеПлатежногоКалендаря.Дата < &НачалоПериодаОтбора
	|				ТОГДА ДОБАВИТЬКДАТЕ(&НачалоПериодаОтбора, ДЕНЬ, -1)
	|			КОГДА ПредставлениеПлатежногоКалендаря.Дата > &ГоризонтТактическогоПланирования
	|				ТОГДА НАЧАЛОПЕРИОДА(ПредставлениеПлатежногоКалендаря.Дата, МЕСЯЦ)
	|			КОГДА ПредставлениеПлатежногоКалендаря.Дата > &ГоризонтОперативногоПланирования
	|				ТОГДА ДОБАВИТЬКДАТЕ(&ГоризонтОперативногоПланирования, ДЕНЬ, 1)
	|			ИНАЧЕ ПредставлениеПлатежногоКалендаря.Дата
	|		КОНЕЦ КАК ДатаПредставление,
	|		ВЫБОР
	|			КОГДА ПредставлениеПлатежногоКалендаря.Сумма > 0
	|				ТОГДА ПредставлениеПлатежногоКалендаря.Сумма * ЕСТЬNULL(ВТ_МатрицаВесовЛиквидности.Вес, 1)
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК Поступление,
	|		ВЫБОР
	|			КОГДА ПредставлениеПлатежногоКалендаря.Сумма < 0
	|				ТОГДА ПредставлениеПлатежногоКалендаря.Сумма * ЕСТЬNULL(ВТ_МатрицаВесовЛиквидности.Вес, 1)
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК Списание,
	|		ВЫБОР
	|			КОГДА ПредставлениеПлатежногоКалендаря.РазделПлатежногоКалендаря <> ЗНАЧЕНИЕ(Перечисление.РазделыПлатежногоКалендаря.ВходящийОстаток)
	|						И ПредставлениеПлатежногоКалендаря.Дата < &НачалоПериодаОтбора
	|					ИЛИ ПредставлениеПлатежногоКалендаря.КрайняяДата <> ДАТАВРЕМЯ(1, 1, 1)
	|						И ПредставлениеПлатежногоКалендаря.Дата > ПредставлениеПлатежногоКалендаря.КрайняяДата
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ЕстьПросрочки,
	|		ПредставлениеПлатежногоКалендаря.Сумма * ЕСТЬNULL(ВТ_МатрицаВесовЛиквидности.Вес, 1) КАК СуммаБезОвердрафта,
	|		ПредставлениеПлатежногоКалендаря.Сумма * ЕСТЬNULL(ВТ_МатрицаВесовЛиквидности.Вес, 1) КАК СуммаСМно
	|	ИЗ
	|		РегистрСведений.ПредставлениеПлатежногоКалендаря КАК ПредставлениеПлатежногоКалендаря
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МатрицаВесовЛиквидности КАК ВТ_МатрицаВесовЛиквидности
	|			ПО ПредставлениеПлатежногоКалендаря.ИсточникДанных = ВТ_МатрицаВесовЛиквидности.Источник
	|				И (ВЫБОР
	|					КОГДА ПредставлениеПлатежногоКалендаря.Сумма > 0
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Приход)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Расход)
	|				КОНЕЦ = ВТ_МатрицаВесовЛиквидности.ПриходРасход)
	|				И (ВЫБОР
	|					КОГДА ПредставлениеПлатежногоКалендаря.Дата > &ГоризонтТактическогоПланирования
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ГоризонтыПланированияЛиквидности.ПерспективноеПланирование)
	|					КОГДА ПредставлениеПлатежногоКалендаря.Дата > &ГоризонтОперативногоПланирования
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ГоризонтыПланированияЛиквидности.ТактическоеПланирование)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ГоризонтыПланированияЛиквидности.ОперативноеПланирование)
	|				КОНЕЦ = ВТ_МатрицаВесовЛиквидности.ГоризонтПланированияЛиквидности)
	|	ГДЕ
	|		ПредставлениеПлатежногоКалендаря.Пользователь = &Пользователь
	|		И ПредставлениеПлатежногоКалендаря.Активна
	|		И ПредставлениеПлатежногоКалендаря.ВерсияПлатежногоКалендаря = &ВерсияПлатежногоКалендаря
	|		И ЕСТЬNULL(ВТ_МатрицаВесовЛиквидности.Вес, 1) <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_ОвердрафтыИМНО.Организация,
	|		ВТ_ОвердрафтыИМНО.СчетОрганизации.ПулЛиквидности,
	|		ВТ_ОвердрафтыИМНО.СчетОрганизации.ВалютаДенежныхСредств,
	|		ВТ_ОвердрафтыИМНО.СчетОрганизации,
	|		ВТ_ОвердрафтыИМНО.СчетОрганизации.Банк,
	|		ВЫБОР
	|			КОГДА ВТ_ОвердрафтыИМНО.ДатаНачала > &ГоризонтТактическогоПланирования
	|				ТОГДА НАЧАЛОПЕРИОДА(ВТ_ОвердрафтыИМНО.ДатаНачала, МЕСЯЦ)
	|			КОГДА ВТ_ОвердрафтыИМНО.ДатаНачала > &ГоризонтОперативногоПланирования
	|				ТОГДА ДОБАВИТЬКДАТЕ(&ГоризонтОперативногоПланирования, ДЕНЬ, 1)
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ВТ_ОвердрафтыИМНО.ДатаНачала < &НачалоПериодаОтбора
	|						ТОГДА ДОБАВИТЬКДАТЕ(&НачалоПериодаОтбора, ДЕНЬ, -1)
	|					ИНАЧЕ ВТ_ОвердрафтыИМНО.ДатаНачала
	|				КОНЕЦ
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА &УчитыватьОвердрафты
	|					И ВТ_ОвердрафтыИМНО.ЭтоОвердрафт
	|				ТОГДА ВТ_ОвердрафтыИМНО.Стоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА &УчитыватьОвердрафты
	|					И ВТ_ОвердрафтыИМНО.ЭтоМНО
	|				ТОГДА -ВТ_ОвердрафтыИМНО.Стоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		0,
	|		0,
	|		ВЫБОР
	|			КОГДА ВТ_ОвердрафтыИМНО.ЭтоМНО
	|				ТОГДА -ВТ_ОвердрафтыИМНО.Стоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		ВТ_ОвердрафтыИМНО КАК ВТ_ОвердрафтыИМНО
	#Вставка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗаявкаДок.Ссылка.Организация,
	|		ЗНАЧЕНИЕ(Справочник.ПулыЛиквидности.ПустаяСсылка),
	|		ЗаявкаДок.Ссылка.Валюта,
	|		ЗаявкаДок.Ссылка.БанковскийСчет,
	|		ЗаявкаДок.Ссылка.БанковскийСчет.Банк,
	|		ВЫБОР
	|			КОГДА ЗаявкаДок.Ссылка.ЖелательнаяДатаПлатежа < &НачалоПериодаОтбора
	|				ТОГДА ДОБАВИТЬКДАТЕ(&НачалоПериодаОтбора, ДЕНЬ, -1)
	|			КОГДА ЗаявкаДок.Ссылка.ЖелательнаяДатаПлатежа > &ГоризонтТактическогоПланирования
	|				ТОГДА НАЧАЛОПЕРИОДА(ЗаявкаДок.Ссылка.ЖелательнаяДатаПлатежа, МЕСЯЦ)
	|			КОГДА ЗаявкаДок.Ссылка.ЖелательнаяДатаПлатежа > &ГоризонтОперативногоПланирования
	|				ТОГДА ДОБАВИТЬКДАТЕ(&ГоризонтОперативногоПланирования, ДЕНЬ, 1)
	|			ИНАЧЕ ЗаявкаДок.Ссылка.ЖелательнаяДатаПлатежа
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ЗаявкаДок.Сумма > 0
	|				ТОГДА ЗаявкаДок.Сумма * ЕСТЬNULL(ВТ_МатрицаВесовЛиквидности.Вес, 1)
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ЗаявкаДок.Сумма < 0
	|				ТОГДА ЗаявкаДок.Сумма * ЕСТЬNULL(ВТ_МатрицаВесовЛиквидности.Вес, 1)
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		0,
	|		ЗаявкаДок.Сумма * ЕСТЬNULL(ВТ_МатрицаВесовЛиквидности.Вес, 1),
	|		ЗаявкаДок.Сумма * ЕСТЬNULL(ВТ_МатрицаВесовЛиквидности.Вес, 1)
	|	ИЗ
	|		Документ.ЗаявкаНаРасходованиеДенежныхСредств.РасшифровкаПлатежа КАК ЗаявкаДок
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МатрицаВесовЛиквидности КАК ВТ_МатрицаВесовЛиквидности
	|			ПО (ВТ_МатрицаВесовЛиквидности.Источник = ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхПлатежногоКалендаря.ллл_ЗаявкиЧерновики))
	|				И (ВЫБОР
	|					КОГДА ЗаявкаДок.Сумма > 0
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Приход)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПриходРасход.Расход)
	|				КОНЕЦ = ВТ_МатрицаВесовЛиквидности.ПриходРасход)
	|				И (ВЫБОР
	|					КОГДА ЗаявкаДок.Ссылка.ЖелательнаяДатаПлатежа > &ГоризонтТактическогоПланирования
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ГоризонтыПланированияЛиквидности.ПерспективноеПланирование)
	|					КОГДА ЗаявкаДок.Ссылка.ЖелательнаяДатаПлатежа > &ГоризонтОперативногоПланирования
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ГоризонтыПланированияЛиквидности.ТактическоеПланирование)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ГоризонтыПланированияЛиквидности.ОперативноеПланирование)
	|				КОНЕЦ = ВТ_МатрицаВесовЛиквидности.ГоризонтПланированияЛиквидности)
	|	ГДЕ
	|		НЕ ЗаявкаДок.Ссылка.Проведен
	|		И НЕ ЗаявкаДок.Ссылка.ПометкаУдаления
	|		И ЕСТЬNULL(ВТ_МатрицаВесовЛиквидности.Вес, 1) <> 0
	#КонецВставки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_ОвердрафтыИМНО.Организация,
	|		ВТ_ОвердрафтыИМНО.СчетОрганизации.ПулЛиквидности,
	|		ВТ_ОвердрафтыИМНО.СчетОрганизации.ВалютаДенежныхСредств,
	|		ВТ_ОвердрафтыИМНО.СчетОрганизации,
	|		ВТ_ОвердрафтыИМНО.СчетОрганизации.Банк,
	|		ВЫБОР
	|			КОГДА ВТ_ОвердрафтыИМНО.СрокДействия > &ГоризонтТактическогоПланирования
	|				ТОГДА НАЧАЛОПЕРИОДА(ВТ_ОвердрафтыИМНО.СрокДействия, МЕСЯЦ)
	|			КОГДА ВТ_ОвердрафтыИМНО.СрокДействия > &ГоризонтОперативногоПланирования
	|				ТОГДА ДОБАВИТЬКДАТЕ(&ГоризонтОперативногоПланирования, ДЕНЬ, 1)
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ВТ_ОвердрафтыИМНО.СрокДействия < &НачалоПериодаОтбора
	|						ТОГДА ДОБАВИТЬКДАТЕ(&НачалоПериодаОтбора, ДЕНЬ, -1)
	|					ИНАЧЕ ВТ_ОвердрафтыИМНО.СрокДействия
	|				КОНЕЦ
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА &УчитыватьОвердрафты
	|					И ВТ_ОвердрафтыИМНО.ЭтоМНО
	|				ТОГДА ВТ_ОвердрафтыИМНО.Стоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА &УчитыватьОвердрафты
	|					И ВТ_ОвердрафтыИМНО.ЭтоОвердрафт
	|				ТОГДА -ВТ_ОвердрафтыИМНО.Стоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		0,
	|		0,
	|		ВЫБОР
	|			КОГДА ВТ_ОвердрафтыИМНО.ЭтоМНО
	|				ТОГДА ВТ_ОвердрафтыИМНО.Стоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		ВТ_ОвердрафтыИМНО КАК ВТ_ОвердрафтыИМНО
	|	ГДЕ
	|		&УчитыватьОвердрафты
	|		И ВТ_ОвердрафтыИМНО.СрокДействия <= &ОкончаниеПериодаОтбора) КАК ВходныеДанныеНесгруппированные
	|
	|СГРУППИРОВАТЬ ПО
	|	ВходныеДанныеНесгруппированные.Организация,
	|	ВходныеДанныеНесгруппированные.ПулЛиквидности,
	|	ВходныеДанныеНесгруппированные.Валюта,
	|	ВходныеДанныеНесгруппированные.БанковскийСчет,
	|	ВходныеДанныеНесгруппированные.Банк,
	|	ВходныеДанныеНесгруппированные.ДатаПредставление
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ВходныеДанные.ДатаПредставление КАК ДеньВходныхДанных
	|ПОМЕСТИТЬ ВТ_ВходныеДаты
	|ИЗ
	|	ВТ_ВходныеДанные КАК ВТ_ВходныеДанные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыЗаПериод.ДатаКалендарь КАК ДатаКалендарь,
	|	СУММА(ДатыЗаПериод.ЭтоРабочийДень) > 0 КАК ЭтоРабочийДень
	|ПОМЕСТИТЬ ВТ_РабочиеДни
	|ИЗ
	|	(ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&НачалоПериодаОтбора, ДЕНЬ, -1), ДЕНЬ) КАК ДатаКалендарь,
	|		1 КАК ЭтоРабочийДень
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ВЫБОР
	|			КОГДА ДанныеПроизводственногоКалендаря.Дата > &ГоризонтТактическогоПланирования
	|					И &ГоризонтТактическогоПланирования <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА НАЧАЛОПЕРИОДА(ДанныеПроизводственногоКалендаря.Дата, МЕСЯЦ)
	|			КОГДА ДанныеПроизводственногоКалендаря.Дата > &ГоризонтОперативногоПланирования
	|					И &ГоризонтОперативногоПланирования <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ДОБАВИТЬКДАТЕ(&ГоризонтОперативногоПланирования, ДЕНЬ, 1)
	|			ИНАЧЕ ДанныеПроизводственногоКалендаря.Дата
	|		КОНЕЦ,
	|		1
	|	ИЗ
	|		РегистрСведений.ДанныеПроизводственногоКалендаря КАК ДанныеПроизводственногоКалендаря
	|	ГДЕ
	|		ДанныеПроизводственногоКалендаря.ПроизводственныйКалендарь = &ПроизводственныйКалендарь
	|		И ДанныеПроизводственногоКалендаря.Дата МЕЖДУ &НачалоПериодаОтбора И &ОкончаниеПериодаОтбора
	|		И ДанныеПроизводственногоКалендаря.ВидДня В (ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Рабочий), ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Предпраздничный))
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ДанныеПроизводственногоКалендаря.Дата,
	|		0
	|	ИЗ
	|		РегистрСведений.ДанныеПроизводственногоКалендаря КАК ДанныеПроизводственногоКалендаря
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ВходныеДаты КАК ВТ_ВходныеДаты
	|			ПО ДанныеПроизводственногоКалендаря.Дата = ВТ_ВходныеДаты.ДеньВходныхДанных
	|				И (ДанныеПроизводственногоКалендаря.ПроизводственныйКалендарь = &ПроизводственныйКалендарь)
	|				И (НЕ ДанныеПроизводственногоКалендаря.ВидДня В (ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Рабочий), ЗНАЧЕНИЕ(Перечисление.ВидыДнейПроизводственногоКалендаря.Предпраздничный)))) КАК ДатыЗаПериод
	|
	|СГРУППИРОВАТЬ ПО
	|	ДатыЗаПериод.ДатаКалендарь
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОборотыДополненныеНесгруппированные.Дата КАК Дата,
	|	МАКСИМУМ(ОборотыДополненныеНесгруппированные.ЭтоРабочийДень) КАК ЭтоРабочийДень,
	|	ОборотыДополненныеНесгруппированные.Организация КАК Организация,
	|	ОборотыДополненныеНесгруппированные.ПулЛиквидности КАК ПулЛиквидности,
	|	ОборотыДополненныеНесгруппированные.Валюта КАК Валюта,
	|	ОборотыДополненныеНесгруппированные.БанковскийСчет КАК БанковскийСчет,
	|	ОборотыДополненныеНесгруппированные.Банк КАК Банк,
	|	СУММА(ОборотыДополненныеНесгруппированные.Поступление) КАК Поступление,
	|	СУММА(ОборотыДополненныеНесгруппированные.Списание) КАК Списание,
	|	СУММА(ОборотыДополненныеНесгруппированные.ЕстьПросрочки) КАК ЕстьПросрочки,
	|	СУММА(ОборотыДополненныеНесгруппированные.СуммаБезОвердрафта) КАК СуммаБезОвердрафта,
	|	СУММА(ОборотыДополненныеНесгруппированные.СуммаСМно) КАК СуммаСМно
	|ПОМЕСТИТЬ ВТ_ОборотыСгруппированные
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТ_РабочиеДни.ДатаКалендарь КАК Дата,
	|		ВТ_РабочиеДни.ЭтоРабочийДень КАК ЭтоРабочийДень,
	|		ВсеДоступныеИзмерения.Организация КАК Организация,
	|		ВсеДоступныеИзмерения.ПулЛиквидности КАК ПулЛиквидности,
	|		ВсеДоступныеИзмерения.Валюта КАК Валюта,
	|		ВсеДоступныеИзмерения.БанковскийСчет КАК БанковскийСчет,
	|		ВсеДоступныеИзмерения.Банк КАК Банк,
	|		0 КАК Поступление,
	|		0 КАК Списание,
	|		0 КАК ЕстьПросрочки,
	|		0 КАК СуммаБезОвердрафта,
	|		0 КАК СуммаСМно
	|	ИЗ
	|		(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ВТ_ВходныеДанные.Организация КАК Организация,
	|			ВТ_ВходныеДанные.ПулЛиквидности КАК ПулЛиквидности,
	|			ВТ_ВходныеДанные.Валюта КАК Валюта,
	|			ВТ_ВходныеДанные.БанковскийСчет КАК БанковскийСчет,
	|			ВТ_ВходныеДанные.Банк КАК Банк
	|		ИЗ
	|			ВТ_ВходныеДанные КАК ВТ_ВходныеДанные) КАК ВсеДоступныеИзмерения,
	|		ВТ_РабочиеДни КАК ВТ_РабочиеДни
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_ВходныеДанные.ДатаПредставление,
	|		ИСТИНА,
	|		ВТ_ВходныеДанные.Организация,
	|		ВТ_ВходныеДанные.ПулЛиквидности,
	|		ВТ_ВходныеДанные.Валюта,
	|		ВТ_ВходныеДанные.БанковскийСчет,
	|		ВТ_ВходныеДанные.Банк,
	|		ВТ_ВходныеДанные.Поступление,
	|		ВТ_ВходныеДанные.Списание,
	|		ВТ_ВходныеДанные.ЕстьПросрочки,
	|		ВТ_ВходныеДанные.СуммаБезОвердрафта,
	|		ВТ_ВходныеДанные.СуммаСМно
	|	ИЗ
	|		ВТ_ВходныеДанные КАК ВТ_ВходныеДанные) КАК ОборотыДополненныеНесгруппированные
	|
	|СГРУППИРОВАТЬ ПО
	|	ОборотыДополненныеНесгруппированные.Дата,
	|	ОборотыДополненныеНесгруппированные.Организация,
	|	ОборотыДополненныеНесгруппированные.ПулЛиквидности,
	|	ОборотыДополненныеНесгруппированные.Валюта,
	|	ОборотыДополненныеНесгруппированные.БанковскийСчет,
	|	ОборотыДополненныеНесгруппированные.Банк
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОборотыСгруппированные.БанковскийСчет КАК БанковскийСчет,
	|	ВТ_ОборотыСгруппированные.Валюта КАК Валюта,
	|	ВТ_ОборотыСгруппированные.Банк КАК Банк,
	|	ВТ_ОборотыСгруппированные.Дата КАК Дата,
	|	ВТ_ОборотыСгруппированные.ЭтоРабочийДень КАК ЭтоРабочийДень,
	|	ВТ_ОборотыСгруппированные.Организация КАК Организация,
	|	ВТ_ОборотыСгруппированные.ПулЛиквидности КАК ПулЛиквидности,
	|	ВЫБОР
	|		КОГДА ВТ_ОборотыСгруппированные.Дата < &НачалоПериодаОтбора
	|			ТОГДА 0
	|		ИНАЧЕ ВТ_ОборотыСгруппированные.Поступление
	|	КОНЕЦ КАК Поступление,
	|	ВЫБОР
	|		КОГДА ВТ_ОборотыСгруппированные.Дата < &НачалоПериодаОтбора
	|			ТОГДА 0
	|		ИНАЧЕ ВТ_ОборотыСгруппированные.Списание
	|	КОНЕЦ КАК Списание,
	|	ВТ_ОборотыСгруппированные.ЕстьПросрочки КАК ЕстьПросрочки,
	|	ЕСТЬNULL(СУММА(Остатки.Поступление + Остатки.Списание), 0) КАК Остаток,
	|	ЕСТЬNULL(СУММА(Остатки.СуммаБезОвердрафта), 0) КАК СуммаБезОвердрафта,
	|	ЕСТЬNULL(СУММА(Остатки.СуммаСМно), 0) КАК СуммаСМно
	|ПОМЕСТИТЬ ВТ_ДанныеКалендаря
	|ИЗ
	|	ВТ_ОборотыСгруппированные КАК ВТ_ОборотыСгруппированные
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОборотыСгруппированные КАК Остатки
	|		ПО ВТ_ОборотыСгруппированные.БанковскийСчет = Остатки.БанковскийСчет
	|			И ВТ_ОборотыСгруппированные.Валюта = Остатки.Валюта
	|			И ВТ_ОборотыСгруппированные.Организация = Остатки.Организация
	|			И ВТ_ОборотыСгруппированные.Банк = Остатки.Банк
	|			И ВТ_ОборотыСгруппированные.ПулЛиквидности = Остатки.ПулЛиквидности
	|			И ВТ_ОборотыСгруппированные.Дата >= Остатки.Дата
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ОборотыСгруппированные.БанковскийСчет,
	|	ВТ_ОборотыСгруппированные.Валюта,
	|	ВТ_ОборотыСгруппированные.Банк,
	|	ВТ_ОборотыСгруппированные.Дата,
	|	ВТ_ОборотыСгруппированные.Организация,
	|	ВТ_ОборотыСгруппированные.ПулЛиквидности,
	|	ВТ_ОборотыСгруппированные.ЭтоРабочийДень,
	|	ВТ_ОборотыСгруппированные.Поступление,
	|	ВТ_ОборотыСгруппированные.Списание,
	|	ВТ_ОборотыСгруппированные.ЕстьПросрочки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПредставлениеПлатежногоКалендаря.БанковскийСчет КАК БанковскийСчет,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ПредставлениеПлатежногоКалендаря.Сумма <> 0
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьДвижения
	|ПОМЕСТИТЬ ВТ_ЕстьДвиженияПоСчетам
	|ИЗ
	|	РегистрСведений.ПредставлениеПлатежногоКалендаря КАК ПредставлениеПлатежногоКалендаря
	|ГДЕ
	|	ПредставлениеПлатежногоКалендаря.Пользователь = &Пользователь
	|	И ПредставлениеПлатежногоКалендаря.Активна
	|	И ПредставлениеПлатежногоКалендаря.ВерсияПлатежногоКалендаря = &ВерсияПлатежногоКалендаря
	|
	|СГРУППИРОВАТЬ ПО
	|	ПредставлениеПлатежногоКалендаря.БанковскийСчет
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	БанковскийСчет"
	);

	Если Параметры.ВыводитьВВалютеОтображения Тогда

		МассивПодзапросы.Добавить(		
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ТЗ_ПлановыеКурсы.Валюта КАК Справочник.Валюты) КАК Валюта,
		|	ВЫРАЗИТЬ(ТЗ_ПлановыеКурсы.Период КАК ДАТА) КАК Период,
		|	ВЫРАЗИТЬ(ТЗ_ПлановыеКурсы.Курс КАК ЧИСЛО(15, 4)) КАК Курс,
		|	ВЫРАЗИТЬ(ТЗ_ПлановыеКурсы.Кратность КАК ЧИСЛО(15, 0)) КАК Кратность
		|ПОМЕСТИТЬ ТЗ_ПлановыеКурсы
		|ИЗ
		|	&ТЗ_ПлановыеКурсы КАК ТЗ_ПлановыеКурсы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТ_ДанныеКалендаря.Валюта КАК Валюта,
		|	ВТ_ДанныеКалендаря.Дата КАК ДатаКалендарь
		|ПОМЕСТИТЬ ВТ_Валюты
		|ИЗ
		|	ВТ_ДанныеКалендаря КАК ВТ_ДанныеКалендаря
		|ГДЕ
		|	НЕ ВТ_ДанныеКалендаря.Валюта ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КурсыВалют.Период КАК Период,
		|	КурсыВалют.Валюта КАК Валюта,
		|	КурсыВалют.Курс КАК Курс,
		|	КурсыВалют.Кратность КАК Кратность
		|ПОМЕСТИТЬ ВТ_ДанныеКурсов
		|ИЗ
		|	РегистрСведений.КурсыВалют КАК КурсыВалют
		|ГДЕ
		|	КурсыВалют.Период МЕЖДУ &НачалоПериодаОтбора И &ОкончаниеПериодаОтбора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЕСТЬNULL(ТЗ_ПлановыеКурсы.Период, ВТ_ДанныеКурсов.Период) КАК Период,
		|	ЕСТЬNULL(ТЗ_ПлановыеКурсы.Валюта, ВТ_ДанныеКурсов.Валюта) КАК Валюта,
		|	ЕСТЬNULL(ТЗ_ПлановыеКурсы.Курс, ВТ_ДанныеКурсов.Курс) КАК Курс,
		|	ЕСТЬNULL(ТЗ_ПлановыеКурсы.Кратность, ВТ_ДанныеКурсов.Кратность) КАК Кратность
		|ПОМЕСТИТЬ ВТ_АктуальныеКурсы
		|ИЗ
		|	ВТ_ДанныеКурсов КАК ВТ_ДанныеКурсов
		|		ПОЛНОЕ СОЕДИНЕНИЕ ТЗ_ПлановыеКурсы КАК ТЗ_ПлановыеКурсы
		|		ПО ВТ_ДанныеКурсов.Валюта = ТЗ_ПлановыеКурсы.Валюта
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КурсыВалютСрезПоследних.Период,
		|	КурсыВалютСрезПоследних.Валюта,
		|	КурсыВалютСрезПоследних.Курс,
		|	КурсыВалютСрезПоследних.Кратность
		|ИЗ
		|	РегистрСведений.КурсыВалют.СрезПоследних(ДОБАВИТЬКДАТЕ(&НачалоПериодаОтбора, ДЕНЬ, -1), ) КАК КурсыВалютСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатыКурсов.Валюта КАК Валюта,
		|	ДатыКурсов.ДатаКалендарь КАК ДатаКалендарь,
		|	ВЫБОР
		|		КОГДА ДатыКурсов.Валюта = &ВалютаОтображения
		|			ТОГДА 1
		|		ИНАЧЕ ЕСТЬNULL(ВТ_АктуальныеКурсы.Курс * АктуальныйКурсВалютыОтображения.Кратность / (ВТ_АктуальныеКурсы.Кратность * АктуальныйКурсВалютыОтображения.Курс), 1)
		|	КОНЕЦ КАК Коэффициент
		|ПОМЕСТИТЬ ВТ_СрезКурсов
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ВТ_Валюты.Валюта КАК Валюта,
		|		ВТ_Валюты.ДатаКалендарь КАК ДатаКалендарь,
		|		МАКСИМУМ(ВТ_АктуальныеКурсы.Период) КАК Период,
		|		МАКСИМУМ(АктуальныйКурсВалютыОтображения.Период) КАК ПериодОтображения
		|	ИЗ
		|		ВТ_Валюты КАК ВТ_Валюты
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АктуальныеКурсы КАК ВТ_АктуальныеКурсы
		|			ПО (ВТ_АктуальныеКурсы.Валюта = ВТ_Валюты.Валюта)
		|				И (ВТ_АктуальныеКурсы.Период <= ВТ_Валюты.ДатаКалендарь)
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АктуальныеКурсы КАК АктуальныйКурсВалютыОтображения
		|			ПО (АктуальныйКурсВалютыОтображения.Валюта = &ВалютаОтображения)
		|				И (АктуальныйКурсВалютыОтображения.Период <= ВТ_Валюты.ДатаКалендарь)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ВТ_Валюты.ДатаКалендарь,
		|		ВТ_Валюты.Валюта) КАК ДатыКурсов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АктуальныеКурсы КАК ВТ_АктуальныеКурсы
		|		ПО ДатыКурсов.Валюта = ВТ_АктуальныеКурсы.Валюта
		|			И ДатыКурсов.Период = ВТ_АктуальныеКурсы.Период
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АктуальныеКурсы КАК АктуальныйКурсВалютыОтображения
		|		ПО (АктуальныйКурсВалютыОтображения.Валюта = &ВалютаОтображения)
		|			И ДатыКурсов.ПериодОтображения = АктуальныйКурсВалютыОтображения.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДанныеКалендаря.БанковскийСчет КАК БанковскийСчет,
		|	ВТ_ДанныеКалендаря.Валюта КАК Валюта,
		|	ВТ_ДанныеКалендаря.Банк КАК Банк,
		|	ВТ_ДанныеКалендаря.Дата КАК Дата,
		|	ВТ_ДанныеКалендаря.Остаток КАК СуммаВВалютеОперации,
		|	ВТ_ДанныеКалендаря.Поступление КАК ПоступлениеВВалютеОперации,
		|	ВТ_ДанныеКалендаря.Списание КАК СписаниеВВалютеОперации,
		|	ВТ_ДанныеКалендаря.Организация КАК Организация,
		|	ВТ_ДанныеКалендаря.ПулЛиквидности КАК ПулЛиквидности,
		|	ВТ_ДанныеКалендаря.ЕстьПросрочки > 0 КАК ЕстьПросрочки,
		|	ЕСТЬNULL(ВТ_ДанныеКалендаря.Остаток * ВТ_СрезКурсов.Коэффициент, ВТ_ДанныеКалендаря.Остаток) КАК Сумма,
		|	ЕСТЬNULL(ВТ_ДанныеКалендаря.Поступление * ВТ_СрезКурсов.Коэффициент, ВТ_ДанныеКалендаря.Поступление) КАК Поступление,
		|	ЕСТЬNULL(ВТ_ДанныеКалендаря.Списание * ВТ_СрезКурсов.Коэффициент, ВТ_ДанныеКалендаря.Списание) КАК Списание,
		|	ВТ_ДанныеКалендаря.СуммаБезОвердрафта КАК СуммаБезОвердрафта,
		|	ВТ_ДанныеКалендаря.СуммаСМно КАК СуммаСМно,
		|	ВТ_ДанныеКалендаря.ЭтоРабочийДень КАК ЭтоРабочийДень,
		|	ДЕНЬНЕДЕЛИ(ВТ_ДанныеКалендаря.Дата) КАК ДеньНедели,
		|	ВЫБОР
		|		КОГДА ДЕНЬНЕДЕЛИ(ВТ_ДанныеКалендаря.Дата) В (&ПлатежныеДниНедели)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ПлатежныйДень,
		|	ЕСТЬNULL(ВТ_ДанныеКалендаря.БанковскийСчет.Закрыт, ЛОЖЬ) КАК СчетЗакрыт
		|ИЗ
		|	ВТ_ДанныеКалендаря КАК ВТ_ДанныеКалендаря
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СрезКурсов КАК ВТ_СрезКурсов
		|		ПО ВТ_ДанныеКалендаря.Дата = ВТ_СрезКурсов.ДатаКалендарь
		|			И ВТ_ДанныеКалендаря.Валюта = ВТ_СрезКурсов.Валюта
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЕстьДвиженияПоСчетам КАК ВТ_ЕстьДвиженияПоСчетам
		|		ПО ВТ_ДанныеКалендаря.БанковскийСчет = ВТ_ЕстьДвиженияПоСчетам.БанковскийСчет
		|ГДЕ
		|	(НЕ ЕСТЬNULL(ВТ_ДанныеКалендаря.БанковскийСчет.Закрыт, ЛОЖЬ)
		|			ИЛИ ВТ_ЕстьДвиженияПоСчетам.ЕстьДвижения)"
		);
	Иначе

		МассивПодзапросы.Добавить(
		"ВЫБРАТЬ
		|	ВТ_ДанныеКалендаря.БанковскийСчет КАК БанковскийСчет,
		|	ВТ_ДанныеКалендаря.Валюта КАК Валюта,
		|	ВТ_ДанныеКалендаря.Банк КАК Банк,
		|	ВТ_ДанныеКалендаря.Дата КАК Дата,
		|	ВТ_ДанныеКалендаря.Остаток КАК СуммаВВалютеОперации,
		|	ВТ_ДанныеКалендаря.Поступление КАК ПоступлениеВВалютеОперации,
		|	ВТ_ДанныеКалендаря.Списание КАК СписаниеВВалютеОперации,
		|	ВТ_ДанныеКалендаря.Организация КАК Организация,
		|	ВТ_ДанныеКалендаря.ПулЛиквидности КАК ПулЛиквидности,
		|	ВТ_ДанныеКалендаря.ЕстьПросрочки > 0 КАК ЕстьПросрочки,
		|	ВТ_ДанныеКалендаря.Остаток КАК Сумма,
		|	ВТ_ДанныеКалендаря.Поступление КАК Поступление,
		|	ВТ_ДанныеКалендаря.Списание КАК Списание,
		|	ВТ_ДанныеКалендаря.СуммаБезОвердрафта КАК СуммаБезОвердрафта,
		|	ВТ_ДанныеКалендаря.СуммаСМно КАК СуммаСМно,
		|	ВТ_ДанныеКалендаря.ЭтоРабочийДень КАК ЭтоРабочийДень,
		|	ДЕНЬНЕДЕЛИ(ВТ_ДанныеКалендаря.Дата) КАК ДеньНедели,
		|	ВЫБОР
		|		КОГДА ДЕНЬНЕДЕЛИ(ВТ_ДанныеКалендаря.Дата) В (&ПлатежныеДниНедели)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ПлатежныйДень,
		|	ЕСТЬNULL(ВТ_ДанныеКалендаря.БанковскийСчет.Закрыт, ЛОЖЬ) КАК СчетЗакрыт
		|ИЗ
		|	ВТ_ДанныеКалендаря КАК ВТ_ДанныеКалендаря
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЕстьДвиженияПоСчетам КАК ВТ_ЕстьДвиженияПоСчетам
		|		ПО ВТ_ДанныеКалендаря.БанковскийСчет = ВТ_ЕстьДвиженияПоСчетам.БанковскийСчет
		|ГДЕ
		|	(НЕ ЕСТЬNULL(ВТ_ДанныеКалендаря.БанковскийСчет.Закрыт, ЛОЖЬ)
		|			ИЛИ ВТ_ЕстьДвиженияПоСчетам.ЕстьДвижения)"
		);

	КонецЕсли;

	ТекстЗапроса = СтрСоединить(МассивПодзапросы, ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета());
	Если Параметры.ВыводитьТолькоКассовыеРазрывы Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	И ВТ_ДанныеКалендаря.Остаток < 0"
		;
	КонецЕсли;

	СтрПериодичность = ПериодичностьВЗапросе(Параметры.ПериодичностьОперативногоПланирования, "МЕСЯЦ");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "МЕСЯЦ", СтрПериодичность);

	Возврат ТекстЗапроса;

КонецФункции
