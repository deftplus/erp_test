// Процедура устанавливает значение параметра СКД с именем ИмяПараметраВход 
// в значение ЗначениеВход.
&НаСервере
Процедура УстановитьЗначениеПараметраСКД(ИмяПараметраВход, ЗначениеВход)
	// Найдем настройку и запомним её идентификатор.
	ЭлементыОтбора = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы;
	НайденныйИдентификатор = "----";
	Для Каждого ТекЭлементыОтбора Из ЭлементыОтбора Цикл
		Если Строка(ТекЭлементыОтбора.Параметр) = ИмяПараметраВход Тогда
			НайденныйИдентификатор = ТекЭлементыОтбора.ИдентификаторПользовательскойНастройки;
		КонецЕсли;
	КонецЦикла;
	// Изменим значение у найденной настройки.
	ЭлементыНастроек = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы;
	Для Каждого ТекЭлементыНастроек Из ЭлементыНастроек Цикл
		Если ТекЭлементыНастроек.ИдентификаторПользовательскойНастройки = НайденныйИдентификатор Тогда
			ТекЭлементыНастроек.Значение = ЗначениеВход;
			ТекЭлементыНастроек.Использование = Истина;
		Иначе
			// Выполняем поиск далее.
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("КлючевойОбъектПроцесса") Тогда
		КлючевойОбъектПроцесса = Параметры.КлючевойОбъектПроцесса;
	Иначе
		КлючевойОбъектПроцесса = Документы.ЗаявкаНаРасходованиеДенежныхСредств.ПустаяСсылка();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ЗначениеЗаполнено(КлючевойОбъектПроцесса) Тогда
		УстановитьЗначениеПараметраСКД("КлючевойОбъектПроцесса", КлючевойОбъектПроцесса);
		СкомпоноватьРезультат();
	Иначе
		// Не изменяем.
	КонецЕсли;
КонецПроцедуры
