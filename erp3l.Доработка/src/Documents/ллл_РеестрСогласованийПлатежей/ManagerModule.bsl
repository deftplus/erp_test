
Процедура Печать(ТабДок, Ссылка) Экспорт
	
	Макет = Документы.ллл_РеестрСогласованийПлатежей.ПолучитьМакет("Печать");
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Т.Ссылка КАК Ссылка,
		|	Т.Номер КАК Номер,
		|	Т.Дата КАК Дата,
		|	Т.Статус КАК Статус
		|ИЗ
		|	Документ.ллл_РеестрСогласованийПлатежей КАК Т
		|ГДЕ
		|	Т.Ссылка В(&Ссылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Ссылка.Ссылка КАК Ссылка,
		|	Т.Приоритет КАК Приоритет,
		|	Т.Заявка КАК Заявка,
		|	Т.ЗаказПоставщику КАК ЗаказПоставщику,
		|	Т.Подразделение КАК Подразделение,
		|	Т.ВидОперации КАК ВидОперации,
		|	Т.НомерДокументаЗаявкаНаОплату КАК НомерДокументаЗаявкаНаОплату,
		|	Т.ДатаДокументаЗаявкаНаОплату КАК ДатаДокументаЗаявкаНаОплату,
		|	Т.СуммаДокумента КАК СуммаДокумента,
		|	Т.ВалютаДокумента КАК ВалютаДокумента,
		|	Т.Контрагент КАК Контрагент,
		|	Т.Сумма КАК Сумма,
		|	Т.ВалютаОплаты КАК ВалютаОплаты,
		|	Т.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	Т.ПланируемаяДатаПлатежа КАК ПланируемаяДатаПлатежа,
		|	Т.КрайняяДатаПлатежа КАК КрайняяДатаПлатежа,
		|	Т.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	ВЫБОР
		|		КОГДА НЕ Т.ЕстьЗаявка
		|			ТОГДА 4
		|		КОГДА Т.ЭтоИмпорт
		|				И Т.ЭтоПредоплата
		|			ТОГДА 0
		|		КОГДА Т.ЭтоИмпорт
		|			ТОГДА 1
		|		КОГДА Т.ЭтоПредоплата
		|			ТОГДА 2
		|		ИНАЧЕ 3
		|	КОНЕЦ КАК РежимОтображения,
		|	ВЫБОР
		|		КОГДА НЕ Т.ЕстьЗаявка
		|			ТОГДА ""Импорт, предоплата (заказы поставщикам)""
		|		КОГДА Т.ЭтоИмпорт
		|				И Т.ЭтоПредоплата
		|			ТОГДА ""Импорт, предоплата""
		|		КОГДА Т.ЭтоИмпорт
		|			ТОГДА ""Импорт, постоплата""
		|		КОГДА Т.ЭтоПредоплата
		|			ТОГДА ""Российские, предоплата""
		|		ИНАЧЕ ""Российские, постоплата""
		|	КОНЕЦ КАК Раздел,
		|	Т.Организация КАК Организация
		|ИЗ
		|	Документ.ллл_РеестрСогласованийПлатежей.Платежи КАК Т
		|ГДЕ
		|	Т.Ссылка В(&Ссылка)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка,
		|	РежимОтображения,
		|	Приоритет";
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	Результат = Запрос.ВыполнитьПакет();
	Выборка = Результат[0].Выбрать();
	ТЗ = Результат[1].Выгрузить();

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	Шапка               = Макет.ПолучитьОбласть("Шапка");
	ОбластьПлатежиШапка = Макет.ПолучитьОбласть("ПлатежиШапка");
	ОбластьПлатежи      = Макет.ПолучитьОбласть("Платежи");
	ОбластьИтоги        = Макет.ПолучитьОбласть("Итоги");
	ОбластьПлатежиШапкаЗаказ = Макет.ПолучитьОбласть("ПлатежиШапкаЗаказы");
	ОбластьПлатежиЗаказ      = Макет.ПолучитьОбласть("ПлатежиЗаказы");
	ОбластьИтогиЗаказ        = Макет.ПолучитьОбласть("ИтогиЗаказы");
	ТабДок.Очистить();

	ВставлятьРазделительСтраниц = Ложь;
	Пока Выборка.Следующий() Цикл
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;

		ТабДок.Вывести(ОбластьЗаголовок);

		Шапка.Параметры.Заполнить(Выборка);
		ТабДок.Вывести(Шапка, 1);
		Для Сч = 0 По 4 Цикл
			Если Сч = 4 Тогда
				ОбластьПШ = ОбластьПлатежиШапкаЗаказ;
				ОбластьП  = ОбластьПлатежиЗаказ;
				ОбластьИ  = ОбластьИтогиЗаказ;
			Иначе
				ОбластьПШ = ОбластьПлатежиШапка;
				ОбластьП  = ОбластьПлатежи;
				ОбластьИ  = ОбластьИтоги;
			КонецЕсли;
			ТЗ_Детали = ТЗ.Скопировать(Новый Структура("Ссылка, РежимОтображения", Выборка.Ссылка, Сч));
			Если ТЗ_Детали.Количество() > 0 Тогда
				ВывестиШапку = Истина;
				Для Каждого ТекСтр Из ТЗ_Детали Цикл
					Если ВывестиШапку Тогда
						ОбластьПШ.Параметры.Раздел = ТекСтр.Раздел;
						ТабДок.Вывести(ОбластьПШ);
						ВывестиШапку = Ложь;
					КонецЕсли;
					ОбластьП.Параметры.Заполнить(ТекСтр);
					ТабДок.Вывести(ОбластьП, 0);
				КонецЦикла;
				ТЗ_Детали.Свернуть("ВалютаДокумента, ВалютаОплаты", "СуммаДокумента, Сумма");
				Итого = "Итого";
				Для Каждого ТекСтр Из ТЗ_Детали Цикл
					ОбластьИ.Параметры.Заполнить(ТекСтр);
					ОбластьИ.Параметры.Итого = Итого;
					ТабДок.Вывести(ОбластьИ);
					Итого = "";
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		ВставлятьРазделительСтраниц = Истина;
	КонецЦикла;
	//}}
КонецПроцедуры
