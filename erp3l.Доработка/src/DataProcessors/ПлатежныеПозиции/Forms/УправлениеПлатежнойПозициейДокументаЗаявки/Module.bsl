
#Область ERP1C_62
// +++ 3L; Лопатин; 25.05.2022; ERP1C-62
// Добавлены гиперссылки (для одного платежа и платежа по частям) для редактирования курса платежа.

&НаСервере
Процедура ллл_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	ДобавляемыеРеквизиты = Новый Массив;
	НовыйРеквизит = Новый РеквизитФормы("ллл_Курс", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 5)));
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
	НовыйРеквизит = Новый РеквизитФормы("ллл_СуммаКОплате", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 3)));
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	НовыйЭлемент = Элементы.Вставить("ллл_Курс", Тип("ПолеФормы"), Элементы.ГруппаСуммаВВалютеЦены, Элементы.ГруппаКурс);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "ллл_Курс";
	НовыйЭлемент.Заголовок = "Курс платежа";
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "ПриИзмененииКурса");
	Элементы["ллл_Курс"].Видимость = Элементы.СтрокаКурсПлатежа.Видимость;
	Элементы.СтрокаКурсПлатежа.Видимость = Ложь;
	ЭтаФорма["ллл_Курс"] = ПлатежнаяПозиция[0].КурсПлатежа;
	
	НовыйЭлемент = Элементы.Добавить("ИзменитьКурс", Тип("ДекорацияФормы"), Элементы.ГруппаКурс);
	НовыйЭлемент.Заголовок = СтрШаблон("%1 за %2 %3",
		ПлатежнаяПозиция[0].ВалютаОплаты, ПлатежнаяПозиция[0].КратностьПлатежа,
		ПлатежнаяПозиция[0].ВалютаВзаиморасчетов);
	
	НовыйЭлемент = Элементы.Вставить("ллл_СуммаКОплате", Тип("ПолеФормы"), Элементы.ГруппаСуммаКОплате, Элементы.СуммаКОплате);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "ллл_СуммаКОплате";
	НовыйЭлемент.Заголовок = "Сумма платежа";
	НовыйЭлемент.УстановитьДействие("ПриИзменении", "ПриИзмененииСуммыПлатежа");
	Элементы["ллл_СуммаКОплате"].Видимость = Элементы.СуммаКОплате.Видимость;
	Элементы.СуммаКОплате.Видимость = Ложь;
	ЭтаФорма["ллл_СуммаКОплате"] = ПлатежнаяПозиция[0].Сумма;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииКурса(Элемент)
	
	ОбработатьИзменениеПоказателяНаСервере(ЭтаФорма["ллл_Курс"]);
	ЭтаФорма["ллл_СуммаКОплате"] = ПлатежнаяПозиция[0].Сумма;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСуммыПлатежа(Элемент)
	
	Результат = ЭтаФорма["ллл_СуммаКОплате"] * ПараметрыКурса.КратностьПлатежа / ПлатежнаяПозиция[0].СуммаВзаиморасчетов;
	ЭтаФорма["ллл_Курс"] = Результат;
	
	СтруктураПараметров = ВстраиваниеОПККлиентСерверПереопределяемый.СтруктураПараметровРасчетаКурса();
	ЗаполнитьЗначенияСвойств(СтруктураПараметров, ПараметрыКурса);
	Для Каждого Операция Из ПлатежнаяПозиция Цикл
		СтруктураПараметров.ДатаПлатежа = Операция.ДатаИсполнения;
		
		Операция.КурсПлатежа = Результат;
		Операция.КратностьПлатежа = 1;
		Операция.Сумма = ЭтаФорма["ллл_СуммаКОплате"];
		Операция.СтрокаКурсПлатежа = ТекстовоеПредставлениеКурса(Операция.КурсПлатежа, Операция.КратностьПлатежа, ВалютаОплаты, ВалютаВзаиморасчетов);
	КонецЦикла;
	
	ОбновитьИтогиРаспределения(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеПоказателяНаСервере(Результат)
	
	Для Каждого ТекСтр Из ПлатежнаяПозиция Цикл
		СтруктураПараметров = ВстраиваниеОПККлиентСерверПереопределяемый.СтруктураПараметровРасчетаКурса();
		ЗаполнитьЗначенияСвойств(СтруктураПараметров, ПараметрыКурса);
		СтруктураПараметров.КурсПлатежа = Результат;
		СтруктураПараметров.СпособОпределенияКурсаПлатежа = Перечисления.СпособыОпределенияКурсаПлатежа.ФиксированныйКурс;
		ПараметрыКурса = Новый ФиксированнаяСтруктура(СтруктураПараметров);
		ПереоценитьОперацию(ТекСтр);
	КонецЦикла;
	
КонецПроцедуры

// --- 3L; Лопатин; 25.05.2022; ERP1C-62
#КонецОбласти