
&НаСервере
&ИзменениеИКонтроль("СохранитьДанныеНаСервере")
Процедура ллл_СохранитьДанныеНаСервере()
	
	ДеревоОбъектов = РеквизитФормыВЗначение("БанковскаяВыписка");
	ПараметрыПоиска = Новый Структура("ДанныеИзменены", Истина);
	КоллекцияИзмененныхДокументов = ДеревоОбъектов.Строки.НайтиСтроки(ПараметрыПоиска);
	
	НачатьТранзакцию();
	Попытка
		Для Каждого СтрокаДокумент Из КоллекцияИзмененныхДокументов Цикл
			
			ДокОбъект = СтрокаДокумент.Ссылка.ПолучитьОбъект();
			
			// 1. Заполнение шапки документа.
			ЗаполнитьЗначенияСвойств(ДокОбъект, СтрокаДокумент, "Подразделение");
			
			#Вставка
			// +++ 3L; Лопатин; 09.06.2022; ERP1C-24
			ДокОбъект.Договор = СтрокаДокумент.ДоговорКонтрагента;
			// --- 3L; Лопатин; 09.06.2022; ERP1C-24
			#КонецВставки
			// 2. Обработка строк Расшифровки платежа либо дозаполнение шапки.

			Для Каждого СтрокаРасшифровки Из СтрокаДокумент.Строки Цикл
				
				Если СтрокаРасшифровки.ЭтоРасшифровка Тогда
				
					Если СтрокаРасшифровки.НомерСтроки <> 0 Тогда
						
						СтрокаТабличнойЧасти = ДокОбъект.РасшифровкаПлатежа.Найти(СтрокаРасшифровки.НомерСтроки, "НомерСтроки");
						Если СтрокаТабличнойЧасти <> Неопределено Тогда
							ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаРасшифровки); 
							СтрокаТабличнойЧасти.Аналитика1 = СтрокаРасшифровки.АналитикаБДДС1;
							СтрокаТабличнойЧасти.Аналитика2 = СтрокаРасшифровки.АналитикаБДДС2;
							СтрокаТабличнойЧасти.Аналитика3 = СтрокаРасшифровки.АналитикаБДДС3;
							СтрокаТабличнойЧасти.Аналитика4 = СтрокаРасшифровки.АналитикаБДДС4;
							СтрокаТабличнойЧасти.Аналитика5 = СтрокаРасшифровки.АналитикаБДДС5;
							СтрокаТабличнойЧасти.Аналитика6 = СтрокаРасшифровки.АналитикаБДДС6;
							#Вставка
							// +++ 3L; Лопатин; 09.06.2022; ERP1C-24
							ллл_НайтиЗаполнитьСчетНаОплату(ДокОбъект, СтрокаТабличнойЧасти);
							// --- 3L; Лопатин; 09.06.2022; ERP1C-24
							#КонецВставки
						КонецЕсли;
						
					Иначе
						
						СтрокаТабличнойЧасти = ДокОбъект.РасшифровкаПлатежа.Найти(СтрокаРасшифровки.НомерИсходнойСтроки, "НомерСтроки");
						Если СтрокаТабличнойЧасти <> Неопределено Тогда
							НоваяСтрока = ДокОбъект.РасшифровкаПлатежа.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
							ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасшифровки);
							#Вставка
							// +++ 3L; Лопатин; 09.06.2022; ERP1C-24
							ллл_НайтиЗаполнитьСчетНаОплату(ДокОбъект, НоваяСтрока);
							// --- 3L; Лопатин; 09.06.2022; ERP1C-24
							#КонецВставки
						КонецЕсли;
						
					КонецЕсли;
					
				Иначе
					
					ЗаполнитьЗначенияСвойств(ДокОбъект, СтрокаРасшифровки, "Заказ,СтатьяДвиженияДенежныхСредств,АналитикаБДДС1,АналитикаБДДС2,АналитикаБДДС3,АналитикаБДДС4,АналитикаБДДС5,АналитикаБДДС6,ДокументПланирования,ЦФО,Проект");
					
				КонецЕсли;
			КонецЦикла;

			ДокОбъект.Записать(РежимЗаписиДокумента.Проведение);

		КонецЦикла;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Выполнение операции'"), УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;
	
	ОбновитьВыпискуНаСервере();
КонецПроцедуры

&НаСервере
Процедура ллл_НайтиЗаполнитьСчетНаОплату(ДокОбъект, СтрокаРасшифровки)
	
	НазначениеПлатежа = ДокОбъект.НазначениеПлатежа;
	НомерСчета = "";
	ПозН = СтрНайти(НазначениеПлатежа, "N");
	Если ПозН > 0 Тогда
		Для Сч = ПозН+1 По СтрДлина(НазначениеПлатежа) Цикл 
			ТекСимвол = Сред(НазначениеПлатежа, Сч, 1);
			Если ТекСимвол = " " Тогда
				Прервать;
			КонецЕсли;
			НомерСчета = НомерСчета + ТекСимвол;
		КонецЦикла;
		Если СтрДлина(НомерСчета) < 16 Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	Сч.Ссылка КАК Ссылка
				|ИЗ
				|	Документ.СчетНаОплатуКлиенту КАК Сч
				|ГДЕ
				|	Сч.Организация = &Организация
				|	И Сч.Партнер = &Партнер
				|	И Сч.Контрагент = &Контрагент
				|	И Сч.Номер = &Номер
				|	";
			
			Запрос.УстановитьПараметр("Организация", ДокОбъект.Организация);
			Запрос.УстановитьПараметр("Контрагент",  ДокОбъект.Контрагент);
			Запрос.УстановитьПараметр("Партнер",     ДокОбъект.Партнер);
			Запрос.УстановитьПараметр("Номер",       НомерСчета);
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Если Выборка.Следующий() Тогда
				СтрокаРасшифровки.ОснованиеПлатежа = Выборка.Ссылка;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры